;/T/ Liste de ce qui reste à emballer (SANS PRIORITÉ)
;
;/P/ Programmeur...: REMY DEMERS
;    Date Création.: 1997-11-27
;
;    Description...: Liste des diagrammes qui reste à emballer sommarise
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS PDDIAGRAMME &
  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE &
  LINK CIECLE    OF PDDIAGRAMME ,&
       COMNUMCOM OF PDDIAGRAMME  &
    TO CIECLE,                       &
       COMNUMCOM OF PDCOMMAN_INTERNE &

  LINK CIECLE    OF PDDIAGRAMME ,&
       COMNUMCOM OF PDDIAGRAMME , &
       DCINUMLIG OF PDDIAGRAMME  &
    TO CIECLE,                       &
       COMNUMCOM,                    &
       DCINUMLIG OF PDDETAIL_C_I     &

  LINK PJTABR OF PDDETAIL_C_I &
    TO PJTABR OF PDPROJET


CHOOSE CIECLE PARM , COMNUMCOM PARM





;-------------------------------------------------------------------------------
;
; Variable nécessaire pour afficher les quantités à emballer
;

DEFINE D_SUR_MC2 FLOAT SIZE 08 = DIASURMC2 OF PDDIAGRAMME
DEFINE D_SUR_QTE FLOAT SIZE 8  = D_SUR_MC2 * DIAQTECMD OF PDDIAGRAMME
DEFINE D_QTE_PLA_GRA INTEGER SIZE 04 = DIAQTEPLAFINGRA OF VDIAQTE
DEFINE D_QTE_FIN_GRA INTEGER SIZE 04 = DIAQTEFINGRA OF VDIAQTE

DEFINE D_QTE_PLA_USC INTEGER SIZE 04 = DIAQTEPLAFINUSC OF VDIAQTE
DEFINE D_QTE_FIN_USC INTEGER SIZE 04 = DIAQTEFINUSC OF VDIAQTE

DEFINE D_QTE_PLA_SCT INTEGER SIZE 04 = DIAQTEPLAFINSCT OF VDIAQTE
DEFINE D_QTE_FIN_SCT INTEGER SIZE 04 = DIAQTEFINSCT OF VDIAQTE


DEFINE D_RESTE_GRA INTEGER SIZE 04 = DIAQTEPLAFINGRA OF VDIAQTE  - DIAQTEPREGRA OF PDDIAGRAMME &
                                     IF 0 < DIAQTEPLAFINGRA OF VDIAQTE - DIAQTEPREGRA OF PDDIAGRAMME &
                                     ELSE 0

DEFINE D_RESTE_USC INTEGER SIZE 04 = DIAQTEPLAFINUSC OF VDIAQTE - DIAQTEPREUSC OF PDDIAGRAMME &
                                     IF 0 < DIAQTEPLAFINUSC OF VDIAQTE  - DIAQTEPREUSC OF PDDIAGRAMME &
                                     ELSE 0

DEFINE D_RESTE_SCT INTEGER SIZE 04 = DIAQTEPLAFINSCT OF VDIAQTE  - DIAQTEPRESCT OF PDDIAGRAMME &
                                     IF 0 < DIAQTEPLAFINSCT OF VDIAQTE - DIAQTEPRESCT OF PDDIAGRAMME &
                                     ELSE 0





DEFINE D_SUR_PLA_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_GRA
DEFINE D_SUR_FIN_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_GRA
DEFINE D_SUR_RES_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_GRA

DEFINE D_SUR_PLA_USC FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_USC
DEFINE D_SUR_FIN_USC FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_USC
DEFINE D_SUR_RES_USC FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_USC

DEFINE D_SUR_PLA_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_SCT
DEFINE D_SUR_FIN_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_SCT
DEFINE D_SUR_RES_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_SCT

SELECT IF  (DIAQTEEMB OF PDDIAGRAMME < DIAQTECMD OF PDDIAGRAMME) &
       AND (COMSTU OF PDCOMMAN_INTERNE = "C" OR COMSTU OF PDCOMMAN_INTERNE = "P") &
       AND (D_RESTE_GRA > 0 OR D_RESTE_USC > 0 OR D_RESTE_SCT > 0)


SORT ON PJTABR    OF PDDIAGRAMME &
     ON COMNUMCOM OF PDDIAGRAMME &
     ON DCINUMLIG OF PDDETAIL_C_I


;select if d_reste_gra <> 0 or d_reste_usc <> 0 or d_reste_sct <> 0


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste des diagrammes à emballer sans priorité sommaire"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD653"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitstd45 NOLIST
  SKIP 1

HEADING AT PJTABR &
  SKIP PAGE &

  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDPROJET &;PRIORITE_DIAG &
  TAB 019 "-" &
  TAB 020 PJTNOM OF PDPROJET &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP 2 &
  TAB 003 "                   *--------- Granicor --------*          *----- Usine secondaire ----*        *---- Sous-contractant -----*" &
  SKIP 1 &
  TAB 003 "              Cmd  Debitage    Finition       Reste        Debitage    Finition   Reste         Debitage   Finition   Reste "

FOOTING AT COMNUMCOM &
  SKIP 2 &
  TAB 001 PJTABR OF PDDIAGRAMME &
  TAB 003 "-" &
  TAB 004 COMNUMCOMINT OF PDDIAGRAMME &
  TAB 015 DIAQTECMD OF PDDIAGRAMME SUBTOTAL RESET AT COMNUMCOM &
  TAB 021 DIAQTEPLADBTGRA OF VDIAQTE SUBTOTAL RESET AT COMNUMCOM &
                PICTURE "^^^^^" &
  TAB 033 DIAQTEFINGRA OF VDIAQTE SUBTOTAL RESET AT COMNUMCOM &
                PICTURE "^^^^^" &
  TAB 045 D_RESTE_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^" &
  TAB 058 DIAQTEPLADBTUSC OF VDIAQTE  SUBTOTAL RESET AT COMNUMCOM &
                PICTURE "^^^^^" &
  TAB 070 DIAQTEFINUSC OF VDIAQTE SUBTOTAL RESET AT COMNUMCOM &
                PICTURE "^^^^^" &
  TAB 082 D_RESTE_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^" &
  TAB 095 DIAQTEPLADBTSCT OF VDIAQTE SUBTOTAL RESET AT COMNUMCOM &
                PICTURE "^^^^^" &
  TAB 107 DIAQTEFINSCT OF VDIAQTE SUBTOTAL RESET AT COMNUMCOM &
                PICTURE "^^^^^" &
  TAB 119 D_RESTE_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^" &

  SKIP  &
  TAB 001 "Tot M2:" &
  TAB 008 D_SUR_QTE     SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 020 D_SUR_PLA_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 032 D_SUR_FIN_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 044 D_SUR_RES_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 057 D_SUR_PLA_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 069 D_SUR_FIN_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 081 D_SUR_RES_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 094 D_SUR_PLA_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 106 D_SUR_FIN_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 118 D_SUR_RES_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  SKIP


FINAL FOOTING &
  SKIP 2 &
  TAB 001 "Grand total " &
  SKIP 2 &
  TAB 001 "Piece:" &
  TAB 015 DIAQTECMD OF PDDIAGRAMME SUBTOTAL &
  TAB 021 DIAQTEPLADBTGRA OF VDIAQTE SUBTOTAL &
                PICTURE "^^^^^" &
  TAB 033 DIAQTEFINGRA OF VDIAQTE  SUBTOTAL &
                PICTURE "^^^^^" &
  TAB 045 D_RESTE_GRA SUBTOTAL PICTURE "^^^^^" &
  TAB 058 DIAQTEPLADBTUSC OF VDIAQTE SUBTOTAL &
                PICTURE "^^^^^" &
  TAB 070 DIAQTEFINUSC OF VDIAQTE SUBTOTAL &
                PICTURE "^^^^^" &
  TAB 082 D_RESTE_USC SUBTOTAL PICTURE "^^^^^" &
  TAB 095 DIAQTEPLADBTSCT OF VDIAQTE SUBTOTAL &
                PICTURE "^^^^^" &
  TAB 107 DIAQTEFINSCT OF VDIAQTE SUBTOTAL &
                PICTURE "^^^^^" &
  TAB 119 D_RESTE_SCT SUBTOTAL PICTURE "^^^^^" &

  SKIP &

  TAB 001 "Grand total M2:" &
  TAB 020 D_SUR_PLA_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 032 D_SUR_FIN_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 044 D_SUR_RES_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 057 D_SUR_PLA_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 069 D_SUR_FIN_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 081 D_SUR_RES_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 094 D_SUR_PLA_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 107 D_SUR_FIN_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 118 D_SUR_RES_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6


BUILD pd653z


@IF FORMAT

PD650                                                      GRANICOR INC.                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num. commande.: XX-XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
Priorité......: 99 (AA/MM/JJ)                                         Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
                                                                      Critères esthé: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

                *--------- Granicor --------*  *----- Usine secondaire ----*  *---- Sous-contractant -----*
Diag.      Cmd  P dbt   Dbt P fin   Fin R emb  P dbt   Dbt P fin   fin R emb  P dbt   Dbt P fin   fin R emb  Gra Fi Épai Long Haut
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999


                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*
                  Planifier        Fini Reste embal    Planifier        Fini Reste embal    Planifier        Fini Reste embal
TOTAL M2 :      999999.9999 999999.9999 999999.9999  999999.9999 999999.9999 999999.9999  999999.9999 999999.9999 999999.9999


@ENDIF
