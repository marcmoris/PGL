;/T/ Liste des pièces à commander
;
;/P/ Programmeur...: Patrick Langlois
;    Date Création.: 5 Aôut 1999
;
;    Description...: Impression des pièces à commander
;
;    Paramètres....: Compagnie
;                    Code de produit (Borne de début)
;                    Code de produit (Borne de fin)
;                    Catégorie       (Une, plusieurs ou tous)
;                    Type            (D=divers, F=frais,I=inventorié,
;                                     N=non inventorié,<retour>=tous)
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS ARPRODUIT            &
   ;
   ; Enrepôts du produit
   ;
  LINK CIECLE OF ARPRODUIT, &
       PRDCLE OF ARPRODUIT  &
    TO CIECLE             , &
       PRDCLE OF ARPRD_ENT


CHOOSE CIECLE PARM,       & ; Compagnie du menu
       PRDCLE PARM RANGE, & ; Code de produit
       PRDCAT PARM          ; Catégorie de produit


DEFINE D_PRDTYP CHARACTER SIZE 6 = PARM UPSHIFT ; Type de produit


DEFINE D_QTEACOM FLOAT SIZE 8 = PREQTEMAX OF ARPRD_ENT - &
                                PREQTEPHY OF ARPRD_ENT - &
                                PREQTECOM OF ARPRD_ENT - &
                                PREQTEDEM OF ARPRD_ENT


SELECT IF (0 <> INDEX (D_PRDTYP,PRDTYP) OR D_PRDTYP = "") AND &
           D_QTEACOM <= PREPOICOM OF ARPRD_ENT AND D_QTEACOM <> 0


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Listes des pièces à commander"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR346Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


SORT ON CIECLE OF ARPRODUIT    & ; Compagnie
     ON PRDCLE OF ARPRODUIT    & ; Code de produit
     ON ENTCLE OF ARPRD_ENT


USE ustitstd NOLIST
  SKIP 2 &
TAB 001 "                                                                       Qté      Point     Qté en     Qté en     Qté en      Qté à" &
  SKIP 1 &
TAB 001 " No produit   Description                              Ent. Local    maximum   Commande   demande   commande  Inventaire  commander" &
  SKIP 1 &
TAB 001 " ------------ ---------------------------------------- ---- ----- ----------- --------- ---------- ---------- ---------- ----------" &
SKIP

REPORT &
  TAB 002 PRDCLE  OF ARPRODUIT   &
  TAB 015 PRDDSC1 OF ARPRODUIT   &
  tab 056 ENTCLE  OF ARPRD_ENT   &
  TAB 061 ENLCLE  OF ARPRD_ENT   &
  TAB 067 PREQTEMAX OF ARPRD_ENT &
          PICTURE "^^,^^^.^^^ "  &
          TRAILING SIGN "-"      &
          SIGNIFICANCE 6         &
          BWZ                    &
  TAB 079 PREPOICOM OF ARPRD_ENT &
          PICTURE "^,^^^.^^^ "   &
          TRAILING SIGN "-"      &
          SIGNIFICANCE 6         &
          BWZ                    &
  TAB 089 PREQTEDEM OF ARPRD_ENT &
          PICTURE "^^,^^^.^^^ "  &
          TRAILING SIGN "-"      &
          SIGNIFICANCE 6         &
          BWZ                    &
  TAB 100 PREQTECOM OF ARPRD_ENT &
          PICTURE "^^,^^^.^^^ "  &
          TRAILING SIGN "-"      &
          SIGNIFICANCE 6         &
          BWZ                    &
  TAB 111 PREQTEPHY OF ARPRD_ENT &
          PICTURE "^^,^^^.^^^ "  &
          TRAILING SIGN "-"      &
          SIGNIFICANCE 6         &
          BWZ                    &
  TAB 122 D_QTEACOM              &
          PICTURE "^^,^^^.^^^ "  &
          TRAILING SIGN "-"      &
          SIGNIFICANCE 6         &
          BWZ


BUILD ar346z


@IF FORMAT
;                                                                      Qté      Point     Qté en     Qté en     Qté en      Qté à
;No produit   Description                              Ent. Local    maximum   Commande   demande   commande  Inventaire  commander
;xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 99,999.9999 9,999.999 99,999.999 99,999.999 99,999.999 99,999.999
;------------ ---------------------------------------- ---- ----- ----------- --------- ---------- ---------- ---------- ----------

@ENDIF
