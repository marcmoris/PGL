screen maj_pdcaisson

file mccompagnie         primary

file pdcaisson           designer
file pdembal_tranche     designer
file pdtranche           designer
file pdtype_granit       designer
file pdembal_diag        designer
file pdembal_prod_dim    designer

temporary tz_largeur     float   size 8
temporary tz_hauteur     float   size 8
temporary tz_epaisseur   float   size 8
temporary tz_volume      float   size 8
temporary tz_mc2         float   size 8

temporary t_emtsurmc2ven float   size 8
temporary t_emtvolmc3ven float   size 8

temporary t_compteur     integer size 8

field ciecle of mccompagnie
field cienom of mccompagnie

procedure internal calvol
begin
  let tz_volume = round ( ((tz_largeur  /1000) *      &
                           (tz_hauteur  /1000) *      &
                           (tz_epaisseur/1000)),4)
  let tz_mc2    = round ( ((tz_largeur  /1000) *      &
                           (tz_hauteur/1000)),4)
end

procedure internal i_maj
begin
  let caipoiest of pdcaisson = 0
  let caimc2 of pdcaisson    = 0

  while retrieving pdembal_tranche via ciecle                , &
                                       cainumcai               &
                                 using ciecle    of pdcaisson, &
                                       cainumcai of pdcaisson

  begin
    get pdtranche via ciecle                         , &
                      tranumtra002                     &
                using ciecle       of pdembal_tranche, &
                      tranumtra002 of pdembal_tranche  opt

    get pdtype_granit via ciecle                , &
                          tgrcodgra               &
                    using ciecle    of pdtranche, &
                          tgrcodgra of pdtranche  opt

    let tz_largeur   = emtlonven of pdembal_tranche
    let tz_hauteur   = emthauven of pdembal_tranche
    let tz_epaisseur = emtepaven of pdembal_tranche

    do internal calvol

    let t_emtsurmc2ven = tz_mc2
    let t_emtvolmc3ven = tz_volume

    let caipoiest of pdcaisson = caipoiest    of pdcaisson       + &
                                (emtqteprd    of pdembal_tranche * &
                                 tgrpoi       of pdtype_granit   * &
                                 t_emtvolmc3ven)

    let caimc2 of pdcaisson    = caimc2       of pdcaisson       + &
                                (emtqteprd    of pdembal_tranche * &
                                 t_emtsurmc2ven)
  end
end

procedure designer maj
begin
  while retrieving pdcaisson sequential
  begin
    let t_compteur = t_compteur + 1
    info = asc(t_compteur,8) now

    get pdembal_diag via ciecle                , &
                         cainumcai               &
                   using ciecle    of pdcaisson, &
                         cainumcai of pdcaisson  opt
    if not accessok
    then begin 
      get pdembal_prod_dim via ciecle                , &
                               cainumcai               &
                         using ciecle    of pdcaisson, &
                               cainumcai of pdcaisson  opt
      if not accessok
      then begin
        do internal i_maj
        put pdcaisson
      end
    end
  end
end

