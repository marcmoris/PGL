; *** Attention, ce programme doit être compilé dans le répertoire $PG_USE.    ***
; *** Cette compilation doit être précédée d'une exécution bidon de pd888t,    ***
; *** pd889z et pd890z pour générer les subfiles nécessaires à sa compilation. ***

; *** Ne pas oublier de transférer le pd891t.qtc dans le répertoire $PG_EXF.   ***


use ussetqts nolist

set lock file update

run pd891t

;-------------------------------------------------------------------------------
request r_pdpriorite

access pdpriorite

choose ciecle    parm, &
       comnumcom parm, &
       pricodpri parm

subfile ypdpriorite keep include ciecle    of pdpriorite, &
                                 comnumcom of pdpriorite, &
                                 pricodpri of pdpriorite, &
                                 pridatfin of pdpriorite  &
                           index ypd891_pri_idx           &
                         segment ciecle                 , &
                                 comnumcom              , &
                                 pricodpri              

;-------------------------------------------------------------------------------
request r_pddiagramme

access pddiagramme

choose ciecle    parm, &
       comnumcom parm, &
       dianum002 parm, &
       dcinumlig parm

subfile ypddiagramme keep include dianum002    of pddiagramme, &
                                  ciecle       of pddiagramme, &
                                  pjtabr       of pddiagramme, &
                                  dianumdia001 of pddiagramme, &
                                  dianumdia002 of pddiagramme, &
                                  diaqtecmd    of pddiagramme, &
                                  diaqteemb    of pddiagramme  &
                            index ypd891_dia_idx               &
                          segment dianum002                  , &
                                  ciecle                     
                                  

;-------------------------------------------------------------------------------
request r_vdiaqteaut

access vdiaqteaut

choose ciecle    parm, &
       comnumcom parm, &
       dianum002 parm, &
       dcinumlig parm

subfile yvdiaqteaut keep include pjtabr          of vdiaqteaut, &
                                 dianumdia001    of vdiaqteaut, &
                                 dianumdia002    of vdiaqteaut, &
                                 ciecle          of vdiaqteaut, &
                                 diaqtepladbtgra of vdiaqteaut, &
                                 diaqteplafingra of vdiaqteaut, & 
                                 diaqtefingra    of vdiaqteaut  &
                           index ypd891_vdq_idx                 &
                         segment pjtabr                       , &
                                 dianumdia001                 , &
                                 dianumdia002                 , &
                                 ciecle                       

;-------------------------------------------------------------------------------
request r_pdcomman_interne

access pdcomman_interne

choose ciecle    parm, &
       comnumcom parm, &
       comstu    parm

select if comstu of pdcomman_interne = "C" or &
          comstu of pdcomman_interne = "P"

subfile ypdcomman_interne keep include comnumcom    of pdcomman_interne, &
                                       ciecle       of pdcomman_interne, &
                                       comstu       of pdcomman_interne  &
                                 index ypd891_com_idx                    &
                               segment comnumcom                       , &
                                       ciecle                          

;-------------------------------------------------------------------------------
request r_pddetail_c_i_cum

access pddetail_c_i

choose ciecle    parm, &
       comnumcom parm, &
       dcinumlig parm

sort on ciecle    of pddetail_c_i &
     on comnumcom of pddetail_c_i 

temporary t_qte_det float size 08

item t_qte_det subtotal dciqteunmprd of pddetail_c_i reset at comnumcom

subfile ypddetail_c_i keep at comnumcom include ciecle    of pddetail_c_i, &
                                                comnumcom of pddetail_c_i, &
                                                t_qte_det                  &
                                          index ypd891_dci_idx             &
                                        segment ciecle                   , &
                                                comnumcom                

;-------------------------------------------------------------------------------
request r_qte_a_finir

access pdpriorite_diag link ciecle       of pdpriorite_diag  , &
                            comnumcom    of pdpriorite_diag  , &
                            pricodpri    of pdpriorite_diag    &
                         to ciecle                           , &
                            comnumcom                        , &
                            pricodpri    of *ypdpriorite       &

                       link dianum002    of pdpriorite_diag  , &
                            ciecle       of pdpriorite_diag    &
                         to dianum002                        , &
                            ciecle       of *ypddiagramme      &

                       link pjtabr       of ypddiagramme     , &
                            dianumdia001 of ypddiagramme     , &
                            dianumdia002 of ypddiagramme     , &
                            ciecle       of ypddiagramme       &
                         to pjtabr,                            &
                            dianumdia001                     , &
                            dianumdia002                     , &
                            ciecle       of *yvdiaqteaut       &

                       link comnumcom    of pdpriorite_diag  , &
                            ciecle       of pdpriorite_diag    &
                         to comnumcom                        , &
                            ciecle       of *ypdcomman_interne &

                       link dianum002    of ypddiagramme     , &
                            ciecle       of pdpriorite_diag    &
                         to dianum002                        , &
                            ciecle       of pduni_trav_dia     optional &

                       link utrnumunitrv of pduni_trav_dia   , &
                            ciecle       of pdpriorite_diag    &
                         to utrnumunitrv                     , &
                            ciecle       of pdunite_travail    &

                       link utrcodunmprd of pdunite_travail  , &
                            ciecle       of pdpriorite_diag    &
                         to unmcodunm                        , &
                            ciecle       of pdunite_mesure    

choose ciecle    parm, &
       comnumcom parm, &
       dianum002 parm, &
       pricodpri parm, &
       dcinumlig parm

select if (diaqtecmd       of ypddiagramme > diaqteemb of ypddiagramme) and &
          (diaqtepladbtgra of yvdiaqteaut <> 0)

sort on ciecle       of pdpriorite_diag &
     on comnumcom    of pdpriorite_diag &
     on pricodpri    of pdpriorite_diag &
     on utrnumunitrv of pduni_trav_dia


temporary t_qte_cum integer size 04
temporary t_qte_res integer size 04
temporary t_qte_dif float   size 08
temporary t_qte_sub float   size 08
temporary t_qte_prd float   size 08


item t_qte_cum =  diaqteplafingra of yvdiaqteaut - diaqtefingra of yvdiaqteaut
item t_qte_dif = (diaqteplafingra of yvdiaqteaut - diaqteemb    of ypddiagramme) * utdqteprd of pduni_trav_dia

item t_qte_sub =  t_qte_cum * utdqteprd of pduni_trav_dia                           &
              if (diaqtefingra of yvdiaqteaut > diaqteemb of ypddiagramme)          &
            else t_qte_dif

item t_qte_res subtotal t_qte_cum reset at utrnumunitrv
item t_qte_prd subtotal t_qte_sub reset at utrnumunitrv

subfile wpd891_635z keep at utrnumunitrv include ciecle       of pdpriorite_diag  , &
                                                 comnumcom    of pdpriorite_diag  , &
                                                 pricodpri    of pdpriorite_diag  , &
                                                 utrnumunitrv of pduni_trav_dia   , &
                                                 t_qte_res                        , &
                                                 t_qte_prd                          &
                                           index wpd891_635z_idx                    &
                                         segment ciecle                           , &
                                                 comnumcom                        , &
                                                 pricodpri                        

;-------------------------------------------------------------------------------
request r_planif_del_1

access pdplanification

output pdplanification delete

;-------------------------------------------------------------------------------
request r_planif_add

access *wpd891_635z link ciecle       of wpd891_635z    , &
                         utrnumunitrv of wpd891_635z      &
                      to ciecle                         , &
                         utrnumunitrv of pdunite_travail 

select if t_qte_prd of wpd891_635z > 0

output pdplanification add
  item ciecle       of pdplanification initial ciecle       of wpd891_635z
  item comnumcom    of pdplanification initial comnumcom    of wpd891_635z
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri    of wpd891_635z
  item priqtecmd    of pdplanification initial 0
  item priqtedeb    of pdplanification initial 0
  item priqteemb    of pdplanification initial 0
  item utrnumunitrv of pdplanification initial utrnumunitrv of wpd891_635z
  item utdqteres    of pdplanification initial t_qte_res    of wpd891_635z
  item utdqteprd    of pdplanification initial t_qte_prd    of wpd891_635z

  item ppltmpest    of pdplanification initial round((t_qte_prd    of wpd891_635z                     &
                                             * ((nco(asc(utrtmpest of pdunite_travail,4)[1:2]) * 60)  &
                                             +  (nco(asc(utrtmpest of pdunite_travail,4)[3:2]))))     &
                                             / 3600,2)

;-------------------------------------------------------------------------------
request r_planif_add_update_01

access *wpd888_625z link ciecle    of wpd888_625z    , &
                         comnumcom of wpd888_625z    , &
                         pricodpri of wpd888_625z      &
                      to ciecle                      , &
                         comnumcom                   , &
                         pricodpri of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle    of wpd888_625z
  item comnumcom    of pdplanification initial comnumcom of wpd888_625z
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri of wpd888_625z
  item priqtecmd    of pdplanification =       round(t_qte_cmd of wpd888_625z,0)
  item priqtedeb    of pdplanification initial 0
  item priqteemb    of pdplanification initial 0
  item utdqteres    of pdplanification initial 0
  item utdqteprd    of pdplanification initial 0

;-------------------------------------------------------------------------------
request r_planif_add_update_02

access *wpd889_640zb link ciecle    of wpd889_640zb   , &
                          comnumcom of wpd889_640zb   , &
                          pricodpri of wpd889_640zb     &
                       to ciecle                      , &
                          comnumcom                   , &
                          pricodpri of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle      of wpd889_640zb
  item comnumcom    of pdplanification initial comnumcom   of wpd889_640zb
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri   of wpd889_640zb
  item priqtecmd    of pdplanification initial 0
  item priqtedeb    of pdplanification =       round(d_qte_a_dbt of wpd889_640zb,0)
  item priqteemb    of pdplanification initial 0
  item utdqteres    of pdplanification initial 0
  item utdqteprd    of pdplanification initial 0

;-------------------------------------------------------------------------------
request r_planif_add_update_03

access *wpd890_656za link ciecle    of wpd890_656za   , &
                          comnumcom of wpd890_656za   , &
                          pricodpri of wpd890_656za     &
                       to ciecle                      , &
                          comnumcom                   , &
                          pricodpri of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle          of wpd890_656za
  item comnumcom    of pdplanification initial comnumcom       of wpd890_656za
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri       of wpd890_656za
  item priqtecmd    of pdplanification initial 0
  item priqtedeb    of pdplanification initial 0
  item priqteemb    of pdplanification =       round(d_qte_a_dbt_sur of wpd890_656za,0)
  item utdqteres    of pdplanification initial 0
  item utdqteprd    of pdplanification initial 0


;-------------------------------------------------------------------------------
request r_planif_update_01

access pdplanification link ciecle       of pdplanification   &
                         to ciecle       of mccompagnie       &
                                         
                       link comnumcom    of pdplanification , &
                            ciecle       of pdplanification   &
                         to comnumcom                       , &
                            ciecle       of pdcomman_interne  &
                                         
                       link cliciecle    of pdcomman_interne, &
                            clicle       of pdcomman_interne, &
                            ciecle       of pdcomman_interne  &
                         to cliciecle                       , &
                            clicle                          , &
                            ciecle       of vclc_cli          &
                            
                       link ciecle       of pdcomman_interne, &
                            pjtabr       of pdcomman_interne, &
                            pjtann       of pdcomman_interne, &
                            comnumcomint of pdcomman_interne, &
                            "99"                              &
                         to ciecle                          , &
                            pjtabr                          , &
                            pjtann                          , &
                            comnumcomint                    , &
                            pricodpri    of pdpriorite optional

output pdplanification update
  item cienom    of pdplanification final cienom    of mccompagnie
  item clicle    of pdplanification final clicle    of pdcomman_interne
  item clinom    of pdplanification final clinom    of vclc_cli
  item comstu    of pdplanification final comstu    of pdcomman_interne
  item comnumcom of pdplanification final comnumcom of pdcomman_interne
  item comnom    of pdplanification final comnom    of pdcomman_interne
  item comdatcmd of pdplanification final comdatcmd of pdcomman_interne
  item pridatcom of pdplanification final pridatfin of pdpriorite
  item comdes    of pdplanification final comdes    of pdcomman_interne
  item comdatdes of pdplanification final comdatdes of pdcomman_interne
  item comdia    of pdplanification final comdia    of pdcomman_interne
  item comdatdia of pdplanification final comdatdia of pdcomman_interne

;-------------------------------------------------------------------------------
request r_planif_update_02

access pdplanification link ciecle    of pdplanification, &
                            comnumcom of pdplanification  & 
                         to ciecle                      , &
                            comnumcom of *ypddetail_c_i   optional &

                       link ciecle    of pdplanification, &
                            comnumcom of pdplanification, &
                            pricodpri of pdplanification  &
                         to ciecle                      , &
                            comnumcom                   , &
                            pricodpri of *ypdpriorite     optional

output pdplanification update
  item dciqteunmprd of pdplanification final round(t_qte_det of ypddetail_c_i,2)
  item pridatfin    of pdplanification final pridatfin of ypdpriorite

;-------------------------------------------------------------------------------
request r_planif_sort

access pdplanification 

sort on ciecle       of pdplanification &
     on comnumcom    of pdplanification &
     on pricodpri    of pdplanification &
     on utrnumunitrv of pdplanification

subfile wpdplanification keep include pdplanification       &
                                index wpdplanification_idx  &
                               segment ciecle             , &
                                       comnumcom          , &
                                       pricodpri          , &
                                       comstu                           

;-------------------------------------------------------------------------------
request r_planif_del_2

access pdplanification

output pdplanification delete

;-------------------------------------------------------------------------------
request r_planif_output

access *wpdplanification

choose ciecle    parm, &
       comnumcom parm, &
       pricodpri parm, &
       comstu    parm

select if comnumcom of wpdplanification <> " "

output pdplanification add


build pd891t
