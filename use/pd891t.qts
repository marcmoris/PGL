
; *** Attention, ce programme doit être compilé dans le répertoire $PG_USE.    ***
; *** Cette compilation doit être précédée d'une exécution bidon de pd888t,    ***
; *** pd889z et pd890z pour générer les subfiles nécessaires à sa compilation. ***

; *** Ne pas oublier de transférer le pd891t.qtc dans le répertoire $PG_EXF.   ***


use ussetqts nolist

set lock file update

run pd891t

global temp g_count_all
global temp g_count_prm

;-------------------------------------------------------------------------------
request r_count_all

access pdcomman_interne

choose ciecle parm

item g_count_all count

;-------------------------------------------------------------------------------
request r_count_prm

access pdcomman_interne

choose ciecle    parm, &
       comnumcom parm

item g_count_prm count

;-------------------------------------------------------------------------------
request r_planif_del_0 execute if g_count_all = g_count_prm

access pdplanification

output pdplanification delete

;-------------------------------------------------------------------------------
request r_pdpriorite

access pdpriorite

choose ciecle    parm, &
       comnumcom parm, &
       pricodpri parm

subfile ypdpriorite keep include ciecle    of pdpriorite, &
                                 comnumcom of pdpriorite, &
                                 pricodpri of pdpriorite, &
                                 pridatfin of pdpriorite  &
                           index ypd891_pri_idx           &
                         segment ciecle                 , &
                                 comnumcom              , &
                                 pricodpri

;-------------------------------------------------------------------------------
request r_pddiagramme

access pddiagramme

choose ciecle    parm, &
       comnumcom parm, &
       dianum002 parm, &
       dcinumlig parm

subfile ypddiagramme keep include dianum002    of pddiagramme, &
                                  ciecle       of pddiagramme, &
                                  pjtabr       of pddiagramme, &
                                  dianumdia001 of pddiagramme, &
                                  dianumdia002 of pddiagramme, &
                                  diaqtecmd    of pddiagramme, &
                                  diaqteemb    of pddiagramme  &
                            index ypd891_dia_idx               &
                          segment dianum002                  , &
                                  ciecle


;-------------------------------------------------------------------------------
request r_vdiaqteaut

access vdiaqteaut

choose ciecle    parm, &
       comnumcom parm, &
       dianum002 parm, &
       dcinumlig parm

subfile yvdiaqteaut keep include pjtabr          of vdiaqteaut, &
                                 dianumdia001    of vdiaqteaut, &
                                 dianumdia002    of vdiaqteaut, &
                                 ciecle          of vdiaqteaut, &
                                 diaqtepladbtgra of vdiaqteaut, &
                                 diaqteplafingra of vdiaqteaut, &
                                 diaqtefingra    of vdiaqteaut  &
                           index ypd891_vdq_idx                 &
                         segment pjtabr                       , &
                                 dianumdia001                 , &
                                 dianumdia002                 , &
                                 ciecle

;-------------------------------------------------------------------------------
request r_pdcomman_interne

access pdcomman_interne

choose ciecle    parm, &
       comnumcom parm, &
       comstu    parm

select if comstu of pdcomman_interne = "C" or &
          comstu of pdcomman_interne = "P"

subfile ypdcomman_interne keep include pdcomman_interne  &
                                 index ypd891_com_idx    &
                               segment comnumcom       , &
                                       ciecle

;-------------------------------------------------------------------------------
request r_pddetail_c_i_cum

access pddetail_c_i

choose ciecle    parm, &
       comnumcom parm, &
       dcinumlig parm, &
       tpdtypprd "DI", "TR"

sort on ciecle    of pddetail_c_i &
     on comnumcom of pddetail_c_i &
     on tpdtypprd of pddetail_c_i

temporary t_qte_det float size 08

item t_qte_det subtotal dciqteunmprd of pddetail_c_i        &
                     if tpdtypprd    of pddetail_c_i = "DI" &
               reset at tpdtypprd

subfile ypddetail_c_i keep at tpdtypprd include ciecle    of pddetail_c_i, &
                                                comnumcom of pddetail_c_i, &
                                                tpdtypprd of pddetail_c_i, &
                                                t_qte_det                  &
                                          index ypd891_dci_di_idx          &
                                        segment ciecle                   , &
                                                comnumcom                , &
                                                tpdtypprd

;-------------------------------------------------------------------------------
request r_qte_a_finir

access pdpriorite_diag link ciecle       of pdpriorite_diag  , &
                            comnumcom    of pdpriorite_diag  , &
                            pricodpri    of pdpriorite_diag    &
                         to ciecle                           , &
                            comnumcom                        , &
                            pricodpri    of *ypdpriorite       &

                       link dianum002    of pdpriorite_diag  , &
                            ciecle       of pdpriorite_diag    &
                         to dianum002                        , &
                            ciecle       of *ypddiagramme      &

                       link pjtabr       of ypddiagramme     , &
                            dianumdia001 of ypddiagramme     , &
                            dianumdia002 of ypddiagramme     , &
                            ciecle       of ypddiagramme       &
                         to pjtabr,                            &
                            dianumdia001                     , &
                            dianumdia002                     , &
                            ciecle       of *yvdiaqteaut       &

                       link comnumcom    of pdpriorite_diag  , &
                            ciecle       of pdpriorite_diag    &
                         to comnumcom                        , &
                            ciecle       of *ypdcomman_interne &

                       link dianum002    of ypddiagramme     , &
                            ciecle       of pdpriorite_diag    &
                         to dianum002                        , &
                            ciecle       of pduni_trav_dia   optional

choose ciecle    parm, &
       comnumcom parm, &
       dianum002 parm, &
       pricodpri parm, &
       dcinumlig parm

select if (diaqtecmd       of ypddiagramme > diaqteemb of ypddiagramme) and &
          (diaqtepladbtgra of yvdiaqteaut <> 0)

sort on ciecle       of pdpriorite_diag &
     on comnumcom    of pdpriorite_diag &
     on pricodpri    of pdpriorite_diag &
     on utrnumunitrv of pduni_trav_dia


temporary t_qte_cum integer size 04
temporary t_qte_res integer size 04
temporary t_qte_dif float   size 08
temporary t_qte_sub float   size 08
temporary t_qte_prd float   size 08


item t_qte_cum =  diaqteplafingra of yvdiaqteaut - diaqtefingra of yvdiaqteaut
item t_qte_dif = (diaqteplafingra of yvdiaqteaut - diaqteemb    of ypddiagramme) * utdqteprd of pduni_trav_dia

item t_qte_sub =  t_qte_cum * utdqteprd of pduni_trav_dia                           &
              if (diaqtefingra of yvdiaqteaut > diaqteemb of ypddiagramme)          &
            else t_qte_dif

item t_qte_res subtotal t_qte_cum reset at utrnumunitrv
item t_qte_prd subtotal t_qte_sub reset at utrnumunitrv

subfile wpd891_635z keep at utrnumunitrv include ciecle       of pdpriorite_diag  , &
                                                 comnumcom    of pdpriorite_diag  , &
                                                 pricodpri    of pdpriorite_diag  , &
                                                 utrnumunitrv of pduni_trav_dia   , &
                                                 t_qte_res                        , &
                                                 t_qte_prd                          &
                                           index wpd891_635z_idx                    &
                                         segment ciecle                           , &
                                                 comnumcom                        , &
                                                 pricodpri

;-------------------------------------------------------------------------------
request r_tranche_donne

access pdcomman_interne link ciecle                                               , &
                             pjtabr                                               , &
                             pjtann                                               , &
                             comnumcomint                                         , &
                             "TR"                                                   &
                          to ciecle                                               , &
                             pjtabr                                               , &
                             pjtann                                               , &
                             comnumcomint                                         , &
                             tpdtypprd    of pddetail_c_i                           &

                        link ciecle       of pddetail_c_i                         , &
                             comnumcom    of pddetail_c_i                         , &
                             dcinumlig    of pddetail_c_i                           &
                         to  ciecle                                               , &
                             comnumcom                                            , &
                             dcinumlig    of pdembal_tranche optional               &

                        link ciecle       of pdembal_tranche                      , &
                             cainumcai    of pdembal_tranche                        &
                          to ciecle,                                                &
                             cainumcai    of pdlivre_caisson optional

choose ciecle    parm, &
       comnumcom parm, &
       comstu "C" "P"

sort on comnumcom of pdcomman_interne &
     on dcinumlig of pddetail_c_i     &
     on cainumcai of pdembal_tranche

define d_qte_emb   float     size 08 = 0

define d_cainumcai character size 09 = cainumcai of pdembal_tranche &
                                    if record pdembal_tranche exist

define d_emb       integer   size 04 = 1 if d_cainumcai <> " " else 0

define d_surface   float     size 08 = emtsurmc2ven of pdembal_tranche * 10.764262                 &
                                    if dciunmcmd    of pddetail_c_i  = "P2" and d_cainumcai <> " " &
                                  else emtsurmc2ven of pdembal_tranche                             &
                                    if dciunmcmd    of pddetail_c_i <> "P2" and d_cainumcai <> " "

define d_surf      float     size 08 = emtsurmc2ven of pdembal_tranche * 10.764262                 &
                                    if dciunmcmd    of pddetail_c_i  = "P2" and d_cainumcai <> " " &
                                  else emtsurmc2ven of pdembal_tranche                             &
                                    if dciunmcmd    of pddetail_c_i <> "P2" and d_cainumcai <> " "

subfile wpd661-tranche keep include ciecle          of pdcomman_interne, &
                                    comnumcom       of pdcomman_interne, &
                                    dcinumlig       of pddetail_c_i    , &
                                    dciqtecmd       of pddetail_c_i    , &
                                    dciunmcmd       of pddetail_c_i    , &
                                    d_cainumcai                        , &
                                    d_qte_emb                          , &
                                    d_emb                              , &
                                    d_surf                             , &
                                    d_surface

;-------------------------------------------------------------------------------
request r_tranche_quantite

access *wpd661-tranche

sort on comnumcom   of wpd661-tranche &
     on dcinumlig   of wpd661-tranche &
     on d_cainumcai of wpd661-tranche

temporary t_sub_emb integer size 04
temporary t_surface float   size 08

item t_surface subtotal d_surface of wpd661-tranche reset at d_cainumcai
item t_sub_emb subtotal d_emb     of wpd661-tranche reset at d_cainumcai

output wpd661-tranche update
  item d_qte_emb of wpd661-tranche final t_sub_emb
  item d_surface of wpd661-tranche final t_surface

;-------------------------------------------------------------------------------
request r_tranche_difference

access *wpd661-tranche

sort on ciecle      of wpd661-tranche &
     on comnumcom   of wpd661-tranche &
     on dcinumlig   of wpd661-tranche &
     on d_cainumcai of wpd661-tranche &
     on d_qte_emb   of wpd661-tranche

temporary t_surface float size 08
temporary t_qte_emb float size 08

item t_surface subtotal d_surface of wpd661-tranche at d_cainumcai reset at dcinumlig
item t_qte_emb subtotal d_qte_emb of wpd661-tranche at d_cainumcai reset at dcinumlig

output wpd661-tranche update at dcinumlig of wpd661-tranche
  item d_surf of wpd661-tranche final t_surface
  item d_emb  of wpd661-tranche final t_qte_emb

;-------------------------------------------------------------------------------
request r_tranche_lig

access *wpd661-tranche

sort on ciecle      of wpd661-tranche &
     on comnumcom   of wpd661-tranche &
     on dcinumlig   of wpd661-tranche &
     on d_cainumcai of wpd661-tranche &
     on d_qte_emb   of wpd661-tranche

temporary t_a_reste     float size 08
temporary t_b_reste     float size 08
temporary t_c_reste     float size 08
temporary t_c_reste_sub float size 08

item t_a_reste     =  dciqtecmd of wpd661-tranche - d_surf of wpd661-tranche  &
                  if  dciunmcmd of wpd661-tranche <> "P2"                     &
                else (dciqtecmd of wpd661-tranche - d_surf of wpd661-tranche) / 10.764262

item t_b_reste     =  dciqtecmd of wpd661-tranche - d_emb  of wpd661-tranche

item t_c_reste     = t_a_reste                                              &
                  if t_a_reste >= 0 and dciunmcmd of wpd661-tranche <> "TR" &
                else t_b_reste * 4                                          &
                  if t_b_reste >= 0 and dciunmcmd of wpd661-tranche  = "TR" &
                else 0

item t_c_reste_sub = t_c_reste_sub + t_c_reste          &
                  at dcinumlig of wpd661-tranche        &
            reset at comnumcom of wpd661-tranche

subfile wpd891_661 keep at comnumcom of wpd661-tranche  &
                        if t_c_reste_sub > 0            &
                   include ciecle    of wpd661-tranche, &
                           comnumcom of wpd661-tranche, &
                           t_c_reste_sub                &
                     index wpd891_661_idx               &
                   segment ciecle                     , &
                           comnumcom

;-------------------------------------------------------------------------------
request r_planif_back

access pdplanification link comnumcom of pdplanification   , &
                            ciecle    of pdplanification     &
                         to comnumcom                      , &
                            ciecle    of *ypdcomman_interne  optional

select if not record ypdcomman_interne exists

subfile wpdplanification keep include pdplanification

;-------------------------------------------------------------------------------
request r_planif_del_1

access pdplanification

output pdplanification delete

;-------------------------------------------------------------------------------
request r_planif_add

access *wpd891_635z link ciecle       of wpd891_635z    , &
                         utrnumunitrv of wpd891_635z      &
                      to ciecle                         , &
                         utrnumunitrv of pdunite_travail

select if t_qte_prd of wpd891_635z > 0

output pdplanification add
  item ciecle       of pdplanification initial ciecle       of wpd891_635z
  item comnumcom    of pdplanification initial comnumcom    of wpd891_635z
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri    of wpd891_635z
  item priqtecmd    of pdplanification initial 0
  item priqtedeb    of pdplanification initial 0
  item priqteemb    of pdplanification initial 0
  item utrnumunitrv of pdplanification initial utrnumunitrv of wpd891_635z
  item utdqteres    of pdplanification initial t_qte_res    of wpd891_635z
  item utdqteprd    of pdplanification initial t_qte_prd    of wpd891_635z

  item ppltmpest    of pdplanification initial round((t_qte_prd    of wpd891_635z                     &
                                             * ((nco(asc(utrtmpest of pdunite_travail,4)[1:2]) * 60)  &
                                             +  (nco(asc(utrtmpest of pdunite_travail,4)[3:2]))))     &
                                             / 3600,2)

;-------------------------------------------------------------------------------
request r_planif_add_update_01

access *wpd888_625z link ciecle    of wpd888_625z    , &
                         comnumcom of wpd888_625z    , &
                         pricodpri of wpd888_625z      &
                      to ciecle                      , &
                         comnumcom                   , &
                         pricodpri of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle    of wpd888_625z
  item comnumcom    of pdplanification initial comnumcom of wpd888_625z
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri of wpd888_625z
  item priqtecmd    of pdplanification =       round(t_qte_cmd of wpd888_625z,4)
  item priqtedeb    of pdplanification initial 0
  item priqteemb    of pdplanification initial 0
  item utdqteres    of pdplanification initial 0
  item utdqteprd    of pdplanification initial 0

;-------------------------------------------------------------------------------
request r_planif_add_update_02

access *wpd889_640zb link ciecle    of wpd889_640zb   , &
                          comnumcom of wpd889_640zb   , &
                          pricodpri of wpd889_640zb     &
                       to ciecle                      , &
                          comnumcom                   , &
                          pricodpri of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle      of wpd889_640zb
  item comnumcom    of pdplanification initial comnumcom   of wpd889_640zb
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri   of wpd889_640zb
  item priqtecmd    of pdplanification initial 0
  item priqtedeb    of pdplanification =       round(d_volmc2 of wpd889_640zb,4)
  item priqteemb    of pdplanification initial 0
  item utdqteres    of pdplanification initial 0
  item utdqteprd    of pdplanification initial 0

;-------------------------------------------------------------------------------
request r_planif_add_update_03

access *wpd890_656za link ciecle    of wpd890_656za   , &
                          comnumcom of wpd890_656za   , &
                          pricodpri of wpd890_656za     &
                       to ciecle                      , &
                          comnumcom                   , &
                          pricodpri of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle          of wpd890_656za
  item comnumcom    of pdplanification initial comnumcom       of wpd890_656za
  item pridatcom    of pdplanification initial 0
  item comdatdes    of pdplanification initial 0
  item comdatdia    of pdplanification initial 0
  item pricodpri    of pdplanification initial pricodpri       of wpd890_656za
  item priqtecmd    of pdplanification initial 0
  item priqtedeb    of pdplanification initial 0
  item priqteemb    of pdplanification =       round(d_qte_a_dbt_sur of wpd890_656za,4)
  item priqteext    of pdplanification =       round(d_sur_res_sct   of wpd890_656za,4)
  item utdqteres    of pdplanification initial 0
  item utdqteprd    of pdplanification initial 0


;-------------------------------------------------------------------------------
request r_planif_add_update_04

access *wpd891_661 link ciecle    of wpd891_661     , &
                        comnumcom of wpd891_661       &
                     to ciecle                      , &
                        comnumcom of pdplanification  optional

output pdplanification add update
  item ciecle       of pdplanification initial ciecle              of wpd891_661
  item comnumcom    of pdplanification initial comnumcom           of wpd891_661
  item dciqterstemb of pdplanification       = round(t_c_reste_sub of wpd891_661,2)
  item tpdtypprd    of pdplanification       = "TR"

;-------------------------------------------------------------------------------
request r_planif_add_update_05

access *ypdcomman_interne link ciecle       of ypdcomman_interne  , &
                               comnumcom    of ypdcomman_interne    &
                            to ciecle                             , &
                               comnumcom    of pdplanification opt  &

                          link ciecle       of ypdcomman_interne    &
                            to ciecle       of mccompagnie          &

                          link cliciecle    of ypdcomman_interne  , &
                               clicle       of ypdcomman_interne  , &
                               ciecle       of ypdcomman_interne    &
                            to cliciecle                          , &
                               clicle                             , &
                               ciecle       of vclc_cli             &

                          link ciecle       of ypdcomman_interne  , &
                               pjtabr       of ypdcomman_interne  , &
                               pjtann       of ypdcomman_interne  , &
                               comnumcomint of ypdcomman_interne  , &
                               "99"                                 &
                            to ciecle                             , &
                               pjtabr                             , &
                               pjtann                             , &
                               comnumcomint                       , &
                               pricodpri    of pdpriorite     opt

output pdplanification add update
  item ciecle          of pdplanification initial ciecle          of ypdcomman_interne
  item comnumcom       of pdplanification initial comnumcom       of ypdcomman_interne
  item pricodpri       of pdplanification initial " "             
  item utrnumunitrv    of pdplanification initial " "             
  item cienom          of pdplanification =       cienom          of mccompagnie
  item clicle          of pdplanification =       clicle          of ypdcomman_interne
  item clinom          of pdplanification =       clinom          of vclc_cli
  item comstu          of pdplanification =       comstu          of ypdcomman_interne
  item comnumcom       of pdplanification =       comnumcom       of ypdcomman_interne
  item comnom          of pdplanification =       comnom          of ypdcomman_interne
  item comdatcmd       of pdplanification =       comdatcmd       of ypdcomman_interne
  item pridatcom       of pdplanification =       pridatfin       of pdpriorite
  item comdes          of pdplanification =       comdes          of ypdcomman_interne
  item comdatdes       of pdplanification =       comdatdes       of ypdcomman_interne
  item comdia          of pdplanification =       comdia          of ypdcomman_interne
  item comdatdia       of pdplanification =       comdatdia       of ypdcomman_interne
  item commem001       of pdplanification =       commem001       of ypdcomman_interne
  item commem002       of pdplanification =       commem002       of ypdcomman_interne
  item commem003       of pdplanification =       commem003       of ypdcomman_interne
  item commem004       of pdplanification =       commem004       of ypdcomman_interne
  item commem005       of pdplanification =       commem005       of ypdcomman_interne
  item ppldatcre       of pdplanification =       sysdate
  item comremdatliv001 of pdplanification =       comremdatliv001 of ypdcomman_interne

;-------------------------------------------------------------------------------
request r_planif_update_02

access pdplanification link ciecle    of pdplanification, &
                            comnumcom of pdplanification, &
                            "DI"                          &
                         to ciecle                      , &
                            comnumcom                   , &
                            tpdtypprd of *ypddetail_c_i   optional &

                       link ciecle    of pdplanification, &
                            comnumcom of pdplanification, &
                            pricodpri of pdplanification  &
                         to ciecle                      , &
                            comnumcom                   , &
                            pricodpri of *ypdpriorite     optional

choose ciecle    parm, &
       comnumcom parm, &
       comstu    parm

output pdplanification update
  item dciqteunmprd of pdplanification final round(t_qte_det of ypddetail_c_i,2)
  item pridatfin    of pdplanification final pridatfin of ypdpriorite

;-------------------------------------------------------------------------------
request r_planif_transfert

access pdplanification link ciecle    of pdplanification                    , &
                            comnumcom of pdplanification                    , &
                            "DI"                                              &
                         to ciecle                                          , &
                            comnumcom                                       , &
                            tpdtypprd of *ypddetail_c_i             optional  &

                       link ciecle    of pdplanification                    , &
                            comnumcom of pdplanification                    , &
                            "TR"                                              &
                         to ciecle                                          , &
                            comnumcom                                       , &
                            tpdtypprd of *ypddetail_c_i alias a_dci optional

select if  record ypddetail_c_i exists or                                     &
          (record a_dci         exists and dciqterstemb of pdplanification > 0)

choose ciecle    parm, &
       comnumcom parm, &
       pricodpri parm, &
       comstu    parm

subfile ypdplanification keep include pdplanification

;-------------------------------------------------------------------------------
request r_planif_del_2

access pdplanification

output pdplanification delete

;-------------------------------------------------------------------------------
request r_planif_add

access *wpdplanification

subfile ypdplanification keep append include wpdplanification

;-------------------------------------------------------------------------------
request r_planif_index

access *ypdplanification

select if comnumcom of ypdplanification <> " " and            &
          comnom    of ypdplanification <> " "

subfile zpdplanification keep include ypdplanification        &
                                index zpdplanification_idx    &
                                        segment ciecle      , &
                                                comnumcom   , &
                                                pricodpri   , &
                                                utrnumunitrv

;-------------------------------------------------------------------------------
request r_planif_output

access *zpdplanification

choose viaindex zpdplanification_idx

sort on ciecle       of zpdplanification &
     on comnumcom    of zpdplanification &
     on pricodpri    of zpdplanification &
     on utrnumunitrv of zpdplanification

output pdplanification add


build pd891t
