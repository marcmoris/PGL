;/T/ Sommaire de la journalisation des équipements
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 25 mars 1994
;
;    Description...: Ce source général sert à imprimer pour n'importe
;                    quel programme de journalisation le sommaire de
;                    journalisation par équipements.
;
;
;
;/A/ Adaptation par Nancy Marceau, 94/06/15
;       - Ajouter les descriptions pour le module Achats & Réceptions ainsi
;         que le module inventaire.
;
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
SET REPORT SPACING 1

ACCESS *WUS900HE &

@IF VAXVMS
  LINK TO RECORD 1 &
@ELSE
  LINK TO RECORD 0 &
@ENDIF
                   OF *WUS900ER OPTIONAL &

  LINK CIECLE OF WUS900HE, &
       IMMCLE OF WUS900HE &
    TO CIECLE            , &
       IMMCLE OF IMIMMOBILISATION


SELECT IF NOT RECORD WUS900ER EXISTS

;
; Titre de la liste.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
  "Journalisation - sommaire équipements "
@ELSE
  "%%%Posting summary"
@ENDIF
;
; Deuxième ligne du titre.
;
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "
;
; Nom de la compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM
;
; Code de la liste.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "US900ZD"

;
; Nom du module comptable.
;
DEFINE D_HISCODMOD CHAR SIZE 40 = &
@IF FRANCAIS
                                  "Comptes à payer" &
@ELSE
                                  "Accounts payable" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "CP" &
                             ELSE &
@IF FRANCAIS
                                  "Comptes à recevoir" &
@ELSE
                                  "Accounts receivable" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "CR" &
                             ELSE &

@IF FRANCAIS
                                  "Grand Livre" &
@ELSE
                                  "General Ledger" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "GL" &
                             ELSE &
@IF FRANCAIS
                                  "Achats & Réceptions" &
@ELSE
                                  "%%%Achats & Réceptions" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "AR" &
                             ELSE &
@IF FRANCAIS
                                  "Inventaire" &
@ELSE
                                  "%%%Inventaire" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "IN"




 DEFINE D_HISTYPECR CHAR SIZE 40 = &
@IF FRANCAIS
                                   "Journal général" &
@ELSE
                                   "General journal" &
@ENDIF
                                     IF HISCODMOD OF WUS900HE = "GL" &
                              ELSE &
@IF FRANCAIS
                                   "Factures - notes de crédit" &
@ELSE
                                   "Invoices - credit note" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "FA" &
                              ELSE &
@IF FRANCAIS
                                   "Ajustements" &
@ELSE
                                   "Adjustements" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "AJ" &
                              ELSE &
@IF FRANCAIS
                                   "Relevés de dépense" &
@ELSE
                                   "Expense statements" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "RD" &
 ELSE &
@IF FRANCAIS
                                   "Encaissements" &
@ELSE
                                   "Payements receipt" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "EN" &
                              ELSE &
@IF FRANCAIS
                                   "Mauvaise créance" &
@ELSE
                                   "Bad debts" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "MC" &
                              ELSE &
@IF FRANCAIS
                                   "Paiements" &
@ELSE
                                   "Payments" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "CH" &
                              ELSE &
@IF FRANCAIS
                                   "Paiements annulés" &
@ELSE
                                   "Cancelled Payments" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "CA" &
                              ELSE &
@IF FRANCAIS
                                   "Réceptions" &
@ELSE
                                   "%%%Réceptions" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "RC" &
                              ELSE &
@IF FRANCAIS
                                   "Retours" &
@ELSE
                                   "Retours" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "RT" &
                              ELSE &
@IF FRANCAIS
                                   "Mouvements d'inventaire" &
@ELSE
                                   "Mouvements d'inventaire" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "MV"




;
; Montant débit / crédit.
;
DEFINE D_MNTDBT FLOAT SIZE 8 = ABSOLUTE( T_MNTGL OF WUS900HE ) &
                                     IF    (     T_CODDC OF WUS900HE = "D" &
                                             AND T_MNTGL OF WUS900HE > 0 ) &
                                        OR (     T_CODDC OF WUS900HE = "C" &
                                             AND T_MNTGL OF WUS900HE < 0 )
DEFINE D_MNTCRT FLOAT SIZE 8 = ABSOLUTE( T_MNTGL OF WUS900HE ) &
                                     IF    (     T_CODDC OF WUS900HE = "C" &
                                             AND T_MNTGL OF WUS900HE > 0 ) &
                                        OR (     T_CODDC OF WUS900HE = "D" &
                                             AND T_MNTGL OF WUS900HE < 0 )

DEFINE D_IMMDSC CHARACTER SIZE 48 = IMMCLE OF IMIMMOBILISATION

;ajout de define pour sort 4
DEFINE D_PECCLE_SIE CHAR SIZE 5 = "1" + PECCLE &
       IF          PECCLE[1:1] < "8"       &
       ELSE  "0" + PECCLE

SORT ON D_PECCLE_SIE &                  ; période
     ON HISNUMLOT &                     ; numéro de lot
     ON IMMCLE


USE ustitstd NOLIST  ; Titre standard pour l'application
  SKIP 2 &
@IF FRANCAIS
    TAB 005 "Module..:" D_HISCODMOD &
    TAB 102 "Période.:" PECCLE &
SKIP 1 &
    TAB 005 "Type....:" D_HISTYPECR &
    TAB 102 "Lot.....:" HISNUMLOT &
@ELSE
    TAB 005 "Module..:" D_HISCODMOD &
    TAB 102 "Period..:" PECCLE &
SKIP 1 &
    TAB 005 "Type....:" D_HISTYPECR &
    TAB 102 "Batch...:" HISNUMLOT &
@ENDIF
SKIP 2 &
    TAB 001 &
@IF FRANCAIS
      "    Numéro                                                                                     Débit          Crédit" &
@ELSE
      "%%% Numéro                                                                                     Débit          Crédit" &
@ENDIF

SKIP 1 &
    TAB 001 &
@IF FRANCAIS
      "    équipement                       " &
@ELSE
      "%%% équipement                       " &
@ENDIF

SKIP 1 &
    TAB 001 &
      "    ----------                                                                        --------------  --------------" &
SKIP 2



FOOT AT IMMCLE &
  TAB 005 IMMCLE    OF WUS900HE  &
  TAB 018 D_IMMDSC               &
  TAB 087 D_MNTDBT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ  &
  TAB 103 D_MNTCRT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ



FOOT AT HISNUMLOT &
  TAB 087 "--------------" &
  TAB 103 "--------------" &
SKIP 1 &
  TAB 071 &
@IF FRANCAIS
          "Total du lot:" &
@ELSE
          "Batch total :" &
@ENDIF
  TAB 087 D_MNTDBT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ &
  TAB 103 D_MNTCRT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ &
SKIP PAGE
; 
;
; 94-03-25 Nancy Marceau
;
;Titre standard...
;
;    Module..: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                               Période.: 99-99
;    Type....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                               Lot.....: xxxx
;
;    Numéro                                                                                     Débit          Crédit
;    équipement
;    ----------                                                                        --------------  --------------
;
;    xxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     999,999,999.99  999,999,999.99
;                                                                                      999,999,999.99  999,999,999.99
;                                                                                      --------------  --------------
;
;                                                                      Total du lot:   999,999,999.99  999,999,999.99
