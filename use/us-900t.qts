;/T/ Source général de journalisation
;
;;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Micheline Bélanger
;    Date Création.: 07-Juillet-1992
;
;===============================================================================
; <<DEBUT>>  TRAITEMENT GENERAUX A TOUS LES MODULES DE JOURNALISATION  <<DEBUT>>
;===============================================================================
;
;/V/ 3.00.03    Guy Chabot 20 mai 1994 (236)
;
;/M/ Modifié par...: Guy Chabot le 30-janvier-1995
;
;            But...: Modification pour la mise à jour de MCHIS_PROJET dans
;                    le but de sommariser du aux renversement des écritures
;                    de courus dans le module achat ( journalisation des
;                    réceptions ).
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par...: Marc Poulin
;    Date..........: 23 février 2000.
;
;    But...........: Éliminer du système les écriture de tpe ARRC et CPFR.
;
;    Référence.....: Stabilisation du module Achat/Réception Inventaire (point 24).
;
;    Signet........: MP-00-02-23_S24.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;
; Cette requète valide les ventilations selon divers criteres.
;
;
REQUEST VALIDE_COMPTE TERMINATE IF G_ERREUR = "ARRET"

ACCESS *WUS900HE &
  LINK CPTCLE OF WUS900HE &
    TO CPTCLE OF                      MCCOMPTE    OPTIONAL &

  LINK CIECLE OF WUS900HE, &
       UNACLE OF WUS900HE &
    TO CIECLE , &
       UNACLE OF                      MCUNITE_ADM OPTIONAL &

  LINK CPTCLE OF WUS900HE, &
       CIECLE OF WUS900HE, &
       UNACLE OF WUS900HE &
    TO CPTCLE, &
       CIECLE, &
       UNACLE OF                      MCCHARTE_UNA OPTIONAL &

  LINK DEVCLE OF WUS900HE &
    TO DEVCLE OF                      MCDEVISE ALIAS A_DEVISE_IMP OPTIONAL &

  LINK CIECLE OF WUS900HE , &
       PRJCLE OF WUS900HE   &
    TO CIECLE , &
       PRJCLE OF                      GPPROJETS OPTIONAL &

  LINK CIECLE OF WUS900HE , &
       PRJCLE OF WUS900HE , &
       PRACLE OF WUS900HE   &
    TO CIECLE , &
       PRJCLE , &
       PRACLE OF                      GPPRO_ACTIVITE OPTIONAL &

  LINK CIECLE OF WUS900HE , &
       IMMCLE OF WUS900HE   &
    TO CIECLE , &
       IMMCLE OF IMIMMOBILISATION OPTIONAL



TEMPORARY T_MESERR CHARACTER SIZE 80 ; Pour les erreurs.

; Ajustement pour le changement de siècle
DEFINE D_CPTPECDEBA_SIE CHARACTER SIZE 5 = "1" + CPTPECDEBA OF MCCOMPTE &
         IF CPTPECDEBA OF MCCOMPTE[1:1] < "8" &
         ELSE "0" + CPTPECDEBA OF MCCOMPTE

; Ajustement pour le changement de siècle
DEFINE D_CPTPECDEBI_SIE CHARACTER SIZE 5 = "1" + CPTPECDEBI OF MCCOMPTE &
         IF CPTPECDEBI OF MCCOMPTE[1:1] < "8" &
         ELSE "0" + CPTPECDEBI OF MCCOMPTE

; Ajustement pour le changement de siècle
DEFINE G_PECCLE_SIE CHARACTER SIZE 5 = "1" + G_PECCLE &
         IF G_PECCLE[1:1] < "8" &
         ELSE "0" + G_PECCLE

; Ajustement pour le changement de siècle
DEFINE D_CCUPECDEBA_SIE CHARACTER SIZE 5 = "1" + CCUPECDEBA OF MCCHARTE_UNA &
         IF CCUPECDEBA OF MCCHARTE_UNA[1:1] < "8" &
         ELSE "0" + CCUPECDEBA OF MCCHARTE_UNA

; Ajustement pour le changement de siècle
DEFINE D_CCUPECDEBI_SIE CHARACTER SIZE 5 = "1" + CCUPECDEBI OF MCCHARTE_UNA &
         IF CCUPECDEBI OF MCCHARTE_UNA[1:1] < "8" &
         ELSE "0" + CCUPECDEBI OF MCCHARTE_UNA

; Ajustement pour le changement de siècle
DEFINE D_CCUPECDEBA_SIE2 CHARACTER SIZE 5 = "1" + CCUPECDEBA OF MCCHARTE_UNA &
         IF CCUPECDEBA OF MCCHARTE_UNA[1:1] < "8" &
         ELSE "0" + CCUPECDEBA OF MCCHARTE_UNA

; Ajustement pour le changement de siècle
DEFINE D_CCUPECDEBI_SIE2 CHARACTER SIZE 5 = "1" + CCUPECDEBI OF MCCHARTE_UNA &
         IF CCUPECDEBI OF MCCHARTE_UNA[1:1] < "8" &
         ELSE "0" + CCUPECDEBI OF MCCHARTE_UNA


ITEM T_MESERR = &
;
; Ce compte n'existe pas.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " Ce compte n'existe pas. " &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " Nonexistant account." &
@ENDIF
  IF NOT RECORD MCCOMPTE EXIST &

ELSE &
;
; Cette unité administrative n'existe pas...
;
@IF FRANCAIS
  "("+UNACLE OF WUS900HE+")" + " Cette unité administrative n'existe pas..." &
@ELSE
  "("+UNACLE OF WUS900HE+")" + " Nonexistant administrative unit." &
@ENDIF
  IF NOT RECORD MCUNITE_ADM EXIST &

ELSE &
;
; On ne peut pas utliser ce compte dans une imputation.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " On ne peut pas utliser ce compte dans une imputation." &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " May not use this account in a charge." &

@ENDIF
    IF CPTTYP OF MCCOMPTE <> "R" &

ELSE &
;
; La devise du compte est différente de la devise de l'imputation.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " La devise du compte est différente de la devise de l'imputation." &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " The account currency differs from the charge currency." &
@ENDIF
   IF (CPTCAT OF MCCOMPTE = "CP" OR CPTCAT OF MCCOMPTE = "CR") AND &
      DEVCLE OF MCCOMPTE <> DEVCLE OF WUS900HE &

;ELSE &
;
; Erreur de devise; compte: 123456 devise: XXX , devise de la transaction: YYY
;
;@IF FRANCAIS
;  "Erreur de devise; compte: "    + &
;  CPTCLE OF WUS900HE              + &
;  " devise: "                     + &
;  DEVCLE OF MCCOMPTE              + &
;  " , devise de la transaction: " + &
;  DEVCLE OF WUS900HE                &
;@ELSE
;  "Currency error; Account: "     + &
;  CPTCLE OF WUS900HE              + &
;  " currency: "                   + &
;  DEVCLE OF MCCOMPTE              + &
;  " , transaction currency: "     + &
;  DEVCLE OF WUS900HE                &
;@ENDIF
;   IF    DEVCLE OF MCCOMPTE <> G_DEVCLE &
;     AND DEVCLE OF MCCOMPTE <> DEVCLE OF WUS900HE &

ELSE &
;
; La combinaison compte/unité adm n'existe pas.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " - " + &
  "("+UNACLE OF WUS900HE+")" + " La combinaison compte/unité adm n'existe pas." &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " - " + &
  "("+UNACLE OF WUS900HE+")" + " Nonexistant account/adm. unit combination"&
@ENDIF
  IF G_HLDCHTCPTRLT = "I" AND NOT RECORD MCCHARTE_UNA EXIST &

ELSE &
;
; Ce compte est inactif.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " Ce compte est inactif." &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " Inactive account." &
@ENDIF
; Ajustement pour le changement de siècle

  IF NOT (G_PECCLE_SIE >= D_CPTPECDEBA_SIE AND &
         (G_PECCLE_SIE < D_CPTPECDEBI_SIE OR CPTPECDEBI OF MCCOMPTE = "")) &

ELSE &
;
; La combinaison compte/unité adm. est inactif.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " - " + &
  "("+UNACLE OF WUS900HE+")" + " La combinaison compte/unité adm. est inactif." &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " - " + &
  "("+UNACLE OF WUS900HE+")" + " The account/adm. unit combination is inactive."&
@ENDIF
; Ajustement pour le changement de siècle

  IF ((G_PECCLE_SIE >= D_CCUPECDEBA_SIE AND &
       (G_PECCLE_SIE < D_CCUPECDEBI_SIE OR &
        CCUPECDEBI OF MCCHARTE_UNA = " ")) AND &
      (G_HLDCHTCPTRLT = "E") AND RECORD MCCHARTE_UNA EXIST) &

ELSE &
;
; La combinaison compte/unité adm. est inactif.
;
@IF FRANCAIS
  "("+CPTCLE OF WUS900HE+")" + " - " + &
  "("+UNACLE OF WUS900HE+")" + " La combinaison compte/unité adm. est inactif." &
@ELSE
  "("+CPTCLE OF WUS900HE+")" + " - " + &
  "("+UNACLE OF WUS900HE+")" + " The account/adm. unit combination is inactive."&
@ENDIF

  IF ((G_PECCLE_SIE < D_CCUPECDEBA_SIE2 OR &
       (G_PECCLE_SIE >= D_CCUPECDEBI_SIE2 AND &
        CCUPECDEBI OF MCCHARTE_UNA <> " ")) AND &
      (G_HLDCHTCPTRLT = "I") AND RECORD MCCHARTE_UNA EXIST) &
ELSE &
;
; Si le client n'utilise par la Gestion de projets, on ne valide par le
; projet et l'activit  de l'imputation. Le projet dans l'imputation est
; optionel.
;
  " " &
  IF    G_HLDNTNPRO = "0" &
     OR "" = TRUNCATE( PRJCLE OF WUS900HE ) &
ELSE &
;
; Ce projet n'existe pas.
;
@IF FRANCAIS
  "("+PRJCLE OF WUS900HE+")" + " Ce projet n'existe pas. " &
@ELSE
  "("+PRJCLE OF WUS900HE+")" + " Nonexistant project." &
@ENDIF
  IF NOT RECORD GPPROJETS EXIST &

ELSE &
;
; Cette activit  n'existe pas.
;
@IF FRANCAIS
  "Projet: "         + &
  PRJCLE OF WUS900HE + &
  " Activité: "      + &
  PRACLE OF WUS900HE + &
  ", Cette activité n'existe pas." &
@ELSE
  "Project: "        + &
  PRJCLE OF WUS900HE + &
  " Activity: "      + &
  PRACLE OF WUS900HE + &
  ", Nonexistant activity." &
@ENDIF
  IF NOT RECORD GPPRO_ACTIVITE EXIST &

ELSE &
;
; Ce projet est inactif.
;
@IF FRANCAIS
  "("+PRJCLE OF WUS900HE+")" + " Ce projet est inactif." &
@ELSE
  "("+PRJCLE OF WUS900HE+")" + " Inactive project." &
@ENDIF
  IF PRJSTU OF GPPROJETS <> "A" &

ELSE &
;
; Cette activité est inactive.
;
@IF FRANCAIS
  "Projet: "         + &
  PRJCLE OF WUS900HE + &
  " Activit : "      + &
  PRACLE OF WUS900HE + &
  ", Cette activité est inactive." &
@ELSE
  "Project: "        + &
  PRJCLE OF WUS900HE + &
  " Activity: "      + &
  PRACLE OF WUS900HE + &
  ", Inactive activity." &
@ENDIF
  IF PRASTU OF GPPRO_ACTIVITE <> "A" &

ELSE &

; Cette équipement n'existe pas

@IF FRANCAIS
  "("+IMMCLE OF WUS900HE+")" + " Cet immobilisation n'existe pas. " &
@ELSE
  "("+IMMCLE OF WUS900HE+")" + " Cet immobilisation n'existe pas. " &
@ENDIF
  IF NOT RECORD IMIMMOBILISATION EXIST &

ELSE &

; Cette équipement est inactive

@IF FRANCAIS
  "("+IMMCLE OF WUS900HE +")" + " Cet immobilisation est inactif." &
@ELSE
  "("+IMMCLE OF WUS900HE +")" + " Cet immobilisation est inactif." &
@ENDIF
  IF IMMSTU OF IMIMMOBILISATION <> "A" &
ELSE ""




ITEM G_ERREUR = "ARRET" IF T_MESERR <> ""

;
;
OUTPUT *WUS900ER ADD &
                 IF T_MESERR <> ""
  ITEM CIECLE   OF WUS900ER FINAL HISCIECLE OF WUS900HE
  ITEM PECCLE   OF WUS900ER FINAL G_PECCLE
  ITEM T_MESERR OF WUS900ER FINAL T_MESERR


;
;  On converti les montants d'imputation selon la devise du compte.
;
ITEM G_FLGDEV = "1" IF DEVCLE OF MCCOMPTE <> DEVCLE OF WUS900HE
;
OUTPUT WUS900HE UPDATE &
                IF DEVCLE OF MCCOMPTE <> DEVCLE OF WUS900HE
  ITEM T_MNTGL OF WUS900HE FINAL &
                             ROUND(T_MNTGL OF WUS900HE * DEVTAU OF A_DEVISE_IMP)


;
; On garde les lignes d'imputation pour les comptes li s.
;
SUBFILE WUS900CL IF 0 <> SIZE( TRUNCATE(CPTCPTLIE OF MCCOMPTE) ) &
  INCLUDE WUS900HE, &
          DEVCLE    OF MCCOMPTE ALIAS CPTDEVCLE, &
          CPTCPTLIE OF MCCOMPTE


;-------------------------------------------------------------------------------
;
;
; Ajout des lignes d'imputation pour les comptes li s.
;
;
REQUEST TRAITE_COMPTE_LIES

ACCESS *WUS900CL &
  LINK CPTDEVCLE OF WUS900CL &
    TO DEVCLE OF MCDEVISE


;
; On indique qu'il y eu une convertion de montant du au traitement des
; devises.
;
ITEM G_FLGDEV = "1"

;
; On g n re les ventilations n cessaire dans le cas
; de compte li s.
;
OUTPUT *WUS900HE ADD
  ITEM HISCLE     OF WUS900HE FINAL HISCLE      OF WUS900CL
  ITEM HISCIECLE  OF WUS900HE FINAL HISCIECLE   OF WUS900CL
  ITEM CPTCLE     OF WUS900HE FINAL CPTCPTLIE   OF WUS900CL
  ITEM HECCIEINT  OF WUS900HE FINAL HECCIEINT   OF WUS900CL
  ITEM HECUNAINT  OF WUS900HE FINAL HECUNAINT   OF WUS900CL
  ITEM CIECLE     OF WUS900HE FINAL CIECLE      OF WUS900CL
  ITEM UNACLE     OF WUS900HE FINAL UNACLE      OF WUS900CL
  ITEM PRJCLE     OF WUS900HE FINAL PRJCLE      OF WUS900CL
  ITEM PRACLE     OF WUS900HE FINAL PRACLE      OF WUS900CL
  ITEM IMMCLE     OF WUS900HE FINAL IMMCLE      OF WUS900CL
  ITEM DEVCLE     OF WUS900HE FINAL DEVCLE      OF WUS900CL
  ITEM PECCLE     OF WUS900HE FINAL PECCLE      OF WUS900CL
  ITEM HECNUMDOC2 OF WUS900HE FINAL HECNUMDOC2  OF WUS900CL
  ITEM T_CODDC    OF WUS900HE FINAL T_CODDC     OF WUS900CL
  ITEM T_MNTGL    OF WUS900HE FINAL &
                          ROUND(T_MNTORI OF WUS900CL * DEVTAU OF MCDEVISE) &
                          - T_MNTORI OF WUS900CL
  ITEM T_MNTORI   OF WUS900HE FINAL 0
  ITEM HECFLGINT  OF WUS900HE FINAL HECFLGINT   OF WUS900CL
  ITEM HISCODMOD  OF WUS900HE FINAL HISCODMOD   OF WUS900CL
  ITEM HISTYPECR  OF WUS900HE FINAL HISTYPECR   OF WUS900CL
  ITEM HISNUMLOT  OF WUS900HE FINAL HISNUMLOT   OF WUS900CL



;-------------------------------------------------------------------------------
;
;
; Cette requete sert a balancer a la cent les d bits et cr dits de
; chacune des  critures du aux convertion en devise.
; S'il y a une diff rence entre les d bit et les cr dits, on ajoute
; cette diff rence a la derniere ligne de ventilation de l'ecriture
; pour la faire balancer.
;
;
REQUEST BALANCE_VENTILATION SKIP IF G_FLGDEV = "0"

ACCESS *WUS900HE


TEMPORARY T_MNTDBT FLOAT SIZE 8 ; pour total des d bits
TEMPORARY T_MNTCRT FLOAT SIZE 8 ; pour total des cr dits
TEMPORARY T_MNTDIF FLOAT SIZE 8 ; pour diff rence a ajouter


;
; Si le montant de la ventilation a  t  converti, alors le montant-gl et le
; montant d'origine sont diff rent, sinon les deux montants sont  gales. Le
; but de cette condition est de balancer les lignes de ventilation qui on
;  t  convertis.
;
DEFINE D_TRI INTEGER SIZE 2 = 1 IF T_MNTGL = T_MNTORI &
                         ELSE 2

;
; Le tri suivant pr sente en premier les ventilations non-convertis et en second les ventilations
; convertis. Donc si on balance la derni re ligne de ventilation, se sera un ventilation converti.
;
SORT ON HISCLE OF WUS900HE &
     ON D_TRI              &
     ON CPTCLE OF WUS900HE &
     ON UNACLE OF WUS900HE &
     ON PRJCLE OF WUS900HE &
     ON PRACLE OF WUS900HE &
     ON IMMCLE OF WUS900HE


ITEM T_MNTDBT SUBTOTAL T_MNTGL OF WUS900HE &
                IF T_CODDC OF WUS900HE = "D" &
                RESET AT HISCLE

ITEM T_MNTCRT SUBTOTAL T_MNTGL OF WUS900HE &
                IF T_CODDC OF WUS900HE = "C" &
                RESET AT HISCLE
;
; Une ligne de ventilation est d bit ou cr dit, c'est pour cette raison qu'il
; faut soit ajouter ou diminuer au montant selon le sens du compte de la ligne
; vis e.
;
ITEM T_MNTDIF = T_MNTCRT - T_MNTDBT IF T_CODDC OF WUS900HE = "D" &
           ELSE T_MNTDBT - T_MNTCRT


OUTPUT WUS900HE UPDATE AT HISCLE &
                IF 0 <> T_MNTDBT - T_MNTCRT
  ITEM T_MNTGL OF WUS900HE FINAL T_MNTGL OF WUS900HE + T_MNTDIF




;-------------------------------------------------------------------------------
;
;
; Mise   jour de la relation de l'historique des  critures comptables.
;
;
REQUEST MAJ_MCHISTO           TERMINATE IF G_ERREUR = "ARRET"

ACCESS *WUS900H

SELECT IF NOT ((G_CODMOD = "AR" AND HISTYPECR OF WUS900H = "RC") OR & ; MP-00-02-23_S24
               (G_CODMOD = "CP" AND HISTYPECR OF WUS900H = "FR"))     ; MP-00-02-23_S24

OUTPUT MCHISTO ADD
  ITEM HISCLE     OF MCHISTO INITIAL HISCLE     OF WUS900H
  ITEM HISCIECLE  OF MCHISTO INITIAL HISCIECLE  OF WUS900H
  ITEM REFCIECLE  OF MCHISTO INITIAL REFCIECLE  OF WUS900H
  ITEM REFCLETYP  OF MCHISTO INITIAL REFCLETYP  OF WUS900H
  ITEM REFCLENUM  OF MCHISTO INITIAL REFCLENUM  OF WUS900H
  ITEM HISNUMDOC  OF MCHISTO INITIAL HISNUMDOC  OF WUS900H
  ITEM SANANN     OF MCHISTO INITIAL PECCLE     OF WUS900H
  ITEM PECCLE     OF MCHISTO INITIAL PECCLE     OF WUS900H
  ITEM HISNUMLOT  OF MCHISTO INITIAL HISNUMLOT  OF WUS900H
  ITEM HISDAT     OF MCHISTO INITIAL HISDAT     OF WUS900H
  ITEM HISDATJOU  OF MCHISTO INITIAL HISDATJOU  OF WUS900H
  ITEM HISCODMOD  OF MCHISTO INITIAL G_CODMOD
  ITEM HISTYPECR  OF MCHISTO INITIAL HISTYPECR  OF WUS900H
  ITEM HISDSCGEN  OF MCHISTO INITIAL HISDSCGEN  OF WUS900H
  ITEM HISDSCSAI  OF MCHISTO INITIAL HISDSCSAI  OF WUS900H
  ITEM HISDSCSAI2 OF MCHISTO INITIAL HISDSCSAI2 OF WUS900H
  ITEM HISUSRCRE  OF MCHISTO INITIAL HISUSRCRE  OF WUS900H
  ITEM DEVCLE     OF MCHISTO INITIAL DEVCLE     OF WUS900H
  ITEM HISCLE1    OF MCHISTO INITIAL HISCLE1    OF WUS900H
  ITEM HISCLE2    OF MCHISTO INITIAL HISCLE2    OF WUS900H
  ITEM HISCLE3    OF MCHISTO INITIAL HISCLE3    OF WUS900H




;-------------------------------------------------------------------------------
;
;
; Cette requete met a jour le fichier de l'historique des ventilations
; des  critures comptables.
;
;
REQUEST  MAJ_MCHIS_ECR

ACCESS *WUS900HE

SELECT IF NOT ((G_CODMOD = "AR" AND HISTYPECR OF WUS900HE = "RC") OR & ; MP-00-02-23_S24
               (G_CODMOD = "CP" AND HISTYPECR OF WUS900HE = "FR"))     ; MP-00-02-23_S24

TEMPORARY T_MNTDBT       FLOAT   SIZE 8 ; Montant d bits
TEMPORARY T_MNTCRT       FLOAT   SIZE 8 ; Montant cr dits
TEMPORARY T_MNTDBTORI    FLOAT   SIZE 8 ; Montant d bits d'origine
TEMPORARY T_MNTCRTORI    FLOAT   SIZE 8 ; Montant cr dits d'origine
TEMPORARY T_TOTMNTDBT    FLOAT   SIZE 8
TEMPORARY T_TOTMNTCRT    FLOAT   SIZE 8
TEMPORARY T_TOTMNTDBTORI FLOAT   SIZE 8
TEMPORARY T_TOTMNTCRTORI FLOAT   SIZE 8


SORT ON HISCLE    OF WUS900HE &
     ON CPTCLE    OF WUS900HE &
     ON CIECLE    OF WUS900HE &
     ON UNACLE    OF WUS900HE &
     ON HECCIEINT OF WUS900HE &
     ON HECUNAINT OF WUS900HE

ITEM T_MNTDBT    = ABSOLUTE(T_MNTGL) IF ( T_CODDC = "D" AND T_MNTGL > 0 ) OR &
                                        ( T_CODDC = "C" AND T_MNTGL < 0 ) &
              ELSE 0
ITEM T_MNTCRT    = ABSOLUTE(T_MNTGL) IF ( T_CODDC = "C" AND T_MNTGL > 0 ) OR &
                                        ( T_CODDC = "D" AND T_MNTGL < 0 ) &
              ELSE 0
ITEM T_MNTDBTORI = ABSOLUTE(T_MNTORI) IF ( T_CODDC = "D" AND T_MNTORI > 0 ) OR &
                                         ( T_CODDC = "C" AND T_MNTORI < 0 ) &
              ELSE 0
ITEM T_MNTCRTORI = ABSOLUTE(T_MNTORI) IF ( T_CODDC = "C" AND T_MNTORI > 0 ) OR &
                                         ( T_CODDC = "D" AND T_MNTORI < 0 ) &
              ELSE 0


ITEM T_TOTMNTDBT    SUBTOTAL T_MNTDBT    RESET AT HECUNAINT
ITEM T_TOTMNTCRT    SUBTOTAL T_MNTCRT    RESET AT HECUNAINT
ITEM T_TOTMNTDBTORI SUBTOTAL T_MNTDBTORI RESET AT HECUNAINT
ITEM T_TOTMNTCRTORI SUBTOTAL T_MNTCRTORI RESET AT HECUNAINT


OUTPUT MCHIS_ECR ADD AT HECUNAINT &
                 IF T_TOTMNTDBT <> T_TOTMNTCRT OR &
                    T_TOTMNTDBTORI <> T_TOTMNTCRTORI
  ITEM HISCLE       OF MCHIS_ECR INITIAL HISCLE     OF WUS900HE
  ITEM CPTCLE       OF MCHIS_ECR INITIAL CPTCLE     OF WUS900HE
  ITEM HECCIEINT    OF MCHIS_ECR INITIAL HECCIEINT  OF WUS900HE
  ITEM HECUNAINT    OF MCHIS_ECR INITIAL HECUNAINT  OF WUS900HE
  ITEM CIECLE       OF MCHIS_ECR INITIAL CIECLE     OF WUS900HE
  ITEM UNACLE       OF MCHIS_ECR INITIAL UNACLE     OF WUS900HE
  ITEM PECCLE       OF MCHIS_ECR INITIAL PECCLE     OF WUS900HE
  ITEM HECNUMDOC2   OF MCHIS_ECR INITIAL HECNUMDOC2 OF WUS900HE
  ITEM HECMNTDBT    OF MCHIS_ECR    =    T_TOTMNTDBT - T_TOTMNTCRT &
                                           IF T_TOTMNTDBT > T_TOTMNTCRT &
                                    ELSE 0
  ITEM HECMNTCRT    OF MCHIS_ECR    =    T_TOTMNTCRT - T_TOTMNTDBT &
                                           IF T_TOTMNTCRT > T_TOTMNTDBT &
                                    ELSE 0
  ITEM HECMNTDBTORI OF MCHIS_ECR    =    T_TOTMNTDBTORI - T_TOTMNTCRTORI &
                                           IF T_TOTMNTDBTORI > T_TOTMNTCRTORI &
                                    ELSE 0
  ITEM HECMNTCRTORI OF MCHIS_ECR    =    T_TOTMNTCRTORI - T_TOTMNTDBTORI &
                                           IF T_TOTMNTCRTORI > T_TOTMNTDBTORI &
                                    ELSE 0
  ITEM HECFLGINT    OF MCHIS_ECR INITIAL HECFLGINT  OF WUS900HE



;-------------------------------------------------------------------------------
;
;
; Mise   jour de la relation d'historique pour la gestion de projet.
;
;
REQUEST MAJ_MCHIS_PROJET               SKIP IF G_HLDNTNPRO = "0"

ACCESS *WUS900HE

SELECT IF PRJCLE OF WUS900HE <> " "                          AND &
      NOT ((G_CODMOD = "AR" AND HISTYPECR OF WUS900HE = "RC") OR & ; MP-00-02-23_S24
           (G_CODMOD = "CP" AND HISTYPECR OF WUS900HE = "FR"))     ; MP-00-02-23_S24

TEMPORARY T_NUMLIG INTEGER SIZE 4

TEMPORARY T_MNTDBT    FLOAT SIZE 8
TEMPORARY T_MNTCRT    FLOAT SIZE 8
TEMPORARY T_MNTDBTORI FLOAT SIZE 8
TEMPORARY T_MNTCRTORI FLOAT SIZE 8

TEMPORARY T_DBTTOT    FLOAT SIZE 8
TEMPORARY T_CRTTOT    FLOAT SIZE 8
TEMPORARY T_DBTORITOT FLOAT SIZE 8
TEMPORARY T_CRTORITOT FLOAT SIZE 8

;
; Trie par pièce et ligne d'imputation
;

SORT ON HISCLE OF WUS900HE &
     ON CIECLE OF WUS900HE &
     ON PRJCLE OF WUS900HE &
     ON PRACLE OF WUS900HE &
     ON IMMCLE OF WUS900HE &
     ON CPTCLE OF WUS900HE &
     ON UNACLE OF WUS900HE

ITEM T_NUMLIG COUNT RESET AT HISCLE

;
; Vérification des montants débit/crédit selon la nature du poste
;

ITEM T_MNTDBT = ABSOLUTE( T_MNTGL ) &
                IF ( T_CODDC = "D" AND T_MNTGL > 0 ) OR &
                   ( T_CODDC = "C" AND T_MNTGL < 0 ) &
                ELSE 0

ITEM T_MNTCRT = ABSOLUTE( T_MNTGL ) &
                IF ( T_CODDC = "C" AND T_MNTGL > 0 ) OR &
                   ( T_CODDC = "D" AND T_MNTGL < 0 ) &
                ELSE 0

ITEM T_MNTDBTORI = ABSOLUTE( T_MNTGL ) &
                IF ( T_CODDC = "D" AND T_MNTORI > 0 ) OR &
                   ( T_CODDC = "C" AND T_MNTORI < 0 ) &
                ELSE 0

ITEM T_MNTCRTORI = ABSOLUTE( T_MNTGL ) &
                IF ( T_CODDC = "C" AND T_MNTORI > 0 ) OR &
                   ( T_CODDC = "D" AND T_MNTORI < 0 ) &
                ELSE 0

;
; Sommarisation des montants
;

ITEM T_DBTTOT    SUBTOTAL T_MNTDBT    RESET AT UNACLE
ITEM T_CRTTOT    SUBTOTAL T_MNTCRT    RESET AT UNACLE
ITEM T_DBTORITOT SUBTOTAL T_MNTDBTORI RESET AT UNACLE
ITEM T_CRTORITOT SUBTOTAL T_MNTCRTORI RESET AT UNACLE

;
; Un déclencheur est activé pour la mise à jour de la gestion de projet
;

OUTPUT MCHIS_PROJET ADD AT UNACLE
  ITEM HISCLE       OF MCHIS_PROJET INITIAL HISCLE OF WUS900HE
  ITEM HIPNUMLIG    OF MCHIS_PROJET    =    T_NUMLIG
  ITEM PECCLE       OF MCHIS_PROJET INITIAL PECCLE OF WUS900HE
  ITEM CIECLE       OF MCHIS_PROJET INITIAL CIECLE OF WUS900HE
  ITEM PRJCLE       OF MCHIS_PROJET INITIAL PRJCLE OF WUS900HE
  ITEM PRACLE       OF MCHIS_PROJET INITIAL PRACLE OF WUS900HE
  ITEM IMMCLE       OF MCHIS_PROJET INITIAL IMMCLE OF WUS900HE
  ITEM UNACLE       OF MCHIS_PROJET INITIAL UNACLE OF WUS900HE
  ITEM CPTCLE       OF MCHIS_PROJET INITIAL CPTCLE OF WUS900HE
  ITEM HIPMNTDBT    OF MCHIS_PROJET = T_DBTTOT
  ITEM HIPMNTCRT    OF MCHIS_PROJET = T_CRTTOT
  ITEM HIPMNTDBTORI OF MCHIS_PROJET = T_DBTORITOT
  ITEM HIPMNTCRTORI OF MCHIS_PROJET = T_CRTORITOT

