;/T/ Procédure de conversion des chiffres en lettres
;
;/P/ Prosig informatique, Marc Morissette

; * NOTE *
;
;  - Le montant doit avoir 2 décimales
;  - Le montant doit être inférieur à 9 millions
;
; -> Vecteur [ NCONVERT( Nombre charatère ) * 11 + 1 : 11] <-
; Cette formule permet accéder les éléments d'un vecteur
;
;  le ( * 11 + 1 ) est le déplacement a faire dans le vecteur
;  le ( : 11 ) permet de prendre les 11 caractères de la position désiré
;

;-------------
; Montant passé en paramètre
;** DEFINE D-MNT-PARM  FLOAT     SIZE 8 = Montant a transformé
;** DEFINE D_MNTLNG    CHARACTER SIZE 1 = Langne d'affichage "F" ou "A"
;------------

; Transforme le montant en charactère
DEFINE D_MNTCNV CHARACTER SIZE 11 = ASCII( D-MNT-PARM ,9 ) IF D_MNTLNG = "F" &
                               ELSE ASCII( D-MNT-PARM ,11 )

;************************************************************
;******* Transformation francais ****************************
;************************************************************


; vecteur conservant les valeurs entre 0 et  9
DEFINE D_CENTAINE CHARACTER SIZE 200 = &
   "           " + &
   "Un         " + &
   "Deux       " + &
   "Trois      " + &
   "Quatre     " + &
   "Cinq       " + &
   "Six        " + &
   "Sept       " + &
   "Huit       " + &
   "Neuf       "

; Vecteur conservant les dizaines
DEFINE D_DIZAINE CHARACTER SIZE 200 = &
   "            " + &
   "Dix         " + &
   "Vingt       " + &
   "Trente      " + &
   "Quarante    " + &
   "Cinquante   " + &
   "Soixante    " + &
   "Soixante    " + &
   "Quatre-Vingt" + &
   "Quatre-Vingt"

; Vecteur conservant les unités pour les montants plus petit que 100 (-)
DEFINE D_UNITE CHARACTER SIZE 300 = &
   "           " + &
   " et Un     " + &
   "-Deux      " + &
   "-Trois     " + &
   "-Quatre    " + &
   "-Cinq      " + &
   "-Six       " + &
   "-Sept      " + &
   "-Huit      " + &
   "-Neuf      " + &
   "-Dix       " + &
   " et Onze   " + &
   "-Douze     " + &
   "-Treize    " + &
   "-Quatorze  " + &
   "-Quinze    " + &
   "-Seize     " + &
   "-Dix-Sept  " + &
   "-Dix-Huit  " + &
   "-Dix-Neuf  "

;Vecteur conservant les unités sans tiret
DEFINE D_UNITE-2 CHARACTER SIZE 300 = &
   "           " + &
   " Un        " + &
   " Deux      " + &
   " Trois     " + &
   " Quatre    " + &
   " Cinq      " + &
   " Six       " + &
   " Sept      " + &
   " Huit      " + &
   " Neuf      " + &
   " Dix       " + &
   " Onze      " + &
   " Douze     " + &
   " Treize    " + &
   " Quatorze  " + &
   " Quinze    " + &
   " Seize     " + &
   " Dix-Sept  " + &
   " Dix-Huit  " + &
   " Dix-Neuf  "

;
; Traite les montant de 1 million et plus
;
DEFINE D_MNTCNV-MILLION CHARACTER SIZE 25 = &
     PACK ( D_CENTAINE[ NCONVERT( D_MNTCNV[1:1] ) * 11 + 1 : 11] + " Million" )  IF D_MNTCNV[1:1] = "1" &
ELSE PACK ( D_CENTAINE[ NCONVERT( D_MNTCNV[1:1] ) * 11 + 1 : 11] + " Millions" ) IF D_MNTCNV[1:1] <> "0"

;
; Traite la partie des centaines de mille
;

DEFINE D_CENTAINE-MILLE CHARACTER SIZE 25 = &
       " Cent "  IF D_MNTCNV[3:2] = "00" AND D_MNTCNV[2:1] = "1" &
 ELSE  " Cent "  IF D_MNTCNV[2:1] = "1" &
 ELSE  PACK ( D_CENTAINE   [ NCONVERT( D_MNTCNV[2:1] ) * 11 + 1 : 11] + " Cent " )  IF D_MNTCNV[2:1] <> "0"

;
; Traite la partie des dizaines de mille
;
; Particularité l'on ne traite pas les montants entre 70 et 80
;            ni les montats

DEFINE D_DIZAINE-MILLE CHARACTER SIZE 25 = &
   PACK( D_DIZAINE[ NCONVERT( D_MNTCNV[3:1] ) * 12 + 1 : 12] ) &
     IF D_MNTCNV[3:1] <> "0" AND &
          (D_MNTCNV[3:1] <> "7" OR &
           D_MNTCNV[3:1] <> "9" ) AND &
        19 < NCONVERT( D_MNTCNV[3:2] )

;
; Traite la partie des unités de mille
; Si unité = 1 alors j'écrit Mille et non Un Mille
; Si unité est entre 10 et 20   |
; Si unité est entre 70 et 80    -> Utilise le vecteur des unites sans tiret
; Si unite est entre 90 et 100  |

DEFINE D_UNITE-MILLE CHARACTER SIZE 25 = &
      " Mille " IF D_MNTCNV[2:3] = "001"  &
 ELSE PACK ( D_UNITE-2[ NCONVERT( D_MNTCNV[3:2] ) * 11 + 1 : 11] + " Mille " ) &
        IF 20 >  NCONVERT( D_MNTCNV[3:2] ) AND D_MNTCNV[2:3] <> "000" &
 ELSE "-Un Mille"    IF 81 = NCONVERT( D_MNTCNV[3:2] ) &
 ELSE "-Onze Mille"  IF 91 = NCONVERT( D_MNTCNV[3:2] ) &
 ELSE PACK ( D_UNITE[ (NCONVERT( D_MNTCNV[3:2]) - 60)  * 11 + 1 : 11] + " Mille ") &
        IF (70 <= NCONVERT( D_MNTCNV[3:2] ) AND 80 >  NCONVERT( D_MNTCNV[3:2] )) &
 ELSE PACK ( D_UNITE[ (NCONVERT( D_MNTCNV[3:2]) - 80 ) * 11 + 1 : 11] + " Mille ") &
        IF (90 <= NCONVERT( D_MNTCNV[3:2] ) AND 99 >=  NCONVERT( D_MNTCNV[3:2] )) &
 ELSE PACK ( D_UNITE[ NCONVERT( D_MNTCNV[4:1] ) * 11 + 1 : 11]  + " Mille " ) &
        IF 20 <= NCONVERT( D_MNTCNV[3:2] )

; Traite la partie des centaines

DEFINE D_MNTCNV-CENT CHARACTER SIZE 25 = &
 PACK( D_CENTAINE[ NCONVERT( D_MNTCNV[5:1] ) * 11 + 1 : 11] + " Cents " )       IF D_MNTCNV[6:2] = "00" AND D_MNTCNV[5:1] > "1" &
 ELSE " Cent " IF D_MNTCNV[5:1] = "1" &
 ELSE PACK ( D_CENTAINE[ NCONVERT( D_MNTCNV[5:1] ) * 11 + 1 : 11] + " Cent " )  IF D_MNTCNV[5:1] <> "0"


;
; Traite la partie des dizaines
;
DEFINE D_MNTCNV-DIZAINE CHARACTER SIZE 25 = &
 " Quatre-Vingts " IF D_MNTCNV[6:2] = "80" &
 ELSE PACK( D_DIZAINE[ NCONVERT( D_MNTCNV[6:1] ) * 12 + 1 : 12] ) &
 IF D_MNTCNV[6:1] <> "0" AND (D_MNTCNV[6:1] <> "7" OR D_MNTCNV[6:1] <> "9" ) AND &
    19 < NCONVERT( D_MNTCNV[6:2] )

;
; Traite la partie des unités
;
DEFINE D_MNTCNV-UNITE CHARACTER SIZE 25 = &
 PACK ( D_UNITE-2[ NCONVERT( D_MNTCNV[6:2] ) * 11 + 1 : 11] )  IF 20 >  NCONVERT( D_MNTCNV[6:2] ) &
 ELSE "-Un "    IF 81 = NCONVERT( D_MNTCNV[6:2] ) &
 ELSE "-Onze "  IF 91 = NCONVERT( D_MNTCNV[6:2] ) &
 ELSE PACK ( D_UNITE[ (NCONVERT( D_MNTCNV[6:2]) - 60)  * 11 + 1 : 11] ) &
      IF (70 <= NCONVERT( D_MNTCNV[6:2] ) AND 80 >  NCONVERT( D_MNTCNV[6:2] )) &
 ELSE PACK ( D_UNITE[ (NCONVERT( D_MNTCNV[6:2]) - 80 ) * 11 + 1 : 11] ) &
      IF (90 <= NCONVERT( D_MNTCNV[6:2] ) AND 99 >=  NCONVERT( D_MNTCNV[6:2] )) &
 ELSE PACK ( D_UNITE[ NCONVERT( D_MNTCNV[7:1] ) * 11 + 1 : 11] ) &
      IF 20 <= NCONVERT( D_MNTCNV[6:2] )






; Le plus Grand chiffre qui peut être écrit est de 175 charactère ( CHAR 180 )
;
DEFINE D_MNTFMTFRA CHARACTER SIZE 180 = &
      "La somme de : " + D_MNTCNV[8:2] + "/100"                 IF D-MNT-PARM <= 99 &
ELSE  "La somme de : " + " Un Dollar " + D_MNTCNV[8:2] + "/100" IF D-MNT-PARM <= 199 &
ELSE  "La somme de : " + &
  PACK (D_MNTCNV-MILLION + D_CENTAINE-MILLE + TRUNC(D_DIZAINE-MILLE) + D_UNITE-MILLE + &
        D_MNTCNV-CENT + TRUNC(D_MNTCNV-DIZAINE) + D_MNTCNV-UNITE + " Dollars   " + D_MNTCNV[8:2] + "/100") &
  IF 0 <> INDEX(D_MNTCNV-UNITE, "-") AND 0 <> INDEX(D_UNITE-MILLE, "-") &
ELSE "La somme de : " + &
  PACK (D_MNTCNV-MILLION + D_CENTAINE-MILLE + TRUNC(D_DIZAINE-MILLE) + D_UNITE-MILLE + &
        D_MNTCNV-CENT + D_MNTCNV-DIZAINE + D_MNTCNV-UNITE + " Dollars   " + D_MNTCNV[8:2] + "/100") &
  IF 0 <> INDEX(D_UNITE-MILLE, "-") &
ELSE "La somme de : " + &
  PACK (D_MNTCNV-MILLION + D_CENTAINE-MILLE + D_DIZAINE-MILLE + D_UNITE-MILLE + &
        D_MNTCNV-CENT + TRUNC(D_MNTCNV-DIZAINE) + D_MNTCNV-UNITE + " Dollars   " + D_MNTCNV[8:2] + "/100") &
  IF 0 <> INDEX(D_MNTCNV-UNITE, "-") &
ELSE "La somme de : " + &
  PACK (D_MNTCNV-MILLION + D_CENTAINE-MILLE + D_DIZAINE-MILLE + D_UNITE-MILLE + &
        D_MNTCNV-CENT + D_MNTCNV-DIZAINE + D_MNTCNV-UNITE + " Dollars   " + D_MNTCNV[8:2] + "/100")


;************************************************************
;******* Transformation anglaise ****************************
;************************************************************


DEFINE D_NUM-LIST CHARACTER SIZE 200 = &
   "          " + &  ; 00
   "One       " + &  ; 01
   "Two       " + &  ; 02
   "Three     " + &  ; 03
   "Four      " + &  ; 04
   "Five      " + &  ; 05
   "Six       " + &  ; 06
   "Seven     " + &  ; 07
   "Eight     " + &  ; 08
   "Nine      " + &  ; 09
   "Ten       " + &  ; 10
   "Eleven    " + &  ; 11
   "Twelve    " + &  ; 12
   "Thirteen  " + &  ; 13
   "Fourteen  " + &  ; 14
   "Fifteen   " + &  ; 15
   "Sixteen   " + &  ; 16
   "Seventeen " + &  ; 17
   "Eighteen  " + &  ; 18
   "Nineteen  "      ; 19

DEFINE D_TENS-LIST CHARACTER SIZE 64 = &
   "Twenty  " + &  ; 20
   "Thirty  " + &  ; 30
   "Forty   " + &  ; 40
   "Fifty   " + &  ; 50
   "Sixty   " + &  ; 60
   "Seventy " + &  ; 70
   "Eighty  " + &  ; 80
   "Ninety  "      ; 90

; Calculate the millions digits

DEFINE D_M-HUNDRED CHARACTER SIZE 20 = &
     "" IF D_MNTCNV[1:1] = "0"  &
ELSE D_NUM-LIST[NCONVERT( D_MNTCNV[1:1] ) * 10 + 1 : 10] + "Hundred"

; If the next two digits are between "21" and "99"

DEFINE D_M-DASH CHARACTER SIZE 1 = &
     "-" IF D_MNTCNV[2:1] > "1"  AND &
            D_MNTCNV[3:1] <> "0" &
ELSE " "

DEFINE D_M-TENS CHARACTER SIZE 13 = &
     "" IF D_MNTCNV[2:2] = "00" &
ELSE D_NUM-LIST[NCONVERT( D_MNTCNV[2:2] ) * 10 + 1 : 10] &
     IF D_MNTCNV[2:2] >= "01" AND &
        D_MNTCNV[2:2] <= "19" &
ELSE TRUNCATE( D_TENS-LIST[NCONVERT( D_MNTCNV[2:1] ) * 8 -15 : 8]) + &
     D_M-DASH + &
     D_NUM-LIST[NCONVERT( D_MNTCNV[3:1] ) * 10 + 1 : 10]


DEFINE D_MILLIONS CHARACTER SIZE 45 = &
     "" IF D_MNTCNV[1:3] = "000" &
ELSE PACK( D_M-HUNDRED + " " + D_M-TENS + "Million")

DEFINE D_T-HUNDRED CHARACTER SIZE 20 = &
     "" IF D_MNTCNV[4:1] = "0" &
ELSE D_NUM-LIST[NCONVERT( D_MNTCNV[4:1] ) * 10 + 1 : 10] + "Hundred"

; If the next two digits are between "21" and "99"

DEFINE D_T-DASH CHARACTER SIZE 1 = &
     "-" IF D_MNTCNV[5:1] > "1"  AND &
            D_MNTCNV[6:1] <> "0" &
ELSE " "

DEFINE D_T-TENS CHARACTER SIZE 13 = &
     "" IF D_MNTCNV[5:2] = "00" &
ELSE D_NUM-LIST[NCONVERT( D_MNTCNV[5:2] ) * 10 + 1 : 10] &
     IF D_MNTCNV[5:2] >= "01" AND &
        D_MNTCNV[5:2] <= "19" &
ELSE TRUNCATE( D_TENS-LIST[NCONVERT( D_MNTCNV[5:1] ) * 8 -15 : 8]) + &
     D_T-DASH + &
     D_NUM-LIST[NCONVERT( D_MNTCNV[6:1] ) * 10 + 1 : 10]


DEFINE D_THOUSANDS CHARACTER SIZE 45 = &
     "" IF D_MNTCNV[4:3] = "000" &
ELSE PACK( D_T-HUNDRED + " " + D_T-TENS + "Thousand")

;Calculate hundreds digits
;Next do the same for numbers under 1000

DEFINE D_H-HUNDRED CHARACTER SIZE 20 = &
     "" IF D_MNTCNV[7:1] = "0" &
ELSE D_NUM-LIST[NCONVERT( D_MNTCNV[7:1] ) * 10 + 1 : 10] + "Hundred"


; If the next two digits are between "21" and "99"

DEFINE D_H-DASH CHARACTER SIZE 1 = &
     "-" IF D_MNTCNV[8:1] > "1"  AND &
            D_MNTCNV[9:1] <> "0" &
ELSE " "

DEFINE D_H-TENS CHARACTER SIZE 13 = &
     "" IF D_MNTCNV[8:2] = "00" &
ELSE D_NUM-LIST[NCONVERT( D_MNTCNV[8:2] ) * 10 + 1 : 10] &
     IF D_MNTCNV[8:2] >= "01" AND &
        D_MNTCNV[8:2] <= "19" &
ELSE TRUNCATE( D_TENS-LIST[NCONVERT( D_MNTCNV[8:1] ) * 8 -15 : 8]) + &
     D_H-DASH + &
     D_NUM-LIST[NCONVERT( D_MNTCNV[9:1] ) * 10 + 1 : 10]

DEFINE D_HUNDREDS CHARACTER SIZE 40 = &
     "" IF D_MNTCNV[7:3] = "000" &
ELSE PACK( D_H-HUNDRED + " " + D_H-TENS + " " )

DEFINE D_CENTS CHARACTER SIZE 12 = " and " + D_MNTCNV[10:2] + "/100"


DEFINE D_MNTFMTANG CHARACTER SIZE 180 = &
   PACK( " The sum of : " + &
         D_MILLIONS  + &
         D_THOUSANDS + &
         D_HUNDREDS  + &
         D_CENTS + &
         " Dollars " ) IF D-MNT-PARM > 99 &
  ELSE   " The sum of : Zero" + D_CENTS + " Dollards "

;************************************************************
;************************************************************
;************************************************************

DEFINE D_MNTFMT CHARACTER SIZE 180 = &
     D_MNTFMTFRA IF  D_MNTLNG = "F" &
ELSE D_MNTFMTANG
; Pour trouver ou se trouve un espace pour la séparation
DEFINE D_POSESP INTEGER SIZE 2 = &
                        90 IF 90 >= SIZE(TRUNC(D_MNTFMT)) OR &
                              " " = D_MNTFMT[90:1] &
                   ELSE 89 IF " " = D_MNTFMT[89:1] &
                   ELSE 88 IF " " = D_MNTFMT[88:1] &
                   ELSE 87 IF " " = D_MNTFMT[87:1] &
                   ELSE 86 IF " " = D_MNTFMT[86:1] &
                   ELSE 85 IF " " = D_MNTFMT[85:1] &
                   ELSE 84 IF " " = D_MNTFMT[84:1] &
                   ELSE 83 IF " " = D_MNTFMT[83:1] &
                   ELSE 82 IF " " = D_MNTFMT[82:1] &
                   ELSE 81 IF " " = D_MNTFMT[81:1] &
                   ELSE 80 IF " " = D_MNTFMT[80:1] &
                   ELSE 79 IF " " = D_MNTFMT[79:1] &
                   ELSE 78 IF " " = D_MNTFMT[78:1] &
                   ELSE 77 IF " " = D_MNTFMT[77:1] &
                   ELSE 76 IF " " = D_MNTFMT[76:1] &
                   ELSE 75 IF " " = D_MNTFMT[75:1] &
                   ELSE 74 IF " " = D_MNTFMT[74:1] &
                   ELSE 73 IF " " = D_MNTFMT[73:1] &
                   ELSE 72 IF " " = D_MNTFMT[72:1] &
                   ELSE 71 IF " " = D_MNTFMT[71:1] &
                   ELSE 70

; Séparation du montant en 2 chaine de 90 caractères
DEFINE D_MNTFMT1 CHARACTER SIZE 90 = D_MNTFMT[1:D_POSESP]
DEFINE D_MNTFMT2 CHARACTER SIZE 90 = D_MNTFMT[D_POSESP + 1:90]

