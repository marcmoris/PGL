; Formate un numéro de téléphone.
;
; Description: Cette procédure formate le numéro de téléphone selon
;              le standard nord americain.
;
; Utilisation: Il faut utiliser ce USE à l'intérieur d'une procédure
;              INPUT. Pourquoi on a choisi la procédure INPUT, c'est
;              pour que le mode SELECT utilise le format nord americain
;              par défaut, sinon l'usager est obligé de saisir champ
;              d'une façon exact incluant les parenthèses.
;
;
;
; Exemple:   Saisie         Résultat
;            6561144             656-1144
;            4186561144     (418)656-1144
;
;
IF 0 <> SIZE(FIELDTEXT)
THEN BEGIN
  ;
  ; Si l'usager saisie 7 chiffres, alors on ne met pas de code régional.
  ;
  IF MATCHPATTERN(FIELDTEXT,"#######")
  THEN BEGIN
    LET FIELDTEXT = "     " + FIELDTEXT[1:3] + "-" + FIELDTEXT[4:4]
  END
  ;
  ; Si l'usager saisie 10 chifres, alors on tient compte du
  ; code régional.
  ;
  IF MATCHPATTERN(FIELDTEXT,"##########")
  THEN BEGIN
    LET FIELDTEXT = "(" + FIELDTEXT[1:3] + ")" + FIELDTEXT[4:3] + &
                           "-" + FIELDTEXT[7:4]
  END
END
;
;
;**FIN USTELFMT
