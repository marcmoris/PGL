;/T/ Sommaire de la journalisation.
;
;/P/ Programmeur...: Micheline Bélanger
;    Date Création.: 17-Aout-1992
;
;    Description...: Ce source général sert a imprimer pour n'importe
;                    quel programme de journalisation le sommaire de
;                    journalisation.
;
;
;
;/A/ Adaptation par Nancy Marceau, 94/06/15
;       - Ajouter les descriptions pour le module Achats & Réceptions ainsi
;         que le module inventaire.
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
SET REPORT SPACING 1

ACCESS *WUS900HE &

@IF VAXVMS
  LINK TO RECORD 1 &
@ELSE
  LINK TO RECORD 0 &
@ENDIF
                   OF *WUS900ER OPTIONAL &

  LINK CPTCLE OF WUS900HE &
    TO CPTCLE OF VCPT_DSC &

  LINK CIECLE OF WUS900HE, &
       UNACLE OF WUS900HE  &
    TO CIECLE, &
       UNACLE OF MCUNITE_ADM

SELECT IF NOT RECORD WUS900ER EXISTS

;
; Titre de la liste.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
  "Journalisation - sommaire"
@ELSE
  "Posting summary"
@ENDIF
;
; Deuxième ligne du titre.
;
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "
;
; Nom de la compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM
;
; Code de la liste.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "US900ZB"

;
; Nom du module comptable.
;
DEFINE D_HISCODMOD CHAR SIZE 40 = &
@IF FRANCAIS
                                  "Comptes à payer" &
@ELSE
                                  "Accounts payable" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "CP" &
                             ELSE &
@IF FRANCAIS
                                  "Comptes à recevoir" &
@ELSE
                                  "Accounts receivable" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "CR" &
                             ELSE &

@IF FRANCAIS
                                  "Grand Livre" &
@ELSE
                                  "General Ledger" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "GL" &
                             ELSE &
@IF FRANCAIS
                                  "Achats & Réceptions" &
@ELSE
                                  "%%%Achats & Réceptions" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "AR" &
                             ELSE &
@IF FRANCAIS
                                  "Inventaire" &
@ELSE
                                  "%%%Inventaire" &
@ENDIF
                                    IF HISCODMOD OF WUS900HE = "IN"




 DEFINE D_HISTYPECR CHAR SIZE 40 = &
@IF FRANCAIS
                                   "Journal général" &
@ELSE
                                   "General journal" &
@ENDIF
                                     IF HISCODMOD OF WUS900HE = "GL" &
                              ELSE &
@IF FRANCAIS
                                   "Factures - notes de crédit" &
@ELSE
                                   "Invoices - credit note" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "FA" &
                              ELSE &
@IF FRANCAIS
                                   "Ajustements" &
@ELSE
                                   "Adjustements" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "AJ" &
                              ELSE &
@IF FRANCAIS
                                   "Relevés de dépense" &
@ELSE
                                   "Expense statements" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "RD" &
 ELSE &
@IF FRANCAIS
                                   "Encaissements" &
@ELSE
                                   "Payements receipt" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "EN" &
                              ELSE &
@IF FRANCAIS
                                   "Mauvaise créance" &
@ELSE
                                   "Bad debts" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "MC" &
                              ELSE &
@IF FRANCAIS
                                   "Paiements" &
@ELSE
                                   "Payments" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "CH" &
                              ELSE &
@IF FRANCAIS
                                   "Paiements annulés" &
@ELSE
                                   "Cancelled Payments" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "CA" &
                              ELSE &
@IF FRANCAIS
                                   "Réceptions" &
@ELSE
                                   "%%%Réceptions" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "RC" &
                              ELSE &
@IF FRANCAIS
                                   "Retours" &
@ELSE
                                   "Retours" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "RT" &
                              ELSE &
@IF FRANCAIS
                                   "Mouvements d'inventaire" &
@ELSE
                                   "Mouvements d'inventaire" &
@ENDIF
                                     IF HISTYPECR OF WUS900HE = "MV"






;
; Montant débit / crédit.
;
DEFINE D_MNTDBT FLOAT SIZE 8 = ABSOLUTE( T_MNTGL OF WUS900HE ) &
                                     IF    (     T_CODDC OF WUS900HE = "D" &
                                             AND T_MNTGL OF WUS900HE > 0 ) &
                                        OR (     T_CODDC OF WUS900HE = "C" &
                                             AND T_MNTGL OF WUS900HE < 0 )
DEFINE D_MNTCRT FLOAT SIZE 8 = ABSOLUTE( T_MNTGL OF WUS900HE ) &
                                     IF    (     T_CODDC OF WUS900HE = "C" &
                                             AND T_MNTGL OF WUS900HE > 0 ) &
                                        OR (     T_CODDC OF WUS900HE = "D" &
                                             AND T_MNTGL OF WUS900HE < 0 )

;ajout de define pour sort 4
DEFINE D_PECCLE_SIE CHAR SIZE 5 = "1" + PECCLE &
       IF          PECCLE[1:1] < "8"       &
       ELSE  "0" + PECCLE

SORT ON D_PECCLE_SIE &                  ; période
     ON HISNUMLOT &                     ; numéro de lot
     ON CPTCLE &                        ; compte comptable
     ON CIECLE &                        ; cie de la transaction
     ON UNACLE                          ; unité administrative



USE ustitstd NOLIST  ; Titre standard pour l'application
  SKIP 2 &
@IF FRANCAIS
    TAB 005 "Module..:" D_HISCODMOD &
    TAB 102 "Période.:" PECCLE &
SKIP 1 &
    TAB 005 "Type....:" D_HISTYPECR &
    TAB 102 "Lot.....:" HISNUMLOT &
@ELSE
    TAB 005 "Module..:" D_HISCODMOD &
    TAB 102 "Period..:" PECCLE &
SKIP 1 &
    TAB 005 "Type....:" D_HISTYPECR &
    TAB 102 "Batch...:" HISNUMLOT &
@ENDIF
SKIP 2 &
    TAB 001 &
@IF FRANCAIS
    "                      Compte                            Unité administrative               Débit          Crédit" &
@ELSE
    "                      Account                            Administrative unit               Debit          Credit" &
@ENDIF
SKIP 1 &
    TAB 001 &
    "    ------------------------------------------   ----------------------------------   --------------  --------------" &
SKIP 2


FOOT AT UNACLE &
  TAB 005 CPTCLE    OF WUS900HE &
  TAB 012 CPTDSC    OF VCPT_DSC &
  TAB 050 CIECLE    OF WUS900HE &
  TAB 055 UNACLE    OF WUS900HE &
  TAB 064 UNANOMABR OF MCUNITE_ADM &
  TAB 087 D_MNTDBT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ &
  TAB 103 D_MNTCRT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ

FOOT AT HISNUMLOT &
  TAB 087 "--------------" &
  TAB 103 "--------------" &
SKIP 1 &
  TAB 071 &
@IF FRANCAIS
          "Total du lot:" &
@ELSE
          "Batch total :" &
@ENDIF
  TAB 087 D_MNTDBT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ &
  TAB 103 D_MNTCRT SUBTOTAL &
                    PIC "^^^,^^^,^^^.^^" &
                    BWZ &
SKIP PAGE
; 
;
; 93-04-21 Alain Côté
;
;Titre standard...
;
;    Module..: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                               Période.: 99-99
;    Type....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                               Lot.....: xxxx
;
;                      Compte                            Unité administrative               Débit          Crédit
;    ------------------------------------------   ----------------------------------   --------------  --------------
;
;    123456 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   xxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx   999,999,999.99  999,999,999.99
;                                                                                      --------------  --------------
;                                                                      Total du lot:   999,999,999.99  999,999,999.99
