;/T/ Calcul l'écart entre deux dates
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-26
;
;    Description..: Cette procédure permet de calculer l'écart entre deux
;                   dates.
;
;    Parametre d'entré:
;
;    TZ_DATDEB  Date de début de l'intervalle (AAAAMMJJ)
;    TZ_TMPDEB  Heure de début de l'intervalle (HHMMSS)
;
;    TZ_DATFIN  Date de fin de l'intervalle (AAAAMMJJ)
;    TZ_TMPFIN  Heure de fin de l'intervalle (HHMMSS)
;
;    Parametre de sortie:
;
;    DZ_DIFSECTOT Nombre de seconde d'écart entre les deux intervalle
;    DZ_DIFJOU    Nombre de jour d'écart
;    DZ_DIFTIM    Nombre d'heure d'écart (HHMMSS) ex.: 6.45.00
;    DZ_DIFTIM_2  Nombre d'heure d'écart (HHCC)   ex.: 6.75
;
;-------------------------------------------------------------------------------
;
; Définition de la date et heure de début
;
TEMPORARY TZ_DATDEB   INTEGER   SIZE 4 ; AAAAMMJJ
TEMPORARY TZ_TMPDEB   CHARACTER SIZE 6 ; HHMMSS
;
; Définition de la date et heure de fin
;
TEMPORARY TZ_DATFIN   INTEGER   SIZE 4 ; AAAAMMJJ
TEMPORARY TZ_TMPFIN   CHARACTER SIZE 6 ; HHMMSS
;
; Donne le nombre de seconde depuis le 31 décembre 1899
;

DEFINE DZ_TMPSECDEB FLOAT     SIZE 8 = (DAYS(TZ_DATDEB) * 24 * 60 * 60) + &
                                       (NCONVERT(TZ_TMPDEB[1:2]) * (60 * 60)) + &
                                       (NCONVERT(TZ_TMPDEB[3:2]) * 60)        + &
                                        NCONVERT(TZ_TMPDEB[5:2])
;
; Donne le nombre de seconde depuis le 31 décembre 1899
;
DEFINE DZ_TMPSECFIN FLOAT     SIZE 8 = (DAYS(TZ_DATFIN) * 24 * 60 * 60) + &
                                       (NCONVERT(TZ_TMPFIN[1:2]) * (60 * 60)) + &
                                       (NCONVERT(TZ_TMPFIN[3:2]) * 60)        + &
                                        NCONVERT(TZ_TMPFIN[5:2])
;
; Différence entre les deux dates
;
DEFINE DZ_DIFSECTOT FLOAT SIZE 8 = DZ_TMPSECFIN - DZ_TMPSECDEB
;
; Différence de jour
;
DEFINE DZ_DIFJOU   FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT / (60 * 60 * 24))
;
;
;
DEFINE DZ_DIFHRE FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT - (DZ_DIFJOU * 60 * 60 * 24)) / (60 * 60 ))
;
;
;
DEFINE DZ_DIFMIN FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT - ( (DZ_DIFJOU * 60 * 60 * 24) + (DZ_DIFHRE * 60 * 60) ) ) / 60)
;
;
;
DEFINE DZ_DIFSEC FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT - ((DZ_DIFJOU * 60 * 60 * 24) + (DZ_DIFHRE * 60 * 60) + (DZ_DIFMIN * 60))))
;
;
;
DEFINE DZ_DIFTIM CHARACTER SIZE 6 = ASCII(DZ_DIFHRE,2) + ASCII(DZ_DIFMIN,2) + ASCII(DZ_DIFSEC,2)
;
;
;
DEFINE DZ_DIFTIM_2 CHARACTER SIZE 06 = ASCII(DZ_DIFHRE,2) + ASCII(DZ_DIFMIN / 60 , 2)
;-------------------------------------------------------------------------------
