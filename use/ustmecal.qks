; Calcul d'une date due pour un terme d'encaissement.
;
; Description: Traitement pour le calcul des dates due pour un terme
;              d'encaissement.
;
; Utilisation: Ce USEFILE doit être utilisé à l'intérieure d'une procédure
;              PROCESS ou INTERNAL.
;
;
;              Il faut utiliser le USEFILE "USTERTMP" pour déclarer les
;              variables nécessaire au calcul.
;
;              Le BUFFER de CRTERME doit être disponible, c-a-d qu'il faut
;              faire un GET avant d'appeller cette procédure.
;
;   exemple: SCREEN ...
;
;            FILE ...
;
;            USE ustertmp NOLIST
;
;            ; Section des procédures
;            USE ustercal NOLIST
;            ;
;            PROCEDURE EDIT toto
;            BEGIN
;              LET TZ_DATCAL = date du calcul
;              GET CRTERME ...
;              DO INTERNAL TER_DATE_DUE
;              LET date résultat = TZ_DATRES
;            END
;
;
PROCEDURE INTERNAL TER_DATE_DUE
BEGIN
  ;
  ; On ajoute le nombre de jours
  ;
  LET TZ_DATRES = DATE(DAYS(TZ_DATCAL) + TMENBRJRSAJT OF CRTERME)
  ;
  ; On ajoute le nombre de mois.
  ;
  IF TMENBRMOI OF CRTERME <> 0
  THEN BEGIN
    ;
    ; On conserve le jour du mois.
    ;
    LET TZ_JRS = MOD(TZ_DATRES,100)
    ;
    ; On ramène la date au premier du mois et on ajoute 30.5 jours
    ; par tranche de mois. Il faut ajouter un jour supplémentaire
    ; au calcul pour les mois de 31 jours qui se suivent.
    ; ( 30.5 = 366 jours / 12 ).
    ;
    LET TZ_DATRES = DATE( DAYS(FLOOR(TZ_DATRES  / 100) * 100 + 1) + &
                    ROUND(TMENBRMOI OF CRTERME * 30.5) + 1)
    ;
    ; On remet le jour du mois de la date initiale à la date calculée,
    ; tout en vérifiant que la date ne dépasse pas la fin mois.
    ;
    IF TZ_JRS <= MOD(LASTDAY(TZ_DATRES),100)
    THEN LET TZ_DATRES = FLOOR(TZ_DATRES / 100) * 100 + TZ_JRS
    ELSE LET TZ_DATRES = LASTDAY(TZ_DATRES)
  END
  ;
  ; On transforme la date en jours.
  ;
  IF TMETYPCAL OF CRTERME = "JR"
  THEN LET TZ_NBRJRS = DAYS(TZ_DATRES)
  ;
  ; Retourne le premier jour du mois suivant.
  ;
  IF TMETYPCAL OF CRTERME = "DM"
  THEN LET TZ_NBRJRS = DAYS( LASTDAY(TZ_DATRES) ) + 1
  ;
  ; Retourne le premier janvier de l'année suivante.
  ;
  IF TMETYPCAL OF CRTERME = "DA"
  THEN LET TZ_NBRJRS = DAYS(( FLOOR(TZ_DATRES/10000) + 1 ) * 10000 + 0101)
  ;
  ; On additionne le nombre de jour de délai et retourne le résultat sous
  ; la forme DATE.
  ;
  LET TZ_DATRES = DATE( TZ_NBRJRS + TMENBRJRSDEL OF CRTERME )
END

; **FIN USTMECAL
