;/T/ Facturation - Entête de la facture
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 3 mai 1995
;
;    Description..: Facturation - Entête de la facture
;
;/M/ François Déry, 30 avril 1999
;       Modifier l'accèss au fichier de séquence car il a
;       été converti en format interbase au lieu d'indexé.
;
;/M/ Marie-Josée Hamel, 96.02.09, enlever projet, activité, immobilisation et
;                                 ajouter lieu de travail et connaissements
;
;----------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN fa004e ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              AUTORETURN &
              FOR 15 LINES &
              ON LINE 9  &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        FACPT_A_REC, &
                        MCREF_ADRESSE

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "FA004E"            ; Nom de l'écran

USE fa004e.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE FAFAR_SEQ IN DICT
                                                ; Le IN DICT est pour unix
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_RAD_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE MCRAD_SEQ IN DICT
                                                ; Le IN DICT est pour unix

;-----------------
; Facture générale
;-----------------
FILE FACPT_A_REC      MASTER
FILE MCREF_ADRESSE    MASTER


;---------------------
; Entête de la facture
;---------------------
FILE FAFAR_ENTETE           PRIMARY &
                            NOITEMS

  ACCESS   VIA     CIECLE, &
                   FARCLE  &
           USING   CIECLE OF FACPT_A_REC, &
                   FARCLE OF FACPT_A_REC

  ITEM CIECLE    OF FAFAR_ENTETE    INITIAL CIECLE     OF FACPT_A_REC
  ITEM FARCLE    OF FAFAR_ENTETE    INITIAL FARCLE     OF FACPT_A_REC

;-------
; client
;-------
FILE VCLC_CLI         REFERENCE

  ACCESS VIA CLICIECLE, &
             CLICLE   , &
             CIECLE     &
         USING REFCIECLE OF FACPT_A_REC, &
               REFCLENUM OF FACPT_A_REC, &
               CIECLE    OF FACPT_A_REC


;---------------------
; Validation des ports
;---------------------
TEMPORARY T_PORORI  CHARACTER SIZE 4
TEMPORARY T_PORORI2 CHARACTER SIZE 4

FILE FAPORT                 REFERENCE &
                            ALIAS FAPORT_PORORI

  ACCESS  VIA   FPOCLE  &
          USING T_PORORI


;---------------------
; Validation des ports
;---------------------
TEMPORARY T_PORDES  CHARACTER SIZE 4
TEMPORARY T_PORDES2 CHARACTER SIZE 4

FILE FAPORT                 REFERENCE &
                            ALIAS FAPORT_PORDES

  ACCESS  VIA   FPOCLE  &
          USING T_PORDES


;---------------------
; Validation des ports
;---------------------
TEMPORARY T_LIUTRV  CHARACTER SIZE 4
TEMPORARY T_LIUTRV2 CHARACTER SIZE 4

FILE FAPORT                 REFERENCE &
                            ALIAS FAPORT_LIUTRV

  ACCESS  VIA   FPOCLE  &
          USING T_LIUTRV


;--------------------------------------------------
; Vérification si il ya déja une entete de facture
;--------------------------------------------------
FILE FAFAR_ENTETE      DESIGNER &
                       ALIAS FAFAR_ENTETE2



;------------------------------------------------------------------------------
;
; Numéro de séquence des adresses pour les clients divers.
;
FILE MCRAD_SEQ        DESIGNER &
                      TRANSACTION GEN_RAD_SEQ

FILE FAFAR_SEQ        DESIGNER &
                      TRANSACTION GEN_NUM_SEQ


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


SKIP
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW FROM  2, 1  TO  2 ,79
DRAW FROM  2, 1  TO  15,1
DRAW FROM  3, 80 TO  15,80
DRAW FROM  15,1  TO  15,80

SKIP 1

TITLE &
@IF FRANCAIS
      " Entête de la facture " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN (,3,6) (,50,66)

FIELD RADNOM OF MCREF_ADRESSE &
      LABEL "À:" DISPLAY

FIELD RADFAX  OF MCREF_ADRESSE &
      DISPLAY

ALIGN (,,6)
FIELD RADADR1 OF MCREF_ADRESSE &
      DISPLAY

FIELD RADADR2 OF MCREF_ADRESSE &
      DISPLAY

ALIGN (,,6) (,50,66)
FIELD RADADR3 OF MCREF_ADRESSE &
      DISPLAY

FIELD FARDAT OF FACPT_A_REC  FORMAT YYYYMMDD PATTERN "####/##/##"&
      DISPLAY

FIELD RADCP   OF MCREF_ADRESSE &
      DISPLAY


SKIP 1

ALIGN (3,3,24) (,,29)
FIELD T_PORORI                         &
      HIDDEN ID 25                     &
      LABEL "Port de chargement.:"     &
      LOOKUP ON FAPORT_PORORI          &
            VIA FPOCLE                 &
          USING T_PORORI               &
        MESSAGE 2714; Ce port est inexistant

FIELD FFEPORORI OF FAFAR_ENTETE

FIELD T_PORDES                         &
      HIDDEN ID 30                     &
      LABEL "Port de destination:"     &
      LOOKUP ON FAPORT_PORDES          &
            VIA FPOCLE                 &
          USING T_PORDES               &
        MESSAGE 2714; Ce port est inexistant

FIELD FFEPORDES OF FAFAR_ENTETE

FIELD T_LIUTRV                         &
      HIDDEN ID 32                     &
      LABEL "Lieu de travail....:"     &
      LOOKUP ON FAPORT_LIUTRV          &
            VIA FPOCLE                 &
          USING T_LIUTRV               &
        MESSAGE 2714; Ce port est inexistant

FIELD FFELIUTRV OF FAFAR_ENTETE

ALIGN (3,3,24)
FIELD FFEDES    OF FAFAR_ENTETE        &
      HIDDEN ID 35                     &
      LABEL "Destinataire.......:"

ALIGN (3,3,24) (,,32) (,,40) (,,48) (,,56) (,,64) (,,72)
FIELD FFECNS1 OF FAFAR_ENTETE &
      HIDDEN ID 40            &
      LABEL "Connaissements.....:"
FIELD FFECNS2 OF FAFAR_ENTETE
FIELD FFECNS3 OF FAFAR_ENTETE
FIELD FFECNS4 OF FAFAR_ENTETE
FIELD FFECNS5 OF FAFAR_ENTETE
FIELD FFECNS6 OF FAFAR_ENTETE
FIELD FFECNS7 OF FAFAR_ENTETE

;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue port d'origine
;
PROCEDURE INPUT T_PORORI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PORORI2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN fa104 MODE F PASSING T_PORORI2 &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PORORI2)
  END
END


PROCEDURE PROCESS T_PORORI
BEGIN
   GET FAPORT_PORORI &
       VIA   FPOCLE  &
       USING T_PORORI OPTIONAL
   IF ACCESSOK
   THEN BEGIN
        IF FFEPORORI OF FAFAR_ENTETE <> " "
        THEN BEGIN
             IF CLILNG OF VCLC_CLI =  "F"
             THEN LET FFEPORORI OF FAFAR_ENTETE = &
                                 TRUNCATE(FFEPORORI OF FAFAR_ENTETE) + ", " + &
                                 TRUNCATE(FPODSCFRA OF FAPORT_PORORI)

             ELSE LET FFEPORORI OF FAFAR_ENTETE = &
                                 TRUNCATE(FFEPORORI OF FAFAR_ENTETE) + ", " + &
                                 TRUNCATE(FPODSCANG OF FAPORT_PORORI)
        END
        ELSE BEGIN
             IF CLILNG OF VCLC_CLI =  "F"
             THEN LET FFEPORORI OF FAFAR_ENTETE = &
                                 TRUNCATE(FPODSCFRA OF FAPORT_PORORI)

             ELSE LET FFEPORORI OF FAFAR_ENTETE = &
                                 TRUNCATE(FPODSCANG OF FAPORT_PORORI)
        END
   END
   DISPLAY FFEPORORI OF FAFAR_ENTETE
   LET T_PORORI = " "
   ACCEPT T_PORORI
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue port de destination
;
PROCEDURE INPUT T_PORDES
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PORDES2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN fa104 MODE F PASSING T_PORDES2 &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PORDES2)
  END
END



PROCEDURE PROCESS T_PORDES
BEGIN
   GET FAPORT_PORDES &
       VIA   FPOCLE  &
       USING T_PORDES OPTIONAL
   IF ACCESSOK
   THEN BEGIN
        IF FFEPORDES OF FAFAR_ENTETE <> " "
        THEN BEGIN
             IF CLILNG OF VCLC_CLI =  "F"
             THEN LET FFEPORDES OF FAFAR_ENTETE = &
                                 TRUNCATE(FFEPORDES OF FAFAR_ENTETE) + ", " + &
                                 TRUNCATE(FPODSCFRA OF FAPORT_PORDES)

             ELSE LET FFEPORDES OF FAFAR_ENTETE = &
                                 TRUNCATE(FFEPORDES OF FAFAR_ENTETE) + ", " + &
                                 TRUNCATE(FPODSCANG OF FAPORT_PORDES)
        END
        ELSE BEGIN
             IF CLILNG OF VCLC_CLI =  "F"
             THEN LET FFEPORDES OF FAFAR_ENTETE = &
                                 TRUNCATE(FPODSCFRA OF FAPORT_PORDES)

             ELSE LET FFEPORDES OF FAFAR_ENTETE = &
                                 TRUNCATE(FPODSCANG OF FAPORT_PORDES)
        END
   END
   DISPLAY FFEPORDES OF FAFAR_ENTETE
   LET T_PORDES = " "
   ACCEPT T_PORDES
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue lieu de travail
;
PROCEDURE INPUT T_LIUTRV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LIUTRV2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN fa104 MODE F PASSING T_LIUTRV2 &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_LIUTRV2)
  END
END



PROCEDURE PROCESS T_LIUTRV
BEGIN
   GET FAPORT_LIUTRV &
       VIA   FPOCLE  &
       USING T_LIUTRV OPTIONAL
   IF ACCESSOK
   THEN BEGIN
        IF FFELIUTRV OF FAFAR_ENTETE <> " "
        THEN BEGIN
             IF CLILNG OF VCLC_CLI =  "F"
             THEN LET FFELIUTRV OF FAFAR_ENTETE = &
                                 TRUNCATE(FFELIUTRV OF FAFAR_ENTETE) + ", " + &
                                 TRUNCATE(FPODSCFRA OF FAPORT_LIUTRV)

             ELSE LET FFELIUTRV OF FAFAR_ENTETE = &
                                 TRUNCATE(FFELIUTRV OF FAFAR_ENTETE) + ", " + &
                                 TRUNCATE(FPODSCANG OF FAPORT_LIUTRV)
        END
        ELSE BEGIN
             IF CLILNG OF VCLC_CLI =  "F"
             THEN LET FFELIUTRV OF FAFAR_ENTETE = &
                                 TRUNCATE(FPODSCFRA OF FAPORT_LIUTRV)

             ELSE LET FFELIUTRV OF FAFAR_ENTETE = &
                                 TRUNCATE(FPODSCANG OF FAPORT_LIUTRV)
        END
   END
   DISPLAY FFELIUTRV OF FAFAR_ENTETE
   LET T_LIUTRV = " "
   ACCEPT T_LIUTRV
END


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST

  GET FAFAR_ENTETE2 &
      VIA   CIECLE, &
            FARCLE  &
      USING CIECLE OF FACPT_A_REC, &
            FARCLE OF FACPT_A_REC OPTIONAL
  IF ACCESSOK
  THEN ERROR 2741 ; Cette facture à déjà une entête de définie.
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF FAFAR_ENTETE        &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF FAFAR_ENTETE        &
     AND NOT NEWRECORD     OF FAFAR_ENTETE        &
     AND NOT DELETEDRECORD OF FAFAR_ENTETE        &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  IF  FARSTU  OF FACPT_A_REC <> "P"    AND &
     (ALTEREDRECORD OF FAFAR_ENTETE     OR &
      NEWRECORD     OF FAFAR_ENTETE     OR &
      DELETEDRECORD OF FAFAR_ENTETE)
  THEN ERROR 2739

  IF FARCLE OF FACPT_A_REC = " "
  THEN BEGIN
    START TRANSACTION GEN_NUM_SEQ
    GET FAFAR_SEQ VIA   CIECLE, &
                        FSEANNCLE  &
                  USING T_CIECLEMNU,&
                        T_PECCLEMNU[1:2] &
                  OPTIONAL
    LET CIECLE    OF FAFAR_SEQ = T_CIECLEMNU
    LET FSEANNCLE OF FAFAR_SEQ = T_PECCLEMNU[1:2]
    ;LET FSENUMSEQ OF FAFAR_SEQ = FSENUMSEQ OF FAFAR_SEQ + 1
;an 2000
    IF NEWRECORD OF FAFAR_SEQ
    THEN LET FSENUMSEQ OF FAFAR_SEQ = 0
    LET FSENUMSEQ OF FAFAR_SEQ = FSENUMSEQ OF FAFAR_SEQ + 1

    LET FARCLE OF FACPT_A_REC = FSEANNCLE + &
           ASCII(FSENUMSEQ OF FAFAR_SEQ, FSELONNUM OF FAFAR_SEQ -2)
    PUT FAFAR_SEQ
    COMMIT TRANSACTION GEN_NUM_SEQ
  END
  ;
  ; On incrémente le numéro d'adresse pour les clients divers.
  ;
  IF     CLITYP OF VCLC_CLI = "D" &
     AND RADCLE OF FACPT_A_REC = 0
  THEN BEGIN
    START TRANSACTION GEN_RAD_SEQ
    GET MCRAD_SEQ VIA REFCIECLE, &
                      REFCLETYP, &
                      REFCLENUM  &
                  USING REFCIECLE OF FACPT_A_REC, &
                        REFCLETYP OF FACPT_A_REC, &
                        REFCLENUM OF FACPT_A_REC  &
                  OPTIONAL
    LET REFCIECLE OF MCRAD_SEQ = REFCIECLE OF FACPT_A_REC
    LET REFCLETYP OF MCRAD_SEQ = REFCLETYP OF FACPT_A_REC
    LET REFCLENUM OF MCRAD_SEQ = REFCLENUM OF FACPT_A_REC
    LET RASNUMSEQ OF MCRAD_SEQ = RASNUMSEQ OF MCRAD_SEQ + 1
    LET RADCLE    OF FACPT_A_REC = RASNUMSEQ OF MCRAD_SEQ
    PUT MCRAD_SEQ
    COMMIT TRANSACTION GEN_RAD_SEQ
  END

  IF FARCLE OF FAFAR_ENTETE = " "
  THEN LET FARCLE OF FAFAR_ENTETE = FARCLE OF FACPT_A_REC
END


BUILD
@IF FORMAT
***************************** Entête de la facture ****************************x
*                                                                              *
* À: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    Fax...........: xxxxxxxxxxxxx *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                  *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                  *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    Date facture..: xxxxxxxx      *
*    xxxxxxx                                                                   *
*                                                                              *
* Port de chargement.: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
* Port de destination: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
* Lieu de travail....: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
* Destinataire.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Connaissements.....: xxxxxxx xxxxxxx xxxxxxx xxxxxxx xxxxxxx xxxxxxx xxxxxxx *
********************************************************************************
@ENDIF
