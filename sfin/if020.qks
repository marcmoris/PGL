;/T/ Gestion de lots d'expédition
;
;/P/ Programmeur..: Francois Richard
;    Date Creation: 21 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les lots d'expédition
;
;
SCREEN if020 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF020"

;USE if020.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFLOT    IN DICT PRIMARY NOITEM

  ACCESS VIA     CIECLE,      & 
                 IFLCLE       &
         USING   T_CIECLEMNU, &
                 IFLCLE       &
         REQUEST IFLCLE       &
         ORDERBY IFLCLE

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY IFLCLE

  ITEM CIECLE OF IFLOT INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Codes bilingues du statut du lot
;
DEFINE    D_IFLSTU CHARACTER SIZE 16 = "IFLSTU"
TEMPORARY T_IFLSTU CHARACTER SIZE 4

FILE VCBI_DSC          REFERENCE ALIAS A_CBI_IFLSTU

  ACCESS  VIA   XELNOM,   &
                CBICLE    &
          USING D_IFLSTU, &
                IFLSTU OF IFLOT

;-------------------------------------------------------------------------------
;
; Type de lot
;
FILE IFTYPE_LOT REFERENCE

   ACCESS VIA   CIECLE,          &
                TYLCLE           &
          USING CIECLE OF IFLOT, &
                TYLCLE OF IFLOT 

;------------------------------------------------------------------------------
;
; Lieu de chargement
;
FILE IFLIEU_CHARG REFERENCE

  ACCESS VIA   CIECLE,          &
               LICCLE           &
         USING CIECLE OF IFLOT, &
               LICCLE OF IFLOT

;------------------------------------------------------------------------------
;
; Lieu de destination
;
FILE IFLIEU_DEST REFERENCE

  ACCESS VIA   CIECLE, &
               LIDCLE  &
         USING CIECLE OF IFLOT, &
               LIDCLE OF IFLOT
         
;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

; Temporaire servant a l'appel des depisteurs
TEMPORARY T_CIECLE CHARACTER SIZE 4
TEMPORARY T_TYLCLE CHARACTER SIZE 3
TEMPORARY T_LIDCLE CHARACTER SIZE 3
TEMPORARY T_LICCLE CHARACTER SIZE 3
TEMPORARY T_FLGDEL CHARACTER SIZE 1

;-------------------------------------------------------------------------------
;
USE usdrwecr    NOLIST
USE ustitstd    NOLIST
USE ushilite    NOLIST
SKIP

TITLE &
      " Gestion des lots d'expéditions " &
      CENTERED AT ,1

SKIP 1

ALIGN (2,3,25)(,51,67)(,,69)
FIELD IFLCLE OF IFLOT               &
      LABEL "Lot.................:" &   
      HIDDEN                        &
      REQUIRED                      &
      NOCHANGE                      &
      NULL VALUE NOT ALLOWED        &
      LOOKUP NOTON IFLOT            &
          VIA   CIECLE,             &
                IFLCLE              &
          USING CIECLE OF IFLOT,    &
                IFLCLE OF IFLOT     &
          MESSAGE 3252 ; PD352 Ce lot existe déjà

FIELD IFLSTU OF IFLOT              &
      REQUIRED                     &
      NULL VALUE NOT ALLOWED       &
      HIDDEN                       &
      LOOKUP ON A_CBI_IFLSTU       &
        VIA   XELNOM,              &
              CBICLE               &
        USING D_IFLSTU,            &
              IFLSTU OF IFLOT      &
        MESSAGE 3251 ; PD351 Ce statut de lot est inexistant

FIELD CBIDSC OF A_CBI_IFLSTU &
      SIZE 10                &
      DISPLAY

SKIP 1
ALIGN (2,3,25)(,,29)
FIELD TYLCLE OF IFLOT               &
      LABEL "Type................:" &
      HIDDEN                        & 
      NULL VALUE NOT ALLOWED        &
      LOOKUP ON IFTYPE_LOT          &
         VIA   CIECLE,              &
               TYLCLE               &
         USING CIECLE OF IFLOT,     &
               TYLCLE OF IFLOT      &
         MESSAGE 3253 ; PD353 Ce type de lot est inexistant

FIELD TYLDSCFRA OF IFTYPE_LOT &
      SIZE 20                 &   
      DISPLAY

SKIP 1
ALIGN(2,3,25)
FIELD IFLDSC OF IFLOT               &
      LABEL "Description.........:" &    
      HIDDEN                        & 
      NULL VALUE NOT ALLOWED

SKIP 1
FIELD IFLDATCHR OF IFLOT            &
      LABEL "Date de chargement..:" &
      FORMAT YYYYMMDD               & 
      HIDDEN                        &
      NULL VALUE NOT ALLOWED

SKIP 1
FIELD IFLDATDEP OF IFLOT                  &
      LABEL "Date départ prévue..:" &
      FORMAT YYYYMMDD                     &
      HIDDEN                              &
      NULL VALUE NOT ALLOWED

SKIP 1
FIELD IFLDATFIN OF IFLOT                  &
      LABEL "Date arrivée prévue.:" &
      FORMAT YYYYMMDD                     & 
      HIDDEN                              &  
      NULL VALUE NOT ALLOWED

SKIP 1
ALIGN(2,3,25) (,,29)
FIELD LICCLE OF IFLOT               &
      LABEL "Lieu chargement.....:" &
      HIDDEN                        & 
      NULL VALUE NOT ALLOWED        &
      LOOKUP ON IFLIEU_CHARG        &
        VIA   CIECLE,               &
              LICCLE                &
        USING CIECLE OF IFLOT,      &
              LICCLE OF IFLOT       &
        MESSAGE 3254 ; PD354 Ce lieu de chargement est inexistant  

FIELD LICDSCFRA OF IFLIEU_CHARG &
      DISPLAY

SKIP 1
FIELD LIDCLE OF IFLOT               &
      LABEL "Lieu destination....:" &
      HIDDEN                        &
      NULL VALUE NOT ALLOWED        &
      LOOKUP ON IFLIEU_DEST         &
        VIA   CIECLE,               &
              LIDCLE                &
        USING CIECLE OF IFLOT,      &  
              LIDCLE OF IFLOT       &
        MESSAGE 3255 ; PD355 Ce lieu de destination est inexistant               

FIELD LIDDSCFRA OF IFLIEU_DEST &
      DISPLAY
;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Pop-up sur le type de lot
;
PROCEDURE INPUT TYLCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLEMNU
    LET T_TYLCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@" 
    RUN SCREEN if101 PASSING  T_CIECLE, &
                              T_TYLCLE  &
                     MODE F
    LET FIELDTEXT = TRUNCATE(T_TYLCLE)
  END
END

;-------------------------------------------------------------------------------
;
; Pop-up sur le lieu de chargement 
;
PROCEDURE INPUT LICCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLEMNU
    LET T_LICCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN if102 PASSING  T_CIECLE, &
                              T_LICCLE  &
                     MODE F
    LET FIELDTEXT = TRUNCATE(T_LICCLE)
  END
END

;-------------------------------------------------------------------------------
;
; Pop-up sur le lieu de destination
;
PROCEDURE INPUT LIDCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLEMNU
    LET T_LIDCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN if103 PASSING  T_CIECLE, &
                              T_LIDCLE  &
                     MODE F
    LET FIELDTEXT = TRUNCATE(T_LIDCLE)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue sur le statut du lot
;
;
PROCEDURE INPUT IFLSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IFLSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_IFLSTU, T_IFLSTU &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_IFLSTU)
  END
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF IFLOT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF IFLOT &
     AND NOT NEWRECORD     OF IFLOT &
     AND NOT DELETEDRECORD OF IFLOT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END


BUILD
