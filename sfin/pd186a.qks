;/T/ 2.4.4. Enregistrer le débitage - OPÉRATEURS
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-11-30
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le débitage des pièces de granit.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd186a ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM,          &
                        PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_DEBITAGE IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD186A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd186a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04
DEFINE    D_DEBSTU    CHARACTER SIZE 16 = "DEBSTU"
TEMPORARY T_DEBSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variables servant à l'appel des dépisteurs
;

TEMPORARY T_EMPLOYE_NUM   CHARACTER SIZE 06


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calculd'un intervalle de temps
;
USE usdiftim NOLIST


;-------------------------------------------------------------------------------
;
; Débitage
;

FILE PDDEBITAGE       MASTER


;-------------------------------------------------------------------------------
;
; Opérateurs débitage
;

FILE PDOPER_DEBITAGE PRIMARY &
                     NOITEM &
                     OCCURS 7 TIMES

  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP        &
         USING   T_CIECLEMNU,                    &
                 DEBNUMRAP        OF PDDEBITAGE  &
         ORDERBY CIECLE,          &
                 DEBNUMRAP,       &
                 DEBDATPRD,       &
                 OPENUMEMP

   ITEM CIECLE    OF PDOPER_DEBITAGE INITIAL T_CIECLEMNU
   ITEM DEBNUMRAP OF PDOPER_DEBITAGE INITIAL DEBNUMRAP OF PDDEBITAGE FIXED
   ITEM DEBDATPRD OF PDOPER_DEBITAGE INITIAL DEBDATPRD OF PDDEBITAGE FIXED
   ITEM OPDDURSEC OF PDOPER_DEBITAGE SUM INTO DEBDURSECOPE OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour le débitage
;

FILE PDSEQ_DEBITAGE  DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Opérateur de débitage validation des plages horraires pour le rapport
;

FILE PDOPER_DEBITAGE DESIGNER ALIAS OPD_VALIDE

  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP,       &
                 DEBDATPRD,       &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,                    &
                 DEBNUMRAP        OF PDDEBITAGE, &
                 DEBDATPRD        OF PDDEBITAGE, &
                 OPENUMEMP        OF PDOPER_DEBITAGE



;-------------------------------------------------------------------------------
;
; Machine débitage
;

FILE PDMACH_DEBITAGE REFERENCE

  ACCESS VIA     CIECLE,          &
                 MDECODMAC        &
         USING   T_CIECLEMNU,     &
                 MDECODMAC        OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_DEBSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_DEBSTU,                             &
               DEBSTU           OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Opérateurs
;

FILE PDOPERATEUR REFERENCE

  ACCESS VIA     CIECLE,          &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,     &
                 OPENUMEMP        OF PDOPER_DEBITAGE


;-------------------------------------------------------------------------------
;
; Opérateurs (VALIDATION)
;

FILE PDOPERATEUR DESIGNER ALIAS OPE_VALID


;-------------------------------------------------------------------------------
;
; Variable necessaire au traitement
;

DEFINE  D_NOM_EMP CHARACTER SIZE 40 = TRUNCATE(OPEPNM OF PDOPERATEUR) + &
                                      " " + &
                                      TRUNCATE(OPENOM OF PDOPERATEUR)


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Débitage " &
@ELSE
      " %%%Débitage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD DEBNUMRAP        OF PDDEBITAGE       &
label "Numéro rapport:"&
        DISPLAY                            &
        FIXED


FIELD DEBDATPRD        OF PDDEBITAGE        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,24)


FIELD MDECODMAC        OF PDDEBITAGE       &
label "Code machine..:"&
        DISPLAY                            &
        FIXED


FIELD MDEDSC001        OF PDMACH_DEBITAGE  &
        DISPLAY                            &
        FIXED


SKIP TO LINE 5


ALIGN (,42,58)


FIELD DEBDEP           OF PDDEBITAGE       &
label "Hre départ....:"&
        DISPLAY                            &
        FIXED


FIELD DEBARR           OF PDDEBITAGE       &
label "Hre fin.......:"&
        DISPLAY                            &
        FIXED


SKIP TO LINE 8


ALIGN (,3,19) (,,24)


FIELD QRTCODQRT        OF PDDEBITAGE       &
label "Quart.........:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY                            &
        FIXED                              &
        SIZE 18


ALIGN (,3,19) (,,24)


FIELD DEBSTU           OF PDDEBITAGE       &
label "Statut.......:"&
        display &
        FIXED


FIELD CBIDSC           OF A_CBI_DEBSTU     &
        DISPLAY                            &
        FIXED                              &
        SIZE 18


DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Opérateurs débitage " &
@ELSE
      " %%%Opérateurs débitage " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
    "    Numéro   Nom                                       Heure   Heure " &
@ELSE
    "%%%    Numéro   Nom                                       Heure   Heure " &
@ENDIF
      AT ,2


SKIP


TITLE &
@IF FRANCAIS
    "    empl.    Employé                                   début   fin   " &
@ELSE
    "%%%    empl.    Employé                                   début   fin   " &
@ENDIF
      AT ,2


ALIGN (5,5,6) (,,15) (,,57) (,,65)


CLUSTER OCCURS WITH PDOPER_DEBITAGE ID BASE 35



FIELD OPENUMEMP        OF PDOPER_DEBITAGE  &
        HIDDEN                             &
        NUMERIC                            &
        LABEL " "



FIELD D_NOM_EMP                            &
        HIDDEN                             &
        DISPLAY


FIELD OPDHREDEB        OF PDOPER_DEBITAGE  &
        HIDDEN


FIELD OPDHREFIN        OF PDOPER_DEBITAGE  &
        HIDDEN


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ.
;

PROCEDURE INPUT OPENUMEMP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EMPLOYE_NUM   = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd110 MODE F &
                     PASSING T_EMPLOYE_NUM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_EMPLOYE_NUM  )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'opérateur existe
;

PROCEDURE EDIT OPENUMEMP
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    GET OPE_VALID OPTIONAL     &
      VIA     CIECLE,          &
              OPENUMEMP        &
      USING   T_CIECLEMNU,     &
              OPENUMEMP        OF PDOPER_DEBITAGE
    IF NOT ACCESSOK
    THEN BEGIN
      WARNING 3057 ; Le numéro d'opérateur n'existe pas
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'heure saisie est compris dans l'intervalle
;

PROCEDURE EDIT OPDHREDEB
BEGIN
  IF OPDHREDEB OF PDOPER_DEBITAGE < DEBDEP OF PDDEBITAGE &
     OR &
     OPDHREDEB OF PDOPER_DEBITAGE > DEBARR OF PDDEBITAGE
  THEN BEGIN
    WARNING 3010 ; L'heure de début n'est pas compris dans l'intervalle du rapport
   END
END


;-------------------------------------------------------------------------------
;
; Valide que l'heure saisie est compris dans l'intervalle
;

PROCEDURE EDIT OPDHREFIN
BEGIN
  IF OPDHREFIN OF PDOPER_DEBITAGE > DEBARR OF PDDEBITAGE &
     OR &
     OPDHREFIN OF PDOPER_DEBITAGE < DEBDEP OF PDDEBITAGE
  THEN BEGIN
    WARNING 3011 ; L'heure de fin n'est pas compris dans l'intervalle du rapport
   END
END

PROCEDURE INTERNAL VALIDE
BEGIN
  WHILE RETRIEVING OPD_VALIDE
  BEGIN
;    IF (OPDHREDEB OF PDOPER_DEBITAGE <> OPDHREDEB OF OPD_VALIDE   &
;        AND                                                       &
;        OPDHREFIN OF PDOPER_DEBITAGE <> OPDHREFIN OF OPD_VALIDE )
;    THEN BEGIN
      IF ( (OPDHREDEB OF PDOPER_DEBITAGE >= OPDHREDEB OF OPD_VALIDE     &
            AND                                                         &
            OPDHREDEB OF PDOPER_DEBITAGE <= OPDHREFIN OF OPD_VALIDE )   &
          OR                                                            &
           (OPDHREFIN OF PDOPER_DEBITAGE >= OPDHREDEB OF OPD_VALIDE     &
            AND                                                         &
            OPDHREFIN OF PDOPER_DEBITAGE <= OPDHREFIN OF OPD_VALIDE ))  &
         OR                                                             &
           (OPDHREDEB OF PDOPER_DEBITAGE <= OPDHREDEB OF OPD_VALIDE     &
            AND                                                         &
            OPDHREFIN OF PDOPER_DEBITAGE >= OPDHREFIN OF OPD_VALIDE )
      THEN BEGIN
        ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
       END
    END
 ; END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ;
  ;
  FOR PDOPER_DEBITAGE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDOPER_DEBITAGE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDOPER_DEBITAGE &
       AND NOT NEWRECORD     OF PDOPER_DEBITAGE &
       AND NOT DELETEDRECORD OF PDOPER_DEBITAGE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

;    DO INTERNAL VALIDE

    ;
    ; Calcul de la durée du traitement pour l'opération
    ;

    LET TZ_DATDEB   = DEBDATPRD        OF PDDEBITAGE
    LET TZ_TMPDEB   = ASCII(OPDHREDEB  OF PDOPER_DEBITAGE,4) + "00"
    LET TZ_DATFIN   = DEBDATPRD        OF PDDEBITAGE
    LET TZ_TMPFIN   = ASCII(OPDHREFIN  OF PDOPER_DEBITAGE,4) + "00"

    LET OPDDURSEC OF PDOPER_DEBITAGE = DZ_DIFSECTOT


    IF ALTEREDRECORD OF PDOPER_DEBITAGE
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF DEBSTU OF PDDEBITAGE = "V"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "E" ; En cours , il faut reprendre la validation
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF DEBSTU OF PDDEBITAGE = "T"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "C"  ; Correction après transfert
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END

    IF NEWRECORD OF PDOPER_DEBITAGE
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF DEBSTU OF PDDEBITAGE = "V"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "E" ; En cours , il faut reprendre la validation
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF DEBSTU OF PDDEBITAGE = "T"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "C"  ; Correction après transfert
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END

  END
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = DEBNUMRAP OF PDDEBITAGE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_DEBITAGE OPTIONAL         &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_DEBITAGE = T_CIECLEMNU
    END
    LET SEQNUMSEQ OF PDSEQ_DEBITAGE = SEQNUMSEQ OF PDSEQ_DEBITAGE + 1
    LET DEBNUMRAP OF PDDEBITAGE     = SEQNUMSEQ OF PDSEQ_DEBITAGE
    PUT PDSEQ_DEBITAGE
    DISPLAY DEBNUMRAP OF PDDEBITAGE
  END
END

PROCEDURE UPDATE
BEGIN
  PUT PDDEBITAGE
  FOR PDOPER_DEBITAGE
  BEGIN
  ;
  ; Validation des plages horraire pour les opérateurs
  ;
   PUT PDOPER_DEBITAGE
  END
END


BUILD


@IF FORMAT

2x                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    x
3--------------------------------- Débitage -----------------------------------+
4                                                                              |
5 Numéro rapport: 999999999              Hre départ....: HH:MM                 |
6 Date product..: AA/MM/JJ               Hre fin.......: HH:MM                 |
7 Code machine..: XXX  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                |
8 Quart.........: X    xxxxxxx                                                 |
9                                                                              |
0---------------------------- Opérateur Débitage ------------------------------+
1                                                                              |
2    Numéro   Nom                                       Heure   Heure          |
3    empl.    Employé                                   début   fin            |
4    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
5    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
6    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
7    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
8    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
9    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
0    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
1    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
2    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
3    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
4    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
5    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
6    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
7    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
8    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
9    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
0    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
1    xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  HH:MM   HH:MM          |
2                                                                              |
3------------------------------------------------------------------------------+
@ENDIF
