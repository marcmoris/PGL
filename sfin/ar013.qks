;/T/ Détail de réquisition
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 25 janvier 1995
;
;/M/ Pascal Tremblay, 98-01-21
;
;    Ajustement pour le changement de siècle
;
;/M/ Programmeur.......: Francois Richard
;    Date modification : 19 juillet 1999
;    Description.......: Aid\351 la saisie de prdcle
;
;-------------------------------------------------------------------------------
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar013  ACTIONBAR &
              MODE LABEL "" AT 2,80  &
              FROM 1,1 TO 23,80 & 
              NOACTION FIELDMARK RETAIN STARTUP &
              RECEIVING T_CIECLEMNU  , &
                        T_CIENOM     , &
                        T_PECCLEMNU  , &
                        T_FOUCIECLE  


USE ussectmp NOLIST     ; Variable pour la sécurité
    "AR013"             ; Nom de l'écran

USE ar013.hlp  NOLIST   ; Use servant à la documentation de l'écran
                      

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-écrans

USE ustrasse NOLIST


;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 04 ; Période du menu
TEMPORARY T_FOUCIECLE CHARACTER SIZE 04 ; Compagnie fournisseur
TEMPORARY T_ARMTYPFND CHARACTER SIZE 1  INITIAL "1"
TEMPORARY T_NUMREFFND CHARACTER SIZE 12
TEMPORARY T_OLDVALUE  CHARACTER SIZE 1
TEMPORARY T_REQCLELEC CHARACTER SIZE 8
TEMPORARY T_REQCLEFND CHARACTER SIZE 9
TEMPORARY T_REQCLENUMFND INTEGER SIGNED SIZE 4
TEMPORARY T_FOUCLELEC CHARACTER SIZE 6
TEMPORARY T_MOD       CHARACTER SIZE 2 ; Code de module.
TEMPORARY T_CIECLE    CHARACTER SIZE 4  ; Compagnie pour écran dépisteur.
TEMPORARY T_FOUCLE    CHARACTER SIZE 6 ; Popup sur le fournisseur.

;-------------------------------------------------------------------------------

DEFINE    DZ_CIECLE   CHARACTER SIZE 04 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Table primaire.
;
FILE ARREQUISITION       PRIMARY &
                        OCCURS 7 TIME &
                        NOITEM

  ACCESS VIA    CIECLE   , &
                REQCLE    &
         USING  T_CIECLEMNU              ,  &
                REQCLE OF ARREQUISITION      &
        REQUEST REQCLE OF ARREQUISITION      &
        ORDERBY CIECLE   , &
                REQCLE   , &
                REQCLENUM

  ACCESS VIA    CIECLE   , &
                FOUCLE   , &
                REQSTUAPP  &
         USING  T_CIECLEMNU               , &
                FOUCLE    OF ARREQUISITION , &
                REQSTUAPP OF ARREQUISITION   &
        REQUEST FOUCLE    OF ARREQUISITION,  &
                REQSTUAPP OF ARREQUISITION   &
        ORDERBY CIECLE   , &
                REQCLE   , &
                REQCLENUM

  ACCESS VIA    CIECLE   , &
                FOUCLE     &
         USING  T_CIECLEMNU               , &
                FOUCLE    OF ARREQUISITION   &
        REQUEST FOUCLE    OF ARREQUISITION   &
        ORDERBY CIECLE   , &
                REQCLE   , &
                REQCLENUM

  ACCESS VIA    CIECLE    &
         USING  T_CIECLEMNU               &
         ORDERBY CIECLE   , &
                 REQCLE   , &
                 REQCLENUM




;-------------------------------------------------------------------------------
; 
; Fichier permettant de prendre par défaut le terme d'escompte sur achat
; du fournisseur.
; 
FILE VFOC_FOU    REFERENCE 

  ACCESS VIA   FOUCIECLE, &
               FOUCLE,    &
               FOCCIECLE  &
         USING FOUCIECLE OF ARREQUISITION, &
               FOUCLE    OF ARREQUISITION, &
               CIECLE    OF ARREQUISITION


;-------------------------------------------------------------------------------
;
; Fichier de produti à l'intérieur de l'entrepôt.
;
FILE VPRE_PRD  REFERENCE 

  ACCESS VIA   CIECLE, &
               PRDCLE, &
               ENTCLE  &
         USING CIECLE OF ARREQUISITION, & 
               PRDCLE OF ARREQUISITION, &
               ENTCLE OF ARREQUISITION 


DEFINE D_DSCPRD CHARACTER SIZE 28 = PRDDSC1 OF VPRE_PRD[1:28] &
                               IF PRDTYP  OF VPRE_PRD <> "D"&
                               ELSE REQDSC  OF ARREQUISITION

USE usvarprd NOLIST     ; Use pour la saisie de prdcle

USE usdrwecr NOLIST     ; Draw pour les écrans 
USE ustitstd NOLIST     ; En-tête pour les écrans 
USE ushilite NOLIST     ; Hilite pour tous les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Réquisition (Sommaire) " &
@ELSE
      "%%%quisition (Sommaire) "  
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

TITLE &
@IF FRANCAIS
  "Réquisition  Item   Statut        Fournisseur  /                   Qte req. /" & 
@ELSE
  "%%%%isition  Item   Statut        Fournisseur  /                   Qte req. /" & 
@ENDIF
  AT ,2

SKIP

TITLE &
@IF FRANCAIS
  "                                    produit                        date liv  " &
@ELSE
  "%%%%                                produit                        date liv  " &
@ENDIF
  AT ,2

;-------------------------------------------------------------------------------

SKIP 1

ALIGN (3,2,3) (,,14) (,,24) (,,27) (,,34) (,,69)

CLUSTER OCCURS WITH ARREQUISITION

FIELD REQCLE OF ARREQUISITION &
        HIDDEN &
        LABEL " " &
        DISPLAY

FIELD REQCLENUM OF ARREQUISITION &
        DISPLAY

FIELD REQSTUAPP OF ARREQUISITION &
        DISPLAY

FIELD FOUCLE OF ARREQUISITION &
        DISPLAY

FIELD FOUNOM OF VFOC_FOU &
        DISPLAY SIZE 30

FIELD REQQTEREQ OF ARREQUISITION &
        DISPLAY

ALIGN (,,27)(,,40)(,,69)
FIELD PRDCLE OF ARREQUISITION &
        DISPLAY

FIELD D_DSCPRD &
        DISPLAY 

FIELD REQDATREQ OF ARREQUISITION  FORMAT YYYYMMDD &
        DISPLAY

CLUSTER
;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour le choix du fournisseur
;
PROCEDURE INPUT FOUCLE     
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*") 
  THEN BEGIN
    LET T_FOUCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cp104 MODE F PASSING T_FOUCIECLE, T_FOUCLE, T_CIECLEMNU &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_FOUCLE)
  END
END

;------------------------------------------------------------------------------
;
;  
PROCEDURE INPUT PRDCLE
BEGIN
  USE usinpprd NOLIST
END

PROCEDURE DESIGNER 1
BEGIN
   LET T_REQCLEFND = REQCLE OF ARREQUISITION
   LET T_REQCLENUMFND = REQCLENUM OF ARREQUISITION
   RUN SCREEN ar006 MODE F PASSING     T_CIECLEMNU, &
                                       T_CIENOM   , &
                                       T_PECCLEMNU, &
                                       T_FOUCIECLE, &
                                       T_REQCLEFND, &
                                       T_REQCLENUMFND
END

PROCEDURE DESIGNER PH01
BEGIN
   LET T_REQCLEFND = REQCLE OF ARREQUISITION
   LET T_REQCLENUMFND = REQCLENUM OF ARREQUISITION
   RUN SCREEN ar006 MODE F PASSING     T_CIECLEMNU, &
                                       T_CIENOM   , &
                                       T_PECCLEMNU, &
                                       T_FOUCIECLE, &
                                       T_REQCLEFND, &
                                       T_REQCLENUMFND
END


PROCEDURE UPDATE
 DISABLE

PROCEDURE DELETE
 DISABLE

PROCEDURE ENTRY
 DISABLE


BUILD 
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Réquisition (Sommaire) ****************************
*                                                                              *
*Réquisition  Item   Statut        Fournisseur  /                   Qte req. / *
*                                    produit                        date liv   *
*                                                                              *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
* xxxxxxxxx  xxxxxxx   x  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxx *
*                         xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxx *
*                                                                              *
********************************************************************************
@ENDIF
