;/T/ 2.1.1 Consulter le sciage du bloc.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-06
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur le sciage du bloc
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd177f ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             ACTIVITIES FIND              &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM ,         &
                       PDBLOC,            &
                       PDSCIES,           &
                       PDSCIAGE_BLOCS

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD177F"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd177f.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC MASTER


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES MASTER


;-------------------------------------------------------------------------------
;
; Sciage de bloc
;

FILE PDSCIAGE_BLOCS MASTER


;-------------------------------------------------------------------------------
;
;
;

FILE PDTRANCHE PRIMARY &
               OCCURS 10 &
               NOAPPEND NODELETE &
               NOITEM

  ACCESS VIA    CIECLE,          &
                BLONUMGRA001APR, &
                BLONUMGRA002APR, &
                BLORECAPR        &
         USING  T_CIECLEMNU,               &
                BLONUMGRA001APR OF PDBLOC, &
                BLONUMGRA002APR OF PDBLOC, &
                BLORECAPR       OF PDBLOC


         ITEM CIECLE OF PDTRANCHE INITIAL T_CIECLEMNU

FILE PDTRANCHE_DEF SECONDARY &
                    OCCURS WITH PDTRANCHE &
                    ALIAS DEFAUT_1
  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE

FILE PDTRANCHE_DEF SECONDARY &
                    OCCURS WITH PDTRANCHE &
                    ALIAS DEFAUT_2
  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE

FILE PDTRANCHE_DEF DESIGNER &
                   ALIAS DEFAUT_3


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE &
                 OCCURS WITH PDTRANCHE

  ACCESS VIA   CIECLE,                      &
               TYFCODFIN                    &
         USING T_CIECLEMNU,                 &
               TYFCODFIN OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Définition des variables pour le traitement
;
TEMPORARY T_SURF FLOAT SIZE 8 OCCURS WITH PDTRANCHE

TEMPORARY T_COMPTEUR  INT SIZE 2

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Sciage " &
@ELSE
      " %%%Sciage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


TITLE "-" AT 5,23


TITLE "-" AT 5,29


SKIP TO LINE 5


ALIGN (,3,19)(,,24)(,,30)(,38,54)


FIELD BLONUMGRA001APR  OF PDBLOC           &
label "No bloc 1 ap..:"&
        NUMERIC                            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD BLONUMGRA002APR  OF PDBLOC           &
        NUMERIC                            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD BLORECAPR        OF PDBLOC           &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD SCBDATSCI        OF PDSCIAGE_BLOCS    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date sciage...:"&
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


ALIGN (,3,19)


FIELD SCBDSC001        OF PDSCIAGE_BLOCS   &
label "Description...:"&
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

ALIGN (,,19)


FIELD SCBDSC002        OF PDSCIAGE_BLOCS   &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD SCBDSC003        OF PDSCIAGE_BLOCS   &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


SKIP TO LINE 9


DRAW 9,1 TO 9,80


TITLE &
@IF FRANCAIS
      " Tranches " &
@ELSE
      " %%%Tranches " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      "Tranche   Long Larg Épais   M2   Statut  Défauts " &
@ELSE
      " %%%Tranche   Long Larg Épais   M2   Statut  Défauts" &
@ENDIF
     AT ,2


SKIP TO LINE 12


ALIGN (,,2) (,,12) (,,17) (,,22) (,,27) (,,37) (,,43) (,,48)


CLUSTER OCCURS WITH PDTRANCHE


FIELD TRANUMTRA002     OF PDTRANCHE        &
        NUMERIC                            &
        DISPLAY                            &
        BWZ

FIELD TRALON           OF PDTRANCHE        &
        PICTURE "^^^^" BWZ DISPLAY

FIELD TRAHAU           OF PDTRANCHE        &
        PICTURE "^^^^" BWZ DISPLAY

FIELD TRAEPA           OF PDTRANCHE        &
        PICTURE "^^^^" BWZ DISPLAY

FIELD T_SURF                               &
        SIGNIFICANCE 6                     &
        PICTURE "^^.^^^^" BWZ DISPLAY

FIELD TRASTU           OF PDTRANCHE        &
        DISPLAY

FIELD DTRCODDEF OF DEFAUT_1 &
        HIDDEN DISPLAY

FIELD DTRCODDEF OF DEFAUT_2 &
        HIDDEN DISPLAY

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


PROCEDURE OUTPUT DTRCODDEF OF DEFAUT_1
BEGIN
  IF NOT ENTRYMODE AND NOT CHANGEMODE AND NOT CORRECTMODE
  THEN BEGIN
  LET T_COMPTEUR = 0
  LET FIELDTEXT = " "
  WHILE RETRIEVING DEFAUT_3 &
         VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE
  BEGIN
    LET T_COMPTEUR = T_COMPTEUR + 1
    IF T_COMPTEUR = 1
    THEN BEGIN
           LET FIELDTEXT = DTRCODDEF OF DEFAUT_3
           BREAK
         END
  END
  END
END

PROCEDURE OUTPUT DTRCODDEF OF DEFAUT_2
BEGIN
  IF NOT ENTRYMODE AND NOT CHANGEMODE AND NOT CORRECTMODE
  THEN BEGIN
  LET T_COMPTEUR = 0
  LET FIELDTEXT = " "
  WHILE RETRIEVING DEFAUT_3 &
         VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE
  BEGIN
    LET T_COMPTEUR = T_COMPTEUR + 1
    IF T_COMPTEUR = 2
    THEN BEGIN
           LET FIELDTEXT = DTRCODDEF OF DEFAUT_3
           BREAK
         END
  END
  END
END
;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;
;-------------------------------------------------------------------------------
use uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Calcul de la surface des tranches
;
PROCEDURE POSTFIND
BEGIN
  FOR EACH PDTRANCHE
    BEGIN
      LET TZ_LARGEUR              = TRALON  OF PDTRANCHE
      LET TZ_HAUTEUR              = TRAHAU  OF PDTRANCHE
      LET TZ_EPAISSEUR            = TRAEPA  OF PDTRANCHE
      DO INTERNAL CALVOL
      LET T_SURF = TZ_MC2
  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Sciage ************************************
*                                                                              *
* No bloc 1 ap..: xxxx-xxxxx-x       Date sciage...: xxxxxxxx                  *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*********************************** Tranches ***********************************
*                                                                              *
*Tranche   Long Larg Épais   M2   Fini                                         *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxxxxxxxx xxxx xxxx xxxx xxxxxxx xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*                                                                              *
*******************************************************************************
@ENDIF
