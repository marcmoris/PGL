;/T/ 2.4.1 Enregistrer le montage. (NIVEAU BLOC)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-06
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le montage des blocs de granit.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd183a ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM,          &
                        PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD183A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd183a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;;;@IF FRANCAIS
;;;ACTIONMENU LABEL "Sous-écrans"
;;;  MENUITEM LABEL "Montage bloc journalier         " ACTION DESIGNER D001 MARK
;;;@ELSE
;;;ACTIONMENU LABEL "Subscreen"
;;;  MENUITEM LABEL "%%%Montage bloc journalier      " ACTION DESIGNER D001 MARK
;;;@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variables nécessaire pour l'apper de dépisteur (pop-up)
;
TEMPORARY T_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_BLONUMGRA002APR  CHARACTER SIZE 04
TEMPORARY T_BLORECAPR        CHARACTER SIZE 01
TEMPORARY T_CHAMP            INTEGER UNSIGNED SIZE 01
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 02
TEMPORARY T_STU              CHARACTER SIZE 1


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_RMOSTU    CHARACTER SIZE 16 = "RMOSTU"
TEMPORARY T_RMOSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;
TEMPORARY T_SILBLO CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE    MASTER


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC PRIMARY &
                    NOITEM  &
                    OCCURS 5

  ACCESS VIA     CIECLE,                              &
                 RMSNUMSEQ                            &
         USING   T_CIECLEMNU,                         &
                 RMSNUMSEQ        OF PDRAP_MONTAGE    &
         ORDERBY RMSNUMSEQ


  ITEM CIECLE       OF PDMONTAGE_BLOC INITIAL T_CIECLEMNU
  ITEM RMSNUMSEQ    OF PDMONTAGE_BLOC INITIAL RMSNUMSEQ       OF PDRAP_MONTAGE
  ITEM BLONUMBLOAPR OF PDMONTAGE_BLOC FINAL   BLONUMGRA001APR OF PDMONTAGE_BLOC + &
                                              BLONUMGRA002APR OF PDMONTAGE_BLOC + &
                                              BLORECAPR       OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC DESIGNER

  ACCESS VIA     CIECLE,          &
                 BLONUMGRA001APR, &
                 BLONUMGRA002APR, &
                 BLORECAPR        &
         USING   T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDMONTAGE_BLOC, &
                 BLONUMGRA002APR OF PDMONTAGE_BLOC, &
                 BLORECAPR       OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Bloc (Référence)
;

FILE PDBLOC REFERENCE OCCURS WITH PDMONTAGE_BLOC ALIAS BLO_REF

  ACCESS VIA     CIECLE,          &
                 BLONUMGRA001APR, &
                 BLONUMGRA002APR, &
                 BLORECAPR        &
         USING   T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDMONTAGE_BLOC, &
                 BLONUMGRA002APR OF PDMONTAGE_BLOC, &
                 BLORECAPR       OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
;Commande interne - assignation
;

FILE PDCOMMAN_ASSIGN REFERENCE OCCURS WITH PDMONTAGE_BLOC

  ACCESS VIA     CIECLE,          &
                 BLONUMGRA001APR, &
                 BLONUMGRA002APR, &
                 BLORECAPR        &
         USING   T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDMONTAGE_BLOC, &
                 BLONUMGRA002APR OF PDMONTAGE_BLOC, &
                 BLORECAPR       OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                              &
                 SCINUMSCI                            &
         USING   T_CIECLEMNU,                         &
                 SCINUMSCI        OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_RMOSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_RMOSTU,                             &
               RMOSTU           OF PDRAP_MONTAGE




FILE PDDET_RAP_MONT DESIGNER
;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Montage bloc " &
@ELSE
      " %%%Montage bloc" &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD RMSNUMSEQ        OF PDRAP_MONTAGE    &
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,25)


FIELD RMOSTU           OF PDRAP_MONTAGE    &
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_RMOSTU     &
        DISPLAY                            &
        FIXED


ALIGN (,3,19)


FIELD RMOPRI           OF PDRAP_MONTAGE    &
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,25)


FIELD SCINUMSCI       OF PDRAP_MONTAGE     &
        DISPLAY                            &
        FIXED


FIELD SCIDSC001        OF PDSCIES          &
        DISPLAY                            &
        FIXED



DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Niveau bloc " &
@ELSE
      " %%%Niveau bloc" &
@ENDIF
      CENTERED AT ,1




SKIP TO LINE 12

TITLE &
@IF FRANCAIS
      "                       Niveau                                        " &
@ELSE
      "%%%                    Niveau                                        " &
@ENDIF
      AT ,2


SKIP


TITLE &
@IF FRANCAIS
      "      No bloc        Haut   Bas    Long   Haut   Larg  Granit Num. cmd." &
@ELSE
      "%%%   No bloc        Haut   Bas    Long   Haut   Larg  Granit Num. cmd." &
@ENDIF
      AT ,2

ALIGN (06,07,08) (,12,13) (,18,19) (,,23) (,,30) (,,37) (,,44) (,,51) (,,58) (,,64)

SKIP 1

CLUSTER OCCURS WITH PDMONTAGE_BLOC ID BASE 30


FIELD BLONUMGRA001APR  OF PDMONTAGE_BLOC   &
        HIDDEN                             &
        LABEL " "                          &
        DISPLAY                            &
        NUMERIC                            &
        BWZ


FIELD BLONUMGRA002APR  OF PDMONTAGE_BLOC   &
        HIDDEN                             &
        LABEL "-"                          &
        DISPLAY                            &
        NUMERIC                            &
        BWZ


FIELD BLORECAPR        OF PDMONTAGE_BLOC    &
        HIDDEN                              &
        LABEL "-"                           &
        DISPLAY


FIELD MOBNIVHAU        OF PDMONTAGE_BLOC   &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED


FIELD MOBNIVBAS        OF PDMONTAGE_BLOC   &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED


FIELD BLOLONNET        OF BLO_REF          &
        HIDDEN                             &
        DISPLAY


FIELD BLOHAUNET        OF BLO_REF          &
        HIDDEN                             &
        DISPLAY


FIELD BLOLARNET        OF BLO_REF          &
        HIDDEN                             &
        DISPLAY


FIELD TGRCODGRA        OF BLO_REF          &
        HIDDEN                             &
        NUMERIC                            &
        DISPLAY                            &
        BWZ


FIELD COMNUMCOM        OF PDCOMMAN_ASSIGN  &
        HIDDEN                             &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA001APR
BEGIN
  ;
  ; Valide que le statut du rapport de montage permette l'ajout
  ; de nouveaux éléments.
  ;
  IF RMOSTU           OF PDRAP_MONTAGE <> "P"
  THEN BEGIN
    ERROR 3068 ; Le statut ne permet pas cette opération
  END

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLONUMGRA002APR  = ""
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 1
    LET T_COD              = 0
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA001APR )
  END
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA002APR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLONUMGRA002APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 2
    LET T_COD              = 0
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDBLOC = TRUNCATE( T_BLONUMGRA001APR )
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLONUMGRA002APR ) ) AND &
     0 =  SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLORECAPR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLONUMGRA002APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLORECAPR        = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CHAMP            = 3
    LET T_COD              = 0
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDBLOC = TRUNCATE( T_BLONUMGRA001APR )
    LET BLONUMGRA002APR OF PDBLOC = TRUNCATE( T_BLONUMGRA002APR )
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLORECAPR ) ) AND &
     0 = SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le niveau haut est plus grand que le niveau bas
;

PROCEDURE EDIT MOBNIVHAU
BEGIN
  ;
  ; Valide que des valeurs ont été saisit avant de faire le test
  ;
  IF MOBNIVHAU OF PDMONTAGE_BLOC <> 0 &
     AND &
     MOBNIVBAS OF PDMONTAGE_BLOC <> 0
  THEN BEGIN
    IF MOBNIVHAU OF PDMONTAGE_BLOC < MOBNIVBAS OF PDMONTAGE_BLOC
    THEN BEGIN
      ERROR 2993 ; Le niveau haut doit être plus grand que le niveau bas
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le niveau haut est plus grand que le niveau bas
;

PROCEDURE EDIT MOBNIVBAS
BEGIN
  ;
  ; Valide que des valeurs ont été saisit avant de faire le test
  ;
  IF MOBNIVHAU OF PDMONTAGE_BLOC <> 0 &
     AND &
     MOBNIVBAS OF PDMONTAGE_BLOC <> 0
  THEN BEGIN
    IF MOBNIVBAS OF PDMONTAGE_BLOC > MOBNIVHAU OF PDMONTAGE_BLOC
    THEN BEGIN
      ERROR 2994 ; Le niveau bas doit être plus petit que le niveau haut
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
  ;
  ; Valide que le statut du rapport de montage permette la mise à jour
  ;
  IF RMOSTU           OF PDRAP_MONTAGE <> "P"
  THEN BEGIN
    ERROR 3068 ; Le statut ne permet pas cette opération
  END
END


;;;;-------------------------------------------------------------------------------
;;;;
;;;; Montage bloc journalier
;;;;
;;;
;;;PROCEDURE DESIGNER D001
;;;BEGIN
;;;  IF ALTEREDRECORD OF PDMONTAGE_BLOC
;;;  THEN BEGIN
;;;    ERROR 2991 ; Faire une mise à jour avant d'accèder au sous-écran
;;;  END
;;;
;;;  RUN SCREEN  pd183d &
;;;              PASSING T_CIECLEMNU,       &
;;;                      T_CIENOM,          &
;;;                      PDMONTAGE_BLOC     &
;;;              MODE SAME
;;;END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDMONTAGE_BLOC &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDMONTAGE_BLOC &
     AND NOT NEWRECORD     OF PDMONTAGE_BLOC &
     AND NOT DELETEDRECORD OF PDMONTAGE_BLOC &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Mise à jour des informations sur la mise à jour
  ;
  IF (NOT NEWRECORD OF PDRAP_MONTAGE  AND &
      ALTEREDRECORD OF PDRAP_MONTAGE)     &
     OR                                   &
      (NEWRECORD OF PDMONTAGE_BLOC OR     &
      ALTEREDRECORD OF PDMONTAGE_BLOC)
  THEN BEGIN
    LET RMODATDERMAJ     OF PDRAP_MONTAGE = SYSDATE
    LET RMOHREDERMAJ     OF PDRAP_MONTAGE = SYSTIME / 10000
    LET RMOUSRDERMAJ     OF PDRAP_MONTAGE = LOGONID
  END

  FOR PDMONTAGE_BLOC
  BEGIN
    GET PDBLOC                        &
      VIA     CIECLE,                 &
              BLONUMGRA001APR,        &
              BLONUMGRA002APR,        &
              BLORECAPR               &
      USING   T_CIECLEMNU,                       &
              BLONUMGRA001APR OF PDMONTAGE_BLOC, &
              BLONUMGRA002APR OF PDMONTAGE_BLOC, &
              BLORECAPR       OF PDMONTAGE_BLOC
    IF NOT DELETEDRECORD OF PDMONTAGE_BLOC
    ;
    ; Indique que le bloc est sur un montage
    ;
    THEN BEGIN
      LET BLOSTU OF PDBLOC = "M"
      PUT PDBLOC
    END
    ;
    ; Indique que le bloc est en inventaire
    ;
    ELSE BEGIN
    GET PDDET_RAP_MONT OPTIONAL                 &
        VIA     CIECLE,                      &
                BLONUMGRA001APR,             &
                BLONUMGRA002APR,             &
                BLORECAPR                    &
      USING   T_CIECLEMNU,                       &
              BLONUMGRA001APR OF PDMONTAGE_BLOC, &
              BLONUMGRA002APR OF PDMONTAGE_BLOC, &
              BLORECAPR       OF PDMONTAGE_BLOC
      IF NOT ACCESSOK
      THEN BEGIN
       LET BLOSTU OF PDBLOC = "I"
       PUT PDBLOC
      END
    END
  END

END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************* Montage bloc *********************************
*                                                                              *
* Num. montage..: xxxxxxxxxxx                                                  *
* Statut........: x     xxxxxxxxxxxxxxxxxxxxxxxxx                              *
* Priorité......: xx                                                           *
* Numéro scie...: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
********************************* Niveau bloc **********************************
*                                                                              *
*                       Niveau                                                 *
*      No bloc        Haut   Bas    Long   Haut   Larg  Granit Projet          *
*                                                                              *
*      xxxx-xxxxx-x   xxxx   xxxx   xxxx   xxxx   xxxx   xxx   xx-xx-xxx       *
*      xxxx-xxxxx-x   xxxx   xxxx   xxxx   xxxx   xxxx   xxx   xx-xx-xxx       *
*      xxxx-xxxxx-x   xxxx   xxxx   xxxx   xxxx   xxxx   xxx   xx-xx-xxx       *
*      xxxx-xxxxx-x   xxxx   xxxx   xxxx   xxxx   xxxx   xxx   xx-xx-xxx       *
*      xxxx-xxxxx-x   xxxx   xxxx   xxxx   xxxx   xxxx   xxx   xx-xx-xxx       *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
