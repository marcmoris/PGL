;/T/ Permet de trouver les numéros de diagrammes associé à un projet.
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1994-10-27
;
;    Description..: Permet de visionner et de choisir un numéro de diagramme.
;
;    Paramètres...: Projet    ==> PJTABR
;                   Diagramme ==> DIANUMDIA002
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ig101 NOMODE NOACTION FIELDMARK    &
             FROM 11,35 TO 23,80          &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND              &
             RECEIVING T_PJTABR,          &
                       T_DIANUMDIA002,    &
                       T_CIECLEMNU

USE ig101.hlp NOLIST

USE ustrasse NOLIST

TEMPORARY T_PJTABR          CHARACTER SIZE 02
TEMPORARY T_DIANUMDIA002    CHARACTER SIZE 05
TEMPORARY T_CIECLEMNU       CHARACTER SIZE 04

;-------------------------------------------------------------------------------
; Priorité diagramme
;
FILE PDDIAGRAMME PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,                  &
                 PJTABR,                  &
                 DIANUMDIA002             &
         USING   T_CIECLEMNU,             &
                 T_PJTABR,                &
                 T_DIANUMDIA002           &
         ORDERBY CIECLE,                  &
                 PJTABR

FILE PDPROJET REFERENCE OCCURS WITH PDDIAGRAMME
  ACCESS VIA     PJTABR,                &
                 CIECLE                 &
         USING   PJTABR OF PDDIAGRAMME, &
                 T_CIECLEMNU

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,46       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF

SKIP TO 2

TITLE &
@IF FRANCAIS
      "    Proj Diagramme Description  " &
@ELSE
      "%%% Proj Diagramme Description  " &
@ENDIF
    AT ,2

SKIP TO LINE 3

TITLE &
@IF FRANCAIS
      "    ---- --------- -----------  " &
@ELSE
      "%%% ---- --------- -----------  " &
@ENDIF
    AT ,2

SKIP TO LINE 4

ALIGN (3,3,7) (,,13) (,,21)

CLUSTER OCCURS WITH PDDIAGRAMME

FIELD PJTABR OF PDDIAGRAMME             &
        HIDDEN LABEL ">>" DISPLAY

FIELD DIANUMDIA002 OF PDDIAGRAMME       &
        DISPLAY

FIELD PJTDSC OF PDPROJET                &
        DISPLAY                         &
        FOR 1,23

CLUSTER

;-------------------------------------------------------------------------------
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_DIANUMDIA002, "@")
  THEN LET T_DIANUMDIA002 = ""
END

;-------------------------------------------------------------------------------
; Retourne la valeur choisi par l'usager.
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_DIANUMDIA002 = DIANUMDIA002 OF PDDIAGRAMME
  RETURN
END

BUILD

@IF FORMAT

@ENDIF
