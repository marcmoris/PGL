;/T/ 2.3.1 Enregistrer la commande interne. (Priorité)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-09
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les priorités des commandes internes.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210o ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
; Transaction QUERY pour les sous-écrans
;
; Ajout d'une nouvelle transaction QUERY particulière au sous-écran
; pour permettre à l'usager de voir ses modifications
;
@IF VAXVMS
SET LIST TRANSACTION ; Pour voir les transactions utilisées

TRANSACTION QUERY COMMIT ON MODE
@ENDIF

USE ustrasse NOLIST

;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
; Variables temporaire pour la sécurité
;
; Description: Déclaration des variables temporaire pour la gestion
;              de la sécurité-prosig.
;
; Utilisation: Doit être utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la définition du premier fichier.
;
;------------------------------------------------------------------------------
;
; Accès au programme.
;
TEMPORARY TZ_SECACC CHARACTER SIZE 1 RESET AT STARTUP
;
; Saisie de données(Entry).
;
TEMPORARY TZ_SECENT CHARACTER SIZE 1 RESET AT STARTUP
;
; Destruction.(Delete)
;
TEMPORARY TZ_SECDEL CHARACTER SIZE 1 RESET AT STARTUP
;
; Modification.(Change mode)
;
TEMPORARY TZ_SECMOD CHARACTER SIZE 1 RESET AT STARTUP
;
; Variable pour l'élément de la sécurité.
;
TEMPORARY TZ_XELNOM CHARACTER SIZE 16 RESET AT STARTUP
TEMPORARY TZ_FLGAUT CHARACTER SIZE  1 RESET AT STARTUP
;
; Nom de l'écran pour retrouver la sécurité.
;
TEMPORARY TZ_NOMECR CHARACTER SIZE 7 RESET AT STARTUP INIT &
;
;
;**FIN USSECTMP
    "PD210O"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
Description of screen &
@IF FRANCAIS
"                                                            " &
"     Cet écran permet de gérer les priorités des commandes  " &
" internes.                                                  " &
"                                                            "
@ELSE
"                                                            " &
"     This screen permits ....                               " &
@ENDIF


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
; ACTIONBAR
;
; Description: Définition du menu déroulant standard situé dans le haut de
;              l'écran.
;
; Utilisation: Il faut l'utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la déclaration des fichiers.
;
;
;
@IF FRANCAIS
ACTIONMENU LABEL "Rech"   ACTION FIND
ACTIONMENU LABEL "MAJ"    ACTION UPDATE
ACTIONMENU LABEL "Entrée" ACTION ENTRY
ACTIONMENU LABEL "Ajout"  ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Supp"   ACTION DELETE
@ELSE
ACTIONMENU LABEL "Find"   ACTION FIND
ACTIONMENU LABEL "Update" ACTION UPDATE
ACTIONMENU LABEL "Entry"  ACTION ENTRY
ACTIONMENU LABEL "Append" ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Del"    ACTION DELETE
@ENDIF

@IF VAXVMS AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Ecran      <PF1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Champ          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Champ dét. <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Recherche     <rech>" ACTION FIND
  MENUITEM LABEL "MAJ            <PF3>" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour <PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde      <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entrée         <ins>" ACTION ENTRY
  MENUITEM LABEL "Ajout      <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Sélection   <sélect>" ACTION SELECT
  MENUITEM LABEL "Supp.    <eff.texte>" ACTION DELETE
  MENUITEM LABEL "Supp.Lig  <PF1 eff.>" ACTION DELETE MARK
  MENUITEM LABEL "Edition         <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant  <page suiv>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.    <PF4>" ACTION RETURN
  MENUITEM LABEL "Sortie         <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "Impression     <F20>" ACTION LIST
  MENUITEM LABEL "Système       <exec>" ACTION SYSTEM
@ENDIF

@IF VAXVMS AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Screen     <pf1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Field          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Ext. Field <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Find Mode     <find>" ACTION FIND
  MENUITEM LABEL "Update         <PF3>" ACTION UPDATE
  MENUITEM LABEL "Update Ret.<PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay    <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode     <ins>" ACTION ENTRY
  MENUITEM LABEL "Append     <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Selection   <select>" ACTION SELECT
  MENUITEM LABEL "Delete      <remove>" ACTION DELETE
  MENUITEM LABEL "Del. Line <PF1 rem.>" ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Next     <next page>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen   <pf4>" ACTION RETURN
  MENUITEM LABEL "Quick Exit     <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "List           <F20>" ACTION LIST
  MENUITEM LABEL "System          <Do>" ACTION SYSTEM
@ENDIF

@IF UNIX AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Aide            F1_H" ACTION HELP
  MENUITEM LABEL "Aide dét.       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Recherche      F1_F7" ACTION FIND
  MENUITEM LABEL "MAJ               F5" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour     F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde           " ACTION UPDATE STAY
  MENUITEM LABEL "Entrée            F7" ACTION ENTRY
  MENUITEM LABEL "Ajout               " ACTION APPEND
  MENUITEM LABEL "Sélection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Supp.             F2" ACTION DELETE
  MENUITEM LABEL "Supp.Lig            " ACTION DELETE MARK
  MENUITEM LABEL "Edition         F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant       <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.       F8" ACTION RETURN
  MENUITEM LABEL "Sortie         F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "Impression écran  F3" ACTION LIST
@ENDIF

@IF UNIX AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Help            F1_H" ACTION HELP
  MENUITEM LABEL "Ext. Help       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Find mode      F1_F7" ACTION FIND
  MENUITEM LABEL "Update            F5" ACTION UPDATE
  MENUITEM LABEL "Update Return  F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay         " ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode        F7" ACTION ENTRY
  MENUITEM LABEL "Append              " ACTION APPEND
  MENUITEM LABEL "Selection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Delete            F2" ACTION DELETE
  MENUITEM LABEL "Del line            " ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Next          <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen      f8" ACTION RETURN
  MENUITEM LABEL "Quick Exit     F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "List              F3" ACTION LIST
@ENDIF
;
;
;**FIN USACTECR


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_COLCODLIV CHARACTER SIZE 16 = "COLCODLIV"
TEMPORARY T_COLCODLIV CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE MASTER


;-------------------------------------------------------------------------------
;
; Priorité
;

FILE PDPRIORITE PRIMARY &
                NOITEM &
                OCCURS 15

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 PRICODPRI


  ITEM CIECLE           OF PDPRIORITE INITIAL T_CIECLEMNU
  ITEM PJTABR           OF PDPRIORITE INITIAL PJTABR       OF PDCOMMAN_INTERNE FIXED
  ITEM PJTANN           OF PDPRIORITE INITIAL PJTANN       OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMSEQ        OF PDPRIORITE INITIAL PJTNUMSEQ    OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOMINT     OF PDPRIORITE INITIAL COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMPRO        OF PDPRIORITE INITIAL PJTABR       OF PDCOMMAN_INTERNE + &
                                              PJTANN       OF PDCOMMAN_INTERNE + &
                                              PJTNUMSEQ    OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOM        OF PDPRIORITE INITIAL PJTABR       OF PDCOMMAN_INTERNE + &
                                              PJTANN       OF PDCOMMAN_INTERNE + &
                                              COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM PRIBCMTRF        OF PDPRIORITE INITIAL "0"
  ITEM PRIBCMSCI        OF PDPRIORITE INITIAL "0"
  ITEM PRICPL           OF PDPRIORITE INITIAL "0"

TEMPORARY T_DIAG CHAR SIZE 1 OCCURS WITH PDPRIORITE

DEFINE D_DIAG CHAR SIZE 1 = T_DIAG

FILE PDPRIORITE_DIAG DESIGNER
;-------------------------------------------------------------------------------
;
;

DRAW 3,01 TO 23,80
;
;
;**FIN USDRWECR
; En-tête standard des écrans pleine page.
;
; Description: Ce USE sert à afficher l'en-tête standard des écrans pleine
;              page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD.
;
;
SKIP TO LINE 2

@IF VAXVMS
HILITE DISPLAY HALFTONE
@ENDIF

ALIGN(,,1) (,,21)

FIELD DZ_CIECLE FIXED
FIELD DZ_CIENOM FIXED

;
;
;**FIN USTITSTD
; HILITE standard de tous les écrans
;
; Description: Définition du HILITE standard de tous les écrans, menu
;              écrans pleine page, popup...
;
; Utilisation: Doit être utilisé avant la première énoncé FIELD.
;
;
@IF VAXVMS
HILITE ACTIONBAR     INVERSE                     , &
       ACTIONBARMARK HALFTONE INVERSE            , &
       FIELDMARK     INVERSE                     , &
       MENUMARK      HALFTONE INVERSE            , &
       MODE          HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       HALFTONE UNDERLINE          , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE HALFTONE AUDIBLE    , &
       ERROR         INVERSE HALFTONE AUDIBLE    , &
       SEVERE        INVERSE HALFTONE AUDIBLE BLINKING
@ELSE
HILITE ACTIONBAR     HALFTONE INVERSE            , &
       ACTIONBARMARK INVERSE                     , &
       FIELDMARK     INVERSE HALFTONE            , &
       MENUMARK      INVERSE HALFTONE            , &
       MODE          HALFTONE                    , &
       LABEL         HALFTONE                    , &
       LINEDRAWING   HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       UNDERLINE                   , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE AUDIBLE             , &
       ERROR         INVERSE AUDIBLE             , &
       SEVERE        INVERSE AUDIBLE BLINKING
@ENDIF
;
;**FIN USHILITE


SKIP


TITLE &
@IF FRANCAIS
      " Priorité " &
@ELSE
      " %%%Priorité " &
@ENDIF
      CENTERED AT ,1


; HILITE TITLE OFF
;
; Description: Sert à désactiver l'option HILITE pour toutes les
;              énoncés TITLE qui suit ce USE. Seul le titre principal
;              a un HILITE particulier.
;
; Utilisation: Doit être utilisé après le titre principal de l'écran.
;
;
HILITE TITLE OFF
;
;
;**FIN USHILITE


SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        FIXED


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        FIXED


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        FIXED


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        FIXED


DRAW 5,2 TO 5,79


SKIP TO LINE 7


TITLE &
@IF FRANCAIS
      "      Prio. Description                               Date fin  Diagramme" &
@ELSE
      " %%%     Prio. Description                               Date fin  Diagramme " &
@ENDIF
      AT ,2


ALIGN (07,07,08) (14,,14) (56,,56) (,,70)


CLUSTER OCCURS WITH PDPRIORITE ID BASE 50


FIELD PRICODPRI        OF PDPRIORITE       &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED                           &
        NUMERIC                            &
        LOOKUP NOTON PDPRIORITE            &
          VIA   CIECLE,                    &
                PJTABR,                    &
                PJTANN,                    &
                COMNUMCOMINT,              &
                PRICODPRI                  &
          USING T_CIECLEMNU,                          &
                PJTABR           OF PDCOMMAN_INTERNE, &
                PJTANN           OF PDCOMMAN_INTERNE, &
                COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                PRICODPRI        OF PDPRIORITE &
          MESSAGE 3097 ; Cette priorité existe déjà


FIELD PRIDSC           OF PDPRIORITE       &
        ID SAME &
        DEFAULT "Priorité : " + PRICODPRI OF PDPRIORITE


FIELD PRIDATFIN        OF PDPRIORITE &
      ID SAME                        &
      REQUIRED                       &
      FORMAT YYYYMMDD                &
      PATTERN "####/##/##"           &
      SIZE 10                        

FIELD D_DIAG ID SAME DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
; Appel de l'écran de sécurité.
;
; Description: Appel de l'écran de sécurité du module Gestion de système.
;              Les variables temporaires utilisées proviennent du USEFILE
;              USSECTMP.
;
; Utilisation: Doit être inséré dans la procédure INITIALIZE de l'écran.
;
;
RUN SCREEN gs000 PASSING TZ_NOMECR, & ; Nom de l'écran
                         TZ_SECACC, & ; Accès
                         TZ_SECENT, & ; Entrée
                         TZ_SECMOD, & ; Modification
                         TZ_SECDEL    ; Destruction

IF "3" = TZ_SECACC
THEN ERROR 6 ; L'usager n'est pas défini au système.

IF "2" = TZ_SECACC
THEN ERROR 7 ; Votre accès au système est expiré.

IF "0" = TZ_SECACC
THEN ERROR 8 ; Vous n'avez pas accès à cet option.
;
;
;**FIN USSECECR
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDPRIORITE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDPRIORITE &
     AND NOT NEWRECORD     OF PDPRIORITE &
     AND NOT DELETEDRECORD OF PDPRIORITE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

PROCEDURE APPEND
  BEGIN
    ACCEPT PRICODPRI OF PDPRIORITE
    ACCEPT PRIDSC OF PDPRIORITE
    ACCEPT PRIDATFIN OF PDPRIORITE
    END
PROCEDURE ENTRY
  BEGIN
    FOR PDPRIORITE
      BEGIN
        PERFORM APPEND
        END
    END
PROCEDURE PATH
  BEGIN
    LET PATH = 1
    END
PROCEDURE FIND
  BEGIN
    FOR MISSING PDPRIORITE
      BEGIN
        GET PDPRIORITE VIA CIECLE , PJTABR , PJTANN , COMNUMCOMINT &
              ORDERBY CIECLE , PJTABR , PJTANN , COMNUMCOMINT , &
              PRICODPRI USING T_CIECLEMNU , PJTABR OF &
              PDCOMMAN_INTERNE , PJTANN OF PDCOMMAN_INTERNE , &
              COMNUMCOMINT OF PDCOMMAN_INTERNE
        GET PDPRIORITE_DIAG VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 PRICODPRI            &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDPRIORITE, &
                 PJTANN           OF PDPRIORITE, &
                 COMNUMCOMINT     OF PDPRIORITE, &
                 PRICODPRI        OF PDPRIORITE OPT
         IF ACCESSOK
         THEN LET T_DIAG = "*"
         ELSE LET T_DIAG = " "
        END
    END
 PROCEDURE UPDATE
   BEGIN
     FOR PDPRIORITE
       BEGIN
         PUT PDPRIORITE
         END
     PUT PDCOMMAN_INTERNE
     END
PROCEDURE DELETE
  BEGIN
    DELETE PDPRIORITE
    END

BUILD list detail

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Adresse de livraison *****************************
* Num. commande.: xx-xx-xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
*                                                                              *
*         Prio. Description                                Date fin            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*          XX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   AA/MM/JJ            *
*                                                                              *
********************************************************************************
@ENDIF
