;/T/ Natures comptables
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 26 février 1992
;
;    Description..: Définition des natures comptable, les codes de natures
;                   comptable ont la valeur de 1 à 5 et ils sont fixes.
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc009 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES ENTRY, FIND, CHANGE &
             RECEIVING T_CIENOM

USE ussectmp NOLIST     ; Variable pour la securite
    "MC009"

USE mc009.hlp NOLIST

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding

;-------------------------------------------------------------------------------
;
FILE MCNATURE_CTB     PRIMARY &
                      OCCURS 5
  ACCESS SEQUENTIAL &
         ORDERBY NACCLE

;
; Valide s'il y a déjà des données.
;
FILE MCNATURE_CTB     ALIAS A_NAC_VALIDE &
                      DESIGNER
  ACCESS VIA NACCLE

;-------------------------------------------------------------------------------
;
TEMPORARY T_BORNE  CHAR SIZE 6  ; Contient le borne à valider
TEMPORARY T_OCC    INT  SIZE 2  ; Numéro d'occurrence

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Natures comptables " &
@ELSE
      " Account Classification " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 2

DRAW 5,1  TO  5,80
DRAW 7,1  TO  7,80
DRAW 5,9  TO 23,9
DRAW 5,37 TO 23,37
DRAW 5,65 TO 23,65

@IF FRANCAIS
TITLE "Français" AT ,19
TITLE "Anglais"  AT ,48
TITLE "Borne"    AT ,70
SKIP 1
TITLE "Genre" AT ,3
TITLE "Nature              Abrév." AT ,10
TITLE "Nature              Abrév." AT ,38
TITLE "Infér. Supér."              AT ,66
SKIP
@ELSE
TITLE "French" AT ,19
TITLE "English"  AT ,48
TITLE "Bounds"    AT ,70
SKIP 1
TITLE "Kind" AT ,3
TITLE "Classification      Abbr." AT ,10
TITLE "Classification      Abbr." AT ,38
TITLE "Lower  Upper "             AT ,66
SKIP
@ENDIF

ALIGN(3,2,5)(,,10)(,,32)(,,38)(,,60)(,,66)(,,73)
CLUSTER OCCURS WITH MCNATURE_CTB
SKIP
FIELD NACCLE OF MCNATURE_CTB &
  HIDDEN LABEL " " &
  DISPLAY
FIELD NACDSCFRA    OF MCNATURE_CTB REQUIRED
FIELD NACDSCABRFRA OF MCNATURE_CTB REQUIRED
FIELD NACDSCANG    OF MCNATURE_CTB REQUIRED
FIELD NACDSCABRANG OF MCNATURE_CTB REQUIRED
FIELD NACBORINF    OF MCNATURE_CTB REQUIRED
FIELD NACBORSUP    OF MCNATURE_CTB REQUIRED
CLUSTER

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT NACDSCFRA OF MCNATURE_CTB
BEGIN
  IF ENTRYMODE
  THEN BEGIN
    GET A_NAC_VALIDE SEQUENTIAL OPTIONAL
    IF ACCESSOK
    THEN ERROR 122 ;Nature comptable déjà définis.
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT NACCLE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si les natures comptable sont déjà définies, si oui oblige l'usager
; à prendre le mode FIND.
;
; La procédure ENTRY suivante empèche la possibilité de faire un APPEND
; à l'écran.
;
;
PROCEDURE ENTRY
BEGIN
  FOR MCNATURE_CTB
  BEGIN
    ;
    ; La clef du fichier correspond au numéro d'occurence(1 à 5).
    ;
    LET NACCLE OF MCNATURE_CTB = OCCURRENCE
    DISPLAY NACCLE OF MCNATURE_CTB
    ACCEPT NACDSCFRA    OF MCNATURE_CTB
    ACCEPT NACDSCABRFRA OF MCNATURE_CTB
    ACCEPT NACDSCANG    OF MCNATURE_CTB
    ACCEPT NACDSCABRANG OF MCNATURE_CTB
    ACCEPT NACBORINF    OF MCNATURE_CTB
    ACCEPT NACBORSUP    OF MCNATURE_CTB
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide que les intervals ne sont pas chevauché
;
;
PROCEDURE INTERNAL VALIDE_BORNE
BEGIN
  FOR MCNATURE_CTB
  BEGIN
    IF T_BORNE >= NACBORINF OF MCNATURE_CTB AND &
       T_BORNE <= NACBORSUP OF MCNATURE_CTB AND &
       T_OCC <> OCCURRENCE
    THEN ERROR 123 ; Erreur dans le définition des intervals de compte.
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  FOR MCNATURE_CTB
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF MCNATURE_CTB &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF MCNATURE_CTB &
       AND NOT NEWRECORD     OF MCNATURE_CTB &
       AND NOT DELETEDRECORD OF MCNATURE_CTB &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
  ;
  ; Validation des intervals
  ;
  FOR MCNATURE_CTB
  BEGIN
    ;
    ; Les bornes sont obligatoires
    ;
    IF NACBORINF OF MCNATURE_CTB = "" or &
       NACBORSUP OF MCNATURE_CTB = ""
    THEN ERROR 123 ; Erreur dans le définition des intervals de compte.

    IF NACBORINF OF MCNATURE_CTB >= NACBORSUP OF MCNATURE_CTB
    THEN ERROR 123 ; Erreur dans le définition des intervals de compte.
  END

  FOR MCNATURE_CTB
  BEGIN
    LET T_BORNE  = NACBORINF OF MCNATURE_CTB
    LET T_OCC    = OCCURRENCE
    DO INTERNAL VALIDE_BORNE

    LET T_BORNE  = NACBORSUP OF MCNATURE_CTB
    LET T_OCC    = OCCURRENCE
    DO INTERNAL VALIDE_BORNE
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Natures comptables ******************************
*                                                                              *
********************************************************************************
*       *         Français          *          Anglais          *    Borne     *
********************************************************************************
* Genre *Nature              Abrév. *Nature              Abrév. *Infér. Supér. *
*       *                           *                           *              *
*   x   *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxx xxxxxx *
*       *                           *                           *              *
*   x   *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxx xxxxxx *
*       *                           *                           *              *
*   x   *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxx xxxxxx *
*       *                           *                           *              *
*   x   *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxx xxxxxx *
*       *                           *                           *              *
*   x   *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxxxxxxxxxxxxxxxx  x    *xxxxxx xxxxxx *
*       *                           *                           *              *
*       *                           *                           *              *
*       *                           *                           *              *
*       *                           *                           *              *
********************************************************************************
@ENDIF
