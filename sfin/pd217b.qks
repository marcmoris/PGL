;/T/ 2.5.2: Enregistrer les expeditions - CONTENEUR
;
;/P/ Programmeur..: Alain Simard, PROSIG informatique inc.
;    Date Création: 11 mai 1995
;
;    Description..: Cet écran a pour but de gérer les conteneurs associés
;                   à un bon de livraison
;
;/M/François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction pour unix.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd217b ACTIONBAR &
        MODE LABEL "" AT 2,80 &
        NOACTION &
        FIELDMARK RETAIN STARTUP &
        HELP POPUP FROM 4,9 TO 20,72 &
        RECEIVING       T_CIECLEMNU , &
                        T_CIENOM, &
                        PDBON_LIVRAISON

;-------------------------------------------------------------------------------
;
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_EXPED IN DICT

;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd217b.hlp NOLIST

;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD217B"

;-------------------------------------------------------------------------------
; definition de la transaction query por les sous-ecrans
USE ustrasse NOLIST

;-------------------------------------------------------------------------------
; Actionbar standard dans les écrans pleine page et sous-ecran
USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Bon de livraison
;

FILE PDBON_LIVRAISON MASTER


;-------------------------------------------------------------------------------
;
; Conteneur
;

FILE PDCONTENEUR PRIMARY &
                 NOITEM

  ACCESS  VIA     CIECLE,    &
                  EXPNUMEXP, &
                  BLINUMBLI, &
                  CTNNUMCON &
          REQUEST CTNNUMCON &
          USING   T_CIECLEMNU,                  &
                  EXPNUMEXP OF PDBON_LIVRAISON, &
                  BLINUMBLI OF PDBON_LIVRAISON, &
                  CTNNUMCON

  ACCESS  VIA     CIECLE,   &
                  EXPNUMEXP, &
                  BLINUMBLI &
          USING   T_CIECLEMNU,                 &
                  EXPNUMEXP OF PDBON_LIVRAISON,&
                  BLINUMBLI OF PDBON_LIVRAISON &
          ORDERBY CIECLE,    &
                  CTNNUMCTR

  ITEM CIECLE    OF PDCONTENEUR INITIAL T_CIECLEMNU
  ITEM EXPNUMEXP OF PDCONTENEUR INITIAL EXPNUMEXP OF PDBON_LIVRAISON
  ITEM BLINUMBLI OF PDCONTENEUR INITIAL BLINUMBLI OF PDBON_LIVRAISON
  ITEM CTNCOULOC OF PDCONTENEUR SUM INTO CTNPRITRP OF PDCONTENEUR
  ITEM CTNCOUTRP OF PDCONTENEUR SUM INTO CTNPRITRP OF PDCONTENEUR


;-------------------------------------------------------------------
; Validation des codes saisie
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

; type d'expedition
DEFINE    D_CTNTYPCON CHARACTER SIZE 16 = "CTNTYPCON"
TEMPORARY T_CTNTYPCON CHARACTER SIZE 4

FILE VCBI_DSC   REFERENCE &
        ALIAS   A_CBI_CTNTYPCON
        ACCESS  VIA     XELNOM, &
                        CBICLE &
                USING   D_CTNTYPCON, &
                        CTNTYPCON OF PDCONTENEUR OPT

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les numéros d'expédition
;

FILE PDSEQ_EXPED DESIGNER &
                     TRANSACTION NO_SEQ

;-------------------------------------------------------------------------------

TEMPORARY T_CLICIECLE    CHARACTER SIZE 04 ; Popup sur les adresses du client
TEMPORARY T_CLICLE       CHARACTER SIZE 06 ; Popup sur les adresses du client
TEMPORARY T_REFCLETYP    CHARACTER SIZE 01 ; Popup sur les adresses du client
TEMPORARY T_RADCLE       INTEGER UNSIGNED &
                                   SIZE 02 ; Popup sur les adresses du client
TEMPORARY T_TYPADR       CHARACTER SIZE 03 ; Popup sur les adresses du client


;===============================================================================
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

;===============================================================================

TITLE &
@IF FRANCAIS
      " CONTENEUR " &
@ELSE
      " %%%CONTENEUR " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

skip 1

ALIGN (,4,20) (,44,60)

FIELD EXPNUMEXP OF PDBON_LIVRAISON &
label "Num. expédi...:"&
        FIXED

FIELD BLINUMBLI OF PDBON_LIVRAISON &
label "Bon livraison.:"&
        NUM &
        FIXED

SKIP

FIELD CTNNUMCON OF PDCONTENEUR &
label "Num. conteneur:"&
        HIDDEN &
        REQUIRED &
        NOCHANGE &
        LOOKUP  NOTON   PDCONTENEUR &
                VIA     CIECLE,     &
                        EXPNUMEXP , &
                        BLINUMBLI , &
                        CTNNUMCON &
                USING   T_CIECLEMNU,               &
                        EXPNUMEXP OF PDCONTENEUR , &
                        BLINUMBLI OF PDCONTENEUR , &
                        CTNNUMCON OF PDCONTENEUR &
                MESSAGE 3159 ; Ce conteneue est déjà assodié à cette exp.

SKIP

FIELD CTNNUMCTR OF PDCONTENEUR &
label "Num. contrat..:"&
        HIDDEN

SKIP

ALIGN (3,4,20) (,,27)

FIELD CTNTYPCON OF PDCONTENEUR &
label "Type conteneur:"&
        HIDDEN &
        LOOKUP  ON      A_CBI_CTNTYPCON &
                VIA     XELNOM, &
                        CBICLE &
                USING   D_CTNTYPCON, &
                        FIELDTEXT &
                MESSAGE 2904 ; Ce code n'existe pas


FIELD CBIDSC OF A_CBI_CTNTYPCON &
        DISPLAY

SKIP

FIELD CTNDSC OF PDCONTENEUR &
label "Description...:"&
        HIDDEN

SKIP

FIELD CTNNUMSCE OF PDCONTENEUR &
label "Num. sceau....:"&
        HIDDEN

SKIP

FIELD CTNDATFER OF PDCONTENEUR  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date fermeture:"&
        HIDDEN

SKIP

FIELD CTNNOMBAT OF PDCONTENEUR &
label "Nom bateau....:"&
        HIDDEN

SKIP

FIELD CTNNUMVOY OF PDCONTENEUR &
label "Num. voyage...:"&
        HIDDEN

SKIP

FIELD CTNLIGMAR OF PDCONTENEUR &
label "Ligne maritime:"&
        HIDDEN

SKIP

FIELD CTNPORCHG OF PDCONTENEUR &
label "Port charge...:"&
        HIDDEN

SKIP

FIELD CTNPORDCH OF PDCONTENEUR &
label "Port décharge.:"&
        HIDDEN

SKIP

FIELD CTNDESFIN OF PDCONTENEUR &
label "Destination...:"&
        HIDDEN

SKIP

FIELD CTNADRRAM OF PDCONTENEUR &
label "Adresse ramas.:"&
        HIDDEN

SKIP

FIELD CTNADRRET OF PDCONTENEUR &
label "Adresse retour:"&
        HIDDEN

SKIP

FIELD CTNCOM OF PDCONTENEUR &
label "Commentaire...:"&
        HIDDEN

SKIP

FIELD CTNCOULOC OF PDCONTENEUR &
label "Coût location.:"&
        HIDDEN

SKIP

ALIGN (3,4,20) (,41,57)

FIELD CTNCOUTRP OF PDCONTENEUR &
label "Coût transport:"&
        HIDDEN


FIELD CTNPRITRP OF PDCONTENEUR &
label "Prix transport:"&
        DISPLAY

SKIP

FIELD CTNNUMFACTRP OF PDCONTENEUR &
label "Facture transp:"&
        HIDDEN

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST

END

;-------------------------------------------------------------------
; Appel du popup pour les codes bilingues.
; type d'expedition
;
PROCEDURE INPUT CTNTYPCON OF PDCONTENEUR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CTNTYPCON = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_CTNTYPCON , &
                             T_CTNTYPCON
    LET FIELDTEXT = TRUNCATE( T_CTNTYPCON )
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR PDCONTENEUR
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDCONTENEUR &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDCONTENEUR &
       AND NOT NEWRECORD     OF PDCONTENEUR &
       AND NOT DELETEDRECORD OF PDCONTENEUR &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END

  ;
  ; Incrémentation du numéro de séquence de l'expedition
  ;
  IF 0 = EXPNUMEXP OF PDCONTENEUR
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_EXPED OPTIONAL &
      VIA     CIECLE     &
      USING   T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE    OF PDSEQ_EXPED = T_CIECLEMNU
    END
    LET SEPNUMEXP OF PDSEQ_EXPED = SEPNUMEXP OF PDSEQ_EXPED + 1
    LET EXPNUMEXP OF PDCONTENEUR = SEPNUMEXP OF PDSEQ_EXPED
    LET EXPNUMEXP OF PDBON_LIVRAISON = SEPNUMEXP OF PDSEQ_EXPED
    PUT PDSEQ_EXPED
    DISPLAY EXPNUMEXP OF PDBON_LIVRAISON
  END
END


;===============================================================================
BUILD

@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************************************************************
*  Num. expédi...: xxxxxxxxxxx             Bon livraison.: xx                  *
*  Num. conteneur: xxxxxxxxxxxxxxxxxxxx                                        *
*  Num. contrat..: xxxxxxxxxxxxxxxxxxxx                                        *
*  Type conteneur: xxxx   xxxxxxxxxxxxxxxxxxxxxxxxx                            *
*  Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Num. sceau....: xxxxxxxxxxxxxxxxxxxx                                        *
*  Date fermeture: xxxxxxxx                                                    *
*  Nom bateau....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Num. voyage...: xxxxxxxxxxxxxxxxxxxx                                        *
*  Ligne maritime: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Port charge...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Port décharge.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Destination...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Adresse ramas.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Adresse retour: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Commentaire...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Coût location.: xxxxxxxxxxxxxxx                                             *
*  Coût transport: xxxxxxxxxxxxxxx      Prix transport: xxxxxxxxxxxxxxx        *
*  Facture transp: xxxxxxxxxxxxxxxxxxxx                                        *
********************************************************************************
@ENDIF
