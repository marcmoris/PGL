;/T/ 2.2.2 Réception de produit (Écran maitre)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-08-19
;
;    Description..: Ce panorama permet de gérer les réceptions de produits.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd176 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             ACTIVITIES CHANGE,FIND,ENTRY &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;
TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_RECEPTION IN DICT


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD176"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd176.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Réception de bloc               " ACTION DESIGNER D001
  MENUITEM LABEL "Réception de tranche            " ACTION DESIGNER D002
  MENUITEM LABEL "Réception de caisson            " ACTION DESIGNER D003
  MENUITEM LABEL "Réception de prod. de dimension " ACTION DESIGNER D004
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Réception de bloc            " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Réception de tranche         " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Réception de caisson         " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Réception de prod. dimension " ACTION DESIGNER D004
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_BCMCODTYPBCM CHARACTER SIZE 16 = "BCMCODTYPBCM"
TEMPORARY T_BCMCODTYPBCM CHARACTER SIZE 04
DEFINE    D_BCMTYPBCM    CHARACTER SIZE 16 = "BCMTYPBCM"
TEMPORARY T_BCMTYPBCM    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Pop-up
;
TEMPORARY T_FOUCLE     CHARACTER SIZE 06 RESET AT STARTUP ; POP-UP Fournisseur


TEMPORARY T_FOUNOM     CHARACTER SIZE 40 RESET AT STARTUP ; NOM DU FOURNISSEUR
                                                          ; POUR LES SOUS-ÉCRANS
TEMPORARY T_USINE_SEC  CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_BLOC       CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_TRANCHE    CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_DIAGRAMME  CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_PROD_DIMEN CHARACTER SIZE 01 RESET AT STARTUP


;-------------------------------------------------------------------------------
;
; Réception
;

FILE PDRECEPTION PRIMARY NOITEM

  ACCESS VIA     CIECLE,                  &
                 RCTNUMREC                &
         USING   T_CIECLEMNU,             &
                 RCTNUMREC OF PDRECEPTION &
         REQUEST RCTNUMREC OF PDRECEPTION &
         ORDERBY CIECLE,                  &
                 RCTNUMREC

  ACCESS VIA     CIECLE                  &
         USING   T_CIECLEMNU             &
         ORDERBY CIECLE,                 &
                 RCTNUMREC


  ITEM RCTDATREC      OF PDRECEPTION    INITIAL SYSDATE
  ITEM RCTDATCRE      OF PDRECEPTION    INITIAL SYSDATE
  ITEM RCTHRECRE      OF PDRECEPTION    INITIAL SYSTIME / 10000
  ITEM RCTUSRCRE      OF PDRECEPTION    INITIAL LOGONID
  ITEM CIECLE         OF PDRECEPTION    INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; séquence pour les réceptions.
;
FILE PDSEQ_RECEPTION DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Bon de commande
;
FILE PDBON_COMMANDE SECONDARY &
                    NOITEM    &
                    NODELETE

  ACCESS VIA     CIECLE,   &
                 BCMNUMBCM &
         USING   CIECLE    OF PDRECEPTION, &
                 BCMNUMBCM OF PDRECEPTION

         ITEM CIECLE OF PDBON_COMMANDE INITIAL CIECLE OF PDRECEPTION

;-------------------------------------------------------------------------------
;
; Réception de bloc
;

FILE PDRECEPTION_BLOC DESIGNER


;-------------------------------------------------------------------------------
;
; Réception de tranche
;

FILE PDREC_TRANCHE DESIGNER


;-------------------------------------------------------------------------------
;
; Réception de diagramme
;

FILE PDREC_CAISSON DESIGNER


;-------------------------------------------------------------------------------
;
; Réception de produit de dimension
;

FILE PDREC_PROD_DIMEN DESIGNER


;-------------------------------------------------------------------------------
;
; Code bilingue (Type bon commande)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_BCMCODTYPBCM

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_BCMCODTYPBCM,                       &
               BCMCODTYPBCM OF PDBON_COMMANDE


;-------------------------------------------------------------------------------
;
; Code bilingue (Type bon commande E/F)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_BCMTYPBCM

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_BCMTYPBCM,                          &
               BCMTYPBCM OF PDBON_COMMANDE


;-------------------------------------------------------------------------------
;
; Fournisseurs bon commande
;

FILE VFOC_FOU REFERENCE ALIAS VFOC_FOU_BON_CMD

  ACCESS VIA     FOUCLE,                      &
                 FOCCIECLE                    &
         USING   FOUCLE    OF PDBON_COMMANDE, &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Fournisseurs transport
;

FILE VFOC_FOU REFERENCE ALIAS VFOC_FOU_TRP

  ACCESS VIA     FOUCLE,                     &
                 FOCCIECLE                   &
         USING   RCTNUMTRP OF PDRECEPTION,   &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Réception de produits " &
@ELSE
      " %%%Réception de produits " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF



DRAW FROM 16,1 TO 16,80


SKIP TO LINE 5


ALIGN (,3,19) (,37,53)


FIELD RCTNUMREC OF PDRECEPTION                &
label "No. réception.:"&
        DISPLAY                               &
        NUMERIC                               &
        REFRESH


FIELD RCTDATREC OF PDRECEPTION                 FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date réception:"&
        HIDDEN ID 10                          &
        DEFAULT SYSDATE


ALIGN (,3,19) (,37,53) (,,55)


FIELD BCMNUMBCM OF PDRECEPTION                &
label "No de P/O.....:"&
        HIDDEN ID 15                          &
        LOOKUP ON PDBON_COMMANDE              &
          MESSAGE  3032 ; Ce numéro n'existe pas


FIELD BCMCODTYPBCM OF PDBON_COMMANDE          &
label "Type de P/O...:"&
        HIDDEN ID 20                          &
        DISPLAY


FIELD CBIDSC           OF A_CBI_BCMCODTYPBCM &
        DISPLAY                              &
        SIZE 20


ALIGN (,3,19) (,37,54)


FIELD BCMPROPOT    OF PDBON_COMMANDE          &
label "Projet........:"&
        HIDDEN ID 25                          &
        DISPLAY


ALIGN (2,3,19) (,,26)


FIELD FOUCLE           OF PDBON_COMMANDE   &
label "Fournisseur...:"&
        HIDDEN ID 30                       &
        DISPLAY


FIELD FOUNOM           OF VFOC_FOU_BON_CMD &
        DISPLAY


ALIGN (2,3,19) (,,21)


FIELD BCMTYPBCM        OF PDBON_COMMANDE   &
label "Forfait/estimé:"&
        HIDDEN ID 40                       &
        DISPLAY


FIELD CBIDSC           OF A_CBI_BCMTYPBCM  &
        DISPLAY


ALIGN (2,3,19) (,,26)


FIELD RCTNUMTRP        OF PDRECEPTION      &
label "No transport..:"&
        HIDDEN ID 45                       &
        LOOKUP ON VFOC_FOU_TRP             &
          MESSAGE 2917 ; Ce fournisseur n'existe pas


FIELD FOUNOM           OF VFOC_FOU_TRP     &
        DISPLAY


ALIGN (2,3,19)


FIELD RCTCNS       OF PDRECEPTION             &
label "Connaissement.:"&
        HIDDEN ID 50


FIELD RCTDSC001    OF PDRECEPTION             &
label "Description...:"&
        HIDDEN ID 55


FIELD RCTDSC002    OF PDRECEPTION             &
        ID SAME                               &
        NOLABEL


ALIGN (2,3,19) (36,37,53)


FIELD BCMPRITOT    OF PDBON_COMMANDE          &
label "Prix total....:"&
        HIDDEN ID 70                          &
        DISPLAY


FIELD RCTPRITOT    OF PDRECEPTION             &
label "Prix total....:"&
        HIDDEN ID 80                          &
        DISPLAY


SKIP TO LINE 18


ALIGN (,3,19) (,43,59)


FIELD RCTDATCRE    OF PDRECEPTION              FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date création.:"&
        DISPLAY


FIELD RCTDATDERMAJ OF PDRECEPTION              FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date MAJ......:"&
        DISPLAY



ALIGN (,3,19) (,,27) (,43,59) (,,67)


FIELD RCTHRECRE    OF PDRECEPTION             &
label "Heure création:"&
        DISPLAY


FIELD RCTUSRCRE    OF PDRECEPTION             &
        DISPLAY


FIELD RCTHREDERMAJ OF PDRECEPTION             &
label "Heure MAJ.....:"&
        DISPLAY


FIELD RCTUSRDERMAJ OF PDRECEPTION             &
        DISPLAY


ALIGN (,3,19) (,43,59)


FIELD T_BLOC                               &
        DISPLAY                            &
        LABEL "Bloc..........:"


FIELD T_DIAGRAMME                          &
        DISPLAY                            &
        LABEL "Caisson.......:"


FIELD T_TRANCHE                            &
        DISPLAY                            &
        LABEL "Tranche.......:"


FIELD T_PROD_DIMEN                         &
        DISPLAY                            &
        LABEL "Prod. dimen...:"


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

PROCEDURE ENTRY
BEGIN
  DISPLAY RCTNUMREC OF PDRECEPTION
  ACCEPT RCTDATREC OF PDRECEPTION
  ACCEPT BCMNUMBCM OF PDRECEPTION
  GET PDBON_COMMANDE &
    VIA     CIECLE,   &
            BCMNUMBCM &
    USING   CIECLE    OF PDRECEPTION, &
            BCMNUMBCM OF PDRECEPTION
  DISPLAY BCMCODTYPBCM OF PDBON_COMMANDE
  DISPLAY CBIDSC OF A_CBI_BCMCODTYPBCM
  DISPLAY BCMPROPOT OF PDBON_COMMANDE
  DISPLAY FOUCLE OF PDBON_COMMANDE
  DISPLAY FOUNOM OF VFOC_FOU_BON_CMD
  DISPLAY BCMTYPBCM OF PDBON_COMMANDE
  DISPLAY CBIDSC OF A_CBI_BCMTYPBCM
  ACCEPT RCTNUMTRP OF PDRECEPTION
  DISPLAY FOUNOM OF VFOC_FOU_TRP
  ACCEPT RCTCNS OF PDRECEPTION
  ACCEPT RCTDSC001 OF PDRECEPTION
  ACCEPT RCTDSC002 OF PDRECEPTION
  DISPLAY BCMPRITOT OF PDBON_COMMANDE
  DISPLAY RCTPRITOT OF PDRECEPTION
  DISPLAY RCTDATCRE OF PDRECEPTION
  DISPLAY RCTDATDERMAJ OF PDRECEPTION
  DISPLAY RCTHRECRE OF PDRECEPTION
  DISPLAY RCTUSRCRE OF PDRECEPTION
  DISPLAY RCTHREDERMAJ OF PDRECEPTION
  DISPLAY RCTUSRDERMAJ OF PDRECEPTION
  DISPLAY T_BLOC
  DISPLAY T_DIAGRAMME
  DISPLAY T_TRANCHE
  DISPLAY T_PROD_DIMEN
END

;-------------------------------------------------------------------------------
;
; Appel dépisteur pour le champ
;

PROCEDURE INPUT RCTNUMTRP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_USINE_SEC = "0"
    LET T_FOUCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd117 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_FOUCLE, &
                             T_USINE_SEC
    LET FIELDTEXT = TRUNCATE( T_FOUCLE )
  END
END


;-------------------------------------------------------------------------------
;
; Cette procédure est identique à la procedure postfind pour permettre
; d'afficher les "*" pour identifier les type de produit reçu.
;

PROCEDURE INTERNAL AFFICHE_DETAIL
BEGIN
  LET T_BLOC = " "
  LET T_TRANCHE = " "
  LET T_DIAGRAMME = " "
  LET T_PROD_DIMEN = " "

  ;
  ; Recherche l'information pour les blocs
  ;
  GET PDRECEPTION_BLOC OPTIONAL &
        VIA     CIECLE,         &
                RCTNUMREC       &
        USING   T_CIECLEMNU,    &
                RCTNUMREC OF PDRECEPTION
  IF ACCESSOK
  THEN BEGIN
    LET T_BLOC = "*"
  END

  ;
  ; Recherche l'information pour les tranches
  ;
  GET PDREC_TRANCHE OPTIONAL &
        VIA     CIECLE,      &
                RCTNUMREC    &
        USING   T_CIECLEMNU, &
                RCTNUMREC OF PDRECEPTION
  IF ACCESSOK
  THEN BEGIN
    LET T_TRANCHE = "*"
  END

  ;
  ; Recherche l'information pour les diagrammes
  ;
  GET PDREC_CAISSON OPTIONAL &
        VIA     CIECLE,        &
                RCTNUMREC      &
        USING   T_CIECLEMNU,   &
                RCTNUMREC OF PDRECEPTION
  IF ACCESSOK
  THEN BEGIN
    LET T_DIAGRAMME = "*"
  END

  ;
  ; Recherche l'information pour les produit de dimension
  ;
  GET PDREC_PROD_DIMEN OPTIONAL &
        VIA     CIECLE,         &
                RCTNUMREC       &
        USING   T_CIECLEMNU,    &
                RCTNUMREC OF PDRECEPTION
  IF ACCESSOK
  THEN BEGIN
    LET T_PROD_DIMEN = "*"
  END

END


;-------------------------------------------------------------------------------
;
; Réception de bloc
;

PROCEDURE DESIGNER D001
BEGIN
  IF ALTEREDRECORD OF PDRECEPTION
  THEN BEGIN
    ERROR 2991 ; Faire une mise à jour avant d'accèder au sous-écran
  END
  LET T_FOUNOM = FOUNOM  OF VFOC_FOU_BON_CMD
  RUN SCREEN  pd176a &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        T_FOUNOM,       &
                        PDRECEPTION,    &
                        PDBON_COMMANDE  &
              MODE SAME

  DO INTERNAL AFFICHE_DETAIL

END


;-------------------------------------------------------------------------------
;
; Réception de tranche
;

PROCEDURE DESIGNER D002
BEGIN
  IF ALTEREDRECORD OF PDRECEPTION
  THEN BEGIN
    ERROR 2991 ; Faire une mise à jour avant d'accèder au sous-écran
  END
  LET T_FOUNOM = FOUNOM  OF VFOC_FOU_BON_CMD
  RUN SCREEN  pd176b &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        T_FOUNOM,       &
                        PDRECEPTION,    &
                        PDBON_COMMANDE  &
              MODE SAME

  DO INTERNAL AFFICHE_DETAIL

END


;-------------------------------------------------------------------------------
;
; Réception de diagramme
;

PROCEDURE DESIGNER D003
BEGIN
  IF ALTEREDRECORD OF PDRECEPTION
  THEN BEGIN
    ERROR 2991 ; Faire une mise à jour avant d'accèder au sous-écran
  END
  LET T_FOUNOM = FOUNOM  OF VFOC_FOU_BON_CMD
  RUN SCREEN  pd176c &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        T_FOUNOM,       &
                        PDRECEPTION,    &
                        PDBON_COMMANDE  &
              MODE SAME

  DO INTERNAL AFFICHE_DETAIL

END


;-------------------------------------------------------------------------------
;
; Réception de produit de dimension
;

PROCEDURE DESIGNER D004
BEGIN
  IF ALTEREDRECORD OF PDRECEPTION
  THEN BEGIN
    ERROR 2991 ; Faire une mise à jour avant d'accèder au sous-écran
  END
  LET T_FOUNOM = FOUNOM  OF VFOC_FOU_BON_CMD
  RUN SCREEN  pd176d &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        T_FOUNOM,       &
                        PDRECEPTION,    &
                        PDBON_COMMANDE  &
              MODE SAME

  DO INTERNAL AFFICHE_DETAIL

END


;-------------------------------------------------------------------------------
;
; Permet de faire afficher les "*" pour indiquer quel type de produit a été
; reçu.
;

PROCEDURE POSTFIND
BEGIN

  DO INTERNAL AFFICHE_DETAIL

END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRECEPTION   &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRECEPTION  &
     AND NOT NEWRECORD     OF PDRECEPTION  &
     AND NOT DELETEDRECORD OF PDRECEPTION  &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Évaluation des champs indiquant une mise à jour de la réception
  ;
  IF NOT NEWRECORD OF PDRECEPTION AND &
     ALTEREDRECORD OF PDRECEPTION
  THEN BEGIN
    LET RCTDATDERMAJ OF PDRECEPTION = SYSDATE
    LET RCTHREDERMAJ OF PDRECEPTION = SYSTIME / 10000
    LET RCTUSRDERMAJ OF PDRECEPTION = LOGONID
  END

  ;
  ; Incrémentation du numéro de séquence
  ;

  IF 0 = RCTNUMREC OF PDRECEPTION
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_RECEPTION OPTIONAL        &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_RECEPTION = T_CIECLEMNU
    END
    LET SERNUMREC OF PDSEQ_RECEPTION = SERNUMREC OF PDSEQ_RECEPTION + 1
    LET RCTNUMREC OF PDRECEPTION     = SERNUMREC OF PDSEQ_RECEPTION
    PUT PDSEQ_RECEPTION
  END

END


;-------------------------------------------------------------------------------
;
; Cette procedure permet de ne pas effacer l'ecran après l'UPDATE
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY RCTNUMREC OF PDRECEPTION
    WARNING = " "
  END
END


BUILD DETAIL LIST
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Réception de produits *****************************
*                                                                              *
* No. réception.: xxxxxxxxx         Date réception:  xxxxxxxx                  *
* No de P/O.....: xxxxxxxxxxx       Type de P/O...:  x xxxxxxxxxxxxxxxxxxxxxxx *
* Projet........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Fournisseur...: xxxxxx            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
* Forfait/estimé: x                                                            *
* No transport..: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
* Connaissement.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Prix total....: xxxxxxxxxxxxxxx   Prix total....: xxxxxxxxxxxxxxx            *
*                                                                              *
********************************************************************************
*                                                                              *
* Date création.: xxxxxxxx                Date MAJ......: xxxxxxxx             *
* Heure création: xxxxx   xxxxxxxxxxxx    Heure MAJ.....: xxxxx   xxxxxxxxxxxx *
*                                                                              *
* Bloc..........: *                       Caisson.......: *                    *
* Tranche.......: *                       Prod. dimen...: *                    *
********************************************************************************
@ENDIF
