;/T/ Fournisseur T_CIECLEMNU T_FOUCLE
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-27
;
;    Description..: Permet de visionner et de choisir un fournisseur
;
;    Paramètres...: T_CIECLEMNU ==> - Numéro de compagnie du menu
;
;                   T_FOUCLE    ==> - code
;                                   - segment de la clé de recherche
;                                   - peut contenir la valeur @
;                                   - contient la valeur de retour.
;
;                   T_USINE_SEC ==> - O usine secondaire
;                                   - N autre
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd117 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU ,      &
                       T_FOUCLE,          &
                       T_USINE_SEC


;-------------------------------------------------------------------------------
;
; Variable passer en parametre
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_FOUCLE    CHARACTER SIZE 06 RESET AT STARTUP ; Chaine de recherche
TEMPORARY T_USINE_SEC CHARACTER SIZE 01 RESET AT STARTUP ; Usine sec O/N


USE pd117.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Fournisseurs
;

FILE VFOC_FOU PRIMARY &
                        OCCURS 11

  ACCESS VIA     FOCCIECLE,                   &
                 FOUCLE                       &
         USING   T_CIECLEMNU,                 &
                 T_FOUCLE                     &
         ORDERBY FOCCIECLE,                   &
                 FOUCLE


;  SELECT IF T_USINE_SEC = FOCUSISEC OR T_USINE_SEC


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
USE usdrwfnd NOLIST     ; Draw pour les écrans de choix et consultations

USE ustitoff NOLIST     ; Pour placer le TITLE OFF

SKIP TO 2

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH VFOC_FOU

FIELD FOUCLE    OF VFOC_FOU          HIDDEN LABEL ">>" DISPLAY
FIELD FOUNOM    OF VFOC_FOU          DISPLAY SIZE 27

CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_FOUCLE , "@")
  THEN LET T_FOUCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_FOUCLE = FOUCLE    OF VFOC_FOU
  RETURN
END

BUILD

@IF FORMAT

                                       *****************************************
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                       *****************************************
@ENDIF
