;/T/ Fournisseurs/compagnie - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 3 septembre 1993
;
;    Description..: Cet écran vérifie si le fournisseur-compagnie reçu en
;                   paramètre est référé dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp001ax NOMODE &
               ACTION LABEL " " &
               FROM 23,70 TO 23,80 &
               RECEIVING CPFOU_CIE , &
                         T_FLGDEL


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Fournisseurs - compagnie
;
FILE CPFOU_CIE        MASTER

;
; Comptes à payer.
;
FILE CPCPT_A_PAYER    REFERENCE

;
; Paiements.
;
FILE CPPAIEMENT       REFERENCE

;
; Mémo sur le fournisseur.
;
FILE CPMEMO           REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans les comptes à payer.
  ;
  GET CPCPT_A_PAYER VIA FOUCIECLE    , &
                        FOUCLE       , &
                        CAPCIECLE      &
                USING FOUCIECLE OF CPFOU_CIE , &
                      FOUCLE    OF CPFOU_CIE , &
                      FOCCIECLE OF CPFOU_CIE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 477 ; Destruction annulée, cette information est référée par un
                 ; compte à payer.

  ;
  ; On vérifie dans les paiements.
  ;
  GET CPPAIEMENT VIA CIECLE    , &
                     REFCIECLE , &
                     REFCLENUM , &
                     REFCLETYP   &
              USING FOCCIECLE OF CPFOU_CIE , &
                    FOUCIECLE OF CPFOU_CIE , &
                    FOUCLE    OF CPFOU_CIE , &
                    "F"                      &
              OPTIONAL
  IF ACCESSOK
  THEN ERROR 575 ; Destruction annulée, cette information est référée par un
                 ; paiement.

  ;
  ; On vérifie dans les mémos.
  ;
  GET CPMEMO VIA CIECLE    , &
                 FOUCIECLE , &
                 FOUCLE      &
             USING FOCCIECLE OF CPFOU_CIE , &
                   FOUCIECLE OF CPFOU_CIE , &
                   FOUCLE    OF CPFOU_CIE   &
             OPTIONAL
  IF ACCESSOK
  THEN ERROR 576 ; Destruction annulée, cette information est référée par un
                 ; mémo.
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
