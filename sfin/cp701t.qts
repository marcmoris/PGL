;/T/ Journalisation des paiements et paiements annul s
;
;/M/ Martin Perron / Modifier la MAJ de T_HISCLE_ANN avec T_HISCLE_TMP
;
;//M/GRA01/Francois Richard/1999-06-22
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Thomas BRENNEUR
;    Date Cr ation.: 19 Juillet 1993
;
;    Description...: Programme de journalisation des lots de paiements
;                    et de paiements annul s.
;
;    Param tres....: Compagnie
;                    Periode CTB
;                    No Lot (Un ou plusieurs)
;                    Type de lots (un seul) :
;                       "CH" --> Cheques
;                       "CA" --> cheques annul s
;
;
;    Note..........: Le type de lot sera pass  en parametre
;                    (non saisie pas l'usager).
;                    Donc on est certain tout au long du programme de ne
;                    traiter qu'un seul type de lot a la fois.
;
;    Particularit .: Une bonne partie du programme se retrouve dans un
;                    use g r ral a toutes les journalisations soit :
;                      US900T.QTS.
;
;
;   Ecritures comptables :
;   ---------------------
;
;     Dans la fonctionalit  des paiements, il y a principalement deux types
;   d' critures comptables :
;
;       1 - Pour un paiement qui ne contient pas de paiement automatique
;           et qui n'est pas un paiement direct ( peu importe qu'il soit
;           r gulier, manuel ou externe) on a l' criture suivante :
;
;             Compte        unit        D bit       Cr dit
;             ----------    ----------  ----------  ----------
;             CPT_CAP       UNA_BILAN   MNT_PAY
;             CPT_ESC       UNA_BILAN               MNT_ESCOMPTE
;             CPT_BQ        UNA_BILAN               MNT_CHEQUE
;
;           Note :  l'escompte est optionel. S'il n'y a pas d'escompte, alors
;                   le MNT_PAY  est  gal au MNT_CHEQUE.
;
;       2 - Pour un paiement direct ou un paiement qui contient un
;           paiement automatique ( peu importe qu'il soit r gulier, manuel
;           ou externe) on a l' criture suivante :
;
;             Compte        unit        D bit       Cr dit
;             ----------    ----------  ----------  ----------
;             CPT_IMP_01    UNA_IMP_01  MNT_IMP_01
;             CPT_IMP_02    UNA_IMP_02  MNT_IMP_02
;                 :            :          :
;             CPT_IMP_n     UNA_IMP_n   MNT_IMP_n
;             CPT_TPS       UNA_BILAN   MNT_CTI
;             CPT_TVQ       UNA_BILAN   MNT_RTI
;             CPT_BQ        UNA_BILAN               MNT_CHEQUE
;
;
;
;     La principale diff rence entre ces deux  criture vient du type de
;   paiement. Si ce n'est ni un paiement direct ni un paiement de paiement
;   automatique, alors il y a une ventilation par facture et on va diminuer
;   le compte   payer de ces factures en fonction des montants pay s. Mais
;   si c'est un paiement direct ou un paiement de paiement automatique alors
;   on utilise l'imputation comptable attach e au paiement.
;
;   NOTE : Dans le cas du paiement automatique, il y a une ventilation par
;   facture ET une imputation comptable. Mais la ventilation ne sert qu'a
;   indiquer le montant pay  du paiement automatique. Elle n'a pas d'incidence
;   sur l' criture comptable.
;
;   NOTE : Les  critures des annulations de paiements sont presques identique.
;   La seule difference sont les montants qui sont invers s ( de +   - ).
;
;
;   Signification des codes utilis s :
;   --------------------------------
;
;   CPT_CAP         = Compte   payer du fournisseur.
;   CPT_ESC         = Compte d'escompte du fournisseur.
;   CPT_BQ          = Compte de banque sur lequel est tir  le paiement.
;   CPT_IMP_01   _n = Comptes contenus dans l'imputation.
;   CPT_TPS         = Compte de taxe f d rale
;   CPT_TVQ         = Compte de taxe provinciale
;   UNA_BILAN       = Unit  de bilan.
;   UNA_IMP_01   _n = Unit s contenus dans l'imputation.
;   MNT_PAY         = Montant que paye le ch que sur les comptes   payer.
;   MNT_ESCOMPTE    = Escompte pris sur le montant   payer.
;   MNT_CHEQUE      = Montant du ch que.
;   MNT_IMP_01   _n = Montants contenus dans l'imputation.
;   MNT_CTI         = Montant remboursable de la taxe f d rale
;   MNT_RTI         = Montant remboursable de la taxe provinciale
;
;
;/M/ Modifié par..: Guy Chabot le 12 octobre 1994
;
;    Description..: Introduction du P.N.A. aux payables sur le même principe
;                   ou presque des recevables.
;
;-------------------------------------------------------------------------------

USE ussetqts  NOLIST ; permet de faire les SET PROCESS NOLIMIT....

;===============================================================================

RUN cp701t

GLOBAL TEMPORARY G_ERREUR       CHARACTER SIZE 5   ; Identificateur d'arret
GLOBAL TEMPORARY G_PECCLE       CHARACTER SIZE 4   ; periode
GLOBAL TEMPORARY G_DATJOU       DATE         & ; date de journalisation
                                INITIAL SYSDATE
GLOBAL TEMPORARY G_HREJOU       INTEGER SIZE 4   & ; heure de journalisation
                                INITIAL SYSTIME / 10000
GLOBAL TEMPORARY G_ANN          CHARACTER SIZE 2 & ; ann e de journalisation
                                INITIAL ASCII(MOD(FLOOR(G_DATJOU / 10000), 100),2)
GLOBAL TEMPORARY G_CODMOD       CHARACTER SIZE 2 & ; Code de module
                                INITIAL "CP"

GLOBAL TEMPORARY G_HLDCHTCPTRLT CHARACTER SIZE 1 ; Parametres de holding pour relation de compte.
;
; Param tres du holding, si notion de projet Oui/Non.
;
GLOBAL TEMPORARY G_HLDNTNPRO    CHARACTER SIZE 1
GLOBAL TEMPORARY G_FLGDEV       CHARACTER SIZE 1 ; Pour savoir s'il y a eu une convertion de montant.
;
; On conserve la devise du Holding
;
GLOBAL TEMPORARY G_DEVCLE       CHARACTER SIZE 3



;-------------------------------------------------------------------------------
;
;
; Affectation des variables globales selon les param tres du holding.
;
;
REQUEST TRAITE_GENERAL

ACCESS MCHOLDING

SELECT IF CIENMC OF MCHOLDING = "1"

ITEM G_HLDCHTCPTRLT = HLDCHTCPTRLT OF MCHOLDING
ITEM G_HLDNTNPRO    = HLDNTNPRO    OF MCHOLDING
ITEM G_FLGDEV       = "0"
ITEM G_DEVCLE       = DEVCLE       OF MCHOLDING


;-------------------------------------------------------------------------------
;
; 1ERE PARTIE DE LA JOURNALISATION
;
; . On recherche toutes les transactions   journaliser et on les contr les
;   pour ne pas g n rer de transactions hors-balance, et pour v rifier que
;   le ch que ait  t  imprim .
;
; . Pour chaque pi ce trouv e :
;
;       - On cr e une ligne d' criture par document de la ventilation
;         (  partir de CPCAP_HISTO) si le paiement poss de une ventilation
;         par facture.
;       - On ajoute la ligne d' criture du compte de banque.
;       - Et la ligne d' criture du compte d'escompte.
;
; . On met   jour le lot, le paiement, les historiques pour les
;   marquer comme  tants journalis s.
;
; . On cumule l'escompte, les jours de delai de paiements et le nombre de
;   paiements dans les statistiques du fournisseur.
;
; Note :   ce stade, si le paiement est un paiement direct ou un paiement de
; paiement automatique, seule la premi re partie de l' criture est cr  .
; Le reste de l' criture (provenant de l'imputation) est rajout  dans le
; request suivant.
;
REQUEST JOURNALISATION_1ERE_PARTIE

;
; Recherche des lots   journaliser
;

ACCESS  CPLOT &

  ;
  ; Lecture du prochain num ro d'historique   utiliser
  ;

  LINK  "1", &
        G_ANN &
    TO  CIENMC, &
        HSSCLEANN         OF MCHIS_SEQ &
  OPTIONAL &

  ;
  ; Lecture de la compagnie du lot pour avoir l'unit  de bilan
  ;

  LINK  CIECLE OF CPLOT &
    TO  CIECLE            OF MCCOMPAGNIE &

  ;
  ; Lecture de tous les paiements li s   ce lot
  ;

  LINK  CIECLE OF CPLOT, &
        PECCLE OF CPLOT, &
        LCPCLE OF CPLOT  &
    TO  CIECLE, &
        PECCLE, &
        LCPCLE            OF CPPAIEMENT &

  ;
  ; Lecture de la r f rence du paiement
  ;

  LINK  REFCIECLE OF CPPAIEMENT, &
        REFCLETYP OF CPPAIEMENT, &
        REFCLENUM OF CPPAIEMENT, &
        RADCLE    OF CPPAIEMENT  &
    TO  REFCIECLE, &
        REFCLETYP, &
        REFCLENUM, &
        RADCLE            OF VRAD_01 &

  ;
  ; Lecture du fournisseur si la r f rence ne pointe pas sur un employ
  ;

  LINK  REFCIECLE OF VRAD_01    , &
        REFCLENUM OF VRAD_01    , &
        CIECLE    OF CPPAIEMENT   &
    TO  FOUCIECLE , &
        FOUCLE    , &
        FOCCIECLE         OF VFOC_FOU &
  OPTIONAL &

  ;
  ; Si le fournisseur est un fournisseur interne, alors on va lire
  ; la compagnie r f r e por obtenir son unit  de bilan.
  ;

  LINK  FOUCIEINT OF VFOC_FOU &
    TO  CIECLE            OF MCCOMPAGNIE ALIAS A_INT_CIE &
  OPTIONAL &

  ;
  ; Lecture de la ventilation du paiement
  ;

  LINK  CIECLE    OF CPPAIEMENT, &
        LBQCPTENC OF CPPAIEMENT, &
        PAMCLE    OF CPPAIEMENT  &
    TO  CPHCLE1, &
        CPHCLE2, &
        CPHCLE3           OF CPCAP_HISTO &
  OPTIONAL &

  ;
  ; Lecture de la pi ce r f r e par cette ventilation
  ;

  LINK  FOUCIECLE OF CPCAP_HISTO, &
        FOUCLE    OF CPCAP_HISTO, &
        CAPCLE    OF CPCAP_HISTO  &
    TO  FOUCIECLE, &
        FOUCLE   , &
        CAPCLE            OF CPCPT_A_PAYER &
  OPTIONAL



CHOOSE  CIECLE    PARM, & ; Compagnie   traiter
        PECCLE    PARM ,& ; P riode comptable
        LCPCLE    PARM, & ; Num ro de lot
        LCPTYPECR PARM, & ; Type de lot (CH = ch ques ou CA = Ch ques annul s)
        LCPATRJOU "1" , & ; Le lot doit  tre autoris
        LCPDATJOU 0       ; Mais il ne doit pas  tre d ja journalis


;
; Le s lect suivant nous sert lorsqu'on veut journaliser certains paiements
; dans un lot, cela facilite la reprise du traitement.
;
SELECT IF PAMDATJOU OF CPPAIEMENT  = 0

TEMPORARY T_PRJCLE    CHARACTER SIZE 8  ; Projet de l'imputation.
TEMPORARY T_PRACLE    CHARACTER SIZE 3  ; Activit  de l'imputation.
TEMPORARY T_IMMCLE    CHARACTER SIZE 12 ; catégorie de l'équipement


; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF CPLOT &
         IF PECCLE OF CPLOT[1:1] < "8" &
         ELSE "0" + PECCLE OF CPLOT

SORT   ON CIECLE    OF CPLOT     , &
          D_PECCLE_SIE     , &
          LCPCLE    OF CPLOT     , &
          LBQCPTENC OF CPPAIEMENT, &
          PAMCLE    OF CPPAIEMENT

;
; On conserve la p riode de journalisation. Sert lors de la validation des comptes...
;
ITEM G_PECCLE RESET AT INITIAL TO PECCLE OF CPLOT

;
; V rification des erreurs :
;
;   Le montant du paiement, la ventilation et l'imputation doivent
;   balancer.
;
;   Et on ne doit pas journaliser un paiement non imprim . A moins que ce ne
;   soit un paiement annul  ou un paiement d'annulation.
;
TEMPORARY T_MESERR CHARACTER SIZE 80

  ITEM T_MESERR = &
@IF FRANCAIS
              "Ventilation hors balance; lot: " &
@ELSE
              "Breakdown out of balance; Batch: " &
@ENDIF
              + LCPCLE OF CPPAIEMENT &
              + "(" + LCPTYPECR OF CPLOT + ")" &
@IF FRANCAIS
              + " ,document: " &
@ELSE
              + " ,document: " &
@ENDIF
              + LBQCPTENC OF CPPAIEMENT + " " &
              + PAMCLE OF CPPAIEMENT &
@IF FRANCAIS
              + " ,r f rence: " &
@ELSE
              + " ,reference: " &
@ENDIF
              + REFCLENUM OF CPPAIEMENT &
  IF     PAMMNT    OF CPPAIEMENT <> PAMCUMMNT OF CPPAIEMENT &
     AND PAMCODANN OF CPPAIEMENT = "0" &
  AT PAMCLE OF CPPAIEMENT &
  RESET AT PAMCLE OF CPPAIEMENT

  ITEM G_ERREUR &
        = "ARRET" IF T_MESERR <> "" AT PAMCLE

;
; Affectation du num ro d'historique
;
TEMPORARY T_NUMSEQ  INTEGER UNSIGNED SIZE 4
TEMPORARY T_HISCLE  INTEGER UNSIGNED SIZE 4
TEMPORARY T_HISCLE_ANN INTEGER UNSIGNED SIZE 4
TEMPORARY T_HISCLE_TMP CHARACTER SIZE 09 

  ITEM  T_NUMSEQ &
          INITIAL 0 & ;NCONVERT( G_ANN ) * 10000000 &
            IF NOT RECORD MCHIS_SEQ EXIST ELSE &
          HSSNUMSEQ OF MCHIS_SEQ

  ITEM  T_HISCLE RESET AT INITIAL TO (T_NUMSEQ + 1)

;
; On incr mente le num ro d'historique   chaque transaction (paiement).
;
ITEM T_HISCLE COUNT AT PAMCLE

ITEM T_HISCLE_TMP = ASCII(T_HISCLE,9)
ITEM T_HISCLE_ANN = NCONVERT((G_ANN + T_HISCLE_TMP[3:7]))

  ;ITEM  T_HISCLE_SIE = NCONVERT(G_ANN + ASCII(T_HISCLE,7))
;
; Inter-Compagnies
;
;   Si le fournisseur est un fournisseur interne, alors on donne le num ro
;   de la compagnie interne ainsi que son unit  de bilan dans le subfile
;   de pr paration de l' criture comptable.
;
TEMPORARY T_HECCIEINT  CHARACTER SIZE 4  ; Pour compagnie d'inter
TEMPORARY T_HECUNAINT  CHARACTER SIZE 8  ; Unit  adm. de bilan de la cie d'inter
TEMPORARY T_HECFLGINT  CHARACTER SIZE 1  ; Pour flag d'inter compagnie

  ITEM  T_HECCIEINT &
          = CIECLE    OF CPPAIEMENT IF FOUTYP OF VFOC_FOU <> "I" ELSE &
            FOUCIEINT OF VFOC_FOU

  ITEM  T_HECUNAINT &
          = CIEUNABIL OF MCCOMPAGNIE IF FOUTYP OF VFOC_FOU <> "I" ELSE &
            CIEUNABIL OF A_INT_CIE

  ITEM  T_HECFLGINT &
          = "0" IF FOUTYP OF VFOC_FOU <> "I" ELSE &
            "1"

;
; Description g n rale, on prend le nom dans l'adresse si c'est
; un fournisseur divers, sinon on la laisse   blanc.
;
TEMPORARY T_HISDSCGEN  CHARACTER SIZE 40 ; Description g n rale

  ITEM  T_HISDSCGEN &
          = RADNOM OF VRAD_01 &
              IF    REFCLETYP OF CPPAIEMENT = "F" &
                AND FOUTYP    OF VFOC_FOU   = "D" ELSE &
            " "

;
; On assigne la cl  du paiement   l'historique.
;
TEMPORARY T_HISCLE1    CHARACTER SIZE 4  ; Cl  d'acc s 1, de l'histo.
TEMPORARY T_HISCLE2    CHARACTER SIZE 15 ; Cl  d'acc s 2, de l'histo.
TEMPORARY T_HISCLE3    CHARACTER SIZE 15 ; Cl  d'acc s 3, de l'histo.

  ITEM T_HISCLE1 = CIECLE     OF CPPAIEMENT
  ITEM T_HISCLE2 = LBQCPTENC  OF CPPAIEMENT
  ITEM T_HISCLE3 = PAMCLE     OF CPPAIEMENT

;
; Montant du paiement, on utilise une variable temporaire pour avoir
; une variable de type FLOAT dans la d finition du sous-fichier, si on
; met directement la variable PAMMNT dans le sous-fichier, alors la
; d finition sera G_FLOAT et cela cause un BUG entre les programmes de
; journalisation.
;
TEMPORARY T_PAMMNT    FLOAT     SIZE  8 ; Montant du paiement
TEMPORARY T_PAMMNTESC FLOAT     SIZE  8 ; Montant d'escompte du paiement
TEMPORARY T_MNTGL     FLOAT     SIZE  8
TEMPORARY T_MNTORI    FLOAT     SIZE  8

  ITEM T_PAMMNT     = PAMMNT    OF CPPAIEMENT
  ITEM T_PAMMNTESC  = PAMMNTESC OF CPPAIEMENT
  ITEM T_MNTGL      = CPHMNTCAP OF CPCAP_HISTO
  ITEM T_MNTORI     = CPHMNTCAP OF CPCAP_HISTO
;
; Sous-fichier des erreurs
;
SUBFILE WUS900ER KEEP &
                 AT PAMCLE &
                 IF T_MESERR <> "" &
  INCLUDE CIECLE    OF CPLOT, &
          PECCLE    OF CPLOT, &
          T_MESERR

;
; Cr ation de l' criture
;   Sous fichier commun   toutes les journalisations, sert   mettre   jour la
;   table MCHISTO.
;
;
SUBFILE WUS900H AT PAMCLE &
  INCLUDE T_HISCLE_ANN                ALIAS HISCLE    , &
          CIECLE    OF CPPAIEMENT     ALIAS HISCIECLE , &
          REFCIECLE OF CPPAIEMENT                     , &
          REFCLETYP OF CPPAIEMENT                     , &
          REFCLENUM OF CPPAIEMENT                     , &
          PAMNUMPIM OF CPPAIEMENT     ALIAS HISNUMDOC , &
          PECCLE    OF CPPAIEMENT                     , &
          LCPCLE    OF CPPAIEMENT     ALIAS HISNUMLOT , &
          PAMDAT    OF CPPAIEMENT     ALIAS HISDAT    , &
          G_DATJOU                    ALIAS HISDATJOU , &
          PAMTYPECR OF CPPAIEMENT     ALIAS HISTYPECR , &
          T_HISDSCGEN                 ALIAS HISDSCGEN , &
          PAMDSC1   OF CPPAIEMENT     ALIAS HISDSCSAI , &
          PAMDSC2   OF CPPAIEMENT     ALIAS HISDSCSAI2, &
          PAMUSRCRE OF CPPAIEMENT     ALIAS HISUSRCRE , &
          DEVCLE    OF VRAD_01                        , &
          T_HISCLE1                   ALIAS HISCLE1   , &
          T_HISCLE2                   ALIAS HISCLE2   , &
          T_HISCLE3                   ALIAS HISCLE3

;
; Sous fichier utilis  dans le traitement g n ral des journalisations
; et servant a mettre a jour GLHIS_ECR.
;
; Ajout du compte de compte   payer pour chaque pi ce de la ventilation
; (sauf si le paiement est un paiement de paiement automatique)
;
TEMPORARY T_HECNUMDOC2 CHARACTER SIZE 6  ; Pour num ro de document-2(= BLANC)
TEMPORARY T_CODDCD     CHARACTER SIZE  1 ; Code de d bit/cr dit

ITEM T_CODDCD RESET AT INITIAL TO "D"

SUBFILE WUS900HE &
        KEEP &
        IF    RECORD CPCAP_HISTO EXISTS &
          AND PAMFLGPA  OF CPPAIEMENT = "0" &
          AND PAMCODANN OF CPPAIEMENT = "0" &
  INCLUDE T_HISCLE_ANN                  ALIAS HISCLE    , &
          CIECLE       OF CPPAIEMENT    ALIAS HISCIECLE , &
          CAPCPTCAP    OF CPCPT_A_PAYER ALIAS CPTCLE    , &
          T_HECCIEINT                   ALIAS HECCIEINT , &
          T_HECUNAINT                   ALIAS HECUNAINT , &
          CIECLE       OF CPPAIEMENT                    , &
          CIEUNABIL    OF MCCOMPAGNIE   ALIAS UNACLE    , &
          T_PRJCLE                      ALIAS PRJCLE    , &
          T_PRACLE                      ALIAS PRACLE    , &
          T_IMMCLE                      ALIAS IMMCLE    , &
          PECCLE       OF CPLOT                         , &
          T_HECNUMDOC2                  ALIAS HECNUMDOC2, &
          T_CODDCD                      ALIAS T_CODDC   , &
          T_MNTGL                                       , &
          T_MNTORI                                      , &
          T_HECFLGINT                   ALIAS HECFLGINT , &
          DEVCLE       OF VRAD_01                       , &
          G_CODMOD                      ALIAS HISCODMOD , &
          PAMTYPECR    OF CPPAIEMENT    ALIAS HISTYPECR , &
          LCPCLE       OF CPLOT         ALIAS HISNUMLOT

;
; Ajout du compte d'encaisse du paiement (sauf si le paiement    t  annul
; avant impression)
;
OUTPUT *WUS900HE ALIAS A_WUS900HE_EN &
                 ADD AT PAMCLE &
                 IF PAMCODANN OF CPPAIEMENT = "0" AND PAMMNT OF CPPAIEMENT <> 0

  ITEM HISCLE     OF A_WUS900HE_EN FINAL T_HISCLE_ANN
  ITEM HISCIECLE  OF A_WUS900HE_EN FINAL CIECLE       OF CPPAIEMENT
  ITEM CPTCLE     OF A_WUS900HE_EN FINAL LBQCPTENC    OF CPPAIEMENT
  ITEM HECCIEINT  OF A_WUS900HE_EN FINAL T_HECCIEINT
  ITEM HECUNAINT  OF A_WUS900HE_EN FINAL T_HECUNAINT
  ITEM CIECLE     OF A_WUS900HE_EN FINAL CIECLE       OF CPPAIEMENT
  ITEM UNACLE     OF A_WUS900HE_EN FINAL CIEUNABIL    OF MCCOMPAGNIE
  ITEM PRJCLE     OF A_WUS900HE_EN FINAL " "
  ITEM PRACLE     OF A_WUS900HE_EN FINAL " "
  ITEM IMMCLE     OF A_WUS900HE_EN FINAL " "
  ITEM PECCLE     OF A_WUS900HE_EN FINAL PECCLE       OF CPLOT
  ITEM HECNUMDOC2 OF A_WUS900HE_EN FINAL T_HECNUMDOC2
  ITEM T_CODDC    OF A_WUS900HE_EN FINAL "C"
  ITEM T_MNTGL    OF A_WUS900HE_EN FINAL T_PAMMNT
  ITEM T_MNTORI   OF A_WUS900HE_EN FINAL T_PAMMNT
  ITEM HECFLGINT  OF A_WUS900HE_EN FINAL T_HECFLGINT
  ITEM DEVCLE     OF A_WUS900HE_EN FINAL DEVCLE       OF VRAD_01
  ITEM HISCODMOD  OF A_WUS900HE_EN FINAL G_CODMOD
  ITEM HISTYPECR  OF A_WUS900HE_EN FINAL PAMTYPECR    OF CPPAIEMENT
  ITEM HISNUMLOT  OF A_WUS900HE_EN FINAL LCPCLE       OF CPLOT

;
; Ajout du compte d'escompte
;
OUTPUT *WUS900HE ALIAS A_WUS900HE_ESC &
                 ADD AT PAMCLE &
                 IF   PAMMNTESC OF CPPAIEMENT <> 0 &
                  AND PAMCODANN OF CPPAIEMENT = "0"

  ITEM HISCLE     OF A_WUS900HE_ESC FINAL T_HISCLE_ANN
  ITEM HISCIECLE  OF A_WUS900HE_ESC FINAL CIECLE       OF CPPAIEMENT
  ITEM CPTCLE     OF A_WUS900HE_ESC FINAL FOCCPTESC    OF VFOC_FOU
  ITEM HECCIEINT  OF A_WUS900HE_ESC FINAL T_HECCIEINT
  ITEM HECUNAINT  OF A_WUS900HE_ESC FINAL T_HECUNAINT
  ITEM CIECLE     OF A_WUS900HE_ESC FINAL CIECLE       OF CPPAIEMENT
  ITEM UNACLE     OF A_WUS900HE_ESC FINAL CIEUNABIL    OF MCCOMPAGNIE
  ITEM PRJCLE     OF A_WUS900HE_ESC FINAL " "
  ITEM PRACLE     OF A_WUS900HE_ESC FINAL " "
  ITEM IMMCLE     OF A_WUS900HE_ESC FINAL " "
  ITEM PECCLE     OF A_WUS900HE_ESC FINAL PECCLE       OF CPLOT
  ITEM HECNUMDOC2 OF A_WUS900HE_ESC FINAL T_HECNUMDOC2
  ITEM T_CODDC    OF A_WUS900HE_ESC FINAL "C"
  ITEM T_MNTGL    OF A_WUS900HE_ESC FINAL T_PAMMNTESC
  ITEM T_MNTORI   OF A_WUS900HE_ESC FINAL T_PAMMNTESC
  ITEM HECFLGINT  OF A_WUS900HE_ESC FINAL T_HECFLGINT
  ITEM DEVCLE     OF A_WUS900HE_ESC FINAL DEVCLE       OF VRAD_01
  ITEM HISCODMOD  OF A_WUS900HE_ESC FINAL G_CODMOD
  ITEM HISTYPECR  OF A_WUS900HE_ESC FINAL PAMTYPECR    OF CPPAIEMENT
  ITEM HISNUMLOT  OF A_WUS900HE_ESC FINAL LCPCLE       OF CPLOT

;
; Mise   jour du lot
;
OUTPUT CPLOT UPDATE AT LCPCLE

  ITEM  LCPDATJOU OF CPLOT  FINAL G_DATJOU
  ITEM  LCPHREJOU OF CPLOT  FINAL G_HREJOU
  ITEM  LCPUSRJOU OF CPLOT  FINAL LOGONID

;
; Mise   jour du paiement et cr ation d'un subfile qui va contenir les
; paiements qui ont une imputation pour pouvoir  tres trait s dans le
; request suivant.
;
OUTPUT CPPAIEMENT UPDATE AT PAMCLE

  ITEM PAMDATJOU  OF CPPAIEMENT FINAL G_DATJOU
  ITEM PAMHREJOU  OF CPPAIEMENT FINAL G_HREJOU
  ITEM HISCLE     OF CPPAIEMENT FINAL T_HISCLE_ANN


SUBFILE WCP701A &
  AT PAMCLE &
  IF    (     PAMDEPDIR OF CPPAIEMENT = "1"   &
          OR  PAMFLGPA  OF CPPAIEMENT = "1" ) &
    AND PAMCODANN OF CPPAIEMENT = "0" &
  INCLUDE CIECLE        OF CPPAIEMENT , &
          LBQCPTENC     OF CPPAIEMENT , &
          PAMCLE        OF CPPAIEMENT , &
          PAMTYPECR     OF CPPAIEMENT , &
          T_HISCLE_ANN ALIAS T_HISCLE , &
          T_HECCIEINT                 , &
          T_HECUNAINT                 , &
          T_HECFLGINT                 , &
          CIEUNABIL     OF MCCOMPAGNIE, &
          PECCLE        OF CPLOT      , &
          LCPCLE        OF CPLOT      , &
          DEVCLE        OF VRAD_01

;
; Mise   jour de l'historique pour enregistrer la date de journalisation
;
OUTPUT CPCAP_HISTO  UPDATE &
                      IF    PAMDEPDIR OF CPPAIEMENT = "0" &
                        AND PAMCODANN OF CPPAIEMENT = "0" &
                        AND RECORD CPCAP_HISTO EXISTS

  ITEM CPHDATJOU OF CPCAP_HISTO FINAL G_DATJOU

;
; Mise   jour du compte   payer pour enregistrer le premier paiement. Si on
; annule ce paiement, alors on efface sa r f rence dans le compte   payer.
;
OUTPUT CPCPT_A_PAYER  UPDATE &
        IF    RECORD CPCAP_HISTO EXISTS &
          AND (   (     PAMCLE        OF CPCPT_A_PAYER  = ""  &
                     AND PAMDEPDIR     OF CPPAIEMENT     = "0" &
                     AND PAMFLGPA      OF CPPAIEMENT     = "0" &
                     AND PAMCODANN     OF CPPAIEMENT     = "0" ) &
          OR      (      PAMTYPECR     OF CPPAIEMENT     = "CA" &
                     AND LBQCPTENC     OF CPPAIEMENT     = LBQCPTENC OF CPCPT_A_PAYER &
                     AND PAMPAMCLEANN  OF CPPAIEMENT     = PAMCLE    OF CPCPT_A_PAYER ) &
              )

  ITEM  PAMDAT    OF CPCPT_A_PAYER  FINAL PAMDAT     OF CPPAIEMENT &
                                    IF PAMTYPECR OF CPPAIEMENT <> "CA"
  ITEM  PAMDAT    OF CPCPT_A_PAYER  FINAL 0 &
                                    IF PAMTYPECR OF CPPAIEMENT =  "CA"
  ITEM  LBQCPTENC OF CPCPT_A_PAYER FINAL LBQCPTENC  OF CPPAIEMENT &
                                    IF PAMTYPECR OF CPPAIEMENT <> "CA"
  ITEM  LBQCPTENC OF CPCPT_A_PAYER FINAL "" &
                                    IF PAMTYPECR OF CPPAIEMENT =  "CA"
  ITEM  PAMCLE    OF CPCPT_A_PAYER FINAL PAMCLE     OF CPPAIEMENT &
                                    IF PAMTYPECR OF CPPAIEMENT <> "CA"
  ITEM  PAMCLE    OF CPCPT_A_PAYER FINAL "" &
                                    IF PAMTYPECR OF CPPAIEMENT =  "CA"
  ;
  ; Pour le paiement non-appliqué.
  ;
  ITEM  CAPDATJOU OF CPCPT_A_PAYER FINAL G_DATJOU &
                                    IF CAPTYPECR OF CPCPT_A_PAYER = "NA"
  ITEM  CAPHREJOU OF CPCPT_A_PAYER FINAL G_HREJOU &
                                    IF CAPTYPECR OF CPCPT_A_PAYER = "NA"


;
; Mise   jour des statistiques d'achat et du nombre de jours de
; paiement du fournisseur
;
OUTPUT CPFOU_STAT &
          ADD UPDATE &
          IF    RECORD VFOC_FOU EXISTS &
            AND RECORD CPCAP_HISTO EXISTS &
            AND PAMDEPDIR OF CPPAIEMENT = "0" &
            AND PAMFLGPA  OF CPPAIEMENT = "0" &
            AND PAMCODANN OF CPPAIEMENT = "0" &
          VIA   FOUCIECLE, &
                FOUCLE   , &
                FOCCIECLE, &
                FOSANN     &
          USING REFCIECLE OF CPCPT_A_PAYER, &
                REFCLENUM OF CPCPT_A_PAYER, &
                CAPCIECLE OF CPCPT_A_PAYER, &
                PECCLE    OF CPLOT[1:2]

  ITEM FOUCIECLE   OF CPFOU_STAT INITIAL REFCIECLE OF CPCPT_A_PAYER
  ITEM FOUCLE      OF CPFOU_STAT INITIAL REFCLENUM OF CPCPT_A_PAYER
  ITEM FOCCIECLE   OF CPFOU_STAT INITIAL CAPCIECLE OF CPCPT_A_PAYER
  ITEM FOSANN      OF CPFOU_STAT INITIAL PECCLE    OF CPLOT[1:2]

  ITEM FOSCUMESC01    OF CPFOU_STAT = FOSCUMESC01 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "01" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM01 OF CPFOU_STAT = FOSNBRJRSPAM01 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "01" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM01    OF CPFOU_STAT = FOSNBRPAM01 OF CPFOU_STAT &
                                    + 1 &
                                    IF "01" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC02    OF CPFOU_STAT = FOSCUMESC02 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "02" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM02 OF CPFOU_STAT = FOSNBRJRSPAM02 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "02" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM02    OF CPFOU_STAT = FOSNBRPAM02 OF CPFOU_STAT &
                                    + 1 &
                                    IF "02" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC03    OF CPFOU_STAT = FOSCUMESC03 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "03" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM03 OF CPFOU_STAT = FOSNBRJRSPAM03 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "03" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM03    OF CPFOU_STAT = FOSNBRPAM03 OF CPFOU_STAT &
                                    + 1 &
                                    IF "03" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC04    OF CPFOU_STAT = FOSCUMESC04 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "04" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM04 OF CPFOU_STAT = FOSNBRJRSPAM04 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "04" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM04    OF CPFOU_STAT = FOSNBRPAM04 OF CPFOU_STAT &
                                    + 1 &
                                    IF "04" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC05    OF CPFOU_STAT = FOSCUMESC05 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "05" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM05 OF CPFOU_STAT = FOSNBRJRSPAM05 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "05" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM05    OF CPFOU_STAT = FOSNBRPAM05 OF CPFOU_STAT &
                                    + 1 &
                                    IF "05" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC06    OF CPFOU_STAT = FOSCUMESC06 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "06" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM06 OF CPFOU_STAT = FOSNBRJRSPAM06 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "06" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM06    OF CPFOU_STAT = FOSNBRPAM06 OF CPFOU_STAT &
                                    + 1 &
                                    IF "06" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC07    OF CPFOU_STAT = FOSCUMESC07 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "07" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM07 OF CPFOU_STAT = FOSNBRJRSPAM07 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "07" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM07    OF CPFOU_STAT = FOSNBRPAM07 OF CPFOU_STAT &
                                    + 1 &
                                    IF "07" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC08    OF CPFOU_STAT = FOSCUMESC08 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "08" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM08 OF CPFOU_STAT = FOSNBRJRSPAM08 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "08" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM08    OF CPFOU_STAT = FOSNBRPAM08 OF CPFOU_STAT &
                                    + 1 &
                                    IF "08" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC09    OF CPFOU_STAT = FOSCUMESC09 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "09" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM09 OF CPFOU_STAT = FOSNBRJRSPAM09 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "09" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM09    OF CPFOU_STAT = FOSNBRPAM09 OF CPFOU_STAT &
                                    + 1 &
                                    IF "09" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC10    OF CPFOU_STAT = FOSCUMESC10 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "10" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM10 OF CPFOU_STAT = FOSNBRJRSPAM10 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "10" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM10    OF CPFOU_STAT = FOSNBRPAM10 OF CPFOU_STAT &
                                    + 1 &
                                    IF "10" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC11    OF CPFOU_STAT = FOSCUMESC11 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "11" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM11 OF CPFOU_STAT = FOSNBRJRSPAM11 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "11" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM11    OF CPFOU_STAT = FOSNBRPAM11 OF CPFOU_STAT &
                                    + 1 &
                                    IF "11" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC12    OF CPFOU_STAT = FOSCUMESC12 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "12" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM12 OF CPFOU_STAT = FOSNBRJRSPAM12 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "12" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM12    OF CPFOU_STAT = FOSNBRPAM12 OF CPFOU_STAT &
                                    + 1 &
                                    IF "12" = PECCLE OF CPLOT[3:2]

  ITEM FOSCUMESC13    OF CPFOU_STAT = FOSCUMESC13 OF CPFOU_STAT &
                                    + CPHMNTESC OF CPCAP_HISTO &
                                    IF "13" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRJRSPAM13 OF CPFOU_STAT = FOSNBRJRSPAM13 OF CPFOU_STAT &
                                    + DAYS(PAMDAT OF CPPAIEMENT) &
                                    - DAYS(CAPDAT OF CPCPT_A_PAYER) &
                                    IF "13" = PECCLE OF CPLOT[3:2]

  ITEM FOSNBRPAM13    OF CPFOU_STAT = FOSNBRPAM13 OF CPFOU_STAT &
                                    + 1 &
                                    IF "13" = PECCLE OF CPLOT[3:2]


;
; Mise   jour du fichier des num ros d'historique
;
OUTPUT MCHIS_SEQ ADD UPDATE AT FINAL

  ITEM CIENMC    OF MCHIS_SEQ INITIAL "1"
  ITEM HSSCLEANN OF MCHIS_SEQ INITIAL G_ANN
  ITEM HSSNUMSEQ OF MCHIS_SEQ FINAL ( T_HISCLE  - 1 )

;-------------------------------------------------------------------------------
;
; 2EME PARTIE DE LA JOURNALISATION
;
; Ajout de la deuxiemme partie (imputation comptable) de l' criture si le
; paiement est un paiement direct ou un paiement de paiement automatique.
;
; Note : L'imputation du paiement automatique est cr   par la pr paration
; des paiements.
;
REQUEST JOURNALISATION_2EME_PARTIE

;
; Transactions qui ont une imputation comptable
;

ACCESS *WCP701A &

  ;
  ; Imputation comptable du paiement
  ;
  LINK  CIECLE    OF WCP701A, &
        LBQCPTENC OF WCP701A, &
        PAMCLE    OF WCP701A  &
    TO  CIECLE, &
        LBQCPTENC, &
        PAMCLE            OF CPPAM_IMPUTATION &

  ;
  ; Taxe f d rale
  ;
  LINK CPITAXTYPTPS OF CPPAM_IMPUTATION, &
       CPITAXCODTPS OF CPPAM_IMPUTATION  &
    TO TAXCLETYP, &
       TAXCLECOD          OF MCTAXE ALIAS A_TAX_TPS &
  OPTIONAL &

  ;
  ; Taxe provinciale
  ;
  LINK CPITAXTYPTVQ OF CPPAM_IMPUTATION, &
       CPITAXCODTVQ OF CPPAM_IMPUTATION  &
    TO TAXCLETYP, &
       TAXCLECOD          OF MCTAXE ALIAS A_TAX_TVP &
  OPTIONAL


;
; Calcul des taxes pour chaque lignes d'imputation
;
TEMPORARY T_CPIMNT    FLOAT     SIZE 8  ; Calcul du montant brut de l'imputation
TEMPORARY T_MNTDEP    FLOAT     SIZE 8  ; pour les montants de d penses

;
; On calcul le montant imput  en ajoutant les taxes si elles sont en sus.
;
ITEM T_CPIMNT = CPIMNT OF CPPAM_IMPUTATION
ITEM T_CPIMNT = T_CPIMNT &
              + CPIMNTTPS OF CPPAM_IMPUTATION &
                IF TAXMOD OF A_TAX_TPS = "S"
ITEM T_CPIMNT = T_CPIMNT &
              + CPIMNTTVQ OF CPPAM_IMPUTATION &
                IF TAXMOD OF A_TAX_TVP = "S"
;
; Le montant utilis  dans l' criture est le montant imput  moins les
; montants de TPS remboursable (CTI) et le montant de TVQ remboursable (RTI)
;
ITEM T_MNTDEP = T_CPIMNT - CPIMNTCTI OF CPPAM_IMPUTATION &
                         - CPIMNTRTI OF CPPAM_IMPUTATION

;
; Ajout de la ligne d' criture avec le compte de l'imputation
;
OUTPUT *WUS900HE ALIAS A_WUS900HE_IMP &
                 ADD IF T_MNTDEP <> 0

  ITEM HISCLE     OF A_WUS900HE_IMP FINAL T_HISCLE      OF WCP701A
  ITEM HISCIECLE  OF A_WUS900HE_IMP FINAL CIECLE        OF WCP701A
  ITEM CPTCLE     OF A_WUS900HE_IMP FINAL CPTCLE        OF CPPAM_IMPUTATION
  ITEM HECCIEINT  OF A_WUS900HE_IMP FINAL T_HECCIEINT   OF WCP701A
  ITEM HECUNAINT  OF A_WUS900HE_IMP FINAL T_HECUNAINT   OF WCP701A
  ITEM CIECLE     OF A_WUS900HE_IMP FINAL CIECLE        OF WCP701A
  ITEM UNACLE     OF A_WUS900HE_IMP FINAL UNACLE        OF CPPAM_IMPUTATION
  ITEM PRJCLE     OF A_WUS900HE_IMP FINAL PRJCLE        OF CPPAM_IMPUTATION
  ITEM PRACLE     OF A_WUS900HE_IMP FINAL PRACLE        OF CPPAM_IMPUTATION
  ITEM IMMCLE     OF A_WUS900HE_IMP FINAL IMMCLE        OF CPPAM_IMPUTATION
  ITEM PECCLE     OF A_WUS900HE_IMP FINAL PECCLE        OF WCP701A
  ITEM HECNUMDOC2 OF A_WUS900HE_IMP FINAL ""
  ITEM T_CODDC    OF A_WUS900HE_IMP FINAL CPICODDC      OF CPPAM_IMPUTATION
  ITEM T_MNTGL    OF A_WUS900HE_IMP FINAL T_MNTDEP
  ITEM T_MNTORI   OF A_WUS900HE_IMP FINAL T_MNTDEP
  ITEM HECFLGINT  OF A_WUS900HE_IMP FINAL T_HECFLGINT   OF WCP701A
  ITEM DEVCLE     OF A_WUS900HE_IMP FINAL DEVCLE        OF WCP701A
  ITEM HISCODMOD  OF A_WUS900HE_IMP FINAL G_CODMOD
  ITEM HISTYPECR  OF A_WUS900HE_IMP FINAL PAMTYPECR     OF WCP701A
  ITEM HISNUMLOT  OF A_WUS900HE_IMP FINAL LCPCLE        OF WCP701A

;
; Ajout de la ligne d' criture avec le compte de taxe f d rale
;
OUTPUT *WUS900HE ALIAS A_WUS900HE_TPS &
                 ADD IF CPIMNTCTI OF CPPAM_IMPUTATION <> 0

  ITEM HISCLE     OF A_WUS900HE_TPS FINAL T_HISCLE      OF WCP701A
  ITEM HISCIECLE  OF A_WUS900HE_TPS FINAL CIECLE        OF WCP701A
  ITEM CPTCLE     OF A_WUS900HE_TPS FINAL TAXCPTCTICAP  OF A_TAX_TPS
  ITEM HECCIEINT  OF A_WUS900HE_TPS FINAL T_HECCIEINT   OF WCP701A
  ITEM HECUNAINT  OF A_WUS900HE_TPS FINAL T_HECUNAINT   OF WCP701A
  ITEM CIECLE     OF A_WUS900HE_TPS FINAL CIECLE        OF WCP701A
  ITEM UNACLE     OF A_WUS900HE_TPS FINAL CIEUNABIL     OF WCP701A
  ITEM PRJCLE     OF A_WUS900HE_TPS FINAL " "
  ITEM PRACLE     OF A_WUS900HE_TPS FINAL " "
  ITEM IMMCLE     OF A_WUS900HE_TPS FINAL " "
  ITEM PECCLE     OF A_WUS900HE_TPS FINAL PECCLE        OF WCP701A
  ITEM HECNUMDOC2 OF A_WUS900HE_TPS FINAL ""
  ITEM T_CODDC    OF A_WUS900HE_TPS FINAL CPICODDC      OF CPPAM_IMPUTATION
  ITEM T_MNTGL    OF A_WUS900HE_TPS FINAL CPIMNTCTI     OF CPPAM_IMPUTATION
  ITEM T_MNTORI   OF A_WUS900HE_TPS FINAL CPIMNTCTI     OF CPPAM_IMPUTATION
  ITEM HECFLGINT  OF A_WUS900HE_TPS FINAL T_HECFLGINT   OF WCP701A
  ITEM DEVCLE     OF A_WUS900HE_TPS FINAL DEVCLE        OF WCP701A
  ITEM HISCODMOD  OF A_WUS900HE_TPS FINAL G_CODMOD
  ITEM HISTYPECR  OF A_WUS900HE_TPS FINAL PAMTYPECR     OF WCP701A
  ITEM HISNUMLOT  OF A_WUS900HE_TPS FINAL LCPCLE        OF WCP701A

;
; Ajout de la ligne d' criture avec le compte de taxe provinciale
;
OUTPUT *WUS900HE ALIAS A_WUS900HE_TVQ &
                 ADD IF CPIMNTRTI OF CPPAM_IMPUTATION <> 0

  ITEM HISCLE     OF A_WUS900HE_TVQ FINAL T_HISCLE      OF WCP701A
  ITEM HISCIECLE  OF A_WUS900HE_TVQ FINAL CIECLE        OF WCP701A
  ITEM CPTCLE     OF A_WUS900HE_TVQ FINAL TAXCPTCTICAP  OF A_TAX_TVP
  ITEM HECCIEINT  OF A_WUS900HE_TVQ FINAL T_HECCIEINT   OF WCP701A
  ITEM HECUNAINT  OF A_WUS900HE_TVQ FINAL T_HECUNAINT   OF WCP701A
  ITEM CIECLE     OF A_WUS900HE_TVQ FINAL CIECLE        OF WCP701A
  ITEM UNACLE     OF A_WUS900HE_TVQ FINAL CIEUNABIL     OF WCP701A
  ITEM PRJCLE     OF A_WUS900HE_TVQ FINAL " "
  ITEM PRACLE     OF A_WUS900HE_TVQ FINAL " "
  ITEM IMMCLE     OF A_WUS900HE_TVQ FINAL " "
  ITEM PECCLE     OF A_WUS900HE_TVQ FINAL PECCLE        OF WCP701A
  ITEM HECNUMDOC2 OF A_WUS900HE_TVQ FINAL ""
  ITEM T_CODDC    OF A_WUS900HE_TVQ FINAL CPICODDC      OF CPPAM_IMPUTATION
  ITEM T_MNTGL    OF A_WUS900HE_TVQ FINAL CPIMNTRTI     OF CPPAM_IMPUTATION
  ITEM T_MNTORI   OF A_WUS900HE_TVQ FINAL CPIMNTRTI     OF CPPAM_IMPUTATION
  ITEM HECFLGINT  OF A_WUS900HE_TVQ FINAL T_HECFLGINT   OF WCP701A
  ITEM DEVCLE     OF A_WUS900HE_TVQ FINAL DEVCLE        OF WCP701A
  ITEM HISCODMOD  OF A_WUS900HE_TVQ FINAL G_CODMOD
  ITEM HISTYPECR  OF A_WUS900HE_TVQ FINAL PAMTYPECR     OF WCP701A
  ITEM HISNUMLOT  OF A_WUS900HE_TVQ FINAL LCPCLE        OF WCP701A


;-------------------------------------------------------------------------------
;
; TRAITEMENTS GENERAUX A TOUTES LES JOURNALISATION
;

USE us-900t NOLIST

BUILD
