;/T/ Facturation
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 2 mai 1995
;
;    Description..: Permet la saisie de note descriptive générale.

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN fa152 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM


USE fa152.hlp NOLIST

USE ussectmp NOLIST     ; Variable pour la sécurité
"FA152"                 ; Nom de l'écran

USE usactecr  NOLIST

USE ustrasse NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie

DEFINE    DZ_CIECLE   CHARACTER SIZE 04 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FGNCLE    CHARACTER SIZE 2 RESET AT MODE
;-------------------------------------------------------------------------------
;
; Table primaire.
;
FILE FANOTE_GENERALE    PRIMARY &
                        NOITEM
  ACCESS VIA     FGNCLE     &
         USING   FGNCLE     &
         REQUEST FGNCLE     &
         ORDERBY FGNCLE

  ACCESS SEQUENTIAL &
         ORDERBY FGNCLE

FILE FANOTE_GEN_DET     DETAIL  &
                        OCCURS 15 &
                        NOITEM

  ACCESS VIA     FGNCLE                    &
         USING   FGNCLE OF FANOTE_GENERALE &
         ORDERBY FGNCLE   , &
                 FNDSEQ

  ITEM FGNCLE OF FANOTE_GEN_DET INITIAL FGNCLE OF FANOTE_GENERALE

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

TITLE &
@IF FRANCAIS
      " Notes générales " &
@ELSE
      " %%%es générales " &
@ENDIF
      CENTERED AT 3,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(2,3,19) (,,25)

FIELD FGNCLE OF FANOTE_GENERALE    &
      HIDDEN                       &
      LABEL "Note générale.:"      &
      REQUIRED NOCHANGE NOCORRECT  &
      LOOKUP NOTON FANOTE_GENERALE &
             VIA   FGNCLE          &
             USING FGNCLE OF FANOTE_GENERALE &
             MESSAGE 2738

FIELD FGNTIT OF FANOTE_GENERALE &
      ID SAME  &
      REQUIRED

SKIP 1
TITLE " Séq. Description                             Impression" &
AT ,02

ALIGN(2,2,3) (,,8) (,,51)

CLUSTER OCCURS WITH FANOTE_GEN_DET  ID BASE 10

FIELD FNDSEQ OF FANOTE_GEN_DET  &
        HIDDEN    &
        LABEL " " &
        REQUIRED

FIELD FNDDSC OF FANOTE_GEN_DET
FIELD FNDIMP OF FANOTE_GEN_DET  SIZE 3

CLUSTER

;-------------------------------------------------------------------------------
;
; Pour la transformation du flag d'impression en 1 et 0
;
PROCEDURE INPUT FNDIMP
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour afficher le falg sous la forme Oui ou Non
;
PROCEDURE OUTPUT FNDIMP
BEGIN
  USE usflgout3 NOLIST
END


PROCEDURE INPUT FGNCLE
BEGIN
   IF 0 <> INDEX(FIELDTEXT,"*")
   THEN BEGIN
        LET T_FGNCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
        RUN SCREEN fa103 MODE F PASSING T_FGNCLE
        LET FIELDTEXT = T_FGNCLE
   END

   IF 0 = NCON(FIELDTEXT) AND NOT FINDMODE
   THEN ERROR 2740

END

PROCEDURE INPUT FGNTIT
BEGIN
   IF 0=SIZE(FIELDTEXT)
   THEN LET FIELDTEXT = OLDVALUE(FGNTIT OF FANOTE_GENERALE)

   IF FIELDTEXT = " "
   THEN ERROR 2740
END

PROCEDURE PREUPDATE
BEGIN
  FOR FANOTE_GEN_DET
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF FANOTE_GEN_DET  &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF FANOTE_GEN_DET  &
       AND NOT NEWRECORD     OF FANOTE_GEN_DET  &
       AND NOT DELETEDRECORD OF FANOTE_GEN_DET  &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

  END
END


BUILD LIST DETAIL
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Notes générales ********************************
*                                                                              *
* Note générale.: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
* Séq. Description                             Impression                      *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
********************************************************************************
@ENDIF
