;/P/ Rapport détaillé de la facturation par client
;
;/M/ Programmeur...: Bernard Samson
;    Date..........: 15/11/99
;    Description...: Detail des facture par période comptable
;
;----------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS PDFACTURE &
LINK   PECCLE OF PDFACTURE &
  TO   PECCLE                          OF MCPERIODE_CTB &
LINK   (CIECLE  OF PDFACTURE,  &
       CLICLE   OF PDFACTURE,  &
       "----")                 &
  TO   CIECLE,                 &
       CLICLE,		       &
       CLICIECLE                       OF VCLC_CLI

CHOOSE CIECLE PARM, &
       CLICLE PARM

DEFINE D_PECCLE CHARACTER SIZE 4 = PARM

;Numéro de commande
DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF PDFACTURE  &
                                    + "-"                 &
                                    + PJTANN OF PDFACTURE &
                                    + "-"                 &
                                    + COMNUMCOMINT OF PDFACTURE      &
                                    IF PJTABR OF PDFACTURE <> "" AND &
                                       PJTANN OF PDFACTURE <> "" AND &
                                       COMNUMCOMINT OF PDFACTURE <> "" &
                                    ELSE ""

DEFINE D_CLINOM CHARACTER SIZE 27 = CLINOM OF VCLC_CLI [1:27] 

;On choisie les factures de commandes de bloc seulement et si
; elles sont incluses dans la période comptable choisie

SELECT IF  PECCLE OF PDFACTURE = D_PECCLE &
           AND &
           FTRFLGBLC OF PDFACTURE = "BL"



SORT ON CLICLE OF PDFACTURE    &
     ON FTRNUMFAC OF PDFACTURE

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
                     "Période..: " + PECCLE OF MCPERIODE_CTB + &
                     "Du " + ASCII( PECDATDEB OF MCPERIODE_CTB, 8 )[3:2] + "/" + &
                             ASCII( PECDATDEB OF MCPERIODE_CTB, 8 )[5:2] + "/" + &
                             ASCII( PECDATDEB OF MCPERIODE_CTB, 8 )[7:2] + &
                    " Au " + ASCII( PECDATFIN OF MCPERIODE_CTB, 8 )[3:2] + "/" + &
                             ASCII( PECDATFIN OF MCPERIODE_CTB, 8 )[5:2] + "/" + &
                             ASCII( PECDATFIN OF MCPERIODE_CTB, 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF328"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
        TAB 003 "CLIENT"   &
        TAB 034 "NO"       &
        TAB 044 "NO"  	   &
        TAB 054 "DATE"     &
        TAB 065 "VENTE"    &
        TAB 078 "VENTE"    &
SKIP 1                     &
        TAB 032 "FACTURE"  &
        TAB 041 "COMMANDE" &
        TAB 053 "FACTURE"  &
        TAB 067 "M3" 	   &
        TAB 080 "$" 	   &
SKIP 1 &
        TAB 003 "___________________________" &
        TAB 032 "_______"                   &
        TAB 041 "_________"  		    &
        TAB 053 "________" 		    &
        TAB 063 "__________"		    &
        TAB 075 "___________" 		    &
SKIP 2
REPORT &
        TAB 003 D_CLINOM PRINT AT CLICLE           &
        TAB 032 FTRNUMFAC    OF PDFACTURE  	   &
        TAB 041 D_NUMCOM 		 	   &
        TAB 053 FTRDATFAC    OF PDFACTURE          &
        TAB 063 FTRVOLMC3BLO OF PDFACTURE          &
        TAB 073 FTRMNT       OF PDFACTURE          &
SKIP 1 						


FOOTING AT CLICLE &
        TAB 003 "_____________"        			  &
        TAB 061 "____________"         			  &
        TAB 074 "_____________"        			  &
SKIP 1                                 			  &
 	TAB 003 COUNT "Factures"     		          &
        TAB 048 "Sous-total:"          			  &
        TAB 063 FTRVOLMC3BLO SUBTOTAL                     &
        TAB 073 FTRMNT SUBTOTAL PICTURE "^^^,^^^,^^.^^"	  &
SKIP 1


FINAL FOOTING &
        TAB 003 "_____________"        			  &
        TAB 061 "____________"         			  &
        TAB 074 "_____________"        			  &
SKIP 1                                 			  &
 	TAB 003 COUNT "Factures"        		  &
        TAB 048 "Total:"               			  &
        TAB 063 FTRVOLMC3BLO SUBTOTAL                     &
        TAB 073 FTRMNT SUBTOTAL PICTURE "^^^,^^^,^^.^^"	      

BUILD if328z
