;/T/ Gestion des acheteurs - Dépisteur.
;
;/P/ Réalisé par.......: Marc Poulin.
;    Date..............: 09 mars 2000.
;    Description.......: Création d'une gestion des acheteurs afin de cibler plus rapidement ceux qui initient les commandes et qui les réceptionnent.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 14.
;    
;    Signet............: MP-00-03-09_S14.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar159 NOMODE NOACTION FIELDMARK    &
             FROM 11,18 TO 23,80          &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND              &
             RECEIVING  T_ACHCLE


USE ustrasse NOLIST

USE ussectmp NOLIST
    "AR159"

USE ar159.hlp NOLIST

TEMPORARY T_ACHCLE CHARACTER SIZE 12; Code d'utilisteur

;-------------------------------------------------------------------------------
;
FILE ARACHETEUR PRIMARY      &
                   OPEN READ &
                   OCCURS 10

 ACCESS VIA ACHCLE   &
      USING T_ACHCLE &
    ORDERBY ACHCLE

;-------------------------------------------------------------------------------
; Table des usagers
;
FILE GSUSAGER REFERENCE
 ACCESS VIA USRCLE   &
      USING ACHCLE OF ARACHETEUR

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST

DRAW 1,1 TO 13,63

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE "Code          Nom" AT 2,6

ALIGN(3,3,6)(,,20)

CLUSTER OCCURS WITH ARACHETEUR

FIELD ACHCLE    OF ARACHETEUR HIDDEN LABEL ">>"
FIELD USREMPNOM OF GSUSAGER

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_ACHCLE, "@")
  THEN LET T_ACHCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_ACHCLE = ACHCLE OF ARACHETEUR
  RETURN
END

BUILD
@IF FORMAT
                 ***************************************************************
                 *    Code          Nom                                        *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 * >> xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
                 ***************************************************************
  
@ENDIF
