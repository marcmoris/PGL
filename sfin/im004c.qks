;
;/M/ François Déry, 7 mai 1999
;       Modifier le run command pour le exécutable en UNIX.
;
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN im004c      NOMODE ACTION LABEL " " &
                   FROM 2,1 TO 13,80 &
                   RECEIVING GPFRM_AFFICHE

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPFRM_AFFICHE          MASTER

FILE GPFRD_DOMAINE_OP       DESIGNER

FILE IMIMM_BDG_UNT          DESIGNER

FILE GPFRM_DETAILS          DESIGNER

FILE IMIMMOBILISATION       DESIGNER

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

PROCEDURE INTERNAL LIRE_BUDGET
BEGIN
        WHILE RETRIEVING IMIMM_BDG_UNT &
              VIA   CIECLE,            &
                    IMMCLE,            &
                    UNTCLE             &
              USING CIECLE OF GPFRM_AFFICHE, &
                    IMMCLE OF GPFRM_AFFICHE, &
                    UNTCLE OF GPFRD_DOMAINE_OP
        BEGIN
                 LET CIECLE    OF GPFRM_DETAILS = CIECLE OF IMIMM_BDG_UNT
                 LET IMMCLE    OF GPFRM_DETAILS = IMMCLE OF IMIMM_BDG_UNT
                 LET PRACLE    OF GPFRM_DETAILS = " "
                 LET PRJCLE    OF GPFRM_DETAILS = " "
                 LET CPTCLE    OF GPFRM_DETAILS = " "
                 LET UNACLE    OF GPFRM_DETAILS = " "
                 LET UNTCLE    OF GPFRM_DETAILS = UNTCLE OF IMIMM_BDG_UNT

                 IF FDUOPE OF GPFRD_DOMAINE_OP = "+"
                 THEN BEGIN
                      LET FDEREE    OF GPFRM_DETAILS = &
                          IBUMNTREE OF IMIMM_BDG_UNT
                 END
                 ELSE BEGIN
                      LET FDEREE    OF GPFRM_DETAILS = &
                          IBUMNTREE OF IMIMM_BDG_UNT * -1
                 END
                 PUT GPFRM_DETAILS RESET
        END
END


PROCEDURE INITIALIZE
BEGIN
   CLOSE GPFRM_DETAILS

   RUN COMMAND "echo 'create file gpfrm_details' | qutil"

   WHILE RETRIEVING GPFRD_DOMAINE_OP &
         VIA   FRMCLE,            &
               FRDCLE             &
         USING FRMCLE OF GPFRM_AFFICHE, &
               FRDCLE OF GPFRM_AFFICHE

   BEGIN
      DO LIRE_BUDGET
   END

   RETURN
END

BUILD
