;/T/ Format H/B - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 10 août 1993
;
;    Description..: Cet écran vérifie si le format reçu en paramètre est
;                   référé dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gl013x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING GLEF_FORMAT , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Format haut/ bas de page.
;
FILE GLEF_FORMAT      MASTER

;
; Etats financiers.
;
FILE GLETAT_FINANCIER REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans les états financier.
  ;
  GET GLETAT_FINANCIER VIA EFFCIECLE , &
                           EFFCLE      &
                   USING EFFCIECLE OF GLEF_FORMAT , &
                         EFFCLE    OF GLEF_FORMAT   &
                   OPTIONAL
  IF ACCESSOK
  THEN ERROR 342 ; Destruction annulée, cette information est référée par un
                 ; état financier.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
@IF FORMAT

@ENDIF
