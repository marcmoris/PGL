;/T/ Gerrer les tranches (Débitage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-26
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur le débitage de la tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd181g ACTIONBAR                     &
             MODE LABEL "" AT 2,80          &
             NOACTION                       &
             ACTIVITIES FIND                &
             FIELDMARK RETAIN STARTUP       &
             HELP POPUP FROM 4,9 TO 20,72   &
             RECEIVING  T_CIECLEMNU ,       &
                        T_CIENOM ,          &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD181G"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd181g.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Détail de débitage
;

FILE PDDET_DEBITAGE PRIMARY ;REFERENCE

  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Débitage
;

FILE PDDEBITAGE REFERENCE

  ACCESS VIA     CIECLE,                      &
                 DEBNUMRAP                    &
         USING   T_CIECLEMNU,                 &
                 DEBNUMRAP    OF PDDET_DEBITAGE


;-------------------------------------------------------------------------------

FILE PDMACH_DEBITAGE REFERENCE

  ACCESS VIA     CIECLE,      &
                 MDECODMAC    &
         USING   T_CIECLEMNU, &
                 MDECODMAC OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Débitage " &
@ELSE
      " %%%Débitage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO LINE 5

ALIGN (,3,19)


FIELD DEBNUMRAP        OF PDDEBITAGE       &
label "Numéro rapport:"&
        DISPLAY


FIELD DEBDATPRD        OF PDDEBITAGE        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        DISPLAY


FIELD QRTCODQRT        OF PDDEBITAGE       &
label "Quart.........:"&
        DISPLAY


FIELD DEBDEP           OF PDDEBITAGE       &
label "Départ........:"&
        DISPLAY


FIELD DEBARR           OF PDDEBITAGE       &
label "Arrêt.........:"&
        DISPLAY

ALIGN (,3,19) (,,26)

FIELD MDECODMAC        OF PDDEBITAGE       &
label "Code machine..:"&
        DISPLAY


FIELD MDEDSC001        OF PDMACH_DEBITAGE  &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


BUILD


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Débitage ***********************************
*                                                                              *
* Numéro rapport: xx                                                           *
* Date product..: xxxxxxxx                                                     *
* Quart.........: x                                                            *
* Départ........: xxxxx                                                        *
* Arrêt.........: xxxxx                                                        *
* Code machine..: xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
