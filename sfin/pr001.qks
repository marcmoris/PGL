;/T/ Gestion des projets
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 7 Octobre
;
;    Description..: Gestion des projets...
;
;/M/ Modifié par..: Guy Chabot le 12 Avril 1994
;    Impact.......: Ajout de l'équipement par défaut pour les imputations.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr001 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CLICIECLE CHARACTER SIZE 4  ; Compagnie du client.
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "PR001"             ; Nom de l'écran

USE pr001.hlp NOLIST    ; Use servant à la documentation de l'écran
                        ; Ne pas oublier de créer un fichier vide dans [exefra], [exeang]


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Sous-projet                " ACTION DESIGNER D001
  MENUITEM LABEL "Informations libre format  " ACTION DESIGNER D002

FILE GPPROJET  PRIMARY
  ACCESS VIA CIECLE, PROCLE REQUEST PROCLE &
                           USING T_CIECLEMNU, PROCLE &
     ORDERBY CIECLE, PROCLE

  ACCESS VIA CIECLE &
       USING T_CIECLEMNU &
     ORDERBY CIECLE, PROCLE

  ITEM CIECLE    OF GPPROJET INIT T_CIECLEMNU
  ITEM PRODATCRE OF GPPROJET INIT SYSDATE
  ITEM PROHRECRE OF GPPROJET INIT SYSTIME
  ITEM PROUSRCRE OF GPPROJET INIT LOGONID
  ITEM PRODATMOD OF GPPROJET FINAL   SYSDATE IF NOT NEWRECORD OF GPPROJET
  ITEM PROHREMOD OF GPPROJET FINAL   SYSTIME IF NOT NEWRECORD OF GPPROJET
  ITEM PROUSRMOD OF GPPROJET FINAL   LOGONID IF NOT NEWRECORD OF GPPROJET

FILE GPACTIVITE DESIGNER

;
; Validation de la compagnie d'inter-cie.
;
FILE MCCOMPAGNIE      REFERENCE
  ACCESS VIA CIECLE &
         USING PROCIEINT OF GPPROJET

;
; Validation du client
;
FILE CRCLIENT        REFERENCE
  ACCESS VIA CLICIECLE, CLICLE &
      USING  T_CLICIECLE, CLICLE OF GPPROJET

;
; Validation du type du projet et lecture de sa description
;
DEFINE    D_PROTYP CHARACTER SIZE 16 = "PROTYP"
TEMPORARY T_PROTYP CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_PROTYP

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_PROTYP, &
                PROTYP OF GPPROJET

;
; Validation du catégorie du projet et lecture de sa description
;
DEFINE    D_PROCAT CHARACTER SIZE 16 = "PROCAT"
TEMPORARY T_PROCAT CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_PROCAT

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_PROCAT, &
                PROCAT OF GPPROJET

;
; Validation du catégorie du projet et lecture de sa description
;
DEFINE    D_PROSTU CHARACTER SIZE 16 = "PROSTU"
TEMPORARY T_PROSTU CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_PROSTU

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_PROSTU, &
                PROSTU OF GPPROJET


;
; Validation du catégorie du projet et lecture de sa description
;
DEFINE    D_PRORPS CHARACTER SIZE 16 = "PRORPS"
TEMPORARY T_PRORPS CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_PRORPS

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_PRORPS, &
                PRORPS OF GPPROJET


;
; Validation du catégorie du projet et lecture de sa description
;
DEFINE    D_PROCAR CHARACTER SIZE 16 = "PROCAR"
TEMPORARY T_PROCAR CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_PROCAR

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_PROCAR, &
                PROCAR OF GPPROJET

;-------------------------------------------------------------------------------
;
;
DEFINE    D_UNICAT CHARACTER SIZE 16 = "UNICAT"
TEMPORARY T_UNICAT CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_UNICAT
  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_UNICAT, &
                UNICAT OF GPPROJET


;-------------------------------------------------------------------------------
; Validation de l'équipement.
;
FILE GPUNITE REFERENCE
  ACCESS VIA CIECLE, &
             UNICAT, &
             UNICLE &
       USING T_CIECLEMNU, &
             UNICAT OF GPPROJET, &
             UNICLE OF GPPROJET


TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies
TEMPORARY T_PROCLE    CHARACTER SIZE 8 ; Popup sur les projets.
TEMPORARY T_PROSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.
TEMPORARY T_CLICLE    CHARACTER SIZE  6 ; Popup sur les clients
TEMPORARY T_GPMCLE1   CHARACTER SIZE 8  ; Mémo
TEMPORARY T_GPMCLE2   CHARACTER SIZE 3  ; Mémo
TEMPORARY T_GPMTYP    CHARACTER SIZE 3  ; Mémo

TEMPORARY T_UNICAT2   CHARACTER SIZE 2 ; Popup sur les équipements
TEMPORARY T_UNICLE    CHARACTER SIZE 8 ; Popup sur les équipements.
TEMPORARY T_UNISTUPAT CHARACTER SIZE 5 ; Popup sur les équipements.
;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Gestion de projet " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(2,2,8)(,,10)(,23,36)(,42,53)(,63,71)(,,73)


FIELD PROTYP OF GPPROJET  NOCHANGE  REQUIRED  LABEL "Type:" HIDDEN SIZE 1 &
      LOOKUP ON VCBI_DSC_PROTYP &
      MESSAGE 2501  ;  "*EE* Ce type de projet n'existe pas"


FIELD CBIDSC OF VCBI_DSC_PROTYP  DISPLAY SIZE 10

FIELD PROCAT OF GPPROJET REQUIRED LABEL "Catégorie..:" SIZE 2 &
      LOOKUP ON VCBI_DSC_PROCAT &
      MESSAGE 2502  ;  "*EE* Cette catégorie n'existe pas"

FIELD PROCLE  OF GPPROJET REQUIRED NOCHANGE LABEL "No projet:"  &
  LOOKUP NOTON GPPROJET VIA CIECLE, PROCLE &
    USING T_CIECLEMNU, PROCLE OF GPPROJET &
    MESSAGE 803 ;= "*EE* Ce PROJET exist déjà"

FIELD PROSTU OF GPPROJET LABEL "Statut:" UPSHIFT DEFAULT "A" &
      LOOKUP ON VCBI_DSC_PROSTU &
      MESSAGE 804  ; = "*EE* Cette statut n'existe pas"

FIELD CBIDSC OF VCBI_DSC_PROSTU  DISPLAY SIZE 7


ALIGN(2,2,16)(,23,36)(,42,53)
FIELD PROANN OF GPPROJET REQUIRED LABEL "Annee projet:" NUM SIGNIF 4 HIDDEN
FIELD PROCAR OF GPPROJET REQUIRED LABEL "Type cargo.:" &
      LOOKUP ON VCBI_DSC_PROCAR &
      MESSAGE 2503 ; = "*EE* Ce cargo n'existe pas"

FIELD CLICLE OF GPPROJET LABEL "Client...:" &
      LOOKUP ON CRCLIENT &
      MESSAGE 2504 ; = "*EE* Ce client n'existe pas "
SKIP

ALIGN(2,2,16)(,,23)(,58,69)
FIELD PRORPS OF GPPROJET LABEL "Responsable.:" HIDDEN &
      LOOKUP ON VCBI_DSC_PRORPS &
      MESSAGE 2505 ;= "*EE* Ce code de responsable n'existe pas"

FIELD CBIDSC OF VCBI_DSC_PRORPS  DISPLAY

FIELD PROCIEINT OF GPPROJET LABEL "Cie inter:" &
        LOOKUP ON MCCOMPAGNIE &
          MESSAGE 801 ; Cette compagnie n'existe pas
SKIP
ALIGN(2,2,16)
FIELD PRODSC  OF GPPROJET LABEL "Description :" FOR 1,60  POPUP ON REQUEST HIDDEN
FIELD PRODSC2 OF GPPROJET NOLABEL FOR 1,60 POPUP ON REQUEST ID SAME
FIELD PRODSC3 OF GPPROJET NOLABEL FOR 1,60 POPUP ON REQUEST ID SAME

SKIP 1

ALIGN (2,2,24) (,34,44) (,55,62)

FIELD PROUNADEP OF GPPROJET LABEL "Unité de dépense :" HIDDEN
FIELD PROUNAREV OF GPPROJET LABEL "Revenus :"
FIELD PROUNAPAI OF GPPROJET LABEL "Paie :"

ALIGN (2,2,24) (,,26)

FIELD UNICAT OF GPPROJET &
        HIDDEN &
@IF FRANCAIS
        LABEL "Equipement..........:"
@ELSE
        LABEL "%%%Equipement....:"
@ENDIF

FIELD UNICLE OF GPPROJET

ALIGN (2,2,24) (,,26)
SKIP
FIELD PROREVEST    OF GPPROJET LABEL "Revenus estimés.....:" HIDDEN
FIELD PRODEPEST    OF GPPROJET LABEL "Depenses estimées...:" HIDDEN
FIELD PROJOUEST    OF GPPROJET LABEL "Jours estimés.......:" HIDDEN
FIELD PROHREEST    OF GPPROJET LABEL "Heures estimées.....:" HIDDEN
FIELD PROTONEST    OF GPPROJET LABEL "Tonnage estimé......:" HIDDEN
FIELD PRODATDEBEST OF GPPROJET  FORMAT YYYYMMDD PATTERN "####/##/##" LABEL "Date début estimée..:" HIDDEN
FIELD PRODATFINEST OF GPPROJET  FORMAT YYYYMMDD PATTERN "####/##/##" LABEL "Date fin estimée....:" HIDDEN
FIELD PROHREDEBEST OF GPPROJET LABEL "Heure début estimée.:" HIDDEN
FIELD PROHREFINEST OF GPPROJET LABEL "Heure fin estimée...:" HIDDEN
SKIP TO 13
ALIGN (48,48,62)
FIELD PROREV    OF GPPROJET LABEL "Revenus.....:"  HIDDEN
FIELD PRODEP    OF GPPROJET LABEL "Dépenses....:"  HIDDEN
FIELD PROJOU    OF GPPROJET LABEL "Jours.......:"  HIDDEN
FIELD PROHRE    OF GPPROJET LABEL "Heures......:"  HIDDEN
FIELD PROTON    OF GPPROJET LABEL "Tonnage.....:"  HIDDEN
FIELD PRODATDEB OF GPPROJET  FORMAT YYYYMMDD PATTERN "####/##/##" LABEL "Date début..:"  HIDDEN
FIELD PRODATFIN OF GPPROJET  FORMAT YYYYMMDD PATTERN "####/##/##" LABEL "Date fin....:"  HIDDEN
FIELD PROHREDEB OF GPPROJET LABEL "Heure début.:"  HIDDEN
FIELD PROHREFIN OF GPPROJET LABEL "Heure fin...:"  HIDDEN
SKIP

ALIGN(2,2,24)(,,31)(,,38)
FIELD PROFRM1 LABEL "Format d'impression :" DEFAULT "RESUL" HIDDEN
FIELD PROFRM2 NOLABEL ID SAME
FIELD PROFRM3 NOLABEL ID SAME
;(,55,76)
;FIELD PRONUIT  LABEL "Traitement de nuit :" ID SAME DEFAULT "O"


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PROTYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROTYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PROTYP, T_PROTYP
    LET FIELDTEXT = TRUNCATE(T_PROTYP)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PROCAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROCAT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PROCAT, T_PROCAT
    LET FIELDTEXT = TRUNCATE(T_PROCAT)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les projets.
;
;
PROCEDURE INPUT PROCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROSTUPAT = "A"
    LET T_PROCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr101 MODE F PASSING T_CIECLEMNU, &
                                    T_PROCLE   , &
                                    T_PROSTUPAT
    LET FIELDTEXT = TRUNCATE(T_PROCLE)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PROSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PROSTU, T_PROSTU
    LET FIELDTEXT = TRUNCATE(T_PROSTU)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PROCAR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROCAR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PROCAR, T_PROCAR
    LET FIELDTEXT = TRUNCATE(T_PROCAR)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRORPS
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRORPS = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRORPS, T_PRORPS
    LET FIELDTEXT = TRUNCATE(T_PRORPS)
  END
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT PROCIEINT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les client.
;
;
PROCEDURE INPUT CLICLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLICLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cr102 MODE F PASSING T_CLICIECLE, T_CIECLEMNU, T_CLICLE
    LET FIELDTEXT = TRUNC(T_CLICLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du client.
;
;
PROCEDURE EDIT CLICLE
BEGIN
  IF CLISTU OF CRCLIENT = "I"
  THEN ERROR 2506 ; Le client est inactif.
END

;-------------------------------------------------------------------------------
; Popup sur les catégories d'équipement.
;
PROCEDURE INPUT UNICAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNICAT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNICAT, T_UNICAT WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNICAT)
  END
END

PROCEDURE EDIT UNICAT
BEGIN
  IF 0 <> SIZE(TRUNCATE(UNICAT OF GPPROJET))
  THEN BEGIN
    GET VCBI_DSC_UNICAT OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 2303  ;  "*EE* Cette catégorie n'existe pas"
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT UNICLE
BEGIN
  ;
  ; Popup sur les équipements.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNICAT2    = UNICAT OF GPPROJET
    LET T_UNISTUPAT = "A"
    LET T_UNICLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr103 MODE F PASSING T_CIECLEMNU, T_UNICAT2, T_UNICLE, T_UNISTUPAT WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNICLE)
  END
  ;
  ; Force le lookup.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(UNICLE OF GPPROJET))
  THEN LET FIELDTEXT = UNICLE OF GPPROJET
  ;
  ; Force le lookup.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(TRUNCATE(FIELDTEXT)) AND &
     0 <> SIZE(TRUNCATE(UNICAT OF GPPROJET))
  THEN LET FIELDTEXT = "X-X-X-X"
END


;-------------------------------------------------------------------------------
; Cette procédure valide que l'équipement est active.
;
PROCEDURE EDIT UNICLE
BEGIN
  IF 0 <> SIZE(TRUNCATE(UNICLE OF GPPROJET))
  THEN BEGIN
    GET GPUNITE OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 2302  ;  "*EE* Ce numéro d'équipement n'existe pas"
    IF UNISTU OF GPUNITE <> "A"
    THEN ERROR 2302 ; Cette équipement n'est pas actif.
  END
END

;
; Sous projet
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN pr001a MODE SAME &
   PASSING T_CIECLEMNU, T_CIENOM, GPPROJET
END
;
; Memo
;
PROCEDURE DESIGNER D002
BEGIN
  LET T_GPMCLE1 = PROCLE OF GPPROJET
  LET T_GPMCLE2 = ""
  LET T_GPMTYP  = "PRO"
  RUN SCREEN pr050 MODE SAME &
   PASSING T_CIECLEMNU, T_CIENOM, T_GPMCLE1, T_GPMCLE2, T_GPMTYP
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPPROJET &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPPROJET &
     AND NOT NEWRECORD     OF GPPROJET &
     AND NOT DELETEDRECORD OF GPPROJET &
     AND TZ_SECMOD = "0"
  THEN ERROR 2


; Fait la creation du sous-GPPROJET par defaut
;
;

END

PROCEDURE POSTUPDATE
BEGIN
 GET GPACTIVITE VIA CIECLE, PROCLE &
    USING  T_CIECLEMNU, PROCLE OF GPPROJET OPTIONAL
  IF NOT ACCESSOK
  THEN BEGIN
    LET CIECLE      OF GPACTIVITE = T_CIECLEMNU
    LET PROCLE      OF GPACTIVITE = PROCLE     OF GPPROJET
    LET ACTCLE      OF GPACTIVITE = "000"
    LET ACTSTU      OF GPACTIVITE = PROSTU     OF GPPROJET
    LET ACTRPS      OF GPACTIVITE = PRORPS     OF GPPROJET
    LET ACTDATDEB   OF GPACTIVITE = PRODATDEB  OF GPPROJET
    LET ACTDATFIN   OF GPACTIVITE = PRODATFIN  OF GPPROJET
    LET ACTDSC1     OF GPACTIVITE = PRODSC     OF GPPROJET
    LET ACTDSC2     OF GPACTIVITE = PRODSC2    OF GPPROJET
    LET ACTDSC3     OF GPACTIVITE = PRODSC3    OF GPPROJET
    LET ACTCIEINT   OF GPACTIVITE = PROCIEINT  OF GPPROJET
    LET CLICLE      OF GPACTIVITE = CLICLE     OF GPPROJET
    LET ACTANN      OF GPACTIVITE = PROANN     OF GPPROJET
    LET ACTCAR      OF GPACTIVITE = PROCAR     OF GPPROJET
    LET ACTUNADEP   OF GPACTIVITE = PROUNADEP  OF GPPROJET
    LET ACTUNAREV   OF GPACTIVITE = PROUNAREV  OF GPPROJET
    LET ACTUNAPAI   OF GPACTIVITE = PROUNAPAI  OF GPPROJET
    LET ACTREVEST   OF GPACTIVITE = PROREVEST  OF GPPROJET
    LET ACTDEPEST   OF GPACTIVITE = PRODEPEST  OF GPPROJET
    LET ACTJOUEST   OF GPACTIVITE = PROJOUEST  OF GPPROJET
    LET ACTHREEST   OF GPACTIVITE = PROHREEST  OF GPPROJET
    LET ACTTONEST   OF GPACTIVITE = PROTONEST  OF GPPROJET
    LET ACTDATDEBEST OF GPACTIVITE = PRODATDEBEST  OF GPPROJET
    LET ACTDATFINEST OF GPACTIVITE = PRODATFINEST  OF GPPROJET
    LET ACTHREDEBEST OF GPACTIVITE = PROHREDEBEST  OF GPPROJET
    LET ACTHREFINEST OF GPACTIVITE = PROHREFINEST  OF GPPROJET
    LET ACTREV       OF GPACTIVITE = PROREV        OF GPPROJET
    LET ACTDEP       OF GPACTIVITE = PRODEP        OF GPPROJET
    LET ACTJOU       OF GPACTIVITE = PROJOU        OF GPPROJET
    LET ACTHRE       OF GPACTIVITE = PROHRE        OF GPPROJET
    LET ACTTON       OF GPACTIVITE = PROTON        OF GPPROJET
    LET ACTFRM1      OF GPACTIVITE = PROFRM1       OF GPPROJET
    LET ACTFRM2      OF GPACTIVITE = PROFRM2       OF GPPROJET
    LET ACTFRM3      OF GPACTIVITE = PROFRM3       OF GPPROJET
    LET ACTHREDEB    OF GPACTIVITE = PROHREDEB     OF GPPROJET
    LET ACTHREFIN    OF GPACTIVITE = PROHREFIN     OF GPPROJET
    PUT GPACTIVITE
  END
END

PROCEDURE DELETE
DISABLE



BUILD


