;/T/ Catégories d'immobilisation
;
;/P/ Programmeur..: Alain Côté
;    Date création: 17 février 1994
;
;/V3.02.00/ Alain Côté, 17 février 1994, Immobilisation
;
;    Description..: Définition des catégories d'immobilisation.
;                   Les catégories sont définies par compagnie.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN im150 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU , &
                       T_CIENOM

;
; Variable pour la securite
;
USE ussectmp NOLIST
    "IM150"

;
; Documentation de l'écran.
;
USE im150.hlp NOLIST


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie de menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------
;
FILE IMCATEGORIE      PRIMARY &
                      OCCURS 16
   ACCESS REQUEST IMCCLE OF IMCATEGORIE &
          VIA     CIECLE , &
                  IMCCLE   &
          USING   CIECLE OF IMCATEGORIE , &
                  IMCCLE OF IMCATEGORIE   &
          ORDERBY CIECLE , &
                  IMCCLE

   ACCESS VIA     CIECLE &
          USING   CIECLE OF IMCATEGORIE &
          ORDERBY CIECLE , &
                  IMCCLE

   ITEM CIECLE OF IMCATEGORIE INITIAL T_CIECLEMNU


;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Catégories d'immobilisation " &
@ELSE
      " %%%Catégories d'immobilisation " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

TITLE &
@IF FRANCAIS
      "Code  Description française           Description anglaise" &
@ELSE
      "Code  Description française           Description anglaise" &
@ENDIF
      AT ,8

ALIGN (8,6,8) (,,14) (,,46)
CLUSTER OCCURS WITH IMCATEGORIE &
               ID BASE 05

FIELD IMCCLE OF IMCATEGORIE LABEL " " HIDDEN &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON IMCATEGORIE &
          VIA CIECLE , &
              IMCCLE &
          USING CIECLE OF IMCATEGORIE , &
                IMCCLE OF IMCATEGORIE   &
          MESSAGE 1601 ; Ce code existe déjà

FIELD IMCDSCFRA OF IMCATEGORIE

FIELD IMCDSCANG OF IMCATEGORIE &
        DEFAULT IMCDSCFRA OF IMCATEGORIE

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT IMCCLE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR IMCATEGORIE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF IMCATEGORIE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF IMCATEGORIE &
       AND NOT NEWRECORD     OF IMCATEGORIE &
       AND NOT DELETEDRECORD OF IMCATEGORIE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'une catégorie.
    ;
    IF DELETEDRECORD OF IMCATEGORIE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la catégorie est référée.
      ;
      RUN SCREEN im150x MODE SAME &
                        PASSING IMCATEGORIE , &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************* Catégories d'immobilisation **************************
*                                                                              *
*      Code  Description française           Description anglaise              *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
********************************************************************************
@ENDIF
