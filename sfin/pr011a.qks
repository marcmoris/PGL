
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN pr011a      NOMODE NOACTION FIELDMARK STARTUP &
                   FROM 2,1 TO 13,80 &
                   MESSAGE ON 24 &
                   AUTOUPDATE &
                   HELP POPUP FROM 4,9 TO 20,72 &
                   ACTIVITIES FIND, CHANGE &
                   RECEIVING T_CIECLEFND, &
                             T_PROCLEFND, &
                             T_ACTCLEFND, &
                             T_FORCLEFND

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche
TEMPORARY T_PROCLEFND CHARACTER SIZE 08 ; Projet de recherche
TEMPORARY T_ACTCLEFND CHARACTER SIZE 03 ; Sous-projet de recherche
TEMPORARY T_FORCLEFND CHARACTER SIZE 05 ; Format de présentation

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPACTIVITE PRIMARY &
                NOITEM  &
                NODELETE

  ACCESS VIA    CIECLE, &
                PROCLE, &
                ACTCLE  &
         USING  T_CIECLEFND, &
                PROCLE OF GPACTIVITE, &
                ACTCLE OF GPACTIVITE  &
        REQUEST PROCLE OF GPACTIVITE, &
                ACTCLE OF GPACTIVITE

FILE GPFORMAT   REFERENCE

  ACCESS VIA    FORCLE &
         USING  ACTFRM1 OF GPACTIVITE

TEMPORARY T_PROCLEPOP CHARACTER SIZE 8
TEMPORARY T_PROSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.

TEMPORARY T_ACTCLEPOP CHARACTER SIZE 3
TEMPORARY T_ACTSTUPAT CHARACTER SIZE 5 ; Popup sur les activités.

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW  1,1 TO 12,80

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1

ALIGN (3,3,19) (,,28)

FIELD PROCLE OF GPACTIVITE &
        HIDDEN ID 10 &
        REQUIRED &
        NOCHANGE
FIELD ACTCLE OF GPACTIVITE &
        NOCHANGE

SKIP 1

ALIGN (,,6)

FIELD ACTDSC1 OF GPACTIVITE &
        DISPLAY
FIELD ACTDSC2 OF GPACTIVITE &
        DISPLAY
FIELD ACTDSC3 OF GPACTIVITE &
        DISPLAY

SKIP 2

ALIGN (3,3,19)

FIELD ACTFRM1 OF GPACTIVITE


;-------------------------------------------------------------------------------
;
; Popup sur les projets.
;
PROCEDURE INPUT PROCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROSTUPAT = "A"
    LET T_PROCLEPOP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr101 MODE F PASSING T_CIECLEFND, &
                                    T_PROCLEPOP, &
                                    T_PROSTUPAT  &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_PROCLEPOP)
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT ACTCLE
BEGIN
  ;
  ; Popup sur les activités.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROCLEPOP    = PROCLE OF GPACTIVITE
    LET T_ACTSTUPAT = "A"
    LET T_ACTCLEPOP    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr102 MODE F PASSING T_CIECLEFND, &
                                    T_PROCLEPOP, &
                                    T_ACTCLEPOP, &
                                    T_ACTSTUPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_ACTCLEPOP)
  END
END

PROCEDURE POSTFIND
BEGIN
  DISPLAY ACTDSC1 OF GPACTIVITE
  DISPLAY ACTDSC2 OF GPACTIVITE
  DISPLAY ACTDSC3 OF GPACTIVITE
  ACCEPT ACTFRM1 OF GPACTIVITE
  LET T_PROCLEFND = PROCLE  OF GPACTIVITE
  LET T_ACTCLEFND = ACTCLE  OF GPACTIVITE
  LET T_FORCLEFND = ACTFRM1 OF GPACTIVITE
  PUSH UPDATE
END

PROCEDURE UPDATE
BEGIN
  PUT GPACTIVITE
  RETURN
END

BUILD
@IF FORMAT
********************************** Paramètres **********************************
*                                                                              *
* Projet........: xxxxxxxx xxx                                                 *
*                                                                              *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
*                                                                              *
* Format........: xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     *
*                                                                              *
********************************************************************************
@ENDIF

