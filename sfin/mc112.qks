;/T/ Cons. des références              REFCIECLE, REFCLETYP, REFCLENUM, REFSTUPAT
;
;/P/ Programmeur..: Micheline Bélanger
;    Date Création: 5 Juin 1992
;
;    Description..: Permet de visionner et de choisir un fournisseur ou employé.
;                   Seul ceux actif sont sélectionnés.
;
;
;    Paramètre....: REFCIECLE ==> - compagnie du fournisseur ou de l'employé.
;                                 - segment de la clé de recherche.
;
;                   REFCLETYP ==> - segment de la clé de recherche.
;                                 - Peut contenir "F" pour fournisseur
;                                              ou "E" pour employé
;                                              ou "C" pour client
;
;                   REFCLENUM ==> - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;                                 - contien la valeur de retour.
;
;                   REFSTUPAT ==> - critère de sélection, sous la forme
;                                   de pattern.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc112 NOMODE NOACTION FIELDMARK &
             FROM 11,20 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_REFCIECLE, &
                       T_REFCLETYP, &
                       T_REFCLENUM, &
                       T_REFSTUPAT

USE mc112.hlp NOLIST

TEMPORARY T_REFCIECLE CHARACTER SIZE 4
TEMPORARY T_REFCLETYP CHARACTER SIZE 1
TEMPORARY T_REFCLENUM CHARACTER SIZE 6
TEMPORARY T_REFSTUPAT CHARACTER SIZE 5


;-------------------------------------------------------------------------------
;
FILE MCREFERENCE    PRIMARY &
                    OCCURS 11

 ACCESS VIA REFCIECLE, &
            REFCLETYP, &
            REFCLENUM  &
        USING T_REFCIECLE, &
              T_REFCLETYP, &
              T_REFCLENUM  &
        ORDERBY REFCLENUM

 SELECT IF MATCHPATTERN(REFSTU OF MCREFERENCE, TRUNCATE(T_REFSTUPAT))

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST
USE usdralar NOLIST

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO 2

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH MCREFERENCE

FIELD REFCLENUM OF MCREFERENCE HIDDEN LABEL ">>"
FIELD REFNOM OF MCREFERENCE

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_REFCLENUM, "@")
  THEN LET T_REFCLENUM = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_REFCLENUM = REFCLENUM OF MCREFERENCE
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       *****************************************
;
;
;
;
;
;
;
;
;
;
