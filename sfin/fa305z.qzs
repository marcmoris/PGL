;/Z/ Statistique de vente par date.

USE ussetqzs NOLIST
SET FORMFEED NOBLANKS
SET NOHEAD

ACCESS  *WFA305A &

        LINK CIECLE    OF WFA305A, &
             FHITRPCLE OF WFA305A  &
          TO CIECLE,               &
             TRPCLE OF FATRAJET_PRODUIT OPTIONAL &

        LINK CIECLE    OF WFA305A, &
             FHIETACLE OF WFA305A  &
          TO CIECLE,               &
             ETACLE OF FAELEMENT_TARIF OPTIONAL


DEFINE D_TRI    CHARACTER SIZE 1 = "1"                             &
                                IF (T_ETATYP OF WFA305A = "1"   OR &
                                    T_ETATYP OF WFA305A = "2"   OR &
                                    T_ETATYP OF WFA305A = "3") AND &
                                    T_NUMTRX OF WFA305A = "1"      &
                              ELSE "2"

DEFINE D_SIZETA   INTEGER SIZE 4 = SIZE(TRUNCATE(FHIETACLE OF WFA305A))

SORT ON T_TYPTRX,   &
        FHITRPCLE,  &
        T_ETATYP,   &
        D_TRI,      &
        T_NUMTRX,   &
        FHIETACLE


DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = " Statistiques de ventes par jour "
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "FA305Z"
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

DEFINE D_ENTETE   CHARACTER SIZE 60 = "Produit : "                 + &
                                      FHITRPCLE OF WFA305A         + &
                                      "  "                         + &
                                      TRPDSCFRA OF FATRAJET_PRODUIT  &
                                   IF FHITRPCLE <> " "               &
                                 ELSE "SOMMAIRE Toutes origines -" + &
                                      " destinations"


DEFINE D_LABEL1   CHARACTER SIZE 30 = "PASSAGERS"               &
                                   IF T_ETATYP OF WFA305A = "1" &
                                 ELSE "VÉHICULES"               &
                                   IF T_ETATYP OF WFA305A = "2" &
                                 ELSE "AUTRES"                  &
                                   IF T_ETATYP OF WFA305A = "3" &
                                 ELSE " "

DEFINE D_LABEL2   CHARACTER SIZE 30 = "---------"               &
                                   IF T_ETATYP OF WFA305A = "1" &
                                 ELSE "---------"               &
                                   IF T_ETATYP OF WFA305A = "2" &
                                 ELSE "------"                  &
                                   IF T_ETATYP OF WFA305A = "3" &
                                 ELSE " "

DEFINE D_ETADSC   CHARACTER SIZE 25 = FHIDSC OF WFA305A                 &
                                   IF FHIETACLE = "9999"                &
                                 ELSE ETADSCFRA OF FAELEMENT_TARIF      &
                                   IF FHIETACLE <> " "                  &
                                 ELSE "Total passagers"                 &
                                   IF T_ETATYP OF WFA305A = "1"     AND &
                                      T_NUMTRX OF WFA305A = "2"         &
                                 ELSE "Total passagers (segment)"       &
                                   IF T_ETATYP OF WFA305A = "1"     AND &
                                      T_NUMTRX OF WFA305A = "3"         &
                                 ELSE "Total véhicules"                 &
                                   IF T_ETATYP OF WFA305A = "2"     AND &
                                      T_NUMTRX OF WFA305A = "2"         &
                                 ELSE "Total véhicules (segment)"       &
                                   IF T_ETATYP OF WFA305A = "2"     AND &
                                      T_NUMTRX OF WFA305A = "3"         &
                                 ELSE "Total autres"                    &
                                   IF T_ETATYP OF WFA305A = "3"     AND &
                                      T_NUMTRX OF WFA305A = "2"         &
                                 ELSE "Total autres (segment)"          &
                                   IF T_ETATYP OF WFA305A = "3"     AND &
                                      T_NUMTRX OF WFA305A = "3"         &
                                 ELSE "Grand total (pass.+véh.)"        &
                                   IF T_ETATYP OF WFA305A = "4"     AND &
                                      T_NUMTRX OF WFA305A = "1"         &
                                 ELSE "Grand total (segment)"           &
                                   IF T_ETATYP OF WFA305A = "4"     AND &
                                      T_NUMTRX OF WFA305A = "2"         &
                                 ELSE " "

DEFINE D_RENFAC    FLOAT SIZE 08 = ROUND((T_MNTFAC8 OF WFA305A / &
                                          T_QTEFAC8 OF WFA305A) * 100)


DEFINE D_SOULIG1  CHARACTER SIZE 6 = "______"    &
                                  IF D_TRI = "1" &
                                ELSE " "

DEFINE D_SOULIG2  CHARACTER SIZE 7 = "_______"   &
                                  IF D_TRI = "1" &
                                ELSE " "

DEFINE D_SOULIG3  CHARACTER SIZE 9 = "_________" &
                                  IF D_TRI = "1" &
                                ELSE " "

USE ustit220 NOLIST
       SKIP 2 &
   TAB 001 D_ENTETE &
       SKIP 2 &
   TAB 037 "Total" &
   TAB 067 " Lundi  " &
   TAB 086 " Mardi  " &
   TAB 105 "Mercredi" &
   TAB 124 " jeudi  " &
   TAB 143 "Vendredi" &
   TAB 162 " Samedi " &
   TAB 181 "Dimanche" &
       SKIP &
   TAB 034 "Qte" &
   TAB 042 "Montant" &
   TAB 052 "Rendement" &
   TAB 066 "Qte   Montant" &
   TAB 085 "Qte   Montant" &
   TAB 104 "Qte   Montant" &
   TAB 123 "Qte   Montant" &
   TAB 142 "Qte   Montant" &
   TAB 161 "Qte   Montant" &
   TAB 180 "Qte   Montant" &
       SKIP 2


REPORT &
   TAB 001 D_LABEL1 PRINT AT T_ETATYP &
       SKIP &
   TAB 001 D_LABEL2 PRINT AT T_ETATYP &
       SKIP &
   TAB 005 D_ETADSC &
   TAB 032 T_QTEFAC8 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 041 T_MNTFAC8 PICTURE "^,^^^,^^^ "  &
                     TRAILING SIGN "-" BWZ &
   TAB 052 D_RENFAC  PICTURE "^^,^^^.^^ "  &
                     TRAILING SIGN "-" BWZ &
   TAB 064 T_QTEFAC1 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 072 T_MNTFAC1 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 083 T_QTEFAC2 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 091 T_MNTFAC2 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 102 T_QTEFAC3 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 110 T_MNTFAC3 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 121 T_QTEFAC4 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 129 T_MNTFAC4 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 140 T_QTEFAC5 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 148 T_MNTFAC5 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 159 T_QTEFAC6 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 167 T_MNTFAC6 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 178 T_QTEFAC7 PICTURE "^^,^^^ "     &
                     TRAILING SIGN "-" BWZ &
   TAB 186 T_MNTFAC7 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ

FOOTING AT T_ETATYP  SKIP 2

FOOTING AT D_TRI &
        SKIP &
        TAB 032 D_SOULIG2 &
        TAB 041 D_SOULIG3 &
        TAB 053 D_SOULIG3 &
        TAB 064 D_SOULIG1 &
        TAB 072 D_SOULIG2 &
        TAB 083 D_SOULIG1 &
        TAB 091 D_SOULIG2 &
        TAB 102 D_SOULIG1 &
        TAB 110 D_SOULIG2 &
        TAB 121 D_SOULIG1 &
        TAB 129 D_SOULIG2 &
        TAB 140 D_SOULIG1 &
        TAB 148 D_SOULIG2 &
        TAB 159 D_SOULIG1 &
        TAB 167 D_SOULIG2 &
        TAB 178 D_SOULIG1 &
        TAB 186 D_SOULIG2 &
        SKIP

FOOTING AT FHITRPCLE SKIP PAGE

BUILD fa305a
