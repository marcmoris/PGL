;/T/ 2.3.2 Historique des diagrammes. (Débitage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-08-07
;
;    Description..: Le but de cette unité de traitement est de permettre de
;                   consulter les information sur le débitage des diagrammes.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd211h ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              ACTIVITIES FIND               &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD211H"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd211h.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_CAISTU    CHARACTER SIZE 16 = "CAISTU"
TEMPORARY T_CAISTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME MASTER


;-------------------------------------------------------------------------------
;
; Détail - débitage
;

FILE PDDIAG_TRACE PRIMARY &
                     NOITEM &
                     NOAPPEND &
                     NODELETE &
                     OCCURS 14

  ACCESS VIA     CIECLE,          &
                 DIANUMDIA002,       &
                 DIANUMDIA001,       &
                 PJTABR           &
         USING   T_CIECLEMNU,                         &
                 DIANUMDIA002        OF PDDIAGRAMME,     &
                 DIANUMDIA001        OF PDDIAGRAMME,     &
                 PJTABR           OF PDDIAGRAMME      &
         ORDERBY CIECLE,          &
                 DIANUMDIA002,       &
                 DTRDAT, &
                 DTRHRE

;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Diagramme " &
@ELSE
      " %%%Diagramme " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD DIANUM002        OF PDDIAGRAMME      &
label "Diagramme....:"&
        DISPLAY                            &
        FIXED


DRAW 7,2 TO 7,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Historique de la quantité commandée " &
@ELSE
      " %%% Historique de la quantitée commandée " &
@ENDIF
      CENTERED AT ,1




SKIP TO LINE 08

TITLE &
@IF FRANCAIS
      "           Usager        Date     Heure   Ancienne    Nouvelle" &
@ELSE
      "%%%   " &
@ENDIF
      AT ,2


SKIP TO LINE 09


ALIGN (09,09,10) (,,25) (,,36) (,,44) (,,56)


CLUSTER OCCURS WITH PDDIAG_TRACE ID BASE 30


FIELD DTRUSR OF PDDIAG_TRACE &
        HIDDEN               &
        LABEL " "            &
        DISPLAY

FIELD DTRDAT OF PDDIAG_TRACE  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        DISPLAY

FIELD DTRHRE OF PDDIAG_TRACE &
        DISPLAY PIC "^^:^^"

FIELD DTRQTEANC OF PDDIAG_TRACE &
        DISPLAY PIC " ^^^,^^^ "

FIELD DTRQTENOU OF PDDIAG_TRACE &
        DISPLAY PIC " ^^^,^^^ "

CLUSTER

BUILD
