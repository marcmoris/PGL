;/T/ 2.3.4. Assignation de blocs.
;
;/P/ Programmeur..: Denis Pouliot
;    Date Création: 1995-05-04
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les assignations des blocs de granit de Granicor.
;                   Les blocs sont assignés par commande interne.
;
;
;    *** NOTE ***
;       La procédure PREUPDATE traite la modification du statut de blocs
;       dans les cas d'ajout ou d'annulation d'assignation et le suivi
;       sur l'historique de blocs.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd219 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD219"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd219.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie



;-------------------------------------------------------------------------------
;
;  Variables utilisées lors de l'appel des dépisteurs
;

TEMPORARY T_BLONUMGRA001APR   CHARACTER SIZE 04
TEMPORARY T_BLONUMGRA002APR   CHARACTER SIZE 05
TEMPORARY T_BLORECAPR         CHARACTER SIZE 01
TEMPORARY T_CHAMP             INTEGER UNSIGNED SIZE 1
TEMPORARY T_COD               INTEGER UNSIGNED SIZE 2
TEMPORARY T_STU               CHARACTER SIZE 1

TEMPORARY T_PJTABR       CHARACTER SIZE 02 ; Popup sur les commandes internes
TEMPORARY T_PJTANN       CHARACTER SIZE 02 ; Popup sur les commandes internes
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 03 ; Popup sur les commandes internes
TEMPORARY T_COMNUMCOM    CHARACTER SIZE 07 ; Popup sur les commandes internes


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable necessaire au traitement
;

TEMPORARY T_SILENT CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE PRIMARY  &
              NOITEM   &
              NODELETE &
              NOAPPEND


  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
         REQUEST PJTABR           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT

  ACCESS VIA     CIECLE,          &
                 PJTABR           &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE  &
         REQUEST PJTABR           OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT

  ACCESS VIA     CIECLE           &
         USING   T_CIECLEMNU      &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT

  ITEM CIECLE OF PDCOMMAN_INTERNE INITIAL T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Référence au dossier client pour récuperer le nom du client
;

FILE VCLC_CLI REFERENCE

  ACCESS VIA    CLICLE &
         USING  CLICLE OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Détail de l'assignation
;

FILE PDCOMMAN_ASSIGN DETAIL   &
                     NOITEM   &
                     OCCURS 10


  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 BLONUMGRA001APR, &
                 BLONUMGRA002APR, &
                 BLORECAPR


SELECT IF TPDTYPPRD OF PDCOMMAN_ASSIGN = "BL"

    ITEM CIECLE       OF PDCOMMAN_ASSIGN INITIAL T_CIECLEMNU
    ITEM TPDTYPPRD    OF PDCOMMAN_ASSIGN INITIAL "BL"
    ITEM PJTABR       OF PDCOMMAN_ASSIGN INITIAL PJTABR       OF PDCOMMAN_INTERNE
    ITEM PJTANN       OF PDCOMMAN_ASSIGN INITIAL PJTANN       OF PDCOMMAN_INTERNE
    ITEM COMNUMCOMINT OF PDCOMMAN_ASSIGN INITIAL COMNUMCOMINT OF PDCOMMAN_INTERNE
    ITEM COMNUMCOM    OF PDCOMMAN_ASSIGN INITIAL PJTABR       OF PDCOMMAN_INTERNE+ &
                                                 PJTANN       OF PDCOMMAN_INTERNE+ &
                                                 COMNUMCOMINT OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Référence au fichier projet
;

FILE PDPROJET REFERENCE

  ACCESS VIA    CIECLE,          &
                PJTABR           &
         USING  T_CIECLEMNU,     &
                PJTABR OF PDCOMMAN_INTERNE

    ITEM PJTNUMPRO    OF PDCOMMAN_ASSIGN FINAL PJTNUMPRO    OF PDPROJET
    ITEM PJTNUMSEQ    OF PDCOMMAN_ASSIGN FINAL PJTNUMSEQ    OF PDPROJET


;-------------------------------------------------------------------------------
;
; Référence au fichier des blocs
;

FILE PDBLOC REFERENCE OCCURS WITH PDCOMMAN_ASSIGN

  ACCESS VIA   CIECLE,          &
               BLONUMGRA001APR, &
               BLONUMGRA002APR, &
               BLORECAPR        &
         USING T_CIECLEMNU,     &
               BLONUMGRA001APR  OF PDCOMMAN_ASSIGN, &
               BLONUMGRA002APR  OF PDCOMMAN_ASSIGN, &
               BLORECAPR        OF PDCOMMAN_ASSIGN


    ITEM BLONUMBLOAPR    OF PDCOMMAN_ASSIGN FINAL BLONUMGRA001APR OF PDBLOC + &
                                                  BLONUMGRA002APR OF PDBLOC + &
                                                  BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Pour permettre la mise à jour du fichier PDBLOC.
;

FILE PDBLOC DESIGNER ALIAS A_BLO_BLOC

  ACCESS VIA   CIECLE,          &
               BLONUMGRA001APR, &
               BLONUMGRA002APR, &
               BLORECAPR        &
         USING T_CIECLEMNU,     &
               BLONUMGRA001APR  OF PDCOMMAN_ASSIGN, &
               BLONUMGRA002APR  OF PDCOMMAN_ASSIGN, &
               BLORECAPR        OF PDCOMMAN_ASSIGN


;-------------------------------------------------------------------------------
;
; Lignes de détails d'une commande interne.
;
; Validation du type de granit.

FILE PDDETAIL_C_I DESIGNER


;------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Assignation " &
@ELSE
      " %%%Assignation " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (2,3,19) (,21,22) (,24,25) (,,30)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        HIDDEN ID 05                       &
        REQUIRED                           &
        NOCHANGE                           &
        LABEL "Num. commande.:"            &
        LOOKUP ON PDPROJET                 &
          MESSAGE 3074 ; Le code de projet n'existe pas


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        NOCHANGE                           &
        LABEL "-"


FIELD COMNUMCOMINT      OF PDCOMMAN_INTERNE &
        HIDDEN ID 06                        &
        LABEL "-"                           &
        REQUIRED                            &
        NOCHANGE                            &
        NUMERIC                             &
        LOOKUP ON PDCOMMAN_INTERNE          &
          VIA   CIECLE,                     &
                PJTABR,                     &
                PJTANN,                     &
                COMNUMCOMINT                &
          USING T_CIECLEMNU,                      &
                PJTABR       OF PDCOMMAN_INTERNE, &
                PJTANN       OF PDCOMMAN_INTERNE, &
                COMNUMCOMINT OF PDCOMMAN_INTERNE  &
          MESSAGE 3008 ; Cette commande interne n'existe pas


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        HIDDEN ID SAME                     &
        DISPLAY


SKIP

ALIGN (2,3,19) (,,26)

FIELD PJTABR OF PDPROJET                   &
        HIDDEN ID 07                       &
        LABEL "Projet.........:"           &
        DISPLAY

FIELD PJTNOM           OF PDPROJET         &
        DISPLAY

SKIP

FIELD CLICLE           OF PDCOMMAN_INTERNE &
        HIDDEN ID 09                       &
        LABEL "Client........:"            &
        DISPLAY

FIELD CLINOM           OF VCLC_CLI         &
        HIDDEN                             &
        DISPLAY


DRAW 08,2 TO 08,79


SKIP TO LINE 08


TITLE &
@IF FRANCAIS
      " Blocs " &
@ELSE
      "%%% Blocs " &
@ENDIF
      AT ,36


SKIP TO LINE 09


TITLE &
@IF FRANCAIS
      "                      Code                                   Erreur" &
@ELSE
      "%%%                   Code                                   Erreur" &
@ENDIF
      AT ,8

SKIP

TITLE &
@IF FRANCAIS
      "     No Bloc          granit   Long.    Haut.    Épais.       O/N" &
@ELSE
      "%%%  No Bloc          granit   Long.    Haut.    Épais.      O/N" &
@ENDIF
      AT ,8


ALIGN (08,09,13)(17,17,18)(23,23,24)(,,31)(,,40)(,,49)(,,59)(,,71)


CLUSTER OCCURS WITH PDCOMMAN_ASSIGN ID BASE 20


FIELD BLONUMGRA001APR  OF PDCOMMAN_ASSIGN  &
        HIDDEN                             &
        NUMERIC                            &
        REQUIRED                           &
;        NOCORRECT                          &
        NOCHANGE                           &
        LABEL " "


FIELD BLONUMGRA002APR  OF PDCOMMAN_ASSIGN  &
        HIDDEN                             &
        NUMERIC                            &
        REQUIRED                           &
;        NOCORRECT                          &
        NOCHANGE                           &
        LABEL "-"


FIELD BLORECAPR        OF PDCOMMAN_ASSIGN  &
        HIDDEN                             &
 ;       NOCORRECT                          &
        NOCHANGE                           &
        LABEL "-"



FIELD T_SILENT                             &
        SILENT                             &
        LOOKUP ON PDBLOC                   &
          MESSAGE 2943, & ; Ce numéro de bloc n'existe pas
               NOTON PDCOMMAN_ASSIGN       &
          VIA   CIECLE,                    &
                BLONUMGRA001APR,           &
                BLONUMGRA002APR,           &
                BLORECAPR                  &
          USING T_CIECLEMNU,               &
                BLONUMGRA001APR OF PDCOMMAN_ASSIGN, &
                BLONUMGRA002APR OF PDCOMMAN_ASSIGN, &
                BLORECAPR       OF PDCOMMAN_ASSIGN  &
          MESSAGE 3160        ; Ce numéro de bloc est déjà assigné


FIELD TGRCODGRA OF PDBLOC &
        DISPLAY


FIELD BLOLONNET OF PDBLOC &
        DISPLAY


FIELD BLOHAUNET OF PDBLOC &
        DISPLAY


FIELD BLOLARNET OF PDBLOC &
        DISPLAY


FIELD CASACCERR &
      HIDDEN

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT PJTABR OF PDCOMMAN_INTERNE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN

    LET T_PJTABR       = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PJTANN       = " "
    LET T_COMNUMCOMINT = " "
    LET T_COMNUMCOM    = " "

    RUN SCREEN pd130 MODE F &
                     PASSING T_PJTABR, &
                             T_PJTANN, &
                             T_COMNUMCOMINT, &
                             T_COMNUMCOM,    &
                             T_CIECLEMNU

    LET     PJTANN       OF PDCOMMAN_INTERNE = TRUNCATE( T_PJTANN )
    LET     COMNUMCOMINT OF PDCOMMAN_INTERNE = TRUNCATE( T_COMNUMCOMINT )
    LET     COMNUMCOM    OF PDCOMMAN_INTERNE = TRUNCATE( T_COMNUMCOM )
    ;
    ; Doit être le dernier champ initialisé sinon on risque de perdre la valeur
    ;
    LET     FIELDTEXT                      = TRUNCATE( T_PJTABR )
  END
END


;-------------------------------------------------------------------------------
;
; Assigne l'annéé du projet par défaut
;

PROCEDURE PROCESS PJTABR
BEGIN
  LET     PJTANN       OF PDCOMMAN_INTERNE = PJTANN    OF PDPROJET
  LET     PJTNUMSEQ    OF PDCOMMAN_INTERNE = PJTNUMSEQ OF PDPROJET
  DISPLAY PJTANN       OF PDCOMMAN_INTERNE
  DISPLAY COMNUMCOMINT OF PDCOMMAN_INTERNE
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA001APR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLONUMGRA002APR  = ""
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 1
    LET T_COD              = 2
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA001APR )
  END
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attention cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA002APR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDCOMMAN_ASSIGN
    LET T_BLONUMGRA002APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 2
    LET T_COD              = 2
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDCOMMAN_ASSIGN = TRUNCATE( T_BLONUMGRA001APR )
    LET FIELDTEXT                          = TRUNCATE( T_BLONUMGRA002APR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLONUMGRA002APR ) ) AND &
     0 =  SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLORECAPR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDCOMMAN_ASSIGN
    LET T_BLONUMGRA002APR  = BLONUMGRA001APR OF PDCOMMAN_ASSIGN
    LET T_BLORECAPR        = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CHAMP            = 3
    LET T_COD              = 2
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDCOMMAN_ASSIGN = TRUNCATE( T_BLONUMGRA001APR )
    LET BLONUMGRA002APR OF PDCOMMAN_ASSIGN = TRUNCATE( T_BLONUMGRA002APR )
    LET FIELDTEXT                          = TRUNCATE( T_BLORECAPR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLORECAPR ) ) AND &
     0 = SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE EDIT T_SILENT
BEGIN
  ;
  ;  Vérifier le type de granit
  ;
  GET A_BLO_BLOC &
    VIA   CIECLE,          &
          BLONUMGRA001APR, &
          BLONUMGRA002APR, &
          BLORECAPR        &
    USING T_CIECLEMNU,     &
          BLONUMGRA001APR  OF PDCOMMAN_ASSIGN, &
          BLONUMGRA002APR  OF PDCOMMAN_ASSIGN, &
          BLORECAPR        OF PDCOMMAN_ASSIGN

  IF BLOSTU OF A_BLO_BLOC = "R"
  THEN WARNING "Ce bloc est déjà réservé !"

  GET PDDETAIL_C_I OPTIONAL &
     VIA   CIECLE,          &
           PJTABR,          &
           COMNUMCOMINT,    &
           TGRCODGRA        &
     USING T_CIECLEMNU,                     &
           PJTABR       OF PDCOMMAN_ASSIGN, &
           COMNUMCOMINT OF PDCOMMAN_ASSIGN, &
           TGRCODGRA    OF A_BLO_BLOC


  IF NOT ACCESSOK
  THEN BEGIN
    WARNING 3121 ; "Le type de granit ne correspond à celui commandé."
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;       OUI, YES = 1
;       NON      = 0
;
;

PROCEDURE INPUT CASACCERR OF PDCOMMAN_ASSIGN
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;
PROCEDURE OUTPUT CASACCERR OF PDCOMMAN_ASSIGN
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Cette procédure permet de modifier le champ CASACCERR
;

PROCEDURE DESIGNER 20
BEGIN
  DISPLAY BLONUMGRA001APR  OF PDCOMMAN_ASSIGN
  DISPLAY BLONUMGRA002APR  OF PDCOMMAN_ASSIGN
  DISPLAY BLORECAPR        OF PDCOMMAN_ASSIGN
  DISPLAY TGRCODGRA        OF PDBLOC
  DISPLAY BLOLONNET        OF PDBLOC
  DISPLAY BLOHAUNET        OF PDBLOC
  DISPLAY BLOLARNET        OF PDBLOC
  ACCEPT  CASACCERR        OF PDCOMMAN_ASSIGN
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
; Met le statut de bloc à "A" (assignation) à la création
; et à "I" lors de la destruction d'une assignation
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Modification dans PDCOMMAN_ASSIGN et A_BLO_BLOC
  ;
  FOR PDCOMMAN_ASSIGN
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDCOMMAN_ASSIGN &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDCOMMAN_ASSIGN &
       AND NOT NEWRECORD     OF PDCOMMAN_ASSIGN &
       AND NOT DELETEDRECORD OF PDCOMMAN_ASSIGN &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

    ;
    ; Utiliser A_BLO_BLOC pour les mise à jour des informations
    ; le fichier PDBLOC étant "REFERENCE"
    ;
    GET A_BLO_BLOC &
      VIA   CIECLE,          &
            BLONUMGRA001APR, &
            BLONUMGRA002APR, &
            BLORECAPR        &
      USING T_CIECLEMNU,     &
            BLONUMGRA001APR  OF PDCOMMAN_ASSIGN, &
            BLONUMGRA002APR  OF PDCOMMAN_ASSIGN, &
            BLORECAPR        OF PDCOMMAN_ASSIGN


    IF NOT DELETEDRECORD OF PDCOMMAN_ASSIGN
    THEN BEGIN
      ;
      ; Valider si accepter les erreurs
      ;
      GET PDDETAIL_C_I OPTIONAL &
         VIA      CIECLE,       &
                  PJTABR,       &
                  COMNUMCOMINT, &
                  TGRCODGRA     &
         USING    T_CIECLEMNU,                      &
                  PJTABR        OF PDCOMMAN_ASSIGN, &
                  COMNUMCOMINT  OF PDCOMMAN_ASSIGN, &
                  TGRCODGRA     OF A_BLO_BLOC

      IF NOT ACCESSOK
      THEN BEGIN
         IF CASACCERR OF PDCOMMAN_ASSIGN = "0"
         THEN BEGIN
            ERROR 3121 ; Le type de granit ne correspond pas à celui commandé
         END
      END
    END

    ;
    ;  Historique des modifications du fichier de PDBLOC
    ;
    IF ALTEREDRECORD OF PDCOMMAN_ASSIGN
    THEN BEGIN
       LET BLODATDERMAJ OF A_BLO_BLOC = SYSDATE
       LET BLOHREDERMAJ OF A_BLO_BLOC = SYSTIME / 10000
       LET BLOUSRDERMAJ OF A_BLO_BLOC = LOGONID
    END
    ;
    ; Modification du statut de bloc
    ;
    IF DELETEDRECORD OF PDCOMMAN_ASSIGN
    THEN BEGIN
      IF BLOSTU OF A_BLO_BLOC <> "T"
      THEN LET BLOSTU OF A_BLO_BLOC = "I"
    END

    IF NEWRECORD OF PDCOMMAN_ASSIGN
    THEN BEGIN
      IF BLOSTU OF A_BLO_BLOC <> "T"
      THEN LET BLOSTU OF A_BLO_BLOC = "A"
    END
     ;
    ; Répercuter les modifications dans le fichier PDBLOC
    ;
    PUT A_BLO_BLOC
  END ; Fin du FOR
END


BUILD

@IF FORMAT

12345678901234567890123456789012345678901234567890123456789012345678901234567890
2xxx     1         2xxxxxxxxx3xxxxxxxxx4xxxxxxxxx5xxxxxxxxx6         7         x
3******************************** Assignation **********************************
4 Num. commande.: xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
5 Projet........: xx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
6 Client........: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
7                                                                              *
8********************************** Blocs **************************************
9                            Code                                   Erreur     *
0           No bloc          granit   Long.    Haut.    Épais.       O/N       *
1                                                                              *
2           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
3           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
4           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
5           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
6           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
7           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
8           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
9           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
0           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
1           xxxx-xxxxx-x      xxx      xxxx     xxxx      xxxx        x        *
2                                                                              *
3*******************************************************************************
@ENDIF
