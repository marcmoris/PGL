;/T/ Gerrer les tranches (Facture)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-27
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur la facture d'une tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd181k ACTIONBAR                     &
             MODE LABEL "" AT 2,80          &
             NOACTION                       &
             ACTIVITIES FIND                &
             FIELDMARK RETAIN STARTUP       &
             HELP POPUP FROM 4,9 TO 20,72   &
             RECEIVING  T_CIECLEMNU ,       &
                        T_CIENOM ,          &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD181K"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd181k.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Tranche facture
;

FILE PDTRANCHE_FAC PRIMARY &
                   NOITEM &
                   NODELETE &
                   NOAPPEND

  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE


         ITEM CIECLE OF PDTRANCHE_FAC INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Facture
;

FILE PDFACTURE     REFERENCE

  ACCESS VIA     CIECLE,                       &
                 FTRNUMFAC                     &
         USING   T_CIECLEMNU,                  &
                 FTRNUMFAC OF PDTRANCHE_FAC


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Facture " &
@ELSE
      " %%%Facture " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO LINE 5

ALIGN (2,3,19)


FIELD FTRNUMFAC        OF PDTRANCHE_FAC    &
label "No. facture...:"&
        HIDDEN ID 05                       &
        DISPLAY


FIELD FTRDATFAC        OF PDFACTURE         FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date facture..:"&
        HIDDEN ID 10                       &
        DISPLAY


FIELD TRFPRIVEN        OF PDTRANCHE_FAC    &
label "Prix..........:"&
        HIDDEN ID 15                       &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Facture ************************************
*                                                                              *
* No. facture...: xxxxxxxxx                                                    *
* Date facture..: xxxxxxxx                                                     *
* Prix..........: xxxxxxxxxxxxxxx                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
