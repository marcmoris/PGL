;/T/ Liste des blocs expédiés par type de lot
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Modifié par André Bilodeau 22/04/94
; -  Ajout du champ date transporté
;
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme et pour que le rapport sorte par type de
;                     lot au lieu de par bateau.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 01 mai 2000
;    Description......: Remplacer le type de lot par la description de ce dernier.
;
;    Référence........: Demande directe 999999.
;
;    Signet...........: MP-00-05-01_DD999999.
;
;    ----------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 05 mai 2000
;    Description......: Ajuster le format de rapport afin espacer les informations
;                       relative au T/M Final et au numéro d'expédition.
;
;    Référence........: Demande directe 999999 - Demandeur: Jacky Lecarlme.
;
;    Signet...........: MP-00-05-05_DD999999.
;
;-----------------------------------------------------------------------------------------------------------------------------------


USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC &
  LINK CIECLE OF IFBLOC, &
       IFLCLE OF IFBLOC  &
    TO CIECLE, &
       IFLCLE                   OF IFLOT &
  LINK CIECLE OF IFLOT, &
       TYLCLE OF IFLOT   &
    TO CIECLE, &
       TYLCLE                    OF IFTYPE_LOT


CHOOSE CIECLE PARM, &
       IFLCLE PARM

DEFINE TMP-DAT-DEB     DATE = PARM
DEFINE TMP-DAT-FIN     DATE = PARM

SELECT IF    (     BLCDAT >= TMP-DAT-DEB &
               AND BLCDAT <= TMP-DAT-FIN &
             )  &
          OR (     TMP-DAT-DEB = 0 &
               AND TMP-DAT-FIN = 0 &
             )
;

DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT

SORT ON IFLCLE &
     ON D_NUMCOM &
     ON D_NUMBLC

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" &
                        IF     TMP-DAT-DEB = 0 &
                           AND TMP-DAT-FIN = 0 &
        ELSE "Entre le : " + ASCII( TMP-DAT-DEB , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[7:2] + &
             " et : "      + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF308"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 3   "NO LOT: "          &
             TAB 11  IFLCLE OF IFBLOC    &
             TAB 23  IFLDSC OF IFLOT     & ; MP-00-05-01_DD999999.
  SKIP 3 &

TAB 006 "NO                             DIMENSIONS       TOTAL         T/M        NO       NO        DATE    " & ; MP-00-05-05_DD999999.
SKIP 1 &
TAB 005 "BLOC           CAT COUL QUAL   LONG HAUT LARG      M3          FINAL     EXPED.   FACT.   TRANSPORTÉ" & ; MP-00-05-05_DD999999.
SKIP 1 &
TAB 003 "_______________  ___ ____ ____   ______________    ______       ______     _____   ______   ________" & ; MP-00-05-05_DD999999.
SKIP 2

REPORT                         &
TAB 003 D_NUMBLC               &
TAB 020 CATCLE       OF IFBLOC &
TAB 024 CLRCLE       OF IFBLOC &
TAB 029 QLTCLE       OF IFBLOC &
TAB 036 BLCVENDULONG OF IFBLOC &
TAB 041 BLCVENDUHAU  OF IFBLOC &
TAB 046 BLCVENDULAR  OF IFBLOC &
TAB 054 BLCM3TOT     OF IFBLOC &
TAB 064 BLCTFINAL    OF IFBLOC & ; MP-00-05-05_DD999999.
TAB 077 EXNCLE       OF IFBLOC & ; MP-00-05-05_DD999999.
TAB 086 FTRNUMFAC    OF IFBLOC & ; MP-00-05-05_DD999999.
TAB 095 BLCDATREC    OF IFBLOC & ; MP-00-05-05_DD999999.
SKIP 2


FOOTING AT D_NUMCOM &
        TAB 051 "=========     =========" &
SKIP 1  &
        TAB 1 COUNT "Blocs"      &
        TAB 37 "Sous-total:"     &
        TAB 51 BLCM3TOT  SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 64 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 2

FOOTING AT IFLCLE &
        TAB 051 "=========     =========" &
SKIP 1  &
        TAB 1 COUNT "Blocs"  &
        TAB 42 "Total:"      &
        TAB 51 BLCM3TOT  SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 64 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP PAGE

BUILD  if308z
