;/T/ Gestion des acheteurs - Validations d'intégrité en cas de destruction du code d'acheteur.
;
;/P/ Réalisé par.......: Marc Poulin.
;    Date..............: 08 mars 2000.
;    Description.......: Création d'une gestion des acheteurs afin de cibler plus rapidement ceux qui initient les commandes et qui les réceptionnent.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 14.
;    
;    Signet............: MP-00-03-08_S14.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar158x NOMODE                 &
              ACTION LABEL " "       &
              FROM 23,70 TO 23,80    &
              RECEIVING ARACHETEUR,  &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.

;-------------------------------------------------------------------------------
;
; Table des acheteurs.
;
FILE ARACHETEUR   MASTER

;-------------------------------------------------------------------------------
;
; Validation d'intégrité sur la table des commandes d'achat.
;
FILE ARCOMMANDE  REFERENCE

;-------------------------------------------------------------------------------
;
; Validation d'intégrité sur la table des receptions.
;
FILE ARRECEPTION REFERENCE


;-------------------------------------------------------------------------------
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN

; Validation d'intégrité sur la table des commandes d'achat.

  GET ARCOMMANDE VIA ACHCLE &
               USING ACHCLE OF ARACHETEUR OPTIONAL
  IF ACCESSOK
  THEN ERROR = "Destruction annulée, l'acheteur est reféré dans une commande d'achat"


; Validation d'intégrité sur la table des réceptions.

  GET ARRECEPTION VIA ACHCLE &
                USING ACHCLE OF ARACHETEUR OPTIONAL
  IF ACCESSOK
  THEN ERROR = "Destruction annulée, l'acheteur est reféré dans une réception"

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END


BUILD
