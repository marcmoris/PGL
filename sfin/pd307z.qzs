;/T/ 1.4.5.1 Liste des bons de commande
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1996-05-31
;
;    Description...: Liste de base des bons de commande
;
;    Paramètres....: Numéro de bon de commande
;
;    Particularités: AUCUNE
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS PDBON_COMMANDE &

  LINK CIECLE          OF PDBON_COMMANDE,       &
       BCMNUMBCM       OF PDBON_COMMANDE        &
    TO CIECLE,                                  &
       BCMNUMBCM       OF PDDET_BON_COMMAN      &

  LINK CIECLE          OF PDBON_COMMANDE,       &
       COMNUMCOM       OF PDBON_COMMANDE        &
    TO CIECLE,                                  &
       COMNUMCOM                                OF PDCOMMAN_INTERNE OPTIONAL &

  LINK CIECLE          OF PDBON_COMMANDE,       &
       PJTNUMPRO       OF PDBON_COMMANDE        &
    TO CIECLE,                                  &
       PJTNUMPRO                                OF PDPROJET         OPTIONAL  &

  LINK FOUCLE          OF PDBON_COMMANDE,       &
       FOCCIECLE       OF PDBON_COMMANDE        &
    TO FOUCLE,                                  &
       FOCCIECLE                                OF VFOC_FOU         OPTIONAL  &

  LINK DEVCLE          OF VFOC_FOU              &
    TO DEVCLE                                   OF MCDEVISE         OPTIONAL  &

  LINK FOUCIECLE       OF VFOC_FOU,             &
       FOUREFTYP       OF VFOC_FOU,             &
       FOUCLE          OF VFOC_FOU,             &
       FOCADRACH       OF VFOC_FOU              &
    TO REFCIECLE,                               &
       REFCLETYP,                               &
       REFCLENUM,                               &
       RADCLE                                   OF MCREF_ADRESSE OPTIONAL &

  LINK CIECLE           OF PDBON_COMMANDE       &
    TO CIECLE                                   OF MCCOMPAGNIE &

  LINK TERCLE           OF PDBON_COMMANDE       &
    TO TERCLE                                   OF CPTERME OPTIONAL &

  LINK ("BCMFRSCOU",                            &
       BCMFRSCOU OF PDBON_COMMANDE)             &
    TO XELNOM,                                  &
       CBICLE                                   OF MCCODE_BILINGUE ALIAS A_FRSCOU OPTIONAL &

  LINK ("BCMFRSTRPTER",                         &
       BCMFRSTRPTER OF PDBON_COMMANDE)          &
    TO XELNOM,                                  &
       CBICLE                                   OF MCCODE_BILINGUE ALIAS A_FRSTRPTER OPTIONAL &

  LINK ("BCMFRSTRPMAR",                         &
       BCMFRSTRPMAR OF PDBON_COMMANDE)          &
    TO XELNOM,                                  &
       CBICLE                                   OF MCCODE_BILINGUE ALIAS  A_FRSTRPMAR OPTIONAL &

  LINK ("F",                                    &
       BCMTAXCODTPS)                            &
    TO TAXCLETYP,                               &
       TAXCLECOD                                OF MCTAXE ALIAS A_TPS OPTIONAL &

  LINK ("P",                                    &
       BCMTAXCODTVQ)                            &
    TO TAXCLETYP,                               &
       TAXCLECOD                                OF MCTAXE ALIAS A_TVP OPTIONAL &

  LINK CIECLE OF PDDET_BON_COMMAN,TGRCODGRA OF PDDET_BON_COMMAN            &
    TO CIECLE,TGRCODGRA                         OF PDTYPE_GRANIT 


CHOOSE CIECLE PARM , BCMNUMBCM PARM


SELECT PDBON_COMMANDE IF BCMCODTYPBCM OF PDBON_COMMANDE = "A"


DEFINE D_BCMNUMBCM     CHARACTER SIZE 09 = ASCII(BCMNUMBCM OF PDBON_COMMANDE,9)

DEFINE D_APPROUVE      CHARACTER SIZE 45 = "Approuvé par: _______________________________" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Approved by: ________________________________"

DEFINE D_COMMANDE_PAR  CHARACTER SIZE 40 = "Commandé par: " + BCMCMDPAR OF PDBON_COMMANDE IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Ordered by : "  + BCMCMDPAR OF PDBON_COMMANDE 

DEFINE D_NO_COMMANDE   CHARACTER SIZE 18 = "NO. DE COMMANDE : " IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "PURCHASE ORDER #: "

; --------------------------------

DEFINE D_LIBEL_CLI     CHARACTER SIZE 25 = "Adressez votre facture à:"   IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Send your invoice to:"     

DEFINE D_LIBEL_FOU     CHARACTER SIZE 23 = "Adresse du fournisseur:"     IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Supplier address:"       

DEFINE D_LIBEL_DEV     CHARACTER SIZE 15 = "Devise........:"             IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Currency......:"     

DEFINE D_DEVDSC        CHARACTER SIZE 15 = DEVDSCFRA OF MCDEVISE         IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE DEVDSCANG OF MCDEVISE

DEFINE D_LIBEL_REF     CHARACTER SIZE 15 = "Référence.....:"             IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Reference.....:"     


DEFINE D_NO_TAXE_PRO   CHARACTER SIZE 25 = "# Taxe provinciale......: "  IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Provincial sales tax no.:"     

DEFINE D_NO_TAXE_FED   CHARACTER SIZE 25 = "# Taxe fédérale.........:"   IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Federal sales tax no....:"     

DEFINE D_CODUNM_X      CHARACTER SIZE 02 = "M3" IF PODCODUNM OF PDDET_BON_COMMAN = " " AND TPDTYPPRD OF PDDET_BON_COMMAN = "BL" &
                                      ELSE "M2" IF PODCODUNM OF PDDET_BON_COMMAN = " " AND TPDTYPPRD OF PDDET_BON_COMMAN = "TR" &
                                      ELSE PODCODUNM OF PDDET_BON_COMMAN

DEFINE D_CODUNM        CHARACTER SIZE 02 = D_CODUNM_X IF FOULNG OF VFOC_FOU = "F"  &
                                      ELSE "CM"       IF D_CODUNM_X         = "M3" &
                                      ELSE "SM"       IF D_CODUNM_X         = "M2" &
                                      ELSE "CF"       IF D_CODUNM_X         = "P3" &
                                      ELSE "SF"       IF D_CODUNM_X         = "P2" 

DEFINE D_MSG_01        CHARACTER SIZE 132 = "1-Veuillez accuser réception de cette demande en confirmant les prix et la date de livraison"                                        IF FOULNG OF VFOC_FOU = "F" &
                                       ELSE "1-Acknowledge receipt of this order confirming prices and delivery date."
DEFINE D_MSG_02        CHARACTER SIZE 132 = "2-Nous n'acceptons aucun changement et aucune substitution sans autorisation."                                                       IF FOULNG OF VFOC_FOU = "F" &
                                       ELSE "2-No substitutions or changes will be accepted without a written authorization."
DEFINE D_MSG_03        CHARACTER SIZE 132 = "3-Nous réservons le droit d'annuler cette commande si l'expédition n'est pas conforme pour les quantités, prix ou date de livraison" IF FOULNG OF VFOC_FOU = "F" &
                                       ELSE "3-We reserve the right to cancel this order if shipment is not made in quantities, prices and delivery shown."
DEFINE D_MSG_04        CHARACTER SIZE 132 = "4-Les numéros de licence pour la taxe de vente doivent s'appliquer lorsqu'ils sont indiqués."                                        IF FOULNG OF VFOC_FOU = "F" &
                                       ELSE "4-Sales taxe licence numbers are applicable when shown."


DEFINE D_PODLONG       CHARACTER SIZE 10 = CENTER(PODLONG OF PDDET_BON_COMMAN) IF PODCODUNM OF PDDET_BON_COMMAN = "P2" OR PODCODUNM OF PDDET_BON_COMMAN = "P3"
DEFINE D_PODHAUT       CHARACTER SIZE 10 = CENTER(PODHAUT OF PDDET_BON_COMMAN) IF PODCODUNM OF PDDET_BON_COMMAN = "P2" OR PODCODUNM OF PDDET_BON_COMMAN = "P3"
DEFINE D_PODLARG       CHARACTER SIZE 10 = CENTER(PODLARG OF PDDET_BON_COMMAN) IF PODCODUNM OF PDDET_BON_COMMAN = "P2" OR PODCODUNM OF PDDET_BON_COMMAN = "P3"

DEFINE D_PODLON        INTEGER   SIZE 04 = PODLON OF PDDET_BON_COMMAN          IF PODCODUNM OF PDDET_BON_COMMAN = "M2" OR PODCODUNM OF PDDET_BON_COMMAN = "M3" OR PODCODUNM OF PDDET_BON_COMMAN = " "
DEFINE D_PODHAU        INTEGER   SIZE 04 = PODHAU OF PDDET_BON_COMMAN          IF PODCODUNM OF PDDET_BON_COMMAN = "M2" OR PODCODUNM OF PDDET_BON_COMMAN = "M3" OR PODCODUNM OF PDDET_BON_COMMAN = " "
DEFINE D_PODEPA        INTEGER   SIZE 04 = PODEPA OF PDDET_BON_COMMAN          IF PODCODUNM OF PDDET_BON_COMMAN = "M2" OR PODCODUNM OF PDDET_BON_COMMAN = "M3" OR PODCODUNM OF PDDET_BON_COMMAN = " "

; --------------------------------

DEFINE D_LIBEL_TELGRA  CHARACTER SIZE 15 = "Téléphone.....:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Phone.........:" 
                                                             
DEFINE D_LIBEL_TELFOU  CHARACTER SIZE 15 = "Téléphone.....:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Phone.........:" 
                                                             
DEFINE D_LIBEL_FAXGRA  CHARACTER SIZE 15 = "Télécopieur...:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Fax...........:" 
                                                             
DEFINE D_LIBEL_FAXFOU  CHARACTER SIZE 15 = "Télécopieur...:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Fax...........:" 
                                                             
DEFINE D_LIBEL_PJT     CHARACTER SIZE 15 = "Projet........:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Project.......:" 
                                                             
DEFINE D_LIBEL_DESCRIP CHARACTER SIZE 15 = "Description...:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Description...:" 
                                                             
DEFINE D_LIBEL_FRSCOU  CHARACTER SIZE 15 = "Frs. courtage.:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Brokerage.....:" 
                                                             
DEFINE D_LIBEL_FRSTRPT CHARACTER SIZE 15 = "Frs. trp. ter.:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Land Transport:" 
                                                             
DEFINE D_LIBEL_FRSTRPM CHARACTER SIZE 15 = "Frs. trp. mar.:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Sea transport.:" 
                                                             
DEFINE D_LIBEL_TPS     CHARACTER SIZE 15 = "Taxe Fédéral..:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Federal tax...:" 
                                                             
DEFINE D_LIBEL_TVP     CHARACTER SIZE 15 = "Taxe prov.....:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Prov. tax.....:" 
                                                             
DEFINE D_LIBEL_TERME   CHARACTER SIZE 15 = "Crédit........:" IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Credit........:" 
                                                                                    
DEFINE D_LIBEL_MNTEST  CHARACTER SIZE 18 = "Montant estimé...:"   IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE "Estimated price..:"                     
                                                                                    
DEFINE D_TERME         CHARACTER SIZE 40 = TERDSCFRA OF CPTERME   IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE TERDSCANG OF CPTERME                     
                                                                                    
DEFINE D_FRSCOU        CHARACTER SIZE 40 = CBIDSCFRA OF A_FRSCOU  IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE CBIDSCANG OF A_FRSCOU                    
                                                                                    
DEFINE D_FRSTRPTER     CHARACTER SIZE 40 = CBIDSCFRA OF A_FRSTRPTER IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE CBIDSCANG OF A_FRSTRPTER                 
                                                                                    
DEFINE D_FRSTRPMAR     CHARACTER SIZE 40 = CBIDSCFRA OF A_FRSTRPMAR IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE CBIDSCANG OF A_FRSTRPMAR                 
                                                                                    
DEFINE D_DSCTAXTPS     CHARACTER SIZE 40 = TAXDSCFRA OF A_TPS IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE TAXDSCANG OF A_TPS 
                                                              
DEFINE D_DSCTAXTVP     CHARACTER SIZE 40 = TAXDSCFRA OF A_TVP IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE TAXDSCANG OF A_TVP 
                                                                                    
DEFINE D_TGRDSCFRA001  CHARACTER SIZE 30 = TGRDSCFRA001 OF PDTYPE_GRANIT IF "F" = FOULNG OF VFOC_FOU &
                                      ELSE TGRDSCANG001 OF PDTYPE_GRANIT 
                                                                         
DEFINE D_QTECMD        FLOAT     SIZE 08 = ROUND((PODVOL OF PDDET_BON_COMMAN * PODQTECMD OF PDDET_BON_COMMAN),4) IF PODCODUNM OF PDDET_BON_COMMAN = "P3" OR &
                                                                                                                    PODCODUNM OF PDDET_BON_COMMAN = "P2"    &
                                      ELSE PODVOLCMD OF PDDET_BON_COMMAN                                         IF TPDTYPPRD OF PDDET_BON_COMMAN = "BL"    &
                                      ELSE PODSURCMD OF PDDET_BON_COMMAN


; --------------------------------

DEFINE D_MNTCMD        FLOAT     SIZE 08 = ROUND(PODVOLCMD OF PDDET_BON_COMMAN * PODPRI OF PDDET_BON_COMMAN,0)                                IF  TPDTYPPRD OF PDDET_BON_COMMAN = "BL"  AND &
                                                                                                                                                 (PODCODUNM OF PDDET_BON_COMMAN = "M3"   OR &
                                                                                                                                                  PODCODUNM OF PDDET_BON_COMMAN = "  ")     &
                                      ELSE ROUND(PODSURCMD OF PDDET_BON_COMMAN * PODPRI OF PDDET_BON_COMMAN,0)                                IF  TPDTYPPRD OF PDDET_BON_COMMAN = "TR"  AND &
                                                                                                                                                 (PODCODUNM OF PDDET_BON_COMMAN = "M2"   OR &
                                                                                                                                                  PODCODUNM OF PDDET_BON_COMMAN = "  ")     &
                                      ELSE ROUND(PODQTECMD OF PDDET_BON_COMMAN * (PODVOL OF PDDET_BON_COMMAN * PODPRI OF PDDET_BON_COMMAN),0) IF (TPDTYPPRD OF PDDET_BON_COMMAN = "BL"  AND &
                                                                                                                                                  PODCODUNM OF PDDET_BON_COMMAN = "P3")  OR &
                                                                                                                                                 (TPDTYPPRD OF PDDET_BON_COMMAN = "TR"  AND &
                                                                                                                                                  PODCODUNM OF PDDET_BON_COMMAN = "P2") 
; --------------------------------


DEFINE D_TITRE      CHARACTER SIZE 130 =  &
"Type         Type de granit      Fini    # Bloc    Longueur    Hauteur    Largeur  Quantité     Prix unitaire      Prix du bloc  " IF TPDTYPPRD OF PDDET_BON_COMMAN = "BL" AND "F" = FOULNG OF VFOC_FOU ELSE &
"Type          Granite type      Finish  Block no.   Lenght     Height      Width   Quantity      Unit price         Block price  " IF TPDTYPPRD OF PDDET_BON_COMMAN = "BL" AND "A" = FOULNG OF VFOC_FOU ELSE &
"Type         Type de granit      Fini    # Bloc    Longueur    Hauteur  Thickness  Quantité     Prix unitaire      Prix du bloc  " IF TPDTYPPRD OF PDDET_BON_COMMAN <>"BL" AND "F" = FOULNG OF VFOC_FOU ELSE &
"Type          Granite type      Finish  Block no.   Lenght     Height   Épaisseur  Quantity      Unit price         Block price  " IF TPDTYPPRD OF PDDET_BON_COMMAN <>"BL" 
 

DEFINE D_SOUS_LIGNE CHARACTER SIZE 130 = &
"----- ------------------------- ------ ---------- ---------- ---------- ---------- ---------    -------------     ---------------"


;-------------------------------------------------------------------------------

SORT ON BCMNUMBCM OF PDBON_COMMANDE    RESET PAGE &
     ON PODNUMLIG OF PDDET_BON_COMMAN

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Bon de commande" IF "F" = FOULNG OF VFOC_FOU &
                       ELSE "Purchase order"
@ELSE
       "Purchase order" IF "A" = FOULNG OF VFOC_FOU &
                       ELSE "Bon de commande"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 07 = "PD307Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER  SIZE 40 = PARM


DEF D_27        INTEGER = 27
DEF D_ESC       CHAR*01 = CHARACTERS(D_27) [2:1]
DEF D_NORMAL    CHAR*05 = (D_ESC + "(s0B")
DEF D_BOLD      CHAR*05 = (D_ESC + "(s3B")
DEF D_UNDERLINE CHAR*05 = (D_ESC + "&d0D")
DEF D_PLAIN     CHAR*04 = (D_ESC + "&d@")

USE ustitprd47_180 NOLIST
  SKIP 02                                        &
  TAB 001 D_LIBEL_FOU                            &
  TAB 062 D_LIBEL_CLI                            &
  SKIP 02                                        &
  TAB 001 FOUNOM           OF VFOC_FOU           &
  TAB 062 CIENOM           OF MCCOMPAGNIE        &
  SKIP                                           &
  TAB 001 RADADR1          OF MCREF_ADRESSE      &
  TAB 062 CIEADR1          OF MCCOMPAGNIE        &
  SKIP                                           &
  TAB 001 RADADR2          OF MCREF_ADRESSE      &
  TAB 062 CIEADR2          OF MCCOMPAGNIE        &
  SKIP                                           &
  TAB 001 RADADR3          OF MCREF_ADRESSE      &
  TAB 062 CIEADR3          OF MCCOMPAGNIE        &
  SKIP                                           &
  TAB 001 RADADR4          OF MCREF_ADRESSE      &
  TAB 062 CIEADR4          OF MCCOMPAGNIE        &
  SKIP                                           &
  TAB 001 RADCP            OF MCREF_ADRESSE      &
  TAB 062 CIECP            OF MCCOMPAGNIE        &
  SKIP 01                                        &
  TAB 001 "Contact.......:"                      &
  TAB 017 BCMCONTACT OF PDBON_COMMANDE           &
  SKIP 01                                        &
  TAB 001 D_LIBEL_TELFOU                         &
  TAB 017 RADTEL           OF MCREF_ADRESSE      &
  TAB 062 D_LIBEL_TELGRA                         &
  TAB 078 CIETEL           OF MCCOMPAGNIE        &
  SKIP                                           &
  TAB 001 D_LIBEL_FAXFOU                         &
  TAB 017 RADFAX           OF MCREF_ADRESSE      &
  TAB 062 D_LIBEL_FAXGRA                         &
  TAB 078 "(418)878-6020"                        &
  SKIP                                           &
  TAB 001 "-----------------------------------------------------------" &
  TAB 062 "----------------------------------------------------------------------" &
  SKIP                                           &
  TAB 001 D_LIBEL_FRSCOU                         &
  TAB 017 D_FRSCOU                               &
  TAB 062 D_LIBEL_TERME                          &
  TAB 078 D_TERME                                &
  SKIP                                           &
  TAB 001 D_LIBEL_FRSTRPT                        &
  TAB 017 D_FRSTRPTER                            &
  TAB 062 D_LIBEL_TPS                            &
  TAB 078 D_DSCTAXTPS                            &
  SKIP                                           &
  TAB 001 D_LIBEL_FRSTRPM                        &
  TAB 017 D_FRSTRPMAR                            &
  TAB 062 D_LIBEL_TVP                            &
  TAB 078 D_DSCTAXTVP                            &
  SKIP                                           &
  TAB 001 D_LIBEL_DESCRIP                        &
  TAB 017 BCMDSC001        OF PDBON_COMMANDE     &
  TAB 062 D_LIBEL_DEV                            &
  TAB 078 D_DEVDSC                               &
  SKIP                                           &
  TAB 017 BCMDSC002        OF PDBON_COMMANDE     &
  TAB 062 D_LIBEL_REF                            &
  TAB 078 PJTABR           OF PDBON_COMMANDE     &
  TAB 080 "-"                                    &
  TAB 081 PJTANN           OF PDBON_COMMANDE     &
  TAB 084 "-"                                    &
  TAB 085 COMNUMCOMINT     OF PDBON_COMMANDE     &
  TAB 092 COMNOM           OF PDCOMMAN_INTERNE   &
  SKIP                                           &
  TAB 001 "___________________________________________________________________________________________________________________________________" &
  SKIP 02                                        &
  TAB 001 D_TITRE                                &
  SKIP                                           &
  TAB 001 D_SOUS_LIGNE                           &
  SKIP 01

REPORT                                           &
    TAB 001 TPDTYPPRD    OF PDDET_BON_COMMAN     &
    TAB 007 D_TGRDSCFRA001                       &
    TAB 035 TYFCODFIN    OF PDDET_BON_COMMAN     &
    TAB 040 PODBLOC      OF PDDET_BON_COMMAN     &
    TAB 051 D_PODLONG                            &
    TAB 062 D_PODHAUT                            &
    TAB 073 D_PODLARG                            &
    TAB 054 D_PODLON                             &
            PICTURE "^^^^"                       &
            BWZ                                  &
    TAB 065 D_PODHAU                             &
            PICTURE "^^^^"                       &
            BWZ                                  &
    TAB 076 D_PODEPA                             &
            PICTURE "^^^^"                       &
            BWZ                                  &
    TAB 084 D_QTECMD                             &
            OUTPUT SCALE 4                       &
            SIGNIFICANCE 6                       &
            PICTURE "^^^.^^^^"                   &
    TAB 094 D_CODUNM                             & 
    TAB 098 PODPRI       OF PDDET_BON_COMMAN     &
            PICTURE " ^^^,^^^.^^ "               &
            FLOAT "$"                            &
            TRAILING SIGN "-"                    &
    TAB 110 "/"                                  & 
    TAB 111 D_CODUNM                             &
    TAB 115 D_MNTCMD                             &
            PICTURE " ^^,^^^,^^^.^^ "            &
            FLOAT "$"                            &
            TRAILING SIGN "-"                    &
 RESERVE 2 LINES


PAGE FOOTING                                     &
  SKIP 02                                        &
  TAB 001 D_NO_TAXE_FED                          &
  TAB 027 CIEENRTPS OF MCCOMPAGNIE               &
  TAB 059 D_NO_TAXE_PRO                          &
  TAB 085 CIEENRTVQ OF MCCOMPAGNIE               &
  SKIP 02                                        &
  TAB 001 D_MSG_01                               &
  SKIP                                           &
  TAB 001 D_MSG_02                               &
  SKIP                                           &
  TAB 001 D_MSG_03                               &
  SKIP                                           &
  TAB 001 D_MSG_04                               &
  SKIP 02                                        &
  TAB 001 D_APPROUVE                             &
  TAB 052 D_COMMANDE_PAR                         &
  TAB 101 D_BOLD                                 &
  TAB 106 D_UNDERLINE                            &
  TAB 111 D_NO_COMMANDE                          &
  TAB 129 D_BCMNUMBCM                            &
  TAB 138 D_PLAIN                                &
  TAB 143 D_NORMAL                               &
 RESERVE 8 LINES                                
                                                
                                                
FOOTING AT BCMNUMBCM                             &
  SKIP 2                                         &
  TAB 091 D_LIBEL_MNTEST                         &
  TAB 111 D_MNTCMD SUBTOTAL                      &
            PICTURE " ^^,^^^,^^^,^^^.^^ "        &
            FLOAT "$"                            &
            TRAILING SIGN "-"                    &
  SKIP PAGE



BUILD pd307z


@IF FORMAT
PD307Z                                                     GRANICOR INC.                                             PAGE:   9
xxxxxxxx                                                  Bon de commande                                        99/99/99  99:99


Adresse du fournisseur:                                      Adressez votre facture à:

XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                     GRANICOR INC.
1229 STE-JULIE, C.P. 1434                                    300 ROTTERDAM
TROIS-RIVIERES                                               SAINT-AUGUSTIN
QUEBEC                                                       QUEBEC
CANADA                                                       CANADA
G9A 5L2                                                      G3A 1T4
Contact.......: XXXXXXXXXXXXXXXXXXXXXXXX    
Téléphone.....: (999)999-9999                                Téléphone.....: (418)878-3530
Télécopieur...: (999)999-9999                                Télécopieur...: (418)878-6020
-----------------------------------------------------------  ----------------------------------------------------------------------
Frs. courtage.: Inclus                                       Crédit........: HABITUELLES
Frs. trp. ter.: Inclus                                       Taxe Fédéral..: T.P.S. 7% EN SUS
Frs. trp. mar.: Exclus                                       Taxe prov.....: T.V.Q. 7.5% EN SUS
Description...: XXXX XX-XXXXXX                               Devise........: DOLLAR CANADIEN
                                                             Référence.....: XX-99 -
___________________________________________________________________________________________________________________________________

Type          Granite type      Finish  Block no.   Lenght     Height      Width   Quantity      Unit price         Block price     ; (anglais)
Type         Type de granit      Fini    # Bloc    Longueur    Hauteur    Largeur  Quantité     Prix unitaire      Prix du bloc     ; (français)
----- ------------------------- ------ ---------- ---------- ---------- ---------- ---------    -------------     ---------------   
xx123 xxxxxxxxxxxxxxxxxxxxxxxxx   xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx 9999.9999 xx  $999,999.99-/xx  $99,999,999.99-
xx123 xxxxxxxxxxxxxxxxxxxxxxxxx   xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx 9999.9999 xx  $999,999.99-/xx  $99,999,999.99-
xx123 xxxxxxxxxxxxxxxxxxxxxxxxx   xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx 9999.9999 xx  $999,999.99-/xx  $99,999,999.99-
xx123 xxxxxxxxxxxxxxxxxxxxxxxxx   xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx 9999.9999 xx  $999,999.99-/xx  $99,999,999.99-
xx123 xxxxxxxxxxxxxxxxxxxxxxxxx   xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx 9999.9999 xx  $999,999.99-/xx  $99,999,999.99-
xx123 xxxxxxxxxxxxxxxxxxxxxxxxx   xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx 9999.9999 xx  $999,999.99-/xx  $99,999,999.99-

                                                                                          Montant estimé...:  $99,999,999,999.99- 





# Taxe fédérale.........: 142152875 RT                    # Taxe provinciale......: 10207001869TQ0001

1-Veuillez accuser réception de cette demande en confirmant les prix et la date de livraison
2-Nous n'acceptons aucun changement et aucune substitution sans autorisation.
3-Nous réservons le droit d'annuler cette commande si l'expédition n'est pas conforme pour les quantités, prix ou date de livraison
4-Les numéros de licence pour la taxe de vente doivent s'appliquer lorsqu'ils sont indiqués.

Approuvé par: _______________________________      Commandé par: CLAUDE W. GRENON                   NO. DE COMMANDE : 999999999
@ENDIF
