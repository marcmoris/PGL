;/T/ Sommaire sous-projet
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 13 Oct 1993
;
;    Description..: Sommaire projet
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr015 ACTIONBAR &
             MODE LABEL "" AT 2,132 &
             NOACTION FIELDMARK RETAIN STARTUP &
             FROM 1,1 TO 23,132 &
             ACTIVITIES FIND &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM, &
                       T_PROCLEFND , &
                       T_ACTCLEFND

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_ACTCLEFND CHARACTER SIZE 6  RESET AT STARTUP
TEMPORARY T_PROCLEFND CHARACTER SIZE 8  RESET AT STARTUP

USE ussectmp NOLIST     ; Variable pour la securite
    "PR015"             ; Nom de l'écran

USE pr015.hlp nolist ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

FILE GPACT_TRANS  PRIMARY OCCURS 15 TIMES

; Attention la procédure path et find sont écrite
;-------------------------------------------------------------------------------
;
; Validation du compte comptable.
;
FILE VCPT_DSC         REFERENCE &
                      OCCURS WITH GPACT_TRANS

  ACCESS  VIA   CPTCLE &
          USING CPTCLE OF GPACT_TRANS

;-------------------------------------------------------------------------------
;
; Validation de l'unité administrative.
;
FILE MCUNITE_ADM      REFERENCE &
                      OCCURS WITH GPACT_TRANS

  ACCESS  VIA   CIECLE, &
                UNACLE &
          USING T_CIECLEMNU, &
                UNACLE OF GPACT_TRANS

;-------------------------------------------------------------------------------
;
; Validation du projet.
;
FILE GPPROJET         REFERENCE

  ACCESS VIA CIECLE, &
             PROCLE  &
         USING T_CIECLEMNU, &
               PROCLE OF GPACT_TRANS

;-------------------------------------------------------------------------------
;
; Validation l'activité
;
FILE GPACTIVITE         REFERENCE

  ACCESS VIA CIECLE, &
             PROCLE,  &
             ACTCLE  &
         USING T_CIECLEMNU, &
               PROCLE OF GPACT_TRANS, &
               ACTCLE OF GPACT_TRANS


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_PROCLE CHARACTER SIZE 8
TEMPORARY T_PROSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.
TEMPORARY T_ACTCLE CHARACTER SIZE 3
TEMPORARY T_ACTSTUPAT CHARACTER SIZE 5 ; Popup sur les activités.
TEMPORARY T_CPTCLE CHARACTER SIZE 6
TEMPORARY T_UNACLE CHARACTER SIZE 8

TEMPORARY T_MNT    INTEGER   SIZE 4   ; Temporaire en attend des achats.


;-------------------------------------------------------------------------------
;
DRAW 3,01 TO 23,132
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans
SKIP
TITLE &
@IF FRANCAIS
      " Sommaire des transactions au sous-projet " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF



SKIP
ALIGN (,2,15)(,,24)
FIELD PROCLE  OF GPACT_TRANS LABEL "Projet.....:" DISPLAY &
   LOOKUP ON GPPROJET &
   MESSAGE 2511 ; Ce projet n'existe pas

FIELD PRODSC OF GPPROJET DISPLAY  NOLABEL

FIELD ACTCLE  OF GPACT_TRANS LABEL "Sous-projet:" DISPLAY &
   LOOKUP ON GPACTIVITE &
   MESSAGE 2512 ; Ce activité n'existe pas
FIELD ACTDSC1 OF GPACTIVITE DISPLAY  NOLABEL
SKIP

TITLE "Unité" AT ,2
SKIP
TITLE "Adm.     Desc.                Compte Desc.                       Budget      Engagement           Réel          Total     Écart" AT ,2
SKIP
ALIGN (2,2,2)(,,11)(,,32)(,,39) (,,60) (,,75) (,,90)(,,105)(,,120)
;xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx Xxxxxxxxxxxxxx Xxxxxxxxxxxxxx Xxxxxxxxxxxxxx Xxxxxxxxxxxxxx xxxxxxxxx
CLUSTER OCCURS WITH GPACT_TRANS
  FIELD UNACLE OF GPACT_TRANS DISPLAY LABEL " "
  FIELD UNANOM OF MCUNITE_ADM DISPLAY SIZE 20
  FIELD CPTCLE OF GPACT_TRANS DISPLAY
  FIELD CPTDSC OF VCPT_DSC    DISPLAY SIZE 20
  FIELD T_MNT DISPLAY SIGNIF 4         PICTURE "^^,^^^,^^^.^^ "
  FIELD T_MNT DISPLAY SIGNIF 4         PICTURE "^^,^^^,^^^.^^ "
  FIELD TRDMNT OF GPACT_TRANS DISPLAY  PICTURE "^^,^^^,^^^.^^ " SIGNIF 4
  FIELD TRDMNT OF GPACT_TRANS DISPLAY  PICTURE "^^,^^^,^^^.^^ " SIGNIF 4
  FIELD T_MNT DISPLAY SIGNIF 4         PICTURE "^,^^^.^^ "
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END
;-------------------------------------------------------------------------------
;
;
; Popup sur les projets.
;
;
PROCEDURE INPUT PROCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROSTUPAT = "A"
    LET T_PROCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr101 MODE F PASSING T_CIECLEMNU, &
                                    T_PROCLE   , &
                                    T_PROSTUPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_PROCLE)
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT ACTCLE
BEGIN
  ;
  ; Popup sur les activités.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROCLE    = PROCLE OF GPACT_TRANS
    LET T_ACTSTUPAT = "A"
    LET T_ACTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr102 MODE F PASSING T_CIECLEMNU, &
                                    T_PROCLE   , &
                                    T_ACTCLE   , &
                                    T_ACTSTUPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_ACTCLE)
  END
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_PROCLE = PROCLE OF GPACT_TRANS
  LET T_ACTCLE = ACTCLE OF GPACT_TRANS
  LET T_CPTCLE = CPTCLE OF GPACT_TRANS
  LET T_UNACLE = UNACLE OF GPACT_TRANS
  RUN SCREEN pr011b PASSING T_CIECLEMNU, T_CIENOM, T_PROCLE, T_ACTCLE, &
                            T_UNACLE, T_CPTCLE MODE F &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

END

PROCEDURE PATH
BEGIN
  IF T_PROCLEFND = ""
  THEN BEGIN
     REQUEST PROCLE OF GPACT_TRANS
     IF PROMPTOK
       THEN REQUEST ACTCLE OF GPACT_TRANS
     IF PROMPTOK
       THEN REQUEST UNACLE OF GPACT_TRANS
     IF PROMPTOK
       THEN REQUEST CPTCLE OF GPACT_TRANS
     IF PROMPTOK
       THEN LET PATH = 1
     IF PATH = 0
       THEN BEGIN
         REQUEST PROCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN REQUEST ACTCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN REQUEST UNACLE OF GPACT_TRANS
         IF PROMPTOK
           THEN LET PATH = 2
         END
     IF PATH = 0
       THEN BEGIN
         REQUEST PROCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN REQUEST ACTCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN REQUEST CPTCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN LET PATH = 3
         END
     IF PATH = 0
       THEN BEGIN
         REQUEST PROCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN REQUEST ACTCLE OF GPACT_TRANS
         IF PROMPTOK
           THEN LET PATH = 4
         END
     IF PATH = 0
       THEN ERROR = "Key required."
  END
  ELSE BEGIN
    LET PATH = 99
  END
END

PROCEDURE FIND
BEGIN
  FOR GPACT_TRANS
  BEGIN
    IF PATH = 1
    THEN GET GPACT_TRANS VIA CIECLE , PROCLE , ACTCLE , UNACLE &
               , CPTCLE ORDERBY CIECLE , PROCLE , ACTCLE , UNACLE , &
               CPTCLE USING T_CIECLEMNU , PROCLE OF GPACT_TRANS , &
               ACTCLE OF GPACT_TRANS , UNACLE OF GPACT_TRANS , CPTCLE &
               OF GPACT_TRANS
    IF PATH = 2
    THEN GET GPACT_TRANS VIA CIECLE , PROCLE , ACTCLE , UNACLE &
               ORDERBY CIECLE , PROCLE , ACTCLE , UNACLE , CPTCLE &
               USING T_CIECLEMNU , PROCLE OF GPACT_TRANS , ACTCLE OF &
               GPACT_TRANS , UNACLE OF GPACT_TRANS
    IF PATH = 3
    THEN GET GPACT_TRANS VIA CIECLE , PROCLE , ACTCLE , CPTCLE &
               ORDERBY CIECLE , PROCLE , ACTCLE , UNACLE , CPTCLE &
               USING T_CIECLEMNU , PROCLE OF GPACT_TRANS , ACTCLE OF &
               GPACT_TRANS , CPTCLE OF GPACT_TRANS
    IF PATH = 4
    THEN GET GPACT_TRANS VIA CIECLE , PROCLE , ACTCLE ORDERBY &
               CIECLE , PROCLE , ACTCLE , UNACLE , CPTCLE USING &
               T_CIECLEMNU , PROCLE OF GPACT_TRANS , ACTCLE OF &
               GPACT_TRANS

    IF PATH = 99
    THEN GET GPACT_TRANS VIA CIECLE, PROCLE, ACTCLE &
         ORDERBY CIECLE , PROCLE, ACTCLE, UNACLE, CPTCLE &
            USING T_CIECLEMNU, T_PROCLEFND, T_ACTCLEFND
  END
END


BUILD
