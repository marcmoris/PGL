;/T/ 2.4.4. Enregistrer le débitage - Détail débitage diagramme (Prototype)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-11-30
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le débitage des pièces de granit.
;
;/M/ François Déry, 12 mai 1999
;       Enlever la table des séquences de débitages et la transaction car les 2
;       ne servent à rien.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd186c ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM,          &
                        PDDEBITAGE,        &
                        PDDET_DEBITAGE



;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD186C"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd186c.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calculd'un intervalle de temps
;
USE usdiftim NOLIST


;-------------------------------------------------------------------------------
;
; Débitage
;

FILE PDDEBITAGE       MASTER


;-------------------------------------------------------------------------------
;
; Détail débitage
;

FILE PDDET_DEBITAGE   MASTER


;-------------------------------------------------------------------------------
;
; Débitage diagramme
;

FILE PDDEB_DIAGRAMME PRIMARY &
                     NOITEM &
                     OCCURS 6

  ACCESS VIA     CIECLE,           &
                 DEBNUMRAP,        &
                 DDENUMLIG         &
         USING   T_CIECLEMNU,                         &
                 DEBNUMRAP        OF PDDEBITAGE,      &
                 DDENUMLIG        OF PDDET_DEBITAGE   &
         ORDERBY CIECLE,           &
                 DEBNUMRAP,        &
                 DDENUMLIG


  ITEM CIECLE    OF PDDEB_DIAGRAMME INITIAL T_CIECLEMNU
  ITEM DEBNUMRAP OF PDDEB_DIAGRAMME INITIAL DEBNUMRAP OF PDDEBITAGE FIXED


;-------------------------------------------------------------------------------
;
; Machine débitage
;

FILE PDMACH_DEBITAGE REFERENCE

  ACCESS VIA     CIECLE,          &
                 MDECODMAC        &
         USING   T_CIECLEMNU,     &
                 MDECODMAC        OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE DESIGNER NOITEM

  ACCESS VIA   CIECLE,      &
               TRANUMTRA002 &
         USING T_CIECLEMNU, &
               TRANUMTRA002 OF PDDET_DEBITAGE


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME DESIGNER NOITEM

  ACCESS VIA   CIECLE,          &
               DIANUM002        &
         USING T_CIECLEMNU,     &
               DIANUM002        OF PDDEB_DIAGRAMME


;-------------------------------------------------------------------------------
;
; Priorité diagramme
;

FILE PDPRIORITE_DIAG DESIGNER NOITEM

  ACCESS VIA   CIECLE,          &
               PRICODPRI,       &
               DIANUM002        &
         USING T_CIECLEMNU,                         &
               PRICODPRI        OF PDPRIORITE_DIAG, &
               DIANUM002        OF PDDEB_DIAGRAMME


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Débitage " &
@ELSE
      " %%%Débitage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19) (,43,59)


FIELD DEBNUMRAP        OF PDDEBITAGE       &
label "Numéro rapport:"&
        DISPLAY                            &
        FIXED


FIELD DEBDEP           OF PDDEBITAGE       &
label "Hre départ....:"&
        DISPLAY                            &
        FIXED


FIELD DEBDATPRD        OF PDDEBITAGE        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        DISPLAY                            &
        FIXED


FIELD DEBARR           OF PDDEBITAGE       &
label "Hre fin.......:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,24)


FIELD QRTCODQRT        OF PDDEBITAGE       &
label "Quart.........:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY                            &
        FIXED

FIELD MDECODMAC        OF PDDEBITAGE       &
label "Code machine..:"&
        DISPLAY                            &
        FIXED


FIELD MDEDSC001        OF PDMACH_DEBITAGE  &
        DISPLAY                            &
        FIXED


DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Tranche " &
@ELSE
      " %%%Tranche " &
@ENDIF
      CENTERED AT ,1


SKIP


TITLE &
@IF FRANCAIS
      "            Numéro           Numéro            Type           Type " &
@ELSE
      "%%%         Numéro           Numéro            Type           Type " &
@ENDIF
      AT ,2


SKIP


TITLE &
@IF FRANCAIS
      "            ligne            tranche           granit         fini " &
@ELSE
      "%%%         ligne            tranche           granit         fini " &
@ENDIF
      AT ,2


ALIGN (14,,15) (31,,31) (50,,50) (65,,65)


FIELD DDENUMLIG        OF PDDET_DEBITAGE   &
        HIDDEN ID 32


FIELD TRANUMTRA002     OF PDDET_DEBITAGE   &
        HIDDEN ID 34


FIELD TGRCODGRA        OF PDTRANCHE        &
        HIDDEN ID 36


FIELD TYFCODFIN        OF PDTRANCHE        &
        HIDDEN ID 38


DRAW 14,2 TO 14,79


SKIP TO LINE 14


TITLE &
@IF FRANCAIS
      " Diagramme " &
@ELSE
      " %%%Diagramme " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 15


TITLE &
@IF FRANCAIS
    " Numéro                        Epais  Epais  Qte                    " &
@ELSE
    "%%% Numéro                        Epais  Epais  Qte                    " &
@ENDIF
      AT ,2


SKIP


TITLE &
@IF FRANCAIS
    " diagramme Prio  Long   Haut   init   resul  fab    Ok  Commentaire " &
@ELSE
    "%%% diagramme Prio  Long   Haut   init   resul  fab    Ok  Commentaire " &
@ENDIF
      AT ,2


ALIGN (2,2,3) (14,,14) (19,,19) (26,,26) (33,,33) (40,,40) (47,,47) (54,,54) (58,,58)


CLUSTER OCCURS WITH PDDEB_DIAGRAMME ID BASE 50


FIELD DIANUM002        OF PDDEB_DIAGRAMME  &
        HIDDEN                             &
        LABEL " "


FIELD PRICODPRI        OF PDPRIORITE_DIAG  &
        HIDDEN                             &
        NUMERIC


FIELD DIALON           OF PDDIAGRAMME      &
        HIDDEN                             size 4


FIELD DIAHAU           OF PDDIAGRAMME      &
        HIDDEN                             size 4


FIELD DIAEPA           OF PDDIAGRAMME      &
        HIDDEN                             size 4


FIELD DDIEPARES        OF PDDEB_DIAGRAMME  &
        HIDDEN                             size 4


FIELD DDIQTEFAB        OF PDDEB_DIAGRAMME  &
        HIDDEN                             size 4


FIELD DDIACCERR        OF PDDEB_DIAGRAMME  &
        HIDDEN


FIELD DDICOM           OF PDDEB_DIAGRAMME  &
        HIDDEN                             &
        SIZE 20


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


BUILD

@IF FORMAT

2x                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    x
3--------------------------------- Débitage -----------------------------------+
4                                                                              |
5 Numéro rapport: 999999999              Hre départ....: HH:MM                 |
6 Date product..: AA/MM/JJ               Hre fin.......: HH:MM                 |
7 Code machine..: XXX  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                |
8 Quart.........: X    xxxxxxx                                                 |
9                                                                              |
0--------------------------Tranche --------------------------------------------+
1                                                                              |
2            Numéro           Numéro            Type           Type            |
3            ligne            tranche           granit         fini            |
4             9999            XXXXXXX            999            99             |
5                                                                              |
6----------------------------- Diagramme --------------------------------------|
7 Numéro                        Epais  Epais  Qte                              |
8 diagramme Prio  Long   Haut   init   resul  fab    Ok  Commentaire           |
9 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
0 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
1 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
2 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
3 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
4 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
5 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
6 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
7 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
8 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
9 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
0 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
1 xx-xxxxx   99   9999   9999   9999   9999   9999   x   <xxxxxxxxxxxxxxxxxxx> |
2                                                                              |
3------------------------------------------------------------------------------+
@ENDIF
