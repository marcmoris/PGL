;/T/ Liste détaillée des transactions au projet
;;
;;
;;

USE ussetqzs NOLIST
SET REP NOBLA

ACCESS MCHIS_PROJET &
  LINK HISCLE TO HISCLE OF MCHISTO OPTIONAL &
  LINK REFCIECLE, &
       REFCLETYP, &
       REFCLENUM &
    TO REFCIECLE, &
       REFCLETYP, &
       REFCLENUM OF MCREFERENCE OPTIONAL

CHOOSE CIECLE PARM, PROCLE PARM, ACTCLE PARM

DEFINE D_UNICLE   CHARACTER SIZE 10 = PACK( UNICAT + UNICLE )

DEFINE D_DSCGEN  CHARACTER SIZE 40 = &
   HISDSCGEN OF MCHISTO IF HISDSCGEN OF MCHISTO <> "" &
                      ELSE REFNOM OF MCREFERENCE

SORT ON PROCLE OF MCHIS_PROJET &
     ON ACTCLE OF MCHIS_PROJET &
     ON D_UNICLE &
     ON UNACLE OF MCHIS_PROJET &
     ON CPTCLE OF MCHIS_PROJET

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
  "Liste détaillée des transactions au projet"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "PR303"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

USE ustit220 NOLIST
  SKIP 2



HEADING AT PROCLE OF MCHIS_PROJET &
  SKIP 1 &
  TAB 001 "No projet....: " PROCLE OF MCHIS_PROJET &
  SKIP 2

HEADING AT ACTCLE OF MCHIS_PROJET &
  SKIP 1 &
  TAB 003 "No sous-projet....: " ACTCLE OF MCHIS_PROJET &
  SKIP 2 &
  TAB 005 "Équipement  No   Unité adm  Cpte   No référence   Src #Lot  Description                                 Montant       Montant" &
  SKIP &
  TAB 005 "            Per                                                                                          débit        Crédit" &
  SKIP &
  TAB 005 "---------- ----- --------- ------ --------------- --- ---- ----------------------------------------  ------------- -------------" &
  SKIP 2

REPORT &
SKIP 1 &
  TAB 005 D_UNICLE                   PRINT AT D_UNICLE &
  TAB 016 PECCLE     OF MCHISTO      &
  TAB 022 UNACLE     OF MCHIS_PROJET &
  TAB 032 CPTCLE     OF MCHIS_PROJET &
  TAB 039 HISNUMDOC  OF MCHISTO      &
  TAB 055 HISCODMOD  OF MCHISTO      &
  TAB 059 HISNUMLOT  OF MCHISTO      &
  TAB 064 D_DSCGEN        &
  TAB 105 HIPMNTDBT  OF MCHIS_PROJET PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 119 HIPMNTCRT  OF MCHIS_PROJET PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
SKIP 1 &
  TAB 064 HISDSCSAI OF MCHISTO      &
SKIP 1 &
  TAB 064 HISDSCSAI2 OF MCHISTO      &
SKIP 2

BUILD pr303z
