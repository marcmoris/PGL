;/T/ 5.3 Projet (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-08-17
;
;    Description..: Ce panorama permet de gérer les projets.
;
;/M/ Programmeur..: Francois Richard
;    Date modif...: 23 septembre 1999
;
;    Description..: Refonte du module columbia
;
;
;/M/ Programmeur..: Claudie Doyon
;    Date modif...: 1999-12-06
;    Description..: Ajout de la procédure délete pour valider la
;                   destruction avec un écran "x".
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd175 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM,          &
                       T_ECRAPP


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD175"

;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd175.hlp NOLIST

;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_ECRAPP    CHARACTER SIZE 5  RESET AT STARTUP ; ecran appellant

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable temporaire du programme pour les traitements.
;

TEMPORARY T_AN        NUMERIC*02

TEMPORARY T_FLGDEL CHARACTER SIZE 1
;-------------------------------------------------------------------------------
;
; Projet
;
FILE PDPROJET PRIMARY NOITEM

  ACCESS VIA     CIECLE, &
                 PJTABR, &
                 PJTANN, &
                 PJTNUMSEQ &
         USING   T_CIECLEMNU,           &
                 PJTABR    OF PDPROJET, &
                 PJTANN    OF PDPROJET, &
                 PJTNUMSEQ OF PDPROJET  &
         REQUEST PJTABR    OF PDPROJET, &
                 PJTANN    OF PDPROJET, &
                 PJTNUMSEQ OF PDPROJET  &
         ORDERBY CIECLE, &
                 PJTANN, &
                 PJTABR, &
                 PJTNUMSEQ

  ACCESS VIA     CIECLE,                &
                 PJTABR                 &
         USING   T_CIECLEMNU,           &
                 PJTABR    OF PDPROJET  &
         REQUEST PJTABR    OF PDPROJET  &
         ORDERBY CIECLE, &
                 PJTANN, &
                 PJTABR, &
                 PJTNUMSEQ

  ACCESS VIA     CIECLE,                &
                 PJTANN                 &
         USING   T_CIECLEMNU,           &
                 PJTANN    OF PDPROJET  &
         REQUEST PJTANN    OF PDPROJET  &
         ORDERBY CIECLE, &
                 PJTANN, &
                 PJTABR, &
                 PJTNUMSEQ

  ACCESS VIA     CIECLE         &
         USING   T_CIECLEMNU    &
         ORDERBY CIECLE, &
                 PJTANN, &
                 PJTABR, &
                 PJTNUMSEQ

    ITEM PJTNUMPRO OF PDPROJET FINAL PJTABR      OF PDPROJET + &
                                     PJTANN      OF PDPROJET + &
                                     PJTNUMSEQ   OF PDPROJET

    ITEM CIECLE OF PDPROJET INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Déclaration du fichier de séquence pour les projets.
;
FILE PDSEQ_PROJET DESIGNER

;-------------------------------------------------------------------------------
;
; Code bilingue (ÉTAT)
;
DEFINE    D_PJTETA    CHARACTER SIZE 16 = "PJTETA"
TEMPORARY T_PJTETA    CHARACTER SIZE 04

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_PJTETA

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_PJTETA,                             &
               PJTETA           OF PDPROJET

;-------------------------------------------------------------------------------
;
; Code bilingue (Code de langue)
;
DEFINE    D_PJTCODLNG CHARACTER SIZE 16 = "PJTCODLNG"
TEMPORARY T_PJTCODLNG CHARACTER SIZE 04

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_PJTCODLNG

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_PJTCODLNG,                          &
               PJTCODLNG        OF PDPROJET

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Projet " &
@ELSE
      " %%%Projet " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF

ALIGN (2,3,19) (21,21,22) (24,24,25) (,35,51) (,,53)
SKIP TO LINE 5

FIELD PJTABR           OF PDPROJET    &
      HIDDEN                          &
      LABEL "No. projet....:"         &
      REQUIRED NOCHANGE               &
      LOOKUP NOTON PDPROJET           &
          VIA   CIECLE,               &
                PJTABR                &
          USING T_CIECLEMNU,          &
                PJTABR OF PDPROJET    &
          MESSAGE 2927 ; Cette abréviation de projet existe déjà

FIELD PJTANN           OF PDPROJET  &
      HIDDEN                        &
      LABEL "-"                     &
      NUMERIC

FIELD PJTNUMSEQ        OF PDPROJET  &
      HIDDEN                        &
      LABEL "-"                     &
      DISPLAY                       &
      NUMERIC                       &
      BWZ

FIELD PJTETA           OF PDPROJET        &
      ID SAME                             &
      LOOKUP ON A_CBI_PJTETA              &
         VIA   XELNOM,                    &
               CBICLE                     &
         USING D_PJTETA,                  &
               PJTETA   OF PDPROJET       &
         MESSAGE 2904 ; Code invalide

FIELD CBIDSC           OF A_CBI_PJTETA     &
      DISPLAY

SKIP 1
ALIGN (2,3,19)

FIELD PJTNOM           OF PDPROJET  &
      HIDDEN                        &
      REQUIRED

SKIP 1

FIELD PJTDSC           OF PDPROJET &
      LABEL "Desc francaise:"      &
      HIDDEN

FIELD PJTDSCFRA002 OF PDPROJET &
      ID SAME                  &
      NOLABEL

FIELD PJTDSCANG001 OF PDPROJET &
      LABEL "Desc. anglaise:"  &
      HIDDEN

FIELD PJTDSCANG002 OF PDPROJET &
      ID SAME                  &
      NOLABEL

SKIP 1
FIELD PJTRPS           OF PDPROJET &
      HIDDEN                       &
      REQUIRED

SKIP 1
FIELD PJTPROMUL        OF PDPROJET &
      ID 50                        &
      HIDDEN                       &
      DEFAULT "0"                  &
      SIZE 3


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager à saisie une année.
;
PROCEDURE INPUT PJTANN
BEGIN
  IF 0 = SIZE(FIELDTEXT) AND PJTANN OF PDPROJET = " " AND ENTRYMODE
  THEN BEGIN
    LET FIELDTEXT = ASCII(T_AN,2)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;
PROCEDURE INPUT PJTETA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PJTETA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 &
               MODE F &
               PASSING D_PJTETA, &
                       T_PJTETA
    LET FIELDTEXT = TRUNCATE(T_PJTETA)
  END
  ELSE BEGIN
    ;
    ; Valeur par défaut
    ;
    IF 0 = SIZE(FIELDTEXT)
    THEN BEGIN
      LET FIELDTEXT = "A"
    END
  END
END

;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;
PROCEDURE INPUT PJTPROMUL
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;
PROCEDURE OUTPUT PJTPROMUL
BEGIN
  USE usflgout3 NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
  LET T_AN = NCONVERT(ASCII(SYSDATE)[3:2])
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDPROJET   &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDPROJET   &
     AND NOT NEWRECORD     OF PDPROJET   &
     AND NOT DELETEDRECORD OF PDPROJET   &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  IF " " = PJTNUMSEQ OF PDPROJET
  THEN BEGIN
    GET PDSEQ_PROJET OPTIONAL &
      VIA   PJTANN,           &
            CIECLE            &
      USING PJTANN,           &
            T_CIECLEMNU
    IF ACCESSOK
    THEN BEGIN
      LET PJTNUMSEQ OF PDSEQ_PROJET = ASCII(NCONVERT(PJTNUMSEQ OF PDSEQ_PROJET) + 1,3)
      LET PJTNUMSEQ OF PDPROJET     = PJTNUMSEQ OF PDSEQ_PROJET
      PUT PDSEQ_PROJET
      END
    ELSE BEGIN
      LET CIECLE    OF PDSEQ_PROJET = T_CIECLEMNU
      LET PJTANN    OF PDSEQ_PROJET = PJTANN
      LET PJTNUMSEQ OF PDSEQ_PROJET = "001"
      LET PJTNUMSEQ OF PDPROJET     = PJTNUMSEQ OF PDSEQ_PROJET
      PUT PDSEQ_PROJET
    END
  DISPLAY PJTNUMSEQ OF PDPROJET
  INFO = "Le numéro séquentiel du projet est " + PJTNUMSEQ OF PDPROJET + &
         " !" NOW RESP
  END
END


;------------------------------------------------------------------------------
;
; Appel de l'écran de validation de la destruction d'un code de projet
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'écran de validation
   RUN SCREEN pd175x MODE SAME &
                   PASSING PDPROJET, &
                           T_FLGDEL

   ; Si le code est utilisé on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN ERROR " Impossible de supprimer ce code puisque déjà utilisé."
   ELSE DELETE PDPROJET

END


BUILD
