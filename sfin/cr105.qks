;/T/ Consultation des C/R,cédule       CLICIECLE,CLICLE,CIECLE,CARCLE,CCDCLELIG
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 28 juin 1993
;
;    Description..: Permet de visionner et de choisir un compte à recevoir.
;                   Seul les factures journsalisées seront sélectionnées.
;
;
;    Paramètre....: CLICIECLE ==> - compagnie du client.
;                                 - segment de la clé de recherche.
;
;                   CLICLE    ==> - numéro de client.
;                                 - segment de la clé de recherche.
;
;                   CIELCE    ==> - compagnie du solde du client.
;
;                   CARCLE    ==> - numéro du compte à recevoir(facture).
;                                 - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;                                 - contient la valeur de retour.
;
;                   CCDCLELIG ==> - numéro de cédule
;                                 - contient la valeur de retour.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr105 NOMODE NOACTION FIELDMARK &
             FROM 11,20 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CLICIECLE, &
                       T_CLICLE   , &
                       T_CIECLEMNU, &
                       T_CARCLE   , &
                       T_CCDCLELIG

USE cr105.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CLICIECLE CHARACTER SIZE 4 ; Compagnie du client.
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie du menu.
TEMPORARY T_CLICLE    CHARACTER SIZE 6 ; Numéro de client
TEMPORARY T_CARCLE    CHARACTER SIZE 15; Numéro de facture
TEMPORARY T_CCDCLELIG INTEGER   SIZE 2 ; Numéro de cédule.


;-------------------------------------------------------------------------------
;
FILE VCAR_CCD         PRIMARY &
                      OCCURS 10

  ACCESS VIA REFCIECLE, &
             REFCLENUM, &
             CIECLE   , &
             CARCLE     &
         USING T_CLICIECLE, &
               T_CLICLE   , &
               T_CIECLEMNU, &
               T_CARCLE     &
         ORDERBY CIECLE, &
                 CARCLE

  SELECT IF     CARDATJOU OF VCAR_CCD <> 0 &
            AND 0 <> (  CCDMNT    OF VCAR_CCD &
                        + V_CCDMNTAUG OF VCAR_CCD &
                        - V_CCDMNTDIM OF VCAR_CCD )


;-------------------------------------------------------------------------------
;
;
; Montant du compte à recevoir.
;
DEFINE D_MNTFAC FLOAT SIZE 8 =   CCDMNT      OF VCAR_CCD &
                               + V_CCDMNTAUG OF VCAR_CCD

;
; Solde à recevoir.
;
DEFINE D_SLDFAC FLOAT SIZE 8 =  D_MNTFAC &
                                - V_CCDMNTDIM OF VCAR_CCD


;-------------------------------------------------------------------------------

DRAW 1,1 TO 13,61

USE ushilite NOLIST

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO 2


TITLE &
@IF FRANCAIS
      "Numéro       Céd   Date            Brut     A recevoir" &
@ELSE
      "Number       Sch   Date         $ Gross   $ Receivable" &
@ENDIF
      AT 2,6

ALIGN(3,3,6) (,,20) (,,23) (,,34) (,,48)

CLUSTER OCCURS WITH VCAR_CCD

FIELD CARCLE    OF VCAR_CCD HIDDEN LABEL ">>" &
        SIZE 12 &
        DISPLAY

FIELD CCDCLELIG OF VCAR_CCD &
        DISPLAY size 2

FIELD CARDAT    OF VCAR_CCD  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY

FIELD D_MNTFAC &
        DISPLAY &
        PICTURE "^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

FIELD D_SLDFAC &
        PICTURE "^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CARCLE, "@")
  THEN LET T_CARCLE = ""
END

;-------------------------------------------------------------------------------
;
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CARCLE    = CARCLE    OF VCAR_CCD
  LET T_CCDCLELIG = CCDCLELIG OF VCAR_CCD
  RETURN
END

BUILD

;                   *************************************************************
;                   *    Numéro       Céd   Date            Brut     A recevoir *
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   * >> xxxxxxxxxxxx  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                   *************************************************************
