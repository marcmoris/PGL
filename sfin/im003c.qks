;
;/M/ François Déry, 7 mai 1999
;       Modifier le run command pour le exécutable en UNIX.
;
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN im003c      NOMODE ACTION LABEL " " &
                   FROM 2,1 TO 13,80 &
                   RECEIVING GPFRM_AFFICHE

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPFRM_AFFICHE          MASTER

FILE GPFRD_DOMAINE          DESIGNER

FILE IMIMM_BDG_CTB          DESIGNER

FILE GPFRM_DETAILS          DESIGNER

FILE IMIMMOBILISATION       DESIGNER

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

PROCEDURE INTERNAL LIRE_BUDGET
BEGIN
        WHILE RETRIEVING IMIMM_BDG_CTB &
              VIA   CIECLE,            &
                    IMMCLE             &
              USING CIECLE OF GPFRM_AFFICHE, &
                    IMMCLE OF GPFRM_AFFICHE
        BEGIN

            IF CPTCLE OF IMIMM_BDG_CTB >= FDOCPTDEB OF GPFRD_DOMAINE AND &
               CPTCLE OF IMIMM_BDG_CTB <= FDOCPTFIN OF GPFRD_DOMAINE
            THEN BEGIN
                 GET IMIMMOBILISATION &
                     VIA   CIECLE, &
                           IMMCLE  &
                     USING CIECLE OF IMIMM_BDG_CTB, &
                           IMMCLE OF IMIMM_BDG_CTB

                 LET CIECLE    OF GPFRM_DETAILS = CIECLE OF IMIMM_BDG_CTB
                 LET IMMCLE    OF GPFRM_DETAILS = IMMCLE OF IMIMM_BDG_CTB
                 LET PRJCLE    OF GPFRM_DETAILS = " "
                 LET PRACLE    OF GPFRM_DETAILS = " "
                 LET CPTCLE    OF GPFRM_DETAILS = CPTCLE OF IMIMM_BDG_CTB
                 LET UNACLE    OF GPFRM_DETAILS = UNACLE OF IMIMM_BDG_CTB

                 IF FDOOPE OF GPFRD_DOMAINE = "+"
                 THEN BEGIN
                      LET FDEMNTENG OF GPFRM_DETAILS = &
                          IBCMNTENG OF IMIMM_BDG_CTB

                      LET FDEMNTREE OF GPFRM_DETAILS = &
                          IBCMNTREE OF IMIMM_BDG_CTB

                 END
                 ELSE BEGIN
                      LET FDEMNTENG OF GPFRM_DETAILS = &
                          IBCMNTENG OF IMIMM_BDG_CTB * -1

                      LET FDEMNTREE OF GPFRM_DETAILS = &
                          IBCMNTREE OF IMIMM_BDG_CTB * -1

                 END
                 PUT GPFRM_DETAILS RESET
            END
        END
END


PROCEDURE INITIALIZE
BEGIN
   CLOSE GPFRM_DETAILS

   RUN COMMAND "echo 'create file gpfrm_details' | qutil"

   WHILE RETRIEVING GPFRD_DOMAINE &
         VIA   FRMCLE,            &
               FRDCLE             &
         USING FRMCLE OF GPFRM_AFFICHE, &
               FRDCLE OF GPFRM_AFFICHE

   BEGIN
      DO LIRE_BUDGET
   END

   RETURN
END

BUILD
