;/T/ Immobilisation - Informations complémentaires
;
;/P/ Programmeur..: Alain Côté
;    Date création: 22 février 1994
;
;/V3.02.00/ Alain Côté, 22 février 1994, Immobilisation
;
;    Description..: Cet écran permet de définir une description complète " &
;                   d'une immobilisation.
;
;-----------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001e FROM 7,1 TO 23,80 &
              MODE LABEL "" AT 1,80 NOACTION &
              FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING IMIMMOBILISATION , &
                        T_CIECLEMNU      , &
                        T_CIENOM

;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "IM001E"

;
; Documentation de l'écran.
;
USE im001e.hlp NOLIST

USE ustrasse NOLIST       ;Transaction QUERY pour les sous-écran


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Numéro de compagnie du menu.


;-------------------------------------------------------------------------------
;
FILE IMIMMOBILISATION MASTER

FILE IMIMM_DESC       PRIMARY   &
                      OCCURS 12 &
                      NOITEM
  ACCESS VIA CIECLE , &
             IMMCLE   &
         USING CIECLE OF IMIMM_DESC , &
               IMMCLE OF IMIMM_DESC   &
         ORDERBY CIECLE , &
                 IMMCLE , &
                 IMDCLELIG

 ITEM CIECLE OF IMIMM_DESC INITIAL CIECLE OF IMIMMOBILISATION
 ITEM IMMCLE OF IMIMM_DESC INITIAL IMMCLE OF IMIMMOBILISATION


;-------------------------------------------------------------------------------
;
TEMPORARY T_NUMLIG INTEGER UNSIGNED SIZE 2 ; Numérotation auto. des lignes.
;                   OCCURS WITH IMIMMOBILISATION

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 01,01 TO 01,79
DRAW 01,01 TO 17,01
DRAW 17,01 TO 17,80
DRAW 02,80 TO 17,80

TITLE &
@IF FRANCAIS
      " Informations complémentaires " &
@ELSE
      " %%%Informations complémentaires " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

TITLE &
@IF FRANCAIS
      "Texte                                                         Ligne" &
@ELSE
      "Text                                                          Line " &
@ENDIF
      AT ,3

SKIP

ALIGN (3,2,3) (,,65)

CLUSTER OCCURS WITH IMIMM_DESC ID BASE 15

FIELD IMDDSC    OF IMIMM_DESC HIDDEN LABEL " "

FIELD IMDCLELIG OF IMIMM_DESC

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT IMDCLELIG
BEGIN
  ;
  ; Valide si l'usager peut faire de la saisie.
  ;
  USE ussecent NOLIST
  ;
  ; Assigne un numéro de ligne automatiquement.
  ;
  IF     0 = SIZE( FIELDTEXT ) &
     AND 0 = IMDCLELIG OF IMIMM_DESC &
     AND NOT FINDMODE
  THEN LET FIELDTEXT = ASCII( ( T_NUMLIG + 100 ) / 100 )
END


;-------------------------------------------------------------------------------
;
;
; Incrémente le numéro de ligne.
;
;
PROCEDURE PROCESS IMDCLELIG
BEGIN
  IF T_NUMLIG < IMDCLELIG OF IMIMM_DESC
  THEN LET T_NUMLIG = IMDCLELIG OF IMIMM_DESC + 100
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR IMIMM_DESC
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF IMIMM_DESC &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF IMIMM_DESC &
       AND NOT NEWRECORD     OF IMIMM_DESC &
       AND NOT DELETEDRECORD OF IMIMM_DESC &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Le timbre de modification est mis à jour si l'usager modifie
    ; la table IMIMM_CEDULE.
    ;
    IF        NEWRECORD OF IMIMM_DESC &
       OR ALTEREDRECORD OF IMIMM_DESC &
       OR DELETEDRECORD OF IMIMM_DESC
    THEN BEGIN
      LET IMMDATMOD OF IMIMMOBILISATION = SYSDATE
      LET IMMHREMOD OF IMIMMOBILISATION = SYSTIME / 10000
      LET IMMUSRMOD OF IMIMMOBILISATION = LOGONID
    END
  END
END

BUILD
@IF FORMAT

************************* Informations complémentaires ************************x
*                                                                              *
* Texte                                                         Ligne          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
*                                                                              *
********************************************************************************
@ENDIF
