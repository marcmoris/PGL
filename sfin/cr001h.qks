;/T/ Clients - Consultation des encaissements
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 21 mai 1993
;
;    Description..: Pour consulter les encaissements effectués par le client
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr001h ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND, CHANGE  &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        CRCLIENT

USE ussectmp NOLIST
    "CR001H"

USE cr001h.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.


;-------------------------------------------------------------------------------
;
; Date de sélection des encaissements.
;
TEMPORARY T_DATDEB DATE


;-------------------------------------------------------------------------------
;
;
; Client.
;
FILE CRCLIENT         MASTER

;
; Encaissement du client.
;
FILE CRDEPOT          PRIMARY &
                      NOITEM  &
                      OCCURS 13
  ACCESS REQUEST T_DATDEB &
         VIA     REFCIECLE, &
                 REFCLETYP, &
                 REFCLENUM, &
                 CIECLE     &
         USING   CLICIECLE OF CRCLIENT, &
                 CLIREFTYP OF CRCLIENT, &
                 CLICLE    OF CRCLIENT, &
                 T_CIECLEMNU            &
         ORDERBY DPODAT DESCENDING

  ACCESS VIA     REFCIECLE, &
                 REFCLETYP, &
                 REFCLENUM, &
                 CIECLE     &
         USING   CLICIECLE OF CRCLIENT, &
                 CLIREFTYP OF CRCLIENT, &
                 CLICLE    OF CRCLIENT, &
                 T_CIECLEMNU            &
         ORDERBY DPODAT DESCENDING


SELECT IF DPOTYPECR OF CRDEPOT = "EN" &
          AND ( DPODAT OF CRDEPOT <= T_DATDEB &
                OR T_DATDEB = 0 )


;-------------------------------------------------------------------------------
;

TEMPORARY T_CIECLE CHARACTER SIZE  4 ; Popup sur le détail du dépôt.
TEMPORARY T_DPOCLE CHARACTER SIZE 12 ; Popup sur le détail du dépôt.

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Consultation des encaissements " &
@ELSE
      " Client Payments " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

ALIGN (,3,19) (,,27)

FIELD CLICLE OF CRCLIENT &
label "Client......:"&
        FIXED

FIELD CLINOM OF CRCLIENT &
        FIXED

ALIGN (,3,26) (,47,76)

FIELD T_DATDEB &
 FORMAT YYYYMMDD   PATTERN "####/##/##"      DISPLAY &
@IF FRANCAIS
        LABEL "Sélectionner jusqu'au:"
@ELSE
        LABEL "%%Sélectionner jusqu'au....:"
@ENDIF

SKIP 1

TITLE &
@IF FRANCAIS
      "  No           Date                                    Montant" &
@ELSE
      "Cheque       Payment              Payment            %%%esc.  " &
@ENDIF
      AT ,7

SKIP

TITLE &
@IF FRANCAIS
      "chèque       encaissé        Montant reçu             escompte" &
@ELSE
      "  no           date                Amount               Amount" &
@ENDIF
      AT ,7

SKIP

ALIGN (6,5,6) (,,20) (,,35) (,,56)

CLUSTER OCCURS WITH CRDEPOT

FIELD DPOCHQNUM    OF CRDEPOT  LABEL " " HIDDEN

FIELD DPODAT       OF CRDEPOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY

FIELD DPOMNTPAM    OF CRDEPOT &
        DISPLAY

FIELD DPOMNTESC    OF CRDEPOT &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Appel sous-écran pour dépôt / facture
;
;
PROCEDURE INPUT DPOCHQNUM
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = CIECLE OF CRDEPOT
    LET T_DPOCLE = DPOCLE OF CRDEPOT
    RUN SCREEN cr108 MODE F PASSING T_CIECLE, T_DPOCLE
  END
  LET FIELDTEXT = ""
END

BUILD

;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;************************ Consultation des encaissements ************************
;*                                                                              *
;* Client........: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
;* Sélectionner jusqu'au: xxxxxxxx                                              *
;*                                                                              *
;*       No           Date                                    Montant           *
;*     chèque       encaissé        Montant reçu             escompte           *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;*    xxxxxxxx      xxxxxxxx       xxxxxxxxxxxxxx       xxxxxxxxxxxxxx          *
;********************************************************************************
