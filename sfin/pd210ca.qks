;/T/ Detail de la commande interne (carte de bloc)
;
;/P/ Programmeur..: Francois Richard (adaptation)
;    Date Creation: 1999-10-27
;
;    Description..: Sous-ecran pour saisir le detail de la commande dans la
;                   gestion de blocs.
;
; Attention : Procedures UPDATE,ENTRY,DELETE recode
;
;***********************************************************************************************************************************
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 18 février 2000
;    Description.......: Ajuster le traitement de la commande interne afin d'éliminer le contrôle d'unicité au niveau du détail 
;                        de la commande (ventilation bloc).
;
;    Référence.........: Stabilisation du module Gestion de blocs / Columbia (point 01).
;
;    Signet............: MP-00-02-18_SC01 - Pas de signet puisque le code a été supprimé.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN pd210ca ACTIONBAR                     &
               MODE LABEL "" AT 2,132        &
               NOACTION                      &
               AUTOUPDATE                    &
               FROM 1,1 TO 23,132            &
               FIELDMARK RETAIN STARTUP      &
               HELP POPUP FROM 4,9 TO 20,72  &
               RECEIVING T_CIECLEMNU,        &
                         T_CIENOM,           &
                         T_ECRAPP,           &
                         PDCOMMAN_INTERNE

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD210CA"

;-------------------------------------------------------------------------------
;
; Panorama d'aide
;
USE pd210c.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;
USE ustrasse NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Ventilation comptable  " ACTION DESIGNER D002 MARK
  MENUITEM LABEL "Blocs assignés          " ACTION DESIGNER D001 MARK
@ENDIF

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_ECRAPP    CHARACTER SIZE 05 RESET AT STARTUP ; Appellé par l'ecran de gestion des bloc ou de production

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Commande interne
;
FILE PDCOMMAN_INTERNE MASTER NODELETE

;-------------------------------------------------------------------------------
;
; Détail de la commande
;
FILE PDDETAIL_C_I          PRIMARY &
                           NOITEM  &
                           OCCURS 14

  ACCESS VIA     CIECLE,                               &
                 PJTABR,                               &
                 PJTANN,                               &
                 COMNUMCOMINT                          &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG

  ITEM CIECLE       OF PDDETAIL_C_I INITIAL T_CIECLEMNU
  ITEM PJTABR       OF PDDETAIL_C_I INITIAL PJTABR OF PDCOMMAN_INTERNE FIXED
  ITEM PJTANN       OF PDDETAIL_C_I INITIAL PJTANN OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOMINT OF PDDETAIL_C_I INITIAL &
       COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMSEQ    OF PDDETAIL_C_I INITIAL PJTNUMSEQ OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMPRO    OF PDDETAIL_C_I INITIAL PJTNUMPRO OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOM    OF PDDETAIL_C_I INITIAL COMNUMCOM OF PDCOMMAN_INTERNE FIXED
  ITEM DCILIADIA    OF PDDETAIL_C_I INITIAL "0"
  ITEM DCISTU       OF PDDETAIL_C_I INITIAL "0"

;-------------------------------------------------------------------------------
;
; Type de produit
;
FILE PDTYPE_PRODUIT REFERENCE OCCURS WITH PDDETAIL_C_I

  ACCESS VIA     CIECLE,          &
                 TPDTYPPRD        &
         USING   T_CIECLEMNU,     &
                 TPDTYPPRD        OF PDDETAIL_C_I

;-------------------------------------------------------------------------------
;
; Détail de la commande interne
;
FILE PDDETAIL_C_I          REFERENCE ALIAS A_DCI_DETAIL

;-------------------------------------------------------------------------------
;
; Diagramme
;
FILE PDDIAGRAMME           DESIGNER

;-------------------------------------------------------------------------------
;
; Ventilation de la ligne de commande
;
FILE PDVENTILATION         DESIGNER

;-------------------------------------------------------------------------------
;
; Unité de mesure (production)
;
FILE PDUNITE_MESURE REFERENCE &
                    ALIAS UNM_COMMANDE &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA     CIECLE,          &
                 UNMCODUNM        &
         USING   T_CIECLEMNU,     &
                 DCIUNMCMD        OF PDDETAIL_C_I

;-------------------------------------------------------------------------------
;
; Unité de mesure (Vente)
;
FILE PDUNITE_MESURE REFERENCE &
                    ALIAS UNM_VENTE &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA     CIECLE,          &
                 UNMCODUNM        &
         USING   T_CIECLEMNU,     &
                 DCIUNMVEN        OF PDDETAIL_C_I

;-------------------------------------------------------------------------------
;
; Type de granit
;
FILE PDTYPE_GRANIT REFERENCE &
                   OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,          &
                TGRCODGRA        &
         USING  T_CIECLEMNU,     &
                TGRCODGRA        OF PDDETAIL_C_I

;-------------------------------------------------------------------------------
;
; Conversion unité mesure de commande à unité mesure production
;
FILE PDCONVERT_UN_M REFERENCE &
                    ALIAS A_CUM_UNM_CMD &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,                           &
                CUMCODUNMINT,                     &
                CUMCODUNMEXT                      &
         USING  T_CIECLEMNU,                      &
                DCIUNMCMD        OF PDDETAIL_C_I, &
                UNMCODUNM        OF PDTYPE_PRODUIT

;-------------------------------------------------------------------------------
;
; Conversion unité mesure vente à unité mesure production
;
FILE PDCONVERT_UN_M REFERENCE &
                    ALIAS A_CUM_PRX_VEN &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,                             &
                CUMCODUNMINT,                       &
                CUMCODUNMEXT                        &
         USING  T_CIECLEMNU,                        &
                UNMCODUNM        OF PDTYPE_PRODUIT, &
                DCIUNMVEN        OF PDDETAIL_C_I

;-----------------------------------------------------------------------------
;
; Validation du numero de bloc
;
FILE IFBLOC DESIGNER ALIAS A_IFBLOC

;-----------------------------------------------------------------------------
;
; Quantite de la ligne de commande a mettre a jour
;
FILE PDDETAIL_C_I DESIGNER ALIAS A_LIGNE_COMMANDE

;-----------------------------------------------------------------------------
;
; Categorie de granit
;
FILE IFCATEGORIE REFERENCE

ACCESS VIA   CIECLE,      &
             CATCLE       &
       USING T_CIECLEMNU, &
             CATCLE OF PDDETAIL_C_I

;-----------------------------------------------------------------------------
;
; Couleur
;
FILE IFCOULEUR REFERENCE

ACCESS VIA   CIECLE,      &
             CLRCLE       &
       USING T_CIECLEMNU, &
             CLRCLE OF PDDETAIL_C_I

;-----------------------------------------------------------------------------
;
; Qualite de granit
;
FILE IFQUALITE REFERENCE

ACCESS VIA   CIECLE,       &
             QLTCLE        &
       USING T_CIECLEMNU,  &
             QLTCLE OF PDDETAIL_C_I

;-----------------------------------------------------------------------------
;
; Lot d'expedition
;
FILE IFLOT REFERENCE

  ACCESS VIA   CIECLE,      &
               IFLCLE       &
         USING T_CIECLEMNU, &
               IFLCLE OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Variable servant \340 l'appel du/des d\351pisteur(s) (pop-up)
;
TEMPORARY T_INFO         CHARACTER SIZE 20
TEMPORARY T_CIECLE       CHARACTER SIZE 04
TEMPORARY T_TPDTYPPRD    CHARACTER SIZE 04 ; Type de produit
TEMPORARY T_TGRCODGRA    CHARACTER SIZE 04 ; Type de granit
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04 ; Unit\351 de mesure
TEMPORARY T_CPTCLE       CHARACTER SIZE 06 ; Compte
TEMPORARY T_UNACLE       CHARACTER SIZE 08 ; Unit\351 administrative
TEMPORARY T_CPTTYPPAT    CHARACTER SIZE 01 ; Pattern type de compte
TEMPORARY T_CPTCATPAT    CHARACTER SIZE 08 ; Pattern cat\351gorie du compte
TEMPORARY T_PECCLE       CHARACTER SIZE 04 ; P\351riode
TEMPORARY T_CATCLE       CHARACTER SIZE 03 ; Categorie de granit
TEMPORARY T_CLRCLE       CHARACTER SIZE 03 ; Couleur
TEMPORARY T_QLTCLE       CHARACTER SIZE 03 ; Qualité
TEMPORARY T_IFLCLE       CHARACTER SIZE 09 ; Lot
TEMPORARY T_CLICLE       CHARACTER SIZE 06 ; Numero client comptes a recevoir

;------------------------------------------------------------------------------
;
; Ancien numero de lot
;
TEMPORARY T_OLD_IFLCLE CHARACTER SIZE 9 OCCURS WITH PDDETAIL_C_I

;-------------------------------------------------------------------------------

; Variable servant au traitement
;
TEMPORARY T_BLCEXT CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_NOLIG  INTEGER   SIZE 04 RESET AT STARTUP ; Pour numero LIGNE AUTOMATIQUE
TEMPORARY T_SILENT CHARACTER SIZE 01

;-------------------------------------------------------------------------------
;
;
USE usdrw132 NOLIST ; Draw pour les ecran 132 colonnes
USE ustit132 NOLIST ; Titre ecran 132 colonnes
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
      " Détail de la commande interne(Gestion de bloc) " &
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

DRAW 5,1 TO 5,132

SKIP TO LINE 6

TITLE &
      " No.   Type  " &
      AT ,2

SKIP

TITLE &
     " lig   prod Quant. com Un  Gra D Cat  Qlt  Clr  No lot     Prx ven         Un  Quant. ass  Quant. exp  Quant. fct  Commentaire" &
      AT ,2

SKIP TO LINE 8

ALIGN (02,02,03) (,,04) (,,9) (,,10) (,,14) (,,25) (,,29) (,,33) (,,35) &
      (,,40) (,,45) (,,50) (,,61) (,,77) (,,81) (,,93) (,,105) (,,117)

CLUSTER OCCURS WITH PDDETAIL_C_I ID BASE 40

FIELD DCISTU           OF PDDETAIL_C_I     &
        HIDDEN                             &
        LABEL " "                          &
        REFRESH                            &
        DISPLAY

FIELD DCINUMLIG        OF PDDETAIL_C_I                  &
        ID SAME                                         &
        NUMERIC                                         &
        LOOKUP NOTON PDDETAIL_C_I                       &
          VIA     CIECLE,                               &
                  PJTABR,                               &
                  PJTANN,                               &
                  COMNUMCOMINT,                         &
                  DCINUMLIG                             &
          USING   T_CIECLEMNU,                          &
                  PJTABR           OF PDCOMMAN_INTERNE, &
                  PJTANN           OF PDCOMMAN_INTERNE, &
                  COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                  DCINUMLIG        OF PDDETAIL_C_I      &
          MESSAGE 3055 ; Ce numéro de ligne existe déjà


FIELD DCILIADIA        OF PDDETAIL_C_I     &
        NOID                               &
        DISPLAY                            &
        REFRESH

FIELD TPDTYPPRD        OF PDDETAIL_C_I     &
        ID SAME                            &
        DUPLICATE                          &
        REQUIRED                           &
        LOOKUP ON PDTYPE_PRODUIT           &
          MESSAGE 2918 ; Ce produit n'existe pas

FIELD DCIQTECMD        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        OUTPUT SCALE 2                     &
        SIGNIFICANCE 4                     &
        PICTURE "^^^^^^^.^^"

FIELD DCIUNMCMD        OF PDDETAIL_C_I      &
        ID SAME                             &
        DEFAULT UNMCODUNM OF PDTYPE_PRODUIT &
        LOOKUP ON UNM_COMMANDE              &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas

FIELD TGRCODGRA        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE                          &
        NUMERIC                            &
        LOOKUP ON PDTYPE_GRANIT            &
          MESSAGE 2910 ; Ce type de granit n'existe pas

FIELD DCITYPGRA        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE

FIELD CATCLE OF PDDETAIL_C_I         &
      LOOKUP ON IFCATEGORIE          &
        VIA   CIECLE,                &
              CATCLE                 &
        USING T_CIECLEMNU,           &
              CATCLE OF PDDETAIL_C_I &
        MESSAGE 3261 ; PD361 Cette catégorie de granit est inexistante

FIELD QLTCLE OF PDDETAIL_C_I         &
     LOOKUP ON IFQUALITE             &
        VIA   CIECLE,                &
              QLTCLE                 &
        USING T_CIECLEMNU,           &
              QLTCLE OF PDDETAIL_C_I &
        MESSAGE 3262 ; PD362E Ce code de qualit\351 est inexistant

FIELD CLRCLE OF PDDETAIL_C_I         &
      LOOKUP ON IFCOULEUR            &
        VIA   CIECLE,                &
              CLRCLE                 &
        USING T_CIECLEMNU,           &
              CLRCLE OF PDDETAIL_C_I &
        MESSAGE 3263 ; PD363E Cette couleur est inexistante

FIELD IFLCLE OF PDDETAIL_C_I         &
      REQUIRED                       &   
      LOOKUP ON IFLOT                &
        VIA   CIECLE,                &
              IFLCLE                 &
        USING T_CIECLEMNU,           &
              IFLCLE OF PDDETAIL_C_I &
        MESSAGE 3272 ; PD372E Ce lot d'expedition n'existe pas

;
; Il ne peut y avoir 2 lignes identique pour une même commande
; Particularité pour les tranches

FIELD DCIPRIVEN        OF PDDETAIL_C_I     &
        ID SAME                            &
        DUPLICATE                          &
        SIGNIFICANCE 4

FIELD DCIUNMVEN        OF PDDETAIL_C_I     &
        ID SAME                            &
        DEFAULT DCIUNMCMD OF PDDETAIL_C_I  &
        DUPLICATE                          &
        LOOKUP ON UNM_VENTE                &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas

FIELD DCIQTEUNMPRD     OF PDDETAIL_C_I     &
        DISPLAY                            &
        PICTURE "^^^^^^^.^^"               &
        OUTPUT SCALE 2                     &
        SIGNIFICANCE 4

FIELD DCIQTEEXP        OF PDDETAIL_C_I     &
        DISPLAY                            &
        PICTURE "^^^^^^^.^^"               &
        OUTPUT SCALE 2                     &
        SIGNIFICANCE 4

FIELD DCIQTEFCT        OF PDDETAIL_C_I     &
        DISPLAY                            &
        PICTURE "^^^^^^^.^^"               &
        OUTPUT SCALE 2                     &
        SIGNIFICANCE 4

FIELD DCICOM           OF PDDETAIL_C_I     &
        ID SAME                            &
        FOR , 14

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Assigne la valeur à afficher pour le champ
;

PROCEDURE OUTPUT DCISTU
BEGIN
  IF FIELDTEXT = "0"
  THEN BEGIN
    LET FIELDTEXT = "*"
  END
  ELSE BEGIN
    LET FIELDTEXT = " "
  END
END

;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT DCINUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT ) &
     AND &
     0 = SIZE(TRUNCATE(DCINUMLIG OF PDDETAIL_C_I))
  THEN BEGIN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
  END

END

;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;
PROCEDURE PROCESS DCINUMLIG
BEGIN
  LET T_NOLIG = NCONVERT(DCINUMLIG OF PDDETAIL_C_I)
END

;-------------------------------------------------------------------------------
;
; Assigne la valeur à afficher pour le champ
;
PROCEDURE OUTPUT DCILIADIA
BEGIN
  IF FIELDTEXT = "1"
  THEN BEGIN
    LET FIELDTEXT = "*"
  END
  ELSE BEGIN
    LET FIELDTEXT = " "
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs.
;

PROCEDURE INPUT TPDTYPPRD OF PDDETAIL_C_I
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TPDTYPPRD = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd118 MODE F &
                      PASSING T_TPDTYPPRD, &
                              T_CIECLEMNU  &
                      WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_TPDTYPPRD )
  END
END

;-------------------------------------------------------------------------------
;
; Validation pour le champ
;
PROCEDURE EDIT TPDTYPPRD OF PDDETAIL_C_I
BEGIN
  IF DCILIADIA    OF PDDETAIL_C_I = "1" &
     AND &
     OLDVALUE(TPDTYPPRD OF PDDETAIL_C_I) <> FIELDTEXT
  THEN BEGIN
    ERROR 3171 ; La production a déjà débuté pour cette ligne de détail
  END
  IF T_ECRAPP = "ifm01" AND FIELDTEXT <> "BL"
  THEN BEGIN
    ERROR 3267 ; On ne peut saisir que des bloc dans cet ecran dans le module gestion de bloc
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;
PROCEDURE INPUT DCIUNMCMD
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END

;-------------------------------------------------------------------------------
;
; Validation de l'unité de vente
;
PROCEDURE EDIT DCIUNMCMD
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = SIZE(TRUNCATE(DCIUNMCMD OF PDDETAIL_C_I))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Unité de mesure permise pour vendre un diagramme
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "DI"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un produit de dimension
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "PD"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2" &
       AND &
       FIELDTEXT <> "UN"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre une tranche
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "TR"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2" &
       AND &
       FIELDTEXT <> "TR"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un bloc
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "BL"
  THEN BEGIN
    IF FIELDTEXT <> "M3" &
       AND &
       FIELDTEXT <> "P3"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
END

;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ.
;
PROCEDURE INPUT TGRCODGRA
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TGRCODGRA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd100 MODE F &
                     PASSING T_TGRCODGRA, &
                             T_CIECLEMNU &
                     WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_TGRCODGRA )
  END
END

;-------------------------------------------------------------------------------
;
; Validation pour le champ
;
PROCEDURE EDIT TGRCODGRA OF PDDETAIL_C_I
BEGIN
  ;
  ; On le peut modifier une ligne de détail si cette ligne est lié à un diagramme
  ;
  IF DCILIADIA OF PDDETAIL_C_I = "1"
  THEN BEGIN
    ERROR 3161 ; On ne peut modifier une ligne de détail liée à un diagramme
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT DCITYPGRA OF PDDETAIL_C_I
BEGIN
  IF 0=SIZE(TRUNC(FIELDTEXT))
  THEN LET FIELDTEXT = DCITYPGRA OF PDDETAIL_C_I ; Forcer la procédure EDIT.
END

PROCEDURE EDIT DCITYPGRA OF PDDETAIL_C_I
BEGIN
  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"
  THEN BEGIN
    IF FIELDTEXT = "1"
    THEN LET T_INFO = TGRDSCFRA001 OF PDTYPE_GRANIT
    ELSE IF FIELDTEXT = "2" 
         THEN LET T_INFO = TGRDSCFRA002 OF PDTYPE_GRANIT
         ELSE IF FIELDTEXT = "3" 
              THEN LET T_INFO = TGRDSCFRA003 OF PDTYPE_GRANIT
              ELSE IF FIELDTEXT = "4" 
                   THEN LET T_INFO = TGRDSCFRA004 OF PDTYPE_GRANIT
  END
  ELSE BEGIN
    IF FIELDTEXT = "1"
    THEN LET T_INFO = TGRDSCANG001 OF PDTYPE_GRANIT
    ELSE IF FIELDTEXT = "2"
         THEN LET T_INFO = TGRDSCANG002 OF PDTYPE_GRANIT
         ELSE IF FIELDTEXT = "3"
              THEN LET T_INFO = TGRDSCANG003 OF PDTYPE_GRANIT
              ELSE IF FIELDTEXT = "4" 
                   THEN LET T_INFO = TGRDSCANG004 OF PDTYPE_GRANIT
  END

  WARNING = T_INFO
END

;-------------------------------------------------------------------------------
;
; Pop-up sur la categorie
;
PROCEDURE INPUT CATCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLEMNU
    LET T_CATCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN if106 PASSING  T_CIECLE, &
                              T_CATCLE &
                     MODE F WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_CATCLE)
  END
END

;------------------------------------------------------------------------------
;
; Affichage de la description de la categorie
;
PROCEDURE PROCESS CATCLE
BEGIN
  INFO = CATDSCFRA OF IFCATEGORIE
END 

;------------------------------------------------------------------------------
;
; Pop-up sur la qualite
;
PROCEDURE INPUT QLTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLEMNU
    LET T_QLTCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN if107 PASSING  T_CIECLE, &
                              T_QLTCLE &
                     MODE F WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_QLTCLE)
  END
END

;-----------------------------------------------------------------------------
;
; Affichage de la description de la qualite
;
PROCEDURE PROCESS QLTCLE
BEGIN
  INFO = QLTDSCFRA OF IFQUALITE
END
 
;-------------------------------------------------------------------------------
;
; Pop-up sur la qualite
;
PROCEDURE INPUT CLRCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLEMNU
    LET T_CLRCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN if108 PASSING  T_CIECLE, &
                              T_CLRCLE &
                     MODE F WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_CLRCLE)
  END

END

;------------------------------------------------------------------------------
;
; Affiche de la description de la qualite
;
PROCEDURE PROCESS CLRCLE
BEGIN
  INFO = CLRDSCFRA OF IFCOULEUR
END 

;------------------------------------------------------------------------------
;
; Pop-up sur le lot d'expedition
;
PROCEDURE INPUT IFLCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IFLCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN if112  MODE F                     &
                      PASSING T_CIECLEMNU ,      &
                              T_IFLCLE           &
                      WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_IFLCLE)
  END
END

;------------------------------------------------------------------------------
;
; Validation du lot d'expedition
;
PROCEDURE EDIT IFLCLE
BEGIN
   IF IFLSTU OF IFLOT = "F"
   THEN ERROR 3273 ; PD373E Ce lot d'expedition est fermé

   ; On doit s'assurer qu'aucun bloc n'a ete expedier avant de changer
   ; le lot d'expedier
   WHILE RETRIEVING A_IFBLOC             &
     VIA   CIECLE,                       &
           PJTABR,                       &
           PJTANN,                       &
           COMNUMCOMINT,                 &
           DCINUMLIG                     &
     USING T_CIECLEMNU,                  &
           PJTABR OF PDDETAIL_C_I,       &
           PJTANN OF PDDETAIL_C_I,       &
           COMNUMCOMINT OF PDDETAIL_C_I, &
           DCINUMLIG OF PDDETAIL_C_I   
   BEGIN
     IF EXNCLE OF A_IFBLOC <> 0
     THEN ERROR "Un bloc est attaché à une expédition, impossible de modier le lot" 
   END  

END

;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;
PROCEDURE EDIT DCIPRIVEN
BEGIN
  ;
  ; Valide que le champ a été saisie
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = DCIPRIVEN OF PDDETAIL_C_I
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END

  IF FIELDVALUE <> OLDVALUE(DCIPRIVEN OF PDDETAIL_C_I) &
     AND &
     0 <> OLDVALUE(DCIPRIVEN OF PDDETAIL_C_I)
  THEN BEGIN
    LET DCISTU OF PDDETAIL_C_I = "0"
    DISPLAY DCISTU OF PDDETAIL_C_I
    WARNING 3128 ; Vérifier l'imputation
  END
END

;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;
PROCEDURE PROCESS DCIPRIVEN
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET DCIUNMPRD OF PDDETAIL_C_I = UNMCODUNM OF PDTYPE_PRODUIT

  LET DCIPRIUNMPRD OF PDDETAIL_C_I = FIELDVALUE  

  LET DCIMNT OF PDDETAIL_C_I = &
      ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD OF PDDETAIL_C_I),2)
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;
PROCEDURE INPUT DCIUNMVEN
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU  &
                     WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END

;-------------------------------------------------------------------------------
;
; Valide que le champ a été saisie
;
PROCEDURE EDIT DCIUNMVEN
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = SIZE(TRUNCATE(DCIUNMVEN OF PDDETAIL_C_I))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Unité de mesure permise pour vendre un diagramme
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "DI"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un produit de dimension
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "PD"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre une tranche
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "TR"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un bloc
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "BL"
  THEN BEGIN
    IF FIELDTEXT <> "M3" &
       AND &
       FIELDTEXT <> "P3"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
END

;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;

PROCEDURE PROCESS DCIUNMVEN
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET DCIUNMPRD OF PDDETAIL_C_I = UNMCODUNM OF PDTYPE_PRODUIT

  LET DCIMNT OF PDDETAIL_C_I = &
      ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD OF PDDETAIL_C_I),2)

  IF FIELDTEXT <> OLDVALUE(DCIUNMVEN OF PDDETAIL_C_I) &
     AND &
     0 <> OLDVALUE(DCIPRIVEN OF PDDETAIL_C_I)
  THEN BEGIN
    WARNING 3128 ; Vérifier l'imputation
  END
END

;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;
PROCEDURE PROCESS DCIQTECMD
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET DCIUNMPRD OF PDDETAIL_C_I = UNMCODUNM OF PDTYPE_PRODUIT

  LET DCIMNT OF PDDETAIL_C_I = &
      ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD     OF PDDETAIL_C_I),2)

END

;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;
PROCEDURE PROCESS DCIUNMCMD
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET DCIUNMPRD OF PDDETAIL_C_I = UNMCODUNM OF PDTYPE_PRODUIT

  LET DCIMNT OF PDDETAIL_C_I = &
      ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD OF PDDETAIL_C_I),2)
END

;-------------------------------------------------------------------------------
;
; Procédure APPEND
;
PROCEDURE APPEND
BEGIN

  LET DCISTU OF PDDETAIL_C_I = "0"
  DISPLAY DCISTU OF PDDETAIL_C_I

  ACCEPT DCINUMLIG OF PDDETAIL_C_I
  DISPLAY DCILIADIA OF PDDETAIL_C_I

  LET TPDTYPPRD OF PDDETAIL_C_I = "BL"
  DISPLAY TPDTYPPRD OF PDDETAIL_C_I
  ACCEPT DCIQTECMD OF PDDETAIL_C_I
  LET DCIUNMCMD OF PDDETAIL_C_I = "M3"
  DISPLAY DCIUNMCMD OF PDDETAIL_C_I

  ACCEPT TGRCODGRA OF PDDETAIL_C_I
  ACCEPT DCITYPGRA OF PDDETAIL_C_I

  ACCEPT CATCLE OF PDDETAIL_C_I
  ACCEPT QLTCLE OF PDDETAIL_C_I
  ACCEPT CLRCLE OF PDDETAIL_C_I

  ACCEPT IFLCLE OF PDDETAIL_C_I

  ACCEPT DCIPRIVEN OF PDDETAIL_C_I
  LET DCIUNMVEN OF PDDETAIL_C_I = "M3"
  DISPLAY DCIUNMVEN OF PDDETAIL_C_I

  ;
  ; Validation spéciale si TRANCHE
  ;
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  ACCEPT DCICOM OF PDDETAIL_C_I

  LET T_CLICLE = CLICLE OF PDCOMMAN_INTERNE

  RUN SCREEN pd210q &
       PASSING T_CIECLEMNU,      &
               T_CIENOM,         &
               T_ECRAPP,         &
               T_CLICLE,         &
               PDDETAIL_C_I      &
       MODE F 

  ; Mise a jour de l'affichage de l'ecran
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I

  ; Ecran de ventilation comptable
  RUN SCREEN pd210f &
      PASSING T_CIECLEMNU, &
              T_CIENOM, &
              PDCOMMAN_INTERNE, &
              PDDETAIL_C_I &
      MODE SAME
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 40
BEGIN

   ; Verifie si il existe deja des blocs de rattache a ligne, si tel est
   ; le cas alors certains champs ne pourront etre modifier
   GET A_IFBLOC &
     VIA   CIECLE,                       &
           PJTABR,                       &
           PJTANN,                       &
           COMNUMCOMINT,                 &
           DCINUMLIG                     &
     USING T_CIECLEMNU,                  &
           PJTABR OF PDDETAIL_C_I,       &
           PJTANN OF PDDETAIL_C_I,       &
           COMNUMCOMINT OF PDDETAIL_C_I, &
           DCINUMLIG OF PDDETAIL_C_I     &
     OPTIONAL

   IF ACCESSOK 
   THEN LET T_BLCEXT = "1"
   ELSE LET T_BLCEXT = "0"
        
   ACCEPT DCISTU OF PDDETAIL_C_I

   IF T_BLCEXT = "0"
   THEN ACCEPT DCINUMLIG OF PDDETAIL_C_I

   DISPLAY DCILIADIA OF PDDETAIL_C_I
   DISPLAY TPDTYPPRD OF PDDETAIL_C_I
   ACCEPT DCIQTECMD OF PDDETAIL_C_I
   DISPLAY DCIUNMCMD OF PDDETAIL_C_I

   IF T_BLCEXT = "0"
   THEN BEGIN    
     ACCEPT TGRCODGRA OF PDDETAIL_C_I
     ACCEPT DCITYPGRA OF PDDETAIL_C_I
     ACCEPT CATCLE OF PDDETAIL_C_I
     ACCEPT QLTCLE OF PDDETAIL_C_I
     ACCEPT CLRCLE OF PDDETAIL_C_I
  END

  ACCEPT IFLCLE OF PDDETAIL_C_I

  ACCEPT DCIPRIVEN OF PDDETAIL_C_I
  DISPLAY DCIUNMVEN OF PDDETAIL_C_I
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  ACCEPT DCICOM OF PDDETAIL_C_I
END

;------------------------------------------------------------------------------
;
; Mise a jour des quantite sur les lignes de commande correspondante
;
PROCEDURE INTERNAL MAJ_LIGNE_COMMANDE
BEGIN

   ; Mise-a-jour de la quantite et du montant de la ligne de commande
   GET A_LIGNE_COMMANDE                   &
      VIA   CIECLE,                       &
            PJTABR,                       &
            PJTANN,                       &
            COMNUMCOMINT,                 &
            DCINUMLIG                     &
      USING T_CIECLEMNU,                  &
            PJTABR OF PDDETAIL_C_I,       &
            PJTANN OF PDDETAIL_C_I,       &
            COMNUMCOMINT OF PDDETAIL_C_I, &
            DCINUMLIG OF PDDETAIL_C_I     &
      OPTIONAL

   ; Attention ne pas enlever cette ligne car elle risque de servir
   ; a nouveau
   ; LET DCIPRIVEN OF A_LIGNE_COMMANDE = 0
   LET DCIQTEUNMPRD OF A_LIGNE_COMMANDE = 0

   ; Recherche tous les blocs rattache a la commande
   WHILE RETRIEVING A_IFBLOC             &
     VIA   CIECLE,                       &
           PJTABR,                       &
           PJTANN,                       &
           COMNUMCOMINT,                 &
           DCINUMLIG                     &
     USING T_CIECLEMNU,                  &
           PJTABR OF PDDETAIL_C_I,       &
           PJTANN OF PDDETAIL_C_I,       &
           COMNUMCOMINT OF PDDETAIL_C_I, &
           DCINUMLIG OF PDDETAIL_C_I
   BEGIN

      ; Attention ne pas enlever cette ligne car elle risque de servir
      ; a nouveau  
      ;LET DCIPRIVEN OF A_LIGNE_COMMANDE = &
      ;    DCIPRIVEN OF A_LIGNE_COMMANDE + &
      ; (((BLCMNTM3 OF A_IFBLOC + BLCMNTEXPM3 OF A_IFBLOC) &
      ;  * BLCM3TOT OF A_IFBLOC) / 100)

      ;LET DCIPRIUNMPRD OF A_LIGNE_COMMANDE = DCIPRIVEN OF A_LIGNE_COMMANDE

      LET DCIQTEUNMPRD OF A_LIGNE_COMMANDE = &
          DCIQTEUNMPRD OF A_LIGNE_COMMANDE + (BLCM3TOT OF A_IFBLOC / 100)

   END

   ; Mise a jour de la ligne de commande
   PUT A_LIGNE_COMMANDE RESET

END

;-------------------------------------------------------------------------------
;
; Blocs assignation
;
PROCEDURE DESIGNER D001
BEGIN

  FOR PDDETAIL_C_I
    IF ALTEREDRECORD OF PDDETAIL_C_I
    THEN ERROR 1390 ; Vous devez faire une mise a jour avant d'acceder
                    ; le sous-ecran.

    LET T_CLICLE = CLICLE OF PDCOMMAN_INTERNE 
    RUN SCREEN pd210p &
        PASSING T_CIECLEMNU,        &
                T_CIENOM,           &
                T_CLICLE,           & 
                PDDETAIL_C_I        &
        MODE F

   ; Mise a jour de la ligne de commande
   DO INTERNAL MAJ_LIGNE_COMMANDE

  PUSH FIND
END

;-------------------------------------------------------------------------------
;
; Ventilation comptable
;
PROCEDURE DESIGNER D002
BEGIN

  FOR PDDETAIL_C_I
    IF ALTEREDRECORD OF PDDETAIL_C_I
    THEN ERROR 1390 ; Vous devez faire une mise a jour avant d'acceder
                    ; le sous-ecran.

  RUN SCREEN  pd210f &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDCOMMAN_INTERNE,   &
                      PDDETAIL_C_I        &
              MODE F
END

;------------------------------------------------------------------------------
;
; Recherche le dernier numéro de ligne utilisé
;
PROCEDURE POSTFIND
BEGIN
  GET A_DCI_DETAIL OPTIONAL  &
    VIA     CIECLE,          &
            PJTABR,          &
            PJTANN,          &
            COMNUMCOMINT     &
    USING   T_CIECLEMNU,                          &
            PJTABR           OF PDCOMMAN_INTERNE, &
            PJTANN           OF PDCOMMAN_INTERNE, &
            COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
    ORDERBY CIECLE,          &
            PJTABR,          &
            PJTANN,          &
            COMNUMCOMINT,    &
            DCINUMLIG        DESCENDING
  IF ACCESSOK
  THEN BEGIN
    LET T_NOLIG = NCONVERT(DCINUMLIG OF A_DCI_DETAIL)
  END

  ; Assignation de la variable T_OLD_IFLCLE
  FOR PDDETAIL_C_I
   LET T_OLD_IFLCLE = IFLCLE OF PDDETAIL_C_I

END

;------------------------------------------------------------------------------
;
; Empeche de saisir une ligne de detail de commande dans le cas
; ou la commande est annule
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST

  IF COMSTU OF PDCOMMAN_INTERNE = "X"
  THEN ERROR 3291 ;PD391E Impossbiel d'ajouter une ligne de commande
END
;-------------------------------------------------------------------------------
;
;
;
PROCEDURE ENTRY
BEGIN
  FOR PDDETAIL_C_I
  BEGIN
    PERFORM APPEND
  END
END

;------------------------------------------------------------------------------
;
;
PROCEDURE INTERNAL VALID_DEST
BEGIN

  WHILE RETRIEVING A_IFBLOC              &
     VIA   PJTABR,                       &
           PJTANN,                       &
           COMNUMCOMINT,                 &
           DCINUMLIG                     &
     USING PJTABR OF PDDETAIL_C_I,       &
           PJTANN OF PDDETAIL_C_I,       &
           COMNUMCOMINT OF PDDETAIL_C_I, &
           DCINUMLIG OF PDDETAIL_C_I
   BEGIN
     IF EXNCLE OF A_IFBLOC <> 0
     THEN ERROR 3289 ; PD389 Impossible de supprimer cette ligne
   END

END
;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ; Verifier que un des blocs de la ligne n'a pas ete
  ; expedier avant de supprimer la ligne

  FOR PDDETAIL_C_I
  BEGIN

    IF DELETEDRECORD OF PDDETAIL_C_I
    THEN DO INTERNAL VALID_DEST

  END
  
  IF (COMSTU OF PDCOMMAN_INTERNE = "T") OR &
     (COMSTU OF PDCOMMAN_INTERNE = "A") OR &
     (COMSTU OF PDCOMMAN_INTERNE = "X")
  THEN ERROR = "Le status de la commande interne ne permet pas de modifier"

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDETAIL_C_I &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDETAIL_C_I &
     AND NOT NEWRECORD     OF PDDETAIL_C_I &
     AND NOT DELETEDRECORD OF PDDETAIL_C_I &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

;------------------------------------------------------------------------------
;
; Mise a jour des bloc dont la ligne de commande a ete supprime
;
PROCEDURE INTERNAL MAJ_BLOC
BEGIN
  WHILE RETRIEVING A_IFBLOC              &
     VIA   PJTABR,                       &
           PJTANN,                       &
           COMNUMCOMINT,                 &
           DCINUMLIG                     &
     USING PJTABR OF PDDETAIL_C_I,       &
           PJTANN OF PDDETAIL_C_I,       &
           COMNUMCOMINT OF PDDETAIL_C_I, &
           DCINUMLIG OF PDDETAIL_C_I
  BEGIN
    LET COMNUMCOMINT OF A_IFBLOC = " "
    LET DCINUMLIG OF A_IFBLOC = " "
    LET COEDATDEB OF A_IFBLOC = 0
    LET IFLCLE OF A_IFBLOC = " "
    PUT A_IFBLOC RESET
  END
END

;------------------------------------------------------------------------------
;
; De-assignation des blocs se rattachant a la ou les lignes de commandes
; supprimes
;
PROCEDURE UPDATE
BEGIN
   FOR PDDETAIL_C_I
   BEGIN

     ; Mise a jour des blocs de-assignes
     IF DELETEDRECORD OF PDDETAIL_C_I
     THEN DO INTERNAL MAJ_BLOC

     PUT PDDETAIL_C_I
   END

   PUT PDCOMMAN_INTERNE
END

;------------------------------------------------------------------------------
;
; Mise-a-jour des lots sur les blocs
;
PROCEDURE INTERNAL MAJ_BLOC_LOT
BEGIN

   WHILE RETRIEVING A_IFBLOC              &
      VIA   PJTABR,                       &
            PJTANN,                       &
            COMNUMCOMINT,                 &
            DCINUMLIG                     &
      USING PJTABR OF PDDETAIL_C_I,       &
            PJTANN OF PDDETAIL_C_I,       &
            COMNUMCOMINT OF PDDETAIL_C_I, &
            DCINUMLIG OF PDDETAIL_C_I
    BEGIN
       LET IFLCLE OF A_IFBLOC = IFLCLE OF PDDETAIL_C_I
       PUT A_IFBLOC RESET
    END

    LET T_OLD_IFLCLE = IFLCLE OF PDDETAIL_C_I
END

;------------------------------------------------------------------------------
;
; Assignation du numero de lot aux blocs se rattachant a la ligne
; de commande dans le cas ou il y a un changement au niveau du numero de lot
;
PROCEDURE POSTUPDATE
BEGIN

  FOR PDDETAIL_C_I
  BEGIN
     IF T_OLD_IFLCLE <> IFLCLE OF PDDETAIL_C_I
     THEN DO INTERNAL MAJ_BLOC_LOT
  END
END

;-------------------------------------------------------------------------------
;
; Destruction des lignes de ventilation associer à une ligne de commande interne
;
PROCEDURE INTERNAL DESTR_LIGNE_VENTILATION
BEGIN
  WHILE RETRIEVING PDVENTILATION &
    VIA     PJTABR,           &
            PJTANN,           &
            COMNUMCOMINT,     &
            DCINUMLIG,        &
            CIECLE            &
    USING   PJTABR            OF PDDETAIL_C_I, &
            PJTANN            OF PDDETAIL_C_I, &
            COMNUMCOMINT      OF PDDETAIL_C_I, &
            DCINUMLIG         OF PDDETAIL_C_I, &
            CIECLE            OF PDDETAIL_C_I
  BEGIN
    DELETE PDVENTILATION
    PUT PDVENTILATION
  END
END

;-------------------------------------------------------------------------------
;
; Procédure DELETE
;
PROCEDURE DELETE
BEGIN

  ;
  ; Valide que l'on peut détruire les lignes de detail de la commande
  ;
  IF NOT DELETEDRECORD OF PDDETAIL_C_I
  THEN BEGIN
    IF DCILIADIA    OF PDDETAIL_C_I = "1"
    THEN BEGIN
      ERROR 3171 ; La production a déjà débuté pour cette ligne de détail
    END
    ;
    ; Destruction des lignes de ventilation de la commande
    ;
    ELSE BEGIN
      WHILE RETRIEVING PDVENTILATION &
        VIA     PJTABR,           &
                PJTANN,           &
                COMNUMCOMINT,     &
                DCINUMLIG,        &
                CIECLE            &
        USING   PJTABR            OF PDDETAIL_C_I, &
                PJTANN            OF PDDETAIL_C_I, &
                COMNUMCOMINT      OF PDDETAIL_C_I, &
                DCINUMLIG         OF PDDETAIL_C_I, &
                CIECLE            OF PDDETAIL_C_I
      BEGIN
        DELETE PDVENTILATION
        PUT PDVENTILATION
      END
      DELETE PDDETAIL_C_I
    END
  END
END

BUILD DETAIL LIST
