;/T/ Approbation des budgets initiaux
;
;/P/ Programmeur...: Nancy Marceau
;
;    Date Création.: 8 juillet 1994
;
;    Description...: Cette procédure permet de faire l'approbation des budgets
;                    pour une compagnie en particulier et une année.
;                    L'approbation met à jour la date ainsi que l'usager qui
;                    a procédé. De plus, les budgets initiaux sont transférés
;                    dans les budgets courants.
;
;    Paramètres....: Compagnie (Compagnie du menu)
;                    Année     (Année)
;
;    Particularités: Aucune

USE ussetqzs NOLIST   ; permet de faire les SET PROCESS NOLIMIT....

ACCESS *WGL800A &
 LINK CIECLE OF WGL800A , &
      UNACLE OF WGL800A   &
   TO CIECLE, &
      UNACLE              OF MCUNITE_ADM &
 LINK CPTCLE OF WGL800A   &
   TO CPTCLE              OF MCCOMPTE

SORT ON CIECLE OF WGL800A &
     ON UNACLE OF WGL800A &
     ON CPTCLE OF WGL800A

DEFINE D_BDGINI FLOAT SIZE 8 = SANBDG1 OF WGL800A &
                             + SANBDG2 OF WGL800A &
                             + SANBDG3 OF WGL800A &
                             + SANBDG4 OF WGL800A &
                             + SANBDG5 OF WGL800A &
                             + SANBDG6 OF WGL800A &
                             + SANBDG7 OF WGL800A &
                             + SANBDG8 OF WGL800A &
                             + SANBDG9 OF WGL800A &
                             + SANBDG10 OF WGL800A &
                             + SANBDG11 OF WGL800A &
                             + SANBDG12 OF WGL800A &
                             + SANBDG13 OF WGL800A &
                         IF CPTCODDC OF MCCOMPTE = "D" &
                          ELSE 0 &
                             - SANBDG1 OF WGL800A &
                             - SANBDG2 OF WGL800A &
                             - SANBDG3 OF WGL800A &
                             - SANBDG4 OF WGL800A &
                             - SANBDG5 OF WGL800A &
                             - SANBDG6 OF WGL800A &
                             - SANBDG7 OF WGL800A &
                             - SANBDG8 OF WGL800A &
                             - SANBDG9 OF WGL800A &
                             - SANBDG10 OF WGL800A &
                             - SANBDG11 OF WGL800A &
                             - SANBDG12 OF WGL800A &
                             - SANBDG13 OF WGL800A
DEFINE D_BDGREV FLOAT SIZE 8 = SANBDGREV1 OF WGL800A &
                             + SANBDGREV2 OF WGL800A &
                             + SANBDGREV3 OF WGL800A &
                             + SANBDGREV4 OF WGL800A &
                             + SANBDGREV5 OF WGL800A &
                             + SANBDGREV6 OF WGL800A &
                             + SANBDGREV7 OF WGL800A &
                             + SANBDGREV8 OF WGL800A &
                             + SANBDGREV9 OF WGL800A &
                             + SANBDGREV10 OF WGL800A &
                             + SANBDGREV11 OF WGL800A &
                             + SANBDGREV12 OF WGL800A &
                             + SANBDGREV13 OF WGL800A &
                         IF CPTCODDC OF MCCOMPTE = "D" &
                          ELSE 0 &
                             - SANBDGREV1 OF WGL800A &
                             - SANBDGREV2 OF WGL800A &
                             - SANBDGREV3 OF WGL800A &
                             - SANBDGREV4 OF WGL800A &
                             - SANBDGREV5 OF WGL800A &
                             - SANBDGREV6 OF WGL800A &
                             - SANBDGREV7 OF WGL800A &
                             - SANBDGREV8 OF WGL800A &
                             - SANBDGREV9 OF WGL800A &
                             - SANBDGREV10 OF WGL800A &
                             - SANBDGREV11 OF WGL800A &
                             - SANBDGREV12 OF WGL800A &
                             - SANBDGREV13 OF WGL800A

;
; On n'imprime pas les lignes a zéro, cela évite d'imprimer
; les comptes de bilan
;
SELECT IF D_BDGINI <> 0 OR D_BDGREV <> 0


 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &               ; titre1  du rapport
@IF FRANCAIS
                "Liste d'approbation des budgets"
@ELSE
                "%%%Liste d'approbation des budgets"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
                "Exécution terminée"             ; titre2  du rapport
 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GL800"         ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM            ; Nom de la compagnie

USE ustit180 NOLIST  ; Titre standard pour l'application
SKIP &
@IF FRANCAIS
  TAB 001 "Année........:" &
@ELSE
  TAB 001 "%%%Année........:" &
@ENDIF
  TAB 016 SANANN OF WGL800A   &
@IF FRANCAIS
  TAB 031 "Approuvé par.:" &
@ELSE
  TAB 031 "%%%Approuvé par.:" &
@ENDIF
  TAB 046 SANUSRAPP OF WGL800A &
 SKIP 2 &
  TAB 003 "       Unité administrarive                                       Compte                     Budget initial Budget courant" &
 SKIP &
  TAB 003 "------------------------------------------------- ------------------------------------------ -------------- --------------" &
 SKIP 2

REPORT &
   TAB 003 UNACLE OF WGL800A     PRINT AT UNACLE &
   TAB 012 UNANOM OF MCUNITE_ADM PRINT AT UNACLE &
   TAB 053 CPTCLE OF WGL800A     PRINT AT CPTCLE &
   TAB 060 CPTDSCFRA OF MCCOMPTE PRINT AT CPTCLE &
   TAB 098 D_BDGINI PICTURE "^^,^^^,^^^ " TRAILING SIGN "-"  OUTPUT SCALE -2 &
   TAB 112 D_BDGREV PICTURE "^^,^^^,^^^ " TRAILING SIGN "-"  OUTPUT SCALE -2

FINAL FOOTING &
   TAB 088 "Total:" &
   TAB 098 "----------" &
   TAB 112 "----------" &
 SKIP &
   TAB 098 D_BDGINI SUBTOTAL PICTURE "^^,^^^,^^^ " TRAILING SIGN "-"  OUTPUT SCALE -2  &
   TAB 112 D_BDGREV SUBTOTAL PICTURE "^^,^^^,^^^ " TRAILING SIGN "-"  OUTPUT SCALE -2



BUILD gl800z

;                                                                                                   1         1         1         1
;         1         2         3         4         5         6         7         8         9         0         1         2         3
;123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
;GL800                                                    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                         Page:9999
;99/99/99 hh:mm xxxxxxxxxxxx                                  Liste d'approbation des budgets
;                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
;
;Année........: xx
;Approuvé par.: xxxxxxxxxxxx
;
;            Unité   administrative                                     Compte                 Budget initial Budget courant
; xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
; xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
; xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;                                                   123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999    99,999,999
;
;

