;/T/ Dépisteur Bloc de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1996-06-18
;
;    Description..: Permet de visionner et de choisir un bloc de granit
;
;    Paramètres...: T_BLONUMGRA001APR ==> Numéro de bloc
;                   T_BLONUMGRA002APR ==> Numéro de bloc
;                   T_BLORECAPR       ==> Numéro de bloc
;                   T_COD             ==> 0 = TOUS  ,
;                                         1 = (R ou T)
;                                         2 = <> T
;                   T_STU             ==> A (Abandonne)
;                   T_FTRNUMFAC       ==> Numéro de facture
;
;
;    Attention....: La procédure find a été codé
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd136 NOMODE NOACTION FIELDMARK &
             FROM 11,27 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_SPRNUMSOR,       &
                       T_BLONUMGRA001APR, &
                       T_BLONUMGRA002APR, &
                       T_BLORECAPR,       &
                       T_CHAMP,           &
                       T_COD,             &
                       T_STU,             &
                       T_CIECLEMNU ,&
                       T_BLOLONBRU,&
                       T_BLOHAUBRU,&
                       T_BLOLARBRU,&
                       T_TGRCODGRA,&
                       T_BLOSTU

TEMPORARY T_BLOLONBRU INTEGER SIZE 4
TEMPORARY T_BLOHAUBRU INTEGER SIZE 4
TEMPORARY T_BLOLARBRU INTEGER SIZE 4
TEMPORARY T_TGRCODGRA CHAR SIZE 3
TEMPORARY T_BLOSTU    CHAR SIZE 1
TEMPORARY T_SPRNUMSOR INTEGER SIZE 4
TEMPORARY T_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_BLONUMGRA002APR  CHARACTER SIZE 05
TEMPORARY T_BLORECAPR        CHARACTER SIZE 01
TEMPORARY T_CHAMP            INTEGER UNSIGNED SIZE 1
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 2
TEMPORARY T_STU              CHARACTER SIZE 1
TEMPORARY T_CIECLEMNU        CHARACTER SIZE 04
TEMPORARY T_FTRNUMFAC        INTEGER SIGNED   SIZE 04

TEMPORARY T_LIRE_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_LIRE_BLONUMGRA002APR  CHARACTER SIZE 05
TEMPORARY T_LIRE_BLORECAPR        CHARACTER SIZE 01


USE pd136.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDSORTIE_BLOCS PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,            &
                 SPRNUMSOR,         &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR          &
         USING   T_CIECLEMNU,       &
                 T_SPRNUMSOR,       &
                 T_BLONUMGRA001APR, &
                 T_BLONUMGRA002APR, &
                 T_BLORECAPR       &
         ORDERBY CIECLE,            &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC REFERENCE OCCURS WITH PDSORTIE_BLOCS

  ACCESS VIA     CIECLE,            &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR          &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR    OF PDSORTIE_BLOCS, &
                 BLONUMGRA002APR    OF PDSORTIE_BLOCS, &
                 BLORECAPR          OF PDSORTIE_BLOCS


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,54       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    No. bloc      Long  Haut  Épa.  Gra S Num. Cmd" &
@ELSE
      "%%% No. bloc      Long  Haut  Épa.  Gra S Num. Cmd" &
@ENDIF
    AT ,2

SKIP TO LINE 3

ALIGN(3,3,6) (,10,11) (,16,17) (,,20) (,,26) (,,32) (,,38) (,,42) (,,44) (,46,47) (,49,50)


CLUSTER OCCURS WITH PDBLOC


FIELD BLONUMGRA001APR  OF PDSORTIE_BLOCS &
        HIDDEN LABEL ">>" DISPLAY


FIELD BLONUMGRA002APR  OF PDSORTIE_BLOCS &
        DISPLAY                            &
        LABEL "-"


FIELD BLORECAPR        OF PDSORTIE_BLOCS &
        DISPLAY                            &
        LABEL "-"


FIELD BLOLONBRU        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL &
        BWZ


FIELD BLOHAUBRU        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL &
        BWZ

FIELD BLOLARBRU        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL &
        BWZ


FIELD TGRCODGRA        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL


FIELD BLOSTU           OF PDBLOC           &
         DISPLAY                           &
         NOLABEL


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
; Met le statut à "A" (abandonné)

PROCEDURE EXIT
BEGIN
  IF T_BLONUMGRA001APR = " " &
  AND T_BLONUMGRA002APR  = " " &
  AND T_BLORECAPR        = " "
  THEN BEGIN
    LET T_STU = "A"
  END
  ELSE BEGIN
    LET T_STU = " "
  END

  IF 0 <> INDEX(T_BLONUMGRA001APR , "@")
  THEN LET T_BLONUMGRA001APR = ""

  IF 0 <> INDEX(T_BLONUMGRA002APR , "@")
  THEN LET T_BLONUMGRA002APR = ""

  IF 0 <> INDEX(T_BLORECAPR , "@")
  THEN LET T_BLORECAPR = ""

END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE FIND
BEGIN

  IF T_BLONUMGRA001APR <> " " &
  OR T_BLONUMGRA002APR  <> " " &
  OR T_BLORECAPR <> " "
  THEN BEGIN
    LET T_LIRE_BLONUMGRA001APR  = T_BLONUMGRA001APR
    LET T_LIRE_BLONUMGRA002APR  = T_BLONUMGRA002APR
    LET T_LIRE_BLORECAPR        = T_BLORECAPR
    LET T_BLONUMGRA001APR  = " "
    LET T_BLONUMGRA002APR  = " "
    LET T_BLORECAPR        = " "
  END

  ;
  ; Lors de l'appel on a passé seulement le premier parametre pour la recherche
  ;
  IF T_CHAMP = 1
  THEN BEGIN
    FOR PDSORTIE_BLOCS
    BEGIN
      GET PDSORTIE_BLOCS &
        VIA     CIECLE,           &
                SPRNUMSOR, &
                BLONUMGRA001APR   &
        USING   T_CIECLEMNU,            &
                T_SPRNUMSOR, &
                T_LIRE_BLONUMGRA001APR  &
        ORDERBY CIECLE,           &
                BLONUMGRA001APR , &
                BLONUMGRA002APR , &
                BLORECAPR
    END
  END
  ELSE BEGIN
    ;
    ; Lors de l'appel on a passé seulement les deux premier parametre
    ; pour la recherche
    ;
    IF T_CHAMP = 2
    THEN BEGIN
      FOR PDSORTIE_BLOCS
      BEGIN
        GET PDSORTIE_BLOCS &
          VIA     CIECLE,             &
                  SPRNUMSOR,&
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR     &
          USING   T_CIECLEMNU,             &
                  T_SPRNUMSOR,&
                   T_LIRE_BLONUMGRA001APR , &
                  T_LIRE_BLONUMGRA002APR   &
          ORDERBY CIECLE,             &
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR ,   &
                  BLORECAPR
      END
    END
    ELSE BEGIN
    ;
    ; Lors de l'appel on a passé les trois parametres pour la recherche
    ;
      FOR PDSORTIE_BLOCS
      BEGIN
        GET PDSORTIE_BLOCS &
          VIA     CIECLE,             &
                  SPRNUMSOR,&
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR ,   &
                  BLORECAPR           &
          USING   T_CIECLEMNU,             &
                  T_SPRNUMSOR,&
                  T_LIRE_BLONUMGRA001APR , &
                  T_LIRE_BLONUMGRA002APR , &
                  T_LIRE_BLORECAPR         &
          ORDERBY CIECLE,             &
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR ,   &
                  BLORECAPR
      END
    END
  END
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDSORTIE_BLOCS
  LET T_BLONUMGRA002APR  = BLONUMGRA002APR OF PDSORTIE_BLOCS
  LET T_BLORECAPR        = BLORECAPR       OF PDSORTIE_BLOCS
  LET T_BLOLONBRU        = BLOLONBRU       OF PDBLOC
  LET T_BLOHAUBRU        = BLOHAUBRU       OF PDBLOC
  LET T_BLOLARBRU        = BLOLARBRU       OF PDBLOC
  LET T_TGRCODGRA        = TGRCODGRA       OF PDBLOC
  LET T_BLOSTU           = BLOSTU          OF PDBLOC

  RETURN
END

BUILD

@IF FORMAT

                          ******************************************************
                          *    No. bloc      Long  Haut  Épa.  Gra S Num. Cmd  *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          *                                                    *
                          ******************************************************
@ENDIF
