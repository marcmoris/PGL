;/T/ Liste des m3 par type/couleur/qualité de granit(non assigné).
;
;/M/ Réalisé par.......: Marc Poulin
;    Date..............: 07 avril 2000
;    Description.......: Calcul du métrage cubes disponible par type et couleur de granit.
;
;    Référence.........: Demande directe - Gestion de blocs (Demandeur: Jacky Lecarlme).
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
; Configuration standard
;
USE ussetqzs NOLIST
;
; Non-impression des lignes blanches
;
SET NOBLANKS
;
; Déclaration des accès
;
ACCESS IFBLOC LINK  CIECLE    OF IFBLOC                 , & ; Table des blocs de granit
                    TGRCODGRA OF IFBLOC                   &
                TO  CIECLE                              , &
                    TGRCODGRA OF PDTYPE_GRANIT  OPTIONAL  & ; Table des types de granit

              LINK  CIECLE    OF IFBLOC                 , &
                    CLRCLE    OF IFBLOC                   &
                TO  CIECLE                              , &
                    CLRCLE    OF IFCOULEUR      OPTIONAL  & ; Table des couleurs de granit

              LINK  CIECLE    OF IFBLOC                 , &
                    QLTCLE    OF IFBLOC                   &
                TO  CIECLE                              , &
                    QLTCLE    OF IFQUALITE      OPTIONAL  & ; Table des qualités de granit

              LINK  CIECLE    OF IFBLOC                 , &
                    CATCLE    OF IFBLOC                   &
                TO  CIECLE                              , &
                    CATCLE    OF IFCATEGORIE    OPTIONAL    ; Table des catégories de granit
;
; Sélection selon les paramètres saisis par l'utilisateur
;
CHOOSE CIECLE       PARM, & ; Code de compagnie
       TGRCODGRA    PARM, & ; Type de granit
       CLRCLE       PARM, & ; Couleur
       QLTCLE       PARM, & ; Qualité
       CATCLE       PARM, & ; Catégorie
       COMNUMCOMINT " "     ; Numéro de commande - Sélection des blocs non assignés à une commande
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF341Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM
;
; Definition de variables de travail
;
DEFINE D_CLRCLE CHARACTER SIZE 03 = RJ(CLRCLE OF IFBLOC) ; Couleur
DEFINE D_QLTCLE CHARACTER SIZE 03 = RJ(QLTCLE OF IFBLOC) ; Qualité
DEFINE D_CATCLE CHARACTER SIZE 03 = RJ(CATCLE OF IFBLOC) ; Catégorie
;
; Definition de variables de travail associées à l'impression sommaire.
;
DEFINE D_DETAIL_PARM CHARACTER SIZE 01  = PARM

DEFINE DV_EFF_DETAIL VARCHAR   SIZE 132 = ""                &
                                     IF D_DETAIL_PARM = "1" &
                                   ELSE " "[1:130]
                                   
DEFINE DV_TOT_CLR    VARCHAR   SIZE 065 = "Total de m3 pour la couleur  " + &
                                          D_CLRCLE                  + " " + &
                                          CLRDSCFRA OF IFCOULEUR            &
                                     IF D_DETAIL_PARM = "0"                 &
                                   ELSE ""

DEFINE DV_TOT_QLT    VARCHAR   SIZE 065 = "Total de m3 pour la qualité  " + &
                                          D_QLTCLE                  + " " + &
                                          QLTDSCFRA OF IFQUALITE            &
                                     IF D_DETAIL_PARM = "0"                 &
                                   ELSE ""

DEFINE D_POINT1      CHARACTER SIZE  01 = "."               &
                                     IF D_DETAIL_PARM = "0" &
                                   ELSE " "

DEFINE D_POINT2      CHARACTER SIZE  01 = "."               &
                                     IF D_DETAIL_PARM = "1" &
                                   ELSE " "
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste des m3 par type/couleur/qualité de granit(non assigné)"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Détaillé"            &
                                     IF D_DETAIL_PARM = "1" &
                                   ELSE "Sommaire"
;
; Tri
;
SORT ON CIECLE    OF IFBLOC   & ; Code de compagnie
     ON TGRCODGRA OF IFBLOC   & ; Type de granit
     ON D_CLRCLE              & ; Couleur
     ON D_QLTCLE              & ; Qualité
     ON D_CATCLE              & ; Catégorie
     ON IFCCLE    OF IFBLOC   & ; Année-mois du bloc
     ON BLCCLE    OF IFBLOC     ; Numéro de bloc
;
; Entête du rapport
;
USE ustit133 NOLIST
  SKIP 002                                                  &
   TAB 001 "Type de granit:"                                &
   TAB 017 TGRCODGRA    OF IFBLOC                           & ; Type de granit
   TAB 021 TGRDSCFRA001 OF PDTYPE_GRANIT                    & ; Description du type de granit
  SKIP 002                                                  &
   TAB 001 "           Couleur                         Qualité                      Catégorie                Bloc        Quantité  (m3)" &
  SKIP                                                      &
   TAB 001 "-----------------------------  -----------------------------  -----------------------------  --------------  --------------" &
  SKIP    
;
; Détail
;
REPORT                                                      &
   TAB 001 D_CLRCLE                                         & ; Couleur de granit
           PRINT AT D_CLRCLE                                &
   TAB 005 CLRDSCFRA    OF IFCOULEUR                        & ; Description de la couleur de granit
           PRINT AT D_CLRCLE                                &
   TAB 032 D_QLTCLE                                         & ; Qualité de granit
           PRINT AT D_QLTCLE                                &
   TAB 036 QLTDSCFRA    OF IFQUALITE                        & ; Description de la qualité de granit
           PRINT AT D_QLTCLE                                &
   TAB 063 D_CATCLE                                         & ; Catégorie de granit
   TAB 067 CATDSCFRA    OF IFCATEGORIE                      & ; Description de la catégorie de granit
   TAB 094 TGRCODGRA    OF IFBLOC                           & ; Numéro de bloc (type de granit)
   TAB 097 IFCCLE       OF IFBLOC                           & ; Numéro de bloc (code de numérotaion)
   TAB 099 "-"                                              &
   TAB 100 BLCCLE       OF IFBLOC                           & ; Numéro de bloc (numéo de bloc)
   TAB 106 "-"                                              &
   TAB 107 BLCRED       OF IFBLOC                           & ; Numéro de bloc (redimensionnent)
   TAB 110 BLCM3TOT     OF IFBLOC                           & ; Quantité totale du bloc en mêtres cube
           PICTURE "^^^,^^^,^^^.^^"                         &
           SIGNIFICANCE 4                                   &
           NOBWZ                                            &
   TAB 001 DV_EFF_DETAIL                                    & ; Variable de travail associée à la gestion de l'impression
   TAB 133 D_POINT1                                         & ; Variable de travail associée à la gestion de l'impression
           PRINT AT TGRCODGRA                               &
   RESERVE 5 LINES
;
; Sous-total par qualité de granit
;                                                 
FOOTING AT D_QLTCLE                                         &
   TAB 110 "--------------"                                 &
   TAB 001 DV_EFF_DETAIL                                    &
  SKIP 1                                                    &
   TAB 070 "Total de m3 pour la qualité      --->"          &
   TAB 098 D_QLTCLE                                         &
   TAB 044 DV_TOT_QLT                                       &
   TAB 110 BLCM3TOT     OF IFBLOC                           &
           SUBTOTAL                                         &
           PICTURE "^^^,^^^,^^^.^^"                         &
           SIGNIFICANCE 4                                   &
           NOBWZ                                            &
  SKIP 1                                                    &
   TAB 133 D_POINT2                                         &
  SKIP 1
;
; Sous-total par couleur de granit
;                                                 
FOOTING AT D_CLRCLE                                         &
   TAB 110 "--------------"                                 &
  SKIP 1                                                    &
   TAB 070 "Total de m3 pour la couleur      --->"          &
   TAB 098 D_CLRCLE                                         &
   TAB 044 DV_TOT_CLR                                       &
   TAB 110 BLCM3TOT     OF IFBLOC                           &
           SUBTOTAL                                         &
           PICTURE "^^^,^^^,^^^.^^"                         &
           SIGNIFICANCE 4                                   &
           NOBWZ                                            &
  SKIP 2
;
; Total par type de granit de granit
;                                                 
FOOTING AT TGRCODGRA                                       &
  TAB 110 "=============="                                 &
 SKIP 1                                                    &
  TAB 063 "Total de m3 pour le type de granit      --->"   &
  TAB 099 TGRCODGRA    OF IFBLOC                           &
  TAB 110 BLCM3TOT     OF IFBLOC                           &
          SUBTOTAL                                         &
          PICTURE "^^^,^^^,^^^.^^"                         &
          SIGNIFICANCE 4                                   &
          NOBWZ                                            &
 SKIP PAGE


BUILD if341z


@IF FORMAT
                                                                                                   1         1         1         1  
         1         2         3         4         5         6         7         8         9         0         1         2         3  
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012

IF341Z                                                   GRANICOR INC. - DEVELOPPEMENT                                    PAGE: 9999
99/99/99 99:99 prosig                     Sommaire des m3 par type et couleur de granit (non assignés)

Type de granit: XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

           Couleur                         Qualité                      Catégorie                Bloc        Quantité  (m3)
-----------------------------  -----------------------------  -----------------------------  --------------  --------------
XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX-XXXXXX-   999,999,999,99
                               XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX-XXXXXX-   999,999,999,99
                               XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX-XXXXXX-   999,999,999,99
                               XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX-XXXXXX-   999,999,999,99
                               XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX-XXXXXX-   999,999,999,99
                                                                                                             --------------
                                           Total de m3 pour la qualité  XXX XXXXXXXXXXXXXXXXXXXXXXXXX --->   999,999,999,99
                                                                                                             --------------
                                                                     Total de m3 pour la couleur  XXX --->   999,999,999,99

                                                                                                             ==============
                                                              Total de m3 pour le type de granit  XXX --->   999,999,999,99


123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
         1         2         3         4         5         6         7         8         9         0         1         2         3  
                                                                                                   1         1         1         1  

                            Maintenance de programme
+------------------------------------------------------------------------------+
|Module........: IFL1 Listes des blocs                                         |
|Rang ds menu..: 0162                                                          |
|Nom Programme.: IF341     Genre.........: R                                   |
|Descrip. Fra..: Liste des m3 par type/couleur/qualité de granit(non assigné)
|Descrip. Ang..:                                                               |
|                                                                              |
|Intéractif....: L  Lot                                                        |
|Liste paramèt.: O  ui                                                         |
|File d'attente: SYSB Batch régulière                                          |
|Forme........:  F02                                                           |
|Code d'imprim.: SYS  Imprimante système                                       |
|Nbre Copies..:  01                                                            |
|Paramèt. PRINT:                                                               |
|PRINT standard: O  ui                           Créé par.....:  prosig        |
|Prio. d'impres:                                 Modifié par...: prosig        |
|                                                Dat. modifica.: 2000/04/07    |
|                                                Dat. fin......:               |
+------------------------------------------------------------------------------+
+--------------------------- Paramètres d'Exécution ---------------------------+
| Lg    Paramètre                                                              |
| 02    $quiz
| 04    execute if341z
| 06    %CUF_CIECLEMNU
| 08    %C TGRCODGRA
| 10    %C CLRCLE
| 12    %C CATCLE
| 14    %C QLTCLE
| 16    %DUF_CIENOM
| 18    %D DETAIL
| 20    exit
+------------------------------------------------------------------------------+
@ENDIF
