;/T/ Adresse Client,Employé,Fourniss.  REFCIECLE, REFCLETYP, REFCLENUM, TYPADR, RADCLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 20 mai 1993
;
;    Description..: Permet de visionner et de choisir une adresse
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN mc118 NOMODE NOACTION FIELDMARK &
             FROM 11,27 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU,&
                       T_REFCIECLE, &
                       T_REFCLETYP, &
                       T_REFCLENUM, &
                       T_TYPADR   , &
                       T_RADCLE   , &
                       T_PJTABR   , &
                       T_COMNUMCOMINT_T

USE mc118.hlp NOLIST

;-------------------------------------------------------------------------------
;

TEMPORARY T_REFCIECLE CHARACTER SIZE 4
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_REFCLETYP CHARACTER SIZE 1
TEMPORARY T_REFCLENUM CHARACTER SIZE 6
TEMPORARY T_TYPADR    CHARACTER SIZE 3
TEMPORARY T_RADCLE    INTEGER UNSIGNED SIZE 2
TEMPORARY T_PJTABR    CHARACTER SIZE 2
TEMPORARY T_COMNUMCOMINT_T CHAR SIZE 3

;-------------------------------------------------------------------------------
;

FILE PDCOMMAN_INTERNE PRIMARY
ACCESS VIA   CIECLE,&
             PJTABR,&
             COMNUMCOMINT,&
             CLICLE &
       USING T_CIECLEMNU,&
             T_PJTABR,&
             T_COMNUMCOMINT_T,&
             T_REFCLENUM &
         ORDERBY COMNUMCOM

;------------------------------------------------------------------------

FILE MCREF_ADRESSE    REFERENCE

  ACCESS VIA   REFCIECLE,                &
               REFCLETYP,                &
               REFCLENUM,                &
               RADCLE                    &
         USING CLICIECLE        OF PDCOMMAN_INTERNE, &
               "C",                                  &
               CLICLE           OF PDCOMMAN_INTERNE, &
               COMNUMADRLIV     OF PDCOMMAN_INTERNE


 ;
 ; Compte à payer
 ;   PAM = paiement
 ;   ACH = achat
 ;   RET = retour de marchandise
 ; Compte à recevoir
 ;   FAC = facturation
 ;   LIV = livraison
 ;   ETA = état de compte
 ;
 SELECT IF (RADFACCR    OF MCREF_ADRESSE = "1") OR &
           (RADLIVCR    OF MCREF_ADRESSE = "1")



;-------------------------------------------------------------------------------
;
TEMPORARY T_TYPADRAFF CHARACTER SIZE 70 ; Pour afficher les types d'adresses

;-------------------------------------------------------------------------------
;
DRAW 1,1 TO 13,54

USE ushilite NOLIST

USE ustitoff NOLIST

SKIP TO 3
ALIGN(3,3,13)
FIELD COMNUMCOM OF PDCOMMAN_INTERNE &
    HIDDEN  &
    DISPLAY &
    LABEL "Commande.:"

FIELD COMNOM OF PDCOMMAN_INTERNE &
    HIDDEN  &
    LABEL "Desc.....:" &
    DISPLAY &
    FOR ,25

ALIGN(3,3,13) (,,18)

FIELD RADCLE    OF MCREF_ADRESSE HIDDEN  &
@IF FRANCAIS
        LABEL ">> Séq..:"
@ELSE
        LABEL ">> Seq..:"
@ENDIF

FIELD T_TYPADRAFF DISPLAY FOR 2,35

;SKIP 1

ALIGN(,3,13)

FIELD RADNOM  OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "Nom.....:"
@ELSE
        LABEL "Name....:"
@ENDIF

FIELD RADADR1 OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "Adresse.:"
@ELSE
        LABEL "Address.:"
@ENDIF

FIELD RADADR2 OF MCREF_ADRESSE NOLABEL
FIELD RADADR3 OF MCREF_ADRESSE NOLABEL
FIELD RADADR4 OF MCREF_ADRESSE NOLABEL

ALIGN(,3,13) (,22,27)

FIELD RADCP   OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "CP......:"
@ELSE
        LABEL "PC......:"
@ENDIF

FIELD RADTEL  OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "Tél:"
@ELSE
        LABEL "Tel:"
@ENDIF


;-------------------------------------------------------------------------------
;
; Affiche les types d'adresse sous une forme textuel.
;
;
PROCEDURE OUTPUT T_TYPADRAFF
BEGIN
  LET FIELDTEXT = ""

  IF RADPAMCP OF MCREF_ADRESSE = "1"
  THEN LET FIELDTEXT = "Paiement"

  IF RADACHCP OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Achat"
  END

  IF RADRETCP OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Retour marchandise"
  END

  IF RADFACCR OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Facturation"
  END

  IF RADLIVCR OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Livraison"
  END

  IF RADETACPTCR OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Etat de compte"
  END
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_RADCLE = RADCLE OF MCREF_ADRESSE
  RETURN
END

;---------------------------------------------------
;PROCEDURE PATH
;BEGIN
;     LET PATH = 1
;END
;---------------------------------------------------
;PROCEDURE FIND
;BEGIN
;     GET MCREF_ADRESSE OPT VIA REFCIECLE,REFCLETYP,REFCLENUM ORDERBY RADCLE &
;         USING CLICIECLE OF PDCOMMAN_INTERNE,"C",CLICLE OF PDCOMMAN_INTERNE
;     IF NOT ACCESSOK
;     THEN GET PDCOMMAN_INTERNE VIA CIECLE , PJTABR , CLICLE ORDERBY &
;           COMNUMCOM USING T_CIECLEMNU , T_PJTABR , T_REFCLENUM
;END

BUILD DETAIL LIST

;                          ******************************************************
;                          *                                                    *
;                          * >> Séq..: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *                                                    *
;                          * Nom.....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          * Adresse.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          * CP......: xxxxxxx  Tél: xxxxxxxxxxxxx              *
;                          *                                                    *
;                          ******************************************************
