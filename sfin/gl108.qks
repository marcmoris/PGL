;/T/ Consultation des E/F              EFITYP, EFICIECLE, EFICLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 18 mars 1992
;
;    Description..: Permet de visionner et de choisir une note ou un état
;                   financier.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gl108 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_EFITYP, T_EFICIECLE, T_EFICLE

USE gl108.hlp NOLIST

USE ussectmp NOLIST
    "GL108"

TEMPORARY T_EFITYP    CHARACTER SIZE 1
TEMPORARY T_EFICIECLE CHARACTER SIZE 4
TEMPORARY T_EFICLE    CHARACTER SIZE 4

FILE GLETAT_FINANCIER PRIMARY OCCURS 11

 ACCESS VIA   EFICIECLE,EFICLE &
        USING T_EFICIECLE,T_EFICLE
 ;
 ; Si le type = N     : alors on affiche seulement les notes, sinon
 ; Si le type = blanc : alors on affiche tous les types d'états financier sauf
 ;                      les notes.
 ;
 SELECT IF ( EFITYP OF GLETAT_FINANCIER = "N" AND &
             T_EFITYP = "N") OR &
           ( EFITYP OF GLETAT_FINANCIER <> "N" AND &
             T_EFITYP <> "N")

USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP 1

ALIGN(3,3,6)(,,11)

CLUSTER OCCURS WITH GLETAT_FINANCIER

FIELD EFICLE    OF GLETAT_FINANCIER HIDDEN LABEL ">>"
FIELD EFITITEF  OF GLETAT_FINANCIER

CLUSTER

;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_EFICLE, "@")
  THEN LET T_EFICLE = ""
END

;
;
; Il faut justifier le titre à gauche pour une meilleur présentation à
; l'écran, car le titre est conservé centré.
;
;
PROCEDURE OUTPUT EFITITEF
BEGIN
  LET FIELDTEXT = LJ(FIELDTEXT)
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_EFICLE = EFICLE OF GLETAT_FINANCIER
  RETURN
END

BUILD
;
;*****************************************
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;* >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;*****************************************
