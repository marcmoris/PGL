;/T/ Sommaire des transaction au sous-Projet

USE ussetqzs NOLIST
SET FORMFEED

ACCESS GPPRA_BDG_CTB &

  LINK  CIECLE, &
        PRJCLE  &
  TO    CIECLE, &
        PRJCLE OF GPPROJETS &

  LINK  CIECLE, &
        PRJCLE, &
        PRACLE  &
  TO    CIECLE, &
        PRJCLE, &
        PRACLE OF GPPRO_ACTIVITE &

  LINK  CIECLE, &
        UNACLE OF GPPRA_BDG_CTB &
  TO    CIECLE, &
        UNACLE OF MCUNITE_ADM &

  LINK  CPTCLE OF GPPRA_BDG_CTB &
  TO    CPTCLE OF VCPT_DSC &

  LINK  NACCLE OF VCPT_DSC &
  TO    NACCLE OF MCNATURE_CTB

CHOOSE CIECLE PARM, &
       PRJCLE PARM, &
       PRACLE PARM

DEFINE D_MNTTOT      FLOAT SIZE 8 = PACMNTENG OF GPPRA_BDG_CTB + &
                                    PACMNTREE OF GPPRA_BDG_CTB

DEFINE D_MNTBDG      FLOAT SIZE 8 = PACMNTBDG    OF GPPRA_BDG_CTB  &
                                 IF PRAPACDATAPP OF GPPRO_ACTIVITE = 0 &
                               ELSE PACMNTBDGREV OF GPPRA_BDG_CTB

DEFINE D_MNTDIS      FLOAT SIZE 8 = D_MNTBDG - D_MNTTOT


DEFINE D_MNTENG  FLOAT   SIZE 8 = PACMNTENG OF GPPRA_BDG_CTB
DEFINE D_MNTREE  FLOAT   SIZE 8 = PACMNTREE OF GPPRA_BDG_CTB

; Pour faire revenu - dépense
DEFINE D_MNTREE2 FLOAT   SIZE 8 = PACMNTREE OF GPPRA_BDG_CTB &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - PACMNTREE OF GPPRA_BDG_CTB

DEFINE D_MNTENG2 FLOAT   SIZE 8 = PACMNTENG OF GPPRA_BDG_CTB &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - PACMNTENG OF GPPRA_BDG_CTB

DEFINE D_MNTTOT2 FLOAT   SIZE 8 = D_MNTTOT                   &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - D_MNTTOT

DEFINE D_MNTBDG2 FLOAT   SIZE 8 = D_MNTBDG                   &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - D_MNTBDG

DEFINE D_MNTDIS2 FLOAT   SIZE 8 = D_MNTDIS                   &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - D_MNTDIS



DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
                       "Sommaire des transactions au sous-projet"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GP303"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

SORT ON PRJCLE &
     ON PRACLE &
     ON NACCLE &
     ON CPTCLE &
     ON UNACLE

USE ustit180 NOLIST
  KEEP

HEADING AT PRJCLE &
  TAB 001 "Projet :" &
  TAB 010 PRJCLE   OF GPPRA_BDG_CTB &
  TAB 020 PRJDSC1  OF GPPROJETS &
SKIP 1 &
  TAB 020 PRJDSC2  OF GPPROJETS &
SKIP 3

HEADING AT PRACLE &
  TAB 001 "Sous-projet :" &
  TAB 015 PRACLE    OF GPPRA_BDG_CTB &
  TAB 020 PRADSC1   OF GPPRO_ACTIVITE &
SKIP 4

REPORT &
  TAB 005 CPTCLE    OF GPPRA_BDG_CTB  HEAD "Compte"      &
  TAB 012 CPTDSCABR OF VCPT_DSC       HEAD "Description" &
  TAB 033 UNACLE    OF GPPRA_BDG_CTB  HEAD "Unite^Adm."  &
  TAB 042 UNANOMABR OF MCUNITE_ADM    HEAD "Description" &
  TAB 063 D_MNTENG PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" HEAD "Engagement" &
  TAB 080 D_MNTREE PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" HEAD "Réel" &
  TAB 097 D_MNTTOT PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Total" &
  TAB 114 D_MNTBDG PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Budget" &
  TAB 131 D_MNTDIS PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Disponibilité"

FOOT AT NACCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
  TAB 131 "---------------" &
SKIP &
  TAB 015 "Total" NACDSCFRA OF MCNATURE_CTB &
  TAB 063 D_MNTENG SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D_MNTREE SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D_MNTTOT SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 114 D_MNTBDG SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 131 D_MNTDIS SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP 2

FOOT AT PRACLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
  TAB 131 "---------------" &
SKIP &
  TAB 015 "Total projet:" &
  TAB 030 PRJCLE &
  TAB 040 "Sous-projet:" &
  TAB 055 PRACLE &
  TAB 063 D_MNTENG2 SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D_MNTREE2 SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D_MNTTOT2 SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 114 D_MNTBDG2 SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 131 D_MNTDIS2 SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP PAGE



FOOT AT PRJCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
SKIP &
  TAB 015 "Grand total projet:" &
  TAB 035 PRJCLE &
  TAB 063 D_MNTENG2 SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D_MNTREE2 SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D_MNTTOT2 SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 114 D_MNTBDG2 SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 131 D_MNTDIS2 SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP PAGE

BUILD gp303z
