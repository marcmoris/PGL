;/T/ Gestion des mémos.
;
;/P/ Programmeur..: Alain Côté / adapté pour les C.P. par Thomas BRENNEUR
;    Date Création: 7 Juin 1993
;
;    Description..: Saisie des mémos. Un mémo peut être spécifique à un
;                   fournisseur, ou sur la compagnie du menu. Un mémo
;                   appartient à un fournisseur s'il y a un numéro de
;                   fournisseur dans le mémo (FOUCIECLE,FOUCLE).
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp014 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_FOUCIECLE, &
                       T_FOUCLEFND, &
                       T_CIECLEMNU, &
                       T_CIENOM

USE ustrasse

USE ussectmp NOLIST
    "CP014"

USE cp014.hlp

USE usactecr NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-Ecrans"
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "Verification                    " ACTION DESIGNER D001
@ENDIF


;-------------------------------------------------------------------------------

TEMPORARY T_FOUCIECLE CHARACTER SIZE 4  ; Compagnie du fournisseur.
TEMPORARY T_FOUCLEFND CHARACTER SIZE 6  ; Numéro de fournisseur à rechercher.
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------
;
; RELATION MÉMO.
;
;   *** ATTENTION : LES PROCÉDURES PATH & FIND ONT ÉTÉ RÉÉCRITES ! ***
;
FILE CPMEMO PRIMARY

  ITEM CIECLE    OF CPMEMO INITIAL T_CIECLEMNU
  ITEM CPMTIMSTP OF CPMEMO INITIAL SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  ITEM FOUCIECLE OF CPMEMO INITIAL T_FOUCIECLE
  ITEM FOUCLE    OF CPMEMO INITIAL T_FOUCLEFND
  ITEM CPMDATCRE OF CPMEMO INITIAL SYSDATE
  ITEM CPMHRECRE OF CPMEMO INITIAL SYSTIME / 10000
  ITEM CPMUSRCRE OF CPMEMO INITIAL LOGONID
  ;
  ; On enregistre la date, et heure de modification, ainsi que l'usager
  ; qui modifie le mémo
  ;
  ITEM CPMDATMOD OF CPMEMO FINAL   SYSDATE        &
                                        IF NOT NEWRECORD OF CPMEMO AND &
                                           ALTEREDRECORD OF CPMEMO
  ITEM CPMHREMOD OF CPMEMO FINAL   SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF CPMEMO AND &
                                           ALTEREDRECORD OF CPMEMO
  ITEM CPMUSRMOD OF CPMEMO FINAL   LOGONID         &
                                        IF NOT NEWRECORD OF CPMEMO AND &
                                           ALTEREDRECORD OF CPMEMO

;-------------------------------------------------------------------------------
;
; Détail du mémo.
;
;   *** ATTENTION : LES PROCÉDURES PATH & FIND ONT ÉTÉ RÉÉCRITES ! ***
;
FILE CPCPM_LIGNE      DETAIL &
                      OCCURS 15 &
                      NOITEM

  ITEM CIECLE    OF CPCPM_LIGNE INITIAL CIECLE    OF CPMEMO
  ITEM CPMTIMSTP OF CPCPM_LIGNE INITIAL CPMTIMSTP OF CPMEMO

;
; On détruit toutes les lignes si le maitre est détruit.
;
FILE CPCPM_LIGNE      DELETE &
                      NOITEM &
                      ALIAS A_CPL_DELETE

  ACCESS VIA    CIECLE, &
                CPMTIMSTP &
         USING  CIECLE    OF CPMEMO, &
                CPMTIMSTP OF CPMEMO

;-------------------------------------------------------------------------------
;
; Validation et affichage du nom du fournisseur
;
FILE CPFOURNISSEUR  REFERENCE

  ACCESS VIA    FOUCIECLE, &
                FOUCLE     &
         USING  FOUCIECLE OF CPMEMO, &
                FOUCLE    OF CPMEMO

;-------------------------------------------------------------------------------

TEMPORARY T_FOUCLE    CHARACTER SIZE  6 ; Popup sur les fournisseurs

TEMPORARY T_NUMLIG INTEGER UNSIGNED SIZE 2 & ; Numérotation auto. des lignes.
                   OCCURS WITH CPMEMO

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des mémos " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN(3,3,19) (,,27)
FIELD FOUCLE OF CPMEMO HIDDEN ID 05 &
label "Fournisseur...:" &
        NOCHANGE &
        IF T_FOUCLEFND = ""

FIELD FOUNOM OF CPFOURNISSEUR &
        DISPLAY

ALIGN(3,3,19)

FIELD CPMDAT OF CPMEMO  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN ID 10 &
label "Date..........:" &
        REQUIRED

SKIP
DRAW ,1 TO ,80
SKIP 1

TITLE &
@IF FRANCAIS
      "Texte                                                         Ligne" &
@ELSE
      "Text                                                          Line " &
@ENDIF
      AT ,3

SKIP

ALIGN (3,2,3) (,,65)

CLUSTER OCCURS WITH CPCPM_LIGNE ID BASE 15

FIELD CPLTXT    OF CPCPM_LIGNE HIDDEN LABEL " "

FIELD CPLCLELIG OF CPCPM_LIGNE

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les fournisseurs.
;
;
PROCEDURE INPUT FOUCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cp104 MODE F PASSING T_FOUCIECLE, &
                                    T_FOUCLE, &
                                    T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_FOUCLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du numéro de fournisseur, ce champ est optionel.
;
;
PROCEDURE EDIT FOUCLE
BEGIN
  GET CPFOURNISSEUR OPTIONAL
  IF     0 <> SIZE( TRUNCATE( FIELDTEXT ) ) &
     AND NOT ACCESSOK
  THEN ERROR 437 ; Ce numéro de fournisseur n'existe pas.
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT CPLTXT
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Assigne un numéro de ligne automatiquement.
;
;
PROCEDURE INPUT CPLCLELIG
BEGIN
  IF     0 = SIZE( FIELDTEXT ) &
     AND 0 = CPLCLELIG OF CPCPM_LIGNE &
     AND NOT FINDMODE
  THEN LET FIELDTEXT = ASCII( ( T_NUMLIG + 100 ) / 100 )
END


;-------------------------------------------------------------------------------
;
;
; Incrémente le numéro de ligne.
;
;
PROCEDURE PROCESS CPLCLELIG
BEGIN
  IF T_NUMLIG < CPLCLELIG OF CPCPM_LIGNE
  THEN LET T_NUMLIG = CPLCLELIG OF CPCPM_LIGNE + 100
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Sous-écran   Vérification.
;
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN cp014a PASSING CPMEMO
END

;-------------------------------------------------------------------------------
;
;
; Recherche par fournisseur ou recherche globale.
;
;
PROCEDURE PATH
BEGIN
  IF T_FOUCLEFND <> ""
  THEN LET PATH = 99
  ELSE BEGIN
    REQUEST FOUCLE OF CPMEMO
    IF PROMPTOK
    THEN BEGIN
      REQUEST CPMDAT OF CPMEMO
      IF PROMPTOK
      THEN LET PATH = 1
      ELSE LET PATH = 2
    END
    IF PATH = 0
    THEN BEGIN
      REQUEST CPMDAT OF CPMEMO
      IF PROMPTOK
      THEN LET PATH = 3
    END
  END
END


PROCEDURE FIND
BEGIN
  ;
  ; Recherche sur le fournisseur reçu en paramètre.
  ;
  IF PATH = 99
  THEN GET CPMEMO VIA   CIECLE   , &
                        FOUCIECLE, &
                        FOUCLE     &
                  USING T_CIECLEMNU, &
                        T_FOUCIECLE, &
                        T_FOUCLEFND
  ;
  ; Recherche par fournisseur et date
  ;
  IF PATH = 1
  THEN GET CPMEMO VIA   CIECLE   , &
                        FOUCIECLE, &
                        FOUCLE   , &
                        CPMDAT     &
                  USING T_CIECLEMNU, &
                        T_FOUCIECLE, &
                        FOUCLE OF CPMEMO, &
                        CPMDAT OF CPMEMO

  ;
  ; Recherche par fournisseur.
  ;
  IF PATH = 2
  THEN GET CPMEMO VIA     CIECLE   , &
                          FOUCIECLE, &
                          FOUCLE     &
                  USING   T_CIECLEMNU, &
                          T_FOUCIECLE, &
                          FOUCLE OF CPMEMO &
                  ORDERBY CIECLE, &
                          CPMDAT DESCENDING
  ;
  ; Recherche par date.
  ;
  IF PATH = 3
  THEN GET CPMEMO VIA     CIECLE   , &
                          CPMDAT     &
                  USING   T_CIECLEMNU, &
                          CPMDAT OF CPMEMO &
                  ORDERBY FOUCIECLE, &
                          FOUCLE, &
                          CIECLE, &
                          CPMDAT

  ;
  ; Recherche par compagnie du menu.
  ;
  IF PATH = 0
  THEN GET CPMEMO VIA     CIECLE &
                  USING   T_CIECLEMNU &
                  ORDERBY FOUCIECLE, &
                          FOUCLE, &
                          CIECLE, &
                          CPMDAT
END


PROCEDURE DETAIL FIND
BEGIN
  FOR CPCPM_LIGNE
  BEGIN
    GET CPCPM_LIGNE VIA     CIECLE   , &
                            CPMTIMSTP  &
                    USING   CIECLE    OF CPMEMO, &
                            CPMTIMSTP OF CPMEMO  &
                    ORDERBY CIECLE, &
                            CPMTIMSTP, &
                            CPLCLELIG
  END
END


;-------------------------------------------------------------------------------
;
; VALIDATION DE LA MISE À JOUR
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPMEMO &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CPMEMO &
     AND NOT NEWRECORD     OF CPMEMO &
     AND NOT DELETEDRECORD OF CPMEMO &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Le timbre de modification du mémo est mis à jour si l'usager modifie
  ; le détail.
  ;
  IF NOT NEWRECORD OF CPMEMO
  THEN BEGIN
    FOR CPCPM_LIGNE
    BEGIN
      IF   NEWRECORD     OF CPCPM_LIGNE &
        OR ALTEREDRECORD OF CPCPM_LIGNE &
        OR DELETEDRECORD OF CPCPM_LIGNE
      THEN BEGIN
        LET CPMDATMOD OF CPMEMO = SYSDATE
        LET CPMHREMOD OF CPMEMO = SYSTIME / 10000
        LET CPMUSRMOD OF CPMEMO = LOGONID
      END
    END
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Gestion des mémos *******************************
* Fournisseur...: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
* Date..........: xxxxxxxx                                                     *
********************************************************************************
* Texte                                                         Ligne          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
********************************************************************************
@ENDIF
