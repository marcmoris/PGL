;/T/ 5.3 Dépisteur pour les projets
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-15
;
;    Description..: Permet de visionner et de choisir un projet
;
;    Paramètres...: T_PJTABR    ==> Code de projet
;                   T_PJTANN    ==> Année du projet
;                   T_PJTNUMSEQ ==> Numéro de séquence du projet
;                   T_PJTNUMPRO ==> Numéro de projet complèt
;                   T_CHAMP
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd129 NOMODE NOACTION FIELDMARK &
             FROM 11,24 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_PJTABR, &
                       T_PJTANN, &
                       T_PJTNUMSEQ, &
                       T_PJTNUMPRO, &
                       T_CHAMP,     &
                       T_CIECLEMNU



TEMPORARY T_PJTABR    CHARACTER SIZE 02
TEMPORARY T_PJTANN    CHARACTER SIZE 02
TEMPORARY T_PJTNUMSEQ CHARACTER SIZE 03
TEMPORARY T_PJTNUMPRO CHARACTER SIZE 07
TEMPORARY T_CHAMP     INTEGER UNSIGNED SIZE 1
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd129.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Projet
;

FILE PDPROJET       PRIMARY OCCURS 11

  ACCESS VIA     CIECLE,            &
                 PJTABR,            &
                 PJTANN,            &
                 PJTNUMSEQ          &
         USING   T_CIECLEMNU,       &
                 T_PJTABR,          &
                 T_PJTANN,          &
                 T_PJTNUMSEQ        &
         ORDERBY CIECLE,            &
                 PJTABR,            &
                 PJTANN,            &
                 PJTNUMSEQ


  ACCESS VIA     CIECLE,            &
                 PJTNUMPRO          &
         USING   T_CIECLEMNU,       &
                 T_PJTNUMPRO        &
         ORDERBY CIECLE,            &
                 PJTNUMPRO


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,57 ; Remplace le DRAW standard

USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

ALIGN (03,03,06) (,,16)

CLUSTER OCCURS WITH PDPROJET

FIELD PJTNUMPRO OF PDPROJET HIDDEN LABEL ">>" DISPLAY
FIELD PJTNOM    OF PDPROJET HIDDEN DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_PJTABR , "@")
  THEN BEGIN
    LET T_PJTABR = ""
  END

  IF 0 <> INDEX(T_PJTANN , "@")
  THEN BEGIN
    LET T_PJTANN = ""
  END

  IF 0 <> INDEX(T_PJTNUMSEQ , "@")
  THEN BEGIN
    LET T_PJTNUMSEQ = ""
  END

  IF 0 <> INDEX(T_PJTNUMPRO , "@")
  THEN BEGIN
    LET T_PJTNUMPRO = ""
  END

END

;-------------------------------------------------------------------------------
;
; Procédure de recherche
;

PROCEDURE FIND
BEGIN
  ;
  ; Lors de l'appel on a passé seulement le premier parametre pour la recherche
  ;
  IF T_CHAMP = 1
  THEN BEGIN
    FOR PDPROJET
    BEGIN
      GET PDPROJET               &
        VIA     CIECLE,          &
                PJTABR           &
        USING   T_CIECLEMNU,     &
                T_PJTABR         &
        ORDERBY CIECLE,          &
                PJTNUMPRO
    END
  END
  IF T_CHAMP = 2
  THEN BEGIN
    ;
    ; Lors de l'appel on a passé seulement les deux premier parametre
    ; pour la recherche
    ;
    FOR PDPROJET
    BEGIN
      GET PDPROJET             &
        VIA     CIECLE,        &
                PJTABR ,       &
                PJTANN         &
        USING   T_CIECLEMNU,   &
                T_PJTABR ,     &
                T_PJTANN       &
        ORDERBY CIECLE,        &
                PJTNUMPRO
    END
  END
  IF T_CHAMP = 3
  THEN BEGIN
    ;
    ; Lors de l'appel on a passé les trois parametres pour la recherche
    ;
    FOR PDPROJET
    BEGIN
      GET PDPROJET             &
        VIA     CIECLE,        &
                PJTABR ,       &
                PJTANN ,       &
                PJTNUMSEQ      &
        USING   T_CIECLEMNU,   &
                T_PJTABR ,     &
                T_PJTANN ,     &
                T_PJTNUMSEQ    &
        ORDERBY CIECLE,        &
                PJTNUMPRO
    END
  END
  IF T_CHAMP = 4
  THEN BEGIN
    ;
    ; Lors de l'appel on a passé les trois parametres pour la recherche
    ;
    FOR PDPROJET
    BEGIN
      GET PDPROJET             &
        VIA     CIECLE,        &
                PJTNUMPRO      &
        USING   T_CIECLEMNU,   &
                T_PJTNUMPRO    &
        ORDERBY CIECLE,        &
                PJTNUMPRO
    END
  END
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_PJTABR    = PJTABR    OF PDPROJET
  LET T_PJTANN    = PJTANN    OF PDPROJET
  LET T_PJTNUMSEQ = PJTNUMSEQ OF PDPROJET
  LET T_PJTNUMPRO = PJTNUMPRO OF PDPROJET
  RETURN

END

BUILD


@IF FORMAT

                      *********************************************************
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      *********************************************************
@ENDIF
