;/T/ 5.3 Commande interne
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-16
;
;    Description..: Permet de visionner et de choisir une commande interne
;
;    Paramètres...: T_PJTABR    ==> Code de chariot
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd130 NOMODE NOACTION FIELDMARK &
             FROM 11,24 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_PJTABR, &
                       T_PJTANN, &
                       T_COMNUMCOMINT, &
                       T_COMNUMCOM, &
                       T_CIECLEMNU



TEMPORARY T_PJTABR       CHARACTER SIZE 02
TEMPORARY T_PJTANN       CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 03
TEMPORARY T_COMNUMCOM    CHARACTER SIZE 07
TEMPORARY T_CIECLEMNU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd130.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Projet
;

FILE PDCOMMAN_INTERNE PRIMARY OCCURS 11

  ACCESS VIA     CIECLE,         &
                 PJTABR          &
         USING   T_CIECLEMNU,    &
                 T_PJTABR        &
         ORDERBY CIECLE,         &
                 PJTABR


;-------------------------------------------------------------------------------
;
;
;

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,57 ; Remplace le DRAW standard

USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

ALIGN (03,03,06) (,08,09) (,11,12) (,,16)

CLUSTER OCCURS WITH PDCOMMAN_INTERNE

FIELD PJTABR           OF PDCOMMAN_INTERNE HIDDEN LABEL ">>" DISPLAY
FIELD PJTANN           OF PDCOMMAN_INTERNE HIDDEN LABEL "-" DISPLAY
FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE HIDDEN LABEL "-" DISPLAY
FIELD COMNOM           OF PDCOMMAN_INTERNE HIDDEN DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_PJTABR , "@")
  THEN LET T_PJTABR = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_PJTABR       = PJTABR       OF PDCOMMAN_INTERNE
  LET T_PJTANN       = PJTANN       OF PDCOMMAN_INTERNE
  LET T_COMNUMCOMINT = COMNUMCOMINT OF PDCOMMAN_INTERNE
  LET T_COMNUMCOM    = PJTABR       OF PDCOMMAN_INTERNE + &
                       PJTANN       OF PDCOMMAN_INTERNE + &
                       COMNUMCOMINT OF PDCOMMAN_INTERNE
  RETURN

END

BUILD


@IF FORMAT

                      *********************************************************
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      * >> xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                      *********************************************************
@ENDIF
