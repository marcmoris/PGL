;/T/ 2.3.1 Enregistrer la commande interne. - Prix / unité travail
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-30
;
;    Description..: Sous-écran pour saisir les prix par unité de travail.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210e ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD210E"


;-------------------------------------------------------------------------------
;
; Panorama d'aide
;

USE pd210e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;

USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour l'appel de dépisteur (pop-up)
;

TEMPORARY T_UTRNUMUNITRV CHARACTER SIZE 04
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE MASTER

;---------------------------------------------------------------------------

FILE PDCOMMAN_INTERNE DESIGNER ALIAS A_PDCOMMAN

;-------------------------------------------------------------------------------
;
; Prix par detail (Commande interne)
;

FILE PDPRIX_DET_C_I PRIMARY &
                    NOITEM &
                    OCCURS 14

  ACCESS VIA     CIECLE,       &
                 PJTABR,       &
                 PJTANN,       &
                 COMNUMCOMINT  &
         USING   T_CIECLEMNU,                      &
                 PJTABR       OF PDCOMMAN_INTERNE, &
                 PJTANN       OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,       &
                 PJTABR,       &
                 PJTANN,       &
                 COMNUMCOMINT, &
                 UTRNUMUNITRV


  ITEM CIECLE       OF PDPRIX_DET_C_I INITIAL T_CIECLEMNU
  ITEM PJTABR       OF PDPRIX_DET_C_I INITIAL PJTABR       OF PDCOMMAN_INTERNE FIXED
  ITEM PJTANN       OF PDPRIX_DET_C_I INITIAL PJTANN       OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOMINT OF PDPRIX_DET_C_I INITIAL COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOM    OF PDPRIX_DET_C_I INITIAL PJTABR       OF PDCOMMAN_INTERNE + &
                                              PJTANN       OF PDCOMMAN_INTERNE + &
                                              COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMSEQ    OF PDPRIX_DET_C_I INITIAL PJTNUMSEQ    OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMPRO    OF PDPRIX_DET_C_I INITIAL PJTABR       OF PDCOMMAN_INTERNE + &
                                              PJTANN       OF PDCOMMAN_INTERNE + &
                                              PJTNUMSEQ    OF PDCOMMAN_INTERNE FIXED


;-------------------------------------------------------------------------------
;
; Unité de travail
;

FILE PDUNITE_TRAVAIL REFERENCE OCCURS WITH PDPRIX_DET_C_I

  ACCESS VIA    CIECLE,          &
                UTRNUMUNITRV     &
         USING  T_CIECLEMNU,     &
                UTRNUMUNITRV     OF PDPRIX_DET_C_I


;-------------------------------------------------------------------------------
;
; Détail unité de travail
;

FILE PDDET_UNIT_TRAV REFERENCE OCCURS WITH PDPRIX_DET_C_I

  ACCESS VIA    CIECLE,          &
                UTRNUMUNITRV,    &
                DUTCODUNMVEN     &
         USING  T_CIECLEMNU,                        &
                UTRNUMUNITRV     OF PDPRIX_DET_C_I, &
                DUTCODUNMVEN     OF PDPRIX_DET_C_I


;-------------------------------------------------------------------------------
;
; Unité mesure (Vente)
;

FILE PDUNITE_MESURE REFERENCE ALIAS A_UNM_VEN OCCURS WITH PDPRIX_DET_C_I

  ACCESS VIA    CIECLE,          &
                UNMCODUNM        &
         USING  T_CIECLEMNU,     &
                DUTCODUNMVEN     OF PDPRIX_DET_C_I


;-------------------------------------------------------------------------------
;
; Unité mesure (Production)
;

FILE PDUNITE_MESURE REFERENCE ALIAS A_UNM_PRD OCCURS WITH PDPRIX_DET_C_I

  ACCESS VIA    CIECLE,          &
                UNMCODUNM        &
         USING  T_CIECLEMNU,     &
                UTRCODUNMPRD     OF PDUNITE_TRAVAIL


;-------------------------------------------------------------------------------
;
; Unité travail - diagramme
;

FILE PDUNI_TRAV_DIA        DESIGNER


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Prix / unité de travail " &
@ELSE
      " %%%Prix / unité de travail " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        FIXED


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        FIXED


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        FIXED


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        FIXED



DRAW 5,2 TO 5,79


SKIP TO LINE 7


TITLE &
@IF FRANCAIS
      " U/T  Description                              U/M V U/M P  Prix " &
@ELSE
      "%%% U/T  Description                              U/M V U/M P  Prix " &
@ENDIF
      AT ,2


SKIP TO LINE 8


ALIGN (02,02,03) (,,08) (50,,50) (,,54) (60,,60)


CLUSTER OCCURS WITH PDPRIX_DET_C_I ID BASE 50


FIELD UTRNUMUNITRV     OF PDPRIX_DET_C_I   &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED                           &
        LOOKUP ON PDUNITE_TRAVAIL          &
          VIA   CIECLE,                    &
                UTRNUMUNITRV               &
          USING T_CIECLEMNU,                       &
                UTRNUMUNITRV     OF PDPRIX_DET_C_I &
          MESSAGE 2925 & ; Cette unité n'existe pas
              , NOTON PDPRIX_DET_C_I       &
          VIA     CIECLE,                  &
                  PJTABR,                  &
                  PJTANN,                  &
                  COMNUMCOMINT,            &
                  UTRNUMUNITRV             &
          USING   T_CIECLEMNU,                      &
                  PJTABR       OF PDCOMMAN_INTERNE, &
                  PJTANN       OF PDCOMMAN_INTERNE, &
                  COMNUMCOMINT OF PDCOMMAN_INTERNE, &
                  UTRNUMUNITRV OF PDPRIX_DET_C_I    &
          MESSAGE  3094  ; Cette unité de travail existe déjà pour cette ligne de détail

FIELD UTRDSC001        OF PDUNITE_TRAVAIL  &
        DISPLAY


FIELD DUTCODUNMVEN     OF PDPRIX_DET_C_I   &
        HIDDEN                             &
        REQUIRED                           &
        LOOKUP ON PDDET_UNIT_TRAV          &
          VIA    CIECLE,                   &
                 UTRNUMUNITRV,             &
                 DUTCODUNMVEN              &
          USING  T_CIECLEMNU,                        &
                 UTRNUMUNITRV     OF PDPRIX_DET_C_I, &
                 DUTCODUNMVEN     OF PDPRIX_DET_C_I  &
          MESSAGE 3095 ; Cette unité de mesure n'existe pas pour cette unité de travail


FIELD UTRCODUNMPRD     OF PDUNITE_TRAVAIL  &
        DISPLAY


FIELD PDCPRI           OF PDPRIX_DET_C_I   &
       HIDDEN                              &
       REQUIRED


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (popup) pour le champs
;

PROCEDURE INPUT UTRNUMUNITRV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UTRNUMUNITRV = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd102 MODE F &
                     PASSING T_UTRNUMUNITRV,  &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UTRNUMUNITRV )
  END
END
;---------------------------------------------------------------------
PROCEDURE EDIT UTRNUMUNITRV
BEGIN
GET A_PDCOMMAN OPT&
          VIA     CIECLE,                  &
                  PJTABR,                  &
                  PJTANN,                  &
                  COMNUMCOMINT,            &
                  COMPRIDET                &
          USING   T_CIECLEMNU,                      &
                  PJTABR       OF PDCOMMAN_INTERNE, &
                  PJTANN       OF PDCOMMAN_INTERNE, &
                  COMNUMCOMINT OF PDCOMMAN_INTERNE, &
                  "0"
IF ACCESSOK
THEN ERROR = "LE PRIX/DETAIL EST A NON"

END
;-------------------------------------------------------------------------------
;
; Appel du dépisteur (popup) pour le champs
;

PROCEDURE INPUT DUTCODUNMVEN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UTRNUMUNITRV = UTRNUMUNITRV OF PDPRIX_DET_C_I
    LET T_UNMCODUNM    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd102a MODE F &
                      PASSING T_UTRNUMUNITRV, &
                              T_UNMCODUNM,    &
                              T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END

END


;-------------------------------------------------------------------------------
;
; Validation pour le champ
;

PROCEDURE EDIT DUTCODUNMVEN
BEGIN
 ;
 ; Valide que l'unité de vente n'est pas utilisée par un diagramme avant
 ; de permettre de le modifier.
 ;
 IF FIELDTEXT <> OLDVALUE(DUTCODUNMVEN OF PDPRIX_DET_C_I) &
    AND &
    0 <> SIZE(TRUNCATE(OLDVALUE(DUTCODUNMVEN OF PDPRIX_DET_C_I)))
 THEN BEGIN
   GET PDUNI_TRAV_DIA OPTIONAL &
     VIA   CIECLE,          &
           COMNUMCOM,       &
           UTRNUMUNITRV     &
     USING T_CIECLEMNU,                         &
           COMNUMCOM        OF PDPRIX_DET_C_I , &
           UTRNUMUNITRV     OF PDPRIX_DET_C_I
   IF ACCESSOK
   THEN BEGIN
     ERROR &
"On ne peut changer l'unité de vente d'un unité de travail référé par un diagramme"
   END
 END

IF COMIMPMET OF PDCOMMAN_INTERNE = "IMP"
THEN BEGIN
 IF DUTCODUNMVEN OF PDPRIX_DET_C_I = "ML" OR &
    DUTCODUNMVEN OF PDPRIX_DET_C_I = "M2" OR &
    DUTCODUNMVEN OF PDPRIX_DET_C_I = "M3"
 THEN ERROR = "UNITE IMPERIAL SEULEMENT"
 END
IF COMIMPMET OF PDCOMMAN_INTERNE = "MET"
THEN BEGIN
 IF DUTCODUNMVEN OF PDPRIX_DET_C_I = "PL" OR &
    DUTCODUNMVEN OF PDPRIX_DET_C_I = "P2" OR &
    DUTCODUNMVEN OF PDPRIX_DET_C_I = "P3"
 THEN ERROR = "UNITE METRIQUE SEULEMENT"
 END

END
;--------------------------------------------------------------------------


;-------------------------------------------------------------------------------
;
; Saisie des informations du cluster
;

PROCEDURE DESIGNER 50
BEGIN
  ACCEPT  UTRNUMUNITRV     OF PDPRIX_DET_C_I
  DISPLAY UTRDSC001        OF PDUNITE_TRAVAIL
  ACCEPT  DUTCODUNMVEN     OF PDPRIX_DET_C_I
  DISPLAY UTRCODUNMPRD     OF PDUNITE_TRAVAIL
  ACCEPT  PDCPRI           OF PDPRIX_DET_C_I
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDPRIX_DET_C_I &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDPRIX_DET_C_I &
     AND NOT NEWRECORD     OF PDPRIX_DET_C_I &
     AND NOT DELETEDRECORD OF PDPRIX_DET_C_I &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*************************** Prix / unité de travail ****************************
* Num. commande.: xx-xx-xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
*                                                                              *
* U/T  Description                              U/M V U/M P  Prix              *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
* xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx  xx    xxxxxxxxxxxxxxx     *
*                                                                              *
********************************************************************************
@ENDIF
