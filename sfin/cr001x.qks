;/T/ Clients - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 9 août 1993
;
;    Description..: Cet écran vérifie si le client reçu en paramètre est
;                   référé dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;                   Il faut vérifie si le record du client dans la table
;                   MCREFERENCE est référé dans l'historique du Grand Livre
;                   si oui, on ne doit pas détruire le record dans MCREFERENCE
;                   sinon, on le détruit MCREFERENCE.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr001x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING CRCLIENT , &
                        T_FLGDEL , &
                        T_FLGREF


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.
TEMPORARY T_FLGREF CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Clients
;
FILE CRCLIENT         MASTER

;
; Clients - compagnie.
;
FILE CRCLI_CIE        REFERENCE

;
; Historique au Grand Livre.
;
FILE MCHISTO          REFERENCE



;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans client par compagnie.
  ;
  GET CRCLI_CIE VIA CLICIECLE, &
                    CLICLE     &
                USING CLICIECLE OF CRCLIENT , &
                      CLICLE    OF CRCLIENT   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 722 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.

  ;
  ; On vérifie si le client est référé dans l'historique du Grand Livre.
  ;
  GET MCHISTO VIA REFCIECLE , &
                  REFCLETYP , &
                  REFCLENUM   &
              USING CLICIECLE OF CRCLIENT , &
                    CLIREFTYP OF CRCLIENT , &
                    CLICLE    OF CRCLIENT   &
              OPTIONAL
  IF NOT ACCESSOK
  THEN LET T_FLGREF = "1"
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
