;/T/ Autorisation des Lots
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 9 avril 1993
;
;    Description..: Cet écran permet d'autoriser les lots pour les
;                   journaliser
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gl002 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND, CHANGE &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_CIENOM    CHARACTER SIZE 40


USE ussectmp NOLIST
    "GL002"

USE gl002.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
;
FILE GLLOT            PRIMARY &
                      OCCURS 17
  ACCESS REQUEST PECCLE OF GLLOT, &
                 LGLCLE OF GLLOT  &
         VIA     LGLCIECLE, &
                 PECCLE   , &
                 LGLCLE     &
         USING   T_CIECLEMNU, &
                 PECCLE OF GLLOT, &
                 LGLCLE OF GLLOT  &
         ORDERBY PECCLE, &
                 LGLCLE
  ACCESS REQUEST PECCLE OF GLLOT &
         VIA     LGLCIECLE, &
                 PECCLE     &
         USING   T_CIECLEMNU    , &
                 PECCLE OF GLLOT  &
         ORDERBY PECCLE, &
                 LGLCLE

  ACCESS VIA     LGLCIECLE &
         USING   T_CIECLEMNU &
         ORDERBY PECCLE, &
                 LGLCLE

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Autorisation des lots " &
@ELSE
      " Batch Consultation " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

TITLE &
@IF FRANCAIS
      "                                   Date           Date" &
@ELSE
      "                               Verification     Posting" &
@ENDIF
      AT ,3
SKIP

TITLE &
@IF FRANCAIS
     "Période Type  Lot Utilisateur  Vérification  Journalisation      $ Total"&
@ELSE
     "Period Type Batch User             Date            Date          Total $"&
@ENDIF
      AT ,3

SKIP

ALIGN (4,3,4) (,,12) (,,16) (,,21) (,,36) (,,50) (,,62)

CLUSTER OCCURS WITH GLLOT ID BASE 05

FIELD PECCLE OF GLLOT HIDDEN LABEL " " &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

FIELD LGLTYPECR  OF GLLOT HIDDEN &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

FIELD LGLCLE     OF GLLOT &
        DISPLAY &
        NUMERIC &
        SIGNIFICANCE 4 &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

FIELD LGLUSR     OF GLLOT &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

FIELD LGLDATVAL  OF GLLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

FIELD LGLDATJOU  OF GLLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

FIELD LGLCUMMNT  OF GLLOT &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LGLNBRTRSERR OF GLLOT <> 0 &
                  ELSE INVERSE          IF LGLATRJOU OF GLLOT = "1" AND &
                                           LGLDATJOU OF GLLOT = 0

USE ushilite NOLIST

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Si l'usager fait un <return> sur un ligne à l'écran il active la procédure
; suivante.
;
; Si le lot n'est pas autorisé, on met le flag d'autorisation à oui et on
; conserve l'usager de l'autorisation.
;
; Si le lot est déjà autorisé, on enlève le flag d'autorisation et l'usager
; qui l'avait autorisé.
;
;
PROCEDURE DESIGNER 05
BEGIN
  IF LGLNBRTRSERR = 0
  THEN BEGIN
    IF LGLDATJOU OF GLLOT = 0
    THEN BEGIN
      IF LGLATRJOU OF GLLOT = "0"
      THEN BEGIN
        LET LGLATRJOU OF GLLOT = "1"
        LET LGLUSRATR OF GLLOT = LOGONID
      END
      ELSE BEGIN
        LET LGLATRJOU OF GLLOT = "0"
        LET LGLUSRATR OF GLLOT = " "
      END
      DISPLAY PECCLE    OF GLLOT
      DISPLAY LGLTYPECR OF GLLOT
      DISPLAY LGLCLE    OF GLLOT
      DISPLAY LGLUSR    OF GLLOT
      DISPLAY LGLDATVAL OF GLLOT
      DISPLAY LGLDATJOU OF GLLOT
      DISPLAY LGLCUMMNT OF GLLOT
    END
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; On ne peut pas détruire dans cette écran.
  ;
  FOR GLLOT
  BEGIN
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GLLOT &
       AND NOT NEWRECORD     OF GLLOT &
       AND NOT DELETEDRECORD OF GLLOT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD ;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;**************************** Autorisation des lots *****************************
;*                                    Date           Date                       *
;* Période Type  Lot Utilisateur  Vérification  Journalisation      $ Total     *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;*  xxxxx   xx  xxxx xxxxxxxxxxxx   xxxxxxxx       xxxxxxxx   xxxxxxxxxxxxxx    *
;********************************************************************************
