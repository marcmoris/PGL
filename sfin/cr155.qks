;/T/ Représentants des Clients
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 10 Nov 1993
;
;    Description..: Définition des Représentant de client dans les deux langues.
;                   Les Représentant sont accessibles par toutes les compagnies

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN cr155 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEHLD, &
                       T_CIENOMHLD

USE ussectmp  NOLIST
    "CR155"

USE cr155.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEHLD CHARACTER SIZE 4  ; Numéro de cie du holding
TEMPORARY T_CIENOMHLD CHARACTER SIZE 40 ; Nom du holding
TEMPORARY T_CLCREP    CHARACTER SIZE 16 INITIAL "CLCREP" RESET AT STARTUP


;-------------------------------------------------------------------------------
;
;
;
FILE MCCODE_BILINGUE  PRIMARY &
                      OCCURS 17
  ACCESS REQUEST CBICLE OF MCCODE_BILINGUE &
         VIA     XELNOM, &
                 CBICLE &
         USING   T_CLCREP, &
                 CBICLE OF MCCODE_BILINGUE &
         ORDERBY CBICLE

  ACCESS VIA     XELNOM   &
         USING   T_CLCREP &
         ORDERBY CBICLE

  ITEM XELNOM    OF MCCODE_BILINGUE INITIAL T_CLCREP


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction


DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEHLD
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOMHLD )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Représentants des clients " &
@ENDIF
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

TITLE &
@IF FRANCAIS
      "Code  Description française          Description anglaise" &
@ELSE
      "Code  French description             English description" &
@ENDIF
      AT ,8

SKIP

ALIGN(8,6,8)(,,14)(,,45)

CLUSTER OCCUR WITH MCCODE_BILINGUE ID BASE 10

FIELD CBICLE OF MCCODE_BILINGUE HIDDEN LABEL " " &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON MCCODE_BILINGUE &
          VIA XELNOM, &
              CBICLE  &
          USING XELNOM OF MCCODE_BILINGUE, &
                CBICLE OF MCCODE_BILINGUE &
        MESSAGE 601
FIELD CBIDSCFRA OF MCCODE_BILINGUE &
        REQUIRED

FIELD CBIDSCANG OF MCCODE_BILINGUE &
        DEFAULT CBIDSCFRA OF MCCODE_BILINGUE

CLUSTER



;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR MCCODE_BILINGUE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF MCCODE_BILINGUE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF MCCODE_BILINGUE &
       AND NOT NEWRECORD     OF MCCODE_BILINGUE &
       AND NOT DELETEDRECORD OF MCCODE_BILINGUE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'une catégorie.
    ;
    IF DELETEDRECORD OF MCCODE_BILINGUE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la catégorie est référée.
      ;
      RUN SCREEN cr155x MODE SAME &
                        PASSING MCCODE_BILINGUE, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Catégories de client *****************************
*                                                                              *
*      Code  Description française          Description anglaise               *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
*      xxxx  xxxxxxxxxxxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxx          *
********************************************************************************
@ENDIF
