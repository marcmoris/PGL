;/T/ Liste des immobilisations par compte comptable
;
;/P/ Programmeur..: Alain Côté
;    Date création: 25 février 1994
;
;    Description...: Ce rapport imprime les informations sur les valeurs se
;                    rapportant aux immobilisation. Un total par compte
;                    comptable, par catégorie et un grand total devrait
;                    permettre de balancer avec le GL. Les comptes comptables
;                    sur lesquels est basé le total est déterminé par l'usager.
;
;
;    Paramétres....: No.cie      (%CUF_CIECLEMNU)
;                    Catégorie   (%D IMCCATCLE)
;                    Type de tri (%DUTYPTRI)
;                    Nom cie     (%DUF_CIENOM)
;
;    Tris..........: Par ordre de catégorie, par type de tri (compte
;                    comptable et unité adm.) et par numéro d'immo.
;
;    Particularités: Les immobilisations doivent être actives et ne pas être
;                    aliénées pour être sélectionnées.
;
;/V3.02.00/ Alain Côté, 25 février 1994, Immobilisation
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS IMIMMOBILISATION &

  LINK CIECLE OF IMIMMOBILISATION , &
       IMCCLE OF IMIMMOBILISATION   &
    TO CIECLE , &
       IMCCLE OF IMCATEGORIE &

  LINK IMMCPTACT OF IMIMMOBILISATION &
    TO CPTCLE OF VCPT_DSC &

  LINK IMMCPTAMTACC OF IMIMMOBILISATION &
    TO CPTCLE OF VCPT_DSC ALIAS A_CPT_AMTACC &

  LINK IMMCPTDEPAMT OF IMIMMOBILISATION &
    TO CPTCLE OF VCPT_DSC ALIAS A_CPT_DEPAMT &

  LINK CIECLE OF IMIMMOBILISATION , &
       UNACLE OF IMIMMOBILISATION   &
    TO CIECLE , &
       UNACLE OF MCUNITE_ADM OPTIONAL &

  LINK CIECLE    OF IMIMMOBILISATION , &
       IMMUNAACT OF IMIMMOBILISATION   &
    TO CIECLE , &
       UNACLE OF MCUNITE_ADM ALIAS A_UNA_ACT OPTIONAL &

  LINK CIECLE       OF IMIMMOBILISATION , &
       IMMUNAAMTACC OF IMIMMOBILISATION   &
    TO CIECLE , &
       UNACLE OF MCUNITE_ADM ALIAS A_UNA_AMTACC OPTIONAL &

  LINK CIECLE       OF IMIMMOBILISATION , &
       IMMUNADEPAMT OF IMIMMOBILISATION   &
    TO CIECLE , &
       UNACLE OF MCUNITE_ADM ALIAS A_UNA_DEPAMT OPTIONAL


CHOOSE CIECLE PARM , &
       IMCCLE PARM

SELECT IMIMMOBILISATION IF     IMMSTU OF IMIMMOBILISATION = "A" &
                           AND IMMPECALI OF IMIMMOBILISATION = ""

;
; Choix du compte comptable désiré:
;  1 - Compte d'actif;
;  2 - Compte d'amortissement accumulé;
;  3 - Compte de dépense d'amortissement.
;
DEFINE D_TYPTRI  CHARACTER SIZE 1 = PARM

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des immobilisations par compte comptable"
@ELSE
       "%%%Liste des immobilisations par compte comptable"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM302"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

DEFINE D_IMMDSC    CHARACTER SIZE 30 = IMMDSC OF IMIMMOBILISATION[1:30]


;
; On prend le bon compte pour le tri du rapport, le choix du compte est fait
; par l'usager, voir la variable D_TYPTRI ci-haut.
;
DEFINE D_TRICPT   CHARACTER SIZE 6 = IMMCPTACT    OF IMIMMOBILISATION &
                                      IF D_TYPTRI = "1" &
                                ELSE IMMCPTAMTACC OF IMIMMOBILISATION &
                                      IF D_TYPTRI = "2" &
                                ELSE IMMCPTDEPAMT OF IMIMMOBILISATION
;
; Description du compte précédemment choisi.
;
DEFINE D_DSCCPT   CHARACTER SIZE 20 = CPTDSCABR OF VCPT_DSC &
                                       IF D_TYPTRI = "1" &
                                 ELSE CPTDSCABR OF A_CPT_AMTACC &
                                       IF D_TYPTRI = "2" &
                                 ELSE CPTDSCABR OF A_CPT_DEPAMT

;
; Unité administrative du compte, on prend l'unité administrative de
; l'immobilisation si l'unité administrative du compte correspondant est
; absente.
;
DEFINE D_TRIUNA    CHARACTER SIZE 8 = IMMUNAACT    OF IMIMMOBILISATION &
                                 IF     D_TYPTRI = "1" &
                                    AND "" <> IMMUNAACT OF IMIMMOBILISATION &
                                 ELSE IMMUNAAMTACC OF IMIMMOBILISATION &
                                 IF     D_TYPTRI = "2" &
                                    AND "" <> IMMUNAAMTACC OF IMIMMOBILISATION &
                                 ELSE IMMUNADEPAMT OF IMIMMOBILISATION &
                                 IF     D_TYPTRI = "3" &
                                    AND "" <> IMMUNADEPAMT OF IMIMMOBILISATION &
                                 ELSE UNACLE OF IMIMMOBILISATION
;
; Description de l'unité administrative précédemmemt choisie.
;
DEFINE D_DSCUNA    CHARACTER SIZE 20 = UNANOMABR OF A_UNA_ACT &
                                 IF     D_TYPTRI = "1" &
                                    AND "" <> IMMUNAACT OF IMIMMOBILISATION &
                                  ELSE UNANOMABR OF A_UNA_AMTACC &
                                 IF     D_TYPTRI = "2" &
                                    AND "" <> IMMUNAAMTACC OF IMIMMOBILISATION &
                                  ELSE UNANOMABR OF A_UNA_DEPAMT &
                                 IF     D_TYPTRI = "3" &
                                    AND "" <> IMMUNADEPAMT OF IMIMMOBILISATION &
                                  ELSE UNANOMABR OF MCUNITE_ADM

;
; Montant à amortir.
;
DEFINE D_MNT_A_AMT FLOAT SIZE 8 =   IMMMNTINI OF IMIMMOBILISATION &
                                  + IMMMNTAJT OF IMIMMOBILISATION &
                                  - IMMMNTREC OF IMIMMOBILISATION
;
; Amortissement accumulé + amortissement annuel.
;
DEFINE D_AMTACCTOT FLOAT SIZE 8 = IMMAMTACC    OF IMIMMOBILISATION &
                                  + IMMMNTAMTANN OF IMIMMOBILISATION

;
; Montant non amorti.
;
DEFINE D_MNTNONAMT FLOAT SIZE 8 = D_MNT_A_AMT &
                                  - D_AMTACCTOT



SORT ON CIECLE OF IMIMMOBILISATION &
     ON D_TRICPT &
     ON D_TRIUNA &
     ON IMMCLE OF IMIMMOBILISATION


USE ustitstd NOLIST
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
          "Compte.:" &
@ELSE
          "%%%pte.:" &
@ENDIF
  TAB 010 D_TRICPT &
  TAB 019 D_DSCCPT &
SKIP &
  TAB 001 &
@IF FRANCAIS
          "Unité..:" &
@ELSE
          "%%%té..:" &
@ENDIF
  TAB 010 D_TRIUNA &
  TAB 019 D_DSCUNA &
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
"    Numéro                                               Type        Valeur          Valeur       Amortissement      Valeur " &
@ELSE
"%%% Numéro                                               Type        Valeur          Valeur       Amortissement      Valeur " &
@ENDIF
SKIP &
  TAB 001 &
@IF FRANCAIS
"immobilisation Descritpion                               amort.    acquisition      à amortir       accumulé       non amortie " &
@ELSE
"%%%obilisation Descritpion                               amort.    acquisition      à amortir       accumulé       non amortie " &
@ENDIF
SKIP &
  TAB 001 &
"-------------- ----------------------------------------  ------   -------------   -------------   -------------   ------------- " &
SKIP 2


REPORT &
  TAB 002 IMMCLE    OF IMIMMOBILISATION &
  TAB 016 IMMDSC    OF IMIMMOBILISATION &
  TAB 060 IMMTYPAMO OF IMIMMOBILISATION &
  TAB 067 IMMMNTINI OF IMIMMOBILISATION &
  TAB 083 D_MNT_A_AMT &
            PICTURE "^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
  TAB 099 D_AMTACCTOT &
            PICTURE "^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
  TAB 115 D_MNTNONAMT &
            PICTURE "^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
SKIP &
  RESERVE 3 LINES


FOOTING AT D_TRIUNA &
  TAB 066 "--------------" &
  TAB 082 "--------------" &
  TAB 098 "--------------" &
  TAB 114 "--------------" &
SKIP &
  TAB 066 IMMMNTINI OF IMIMMOBILISATION &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
  TAB 082 D_MNT_A_AMT &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
  TAB 098 D_AMTACCTOT &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
  TAB 114 D_MNTNONAMT &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
SKIP PAGE

BUILD im302z
@IF FORMAT
IM302                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx              Liste des immobilisations par compte comptable
                                    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Compte.: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Unité..: xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

    Numéro                                               Type        Valeur          Valeur       Amortissement      Valeur
immobilisation Descritpion                               amort.    acquisition      à amortir       accumulé       Non amortie
-------------- ----------------------------------------  ------   -------------   -------------   -------------   -------------
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x      99,999,999.99   99,999,999.99   99,999,999.99   99,999,999.99
                                                                 --------------  --------------  --------------  --------------
                                                                 999,999,999.99  999,999,999.99  999,999,999.99  999,999,999.99
@ENDIF
