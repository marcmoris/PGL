
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN pr012a      NOMODE NOACTION FIELDMARK STARTUP &
                   FROM 2,1 TO 13,80 &
                   MESSAGE ON 24 &
                   AUTOUPDATE &
                   HELP POPUP FROM 4,9 TO 20,72 &
                   ACTIVITIES FIND, CHANGE &
                   RECEIVING T_CIECLEFND, &
                             T_UNICATFND, &
                             T_UNICLEFND, &
                             T_FORCLEFND

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche
TEMPORARY T_UNICATFND CHARACTER SIZE 02 ; Projet de recherche
TEMPORARY T_UNICLEFND CHARACTER SIZE 08 ; Sous-projet de recherche
TEMPORARY T_FORCLEFND CHARACTER SIZE 05 ; Format de présentation

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPUNITE    PRIMARY &
                NOITEM  &
                NODELETE

  ACCESS VIA    CIECLE, &
                UNICAT, &
                UNICLE  &
         USING  T_CIECLEFND, &
                UNICAT OF GPUNITE, &
                UNICLE OF GPUNITE  &
        REQUEST UNICAT OF GPUNITE, &
                UNICLE OF GPUNITE

FILE GPFORMAT   REFERENCE

  ACCESS VIA    FORCLE &
         USING  UNIFRM1 OF GPUNITE

DEFINE    D_UNICAT CHARACTER SIZE 16 = "UNICAT"

TEMPORARY T_UNICATPOP CHARACTER SIZE 4
TEMPORARY T_UNICAT2   CHARACTER SIZE 2
TEMPORARY T_UNICLEPOP CHARACTER SIZE 8
TEMPORARY T_UNISTUPAT CHARACTER SIZE 5 ; Popup sur les équipements.

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW  1,1 TO 12,80

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1

ALIGN (3,3,19) (,,22)

FIELD UNICAT OF GPUNITE &
        HIDDEN ID 10 &
        REQUIRED &
        NOCHANGE
FIELD UNICLE OF GPUNITE &
        NOCHANGE

SKIP 1

ALIGN (,,6)

FIELD UNIDSC1 OF GPUNITE &
        DISPLAY
FIELD UNIDSC2 OF GPUNITE &
        DISPLAY

SKIP 3

ALIGN (3,3,19)

FIELD UNIFRM1 OF GPUNITE


;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNICAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNICATPOP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNICAT, T_UNICATPOP
    LET FIELDTEXT = TRUNCATE(T_UNICATPOP)
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT UNICLE
BEGIN
  ;
  ; Popup sur les activités.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNICAT2    = UNICAT OF GPUNITE
    LET T_UNISTUPAT  = "A"
    LET T_UNICLEPOP  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr103 MODE F PASSING T_CIECLEFND, T_UNICAT2, T_UNICLEPOP, T_UNISTUPAT &
                          WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNICLEPOP)
  END
END

PROCEDURE POSTFIND
BEGIN
  DISPLAY UNIDSC1 OF GPUNITE
  DISPLAY UNIDSC2 OF GPUNITE
  ACCEPT UNIFRM1 OF GPUNITE
  LET T_UNICATFND = UNICAT  OF GPUNITE
  LET T_UNICLEFND = UNICLE  OF GPUNITE
  LET T_FORCLEFND = UNIFRM1 OF GPUNITE
  PUSH UPDATE
END

PROCEDURE UPDATE
BEGIN
  PUT GPUNITE
  RETURN
END

BUILD
@IF FORMAT
********************************** Paramètres **********************************
*                                                                              *
* Equipement....: xx xxxxxxxx                                                  *
*                                                                              *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
*                                                                              *
*                                                                              *
* Format........: xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     *
*                                                                              *
********************************************************************************
@ENDIF

