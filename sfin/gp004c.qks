;
;/M/ François Déry, 7 mai 1999
;       Modifier le run command pour le exécutable en UNIX.
;
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN gp004c      NOMODE ACTION LABEL " " &
                   FROM 2,1 TO 13,80 &
                   RECEIVING GPFRM_AFFICHE

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPFRM_AFFICHE          MASTER

FILE GPFRD_DOMAINE_OP       DESIGNER

FILE GPPRO_BDG_UNT          DESIGNER

FILE GPPRA_BDG_UNT          DESIGNER

FILE GPFRM_DETAILS          DESIGNER

FILE GPPROJETS              DESIGNER

FILE GPPRO_ACTIVITE         DESIGNER
;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

PROCEDURE INTERNAL LIRE_BUDGET
BEGIN
   IF 0 = SIZE(TRUNCATE(PRACLE OF GPFRM_AFFICHE))
   THEN BEGIN
        WHILE RETRIEVING GPPRO_BDG_UNT &
              VIA   CIECLE,            &
                    PRJCLE,            &
                    UNTCLE             &
              USING CIECLE OF GPFRM_AFFICHE, &
                    PRJCLE OF GPFRM_AFFICHE, &
                    UNTCLE OF GPFRD_DOMAINE_OP
        BEGIN
                 GET GPPROJETS &
                     VIA   CIECLE, &
                           PRJCLE  &
                     USING CIECLE OF GPPRO_BDG_UNT, &
                           PRJCLE OF GPPRO_BDG_UNT

                 LET CIECLE    OF GPFRM_DETAILS = CIECLE OF GPPRO_BDG_UNT
                 LET PRJCLE    OF GPFRM_DETAILS = PRJCLE OF GPPRO_BDG_UNT
                 LET PRACLE    OF GPFRM_DETAILS = " "
                 LET IMMCLE    OF GPFRM_DETAILS = " "
                 LET CPTCLE    OF GPFRM_DETAILS = " "
                 LET UNACLE    OF GPFRM_DETAILS = " "
                 LET UNTCLE    OF GPFRM_DETAILS = UNTCLE OF GPPRO_BDG_UNT

                 IF FDUOPE OF GPFRD_DOMAINE_OP = "+"
                 THEN BEGIN
                      LET FDEREE    OF GPFRM_DETAILS = &
                          PBUREE    OF GPPRO_BDG_UNT

                      IF PRJPBUDATAPP OF GPPROJETS = 0
                      THEN LET FDEBDG       OF GPFRM_DETAILS = &
                               PBUBDG       OF GPPRO_BDG_UNT

                      ELSE LET FDEBDG       OF GPFRM_DETAILS = &
                               PBUBDGREV    OF GPPRO_BDG_UNT
                 END
                 ELSE BEGIN
                      LET FDEREE    OF GPFRM_DETAILS = &
                          PBUREE    OF GPPRO_BDG_UNT * -1

                      IF PRJPBUDATAPP OF GPPROJETS = 0
                      THEN LET FDEBDG       OF GPFRM_DETAILS = &
                               PBUBDG       OF GPPRO_BDG_UNT * -1

                      ELSE LET FDEBDG       OF GPFRM_DETAILS = &
                               PBUBDGREV    OF GPPRO_BDG_UNT * -1
                 END
                 PUT GPFRM_DETAILS RESET
        END
   END
   ELSE BEGIN
        WHILE RETRIEVING GPPRA_BDG_UNT &
              VIA   CIECLE,            &
                    PRJCLE,            &
                    PRACLE,            &
                    UNTCLE             &
              USING CIECLE OF GPFRM_AFFICHE, &
                    PRJCLE OF GPFRM_AFFICHE, &
                    PRACLE OF GPFRM_AFFICHE, &
                    UNTCLE OF GPFRD_DOMAINE_OP
        BEGIN
                 GET GPPRO_ACTIVITE &
                     VIA   CIECLE, &
                           PRJCLE, &
                           PRACLE  &
                     USING CIECLE OF GPPRA_BDG_UNT, &
                           PRJCLE OF GPPRA_BDG_UNT, &
                           PRACLE OF GPPRA_BDG_UNT

                 LET CIECLE    OF GPFRM_DETAILS = CIECLE OF GPPRA_BDG_UNT
                 LET PRJCLE    OF GPFRM_DETAILS = PRJCLE OF GPPRA_BDG_UNT
                 LET PRACLE    OF GPFRM_DETAILS = PRACLE OF GPPRA_BDG_UNT
                 LET IMMCLE    OF GPFRM_DETAILS = " "
                 LET CPTCLE    OF GPFRM_DETAILS = " "
                 LET UNACLE    OF GPFRM_DETAILS = " "
                 LET UNTCLE    OF GPFRM_DETAILS = UNTCLE OF GPPRA_BDG_UNT

                 IF FDUOPE OF GPFRD_DOMAINE_OP = "+"
                 THEN BEGIN
                      LET FDEREE    OF GPFRM_DETAILS = &
                          PAUREE    OF GPPRA_BDG_UNT

                      IF PRAPAUDATAPP OF GPPRO_ACTIVITE = 0
                      THEN LET FDEBDG       OF GPFRM_DETAILS = &
                               PAUBDG       OF GPPRA_BDG_UNT

                      ELSE LET FDEBDG       OF GPFRM_DETAILS = &
                               PAUBDGREV    OF GPPRA_BDG_UNT
                 END
                 ELSE BEGIN
                      LET FDEREE    OF GPFRM_DETAILS = &
                          PAUREE    OF GPPRA_BDG_UNT * -1

                      IF PRAPAUDATAPP OF GPPRO_ACTIVITE = 0
                      THEN LET FDEBDG       OF GPFRM_DETAILS = &
                               PAUBDG       OF GPPRA_BDG_UNT * -1

                      ELSE LET FDEBDG       OF GPFRM_DETAILS = &
                               PAUBDGREV    OF GPPRA_BDG_UNT * -1
                 END
                 PUT GPFRM_DETAILS RESET
        END
   END
END


PROCEDURE INITIALIZE
BEGIN
   CLOSE GPFRM_DETAILS

   RUN COMMAND "echo 'create file gpfrm_details' | qutil"

   WHILE RETRIEVING GPFRD_DOMAINE_OP &
         VIA   FRMCLE,            &
               FRDCLE             &
         USING FRMCLE OF GPFRM_AFFICHE, &
               FRDCLE OF GPFRM_AFFICHE

   BEGIN
      DO LIRE_BUDGET
   END

   RETURN
END

BUILD
