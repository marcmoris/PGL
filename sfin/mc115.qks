;/T/ Cons. Unité adm. inclusive        CIECLE, CPTCLE, UNACLE, PECCLE
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 02-Mar-1992
;
;    Description..: Permet de visionner et de choisir une unité adm.
;
;    Paramètres...: CIECLE    ==> - numéro de compagnie
;                                 - segment de la clé de recherche
;
;                   CPTCLE    ==> - numéro de compte comptable
;                                 - segment de la clé de recherche
;
;                   UNACLE    ==> - numéro d'unité administrative
;                                 - segment de la clé de recherche, peut
;                                   contenir un @
;                                 - contient la valeur de retour
;
;                   PECCLE    ==> - numéro de période
;                                 - critère de sélection.
;                                 - peut être à blanc.
;
;/M/ Stephane Jackson, 98-08-20
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc115 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_CIEUNA, &
                       T_CPTCLE, &
                       T_UNACLE, &
                       T_PECCLE

TEMPORARY T_CIEUNA CHARACTER SIZE 4
TEMPORARY T_CPTCLE CHARACTER SIZE 6
TEMPORARY T_UNACLE CHARACTER SIZE 8
TEMPORARY T_PECCLE CHARACTER SIZE 4

USE mc115.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE MCCHARTE_UNA     PRIMARY &
                      OCCURS 11
  ACCESS VIA CIECLE, &
             CPTCLE, &
             UNACLE  &
         USING T_CIEUNA, &
               T_CPTCLE, &
               T_UNACLE  &
         ORDERBY UNACLE

  ;
  ; Si la charte est inclusive, alors la période doit faire partie de
  ; l'intervalle de période défini dans la charte.
  ;
;  SELECT IF &
;          T_PECCLE >= CCUPECDEBA OF MCCHARTE_UNA &
;         AND (    T_PECCLE < CCUPECDEBI OF MCCHARTE_UNA &
;               OR CCUPECDEBI OF MCCHARTE_UNA = " " )  

; Ajustement pour le changement de siècle
  SELECT IF   &
 ( ( &
     CCUPECDEBA OF MCCHARTE_UNA[1:1] <> "0" &
     OR &
     CCUPECDEBI OF MCCHARTE_UNA[1:1] <> "9" &
   ) &
    AND &
    ( &
      T_PECCLE[1:1] <> "9" &
      OR &
      CCUPECDEBA OF MCCHARTE_UNA[1:1] <> "0" &
      OR &
      CCUPECDEBI OF MCCHARTE_UNA[1:1] <> " " &
    ) &
   AND &
   ( &
      ( &
        T_PECCLE >= CCUPECDEBA OF MCCHARTE_UNA &
        AND ( T_PECCLE <  CCUPECDEBI OF MCCHARTE_UNA &
            OR CCUPECDEBI OF MCCHARTE_UNA = " " &
            )  &
      ) &
      OR &
      ( &
        T_PECCLE >= CCUPECDEBA OF MCCHARTE_UNA &
        AND &
        T_PECCLE[1:1] = "9" &
        AND &
        CCUPECDEBI OF MCCHARTE_UNA[1:1] = "0" &
        AND &
        CCUPECDEBA OF MCCHARTE_UNA[1:1] = "9" &
      ) &
      OR &
      ( &
        T_PECCLE < CCUPECDEBI OF MCCHARTE_UNA &
        AND &
        T_PECCLE[1:1] = "0" &
        AND &
        CCUPECDEBA OF MCCHARTE_UNA[1:1] = "9" &
        AND &
        CCUPECDEBI OF MCCHARTE_UNA[1:1] = "0" &
      ) &
      OR &
      ( &
        T_PECCLE[1:1] = "0" &
        AND &
        CCUPECDEBA OF MCCHARTE_UNA[1:1] = "9" &
        AND &
        0 = SIZE(TRUNC(CCUPECDEBI OF MCCHARTE_UNA)) &
      ) &
   ) )  &
   OR T_PECCLE = ' '

;
; Pour afficher le nom de l'unité administrative.
;
FILE MCUNITE_ADM      REFERENCE &
                      OCCURS WITH MCCHARTE_UNA
  ACCESS VIA CIECLE, &
             UNACLE  &
         USING CIECLE OF MCCHARTE_UNA, &
               UNACLE OF MCCHARTE_UNA

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN(3,3,6)(,,16)(,,37)

CLUSTER OCCURS WITH MCCHARTE_UNA

FIELD UNACLE    OF MCCHARTE_UNA HIDDEN LABEL ">>"
FIELD UNANOMABR OF MCUNITE_ADM  DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_UNACLE, "@")
  THEN LET T_UNACLE = ""
END

;-------------------------------------------------------------------------------
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_UNACLE = UNACLE OF MCCHARTE_UNA
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       * >> xxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
;                                       *****************************************
