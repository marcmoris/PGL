;/T/ Liste des clients
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 93-06-11
;
;    Description...: Liste des clients
;
;    Paramètres....: Client    (1, plusieurs, tous)
;                    Statut    (1, plusieurs, tous)
;                    Catégorie (1, plusieurs, tous)
;                    Ordre Alpha-Num
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....

ACCESS VCLC_CLI &

  LINK ( CLICIECLE OF VCLC_CLI, &
         CLICLE    OF VCLC_CLI, &
         1 )                    &
    TO   CLICIECLE, &
         CLICLE   , &
         CCTCLE     OF CRCLI_CONTACT &

  LINK ( CLICIECLE OF VCLC_CLI, &
         CLIREFTYP OF VCLC_CLI, &
         CLICLE    OF VCLC_CLI, &
         1 )                    &
    TO   REFCIECLE, &
         REFCLETYP, &
         REFCLENUM, &
         RADCLE     OF MCREF_ADRESSE &

  LINK   CIECLE &
    TO   CIECLE OF MCCOMPAGNIE

CHOOSE CLICIECLE PARM, &              ; Compagnie client
       CIECLE    PARM, &              ; Compagnie menu
       CLICLE    PARM, &              ; Client
       CLICAT    PARM, &              ; Catégorie
       CLISTU    PARM                 ; Statut

;
; Ordre du tri : 1 - Nom client
;                2 - Numéro client
;
DEFINE D_TYP_TRI CHAR SIZE 1  = PARM

;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
                    "Liste des clients"
@ELSE
                    "%%%Liste des clients"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
                    "Par ordre de nom"    IF D_TYP_TRI="1" &
               ELSE "Par ordre de numéro" IF D_TYP_TRI="2"
@ELSE
                    "In Name Order"       IF D_TYP_TRI="1" &
               ELSE "In Number Order"     IF D_TYP_TRI="2"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CR301"

;
; Nom de la compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; Assigne le numéro ou nom du client dans variable du tri
;
DEFINE D_TRI   CHARACTER SIZE 46 = CLINOM OF VCLC_CLI + CLICLE OF VCLC_CLI IF D_TYP_TRI ="1" &
                              ELSE CLICLE OF VCLC_CLI IF D_TYP_TRI ="2"



DEFINE D_CLINOM CHARACTER SIZE 35 =  CLINOM  OF VCLC_CLI[1:35]

;
; Ordre du tri (Nom client ou Numéro client)
;
SORT ON D_TRI

;
; Impresseion du titre de la liste.
;
USE ustitstd NOLIST
 SKIP 2   &
TAB 001 "  No                                                                                                                  Limite de " SKIP  &
TAB 001 "client Catégorie           Nom client                Téléphone     Fax           Contact              T Dev. net  S L   crédit " SKIP  &
TAB 001 "------ --------- ----------------------------------- ------------- ------------- -------------------- - ---- ---- - - -----------" SKIP 2

REPORT &
  TAB 001 CLICLE     OF VCLC_CLI      PRINT AT D_TRI &
  TAB 011 CLICAT     OF VCLC_CLI      PRINT AT D_TRI &
  TAB 018 D_CLINOM                    PRINT AT D_TRI &
  TAB 054 RADTEL     OF MCREF_ADRESSE PRINT AT D_TRI &
  TAB 068 RADFAX     OF MCREF_ADRESSE PRINT AT D_TRI &
  TAB 082 CCTNOM     OF CRCLI_CONTACT PRINT AT D_TRI &
  TAB 105 CLITYP     OF VCLC_CLI      PRINT AT D_TRI &
  TAB 107 DEVCLE     OF VCLC_CLI      PRINT AT D_TRI &
  TAB 111 CLCTERNET  OF VCLC_CLI      PRINT AT D_TRI &
  TAB 116 CLISTU     OF VCLC_CLI      PRINT AT D_TRI &
  TAB 118 CLILNG     OF VCLC_CLI      PRINT AT D_TRI &
  TAB 120 CLCLIMCRT  OF VCLC_CLI

BUILD cr301z
@IF FORMAT

CR301Z


CR301                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                          Liste des clients / Sommaire
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx (1)

  No                                                                                                    ---------Terme--------                                                               Marge de
client Catégorie                Nom client                Téléphone     Contact              Type  Dev. net  esc1  esc2  frais  Statut Lang. Compagnies actives                                crédit
------ --------- ---------------------------------------- ------------- -------------------- ----  ---- ---- ----- ----- -----  ------ ----- --------------------------------------------- -----------
xxxxxx    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx  xxxx  xxxx     x     x    xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx
                                                                                                                                             xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx
                                                                                                                                             xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx
                                                                                                                                             xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx

xxxxxx    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx  xxxx  xxxx     x     x    xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx
                                                                                                                                             xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx
                                                                                                                                             xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx
                                                                                                                                             xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxx


 (1) par ordre alphabétique ou par ordre numérique
 (2) Sélection additionnelle ---> Compagnie: Optionnel (1,+sieurs, toutes)
@ENDIF
