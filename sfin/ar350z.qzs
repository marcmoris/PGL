;/T/ Valorisation des produits inventoriés par entrepôt / Détaillé
;
;/P/ Programmeur...: Guy Chabot
;    Date Création.: 14 septembre 1994
;
;    Description...: Ce rapport permet de présenter la valorisation des
;                    produits inventoriés par entrepôt en date donnée.
;
;    Paramètres....: Compagnie
;                    Entrepôt        (Une,plusieurs ou tous)
;                    Code de produit (Borne de début)
;                    Code de produit (Borne de fin)
;                    Flag quantité en main > 0  (O/N)
;
;/M/674/Marc Trudel/1999-10-14
;  Etat..........: Termin\351
;  Description...: GRA01
;                 Demander un paramètre de plus afin de pouvoir obtenir la
;                 liste des produits ayant une quantité en main > 0.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS VPRE_PRD &
  ;
  ; Pour avoir le nom de l'entrepôt
  ;
 LINK CIECLE OF VPRE_PRD, &
      ENTCLE OF VPRE_PRD  &
   TO CIECLE, &
      ENTCLE OF ARENTREPOT


CHOOSE CIECLE PARM,       &  ; Compagnie du menu
       PRDCLE PARM RANGE, &  ; Code de produit
       PRDCAT PARM,       &  ; Catégorie de produit
       PRDTYP "I"            ; Type de produit Inventorié

DEFINE D_FLGQTE CHARACTER SIZE 1 = PARM  ; Si "1" inclure seulement les produits
                                         ; dont la quantité en main est > 0.

SELECT IF ( D_FLGQTE = "1" AND PREQTEPHY OF VPRE_PRD > 0 ) OR &
          ( D_FLGQTE = "0" )


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Valorisation des produits inventoriés / Détaillée"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR350Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; Valorisation au dernier coûtant
;
DEFINE D_VALDERCOU FLOAT SIZE 8 = ( PREQTEPHY OF VPRE_PRD * &
                                    PREDERCOU OF VPRE_PRD ) / 100000

SORT ON CIECLE OF VPRE_PRD & ; Compagnie
     ON ENTCLE OF VPRE_PRD & ; Entrepôt
     ON PRDCLE OF VPRE_PRD   ; Code de produit


USE ustitstd NOLIST
  SKIP 2 &
TAB 001 "Entrepôt....:"      &
TAB 015 ENTCLE OF VPRE_PRD   &
TAB 020 ENTNOM OF ARENTREPOT &
        " Au : "  SYSDATE FORMAT YYYYMMDD   &
  SKIP 2 &
TAB 001 "                                                       Quantité                   Valorisation     Dernier          Valorisation" &
  SKIP &
TAB 001 "Code de produit                                        en main      Coût moyen    au coût moyen    coûtant        au dernier coûtant" &
  SKIP &
TAB 001 "----------------------------------------------------- ------------  ------------  ---------------  -------------  ------------------" &
  SKIP 1


REPORT &
TAB 001 PRDCLE    OF VPRE_PRD &
TAB 014 PRDDSC1   OF VPRE_PRD &
TAB 055 PREQTEPHY OF VPRE_PRD &
        PICTURE "^^^,^^^.^^^ "&
        TRAILING SIGN "-"     &
        SIGNIFICANCE 6        &
       BWZ                    &
TAB 069 PRECOUMYN OF VPRE_PRD &
        PICTURE "^^,^^^.^^^^ "&
        TRAILING SIGN "-"     &
        SIGNIFICANCE 7        &
        BWZ                   &
TAB 083 PREVAL    OF VPRE_PRD &
        PICTURE "^^^,^^^,^^^.^^ " &
        TRAILING SIGN "- "    &
        SIGNIFICANCE 5        &
        BWZ                   &
TAB 100 PREDERCOU OF VPRE_PRD &
        PICTURE "^^^,^^^.^^^^ " &
        TRAILING SIGN "-"     &
        SIGNIFICANCE 7        &
        BWZ                   &
TAB 118 D_VALDERCOU           &
        PICTURE "^^^,^^^,^^^.^^ " &
        TRAILING SIGN "-"     &
        SIGNIFICANCE 5        &
        BWZ


FOOTING AT ENTCLE &
  SKIP 1 &
TAB 083 "---------------                    ---------------" &
  SKIP &
TAB 008 "Total pour l'entrepôt" &
TAB 083 PREVAL OF VPRE_PRD SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
TAB 118 D_VALDERCOU SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
  SKIP &
TAB 083 "---------------                    ---------------" &
  SKIP PAGE


FINAL FOOTING &
  SKIP 2 &
TAB 083 "---------------                    ---------------" &
  SKIP &
TAB 008 "Total pour tous les entrepôts" &
TAB 083 PREVAL OF VPRE_PRD SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
TAB 118 D_VALDERCOU SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
  SKIP &
TAB 083 "---------------                    ---------------"


BUILD ar350z


@IF FORMAT
         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20
12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
AR350                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                           Page: 999
99/99/99 HH:MM xxxxxxxxxxxx                 Valorisation des produits inventoriés par entrepôt / Détaillée
                                             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Entrepôt....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

                                                       Quantité                   Valorisation     Dernier          Valorisation
Code de produit                                        en main      Coût moyen    au coût moyen    coûtant        au dernier coûtant
----------------------------------------------------- ------------  ------------  ---------------  -------------  ------------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999,999.999-  99,999.9999-  999,999,999.99-  999,999.9999-     999,999,999.99-
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999,999.999-  99,999.9999-  999,999,999.99-  999,999.9999-     999,999,999.99-
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999,999.999-  99,999.9999-  999,999,999.99-  999,999.9999-     999,999,999.99-


                                                                                  ---------------                    ---------------
       Total pour l'entrepôt                                                      999,999,999.99-                    999,999,999.99-
                                                                                  ---------------                    ---------------

                                                                                  ---------------                    ---------------
       Total pour tous les entrepôts                                              999,999,999.99-                    999,999,999.99-
                                                                                  ---------------                    ---------------
@ENDIF
