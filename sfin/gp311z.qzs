;/T/ Rapport sur les comptes prix de revient (court)
;
;/M/ Marie-Josée Hamel, 96.03.14, faire un lien entre les réceptions et les
;                                 facture pour que les mnts et les commentaires
;                                 soient ensemble.
;-------------------------------------------------------------------------------
USE ussetqzs NOLIST

ACCESS *WGP311A &
  LINK REFCIECLE, REFCLETYP, REFCLENUM TO &
       REFCIECLE, REFCLETYP, REFCLENUM OF MCREFERENCE OPTIONAL &
  LINK CIECLE, PRJCLE TO &
       CIECLE, PRJCLE OF GPPROJETS OPTIONAL  &
  LINK G-CPTCLE1 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE1 OPTIONAL &
  LINK G-CPTCLE2 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE2 OPTIONAL &
  LINK G-CPTCLE3 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE3 OPTIONAL &
  LINK G-CPTCLE4 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE4 OPTIONAL


DEFINE D_DAT DATE              = HISDAT OF WGP311A &
                              IF T_RECDAT = 0 &
                            ELSE T_RECDAT

DEFINE D_TRI CHARACTER SIZE 27 = T_RECCLE OF WGP311A + HISNUMDOC OF WGP311A &
                             IF    HISCODMOD OF WGP311A <> "AR" &
                             ELSE HISNUMDOC OF WGP311A

SORT ON CIECLE &
     ON PRJCLE &
     ON REFCLENUM &
     ON D_DAT &
     ON D_TRI


; Description des quatre comptes en en-tete
;
DEFINE D-CPTDSC1 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE1
DEFINE D-CPTDSC2 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE2
DEFINE D-CPTDSC3 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE3
DEFINE D-CPTDSC4 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE4

; Séparation des montants sous les quatres comptes (un seul mnt par compte)
;
DEFINE D_MNT1 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE1 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT2 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE2 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT3 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE3 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT4 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE4 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT

DEFINE D-MNT1 INTEGER SIZE 4 = D_MNT1 IF CPTCLE = G-CPTCLE1 &
                               ELSE 0
DEFINE D-MNT2 INTEGER SIZE 4 = D_MNT2 IF CPTCLE = G-CPTCLE2 &
                               ELSE 0
DEFINE D-MNT3 INTEGER SIZE 4 = D_MNT3 IF CPTCLE = G-CPTCLE3 &
                               ELSE 0
DEFINE D-MNT4 INTEGER SIZE 4 = D_MNT4 IF CPTCLE = G-CPTCLE4 &
                               ELSE 0
DEFINE D-TOT  INTEGER SIZE 4 = D-MNT1 + D-MNT2 + D-MNT3 + D-MNT4

;
; Affichage des descriptions correspondantes
;
DEFINE D_DSC1 CHARACTER SIZE 50 = T_DSC1 &
                              IF 0 <> SIZE( TRUNCATE( T_DSC1 ) ) &
                            ELSE HISDSCGEN OF WGP311A &
                              IF HISDSCGEN OF WGP311A <> "" &
                            ELSE HISDSCSAI OF WGP311A &
                              IF HISDSCSAI OF WGP311A <> ""
DEFINE D_DSC2 CHARACTER SIZE 50 = T_DSC2 &
                              IF 0 <> SIZE( TRUNCATE( T_DSC2 ) ) &
                            ELSE HISDSCGEN OF WGP311A &
                              IF    HISDSCGEN OF WGP311A <> "" &
                                AND HISDSCGEN OF WGP311A <> D_DSC1 &
                            ELSE HISDSCSAI OF WGP311A &
                              IF    HISDSCSAI OF WGP311A <> "" &
                                AND HISDSCSAI OF WGP311A <> D_DSC1 &
                            ELSE HISDSCSAI2 OF WGP311A &
                              IF    HISDSCSAI2 OF WGP311A <> ""
DEFINE D_DSC3 CHARACTER SIZE 50 = T_DSC3 &
                              IF 0 <> SIZE( TRUNCATE( T_DSC3 ) ) &
                            ELSE HISDSCGEN OF WGP311A &
                              IF    HISDSCGEN OF WGP311A <> "" &
                                AND HISDSCGEN OF WGP311A <> D_DSC1 &
                                AND HISDSCGEN OF WGP311A <> D_DSC2 &
                            ELSE HISDSCSAI OF WGP311A &
                              IF    HISDSCSAI OF WGP311A <> "" &
                                AND HISDSCSAI OF WGP311A <> D_DSC1 &
                                AND HISDSCSAI OF WGP311A <> D_DSC2 &
                            ELSE HISDSCSAI2 OF WGP311A &
                              IF    HISDSCSAI2 OF WGP311A <> "" &
                                AND HISDSCSAI2 OF WGP311A <> D_DSC2


DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
  "Rapport sur les comptes au prix de revient"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GP311"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie


USE ustit220 NOLIST
  TAB 001 "Projet :" &
  TAB 010 PRJCLE OF WGP311A &
  TAB 020 PRJDSC1 OF GPPROJETS &
SKIP 2 &
  TAB 001 "# Client/" &
  TAB 010 "Nom" &
  TAB 047 "Date" &
  TAB 055 "# piece" &
  TAB 075 "Commentaires" &
  TAB 130 D-CPTDSC1 &
  TAB 146 D-CPTDSC2 &
  TAB 162 D-CPTDSC3 &
  TAB 178 D-CPTDSC4 &
  TAB 199 "Total" &
  TAB 208 "Source" &
SKIP &
  TAB 001 "# fourn." &
  TAB 136 "(" &
  TAB 137 G-CPTCLE1 &
  TAB 143 ")" &
  TAB 151 "(" &
  TAB 152 G-CPTCLE2 &
  TAB 158 ")" &
  TAB 166 "(" &
  TAB 167 G-CPTCLE3 &
  TAB 173 ")" &
  TAB 181 "(" &
  TAB 182 G-CPTCLE4 &
  TAB 188 ")" &
SKIP 2

FOOT AT D_TRI &
  TAB 001 REFCLENUM   PRINT AT REFCLENUM &
  TAB 010 REFNOM      PRINT AT REFCLENUM &
  TAB 045 HISDAT &
  TAB 055 HISNUMDOC &
  TAB 075 D_DSC1 &
  TAB 130 D-MNT1 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 145 D-MNT2 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 160 D-MNT3 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 175 D-MNT4 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 190 D-TOT  SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 210 HISCODMOD &
SKIP &
  TAB 075 D_DSC2 &
SKIP &
  TAB 075 D_DSC3

FOOT AT PRJCLE &
  TAB 130 "--------------" &
  TAB 145 "--------------" &
  TAB 160 "--------------" &
  TAB 175 "--------------" &
  TAB 190 "--------------" &
SKIP &
  TAB 001 "Total" &
  TAB 007 PRJCLE &
  TAB 130 D-MNT1 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 145 D-MNT2 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 160 D-MNT3 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 175 D-MNT4 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 190 D-TOT  SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
SKIP PAGE

BUILD gp311z
