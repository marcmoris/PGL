;/T/ Termes - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 9 août 1993
;
;    Description..: Cet écran vérifie si le terme d'encaissement reçu en
;                   paramètre est référé dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr153x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING CRTERME , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Termes d'encaissement.
;
FILE CRTERME          MASTER

;
; Client - compagnie
;
FILE CRCLI_CIE        DESIGNER &
                      OPEN READ

;
; Comptes à recevoir.(facture...)
;
FILE CRCPT_A_REC      DESIGNER &
                      OPEN READ


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Vérification dans le client par compagnie.
  ;
  GET CRCLI_CIE VIA   CLCTERNET         &
                USING TMECLE OF CRTERME &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 722 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.
  ;
  GET CRCLI_CIE VIA   CLCTERESC1        &
                USING TMECLE OF CRTERME &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 722 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.
  ;
  GET CRCLI_CIE VIA   CLCTERESC2        &
                USING TMECLE OF CRTERME &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 722 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.
  ;
  GET CRCLI_CIE VIA   CLCTERFRS         &
                USING TMECLE OF CRTERME &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 722 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.
  ;
  ; Vérification dans les comptes à recevoir(facture...).
  ;
  GET CRCPT_A_REC VIA   CARTERNET         &
                  USING TMECLE OF CRTERME &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 723 ; Destruction annulée, cette information est référée par
                 ; un compte à recevoir
  ;
  GET CRCPT_A_REC VIA   CARTERESC1        &
                  USING TMECLE OF CRTERME &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 723 ; Destruction annulée, cette information est référée par
                 ; un compte à recevoir
  ;
  GET CRCPT_A_REC VIA   CARTERESC2        &
                  USING TMECLE OF CRTERME &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 723 ; Destruction annulée, cette information est référée par
                 ; un compte à recevoir
  ;
  GET CRCPT_A_REC VIA   CARTERFRS         &
                  USING TMECLE OF CRTERME &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 723 ; Destruction annulée, cette information est référée par
                 ; un compte à recevoir

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
