;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Unités de travail
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp155 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "GP155"             ; Nom de l'écran

USE gp155.hlp NOLIST    ; Use servant à la documentation de l'écran
                        ; Ne pas oublier de créer un fichier vide dans [exefra], [exeang]


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

;------------------------------------
; Fichier d'unité de travail
;------------------------------------
FILE GPUNITE_TRAVAIL        PRIMARY &
                            NOITEMS

  ACCESS  VIA     UNTCLE &
          USING   UNTCLE OF GPUNITE_TRAVAIL &
          REQUEST UNTCLE &
          ORDERBY UNTCLE

  ACCESS  VIA     CPTCLE &
          USING   CPTCLE OF GPUNITE_TRAVAIL &
          REQUEST CPTCLE &
          ORDERBY CPTCLE

  ACCESS  VIA     UNMCLE &
          USING   UNMCLE OF GPUNITE_TRAVAIL &
          REQUEST UNMCLE &
          ORDERBY UNTCLE

  ACCESS  SEQUENTIAL &
          ORDERBY UNTCLE

  ITEM UNTUSRCRE OF GPUNITE_TRAVAIL INITIAL LOGONID
  ITEM UNTDATCRE OF GPUNITE_TRAVAIL INITIAL SYSDATE
  ITEM UNTHRECRE OF GPUNITE_TRAVAIL INITIAL SYSTIME
  ITEM UNTDATMOD OF GPUNITE_TRAVAIL FINAL   SYSDATE        &
                                       IF NOT NEWRECORD OF GPUNITE_TRAVAIL AND &
                                          ALTEREDRECORD OF GPUNITE_TRAVAIL
  ITEM UNTHREMOD OF GPUNITE_TRAVAIL FINAL   SYSTIME &
                                       IF NOT NEWRECORD OF GPUNITE_TRAVAIL AND &
                                          ALTEREDRECORD OF GPUNITE_TRAVAIL
  ITEM UNTUSRMOD OF GPUNITE_TRAVAIL FINAL   LOGONID         &
                                       IF NOT NEWRECORD OF GPUNITE_TRAVAIL AND &
                                          ALTEREDRECORD OF GPUNITE_TRAVAIL


;-------------------------------------------------------------------
; Validation du type d'unité de travail et lecture de sa description
;-------------------------------------------------------------------
DEFINE    D_UNTTYP CHARACTER SIZE 16 = "UNTTYP"
TEMPORARY T_UNTTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_UNTTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_UNTTYP, &
                UNTTYP OF GPUNITE_TRAVAIL


;----------------------------------------------------------
; Validation du statut d'unité et lecture de sa description
;----------------------------------------------------------
DEFINE    D_UNTSTU CHARACTER SIZE 16 = "UNTSTU"
TEMPORARY T_UNTSTU CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_UNTSTU

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_UNTSTU, &
                UNTSTU OF GPUNITE_TRAVAIL


;--------------------------
; Fichier d'unité de mesure
;--------------------------
TEMPORARY T_UNMCLE CHARACTER SIZE 4

FILE GPUNITE_MESURE         REFERENCE

  ACCESS  VIA   UNMCLE  &
          USING UNMCLE OF GPUNITE_TRAVAIL


;--------------------------
; Fichier compte client
;--------------------------
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes.
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes.
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes.

FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPUNITE_TRAVAIL


;----------------------------
; Fichier usager de créeation
;----------------------------
FILE GSUSAGER               REFERENCE &
                            ALIAS GSUSAGER_CRE

  ACCESS  VIA   USRCLE  &
          USING UNTUSRCRE OF GPUNITE_TRAVAIL


;-------------------------------
; Fichier usager de modification
;-------------------------------
FILE GSUSAGER               REFERENCE &
                            ALIAS GSUSAGER_MOD

  ACCESS  VIA   USRCLE  &
          USING UNTUSRMOD OF GPUNITE_TRAVAIL



;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Unités de travail " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19)(,45,61)(,,64)

FIELD UNTCLE OF GPUNITE_TRAVAIL                       &
label "Unité de trav.:" &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LOOKUP NOTON GPUNITE_TRAVAIL                    &
        VIA   UNTCLE                                  &
        USING UNTCLE OF GPUNITE_TRAVAIL               &
        MESSAGE 819; Cette unité de travail existe déjà

FIELD UNTTYP OF GPUNITE_TRAVAIL                       &
label "Type..........:" &
      ID SAME                                         &
      REQUIRED                                        &
      LOOKUP ON MCCODE_BUIL_UNTTYP                    &
        MESSAGE 820; Ce type d'unité de travail est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_UNTTYP                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 15

SKIP 1

ALIGN (3,3,19)
FIELD UNTDSC OF GPUNITE_TRAVAIL                       &
label "Description...:" &
      HIDDEN

SKIP 1

ALIGN (3,3,19) (,,24) (,,55)
FIELD UNMCLE OF GPUNITE_TRAVAIL                       &
label "Unité mesure..:" &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LOOKUP ON GPUNITE_MESURE                        &
        MESSAGE 821; Cette unité de mesure est inexistante

FIELD UNMDSC OF GPUNITE_MESURE                        &
      ID SAME                                         &
      DISPLAY

FIELD UNMABR OF GPUNITE_MESURE                        &
      ID SAME                                         &
      DISPLAY                                         &
      PICTURE "/^^"


SKIP 1

ALIGN (,3,19)
FIELD UNMNBRDEC OF GPUNITE_MESURE                     &
label "Nbr. décimale.:" &
      ID SAME                                         &
      DISPLAY


SKIP 1

ALIGN (3,3,19) (,,21)
FIELD UNTSTU OF GPUNITE_TRAVAIL                       &
label "Statut........:" &
      HIDDEN                                          &
      DEFAULT "A"                                     &
      LOOKUP ON MCCODE_BUIL_UNTSTU                    &
        MESSAGE 822; Ce statut d'unité de travail est invalide

FIELD CBIDSC    OF MCCODE_BUIL_UNTSTU                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 15


SKIP 1

ALIGN (3,3,19) (,,26)
FIELD CPTCLE OF GPUNITE_TRAVAIL                       &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LABEL "Compte réf....:"                         &
      LOOKUP ON MCCOMPTE                              &
        MESSAGE 823; Ce statut compte est inexistant

FIELD CPTDSCFRA OF MCCOMPTE                           &
      ID SAME                                         &
      DISPLAY

SKIP 1

ALIGN (,3,19) (,,32)
FIELD UNTDATCRE OF GPUNITE_TRAVAIL                     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Créé le.......:" &
      DISPLAY

FIELD UNTHRECRE OF GPUNITE_TRAVAIL                    &
      DISPLAY

ALIGN (,3,19) (,,33)
FIELD UNTUSRCRE OF GPUNITE_TRAVAIL                    &
label "Créé par......:" &
      DISPLAY

FIELD USREMPNOM OF GSUSAGER_CRE                       &
      DISPLAY

SKIP 1

ALIGN (,3,19) (,,32)
FIELD UNTDATMOD OF GPUNITE_TRAVAIL                     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Modifié le....:" &
      DISPLAY

FIELD UNTHREMOD OF GPUNITE_TRAVAIL                    &
      DISPLAY

ALIGN (,3,19) (,,33)
FIELD UNTUSRMOD OF GPUNITE_TRAVAIL                    &
label "Modifié par...:" &
      DISPLAY

FIELD USREMPNOM OF GSUSAGER_MOD                       &
      DISPLAY




;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNTTYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNTTYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNTTYP, T_UNTTYP
    LET FIELDTEXT = TRUNCATE(T_UNTTYP)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNTSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNTSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNTSTU, T_UNTSTU
    LET FIELDTEXT = TRUNCATE(T_UNTSTU)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les unités de mesure.
;
;
PROCEDURE INPUT UNMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp101 MODE F PASSING T_UNMCLE
    LET FIELDTEXT = TRUNCATE(T_UNMCLE)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les comptes.
;
;
PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "O"
    LET T_CPTCATPAT = "@"
    RUN SCREEN gp110 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTCLE)
  END
END

PROCEDURE EDIT CPTCLE
BEGIN
   IF CPTTYP OF MCCOMPTE <> "O"
   THEN ERROR 880 ;Le type de compte doit être d'opération
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPUNITE_TRAVAIL &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPUNITE_TRAVAIL &
     AND NOT NEWRECORD     OF GPUNITE_TRAVAIL &
     AND NOT DELETEDRECORD OF GPUNITE_TRAVAIL &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Destruction de l'unité de travail
  ;
  IF DELETEDRECORD OF GPUNITE_MESURE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le type d'unites de travail est
    ; référée dans la définition de unités de travail.
    ;
    RUN SCREEN gp155x MODE SAME &
                      PASSING GPUNITE_MESURE, &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END


END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Unités de travail ******************************
*                                                                             *
*Unité de trav.: xxxx                      Type..........: xx xxxxxxxxxxxxxxx *
*                                                                             *
*Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                             *
*Unité mesure..: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx                       *
*                                                                             *
*Nbr. décimale.: xx                                                           *
*                                                                             *
*Statut........: x xxxxxxxxxxxxxxx                                            *
*                                                                             *
*Compte........: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
*                                                                             *
*Créé le.......: xxxxxxxx xxxxx                                               *
*Créé par......: xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       *
*                                                                             *
*Modifié le....: xxxxxxxx xxxxx                                               *
*Modifié par...: xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       *
*                                                                             *
*******************************************************************************
@ENDIF

