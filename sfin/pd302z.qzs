;/T/ 1.4.5.2 P/O par commande interne
;
;/P/ Programmeur...: René Bonneau
;    Date Création.: 1994-10-18
;
;    Description...: Liste de base des bons de commande par commande interne
;
;    Paramètres....: Numéro de commande interne = COMNUMCOMINT
;
;    Particularités: AUCUNE
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS PDBON_COMMANDE &
  LINK CIECLE          OF PDBON_COMMANDE,  &
       COMNUMCOM       OF PDBON_COMMANDE   &
    TO CIECLE,                             &
       COMNUMCOM                           OF PDCOMMAN_INTERNE OPTIONAL &
  LINK CIECLE          OF PDBON_COMMANDE,  &
       PJTABR          OF PDBON_COMMANDE,  &
       PJTANN          OF PDBON_COMMANDE,  &
       PJTNUMSEQ       OF PDBON_COMMANDE   &
    TO CIECLE,                             &
       PJTABR,                             &
       PJTANN,                             &
       PJTNUMSEQ                           OF PDPROJET         OPTIONAL  &
  LINK FOUCLE          OF PDBON_COMMANDE,  &
       FOCCIECLE       OF PDBON_COMMANDE   &
    TO FOUCLE,                             &
       FOCCIECLE                           OF VFOC_FOU         OPTIONAL  &
  LINK FOUCIECLE       OF VFOC_FOU,        &
       FOUREFTYP       OF VFOC_FOU,        &
       FOUCLE          OF VFOC_FOU,        &
       FOCADRACH       OF VFOC_FOU         &
    TO REFCIECLE,                          &
       REFCLETYP,                          &
       REFCLENUM,                          &
       RADCLE                              OF MCREF_ADRESSE    OPTIONAL


CHOOSE CIECLE PARM , COMNUMCOM PARM


DEFINE D_BCMCODTYPBCM CHARACTER SIZE 15 = &
                      "Transfert"      IF BCMCODTYPBCM = "T" ELSE &
                      "Sous-traitance" IF BCMCODTYPBCM = "S"


DEFINE D_COMMANDE_INT CHAR SIZE 8 = COMNUMCOM    OF PDBON_COMMANDE


SORT ON D_COMMANDE_INT &
     ON BCMNUMBCM OF PDBON_COMMANDE


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "P/O par commande interne"
@ELSE
       "%%%%P/O par commande interne"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD302Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
SKIP 2

HEADING AT D_COMMANDE_INT &
  TAB 001 "Com. int......:" &
  TAB 022 COMNUMCOM OF PDBON_COMMANDE &
SKIP 1 &
  TAB 001 "Description...:" &
  TAB 022 BCMDSC001 OF PDBON_COMMANDE &
SKIP 1 &
  TAB 022 BCMDSC002 OF PDBON_COMMANDE &
SKIP 1 &
  TAB 022 BCMPROPOT OF PDBON_COMMANDE &
SKIP 1 &
  TAB 001 "Projet........:" &
  TAB 022 PJTABR OF PDBON_COMMANDE &
  TAB 025 PJTNOM OF PDPROJET &
SKIP 2 &
  TAB 003 "No de P/O" &
  TAB 016 "Type de P/O" &
  TAB 031 "No four" &
  TAB 039 "Nom fournisseur" &
  TAB 080 "Téléphone" &
  TAB 096 "FAX" &
SKIP 1 &
  TAB 003 "---------" &
  TAB 016 "-----------" &
  TAB 031 "-------" &
  TAB 039 "---------------" &
  TAB 080 "---------" &
  TAB 096 "---" &
SKIP 2

REPORT &
    TAB 001 BCMNUMBCM    OF PDBON_COMMANDE     &
    TAB 016 D_BCMCODTYPBCM                     &
    TAB 031 FOUCLE       OF PDBON_COMMANDE     &
    TAB 039 FOUNOM       OF VFOC_FOU           &
    TAB 080 RADTEL       OF MCREF_ADRESSE      &
    TAB 096 RADFAX       OF MCREF_ADRESSE


FOOTING AT D_COMMANDE_INT &
SKIP PAGE



BUILD pd302z
