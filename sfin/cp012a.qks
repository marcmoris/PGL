;/T/ Numérotation - Consultation des paiement
;
;/P/ Programmeur..: Alain Côté
;    Date création: 9 février 1994
;
;    Description..: Permet de visionner les paiements d'une numéroration et
;                   de choisir un paiement si l'écran est appellé en mode
;                   dépisteur.
;
;    Parametres...: CPRENUM_PAM     = Numérotation des paiements
;                   T_PAMCLE        = Valeur de retour
;
;/V3.00.01/ Alain Côté, 10 février 1994, 212
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp012a NOMODE NOACTION FIELDMARK &
              FROM 10,28 TO 23,80 &
              ACTIVITIES FIND &
              RECEIVING  CPRENUM_PAM , &
                         T_PAMCLE


USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP012A"

USE cp012a.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_PAMCLE    CHARACTER SIZE 7

;-------------------------------------------------------------------------------

FILE CPRENUM_PAM      MASTER

FILE CPPAIEMENT       PRIMARY &
                      OCCURS 10 &
                      NOITEM

  ACCESS  VIA   CIECLE    , &
                LBQCPTENC , &
                PECCLE    , &
                LCPCLE      &
          USING CIECLE    OF CPRENUM_PAM , &
                LBQCPTENC OF CPRENUM_PAM , &
                PECCLE    OF CPRENUM_PAM , &
                LCPCLE    OF CPRENUM_PAM   &
          ORDERBY   CIECLE   , &
                    LBQCPTENC, &
                    PAMCLE

SELECT  IF     PAMFLGIMP OF CPPAIEMENT = "1" &
           AND PAMCODANN OF CPPAIEMENT = "0" &
           AND PAMCLE OF CPPAIEMENT >= RENPAMDEB OF CPRENUM_PAM     &
           AND (   PAMCLE OF CPPAIEMENT <= RENPAMFIN OF CPRENUM_PAM &
                OR RENPAMFIN OF CPRENUM_PAM = "" )

;-------------------------------------------------------------------------------

USE ushilite NOLIST

DRAW 1,1 TO 14,53

USE ustitoff NOLIST

SKIP 1

TITLE &
@IF FRANCAIS
      "   No         No         Date         Montant  " &
@ELSE
      "%%%No         No         Date         Montant  " &
@ENDIF
      AT ,5

SKIP

TITLE &
@IF FRANCAIS
      "paiement  pré-imprimé  paiement     du paiement" &
@ELSE
      "paiement  pré-imprimé  paiement     du paiement" &
@ENDIF
      AT ,5
SKIP

ALIGN (5,2,5) (,,17) (,,27) (,,38)

CLUSTER OCCURS WITH CPPAIEMENT

FIELD PAMCLE    OF CPPAIEMENT DISPLAY HIDDEN LABEL ">>"
FIELD PAMNUMPIM OF CPPAIEMENT DISPLAY
FIELD PAMDAT    OF CPPAIEMENT  FORMAT YYYYMMDD PATTERN "####/##/##" DISPLAY
FIELD PAMMNT    OF CPPAIEMENT DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_PAMCLE, "@")
  THEN LET T_PAMCLE = ""
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_PAMCLE    = PAMCLE OF CPPAIEMENT
  RETURN
END

BUILD
@IF FORMAT


                           *****************************************************
                           *      No         No         Date         Montant   *
                           *   paiement  pré-imprimé  paiement     du paiement *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *>> xxxxxxx     xxxxxxxx   xxxxxxxx  xxxxxxxxxxxxxx *
                           *****************************************************
@ENDIF
