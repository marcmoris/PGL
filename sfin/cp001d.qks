;/T/ Adresses supplémentaires
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 7 Juin 1993
;
;    Description..: Cet écran permet de gérer les adresses des fournisseurs.
;                   Il peut exister plusieurs adresse par fournisseurs.
;                   Les fournisseur divers n'ont pas d'adresses permanantes,
;                   elles sont saisies lors de la création des pièces.
;
;/M/ Adatation....: Marc Morissette 10 Novembre 1993
;
;    Description..: Ajout du code de région
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp001d ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FOR 10 LINES &
              ON LINE 14 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING CPFOURNISSEUR

USE ustrasse NOLIST

USE ussectmp   NOLIST
    "CP001D"

USE cp001d.hlp NOLIST

USE usactecr   NOLIST

;-------------------------------------------------------------------------------

FILE CPFOURNISSEUR MASTER

;-------------------------------------------------------------------------------

FILE MCREF_ADRESSE PRIMARY NOITEM

  ACCESS  VIA     REFCIECLE, &
                  REFCLETYP, &
                  REFCLENUM &
          USING   FOUCIECLE OF CPFOURNISSEUR, &
                  FOUREFTYP OF CPFOURNISSEUR, &
                  FOUCLE    OF CPFOURNISSEUR  &
          ORDERBY REFCIECLE, &
                  REFCLETYP, &
                  REFCLENUM, &
                  RADCLE

  ITEM REFCIECLE OF MCREF_ADRESSE INIT FOUCIECLE OF CPFOURNISSEUR
  ITEM REFCLETYP OF MCREF_ADRESSE INIT FOUREFTYP OF CPFOURNISSEUR
  ITEM REFCLENUM OF MCREF_ADRESSE INIT FOUCLE    OF CPFOURNISSEUR
  ;
  ; Par défaut on crée une adresse de paiement
  ;
  ITEM RADPAMCP  OF MCREF_ADRESSE INIT "1"

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

;-------------------------------------------------------------------------------


USE ushilite NOLIST

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO 10, 1
DRAW FROM  3,80 TO 10,80
DRAW FROM 10, 1 TO 10,80

SKIP 1

TITLE &
@IF FRANCAIS
      " Adresses supplémentaires " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN(,3,19) (,24,35) (,38,46) (,49,58)(,61,70)

FIELD RADCLE OF MCREF_ADRESSE &
      HIDDEN ID 05 &
      REQUIRED &
      NOCHANGE &
      LOOKUP NOTON MCREF_ADRESSE &
              VIA   REFCIECLE, &
                    REFCLETYP, &
                    REFCLENUM, &
                    RADCLE     &
              USING REFCIECLE OF MCREF_ADRESSE, &
                    REFCLETYP OF MCREF_ADRESSE, &
                    REFCLENUM OF MCREF_ADRESSE, &
                    RADCLE    OF MCREF_ADRESSE  &
              MESSAGE 433 ; Ce numéro d'adresse existe déjà.


FIELD RADPAMCP OF MCREF_ADRESSE &
@IF FRANCAIS
      LABEL "Paiement.:"
@ELSE
      LABEL "Payment..:"
@ENDIF

FIELD RADACHCP OF MCREF_ADRESSE &
@IF FRANCAIS
      LABEL "Achat.:"
@ELSE
      LABEL "Purch.:"
@ENDIF

FIELD RADRETCP OF MCREF_ADRESSE &
@IF FRANCAIS
      LABEL "Retour.:"
@ELSE
      LABEL "Return.:"
@ENDIF

FIELD RADREG OF MCREF_ADRESSE &
@IF FRANCAIS
      LABEL "Région.:"
@ELSE
      LABEL "R%%%%%.:"
@ENDIF


ALIG(3,3,19)

FIELD RADNOM OF MCREF_ADRESSE &
      HIDDEN ID 10 &
      REQUIRED

FIELD RADNOMABR OF MCREF_ADRESSE &
      HIDDEN ID SAME &
      DEFAULT RADNOM OF MCREF_ADRESSE

FIELD RADADR1 OF MCREF_ADRESSE &
      HIDDEN ID 15 &
      REQUIRED

FIELD RADADR2 OF MCREF_ADRESSE &
      HIDDEN ID SAME &
      NOLABEL

FIELD RADADR3 OF MCREF_ADRESSE &
      HIDDEN ID SAME &
      NOLABEL

FIELD RADADR4 OF MCREF_ADRESSE &
      HIDDEN ID SAME &
      NOLABEL

SKIP TO 9
ALIGN(,60,67)

FIELD RADCP OF MCREF_ADRESSE &
      HIDDEN ID SAME &
@IF FRANCAIS
      LABEL "C.P..:"
@ELSE
      LABEL "P.C..:"
@ENDIF

SKIP TO 6

ALIGN(60,60,67)

FIELD RADTEL OF MCREF_ADRESSE &
      HIDDEN ID SAME &
@IF FRANCAIS
      LABEL "Tél..:"
@ELSE
      LABEL "Tel..:"
@ENDIF

FIELD RADFAX OF MCREF_ADRESSE &
      HIDDEN ID SAME &
@IF FRANCAIS
      LABEL "Fax..:"
@ELSE
      LABEL "Fax..:"
@ENDIF

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Flag OUI/NON sur le type d'adresse de paiement
;
;
PROCEDURE INPUT RADPAMCP
BEGIN
  USE usflginp NOLIST
END

PROCEDURE OUTPUT RADPAMCP
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
; Flag OUI/NON sur le type d'adresse d'achats
;
;
PROCEDURE INPUT RADACHCP
BEGIN
  USE usflginp NOLIST
END

PROCEDURE OUTPUT RADACHCP
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
; Flag OUI/NON sur le type d'adresse de retour
;
;
PROCEDURE INPUT RADRETCP
BEGIN
  USE usflginp NOLIST
END

PROCEDURE OUTPUT RADRETCP
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
; Procedure de formatage du numero de code postal
;
PROCEDURE INPUT RADCP
BEGIN
  USE uscpfmt NOLIST  ;Fomate le numéro de code postal
END

;-------------------------------------------------------------------------------
;
; Mise en forme et affichage du numéro de téléphone
;
;
PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END

;-------------------------------------------------------------------------------
;
; Mise en forme et affichage du numéro de téléphone
;
PROCEDURE INPUT RADFAX
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END

;-----------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF      DELETEDRECORD OF MCREF_ADRESSE &
      AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification
  ;
  IF     ALTEREDRECORD     OF MCREF_ADRESSE &
     AND NOT NEWRECORD     OF MCREF_ADRESSE &
     AND NOT DELETEDRECORD OF MCREF_ADRESSE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Le timbre de modification est mis à jour si l'usager modifie
  ; la table MCREF_ADRESSE.
  ;
  IF        NEWRECORD OF MCREF_ADRESSE &
     OR ALTEREDRECORD OF MCREF_ADRESSE &
     OR DELETEDRECORD OF MCREF_ADRESSE
  THEN BEGIN
    LET FOUDATMOD OF CPFOURNISSEUR = SYSDATE
    LET FOUHREMOD OF CPFOURNISSEUR = SYSTIME / 10000
    LET FOUUSRMOD OF CPFOURNISSEUR = LOGONID
  END
  ;
  ; Destruction d'une adresse.
  ;
  IF DELETEDRECORD OF MCREF_ADRESSE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si l'adresse est référé.
    ;
    RUN SCREEN cp001dx MODE SAME &
                       PASSING MCREF_ADRESSE , &
                               T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END


;-------------------------------------------------------------------------------
;
; L'adresse numéro 1 est toujours une adresse de paiement.
;
;
PROCEDURE DESIGNER 5
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN ACCEPT RADPAMCP OF MCREF_ADRESSE
  ACCEPT RADACHCP OF MCREF_ADRESSE
  ACCEPT RADRETCP OF MCREF_ADRESSE
  ACCEPT RADREG   OF MCREF_ADRESSE
END

;-------------------------------------------------------------------------------
;
; Le nom de l'adresse numéro 1 est non modifiable, car il doit être identique
; au nom du fournisseur.
;
;
PROCEDURE DESIGNER 10
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN BEGIN
    ACCEPT RADNOM     OF MCREF_ADRESSE
    ACCEPT RADNOMABR  OF MCREF_ADRESSE
  END
END


;-------------------------------------------------------------------------------
;
; Il est impossible de detruire l'adresse de base (RADCLE = 1)
;
PROCEDURE DELETE
BEGIN
  IF RADCLE OF MCREF_ADRESSE = 1
  THEN ERROR 560 ; Vous ne pouvez pas détruire l'adresse de base
  ELSE DELETE MCREF_ADRESSE
END

BUILD
@IF FORMAT

*************************** Adresses supplémentaires **************************x
* No adresse....: xxxx Paiement.: x  Achat.: x  Retour.: x  Région.: xxx        *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                                         *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Tél..: xxxxxxxxxxxxx*
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Fax..: xxxxxxxxxxxxx*
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx C.P..: xxxxxxx      *
********************************************************************************
@ENDIF
