;/T/ Impression de l'inventaire physique des blocs avec valorisation.
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-02
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste de valorisation des blocs de l'inventaire
;                   de bloc de granit de Granicor.
;
;    Paramètres....: Aucun
;
;/P/ Programmeur...: Rémy Demers
;    Date de Modif.: 1997-10-16
;    Description...: Faire une sommarisation des prix par type de bloc
;                    et ajout d'un qtp...ig312t.qts
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

;===============================================================================
;
;  ACCESS AU SOUS-FICHIER ET LIEN AVEC PRIX GRANIT
;
ACCESS *WIG312B                              &
   LINK TGRCODGRA OF WIG312B               , &
        CIECLE    OF WIG312B                 &
     TO TGRCODGRA                          , &
        CIECLE    OF PDTYPE_GRANIT OPTIONAL  &
   LINK CIECLE    OF WIG312B                 &
     TO CIECLE    OF MCCOMPAGNIE   OPTIONAL

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG312"
;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTRE (CIENOM OF MCCOMPAGNIE)

DEFINE D-DESC-GRANIT CHARACTER SIZE 10 = &
                        TRUNCATE(TGRDSCFRA001 OF PDTYPE_GRANIT[1:10])


SORTED ON CIECLE        OF WIG312B &
       ON TGRCODGRA     OF WIG312B





DEFINE D-PRIX-MOYEN FLOAT SIZE 8 = TVAL OF WIG312B / TPYE OF WIG312B


DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("RESUME DE L'INVENTAIRE DES BLOCS                                                ")

DEFINE D-TITRE-CENTRE-1 CHARACTER SIZE 80 = &
        CENTRE ("VALORISATION                                                                    ")


USE ustitstd52 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP &
  TAB 025 D-TITRE-CENTRE-1 &
SKIP 3 &
  TAB 003 "                   ------------------------------ METRE CUBE --------------------------                           "  &
SKIP 1 &
  TAB 003 "TYPE       NOMBRE                                             NET A PAYER   NET UTILISE" &
SKIP 1 &
  TAB 003 "GRANIT     BLOC            BRUT           NET       A PAYER    SANS REFUS    SANS REFUS            PRIX       TOTALE" &
SKIP 1 &
  TAB 003 "------     ------  ------------  ------------  ------------  ------------  ------------  --------------  -----------"  &
SKIP 2

REPORT                                            &
  TAB 003 D-DESC-GRANIT PRINT AT TGRCODGRA                          &
  TAB 014 TCOMPTEUR  OF WIG312B PRINT AT TGRCODGRA PICTURE "^^,^^^"        &
  TAB 022 BRU        OF WIG312B PRINT AT TGRCODGRA OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^"  &
  TAB 036 NET        OF WIG312B PRINT AT TGRCODGRA OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^"  &
  TAB 050 PYE        OF WIG312B PRINT AT TGRCODGRA OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^"  &
  TAB 064 TPYE       OF WIG312B PRINT AT TGRCODGRA OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^"  &
  TAB 078 TNET       OF WIG312B PRINT AT TGRCODGRA OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^"  &
  TAB 091 D-PRIX-MOYEN          PRINT AT TGRCODGRA  PICTURE "^^^,^^^,^^^.^^ " &
  TAB 106 TVAL OF WIG312B       PRINT AT TGRCODGRA  PICTURE " ^,^^^,^^^.^^" FLOAT "$"




FOOTING AT CIECLE &
SKIP 2 &
  TAB 003 "           ------  ------------  ------------  ------------  ------------  ------------                  -----------"  &
SKIP 1 &
  TAB 014 TCOMPTEUR OF WIG312B SUBTOTAL  PICTURE "^^,^^^"       &
  TAB 022 BRU       OF WIG312B SUBTOTAL OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^" &
  TAB 036 NET       OF WIG312B SUBTOTAL OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^" &
  TAB 050 PYE       OF WIG312B SUBTOTAL OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^" &
  TAB 064 TPYE      OF WIG312B SUBTOTAL OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^" &
  TAB 078 TNET      OF WIG312B SUBTOTAL OUTPUT SCALE 4  PICTURE "^^^,^^^.^^^^" &
  TAB 106 TVAL      OF WIG312B SUBTOTAL  PICTURE " ^,^^^,^^^.^^" FLOAT "$" &
SKIP PAGE

SET NOBLANK
BUILD ig312z2
