;/T/ Dimension de ventes
;
;/P/ Programmeur..: René Bonneau
;    Date création: 1994-11-02
;
;    Description..: Ce panorama permet de saisir les dimension ventes
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd203 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD203"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd203.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimun
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;
TEMPORARY T_TYCCODCHA CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Dimension ventes
;

FILE PDDIMEN_VENTES PRIMARY OCCURS 17 NOITEM

  ACCESS VIA     CIECLE,   &
                 DIVEPAVNT &
         USING   T_CIECLEMNU,                &
                 DIVEPAVNT OF PDDIMEN_VENTES &
         REQUEST DIVEPAVNT OF PDDIMEN_VENTES &
         ORDERBY CIECLE ,  &
                 DIVEPAVNT

  ACCESS VIA     CIECLE       &
         USING   T_CIECLEMNU  &
         ORDERBY CIECLE,      &
                 DIVEPAVNT

   ITEM CIECLE OF PDDIMEN_VENTES INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Dimension ventes
;

FILE PDDIMEN_VENTES DESIGNER ALIAS PDDIMEN_VENTES_RECH OCCURS WITH PDDIMEN_VENTES

;  ACCESS VIA     CIECLE,   &
;                 DIVEPAVNT &
;         USING   T_CIECLEMNU, &
;                 DIVEPAVNT OF PDDIMEN_VENTES

   ACCESS VIA CIECLE       &
   USING      T_CIECLEMNU       &
   ORDERBY    CIECLE,         &
              DIVEPAVNT

SELECT &
    IF ((DIVEPAMIN OF PDDIMEN_VENTES >= DIVEPAMIN OF PDDIMEN_VENTES_RECH AND &
         DIVEPAMIN OF PDDIMEN_VENTES <= DIVEPAMAX OF PDDIMEN_VENTES_RECH) OR &
        (DIVEPAMAX OF PDDIMEN_VENTES >= DIVEPAMIN OF PDDIMEN_VENTES_RECH AND &
         DIVEPAMAX OF PDDIMEN_VENTES <= DIVEPAMAX OF PDDIMEN_VENTES_RECH) OR &
        (DIVEPAMIN OF PDDIMEN_VENTES <= DIVEPAMIN OF PDDIMEN_VENTES_RECH AND &
         DIVEPAMAX OF PDDIMEN_VENTES >= DIVEPAMAX OF PDDIMEN_VENTES_RECH))


;------------------------------------------------------------------------------
;
; Varaibles pour le traitement
;

DEFINE D_DIVEPAVNT_P2 INTEGER SIZE 4 = DIVEPAVNT OF PDDIMEN_VENTES_RECH + 2
DEFINE D_DIVEPAVNT_M2 INTEGER SIZE 4 = DIVEPAVNT OF PDDIMEN_VENTES_RECH - 2


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Dimension ventes " &
@ELSE
      " %%%Dimension ventes " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19) (,30,46) (,57,73)


CLUSTER OCCURS WITH PDDIMEN_VENTES ID BASE 05


FIELD DIVEPAVNT    OF PDDIMEN_VENTES        &
        HIDDEN                              &
        REQUIRED                            &
        LOOKUP NOTON PDDIMEN_VENTES         &
          VIA   CIECLE,                     &
                DIVEPAVNT                   &
          USING T_CIECLEMNU,                &
                DIVEPAVNT OF PDDIMEN_VENTES &
          MESSAGE 3020 ; Cette dimension de vente existe déjà



FIELD DIVEPAMIN    OF PDDIMEN_VENTES       &
        HIDDEN                             &
        DISPLAY


FIELD DIVEPAMAX    OF PDDIMEN_VENTES       &
        HIDDEN                             &
        DISPLAY


CLUSTER



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST

END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions de ventes
; N.B.: on ne peut pas avoir une dimension de ventes plus petite que 010 mm.
;        ou plus grande que 4500 mm.
;

PROCEDURE EDIT DIVEPAVNT
BEGIN

  IF DIVEPAVNT  OF PDDIMEN_VENTES < T_DIM_MIN_VENTE OR &
     DIVEPAVNT  OF PDDIMEN_VENTES > T_DIM_MAX_VENTE
  THEN BEGIN
    ERROR 2962 ; Cette dimension hors des dimensions limites
  END

  LET     DIVEPAMIN        OF PDDIMEN_VENTES = &
          DIVEPAVNT        OF PDDIMEN_VENTES - 2
  DISPLAY DIVEPAMIN        OF PDDIMEN_VENTES

  LET     DIVEPAMAX        OF PDDIMEN_VENTES = &
          DIVEPAVNT        OF PDDIMEN_VENTES + 2
  DISPLAY DIVEPAMAX        OF PDDIMEN_VENTES

  GET PDDIMEN_VENTES_RECH  OPTIONAL
  IF ACCESSOK
  THEN ERROR 3021 ; Cette intervale en chevauche une autre

END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDIMEN_VENTES &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDIMEN_VENTES &
     AND NOT NEWRECORD     OF PDDIMEN_VENTES &
     AND NOT DELETEDRECORD OF PDDIMEN_VENTES &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END


PROCEDURE UPDATE
BEGIN
  FOR PDDIMEN_VENTES
  BEGIN
    PUT PDDIMEN_VENTES
  END
  ;
  ; Validation des plages de dimension de ventes
  ;
  WHILE RETRIEVING PDDIMEN_VENTES_RECH
  BEGIN
    IF DIVEPAVNT OF PDDIMEN_VENTES <> DIVEPAVNT OF PDDIMEN_VENTES_RECH
    THEN BEGIN
      IF DIVEPAVNT OF PDDIMEN_VENTES >= D_DIVEPAVNT_P2 AND &
         DIVEPAVNT OF PDDIMEN_VENTES <= D_DIVEPAVNT_M2
      THEN ERROR 3021
    END
  END

END


BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Dimension ventes *******************************
*                                                                              *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
*                                                                              *
********************************************************************************
@ENDIF
