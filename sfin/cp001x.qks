;/T/ Fournisseurs - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 3 septembre 1993
;
;/V3.02.00/ Alain Côté, 3 mars 1994, Immobilisation
;
;    Description..: Cet écran vérifie si le fournisseur reçu en paramètre est
;                   référé dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;                   Il faut vérifie si le record du fournisseur dans la table
;                   MCREFERENCE est référé dans l'historique du Grand Livre
;                   si oui, on ne doit pas détruire le record dans MCREFERENCE
;                   sinon, on détruit MCREFERENCE.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp001x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING CPFOURNISSEUR , &
                        T_FLGDEL      , &
                        T_FLGREF


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.
TEMPORARY T_FLGREF CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Fournisseurs
;
FILE CPFOURNISSEUR    MASTER

;
; Fournisseurs - compagnie.
;
FILE CPFOU_CIE        REFERENCE

;
; Encaissements(Un fournisseur peut nous rembourser un chèque fait en double).
;
FILE CRDEPOT          REFERENCE

;
; Historique au Grand Livre.
;
FILE MCHISTO          REFERENCE

;
; Immobilisation.
;
FILE IMIMMOBILISATION DESIGNER &
                      OPEN READ SHARE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans fournisseur par compagnie.
  ;
  GET CPFOU_CIE VIA FOUCIECLE, &
                    FOUCLE     &
                USING FOUCIECLE OF CPFOURNISSEUR , &
                      FOUCLE    OF CPFOURNISSEUR   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 468 ; Destruction annulée, cette information est référée par un
                 ; fournisseurs-compagnie.

  ;
  ; On vérifie dans les encaissements.
  ;
  GET CRDEPOT VIA REFCIECLE , &
                  REFCLENUM , &
                  REFCLETYP   &
              USING FOUCIECLE OF CPFOURNISSEUR , &
                    FOUCLE    OF CPFOURNISSEUR , &
                    "F"                          &
              OPTIONAL
  IF ACCESSOK
  THEN ERROR 574 ; Destruction annulée, cette information est référée par un
                 ; encaissement.

  ;
  ; Immobilisation.
  ;
  GET IMIMMOBILISATION VIA REFCIECLE , &
                           FOUCLE      &
                       USING FOUCIECLE OF CPFOURNISSEUR , &
                             FOUCLE    OF CPFOURNISSEUR   &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 580 ; Destruction annulée, cette information est référée par une
                 ; immobilisation.

  ;
  ; Immobilisation.
  ;
  GET IMIMMOBILISATION VIA REFCIECLE , &
                           IMMFOUASS   &
                       USING FOUCIECLE OF CPFOURNISSEUR , &
                             FOUCLE    OF CPFOURNISSEUR   &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 580 ; Destruction annulée, cette information est référée par une
                 ; immobilisation.

  ;
  ; On vérifie si le fournisseur est référé dans l'historique du Grand Livre.
  ;
  GET MCHISTO VIA REFCIECLE , &
                  REFCLETYP , &
                  REFCLENUM   &
              USING FOUCIECLE OF CPFOURNISSEUR , &
                    FOUREFTYP OF CPFOURNISSEUR , &
                    FOUCLE    OF CPFOURNISSEUR   &
              OPTIONAL
  IF NOT ACCESSOK
  THEN LET T_FLGREF = "1"

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
@IF FORMAT

@ENDIF
