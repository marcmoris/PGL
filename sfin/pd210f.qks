;/T/ 2.3.1 Enregistrer la commande interne. - Ventilation détail de la commande
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-01
;
;    Description..: Sous-écran pour saisir la ventilation du détail de
;                   la commande interne.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210f ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDCOMMAN_INTERNE,   &
                        PDDETAIL_C_I

TEMPORARY T_CCUPECDEBI_SIE1 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CCUPECDEBA_SIE1 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_PECCLE_SIE1 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle


;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;

USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD210F"



;-------------------------------------------------------------------------------
;
; Panorama d'aide
;

USE pd210f.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_ROUND FLOAT SIZE 8 RESET AT STARTUP
TEMPORARY T-T CHAR SIZE 6
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable servant à l'appel du/des dépisteur(s) (pop-up)
;
TEMPORARY T_DCI          CHAR SIZE 3
TEMPORARY T_CPTCLE       CHARACTER SIZE 06 ; Popup sur les comptes
TEMPORARY T_CPTTYPPAT    CHARACTER SIZE 01 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT    CHARACTER SIZE 08 ; Popup sur les comptes
TEMPORARY T_PECCLE       CHARACTER SIZE 04 ; Popup sur les comptes et unité adm.
TEMPORARY T_UNACLE       CHARACTER SIZE 08 ; Popup unité administrative


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE MASTER

;-------------------------------------------------------------------------------
;
; Détail commande interne
;

FILE PDDETAIL_C_I MASTER
;---------------------------------------------------------------------------
FILE PDDETAIL_C_I  DESIGNER ALIAS PDDETAIL_C_I_REF
ACCESS   VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG        &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                 DCINUMLIGREF     OF PDDETAIL_C_I
;--------------------------------------------------------------------------

FILE PDVENTILATION DESIGNER ALIAS PDVENTILATION_REF
;------------------------------------------------------------------------
FILE PDVENTILATION DESIGNER ALIAS PDVENTILATION_COP
ACCESS   VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG        &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                 DCINUMLIG        OF PDDETAIL_C_I

;-------------------------------------------------------------------------
; Détail de l'imputation
;

FILE PDVENTILATION         PRIMARY &
                           NOITEM &
                           OCCURS 11


  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG        &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                 DCINUMLIG        OF PDDETAIL_C_I      &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG,       &
                 CPTCLE,          &
                 UNACLE


  ITEM CIECLE       OF PDVENTILATION INITIAL T_CIECLEMNU
  ITEM PJTABR       OF PDVENTILATION INITIAL PJTABR       OF PDCOMMAN_INTERNE FIXED
  ITEM PJTANN       OF PDVENTILATION INITIAL PJTANN       OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMSEQ    OF PDVENTILATION INITIAL PJTNUMSEQ    OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOMINT OF PDVENTILATION INITIAL COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM DCINUMLIG    OF PDVENTILATION INITIAL DCINUMLIG    OF PDDETAIL_C_I FIXED
  ITEM COMNUMCOM    OF PDVENTILATION INITIAL PJTABR       OF PDCOMMAN_INTERNE + &
                                             PJTANN       OF PDCOMMAN_INTERNE + &
                                             COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMPRO    OF PDVENTILATION INITIAL PJTABR       OF PDCOMMAN_INTERNE + &
                                             PJTANN       OF PDCOMMAN_INTERNE + &
                                             PJTNUMSEQ    OF PDCOMMAN_INTERNE FIXED


;-------------------------------------------------------------------------------
;
; Compte
;

FILE MCCOMPTE       REFERENCE OCCURS WITH PDVENTILATION

  ACCESS VIA     CPTCLE           &
         USING   CPTCLE           OF PDVENTILATION
;-----------------------------------------------------------------------
;
; Unité administrative
;

FILE MCUNITE_ADM    REFERENCE OCCURS WITH PDVENTILATION

  ACCESS VIA     CIECLE,          &
                 UNACLE           &
         USING   T_CIECLEMNU,     &
                 UNACLE           OF PDVENTILATION


;-------------------------------------------------------------------------------
;
; Notion de charte de compte et pour avoir la devise du G/L.
;

FILE MCHOLDING        REFERENCE

  ACCESS  VIA   CIENMC &
          USING "1"


;-------------------------------------------------------------------------------
;
; Validation de la charte de compte/unité.
;

FILE MCCHARTE_UNA     REFERENCE OCCURS WITH PDVENTILATION

  ACCESS  VIA   CPTCLE, &
                CIECLE, &
                UNACLE  &
          USING CPTCLE OF PDVENTILATION, &
                T_CIECLEMNU            , &
                UNACLE OF PDVENTILATION


;-------------------------------------------------------------------------------
;
; Validation de la charte de compte/unité.
;

FILE MCPERIODE_CIE    REFERENCE

  ACCESS  VIA   CIECLE,          &
                PCCSTUCR         &
          USING T_CIECLEMNU,     &
                "C"


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;
DEFINE    D_ECART_VENT   FLOAT     SIZE 08 = DCIPRIUNMPRD OF PDDETAIL_C_I - &
                                             DCIMNTTOTIMP OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Ventilation comptable " &
@ELSE
      " %%%Ventilation comptable " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


ALIGN (,3,19) (,46,62)


FIELD DCINUMLIG        OF PDDETAIL_C_I     &
LABEL "Numéro ligne..:"&
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD DCINUMLIGREF  OF PDDETAIL_C_I        &
      LABEL "Reference..:"                 &
      HIDDEN

FIELD DCIPRIUNMPRD     OF PDDETAIL_C_I     &
        DISPLAY                            &
        LABEL "Mnt à imputé..:"            &
        FIXED


ALIGN (,3,19) (,46,62)


FIELD DCIMNTTOTIMP     OF PDDETAIL_C_I     &
        DISPLAY                            &
        LABEL "Mnt imputé....:"            &
        FIXED


FIELD D_ECART_VENT                         &
        DISPLAY                            &
        PREDISPLAY                         &
        LABEL "Écart.........:"            &
        PICTURE "^^^,^^^,^^^.^^ "          &
        INPUT SCALE 2                      &
        TRAILING SIGN "-"                  &
        SIGNIFICANCE 5



DRAW 8,2 TO 8,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      "   Compte   Unité      Description                          Prix vent. " &
@ELSE
      "%%%Compte   Unité      Description                          Prix vent. " &
@ENDIF
      AT ,2


SKIP TO LINE 11


ALIGN (04,04,05) (,,14) (,,25) (,,62)


CLUSTER OCCURS WITH PDVENTILATION ID BASE 40


FIELD CPTCLE           OF PDVENTILATION    &
       HIDDEN                              &
       LABEL " "                           &
       REQUIRED                            &
       LOOKUP ON MCCOMPTE                  &
         MESSAGE 3087 ; Ce numéro de compte n'existe pas


FIELD UNACLE           OF PDVENTILATION    &
       HIDDEN                              &
       REQUIRED                            &
       LOOKUP ON MCCOMPTE                  &
         MESSAGE 3088 ; Cette unité administrative n'existe pas


FIELD CPTDSCFRA        OF MCCOMPTE         &
        HIDDEN                             &
        DISPLAY


FIELD VENPRIUNI        OF PDVENTILATION    &
       HIDDEN                              &
       REQUIRED


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END
;--------------------------------------------------------------------------
PROCEDURE EDIT DCINUMLIGREF
BEGIN
  IF 0 <> SIZE(TRUNC(DCINUMLIGREF OF PDDETAIL_C_I))
  THEN BEGIN
  GET PDDETAIL_C_I_REF OPT         &
          VIA     CIECLE,                  &
                  PJTABR,                  &
                  PJTANN,                  &
                  COMNUMCOMINT,            &
                  DCINUMLIG                &
          USING   T_CIECLEMNU,                          &
                  PJTABR           OF PDCOMMAN_INTERNE, &
                  PJTANN           OF PDCOMMAN_INTERNE, &
                  COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                  DCINUMLIGREF     OF PDDETAIL_C_I
   IF NOT ACCESSOK
   THEN ERROR = "LIGNE INCONNUE"
   ELSE BEGIN
    IF DCIPRIVEN OF PDDETAIL_C_I_REF <> DCIPRIVEN OF PDDETAIL_C_I
    THEN ERROR = "LE PRIX NE CORRESPOND PAS"
   END
 END
END
;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ.
;

PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;

PROCEDURE INPUT UNACLE
BEGIN
  ;
  ; Popup sur les unités administrative.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 &
               MODE F &
               PASSING T_CIECLEMNU, &
                       T_UNACLE
  END

  ;Forcer l'edit
  IF FIELDTEXT = " " AND UNACLE OF PDVENTILATION <> " "
  THEN LET FIELDTEXT = UNACLE OF PDVENTILATION
END


;-------------------------------------------------------------------------------
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, soit excluse.
;

PROCEDURE EDIT UNACLE
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 3085 ; La combinaison compte/unité administrative n'existe pas
    END
    ELSE BEGIN
      ;
      ; Si la charte est Exclusive et que la période fait partie de
      ; l'intervalle de période de la charte alors c'est une erreur.
      ;
      ; Si la charte est inclusive et que la période ne fait pas partie de
      ; l'intervalle de période de la charte alors c'est une erreur.
      ;

      ; Ajustement pour le changement de siècle
      IF PECCLE OF MCPERIODE_CIE[1:1] < "8"
      THEN LET T_PECCLE_SIE1 = "1" + PECCLE OF MCPERIODE_CIE
      ELSE LET T_PECCLE_SIE1 = "0" + PECCLE OF MCPERIODE_CIE

      ; Ajustement pour le changement de siècle
      IF CCUPECDEBA OF MCCHARTE_UNA[1:1] < "8"
      THEN LET T_CCUPECDEBA_SIE1 = "1" + CCUPECDEBA OF MCCHARTE_UNA
      ELSE LET T_CCUPECDEBA_SIE1 = "0" + CCUPECDEBA OF MCCHARTE_UNA

      ; Ajustement pour le changement de siècle
      IF CCUPECDEBI OF MCCHARTE_UNA[1:1] < "8"
      THEN LET T_CCUPECDEBI_SIE1 = "1" + CCUPECDEBI OF MCCHARTE_UNA
      ELSE LET T_CCUPECDEBI_SIE1 = "0" + CCUPECDEBI OF MCCHARTE_UNA

      IF ( &
           T_PECCLE_SIE1 >= T_CCUPECDEBA_SIE1 AND &
           ( &
             T_PECCLE_SIE1 < T_CCUPECDEBI_SIE1 OR &
             CCUPECDEBI OF MCCHARTE_UNA = " " &
           ) &
         )
      THEN BEGIN
        IF HLDCHTCPTRLT OF MCHOLDING = "E"
        THEN ERROR 3086 ; La combinaison compte/unité administrative est inactive
      END
      ELSE BEGIN
        IF HLDCHTCPTRLT OF MCHOLDING = "I"
        THEN ERROR 3086 ; La combinaison compte/unité administrative est inactive
      END
    END
  END
END


;-------------------------------------------------------------------------------
;
; Mise à jour du cumulatif de l'imputation.
;

PROCEDURE EDIT VENPRIUNI
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec NOLIST

  LET DCIMNTTOTIMP OF PDDETAIL_C_I = DCIMNTTOTIMP OF PDDETAIL_C_I - &
                                     OLDVALUE(VENPRIUNI OF PDVENTILATION) + &
                                     FIELDVALUE
  DISPLAY DCIMNTTOTIMP OF PDDETAIL_C_I
  DISPLAY D_ECART_VENT
END


;-------------------------------------------------------------------------------
;
; Affile l'écart
;

PROCEDURE PROCESS VENPRIUNI
BEGIN
  DISPLAY VENPRIUNI OF PDVENTILATION
  DISPLAY D_ECART_VENT
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDVENTILATION &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDVENTILATION &
     AND NOT NEWRECORD     OF PDVENTILATION &
     AND NOT DELETEDRECORD OF PDVENTILATION &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ;
  ;
  LET T_ROUND = ROUND(DCIPRIUNMPRD OF PDDETAIL_C_I)
  IF T_ROUND <> DCIMNTTOTIMP OF PDDETAIL_C_I
  THEN BEGIN
    LET DCISTU OF PDDETAIL_C_I  = "0"
  END
  ELSE BEGIN
    LET DCISTU OF PDDETAIL_C_I  = "1"
  END
END
;-----------------------------------------------------------------
PROCEDURE UPDATE
BEGIN
  PUT PDCOMMAN_INTERNE
  IF         NEWRECORD     OF PDVENTILATION &
     AND NOT ALTEREDRECORD OF PDVENTILATION &
     AND NOT DELETEDRECORD OF PDVENTILATION &
     AND (DCINUMLIGREF OF PDDETAIL_C_I <> "")
  THEN BEGIN
  GET PDDETAIL_C_I_REF OPT
  IF ACCESSOK
  THEN LET DCIMNTTOTIMP OF PDDETAIL_C_I = DCIMNTTOTIMP OF PDDETAIL_C_I_REF
  PUT PDDETAIL_C_I

   WHILE RETRIEVING PDVENTILATION_REF &
         VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG        &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                 DCINUMLIGREF    OF PDDETAIL_C_I
     BEGIN
       LET CIECLE       OF PDVENTILATION_COP = T_CIECLEMNU
       LET PJTABR       OF PDVENTILATION_COP = PJTABR       OF PDVENTILATION_REF
       LET PJTANN       OF PDVENTILATION_COP = PJTANN       OF PDVENTILATION_REF
       LET COMNUMCOMINT OF PDVENTILATION_COP = COMNUMCOMINT OF PDVENTILATION_REF
       LET DCINUMLIG    OF PDVENTILATION_COP = DCINUMLIG    OF PDVENTILATION


       LET CPTCLE    OF PDVENTILATION_COP = CPTCLE    OF PDVENTILATION_REF
       LET UNACLE    OF PDVENTILATION_COP = UNACLE    OF PDVENTILATION_REF
       LET VENPRIUNI OF PDVENTILATION_COP = VENPRIUNI OF PDVENTILATION_REF
       PUT PDVENTILATION_COP RESET
     END
    LET T_ROUND = ROUND(DCIPRIUNMPRD OF PDDETAIL_C_I)
    IF T_ROUND <> DCIMNTTOTIMP OF PDDETAIL_C_I
    THEN BEGIN
      LET DCISTU OF PDDETAIL_C_I  = "0"
    END
    ELSE BEGIN
      LET DCISTU OF PDDETAIL_C_I  = "1"
    END

  END
  ELSE BEGIN
  PUT PDDETAIL_C_I
   FOR PDVENTILATION
    BEGIN
     PUT PDVENTILATION
    END
  END

END
;-------------------------------------------------------------------------------
;
; Mise à jour du solde imputer
;

PROCEDURE DELETE
BEGIN
  LET DCIMNTTOTIMP OF PDDETAIL_C_I = DCIMNTTOTIMP OF PDDETAIL_C_I - &
                                     VENPRIUNI OF PDVENTILATION
  DISPLAY DCIMNTTOTIMP     OF PDDETAIL_C_I
  DISPLAY D_ECART_VENT
  DELETE PDVENTILATION
END


;----------------------------------------------------------------------
;
PROCEDURE APPEND
BEGIN
  ACCEPT CPTCLE OF PDVENTILATION
  ACCEPT UNACLE OF PDVENTILATION
  DISPLAY CPTDSCFRA OF MCCOMPTE
  ACCEPT VENPRIUNI OF PDVENTILATION
END

PROCEDURE ENTRY
BEGIN
  ACCEPT DCINUMLIGREF OF PDDETAIL_C_I
  DISPLAY D_ECART_VENT

  IF DCINUMLIGREF OF PDDETAIL_C_I = " "
  THEN BEGIN
    FOR PDVENTILATION
    BEGIN
      PERFORM APPEND
    END
  END
END


BUILD


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Ventilation comptable *****************************
* Num. commande.: xx-xx-xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Numéro ligne..: xxx                        NUMERO REF:XXXXXX                 *
* Mnt à imputé..: xxxxxxxxxxxxxxx                                              *
* Mnt imputé....: xxxxxxxxxxxxxxx            Écart.........: xxxxxxxxxxxxxxx   *
********************************************************************************
*                                                                              *
*   Compte   Unité      Description                          Prix vent.        *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*   xxxxxx   xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx   *
*                                                                              *
********************************************************************************
@ENDIF
