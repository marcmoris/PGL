;/T/ Immobilisation - Cédules d'entretien
;
;/P/ Programmeur..: Alain Côté
;    Date création: 22 février 1994
;
;/V3.02.00/ Alain Côté, 22 février 1994 , Immobilisation
;
;    Description..: Cet écran permet la consultation et la saisie
;                   d'une cédule d'entretien pour les biens
;                   répertoriés dans les immobilisations.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001d FROM 7,1 TO 23,80 &
              MODE LABEL "" AT 1,80 NOACTION &
              FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING IMIMMOBILISATION , &
                        T_CIECLEMNU      , &
                        T_CIENOM

;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "IM001D"

;
; Documentation de l'écran.
;
USE im001d.hlp NOLIST

USE ustrasse NOLIST       ;Transaction QUERY pour les sous-écran

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Numéro de compagnie du menu.

;-------------------------------------------------------------------------------
;

FILE IMIMMOBILISATION MASTER

FILE IMIMM_CEDULE     PRIMARY &
                      OCCURS 12 &
                      NOITEM
  ACCESS VIA CIECLE , &
             IMMCLE   &
         USING CIECLE OF IMIMM_CEDULE , &
               IMMCLE OF IMIMM_CEDULE   &
         ORDERBY CIECLE , &
                 IMMCLE , &
                 CEDDATPRE

  ITEM CIECLE OF IMIMM_CEDULE INITIAL CIECLE OF IMIMMOBILISATION
  ITEM IMMCLE OF IMIMM_CEDULE INITIAL IMMCLE OF IMIMMOBILISATION


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 01,01 TO 01,79
DRAW 01,01 TO 17,01
DRAW 17,01 TO 17,80
DRAW 02,80 TO 17,80

TITLE &
@IF FRANCAIS
      " Cédule d'entretien " &
@ELSE
      " %%%Cédule d'entretien " &
@ENDIF
      AT ,1 CENTERED

SKIP 1


TITLE &
@IF FRANCAIS
      "  Date        Date" &
@ELSE
      "%%%Date       Date" &
@ENDIF
      AT ,5

SKIP

TITLE &
@IF FRANCAIS
      " Prévue      Réelle      Commentaire " &
@ELSE
      "%%%Prévue     Réelle      Commentaire " &
@ENDIF
      AT ,5

SKIP

ALIGN (5,4,5) (,,17) (,,29)

CLUSTER OCCURS WITH IMIMM_CEDULE ID BASE 05

FIELD CEDDATPRE OF IMIMM_CEDULE   HIDDEN  FORMAT YYYYMMDD PATTERN "####/##/##" LABEL " " &
        REQUIRED &
        LOOKUP NOTON IMIMM_CEDULE &
          VIA CIECLE , &
              IMMCLE , &
              CEDDATPRE &
          USING CIECLE    OF IMIMM_CEDULE , &
                IMMCLE    OF IMIMM_CEDULE , &
                CEDDATPRE OF IMIMM_CEDULE &
          MESSAGE 1611 ; Cette cédule entretien existe déjà.

FIELD CEDDATREE OF IMIMM_CEDULE  FORMAT YYYYMMDD PATTERN "####/##/##"&
        NOENTRY

FIELD CEDCOM    OF IMIMM_CEDULE

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT CEDDATPRE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR IMIMM_CEDULE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF IMIMM_CEDULE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF IMIMM_CEDULE &
       AND NOT NEWRECORD     OF IMIMM_CEDULE &
       AND NOT DELETEDRECORD OF IMIMM_CEDULE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Le timbre de modification est mis à jour si l'usager modifie
    ; la table IMIMM_CEDULE.
    ;
    IF        NEWRECORD OF IMIMM_CEDULE &
       OR ALTEREDRECORD OF IMIMM_CEDULE &
       OR DELETEDRECORD OF IMIMM_CEDULE
    THEN BEGIN
      LET IMMDATMOD OF IMIMMOBILISATION = SYSDATE
      LET IMMHREMOD OF IMIMMOBILISATION = SYSTIME / 10000
      LET IMMUSRMOD OF IMIMMOBILISATION = LOGONID
    END
  END
END

BUILD
@IF FORMAT

****************************** Cédule d'entretien *****************************x
*                                                                              *
*     Date        Date                                                         *
*    Prévue      Réelle      Commentaire                                       *
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
*   xxxxxxxx    xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
********************************************************************************
@ENDIF
