;/T/ Registre des chèques par date
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 10 février 1994
;
;    Description...: Ce programme permet de faire une liste des chèques
;                    par compte d'encaisse et intervalle de dates afin
;                    de connaître les montants des chèques ainsi que le
;                    bénéficiaire.
;
;    Paramètres....: Compagnie
;                    Compte d'encaisse (Un, plusieurs ou tous)
;                    Intervalle de date (Borne début, borne fin)
;
;    Particularités:Aucune
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *WCP351A &
  LINK LBQCPTENC OF WCP351A &
    TO CPTCLE    OF VCPT_DSC OPTIONAL &

  LINK REFCIECLE OF WCP351A, &
       REFCLETYP OF WCP351A, &
       REFCLENUM OF WCP351A, &
       RADCLE    OF WCP351A  &
    TO REFCIECLE           , &
       REFCLETYP           , &
       REFCLENUM           , &
       RADCLE    OF MCREF_ADRESSE OPTIONAL

SORT ON LBQCPTENC &
     ON PAMCLE


DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Registre des chèques par date"
@ELSE
       "%%%"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
  "du "+ ASCII(D_DATDEB)[3:2] + &
  "/"  + ASCII(D_DATDEB)[5:2] + &
  "/"  + ASCII(D_DATDEB)[7:2] + &
  " au "  + ASCII(D_DATFIN)[3:2] + &
  "/"     + ASCII(D_DATFIN)[5:2] + &
  "/"     + ASCII(D_DATFIN)[7:2]


DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "CP351"
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


USE ustitstd NOLIST
  SKIP 2 &
TAB 001 "Compte d'encaisse :" &
TAB 021 LBQCPTENC OF WCP351A  &
TAB 028 CPTDSC    OF VCPT_DSC &
  SKIP 1 &
TAB 001 "Devise            :" &
TAB 021 DEVCLE    OF VCPT_DSC &
  SKIP 2 &
TAB 001 "Numéro       Numéro            Date " &
  SKIP 1 &
TAB 001 "chèque       pré-imprimé       chèque        Bénéficiaire " &
TAB 091 "Montant chèque " &
  SKIP 1 &
TAB 001 "-------      -----------       --------      ---------------------------------------- " &
TAB 091 "---------------" &
  SKIP 2


REPORT &
TAB 001 PAMCLE     OF WCP351A &
TAB 015 PAMNUMPIM  OF WCP351A &
TAB 032 PAMDAT     OF WCP351A &
TAB 046 RADNOM     OF MCREF_ADRESSE &
TAB 091 PAMMNT     OF WCP351A  &
   PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 5  &
SKIP 1

FOOTING AT LBQCPTENC &
  SKIP 2 &
TAB 091 "Montant chèque" &
  SKIP 1 &
TAB 091 "---------------" &
  SKIP 1 &
TAB 047 "Total pour le compte d'encaisse" &
TAB 079 LBQCPTENC OF WCP351A &
TAB 091 PAMMNT OF WCP351A SUBTOTAL &
   PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 5 &
  SKIP 1 &
TAB 091 "---------------"

FINAL FOOTING &
  SKIP 2 &
TAB 091 "Montant chèque" &
  SKIP 1 &
TAB 091 "---------------" &
  SKIP 1 &
TAB 047 "Grand total" &
TAB 091 PAMMNT OF WCP351A SUBTOTAL &
   PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 5 &
  SKIP 1 &
TAB 091 "---------------"

BUILD cp351z

;        1         2         3        4         5         6         7          8         9        10        11        12        13
;23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
@IF FORMAT
CP351                                                                      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                         Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                                                    Registre des chèques par date
                                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Compte d'encaisse : xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Devise            : xxx

Numéro       Numéro            Date
chèque       pré-imprimé       chèque        Bénéficiaire                                 Montant chèque
-------      -----------       --------      ----------------------------------------     ---------------
xxxxxxx       xxxxxxxx         99/99/99      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     999,999,999.99-


                                                                                          Montant chèque
                                                                                          ---------------
                                              Total pour le compte d'encaisse  xxxxxx     999,999,999.99-
                                                                                          ---------------


                                                                                          Montant chèque
                                                                                          ---------------
                                              Grand total                                 999,999,999.99-
                                                                                          ---------------


@ENDIF

