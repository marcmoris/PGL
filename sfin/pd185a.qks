;/T/ 2.4.3 Enregistrer les opérations de surface (OPÉRATEUR)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-28
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les opérateurs pour une opération de surface.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd185a ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_OPER_SURF IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD185A"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd185a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

TEMPORARY T_OOSHREDEB INT SIZE 2
TEMPORARY T_OOSHREFIN INT SIZE 2
;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_QRTCODQRT    CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT    CHARACTER SIZE 04
DEFINE    D_OSUSTU       CHARACTER SIZE 16 = "OSUSTU"
TEMPORARY T_OSUSTU       CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calcul d'un intervalle de temps
;

USE usdiftim NOLIST

;-------------------------------------------------------------------------------
;
; Variables nécessaire à l'appel des dépisteur (POP_UP)
;

TEMPORARY T_EMPLOYE_NUM CHARACTER SIZE 06


;-------------------------------------------------------------------------------
;
; Opération de surface
;

FILE PDOPERATION_SURF  MASTER


;-------------------------------------------------------------------------------
;
; Opérateurs opération de surface
;

FILE PDOPERATEURS_O_S PRIMARY &
                      NOITEM &
                      OCCURS 8 TIMES

  ACCESS VIA     CIECLE,          &
                 OSUNUMRAP        &
         USING   T_CIECLEMNU,                          &
                 OSUNUMRAP        OF PDOPERATION_SURF  &
         ORDERBY CIECLE,          &
                 OSUNUMRAP

  ITEM CIECLE    OF PDOPERATEURS_O_S INITIAL T_CIECLEMNU
  ITEM OSUNUMRAP OF PDOPERATEURS_O_S INITIAL OSUNUMRAP OF PDOPERATION_SURF FIXED
  ITEM OSUDATPRD OF PDOPERATEURS_O_S INITIAL OSUDATPRD OF PDOPERATION_SURF FIXED
  ITEM OSUDATPRD OF PDOPERATEURS_O_S INITIAL OSUDATPRD OF PDOPERATION_SURF FIXED
  ITEM OOSDURSEC OF PDOPERATEURS_O_S SUM INTO OSUDURSECOPE OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les opération de surface
;

FILE PDSEQ_OPER_SURF DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Opérateur de fini de surface validation des plages horraire pour le rapport
;

FILE PDOPERATEURS_O_S DESIGNER ALIAS OOS_VALIDE

  ACCESS VIA     CIECLE,          &
                 OSUNUMRAP,       &
                 OSUDATPRD,       &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,                          &
                 OSUNUMRAP        OF PDOPERATION_SURF, &
                 OSUDATPRD        OF PDOPERATION_SURF, &
                 OPENUMEMP        OF PDOPERATEURS_O_S

  ;
  ; Sélectionne si une des deux bornes du nouvel enregistrement se retrouve
  ; parmis l'intervale déjà enregistré.
  ;




;-------------------------------------------------------------------------------
;
; Opérateurs
;

FILE PDOPERATEUR     REFERENCE OCCURS WITH PDOPERATEURS_O_S

  ACCESS VIA     CIECLE,          &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,     &
                 OPENUMEMP        OF PDOPERATEURS_O_S


;-------------------------------------------------------------------------------
;
; Machine opération de surface
;

FILE PDMACH_OP_SURF  REFERENCE

  ACCESS VIA     CIECLE,          &
                 MOPCODMAC        &
         USING   T_CIECLEMNU,     &
                 MOPCODMAC        OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_OSUSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_OSUSTU,                             &
               OSUSTU              OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Concaténation du prénom et du nom pour affichage dans le panorama
;

DEFINE D_NOM CHARACTER SIZE 40 = TRUNCATE (OPEPNM           OF PDOPERATEUR) + " " + &
                                 TRUNCATE (OPENOM           OF PDOPERATEUR)


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Opération de surface " &
@ELSE
      " %%%Opération de surface " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19) (,42,58)


FIELD OSUNUMRAP        OF PDOPERATION_SURF &
LABEL "Numéro rapport:"&
        FIXED


FIELD OSUDEP           OF PDOPERATION_SURF &
LABEL "Heure départ..:"&
        FIXED


FIELD OSUDATPRD        OF PDOPERATION_SURF  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
LABEL "Date oper surf:"&
        FIXED


FIELD OSUARR           OF PDOPERATION_SURF &
LABEL "Heure arrêt...:"&
        FIXED


ALIGN (,3,19) (,,24)


FIELD MOPCODMAC        OF PDOPERATION_SURF &
LABEL "Code machine..:"&
        FIXED


FIELD MOPDSC001        OF PDMACH_OP_SURF   &
        FIXED


FIELD QRTCODQRT        OF PDOPERATION_SURF &
LABEL "Quart travail.:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY                            &
        FIXED


FIELD OSUSTU           OF PDOPERATION_SURF &
LABEL "Statut........:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_OSUSTU     &
        DISPLAY                            &
        FIXED


DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Opérateurs " &
@ELSE
      " %%%Opérateurs " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
    "No         Nom                                       Début      Fin" &
@ELSE
    "%%%No         Nom                                       Début      Fin" &
@ENDIF
     AT ,4


SKIP TO LINE 14


ALIGN (03,03,04) (,,15) (,,57) (,,68)


CLUSTER OCCURS WITH PDOPERATEURS_O_S ID BASE 40


FIELD OPENUMEMP        OF PDOPERATEURS_O_S &
        HIDDEN                             &
        LABEL " "                          &
        NUMERIC                            &
        REQUIRED NOCHANGE                  &
        LOOKUP ON PDOPERATEUR              &
          MESSAGE 2913 ; Ce numéro d'employé n'existe pas


FIELD D_NOM                                &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY


FIELD OOSHREDEB        OF PDOPERATEURS_O_S &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                          &
        REQUIRED


FIELD OOSHREFIN        OF PDOPERATEURS_O_S &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                          &
        REQUIRED



CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs
;

PROCEDURE INPUT OPENUMEMP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EMPLOYE_NUM   = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd110 MODE F &
                     PASSING T_EMPLOYE_NUM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_EMPLOYE_NUM  )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'heure de début est compris dans l'intervalle
;

PROCEDURE EDIT OOSHREDEB
BEGIN
  IF OOSHREDEB OF PDOPERATEURS_O_S < OSUDEP OF PDOPERATION_SURF &
     OR                                                         &
     OOSHREDEB OF PDOPERATEURS_O_S > OSUARR OF PDOPERATION_SURF
  THEN BEGIN
    ERROR 3010 ; L'heure de début n'est pas compris dans l'intervalle du rapport
  END
  ;
  ; Valide parmis les intervaledéja enregistrer
  ; Fait le test seulement si l'heure de fin a déjà été saisie
  ;
;  IF OOSHREFIN OF PDOPERATEURS_O_S <> 0
;  THEN BEGIN
;    GET OOS_VALIDE OPTIONAL
;    IF ACCESSOK
;    THEN BEGIN
;      ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
;    END
;  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'heure de fin est compris dans l'intervalle
;

PROCEDURE EDIT OOSHREFIN
BEGIN
  IF OOSHREFIN OF PDOPERATEURS_O_S < OSUDEP OF PDOPERATION_SURF &
     OR                                                         &
     OOSHREFIN OF PDOPERATEURS_O_S > OSUARR OF PDOPERATION_SURF
  THEN BEGIN
    ERROR 3011 ; L'heure de début n'est pas compris dans l'intervalle du rapport
  END
  ;
  ; Valide parmis les intervaledéja enregistrer
  ;
;  GET OOS_VALIDE OPTIONAL
;  IF ACCESSOK
;  THEN BEGIN
;    ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
;  END
END


PROCEDURE INTERNAL VALIDE
BEGIN
  WHILE RETRIEVING OOS_VALIDE
  BEGIN
    IF (OOSHREDEB OF PDOPERATEURS_O_S <> OOSHREDEB OF OOS_VALIDE   &
        AND                                                        &
        OOSHREFIN OF PDOPERATEURS_O_S <> OOSHREFIN OF OOS_VALIDE )
    THEN BEGIN
      IF ( (OOSHREDEB OF PDOPERATEURS_O_S >= OOSHREDEB OF OOS_VALIDE     &
            AND                                                          &
            OOSHREDEB OF PDOPERATEURS_O_S <= OOSHREFIN OF OOS_VALIDE )   &
          OR                                                             &
           (OOSHREFIN OF PDOPERATEURS_O_S >= OOSHREDEB OF OOS_VALIDE     &
            AND                                                          &
            OOSHREFIN OF PDOPERATEURS_O_S <= OOSHREFIN OF OOS_VALIDE ))  &
         OR                                                              &
           (OOSHREDEB OF PDOPERATEURS_O_S <= OOSHREDEB OF OOS_VALIDE     &
            AND                                                          &
            OOSHREFIN OF PDOPERATEURS_O_S >= OOSHREFIN OF OOS_VALIDE )
      THEN BEGIN
        ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
       END
    END
  END
END

PROCEDURE INTERNAL VALIDE1
BEGIN
  LET T_OOSHREDEB = OOSHREDEB OF PDOPERATEURS_O_S
  LET T_OOSHREFIN = OOSHREFIN OF PDOPERATEURS_O_S
  FOR PDOPERATEURS_O_S
  BEGIN
    IF (OOSHREDEB OF PDOPERATEURS_O_S <> T_OOSHREDEB &
        AND                                                        &
        OOSHREFIN OF PDOPERATEURS_O_S <> T_OOSHREFIN )
    THEN BEGIN
      IF ( (OOSHREDEB OF PDOPERATEURS_O_S >= T_OOSHREDEB &
            AND                                                          &
            OOSHREDEB OF PDOPERATEURS_O_S <= T_OOSHREFIN )   &
          OR                                                             &
           (OOSHREFIN OF PDOPERATEURS_O_S >= T_OOSHREDEB &
            AND                                                          &
            OOSHREFIN OF PDOPERATEURS_O_S <= T_OOSHREFIN ))  &
         OR                                                              &
           (OOSHREDEB OF PDOPERATEURS_O_S <= T_OOSHREDEB &
            AND                                                          &
            OOSHREFIN OF PDOPERATEURS_O_S >= T_OOSHREFIN )
      THEN BEGIN
        ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
       END
    END
  END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ;
  ;
  FOR PDOPERATEURS_O_S
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDOPERATEURS_O_S &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDOPERATEURS_O_S &
       AND NOT NEWRECORD     OF PDOPERATEURS_O_S &
       AND NOT DELETEDRECORD OF PDOPERATEURS_O_S &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Calcul de la durée du traitement pour l'opération de surface
    ;
    LET TZ_DATDEB   = OSUDATPRD        OF PDOPERATEURS_O_S
    LET TZ_TMPDEB   = ASCII(OOSHREDEB  OF PDOPERATEURS_O_S,4) + "00"
    LET TZ_DATFIN   = OSUDATPRD        OF PDOPERATEURS_O_S
    LET TZ_TMPFIN   = ASCII(OOSHREFIN  OF PDOPERATEURS_O_S,4) + "00"

    LET OOSDURSEC OF PDOPERATEURS_O_S = DZ_DIFSECTOT

  ;
  ; Validation des plages horraire pour les opérateurs
  ;
;  DO INTERNAL VALIDE
;  DO INTERNAL VALIDE1

    IF ALTEREDRECORD OF PDOPERATEURS_O_S
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF OSUSTU OF PDOPERATION_SURF = "V"
      THEN BEGIN
        LET OSUSTU OF PDOPERATION_SURF = "E" ; En cours , il faut reprendre la validation
        DISPLAY OSUSTU OF PDOPERATION_SURF
        DISPLAY CBIDSC OF A_CBI_OSUSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF OSUSTU OF PDOPERATION_SURF = "T"
      THEN BEGIN
        LET OSUSTU OF PDOPERATION_SURF = "C"  ; Correction après transfert
        DISPLAY OSUSTU OF PDOPERATION_SURF
        DISPLAY CBIDSC OF A_CBI_OSUSTU
      END
    END

    IF NEWRECORD OF PDOPERATEURS_O_S
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF OSUSTU OF PDOPERATION_SURF = "V"
      THEN BEGIN
        LET OSUSTU OF PDOPERATION_SURF = "E" ; En cours , il faut reprendre la validation
        DISPLAY OSUSTU OF PDOPERATION_SURF
        DISPLAY CBIDSC OF A_CBI_OSUSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF OSUSTU OF PDOPERATION_SURF = "T"
      THEN BEGIN
        LET OSUSTU OF PDOPERATION_SURF = "C"  ; Correction après transfert
        DISPLAY OSUSTU OF PDOPERATION_SURF
        DISPLAY CBIDSC OF A_CBI_OSUSTU
      END
    END

  END
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = OSUNUMRAP        OF PDOPERATION_SURF
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_OPER_SURF OPTIONAL        &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_OPER_SURF = T_CIECLEMNU
    END
    LET SEONUMRAP     OF PDSEQ_OPER_SURF  = SEONUMRAP OF PDSEQ_OPER_SURF + 1
    LET OSUNUMRAP     OF PDOPERATION_SURF = SEONUMRAP OF PDSEQ_OPER_SURF
    PUT PDSEQ_OPER_SURF
    DISPLAY OSUNUMRAP     OF PDOPERATION_SURF
  END
END

PROCEDURE UPDATE
BEGIN
  PUT PDOPERATION_SURF
  FOR PDOPERATEURS_O_S
  BEGIN
  PUT PDOPERATEURS_O_S
  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Opération de surface *****************************
*                                                                              *
* Numéro rapport: xxxxxxxxxxx            Heure départ..: xxxxx                 *
* Date oper surf: xxxxxxxx               Heure arrêt...: xxxxx                 *
* Code machine..: xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Quart travail.: x    xxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                              *
********************************************************************************
*                                                                              *
*  No.        Nom                                         Début    Fin         *
*                                                                              *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*  xxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx       *
*                                                                              *
********************************************************************************
@ENDIF
