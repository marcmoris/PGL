;/T/ Achats par region et par fournisseur
;
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;
;    Description...: Pour consulter les achats par region et par fournisseur
;                    et cela par année financière.
;
;    Paramètres....: Année (une seule)
;                    Catégorie (1, plusieurs,tous)
;
;
;------------------------------------------------------------------------------

USE ussetqzs NOLIST

;
;
; Dans la première étape on demande les catégories en paramètre, c'est pour
; cette raison que l'on accède la table fournisseur en premier.
;
;
ACCESS CPFOURNISSEUR link (fouciecle,foureftyp,foucle, 1) &
                      to  refciecle,refcletyp,refclenum,radcle of mcref_adresse  opt &
                     LINK RADREG OF MCREF_ADRESSE TO REGCLE OF CPREGION opt

CHOOSE FOUCIECLE PARM
;       FOUCAT    PARM     ; Catégorie

DEFINE D_REGION CHARACTER SIZE 3 = PARM ; REGION
DEFINE D_ANNFIN CHARACTER SIZE 2 = PARM ; Annee financiere
DEFINE D_CIECLE CHARACTER SIZE 4 = PARM ; Compagnie du menu

SELECT IF RADREG = D_REGION OR  D_REGION = " "

SET SUBFILE NAME WCP348

REPORT SUMMARY &
  FOUCIECLE OF CPFOURNISSEUR &
  FOUCLE    OF CPFOURNISSEUR &
  FOUNOM    OF CPFOURNISSEUR &
  FOUCAT    OF CPFOURNISSEUR &
  radreg of mcref_adresse    &
  REGDSCABR of CPREGION      &
  D_CIECLE                   &
  D_ANNFIN

BUILD cp348za


;-------------------------------------------------------------------------------
;
;
; Lecture des statistiques selon la compagnie et l'année financière reçue
; de l'étape précédente.
;
;
;
ACCESS *WCP348 &
  ;
  ; Statistique.
  ;
  LINK FOUCIECLE OF WCP348  , &
       FOUCLE    OF WCP348  , &
       D_CIECLE  OF WCP348  , &
       D_ANNFIN  OF WCP348    &
    TO FOUCIECLE            , &
       FOUCLE               , &
       FOCCIECLE            , &
       FOSANN                   OF CPFOU_STAT &
  ;
  ; Catégorie, pour avoir la description.
  ;
  LINK ( "FOUCAT" , &
          FOUCAT OF WCP348 ) &
    TO XELNOM , &
       CBICLE OF     VCBI_DSC OPTIONAL



;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
             "  Achats par region et par fournisseur "
@ELSE
             "  %%%Achats par region et par fournisseur "
@ENDIF



DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP348"

;
; Compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; On fait le total annuel des statistiques pour l'impression.
;
DEFINE D_FOSCUMACH FLOAT SIZE 8 = FOSCUMACH01 OF CPFOU_STAT + &
                                  FOSCUMACH02 OF CPFOU_STAT + &
                                  FOSCUMACH03 OF CPFOU_STAT + &
                                  FOSCUMACH04 OF CPFOU_STAT + &
                                  FOSCUMACH05 OF CPFOU_STAT + &
                                  FOSCUMACH06 OF CPFOU_STAT + &
                                  FOSCUMACH07 OF CPFOU_STAT + &
                                  FOSCUMACH08 OF CPFOU_STAT + &
                                  FOSCUMACH09 OF CPFOU_STAT + &
                                  FOSCUMACH10 OF CPFOU_STAT + &
                                  FOSCUMACH11 OF CPFOU_STAT + &
                                  FOSCUMACH12 OF CPFOU_STAT + &
                                  FOSCUMACH13 OF CPFOU_STAT
;
DEFINE D_FOSCUMESC FLOAT SIZE 8 = FOSCUMESC01 OF CPFOU_STAT + &
                                  FOSCUMESC02 OF CPFOU_STAT + &
                                  FOSCUMESC03 OF CPFOU_STAT + &
                                  FOSCUMESC04 OF CPFOU_STAT + &
                                  FOSCUMESC05 OF CPFOU_STAT + &
                                  FOSCUMESC06 OF CPFOU_STAT + &
                                  FOSCUMESC07 OF CPFOU_STAT + &
                                  FOSCUMESC08 OF CPFOU_STAT + &
                                  FOSCUMESC09 OF CPFOU_STAT + &
                                  FOSCUMESC10 OF CPFOU_STAT + &
                                  FOSCUMESC11 OF CPFOU_STAT + &
                                  FOSCUMESC12 OF CPFOU_STAT + &
                                  FOSCUMESC13 OF CPFOU_STAT
;
DEFINE D_FOSNBRJRSPAM FLOAT SIZE 8 = FOSNBRJRSPAM01 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM02 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM03 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM04 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM05 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM06 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM07 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM08 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM09 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM10 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM11 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM12 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM13 OF CPFOU_STAT
;
DEFINE D_FOSNBRPAM FLOAT SIZE 8 = FOSNBRPAM01 OF CPFOU_STAT + &
                                  FOSNBRPAM02 OF CPFOU_STAT + &
                                  FOSNBRPAM03 OF CPFOU_STAT + &
                                  FOSNBRPAM04 OF CPFOU_STAT + &
                                  FOSNBRPAM05 OF CPFOU_STAT + &
                                  FOSNBRPAM06 OF CPFOU_STAT + &
                                  FOSNBRPAM07 OF CPFOU_STAT + &
                                  FOSNBRPAM08 OF CPFOU_STAT + &
                                  FOSNBRPAM09 OF CPFOU_STAT + &
                                  FOSNBRPAM10 OF CPFOU_STAT + &
                                  FOSNBRPAM11 OF CPFOU_STAT + &
                                  FOSNBRPAM12 OF CPFOU_STAT + &
                                  FOSNBRPAM13 OF CPFOU_STAT


; Ajustement pour le changement de siècle
DEFINE D_FOSANN_SIE CHARACTER SIZE 3 = "1" + FOSANN OF CPFOU_STAT[1:2] &
         IF FOSANN OF CPFOU_STAT[1:1] < "8" &
         ELSE "0" + FOSANN OF CPFOU_STAT[1:2]

SORT ON D_FOSANN_SIE         & ; Année financière.
     ON radreg OF WCP348     & ; region
     ON FOUCAT OF WCP348     & ; categorie
     ON FOUCLE OF WCP348       ; Numéro de fournisseur


;
; Impression de l'en-tête.
;
USE ustit220 NOLIST
  SKIP 2 &
@IF FRANCAIS
    TAB 001 "Annee financiere:" &
@ELSE
    TAB 001 "Fiscal Year     :" &
@ENDIF
    TAB 019 FOSANN OF CPFOU_STAT &
  SKIP 2 &
    TAB 001 &
@IF FRANCAIS
            "                                                                                                                                                                    Montant          Delai moyen" &
@ELSE
            "%%%                                                                                                       Montant      Delai moyen" &
@ENDIF
  SKIP &
    TAB 001 &
@IF FRANCAIS
            "Region          Description                                 Catégorie                                     Fournisseur                            Achats            escompte           paiement" &
@ELSE
            "%%%        Categorie                                                      Fournisseur                         Achats            escompte       paiement" &
@ENDIF
  SKIP &
    TAB 001 &
            "------- ------------------------------     ---------------------------------------------    ------------------------------------------       ---------------     -------------      ------------"



HEADING AT radreg &
  SKIP PAGE


REPORT &
    TAB 002 RADREG OF WCP348     PRINT AT radreg &
    TAB 009 REGDSCABR OF WCP348     PRINT AT RADREG &
    TAB 050 CBIDSC OF VCBI_DSC     PRINT AT FOUCAT &
    TAB 093 FOUCLE OF WCP348                   &
    TAB 100 FOUNOM OF WCP348                   &
    TAB 140 D_FOSCUMACH                        &
            PICTURE "^^^,^^^,^^^.^^ "          &
            TRAILING SIGN "-"                  &
            SIGNIFICANCE 5                     &
    TAB 158 D_FOSCUMESC                        &
            PICTURE "^^^,^^^,^^^.^^ "          &
            TRAILING SIGN "-"                  &
            SIGNIFICANCE 5                     &
    TAB 183 D_FOSNBRJRSPAM RATIO D_FOSNBRPAM   &
            PICTURE "^,^^^ "                   &
            TRAILING SIGN "-"                  &
            OUTPUT SCALE 0                     &
            SIGNIFICANCE 2



FOOTING AT radreg &
  SKIP 2 &
    TAB 142 "---------------" &
    TAB 162 "-------------" &
    TAB 181 "------------"          &
  SKIP 1 &
@IF FRANCAIS
    TAB 120 "Total region:" &
    tab 134 radreg &
@ELSE
    TAB 134 radreg &
@ENDIF
    TAB 140 D_FOSCUMACH    SUBTOTAL     &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 158 D_FOSCUMESC    SUBTOTAL     &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 183 D_FOSNBRJRSPAM RATIO D_FOSNBRPAM &
            PICTURE "^,^^^ "            &
            TRAILING SIGN "-"           &
            OUTPUT SCALE 0              &
            SIGNIFICANCE 2              &
 SKIP 2


FOOTING AT D_FOSANN_SIE             &
  SKIP 2                      &
    TAB 142 "---------------" &
    TAB 162 "-------------" &
    TAB 181 "------------"          &
  SKIP 1                      &
    TAB 120 "Grand total    :"          &
    TAB 140 D_FOSCUMACH  SUBTOTAL       &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 158 D_FOSCUMESC  SUBTOTAL       &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 183 D_FOSNBRJRSPAM RATIO D_FOSNBRPAM &
            PICTURE "^,^^^ "            &
            TRAILING SIGN "-"           &
            OUTPUT SCALE 0              &
            SIGNIFICANCE 2              &
  SKIP 1 &
    TAB 142 "===============" &
    TAB 162 "===============" &
    TAB 181 "============"


BUILD cp348zb

@IF FORMAT

CP348                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                   Achats par catégorie et par fournisseur
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Année financière: 99

                                                                                                          Montant      Delai moyen
           Catégorie                                Fournisseur                         Achats            escompte       paiement
 ------------------------------   -----------------------------------------------   ---------------   ---------------  ------------
 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

                                                                                    ---------------   ---------------     ------
                                                     Grand total    :               999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ===============   ===============     ======
@ENDIF
