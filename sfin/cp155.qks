;/T/ Gestion des employés
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 14 Juin 1993
;
;    Description..: Saisie et maintenance de référence d'employés pour
;                   les rapports de dépenses.
;
;/M/ description..: Ecran en mode find..
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp155 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND, CHANGE &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_EMPCIECLE

USE ussectmp NOLIST
    "CP155"

USE cp155.hlp

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.
TEMPORARY T_EMPCIECLE CHARACTER SIZE  4 ; Compagnie de l'employé

;-------------------------------------------------------------------------------
;
; RÉFÉRENCE DE L'EMPLOYÉ
;
FILE MCREFERENCE  PRIMARY &
                  NOITEM

  ACCESS  VIA     REFCIECLE, &
                  REFCLETYP, &
                  REFCLENUM  &
          USING   T_EMPCIECLE, &
                  "E"        , &
                  REFCLENUM OF MCREFERENCE &
          REQUEST REFCLENUM

  ACCESS  VIA     REFCIECLE, &
                  REFCLETYP  &
          USING   T_EMPCIECLE, &
                  "E"          &
          ORDERBY REFCIECLE, &
                  REFCLETYP, &
                  REFCLENUM


  ITEM REFCIECLE  OF MCREFERENCE INITIAL  T_EMPCIECLE
  ITEM REFCLETYP  OF MCREFERENCE INITIAL  "E"
  ITEM REFADRPAM  OF MCREFERENCE INITIAL  1
  ITEM REFSTU     OF MCREFERENCE INITIAL  "A"
  ITEM REFCAT     OF MCREFERENCE INITIAL  " "

;-------------------------------------------------------------------------------
;
; ADRESSE DE L'EMPLOYÉ
;
;   On considère que l'employé n'à toujours qu'une adresse : l'adresse No 1
;
FILE MCREF_ADRESSE  SECONDARY &
                    NOITEM

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM  &
          USING REFCIECLE OF MCREFERENCE, &
                REFCLETYP OF MCREFERENCE, &
                REFCLENUM OF MCREFERENCE

  ITEM REFCIECLE OF MCREF_ADRESSE INITIAL REFCIECLE OF MCREFERENCE
  ITEM REFCLETYP OF MCREF_ADRESSE INITIAL REFCLETYP OF MCREFERENCE
  ITEM REFCLENUM OF MCREF_ADRESSE INITIAL REFCLENUM OF MCREFERENCE
  ITEM RADCLE    OF MCREF_ADRESSE INITIAL 1
  ITEM RADNOM    OF MCREF_ADRESSE FINAL   REFNOM    OF MCREFERENCE
  ITEM RADNOMABR OF MCREF_ADRESSE FINAL   REFNOM    OF MCREFERENCE
  ITEM RADPAMCP  OF MCREF_ADRESSE INITIAL "1"

;-------------------------------------------------------------------------------
; Validation du type de liste(bilingue).
;
DEFINE    D_REFSTU CHARACTER SIZE 16 = "REFSTU"
TEMPORARY T_REFSTU CHARACTER SIZE 4

FILE VCBI_DSC REFERENCE

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_REFSTU, &
                REFSTU OF MCREFERENCE


;-------------------------------------------------------------------------------
;
; Lecture des paramètres de l'application
;

FILE MCHOLDING        REFERENCE

  ACCESS  VIA   CIENMC USING "1"

;-------------------------------------------------------------------------------
;
; Recherche de la description de la devise
;
FILE VDEV_DSC REFERENCE

  ACCESS  VIA   DEVCLE &
          USING DEVCLE OF MCREFERENCE

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE    DZ_CIECLE   CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_DEVCLE    CHARACTER SIZE 3 ; Code de devise

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST   ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des employés " &
@ELSE
      " Employee management " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN (3,3,19) (,30,46) (,,51)

FIELD REFCLENUM OF MCREFERENCE &
      ID 05 HIDDEN &
      REQUIRED &
      NOCHANGE &
      LOOKUP NOTON MCREFERENCE &
          VIA     REFCIECLE, &
                  REFCLETYP, &
                  REFCLENUM  &
          USING   REFCIECLE OF MCREFERENCE, &
                  REFCLETYP OF MCREFERENCE, &
                  REFCLENUM OF MCREFERENCE  &
          MESSAGE 521 ; Cet employé à déja été déclaré.

FIELD REFSTU OF MCREFERENCE &
      REQUIRED &
      LOOKUP ON VCBI_DSC &
        VIA   XELNOM, &
              CBICLE &
        USING D_REFSTU, &
              REFSTU OF MCREFERENCE &
          MESSAGE 439 ; Ce code est invalide.

FIELD CBIDSC OF VCBI_DSC DISPLAY

SKIP TO GROUP 2

FIELD DEVCLE OF MCREFERENCE &
      NOCHANGE &
      DEFAULT DEVCLE OF MCHOLDING &
      LOOKUP ON VDEV_DSC &
        VIA   DEVCLE &
        USING DEVCLE OF MCREFERENCE &
        MESSAGE 418 ; Cette devise est inexistante

FIELD DEVDSC OF VDEV_DSC &
      DISPLAY

SKIP 1

ALIGN (3,3,19)

FIELD REFNOM OF MCREFERENCE &
      ID 10 HIDDEN

FIELD REFNOMABR OF MCREFERENCE &
      ID SAME &
      DEFAULT REFNOM OF MCREFERENCE

SKIP 1

FIELD RADADR1 OF MCREF_ADRESSE &
      ID 15 &
      REQUIRED

FIELD RADADR2 OF MCREF_ADRESSE &
      ID SAME &
      NOLABEL

FIELD RADADR3 OF MCREF_ADRESSE &
      ID SAME &
      NOLABEL

FIELD RADADR4 OF MCREF_ADRESSE &
      ID SAME &
      NOLABEL

SKIP 1

FIELD RADCP OF MCREF_ADRESSE &
      ID 20

FIELD RADTEL OF MCREF_ADRESSE &
      ID 25

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT REFSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_REFSTU, T_REFSTU
    LET FIELDTEXT = TRUNCATE(T_REFSTU)
  END
;  IF NOT FINDMODE &
;    AND REFSTU OF MCREFERENCE <> ""
;  THEN LET FIELDTEXT = REFSTU OF MCREFERENCE
END


;-------------------------------------------------------------------------------
;
; Popup sur les devises
;
PROCEDURE INPUT DEVCLE
BEGIN
 IF 0 <> INDEX(FIELDTEXT,"*")
 THEN BEGIN
   LET T_DEVCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
   RUN SCREEN mc101 PASSING T_DEVCLE MODE F
   LET FIELDTEXT = TRUNCATE(T_DEVCLE)
 END
END

;-------------------------------------------------------------------------------
;
; Procedure de formatage du numero de code postal
;
PROCEDURE INPUT RADCP
BEGIN
  USE uscpfmt NOLIST  ;Fomate le numéro de code postal
END

;-------------------------------------------------------------------------------
;
; Procedure de formatage du numero de telephone
;
PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF    DELETEDRECORD OF MCREFERENCE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     (   ALTEREDRECORD OF MCREFERENCE     &
          OR ALTEREDRECORD OF MCREF_ADRESSE ) &
     AND NOT NEWRECORD     OF MCREFERENCE     &
     AND NOT DELETEDRECORD OF MCREFERENCE     &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction d'un employé.
  ;
  IF DELETEDRECORD OF MCREFERENCE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si l'employé est référée.
    ;
    RUN SCREEN cp155x MODE SAME &
                      PASSING MCREFERENCE , &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END


BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Gestion des employés *****************************
*                                                                              *
* Référence.....: xxxxxx     Statut........: x    xxxxxxxxxxxxxxxxxxxxxxxxx    *
*                            Code devise...: xxx  xxxxxxxxxxxxxxx              *
*                                                                              *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Code Postal...: xxxxxxx                                                      *
* Téléphone.....: xxxxxxxxxxxxx                                                *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
