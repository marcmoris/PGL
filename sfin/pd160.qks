;/T/ Raison de sortie (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-13
;
;    Description..: Cette table contient les informations de base des
;                   raisons de sortie.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd160 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD160"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd160.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;
TEMPORARY T_RSOCODSOR CHARACTER SIZE 05


;-------------------------------------------------------------------------------
;
; Raison de sortie
;

FILE PDRAISON_SORTIE PRIMARY NOITEM
  ACCESS VIA     CIECLE,   &
                 RSOCODSOR &
         USING   T_CIECLEMNU,                 &
                 RSOCODSOR OF PDRAISON_SORTIE &
         REQUEST RSOCODSOR OF PDRAISON_SORTIE &
         ORDERBY CIECLE,   &
                 RSOCODSOR

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 RSOCODSOR

         ITEM CIECLE OF PDRAISON_SORTIE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Raison de sortie " &
@ELSE
      " %%%Raison de sortie " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD RSOCODSOR OF PDRAISON_SORTIE &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE                       &
        LOOKUP NOTON PDRAISON_SORTIE            &
          VIA   CIECLE,                         &
                RSOCODSOR                       &
          USING T_CIECLEMNU,                    &
                RSOCODSOR OF PDRAISON_SORTIE    &
          MESSAGE 2909 ; Cette raison existe déjà


SKIP 1


FIELD RSODSC001 OF PDRAISON_SORTIE         &
        HIDDEN ID 10                       &
        REQUIRED

FIELD RSODSC002 OF PDRAISON_SORTIE         &
        ID SAME                            &
        NOLABEL

FIELD RSODSC003 OF PDRAISON_SORTIE         &
        ID SAME                            &
        NOLABEL


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs code unité de travail.
;

PROCEDURE INPUT RSOCODSOR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RSOCODSOR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd105 MODE F &
                     PASSING T_RSOCODSOR, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RSOCODSOR )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRAISON_SORTIE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRAISON_SORTIE &
     AND NOT NEWRECORD     OF PDRAISON_SORTIE &
     AND NOT DELETEDRECORD OF PDRAISON_SORTIE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Raison de sortie *******************************
*                                                                              *
* Code sortie...: xxxxx                                                        *
*                                                                              *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
