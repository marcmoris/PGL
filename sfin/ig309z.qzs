;/T/ Impression de la liste d'erreur de l'inventaire physique
;    des produits de dimension .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-02
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste d'erreur des produits de dimension de l'inventaire physique
;                   de produit de dimension de Granicor.
;
;    Paramètres....: Compagnie
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INPROD_DIMENSION                         &
    LINK CAINUMCAI      OF INPROD_DIMENSION,    &
         CIECLE         OF INPROD_DIMENSION     &
      TO CAINUMCAI,                             &
         CIECLE         OF PDCAISSON OPTIONAL   &
    LINK CIECLE                                 &
      TO CIECLE         OF MCCOMPAGNIE OPTIONAL

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG309"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-ERREUR CHARACTER SIZE 40 =                             &
        "NUMERO DE CAISSON INEXISTANT"                          &
     IF NOT RECORD PDCAISSON EXIST                              &
     ELSE                                                       &
;        "C'EST UN CAISSON - PROJET"                            &
;     IF TPDTYPPRD OF PDCAISSON <> "T"                          &
;     ELSE                                                      &
        "C'EST UN CAISSON - PROJET"                             &
     IF TPDTYPPRD OF PDCAISSON <> "PD"                          &
     ELSE                                                       &
        "STATUT DU CAISSON INVALIDE  "                          &
     IF CAISTU OF PDCAISSON <> "I"                              &
     ELSE ""

SELECT IF D-ERREUR <> ""

SORT ON IPRNUMPGE OF INPROD_DIMENSION &
     ON IPRDATINV OF INPROD_DIMENSION &
     ON IPRHREINV OF INPROD_DIMENSION &
     ON CAINUMCAI OF INPROD_DIMENSION

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES ERREURS DE L'INVENTAIRE PHYSIQUE DE CAISSONS (PF)                     ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4 &
  TAB 005 " NO   NUMÉRO                             NUMÉRO     TYPE                       " &
SKIP 1 &
  TAB 005 "PAGE  CAISSON    LOCALISATION  STATUT  EXPÉDITION  PRODUIT  ERREUR(Description)" &
SKIP 1 &
  TAB 005 "COMMENTAIRE " &
SKIP 1 &
  TAB 005 "-------------    ------------  ------  ----------  -------  -------------------" &
SKIP

REPORT &
  TAB 003 IPRNUMPGE  OF INPROD_DIMENSION        &
  TAB 011 CAINUMCAI  OF INPROD_DIMENSION        &
  TAB 026 IPRNUMLOC  OF INPROD_DIMENSION        &
  TAB 038 CAISTU     OF PDCAISSON               &
  TAB 046 SPRNUMSOR  OF PDCAISSON               &
  TAB 058 TPDTYPPRD  OF PDCAISSON               &
  TAB 065 D-ERREUR                              &
SKIP                                            &
  TAB 005 IPRDESC    OF INPROD_DIMENSION        &
SKIP 2

FINAL FOOT SKIP 2       &
       TAB 20 "NOMBRE TOTAL DE CAISSONS EN ERREUR: " COUNT

BUILD ig309z_1

;=============================================================================

ACCESS PDCAISSON                &
    LINK CAINUMCAI,             &
         CIECLE                 &
      TO CAINUMCAI,             &
         CIECLE         OF INPROD_DIMENSION OPTIONAL

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG309"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

SELECT IF CAISTU        OF PDCAISSON <> "E" AND      &
          TPDTYPPRD     OF PDCAISSON = "PD" AND      &
          NOT RECORD INPROD_DIMENSION EXIST

DEFINE D-ERREUR CHARACTER SIZE 40 = &
   "NUMERO DE CAISSON EXISTE AVANT NON APRES"

SORT ON IPRNUMPGE OF INPROD_DIMENSION &
     ON IPRDATINV OF INPROD_DIMENSION &
     ON IPRHREINV OF INPROD_DIMENSION &
     ON CAINUMCAI OF INPROD_DIMENSION

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES CAISSONS EXISTANTS AVANT ET NON APRES                                 ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4 &
  TAB 003 "NUMÉRO                          NUMÉRO     TYPE                       " &
SKIP 1 &
  TAB 003 "CAISSON    PROJET     STATUT  EXPÉDITION  PRODUIT  ERREUR(Description)" &
SKIP 1 &
  TAB 003 "-------    ------     ------  ----------  -------  -------------------" &
SKIP

REPORT                                  &
  TAB 003 CAINUMCAI  OF PDCAISSON       &
  TAB 014 PJTNUMPRO  OF PDCAISSON       &
  TAB 027 CAISTU     OF PDCAISSON       &
  TAB 035 SPRNUMSOR  OF PDCAISSON       &
  TAB 047 TPDTYPPRD  OF PDCAISSON       &
  TAB 054 D-ERREUR                      &
SKIP 2

FINAL FOOT SKIP 2 &
       TAB 20 "NOMBRE TOTAL DE CAISSONS EN ERREUR: " COUNT

BUILD ig309z_2

;============================================================================

ACCESS INPROD_DIMENSION

CHOOSE CIECLE PARM

SORT ON CAINUMCAI OF INPROD_DIMENSION

DEFINE D-NBRE-PROD-DIMENSION INTEGER SIZE 2 = 1

SET SUBFILE NAME W_IG309 AT CAINUMCAI

REPORT SUMMARY                                  &
        CIECLE          OF INPROD_DIMENSION         &
        CAINUMCAI       OF INPROD_DIMENSION         &
        D-NBRE-PROD-DIMENSION SUBTOTAL

BUILD ig309z_3

;============================================================================

ACCESS *W_IG309                 &
  LINK CAINUMCAI,               &
       CIECLE                   &
    TO CAINUMCAI,               &
       CIECLE           OF INPROD_DIMENSION

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG309"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

SELECT IF D-NBRE-PROD-DIMENSION > 1

DEFINE D-ERREUR CHARACTER SIZE 40 = &
   "NUMERO DE CAISSON EN DOUBLE"

SORT ON IPRNUMPGE OF INPROD_DIMENSION &
     ON IPRDATINV OF INPROD_DIMENSION &
     ON IPRHREINV OF INPROD_DIMENSION &
     ON CAINUMCAI OF INPROD_DIMENSION

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES CAISSONS EN DOUBLE                                                    ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4 &
  TAB 005 " NO   NUMÉRO                   " &
SKIP 1 &
  TAB 005 "PAGE  CAISSON    LOCALISATION  ERREUR(Description)                       COMMENTAIRE" &
SKIP 1 &
  TAB 005 "----  -------    ------------  -------------------                       -----------" &
SKIP

REPORT                                          &
  TAB 003 IPRNUMPGE  OF INPROD_DIMENSION        &
  TAB 011 CAINUMCAI  OF INPROD_DIMENSION        &
  TAB 025 IPRNUMLOC  OF INPROD_DIMENSION        &
  TAB 036 D-ERREUR                              &
  TAB 078 IPRDESC    OF INPROD_DIMENSION        &
SKIP 2

FINAL FOOT SKIP 2 &
       TAB 20 "NOMBRE TOTAL DE CAISSONS EN ERREUR: " COUNT

BUILD ig309z_4

