;/T/ Gestion des ports
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 29 mars 1995
;
;    Description..: Définition des ports et de leur
;                   description française et anglaise. Ces ports
;                   sont communes à toutes les compagnies.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN fa151 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "FA151"

USE fa151.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom du holding
TEMPORARY T_PORCLE    CHARACTER SIZE 16 INITIAL "PORCLE" RESET AT STARTUP

;-------------------------------------------------------------------------------
;
; Recherche de la valeur du code bilingue
;
FILE FAPORT           PRIMARY &
                      OCCURS 16 &
                      NOITEMS

  ACCESS  VIA     FPOCLE &
          USING   FPOCLE &
          REQUEST FPOCLE &
          ORDERBY FPOCLE

  ACCESS  SEQUENTIAL &
          ORDERBY FPOCLE

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Port / Lieu de travail " &
@ELSE
      "%%%Port " &
@ENDIF
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

TITLE &
@IF FRANCAIS
    "Code  Réf.  Description française         Description anglaise" &
@ELSE
    "Cod   Ref.  French description            English description " &
@ENDIF
  AT 5,8
SKIP TO 6

ALIGN(8,6,8)(,,15)(,,20)(,,50)

CLUSTER OCCUR WITH FAPORT ID BASE 10

FIELD FPOCLE OF FAPORT &
      LABEL " " HIDDEN &
      REQUIRED NOCHANGE &
      LOOKUP NOTON FAPORT &
        VIA   FPOCLE &
        USING FPOCLE OF FAPORT &
        MESSAGE 2703; Ce port existe déjà

FIELD FPOPOR       OF FAPORT

FIELD FPODSCFRA    OF FAPORT

FIELD FPODSCANG    OF FAPORT &
      DEFAULT FPODSCFRA OF FAPORT

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Validation de l'autorisation d'entrer des données
;
PROCEDURE INPUT FPOCLE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR FAPORT
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF DELETEDRECORD OF FAPORT &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification
    ;
    IF    ALTEREDRECORD     OF FAPORT &
      AND NOT NEWRECORD     OF FAPORT &
      AND NOT DELETEDRECORD OF FAPORT &
      AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction du port
    ;
    IF DELETEDRECORD OF FAPORT
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si le port est
      ; référée dans la définition des trajets/produits.
      ;
      RUN SCREEN fa151x MODE SAME &
                        PASSING FAPORT, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Ports *************************************
*                                                                              *
*      Code  Réf.   Description française         Description anglaise         *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*      xxxx   x     xxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
********************************************************************************
@ENDIF
