;/T/ Autorisation des lots
;
;/P/ Programmeur..: Alain Côté / Adapté pour les C.P. par Thomas BRENNEUR
;    Date Création: 8 Juin 1993
;
;    Description..: Cet écran permet d'autoriser les lots pour les
;                   journaliser
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp002 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND,CHANGE &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "CP002"

USE cp002.hlp NOLIST

USE usactecr  NOLIST


;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------

FILE CPLOT            PRIMARY &
                      NODELETE &
                      OCCURS 16

  ACCESS  VIA     CIECLE, &
                  PECCLE, &
                  LCPCLE  &
          USING   T_CIECLEMNU, &
                  PECCLE OF CPLOT, &
                  LCPCLE OF CPLOT  &
          REQUEST PECCLE OF CPLOT, &
                  LCPCLE OF CPLOT  &
          ORDERBY CIECLE, &
                  PECCLE, &
                  LCPCLE
;;          ORDERED

  ACCESS  VIA     CIECLE, &
                  PECCLE  &
          USING   T_CIECLEMNU, &
                  PECCLE OF CPLOT &
          REQUEST PECCLE OF CPLOT &
          ORDERBY CIECLE, &
                  PECCLE, &
                  LCPCLE

;-------------------------------------------------------------------------------
;
; Pour afficher la description du type de lot.
;
DEFINE D_LCPTYPECR CHAR SIZE 16 = "LCPTYPECR"

FILE VCBI_DSC         REFERENCE

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_LCPTYPECR, &
                LCPTYPECR OF CPLOT


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Autorisation des lots " &
@ELSE
      " Batch Consultation " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN (,3,19)

FIELD PECCLE OF CPLOT &
label "Période..:"&
        DISPLAY

SKIP

TITLE &
@IF FRANCAIS
 "                                          Vérifié  Journalisé       Montant" &
@ELSE
 "                                           Verify     Posting         Total" &
@ENDIF
     AT ,3

SKIP

TITLE &
@IF FRANCAIS
 "Type                    Lot  Usager          le        le             total" &
@ELSE
 "Type                   Batch User            at        at            Amount" &
@ENDIF
     AT ,3

SKIP

ALIGN(3,2,3) (,,6) (,,27) (,,32) (,,44) (,,55) (,,66)

CLUSTER OCCURS WITH CPLOT ID BASE 10

FIELD LCPTYPECR  OF CPLOT HIDDEN LABEL " " &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

USE ushilite NOLIST

FIELD CBIDSC OF VCBI_DSC &
        SIZE 20 &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

USE ushilite NOLIST

FIELD LCPCLE     OF CPLOT &
        NUMERIC &
        SIGNIFICANCE 4 &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

USE ushilite NOLIST

FIELD LCPUSR     OF CPLOT &
        DISPLAY size 10 &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

USE ushilite NOLIST

FIELD LCPDATVAL  OF CPLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

USE ushilite NOLIST

FIELD LCPDATJOU  OF CPLOT  FORMAT YYYYMMDD PATTERN "####/##/##" &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

USE ushilite NOLIST

FIELD LCPCUMMNT  OF CPLOT &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCPNBRTRSERR OF CPLOT <> 0 &
                  ELSE INVERSE          IF LCPATRJOU OF CPLOT = "1" AND &
                                           LCPDATJOU OF CPLOT = 0

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Si l'usager fait un <return> sur une ligne à l'écran il active la procédure
; suivante.
;
; Si le lot n'est pas autorisé, on met le flag d'autorisation à oui et on
; conserve l'usager de l'autorisation.
;
; Si le lot est déjà autorisé, on enlève le flag d'autorisation et l'usager
; qui l'avait autorisé.
;
;
PROCEDURE DESIGNER 10
BEGIN
  IF LCPDATVAL OF CPLOT <> 0
  THEN BEGIN
    IF LCPNBRTRSERR = 0
    THEN BEGIN
      IF LCPDATJOU OF CPLOT = 0
      THEN BEGIN
        IF LCPATRJOU OF CPLOT = "0"
        THEN BEGIN
          LET LCPATRJOU OF CPLOT = "1"
          LET LCPUSRATR OF CPLOT = LOGONID
        END
        ELSE BEGIN
          LET LCPATRJOU OF CPLOT = "0"
          LET LCPUSRATR OF CPLOT = " "
        END
        DISPLAY LCPTYPECR OF CPLOT
        DISPLAY CBIDSC    OF VCBI_DSC
        DISPLAY LCPCLE    OF CPLOT
        DISPLAY LCPUSR    OF CPLOT
        DISPLAY LCPDATVAL OF CPLOT
        DISPLAY LCPDATJOU OF CPLOT
        DISPLAY LCPCUMMNT OF CPLOT
      END
    END
  END
  ELSE ERROR 581      ; Le lot doit être validé avant de l'autoriser
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; On ne peut pas détruire dans cette écran.
  ;
  FOR CPLOT
  BEGIN
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF CPLOT &
       AND NOT NEWRECORD     OF CPLOT &
       AND NOT DELETEDRECORD OF CPLOT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD DETAIL LIST

;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;**************************** Autorisation des lots *****************************
;* Période ......: xxxxx                                                        *
;*                                           Vérifié  Journalisé       Montant  *
;* Type                    Lot  Usager          le        le             total  *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;* xx xxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
;********************************************************************************
