;/P/ Liste des blocs facturés par contrat
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Modifié par André Bilodeau 25/04/94
;  - Ajouter date de transport
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme, Remplacer contrat par commande, et faire
;		      sortir le rapport par type de granit au lieu de par
;		      carriere.	
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC &
 LINK CIECLE OF IFBLOC, &
      FTRNUMFAC OF IFBLOC  &
   TO CIECLE, &
      FTRNUMFAC                    OF PDFACTURE OPTIONAL


CHOOSE CIECLE PARM, &
       CLICLE PARM, &
       TGRCODGRA PARM

DEFINE TMP-DAT-DEB DATE = PARM ;--------PROMPT             &
;---------               "ENTRER LA DATE DE DEBUT (999999): "
DEFINE TMP-DAT-FIN DATE = PARM ;--------PROMPT             &
;---------               "ENTRER LA DATE DE FIN   (999999): "

SELECT IF     FTRNUMFAC OF IFBLOC <> 0 &
          AND (   (     FTRDATFAC >= TMP-DAT-DEB &
                    AND FTRDATFAC <= TMP-DAT-FIN &
                  ) &
               OR (     TMP-DAT-DEB = 0 &
                    AND TMP-DAT-FIN = 0 &
                  ) &
              )
;

DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""

SORT ON D_NUMCOM &
     ON TGRCODGRA OF IFBLOC &
     ON CATCLE OF IFBLOC      &
     ON FTRNUMFAC OF PDFACTURE &
     ON ETRCLE OF IFBLOC

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" &
                        IF     TMP-DAT-DEB = 0 &
                           AND TMP-DAT-FIN = 0 &
        ELSE "Entre le : " + ASCII( TMP-DAT-DEB , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[7:2] + &
             " et : "      + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF304"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 3 "NO"                    &
             TAB 13 "GRA"                   &
             TAB 17 "CAT"                   &
             TAB 21 "COUL"                  &
             TAB 26 "QUAL"                 &
             TAB 31 "DATE"                 &
             TAB 40 "NO"                   &
             TAB 55 "DATE"                 &
             TAB 67 "DIMENSIONS "          &
             TAB 85 "TOTAL"                &
             TAB 96 "T/M"                  &
             TAB 102 "NO"                   &
             TAB 109 "NO"                   &
             TAB 118 "DATE DE"              &
SKIP 1 &
             TAB 3 "COMM."               &
             TAB 31 "FACTURE"              &
             TAB 40 "BLOC"                 &
             TAB 55 "PROD"                 &
             TAB 65 "LONG HAUT LARG"       &
             TAB 86 "M3"                   &
             TAB 102 "EXP"                  &
             TAB 107 "FACTURE"              &
             TAB 118 "TRANSPORT"            &
SKIP 1 &
             TAB 3 "_________"             &
             TAB 13 "___"                  &
             TAB 17 "___"                  &
             TAB 21 "____"                 &
             TAB 26 "____"                 &
             TAB 31 "________"             &
             TAB 40 "______________"       &
             TAB 55 "________"             &
             TAB 64 "________________"     &
             TAB 81 "_________"            &
             TAB 91 "_________"            &
             TAB 101 "_____"                &
             TAB 107 "_______"              &
             TAB 118 "________"            &
SKIP 2
REPORT  TAB 003 D_NUMCOM PRINT AT D_NUMCOM             &
        TAB 013 TGRCODGRA OF IFBLOC PRINT AT TGRCODGRA &
        TAB 017 CATCLE OF IFBLOC PICTURE "^^^" PRINT AT CATCLE      &
        TAB 021 CLRCLE OF IFBLOC                       &
        TAB 026 QLTCLE OF IFBLOC                       &
        TAB 031 FTRDATFAC OF PDFACTURE                 &
        TAB 040 D_NUMBLC                               &
        TAB 055 BLCDAT OF IFBLOC                       &
        TAB 064 BLCVENDULONG OF IFBLOC                 &
        TAB 070 BLCVENDUHAU OF IFBLOC                  &
        TAB 076 BLCVENDULAR OF IFBLOC                  &
        TAB 081 BLCM3TOT OF IFBLOC  PICTURE "^^^^^^.^^"&
        TAB 091 BLCTFINAL OF IFBLOC PICTURE "^^^^^^.^^"&
        TAB 100 EXNCLE OF IFBLOC                         &
        TAB 108 FTRNUMFAC OF IFBLOC                         &
        TAB 118 BLCDATREC OF IFBLOC

FOOTING AT D_NUMCOM &
        TAB 29 "______________"            &
        TAB 81 "_________"         &
        TAB 91 "_________"          &
SKIP 1 &
        TAB 30 COUNT "Blocs"        &
        TAB 68 "Sous-total:"        &
        TAB 81 BLCM3TOT SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 91 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 2
FINAL FOOTING  &
        TAB 29 "______________"            &
        TAB 81 "_________"         &
        TAB 91 "_________"          &
SKIP 1  &
        TAB 30 COUNT "Blocs"        &
        TAB 68 "Total:"             &
        TAB 81 BLCM3TOT SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 91 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 2
BUILD  if304z
