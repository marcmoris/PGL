;/T/ 2.3.1 Enregistrer la commande interne. - Nom client
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-02
;
;    Description..: Sous-écran pour consulter le nom d'un client.
;
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210n NOMODE NOACTION FIELDMARK &
              FROM 11,40 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM ,         &
                        T_CLICLE


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_CLICLE    CHARACTER SIZE 06


USE pd210n.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Clients
;

FILE VCLC_CLI PRIMARY &
              OCCURS 11 

  ACCESS VIA     CLICLE,                 &
                 CIECLE                  &
         USING   T_CLICLE,               &
                 T_CIECLEMNU             &
         ORDERBY CLICLE,                 &
                 CIECLE


;------------------------------------------------------------------------------
;
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
USE usdrwfnd NOLIST     ; Draw pour les écrans de choix et consultations
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


TITLE &
@IF FRANCAIS
      " Client " &
@ELSE
      " %%%Client " &
@ENDIF
      CENTERED AT 1,1




ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH VCLC_CLI

FIELD CLICLE           OF VCLC_CLI         &
        HIDDEN                             &
        LABEL ">>"                         &
        DISPLAY

FIELD CLINOM           OF VCLC_CLI         &
        DISPLAY SIZE 26

CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_CLICLE, "@")
  THEN LET T_CLICLE = ""
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_CLICLE = CLICLE OF VCLC_CLI
  RETURN
END


BUILD
@IF FORMAT

                                       **************** Client *****************
                                       *                                       *
                                       *                                       *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxx  *
                                       *****************************************
@ENDIF
