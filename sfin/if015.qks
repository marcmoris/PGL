;/T/ Bateau
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
SCREEN if015 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES ENTRY, FIND, CHANGE &
             RECEIVING T_CIECLE, &
                       T_CIENOM, &
                       T_CLICIECLE

USE ussectmp  NOLIST
    "IF015"

USE if015.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE    CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_CLICIECLE CHARACTER SIZE 4  ; Compagnie du client
;-------------------------------------------------------------------------------
;
;
FILE IFBATEAU         IN DICT PRIMARY NOITEM
  ACCESS VIA     CIECLE, &
                 BATCLE  &
         USING   T_CIECLE, &
                 BATCLE OF IFBATEAU &
         REQUEST BATCLE OF IFBATEAU &
         ORDERBY CIECLE, &
                 BATCLE
  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY CIECLE, &
                 BATCLE
  ITEM CIECLE    OF IFBATEAU INITIAL T_CIECLE
;-------------------------------------------------------------------------------
;
; Référence client
;
FILE IFCLIENT         IN DICT REFERENCE
  ACCESS VIA   CIECLE, &
               CLTCLE  &
         USING T_CIECLE, &
               CLTCLE OF IFBATEAU

FILE CRCLIENT         IN DICT REFERENCE
  ACCESS VIA   CLICIECLE, &
               CLICLE &
         USING T_CLICIECLE, &
               CLICLE OF IFCLIENT
;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Enregistrement des bateaux " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

SKIP 3
ALIGN(2,5,24)(,36,48)
FIELD BATCLE OF IFBATEAU &
        HIDDEN &
        LABEL "No contrat bateau:" &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFBATEAU &
          VIA   CIECLE, &
                BATCLE  &
          USING CIECLE OF IFBATEAU, &
                BATCLE OF IFBATEAU  &
          MESSAGE "*E* Ce contrat bateau existe déjà"
FIELD BATNOM OF IFBATEAU &
        LABEL "Nom bateau:" &
        REQUIRED

SKIP 1
FIELD CLTCLE OF IFBATEAU &
        HIDDEN &
        LABEL "No client.......:" &
        REQUIRED &
        LOOKUP ON IFCLIENT &
          VIA   CIECLE, &
                CLTCLE  &
          USING T_CIECLE, &
                CLTCLE OF IFBATEAU &
          MESSAGE "*E* Numéro de client invalide..."
FIELD CLINOMABR OF CRCLIENT &
        LABEL "Nom client:" &
        DISPLAY

SKIP 1
FIELD BATSTU OF IFBATEAU &
        HIDDEN &
        LABEL "Statut..........:"  &
        REQUIRED


SKIP 1
ALIGN(2,5,19)
FIELD BATCOM OF IFBATEAU &
        HIDDEN &
        LABEL "Commentaire.:"


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************** Enregistrement des bateaux **************************
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*   No contrat bateau: xxxxxxxxx   Nom bateau: xxxxxxxxxxxxxxxxxxxxxxxxx       *
*                                                                              *
*   No client.......:  xx          Nom client: xxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
*   Statut..........:  x                                                       *
*                                                                              *
*   Commentaire.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
