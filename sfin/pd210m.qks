;/T/ 2.3.1 Commande interne - Contact
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-01
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les contacts pour les commandes internes.
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210m ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 9,1 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING VCLC_CLI, &
                        T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Variable passé en paramêtre
;

TEMPORARY T_CIECLEMNU      CHARACTER        SIZE 04

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp   NOLIST
    "PD210M"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd210m.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr   NOLIST

;-------------------------------------------------------------------------------
;
; Vue client
;

FILE VCLC_CLI MASTER


;-------------------------------------------------------------------------------
;
; Contact chez le client.
;
FILE CRCLI_CONTACT    PRIMARY &
                      NOITEM  &
                      OPEN EACH LEVEL &
                      OCCURS 5

  ACCESS VIA     CLICIECLE, &
                 CLICLE     &
         USING   CLICIECLE OF VCLC_CLI, &
                 CLICLE    OF VCLC_CLI  &
         ORDERBY CCTCLE


 ITEM CLICIECLE OF CRCLI_CONTACT INITIAL CLICIECLE OF VCLC_CLI
 ITEM CLICLE    OF CRCLI_CONTACT INITIAL CLICLE    OF VCLC_CLI


;-------------------------------------------------------------------------------
;
DRAW 02,01 TO 02,79
DRAW 02,01 TO 15,01
DRAW 15,01 TO 15,80
DRAW 03,80 TO 15,80

USE ushilite NOLIST

SKIP 1

TITLE &
@IF FRANCAIS
      " Contacts " &
@ELSE
      " Contact " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

TITLE &
@IF FRANCAIS
      "                                                                Fax /" &
@ELSE
      "                                                                Fax /" &
@ENDIF
      at ,3

SKIP

TITLE &
@IF FRANCAIS
  "No Titre                Nom                  Téléphone    Poste Céllulaire" &
@ELSE
  "No Title                Name                 Telephone    Ext.  %%cell" &
@ENDIF
  at ,3

CLUSTER OCCURS WITH CRCLI_CONTACT ID BASE 10

ALIGN(3,2,3) (6,,6) (27,,27) (48,,48) (62,,62) (67,,67)

FIELD CCTCLE OF CRCLI_CONTACT HIDDEN LABEL " " &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON CRCLI_CONTACT &
          VIA CLICIECLE, &
              CLICLE   , &
              CCTCLE     &
          USING CLICIECLE OF CRCLI_CONTACT, &
                CLICLE    OF CRCLI_CONTACT, &
                CCTCLE    OF CRCLI_CONTACT  &
          MESSAGE 700 ; Ce numéro de contact existe déjà.

FIELD CCTTIT OF CRCLI_CONTACT

FIELD CCTNOM OF CRCLI_CONTACT

FIELD CCTTEL OF CRCLI_CONTACT

FIELD CCTTELPOS OF CRCLI_CONTACT

FIELD CCTFAX OF CRCLI_CONTACT

SKIP

ALIGN (,,67)

FIELD CCTTELCEL OF CRCLI_CONTACT

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT CCTCLE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CCTTEL
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CCTFAX
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CCTTELCEL
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valider que le Contact "1" ne soit pas modifié.
;
;
PROCEDURE DESIGNER 10
BEGIN
  IF CCTCLE OF CRCLI_CONTACT <> 1
  THEN BEGIN
    ACCEPT CCTTIT    OF CRCLI_CONTACT
    ACCEPT CCTNOM    OF CRCLI_CONTACT
    ACCEPT CCTTEL    OF CRCLI_CONTACT
    ACCEPT CCTTELPOS OF CRCLI_CONTACT
    ACCEPT CCTFAX    OF CRCLI_CONTACT
    ACCEPT CCTTELCEL OF CRCLI_CONTACT
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  FOR CRCLI_CONTACT
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF CRCLI_CONTACT &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF CRCLI_CONTACT &
       AND NOT NEWRECORD     OF CRCLI_CONTACT &
       AND NOT DELETEDRECORD OF CRCLI_CONTACT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

;-------------------------------------------------------------------------------
;
; Valider que le Contact "1" ne soit pas détruit
;

PROCEDURE DELETE
BEGIN
  IF CCTCLE OF CRCLI_CONTACT <> 1
  THEN DELETE CRCLI_CONTACT
END


BUILD DETAIL LIST
@IF FORMAT

*********************************** Contacts **********************************x
*                                                                 Fax /        *
* No Titre                Nom                  Téléphone    Poste Céllulaire   *
* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
*                                                                 xxxxxxxxxxxxx*
* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
*                                                                 xxxxxxxxxxxxx*
* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
*                                                                 xxxxxxxxxxxxx*
* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
*                                                                 xxxxxxxxxxxxx*
* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
*                                                                 xxxxxxxxxxxxx*
********************************************************************************
@ENDIF
