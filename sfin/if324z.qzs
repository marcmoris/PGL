;/P/ Impression de la note de crédit
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
USE ussetqzs NOLIST

ACCESS IFNOT_CRE_MAITRE &
  LINK CIECLE OF IFNOT_CRE_MAITRE, &
       CRECLE OF IFNOT_CRE_MAITRE  &
    TO CIECLE, &
       CRECLE                           OF IFNOTE_CREDIT &
  LINK CIECLE OF IFNOT_CRE_MAITRE, &
       FCECLE OF IFNOT_CRE_MAITRE, &
       CRRCLENUM OF IFNOTE_CREDIT, &
       CRRCLEANN OF IFNOTE_CREDIT, &
       BLCCLE    OF IFNOTE_CREDIT  &
    TO CIECLE, &
       FCECLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       BLCCLE                           OF IFFACTURE_DETAIL &
  LINK CIECLE OF IFNOT_CRE_MAITRE, &
       FCECLE OF IFNOT_CRE_MAITRE  &
    TO CIECLE, &
       FCECLE                           OF IFFACTURE &
  LINK CIECLE OF IFFACTURE, &
       CLTCLE OF IFFACTURE  &
    TO CIECLE, &
       CLTCLE                           OF IFCLIENT &
  LINK CLICIECLE OF IFCLIENT, &
       CLICLE OF IFCLIENT &
    TO CLICIECLE, &
       CLICLE                           OF CRCLIENT &
   LINK ( CLICIECLE OF IFCLIENT, &
          CLICLE OF IFCLIENT, &
          CLIREFTYP OF CRCLIENT, &
          1 ) &
     TO REFCIECLE, &
        REFCLENUM, &
        REFCLETYP, &
        RADCLE                          OF MCREF_ADRESSE &
   LINK CLICIECLE OF IFCLIENT, &
        CLICLE OF IFCLIENT, &
        CIECLE OF IFCLIENT &
     TO CLICIECLE, &
        CLICLE, &
        CIECLE                          OF CRCLI_CIE OPTIONAL &
   LINK CLCTAXTYPTPS OF CRCLI_CIE, &
        CLCTAXCODTPS OF CRCLI_CIE  &
     TO TAXCLETYP, &
        TAXCLECOD                       OF VTAX_DSC ALIAS A_TAXFED OPTIONAL &
   LINK CLCTAXTYPTVP OF CRCLI_CIE, &
        CLCTAXCODTVP OF CRCLI_CIE  &
     TO TAXCLETYP, &
        TAXCLECOD                       OF VTAX_DSC ALIAS A_TAXPRO OPTIONAL


CHOOSE CIECLE PARM, CRECLE PARM

DEFINE D_FLGIMP CHARACTER SIZE 1 = PARM

SELECT IF      (NCMFIN = "O" OR NCMANNUL = "O") &
           AND (    ( NCMIMP <> "O" AND D_FLGIMP = "0" ) &
                 OR ( NCMIMP = "O" AND D_FLGIMP = "1" ) &
               )

DEFINE TMP-QTE-BL-AJUS ZONED SIZE 3 PIC "^^^" = CREQTEBLAJUS
DEFINE TMP-M3-DIFF INTEGER SIZE 2 = (FDEM3PROD - CREM3AJUS )
DEFINE TOT-M3-DIFF INTEGER SIZE 2 PIC "^^^^.^^" = TMP-M3-DIFF
DEFINE TMP-TOTAL-DIFF INTEGER SIZE 4 = (FDETOTAL - CRETOTAL )
DEFINE TOT-TOTAL-DIFF INTEGER SIZE 4 = TMP-TOTAL-DIFF
DEFINE TMP-CRE-FOB INTEGER SIZE 4 = FLOOR(CREPRIXFOB OF IFNOTE_CREDIT * &
                                    CREM3AJUS OF IFNOTE_CREDIT / 100 + .5)
DEFINE TMP-CRE-FAS INTEGER SIZE 4 = FLOOR(CREPRIXFAS OF IFNOTE_CREDIT * &
                                    CREM3AJUS OF IFNOTE_CREDIT / 100 + .5)
DEFINE TMP-FAD-FOB INTEGER SIZE 4 = FLOOR(FDEPRIXFOB OF IFFACTURE_DETAIL * &
                                    FDEM3PROD OF IFFACTURE_DETAIL / 100 + .5)
DEFINE TMP-FAD-FAS INTEGER SIZE 4 = FLOOR(FDEPRIXFAS OF IFFACTURE_DETAIL * &
                                    FDEM3PROD OF IFFACTURE_DETAIL / 100 + .5)
DEFINE TMP-TOT-FOB INTEGER SIZE 4 = TMP-FAD-FOB - TMP-CRE-FOB
DEFINE TMP-TOT-FAS INTEGER SIZE 4 = TMP-FAD-FAS - TMP-CRE-FAS
DEFINE D_PCTFED FLOAT SIZE 8 = TAXPCT OF A_TAXFED
DEFINE D_PCTPRO FLOAT SIZE 8 = TAXPCT OF A_TAXPRO

DEFINE D_NOTCRRCLENUM INTEGER SIZE 2 = CRRCLENUM OF IFNOTE_CREDIT
DEFINE D_NOTCRRCLEANN INTEGER SIZE 2 = CRRCLEANN OF IFNOTE_CREDIT

SORT ON CRECLE

SET SUBFILE NAME WIF324 AT CRECLE
REPORT SUMMARY &
        CIECLE &
        CRECLE  &
        NCMDAT &
        NCMCOM1 &
        NCMCOM2 &
        FCECLE OF IFFACTURE &
        CLTCLE OF IFFACTURE &
        CRRCLENUM OF IFFACTURE &
        CRRCLEANN OF IFFACTURE &
        FCECAT OF IFFACTURE &
        COEDATDEB OF IFFACTURE &
        D_NOTCRRCLENUM &
        D_NOTCRRCLEANN &
        CLINOM     &
        RADADR1 &
        FCEADR1    &
        RADADR2 &
        FCEADR2    &
        RADADR3 &
        FCEADR3    &
        RADCP &
        CLILNG   &
        FCECP  &
        CLCEXPTVP &
        D_PCTFED &
        D_PCTPRO &
        TMP-QTE-BL-AJUS SUB RESET AT CRECLE &
        TOT-M3-DIFF SUB RESET AT CRECLE &
        TMP-TOT-FOB SUB RESET AT CRECLE &
        TMP-TOT-FAS SUB RESET AT CRECLE &
        TOT-TOTAL-DIFF SUB RESET AT CRECLE &
        CREMNTTPSFED OF IFNOTE_CREDIT SUB RESET AT CRECLE &
        CREMNTTVQPRO OF IFNOTE_CREDIT SUB RESET AT CRECLE &
        CREPRIXFOB PIC " ^,^^^.^^" &
        CREPRIXFAS

BUILD if324za
;
SET REP NOLIMIT

SET PAGE LENGTH 60 WIDTH 132
SET FORMFEED

ACCESS *WIF324 &
  LINK CIECLE OF WIF324, &
       D_NOTCRRCLENUM OF WIF324, &
       D_NOTCRRCLEANN OF WIF324  &
    TO CIECLE, &
       CRRCLENUM, &
       CRRCLEANN                        OF IFCARRIERE    &
  LINK CIECLE OF WIF324, &
       CLTCLE OF WIF324, &
       CRRCLENUM OF WIF324, &
       CRRCLEANN OF WIF324, &
       FCECAT OF WIF324, &
       COEDATDEB OF WIF324 &
    TO CIECLE, &
       CLTCLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       CATCLE, &
       COEDATDEB                        OF IFCONTRAT_LIGNE &
  LINK CIECLE OF WIF324, &
       CLTCLE OF WIF324, &
       CRRCLENUM OF WIF324, &
       CRRCLEANN OF WIF324 &
    TO CIECLE, &
       CLTCLE, &
       CRRCLENUM, &
       CRRCLEANN                        OF IFCONTRAT &
  LINK CIECLE OF WIF324 &
    TO CIECLE                           OF MCCOMPAGNIE


SORT ON CLTCLE OF WIF324 &
     ON CRRCLENUM OF WIF324 &
     ON CRRCLEANN OF WIF324 &
     ON FCECAT OF WIF324 &
     ON COEDATDEB OF WIF324

DEF TMP-LABEL-1 CHARACTER SIZE 28 = &
    "NOTE DE CREDIT SUR FACTURE #" IF CLILNG = "F" ELSE &
    "CREDIT NOTE ON INVOICE     #"
DEF TMP-LABEL-2 CHARACTER SIZE 25 = "(VOIR LISTE CI-JOINTE)" &
    IF CLILNG = "F" ELSE "(SEE ENCLOSED LIST)"
DEF TMP-LABEL-3 CHARACTER SIZE 20 = "BLOC BRUT DE GRANIT" &
    IF CLILNG = "F" ELSE "ROUGH GRANIT BLOCK"
DEF TMP-LABEL-4 CHARACTER SIZE 16 = "POUR UN TOTAL DE" &
    IF CLILNG = "F" ELSE "FOR A TOTAL OF  "
DEF TMP-LABEL-5 CHARACTER SIZE 10 = "BLOCS ET " &
    IF CLILNG = "F" ELSE "BLOCKS AND"
DEF TMP-LABEL-6 CHARACTER SIZE 12 = " M3"  &
    IF CLILNG = "F" ELSE "CUBIC METERS"
DEF TMP-LABEL-7 CHARACTER SIZE 12 = "DE GRANIT A" &
    IF CLILNG = "F" ELSE "OF GRANIT AT"
DEF TMP-LABEL-8 CHARACTER SIZE 14 = "/ M3"   &
    IF CLILNG = "F" ELSE "/ CUBIC METERS"
DEF TMP-LABEL-9 CHARACTER SIZE 12 = "FOB CARRIERE"  &
    IF CLILNG = "F" ELSE "FOB QUARRY"

DEF TMP-LABEL-10 CHARACTER SIZE 26 = "SOUS-TOTAL" &
    IF CLILNG = "F" ELSE "SUBTOTAL"
DEF TMP-LABEL-101 CHARACTER SIZE 5 = "TPS" &
    IF CLILNG = "F" ELSE "GST"
DEF TMP-LABEL-102 CHARACTER SIZE 5 = "TVQ" &
    IF CLILNG = "F" ELSE "TVQ"
DEF TMP-LABEL-103 CHARACTER SIZE 30 = "TOTAL DE CETTE NOTE DE CREDIT" &
    IF CLILNG = "F" ELSE "TOTAL FOR THIS CREDIT NOTE"

DEF TMP-SOUS-TOTAL  = TOT-TOTAL-DIFF + CREMNTTPSFED
DEF TMP-GRAND-TOTAL = TMP-SOUS-TOTAL + CREMNTTVQPRO

DEF TMP-LABEL-11 CHARACTER SIZE 13 = "COMMENTAIRES:" &
    IF CLILNG ="F" ELSE "COMMENTS:"

PAGE HEADING TAB 3 "." &
SKIP 9  TAB 72 CRECLE      &
SKIP 1 &
        TAB 10 CLINOM     &
SKIP 1 &
        TAB 10 RADADR1 &
        TAB 51 FCEADR1    &
SKIP 1 &
        TAB 10 RADADR2 &
        TAB 51 FCEADR2    &
SKIP 1 &
        TAB 10 RADADR3 &
        TAB 51 FCEADR3    &
SKIP 1 &
        TAB 10 RADCP &
        TAB 51 FCECP  &
SKIP 3  &
        TAB 11 CONCONDITION1 &
SKIP 1 &
        TAB 11 CONCONDITION2 &
SKIP 2 &
        TAB 63 NCMDAT &
SKIP 1 &
        TAB 10 COEREFCLI  &
        TAB 26 "PROV:" CLCEXPTVP &
SKIP 1 &
        TAB 02 "NUMERO INSCRIPTION TPS: " CIEENRTPS &
SKIP 4 &
        TAB 12 TMP-LABEL-1   &
        TAB 40 FCECLE           &
SKIP 2 &
        TAB 12 TMP-LABEL-2 &
SKIP 3 &
        TAB 12 TMP-LABEL-3 &
        CRRDSC &
SKIP 3 &
        TAB 12 TMP-LABEL-4 TMP-QTE-BL-AJUS &
        TAB 35 TMP-LABEL-5 TOT-M3-DIFF TMP-LABEL-6  &
SKIP 2  &
        TAB 12 TOT-M3-DIFF TMP-LABEL-7 CREPRIXFOB TMP-LABEL-8   &
        TAB 69 TMP-TOT-FOB FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 2 &
        TAB 12 TMP-LABEL-9 &
SKIP 2 &
        TAB 12 "FAS: " CREPRIXFAS &
        TAB 69 TMP-TOT-FAS FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 2
REPORT
FINAL FOOTING &
        TAB 69 "___________" &
SKIP &
        TAB 40 TMP-LABEL-10 &
        TAB 69 TOT-TOTAL-DIFF FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 40 TMP-LABEL-101 &
        TAB 45 D_PCTFED PIC "^^^.^^%" OUTPUT SCALE 4 &
        TAB 69 CREMNTTPSFED FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 69 "___________" &
SKIP 1&
        TAB 69 TMP-SOUS-TOTAL FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1&
        TAB 40 TMP-LABEL-102 &
        TAB 45 D_PCTPRO PIC "^^^.^^%" OUTPUT SCALE 4 &
        TAB 69 CREMNTTVQPRO FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 40 TMP-LABEL-103 &
        TAB 69 TMP-GRAND-TOTAL FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 2  &
        TAB 12 TMP-LABEL-11 &
SKIP 1 &
        TAB 12 NCMCOM1 &
SKIP 1 &
        TAB 12 NCMCOM2

BUILD if324zb
;
SET PAGE NUMBER 2

ACCESS *WIF324 &
  LINK CIECLE OF WIF324, &
       CRECLE OF WIF324  &
    TO CIECLE, &
       CRECLE                           OF IFNOTE_CREDIT &
  LINK CIECLE OF WIF324, &
       FCECLE OF WIF324, &
       CRRCLENUM OF IFNOTE_CREDIT, &
       CRRCLEANN OF IFNOTE_CREDIT, &
       BLCCLE OF IFNOTE_CREDIT &
    TO CIECLE, &
       FCECLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       BLCCLE                           OF IFFACTURE_DETAIL

DEFINE TMP-M3-DIFF INTEGER SIZE 2 = (FDEM3PROD - CREM3AJUS )
DEFINE TMP-TOTAL-DIFF INTEGER SIZE 4 = (FDETOTAL - CRETOTAL )

DEF TMP-SOUS-TOTAL  = TMP-TOTAL-DIFF + CREMNTTPSFED OF IFNOTE_CREDIT
DEF TMP-GRAND-TOTAL = TMP-SOUS-TOTAL + CREMNTTVQPRO OF IFNOTE_CREDIT
DEF TMP-LABEL-TPS CHARACTER SIZE 5 = "TPS "
DEF TMP-LABEL-TVQ CHARACTER SIZE 5 = "TVQ "

SORT ON FCECLE &
     ON CRRCLENUM OF IFNOTE_CREDIT &
     ON CRRCLEANN OF IFNOTE_CREDIT &
     ON BLCCLE OF IFNOTE_CREDIT

USE uspitch NOLIST

PAGE HEADING TAB 3 "." & ; -- IMP -- FONT-LQ &
        TAB 69 "PAGE:" SYSPAGE &
SKIP 11 TAB 72 CRECLE      &
SKIP 1 &
        TAB 10 CLINOM     &
SKIP 2 &
        TAB 12 "NO CONTRAT:" &
        TAB 25 CLTCLE OF WIF324 &
        TAB 27 CRRCLENUM OF WIF324 &
        TAB 29 CRRCLEANN OF WIF324 &
SKIP 1 &
        TAB 12 "CATEGORIE :" &
        TAB 25 FCECAT   &
SKIP 1 &
        TAB 12 "DATE DEBUT:" &
        TAB 25 COEDATDEB &
SKIP 4 &
        TAB 61 NCMDAT PITCH-12 &
SKIP 3 &
        TAB 12 "NO BLOC"        &
        TAB 22 "QTE"            &
        TAB 29 "DIMENSIONS"     &
        TAB 43 "TOT M3"         &
        TAB 53 "FOB"            &
        TAB 63 "FAS"            &
SKIP 1 &
        TAB 27 "LONG HAUT LARG" &
SKIP 1
REPORT &
        TAB 12 CRRCLENUM OF IFNOTE_CREDIT &
        TAB 14 CRRCLEANN OF IFNOTE_CREDIT &
        TAB 15 "-" &
        TAB 16 BLCCLE OF IFNOTE_CREDIT &
        TAB 23 CREQTEBLAJUS  &
        TAB 26 FDEDIMLON      &
        TAB 30 "x"              &
        TAB 32 FDEDIMHAU      &
        TAB 36 "x"              &
        TAB 38 FDEDIMLAR      &
        TAB 43 FDEM3PROD      &
        TAB 51 FDEPRIXFOB     PIC " ^,^^^.^^" &
        TAB 61 FDEPRIXFAS     &
        TAB 69 FDETOTAL  FLOAT "$" PIC "^^^^,^^^.^^"      &
SKIP 1 &
        TAB 26 CREDIMLONAJUS &
        TAB 30 "x"              &
        TAB 32 CREDIMHAUAJUS &
        TAB 36 "x"              &
        TAB 38 CREDIMLARAJUS &
        TAB 43 CREM3AJUS      &
        TAB 51 CREPRIXFOB     PIC " ^,^^^.^^" &
        TAB 61 CREPRIXFAS     &
        TAB 69 CRETOTAL  FLOAT "$" PIC "^^^^,^^^.^^"      &
SKIP 1 &
        TAB 42 "_______"     &
        TAB 69 "___________" &
SKIP 1 &
        TAB 42 TMP-M3-DIFF PIC "^^^^.^^"     &
        TAB 69 TMP-TOTAL-DIFF  FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 60 TMP-LABEL-TPS &
        TAB 69 CREMNTTPSFED OF IFNOTE_CREDIT FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 69 TMP-SOUS-TOTAL FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 60 TMP-LABEL-TVQ &
        TAB 69 CREMNTTVQPRO OF IFNOTE_CREDIT FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 69 TMP-GRAND-TOTAL FLOAT "$" PIC "^^^^,^^^.^^" &

SKIP 2
BUILD if324zc
