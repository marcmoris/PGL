;/T/ Menu du système de inventaire
;
;/P/ Programmeur..: René Bonneau
;    Date création: 1996-03-27
;
;    Description..: Menu générale pour la inventaire
;
;-------------------------------------------------------------------------------
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN igm01 MENU &
             STOPSCREEN &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES CHANGE &
             RECEIVING T_FLGHLDOK

USE ussectmp NOLIST
    "IGM01"

USE igm01.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le holding est présent.

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      DESIGNER &
                      OPEN READ

;
; Pour la securite sur les compagnies.
;
FILE VSEC_USR         DESIGNER &
                      OPEN READ

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4                   ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_CIECLE    CHARACTER SIZE 4                   ; Popup sur les compagnies
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4
TEMPORARY T_PECDATFIN DATE
TEMPORARY T_PECDATDEB DATE



;-------------------------------------------------------------------------------
;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "IGL@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "IGP@" ; Code pour les procédures

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST ; Hilite pour tout les écrans

DRAW 1,20 TO 3,61
DRAW 2,1  TO 2,20
DRAW 2,61 TO 2,80

DRAW 2,1  TO 5,1
DRAW 2,80 TO 5,80

DRAW 5,1  TO 5,80

DRAW 5,40 TO 19,40
DRAW 19,1 TO 19,80

TITLE &
@IF FRANCAIS
      "I N V E N T A I R E" &
@ELSE
      "I N V E N T A I R E" &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

ALIGN(4,2,20) (,,26)

FIELD T_CIECLEMNU HIDDEN ID 05  &
      FIXED &
      UPSHIFT REQUIRED &
      PATTERN "((^|#)>)|!0" &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Company......:"  &
@ENDIF
        LOOKUP ON MCCOMPAGNIE &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 402, & ; Cette compagnie est inexistante

          ON VSEC_USR &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 424 ; Vous n'êtes pas autorisé a utiliser cette compagnie.

FIELD T_CIENOM &
        FIXED

SKIP TO 7

ALIGN (2,2)


SUBSCREEN ig001 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Inventaire des blocs                  "
@ELSE
                LABEL "%%%Inventaire des blocs               "
@ENDIF


SKIP 1


SUBSCREEN ig002 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Inventaire des tranches               "
@ELSE
                LABEL "%%%Inventaire des tranches            "
@ENDIF

SKIP 1


SUBSCREEN ig003 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Inventaire des diagrammes             "
@ELSE
                LABEL "%%%Inventaire des diagrammes          "
@ENDIF

SKIP 1


SUBSCREEN ig004 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Inventaire des caissons de projets   "
@ELSE
                LABEL "%%%Inventaire caissons de projets     "
@ENDIF

SKIP 1


SUBSCREEN ig005 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Inv. caissons de produits de dimension"
@ELSE
                LABEL "%%%Caissons de produits de dimension  "
@ENDIF


SKIP TO 7


ALIGN (42,42)


SUBSCREEN pd188 HIDDEN &
                MODE F &
                PASSING T_CIECLEMNU , &
                        T_CIENOM      &
@IF FRANCAIS
                LABEL "Tranche                               "
@ELSE
                LABEL "%%%Tranche                            "
@ENDIF


SKIP 1


SKIP TO 20

ALIGN(31,31)

SUBSCREEN pdm02 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L,   &
                        T_CIECLEMNU , &
                        T_CIENOM      &
@IF FRANCAIS
                LABEL " Listes et rapports "
@ELSE
                LABEL " Lists and Reports  "
@ENDIF

SUBSCREEN pdm03 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P  , &
                        T_CIECLEMNU , &
                        T_CIENOM      &
@IF FRANCAIS
                LABEL "    Procédures      "
@ELSE
                LABEL "    Procedures      "
@ENDIF

SUBSCREEN pdm04 HIDDEN &
                MODE F &
                PASSING T_CIECLEMNU , &
                        T_CIENOM      &
@IF FRANCAIS
                LABEL "Informations de base"
@ELSE
                LABEL " Basic Information  "
@ENDIF


TITLE &
@IF FRANCAIS
      "I N V E N T A I R E " &
@ELSE
      "%%%T I T R E   A N G L A I S" &
@ENDIF
      CENTERED AT 2,1


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE INITIALIZE
BEGIN
  ; Si le holding n'est pas défini,on retourne au menu principale.
  ;
  IF T_FLGHLDOK = '0'
    THEN RETURN

  USE ussececr NOLIST

  ACCEPT T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;  Dépisteur sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*")-1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
  ;
  ; A cause del l'option requis,si l'usager fait un retour et qu'il
  ; n'y a pas de valeur on remet la même.
  ;
  IF 0 = SIZE(FIELDTEXT) &
     AND &
     0 <> SIZE(TRUNCATE(T_CIECLEMNU))
  THEN BEGIN
    LET FIELDTEXT = T_CIECLEMNU
  END
END

;-------------------------------------------------------------------------------
;
; On conserve le nom de la compagnie du menu
;

PROCEDURE PROCESS T_CIECLEMNU
BEGIN
  LET T_CIENOM = CIENOM OF MCCOMPAGNIE
  DISPLAY T_CIENOM
END

;-------------------------------------------------------------------------------
;
; On demande la compagnie, il faut une procedure designer sur ce champ
; car l'option FIXED nous empèche par defaut de modifier le champ.
; l'option FIXED est due à un bug de powerhouse sur Unix.
;

PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_CIECLEMNU
END

BUILD
@IF FORMAT

@ENDIF
