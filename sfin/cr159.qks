;/T/ Paramètres passerelle
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 5 avril 1994
;
;    Description..: Les paramètres de passerelle servent lors de la passerelle
;                   d'entrée aux comptes à recevoir.  Ces paramètres permettent
;                   de définir le code d'escompte sur ventes et le code de
;                   prime sur ventes provenant des éléments facturables par
;                   compagnies.
;
;                   Seul un usager averti doit avoir accès à cet écran.
;
;
;-------------------------------------------------------------------------------
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr159 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES ENTRY  , &
                        FIND   , &
                        CHANGE   &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp NOLIST
    "CR159"

USE cr159.hlp NOLIST

USE usactecr NOLIST


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------
;

FILE CRPARAM_CAS   PRIMARY
  ACCESS VIA   CIECLE &
         USING T_CIECLEMNU

  ITEM CIECLE OF CRPARAM_CAS INITIAL T_CIECLEMNU


;
; Fichier de validation des codes d'escompte sur ventes.
;
FILE CRELE_FACT REFERENCE ALIAS A_CRELE_FACT_ESC
  ACCESS VIA   CIECLE, &
               ELECLE  &
         USING T_CIECLEMNU, &
               CPSCODESC OF CRPARAM_CAS


;
; Fichier de validation des codes de prime sur ventes.
;
FILE CRELE_FACT REFERENCE ALIAS A_CRELE_FACT_PRI
  ACCESS VIA   CIECLE, &
               ELECLE  &
         USING T_CIECLEMNU, &
               CPSCODPRI OF CRPARAM_CAS




;-------------------------------------------------------------------------------
;
TEMPORARY T_ELECLE CHARACTER SIZE 4 ; Code d'élément facturable

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tous les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Paramètres des passerelles " &
@ELSE
      " %%%Paramètres des passerelles " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 2

ALIGN (3,3,31) (,,36)

FIELD CPSCODESC OF CRPARAM_CAS HIDDEN ID 10 &
        REQUIRED &
        LOOKUP NOTON CRPARAM_CAS &
          VIA   CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 699, & ; Les paramètres sont déjà définis.
          ON A_CRELE_FACT_ESC &
            MESSAGE 2402  & ; Ce code d'élément facturable n'existe pas
@IF FRANCAIS
        LABEL "Code d'escompte sur ventes:"
@ELSE
        LABEL "%%%Code d'escompte sur ventes:"
@ENDIF


FIELD ELEDSC OF A_CRELE_FACT_ESC &
        DISPLAY


ALIGN (3,3,31) (,,36)

FIELD CPSCODPRI OF CRPARAM_CAS HIDDEN ID 15 &
        REQUIRED &
        LOOKUP ON A_CRELE_FACT_PRI &
          MESSAGE 2402  & ; Ce code d'élément facturable n'existe pas
@IF FRANCAIS
        LABEL "Code de prime sur ventes..:"
@ELSE
        LABEL "%%%Code de prime sur ventes..:"
@ENDIF


FIELD ELEDSC OF A_CRELE_FACT_PRI &
        DISPLAY



;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
; Ecran dépisteur sur le code d'escompte sur ventes.
;
PROCEDURE INPUT CPSCODESC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ELECLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cr160 MODE F PASSING T_CIECLEMNU, T_ELECLE &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_ELECLE)
  END
  ;
  ; Force l'éxécution du LOOKUP.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(CPSCODESC OF CRPARAM_CAS))
  THEN LET FIELDTEXT = CPSCODESC OF CRPARAM_CAS
END



;-------------------------------------------------------------------------------
; Ecran dépisteur sur le code de prime sur ventes.
;
PROCEDURE INPUT CPSCODPRI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ELECLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cr160 MODE F PASSING T_CIECLEMNU, T_ELECLE &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_ELECLE)
  END
  ;
  ; Force l'éxécution du LOOKUP.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(CPSCODPRI OF CRPARAM_CAS))
  THEN LET FIELDTEXT = CPSCODPRI OF CRPARAM_CAS
END



;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CRPARAM_CAS &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CRPARAM_CAS &
     AND NOT NEWRECORD     OF CRPARAM_CAS &
     AND NOT DELETEDRECORD OF CRPARAM_CAS &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************** Paramètres passerelle      **************************
*                                                                              *
*                                                                              *
* Code d'escompte sur ventes: xxxx                                             *
* Code de prime sur ventes..: xxxx                                             *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
