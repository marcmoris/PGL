;/T/ Génération des commandes d'achat à partir des réquisitions.
;
;
;/M/ Martin Perron / Modifier la MAJ de T_HENCLE_ANN avec T_HENCLE_TMP;
;    Date de Modification...: 30 novembre 1999
;
;/P/ Programmeur...: Guy Chabot
;    Date Création.: 20 septembre 1994
;
;    Description...: Permet de générer des commandes d'achat à partir
;                    des réquisitions.
;
;    Particularitée: On peut à partir des réquisitions générer plus d'une
;                    commande. Le numéro de la commande peut déjà avoir
;                    été réservé.
;
;    Paramètres....: Compagnie
;                    Fournisseur
;                    Numéro de réquisition
;
;/M/ Programmeur.......: Francois Richard
;    Date modification.: 15 Juin 1999
;    Description.......: Migration Achat/Réception/Inventaire de l'environnement
;                        BOC
;
;/M/ Francois Richard, 99-07-09
;    Ajustement pour le changement de siècle
;
;-----------------------------------------------------------------------------------------

USE ussetqts NOLIST   ; permet de faire les SET PROCESS NOLIMIT....

RUN ar702t

GLOBAL TEMPORARY G_PECCLE    CHARACTER SIZE 4 PARM ; Période d'engagement.
GLOBAL TEMPORARY G_ERREUR    CHARACTER SIZE 5      ; Pour stopper le traitement
GLOBAL TEMPORARY G_ANN       CHARACTER SIZE 02 INITIAL G_PECCLE[1:2]

;------------------------------------------------------------------------------
;
; Sélection des réquisitions par le détail pour permettre le CHOOSE sur le
; numéro de fournisseur.
;
; Les lignes de détail qui font l'objet de soumission ne sont pas considérées.
;
REQUEST SELECTION_REQUISITION

ACCESS ARREQUISITION   &
   ;
   ; Pour le type de produits
   ;
  LINK CIECLE    OF ARREQUISITION, &
       PRDCLE    OF ARREQUISITION  &
    TO CIECLE, &
       PRDCLE    OF ARPRODUIT   &

   ;
   ; Pour valider la période comptable au achat
   ;
  LINK CIECLE OF ARREQUISITION, &
       G_PECCLE                &
    TO CIECLE, &
       PECCLE  OF MCPERIODE_CIE


CHOOSE CIECLE    PARM, &
       REQCLE    PARM RANGE, &
       FOUCLE    PARM, &
       REQSTUAPP "A" , &
       REQFLGSOU "0"

TEMPORARY T_MESERR CHARACTER SIZE 80

SORT ON CIECLE OF ARREQUISITION &
     ON REQCLE OF ARREQUISITION

ITEM G_ERREUR = "ARRET" IF PCCSTUAR OF MCPERIODE_CIE = "F"
ITEM T_MESERR = "La période comptable est fermée..."

;
; Pour indiquer le message d'erreur.
;
SUBFILE WAR702ER KEEP AT FINAL IF G_ERREUR = "ARRET" &
  INCLUDE CIECLE OF ARREQUISITION, &
          G_PECCLE  ALIAS PECCLE, &
          T_MESERR

;
; Conserve le détail pour les numéros déjà réservé.
;
SUBFILE WAR702RS IF     0  <> SIZE(TRUNCATE(CMDCLE OF ARREQUISITION)) &
                    AND "ARRET" <> G_ERREUR &
  INCLUDE CIECLE       OF ARREQUISITION , &
          REQCLE       OF ARREQUISITION , &
          REQCLENUM    OF ARREQUISITION , &
          CMDCLE       OF ARREQUISITION , &
          FOUCIECLE    OF ARREQUISITION , &
          FOUCLE       OF ARREQUISITION , &
          PRDCLE       OF ARREQUISITION , &
          ENTCLE       OF ARREQUISITION , &
          PRJCLE       OF ARREQUISITION , &
          PRACLE       OF ARREQUISITION , &
          IMMCLE       OF ARREQUISITION , &
          CPTCLE       OF ARREQUISITION , &
          UNACLE       OF ARREQUISITION , &
          RADCLE       OF ARREQUISITION , &
          REQDATAPP    OF ARREQUISITION , &
          REQDATREQ    OF ARREQUISITION , &
          REQDSC       OF ARREQUISITION , &
          REQFAB       OF ARREQUISITION , &
          REQLIE       OF ARREQUISITION , &
          REQMNTCTI    OF ARREQUISITION , &
          REQMNTESC    OF ARREQUISITION , &
          REQMNTNET    OF ARREQUISITION , &
          REQMNTRTI    OF ARREQUISITION , &
          REQMNTTOT    OF ARREQUISITION , &
          REQMNTTPS    OF ARREQUISITION , &
          REQMNTTVQ    OF ARREQUISITION , &
          REQPRIUNI    OF ARREQUISITION , &
          REQQTEREQ    OF ARREQUISITION , &
          REQTAXCODTPS OF ARREQUISITION , &
          REQTAXCODTVQ OF ARREQUISITION , &
          REQTERESCACH OF ARREQUISITION , &
          REQTRP       OF ARREQUISITION , &
          ADECLE       OF ARREQUISITION , &
          PRDTYP       OF ARPRODUIT

;
; Conserve le détail pour les numéros non-connu.
;
SUBFILE WAR702CM IF      0  =  SIZE(TRUNCATE(CMDCLE OF ARREQUISITION)) &
                    AND "ARRET" <> G_ERREUR &
  INCLUDE CIECLE       OF ARREQUISITION , &
          REQCLE       OF ARREQUISITION , &
          REQCLENUM    OF ARREQUISITION , &
          CMDCLE       OF ARREQUISITION , &
          FOUCIECLE    OF ARREQUISITION , &
          FOUCLE       OF ARREQUISITION , &
          PRDCLE       OF ARREQUISITION , &
          ENTCLE       OF ARREQUISITION , &
          PRJCLE       OF ARREQUISITION , &
          PRACLE       OF ARREQUISITION , &
          IMMCLE       OF ARREQUISITION , &
          CPTCLE       OF ARREQUISITION , &
          UNACLE       OF ARREQUISITION , &
          RADCLE       OF ARREQUISITION , &
          REQDATAPP    OF ARREQUISITION , &
          REQDATREQ    OF ARREQUISITION , &
          REQDSC       OF ARREQUISITION , &
          REQFAB       OF ARREQUISITION , &
          REQLIE       OF ARREQUISITION , &
          REQMNTCTI    OF ARREQUISITION , &
          REQMNTESC    OF ARREQUISITION , &
          REQMNTNET    OF ARREQUISITION , &
          REQMNTRTI    OF ARREQUISITION , &
          REQMNTTOT    OF ARREQUISITION , &
          REQMNTTPS    OF ARREQUISITION , &
          REQMNTTVQ    OF ARREQUISITION , &
          REQPRIUNI    OF ARREQUISITION , &
          REQQTEREQ    OF ARREQUISITION , &
          REQTAXCODTPS OF ARREQUISITION , &
          REQTAXCODTVQ OF ARREQUISITION , &
          REQTERESCACH OF ARREQUISITION , &
          REQTRP       OF ARREQUISITION , &
          ADECLE       OF ARREQUISITION , &
          PRDTYP       OF ARPRODUIT
;
; Conserve tout les produits pour la mise à jour des qtes en demande.
;
SUBFILE WAR702PR &
  INCLUDE CIECLE    OF ARREQUISITION , &
          ENTCLE    OF ARREQUISITION , &
          PRDCLE    OF ARREQUISITION , &
          REQCLE    OF ARREQUISITION , &
          REQCLENUM OF ARREQUISITION , &
          PRDTYP    OF ARPRODUIT     , &
          REQQTEREQ OF ARREQUISITION
;
; Indique que la ligne a créée une commande.
;
OUTPUT ARREQUISITION UPDATE

  ITEM REQSTUAPP OF ARREQUISITION = "C"

;------------------------------------------------------------------------------
;
; Traitement des réquisitions qui possède déjà un numéro de commande.
;
REQUEST TRAITE_RESERVE TERMINATE IF G_ERREUR = "ARRET"

ACCESS *WAR702RS  &
   ;
   ; Pour la devise du fournisseur
   ;
  LINK FOUCIECLE  OF WAR702RS, &
       FOUCLE     OF WAR702RS, &
       CIECLE     OF WAR702RS  &
    TO FOUCIECLE, &
       FOUCLE   , &
       FOCCIECLE             OF VFOC_FOU   &

  LINK DEVCLE OF VFOC_FOU &
    TO DEVCLE                OF MCDEVISE   &

   ;
   ; Pour l'utilisation du numéro de commande.
   ;
  LINK  CIECLE OF WAR702RS, &
        CMDCLE OF WAR702RS  &
    TO  CIECLE, &
        CMDCLE            OF ARRESERVATION   OPTIONAL &


  LINK FOUCIECLE OF WAR702RS, &
       "F"                  , &
       FOUCLE    OF WAR702RS, &
       RADCLE    OF WAR702RS  &
    TO REFCIECLE            , &
       REFCLETYP            , &
       REFCLENUM            , &
       RADCLE                OF MCREF_ADRESSE   OPTIONAL

TEMPORARY T_CDECLE  INTEGER SIZE 4 ; Compteur pour les items
TEMPORARY T_CPT     INTEGER SIZE 2 ; Compteur d'item.

TEMPORARY T_MNTTOT     FLOAT SIZE 8
TEMPORARY T_MNTNET     FLOAT SIZE 8
TEMPORARY T_MNTTPS     FLOAT SIZE 8
TEMPORARY T_MNTTVQ     FLOAT SIZE 8
TEMPORARY T_MNTESC     FLOAT SIZE 8
TEMPORARY T_MNTRTI     FLOAT SIZE 8
TEMPORARY T_MNTCTI     FLOAT SIZE 8
TEMPORARY T_MNTENGDES  FLOAT SIZE 8
TEMPORARY T_HENDSCGEN  CHARACTER SIZE 40

ITEM T_HENDSCGEN = " " IF FOUTYP OF VFOC_FOU <> "D" &
               ELSE RADNOM OF MCREF_ADRESSE

; Ajustement pour le changement de siècle
DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = "1" + CMDCLE OF WAR702RS &
   IF CMDCLE OF WAR702RS[1:1] < "8" &
   ELSE "0" + CMDCLE OF WAR702RS

SORT ON CIECLE OF WAR702RS &
     ON D_CMDCLE_TRI       &
     ON REQCLE OF WAR702RS &
     ON REQCLENUM OF WAR702RS

ITEM T_CDECLE = T_CDECLE + 1 AT REQCLENUM RESET AT D_CMDCLE_TRI TO 1
ITEM T_CPT      COUNT                  RESET AT D_CMDCLE_TRI

ITEM T_MNTTOT SUBTOTAL REQMNTTOT OF WAR702RS RESET AT D_CMDCLE_TRI
ITEM T_MNTNET SUBTOTAL REQMNTNET OF WAR702RS RESET AT D_CMDCLE_TRI
ITEM T_MNTESC SUBTOTAL REQMNTESC OF WAR702RS RESET AT D_CMDCLE_TRI
ITEM T_MNTTPS SUBTOTAL REQMNTTPS OF WAR702RS RESET AT D_CMDCLE_TRI
ITEM T_MNTTVQ SUBTOTAL REQMNTTVQ OF WAR702RS RESET AT D_CMDCLE_TRI
ITEM T_MNTRTI SUBTOTAL REQMNTRTI OF WAR702RS RESET AT D_CMDCLE_TRI
ITEM T_MNTCTI SUBTOTAL REQMNTCTI OF WAR702RS RESET AT D_CMDCLE_TRI

ITEM T_MNTENGDES = ROUND((REQMNTTOT OF WAR702RS  - &
                          REQMNTCTI OF WAR702RS  - &
                          REQMNTRTI OF WAR702RS) * &
                          DEVTAU    OF MCDEVISE)

OUTPUT ARCMD_DETAIL      ADD &
                        NOITEMS
  ITEM CIECLE       OF ARCMD_DETAIL INITIAL CIECLE       OF WAR702RS
  ITEM CMDCLE       OF ARCMD_DETAIL INITIAL CMDCLE       OF WAR702RS
  ITEM CMDAJT       OF ARCMD_DETAIL INITIAL "000"
  ITEM REQCLE       OF ARCMD_DETAIL INITIAL REQCLE       OF WAR702RS
  ITEM PECCLE       OF ARCMD_DETAIL INITIAL G_PECCLE
  ITEM CDETRP       OF ARCMD_DETAIL INITIAL REQTRP       OF WAR702RS
  ITEM CDEFAB       OF ARCMD_DETAIL INITIAL REQFAB       OF WAR702RS
  ITEM CDELIE       OF ARCMD_DETAIL INITIAL REQLIE       OF WAR702RS
  ITEM CDEDATLIVPRE OF ARCMD_DETAIL INITIAL REQDATREQ    OF WAR702RS
  ITEM CDECLE       OF ARCMD_DETAIL INITIAL T_CDECLE * 1000
  ITEM PRDCLE       OF ARCMD_DETAIL INITIAL PRDCLE       OF WAR702RS
  ITEM PRDTYP       OF ARCMD_DETAIL INITIAL PRDTYP       OF WAR702RS
  ITEM CDEQTECOM    OF ARCMD_DETAIL INITIAL REQQTEREQ    OF WAR702RS
  ITEM CDEQTEREC    OF ARCMD_DETAIL INITIAL 0
  ITEM CDEQTEREJ    OF ARCMD_DETAIL INITIAL 0
  ITEM CDETAXTYPTPS OF ARCMD_DETAIL INITIAL "F"
  ITEM CDETAXCODTPS OF ARCMD_DETAIL INITIAL REQTAXCODTPS OF WAR702RS
  ITEM CDETAXTYPTVQ OF ARCMD_DETAIL INITIAL "P"
  ITEM CDETAXCODTVQ OF ARCMD_DETAIL INITIAL REQTAXCODTVQ OF WAR702RS
  ITEM CDETERESCACH OF ARCMD_DETAIL INITIAL REQTERESCACH OF WAR702RS
  ITEM CDESTU       OF ARCMD_DETAIL INITIAL " "
  ITEM CDEDSCPRD    OF ARCMD_DETAIL INITIAL REQDSC       OF WAR702RS
  ITEM CDEPRIUNI    OF ARCMD_DETAIL INITIAL REQPRIUNI    OF WAR702RS
  ITEM CPTCLE       OF ARCMD_DETAIL INITIAL CPTCLE       OF WAR702RS
  ITEM PRJCLE       OF ARCMD_DETAIL INITIAL PRJCLE       OF WAR702RS
  ITEM UNACLE       OF ARCMD_DETAIL INITIAL UNACLE       OF WAR702RS
  ITEM PRACLE       OF ARCMD_DETAIL INITIAL PRACLE       OF WAR702RS
  ITEM IMMCLE       OF ARCMD_DETAIL INITIAL IMMCLE       OF WAR702RS
  ITEM CDEFLGSUP    OF ARCMD_DETAIL INITIAL "0"
  ITEM CDEMNTTOT    OF ARCMD_DETAIL INITIAL REQMNTTOT    OF WAR702RS
  ITEM CDEMNTTPS    OF ARCMD_DETAIL INITIAL REQMNTTPS    OF WAR702RS
  ITEM CDEMNTTVQ    OF ARCMD_DETAIL INITIAL REQMNTTVQ    OF WAR702RS
  ITEM CDEMNTCTI    OF ARCMD_DETAIL INITIAL REQMNTCTI    OF WAR702RS
  ITEM CDEMNTRTI    OF ARCMD_DETAIL INITIAL REQMNTRTI    OF WAR702RS
  ITEM CDEMNTESC    OF ARCMD_DETAIL INITIAL REQMNTESC    OF WAR702RS
  ITEM CDEMNTENG    OF ARCMD_DETAIL INITIAL ROUND((REQMNTTOT    OF WAR702RS - &
                                                   REQMNTCTI    OF WAR702RS - &
                                                   REQMNTRTI    OF WAR702RS)  &
                                                   * DEVTAU OF MCDEVISE)
  ITEM CDEMNTDES    OF ARCMD_DETAIL INITIAL 0
  ITEM CDEMNTNET    OF ARCMD_DETAIL INITIAL REQMNTNET    OF WAR702RS
  ITEM DEVTAU       OF ARCMD_DETAIL INITIAL DEVTAU       OF MCDEVISE
  ITEM CDEDATIMP    OF ARCMD_DETAIL INITIAL 0



OUTPUT ARCOMMANDE        ADD AT D_CMDCLE_TRI &
                        NOITEMS

  ITEM CIECLE       OF ARCOMMANDE INITIAL CIECLE    OF WAR702RS
  ITEM ENTCLE       OF ARCOMMANDE INITIAL ENTCLE    OF WAR702RS
  ITEM CMDCLE       OF ARCOMMANDE INITIAL CMDCLE    OF WAR702RS
  ITEM ADECLE       OF ARCOMMANDE INITIAL ADECLE    OF WAR702RS
  ITEM CMDAJT       OF ARCOMMANDE INITIAL "000"
  ITEM CMDSTU       OF ARCOMMANDE INITIAL "A"
  ITEM CMDFLGIMP    OF ARCOMMANDE INITIAL "0"
  ITEM PECCLE       OF ARCOMMANDE INITIAL G_PECCLE
  ITEM FOUCIECLE    OF ARCOMMANDE INITIAL FOUCIECLE OF WAR702RS
  ITEM FOUCLE       OF ARCOMMANDE INITIAL FOUCLE    OF WAR702RS
  ITEM REFCLETYP    OF ARCOMMANDE INITIAL "F"
  ITEM RADCLE       OF ARCOMMANDE INITIAL RADCLE    OF WAR702RS
  ITEM CMDDAT       OF ARCOMMANDE INITIAL SYSDATE
  ITEM CMDDATAPP    OF ARCOMMANDE INITIAL REQDATAPP OF WAR702RS
  ITEM CMDUSRCRE    OF ARCOMMANDE INITIAL LOGONID
  ITEM CMDDATCRE    OF ARCOMMANDE INITIAL SYSDATE
  ITEM CMDHRECRE    OF ARCOMMANDE INITIAL SYSTIME / 10000
  ITEM CMDUSRMOD    OF ARCOMMANDE INITIAL LOGONID
  ITEM CMDDATMOD    OF ARCOMMANDE INITIAL SYSDATE
  ITEM CMDHREMOD    OF ARCOMMANDE INITIAL SYSTIME / 10000
  ITEM CMDACH       OF ARCOMMANDE INITIAL ""
  ITEM CMDLIE       OF ARCOMMANDE INITIAL ""
  ITEM CMDCOM       OF ARCOMMANDE INITIAL ""
  ITEM CMDNBRIMP    OF ARCOMMANDE INITIAL 0
  ITEM CMDNBRDUP    OF ARCOMMANDE INITIAL 0
  ITEM CMDNBRITECMP OF ARCOMMANDE INITIAL 0     
  ITEM CMDMNTTOT    OF ARCOMMANDE = T_MNTTOT
  ITEM CMDMNTTPS    OF ARCOMMANDE = T_MNTTPS
  ITEM CMDMNTTVQ    OF ARCOMMANDE = T_MNTTVQ
  ITEM CMDMNTCTI    OF ARCOMMANDE = T_MNTCTI
  ITEM CMDMNTRTI    OF ARCOMMANDE = T_MNTRTI
  ITEM CMDMNTESC    OF ARCOMMANDE = T_MNTESC
  ITEM CMDMNTNET    OF ARCOMMANDE = T_MNTNET
  ITEM CMDNBRITE    OF ARCOMMANDE = T_CPT
  ITEM CMDTYP       OF ARCOMMANDE INITIAL "CM"


OUTPUT ARPRD_HISTO   ADD &
       IF PRDTYP OF WAR702RS <> "D" AND &
          PRDTYP OF WAR702RS <> "F" NOITEMS

  ITEM CIECLE       OF ARPRD_HISTO INITIAL CIECLE OF WAR702RS
  ITEM PRDCLE       OF ARPRD_HISTO INITIAL PRDCLE OF WAR702RS
  ITEM PRDTYP       OF ARPRD_HISTO INITIAL PRDTYP OF WAR702RS
  ITEM PRHTIMSTP    OF ARPRD_HISTO INITIAL SYSDATE*1000000 + ROUND(SYSTIME/100)
  ITEM ENTCLE       OF ARPRD_HISTO INITIAL ENTCLE OF WAR702RS
  ITEM PECCLE       OF ARPRD_HISTO INITIAL G_PECCLE
  ITEM PRHNUMREF    OF ARPRD_HISTO INITIAL CMDCLE OF WAR702RS + "000"
  ITEM PRHDATREF    OF ARPRD_HISTO INITIAL SYSDATE
  ITEM PRHTYP       OF ARPRD_HISTO INITIAL "01"   ; Commande.
  ITEM PRHQTETRS    OF ARPRD_HISTO INITIAL REQQTEREQ OF WAR702RS
  ITEM PRHUSRCRE    OF ARPRD_HISTO INITIAL LOGONID
  ITEM PRHDATCRE    OF ARPRD_HISTO INITIAL SYSDATE
  ITEM PRHHRECRE    OF ARPRD_HISTO INITIAL SYSTIME / 10000
  ITEM PRHITM       OF ARPRD_HISTO INITIAL T_CDECLE * 1000
  ITEM PRHDERCOU    OF ARPRD_HISTO INITIAL REQPRIUNI OF WAR702RS
  ITEM FOUCIECLE    OF ARPRD_HISTO INITIAL FOUCIECLE OF WAR702RS
  ITEM FOUCLE       OF ARPRD_HISTO INITIAL FOUCLE    OF WAR702RS
  ITEM PRHDSC       OF ARPRD_HISTO INITIAL RADNOM    OF MCREF_ADRESSE


;
; Indique que le numéro réservé est utilisé.
;
OUTPUT ARRESERVATION    UPDATE AT D_CMDCLE_TRI &
                        IF RECORD ARRESERVATION EXISTS

  ITEM RESDATUTI OF ARRESERVATION = SYSDATE

;
; Subfile pour les impressions.
;
SUBFILE WAR702IM        KEEP AT D_CMDCLE_TRI &
        INCLUDE CIECLE       OF ARCOMMANDE, &
                ENTCLE       OF ARCOMMANDE, &
                FOUCIECLE    OF ARCOMMANDE, &
                FOUCLE       OF ARCOMMANDE, &
                REFCLETYP    OF ARCOMMANDE, &
                RADCLE       OF ARCOMMANDE, &
                REQCLE       OF ARCMD_DETAIL, &
                CMDCLE       OF ARCOMMANDE, &
                PECCLE       OF ARCOMMANDE, &
                CMDDAT       OF ARCOMMANDE, &
                CDEDATLIVPRE OF ARCMD_DETAIL, &
                CMDNBRITE    OF ARCOMMANDE, &
                CMDMNTTOT

;
; Subfile pour les impressions.
;
SUBFILE WAR702IM2       KEEP           &
        INCLUDE CIECLE       OF ARCOMMANDE, &
                ENTCLE       OF ARCOMMANDE, &
                FOUCIECLE    OF ARCOMMANDE, &
                FOUCLE       OF ARCOMMANDE, &
                REFCLETYP    OF ARCOMMANDE, &
                RADCLE       OF ARCOMMANDE, &
                REQCLE       OF ARCMD_DETAIL, &
                CDECLE       OF ARCMD_DETAIL, &
                REQMNTTOT    OF WAR702RS, &
                REQCLENUM    OF WAR702RS, &
                CMDCLE       OF ARCOMMANDE, &
                PECCLE       OF ARCOMMANDE, &
                CMDDAT       OF ARCOMMANDE, &
                CDEDATLIVPRE OF ARCMD_DETAIL, &
                CMDNBRITE    OF ARCOMMANDE

;
; Subfile pour les engagements.
;
SUBFILE WAR702EG        KEEP              &
        INCLUDE CIECLE       OF WAR702RS, &
                ENTCLE       OF WAR702RS, &
                FOUCIECLE    OF WAR702RS, &
                FOUCLE       OF WAR702RS, &
                CMDCLE       OF WAR702RS, &
                REQDSC       OF WAR702RS, &
                CPTCLE       OF WAR702RS, &
                UNACLE       OF WAR702RS, &
                PRJCLE       OF WAR702RS, &
                PRACLE       OF WAR702RS, &
                IMMCLE       OF WAR702RS, &
                DEVCLE       OF VFOC_FOU, &
                T_HENDSCGEN,              &
                T_MNTENGDES

;------------------------------------------------------------------------------
;
; Traitement des notes descriptives pour les réquisitions qui possède déjà
; un numéro de commande.
;
REQUEST TRAITE_RESERVE_NOTE

ACCESS *WAR702RS  &

  LINK  CIECLE     OF WAR702RS, &
        REQCLE     OF WAR702RS, &
        REQCLENUM  OF WAR702RS, &
        "C"                     &
    TO  CIECLE   , &
        REQCLE   , &
        REQCLENUM   , &
        REQSTUAPP  OF ARREQUISITION   &

   ;
   ; Pour les notes descriptives.
   ;
  LINK  CIECLE     OF WAR702RS, &
        REQCLE     OF WAR702RS, &
        REQCLENUM  OF WAR702RS  &
    TO  CIECLE   , &
        REQCLE   , &
        REQCLENUM     OF ARREQ_NOTE


TEMPORARY T_CDECLE      INTEGER SIZE 4 ; Compteur pour les items

; Ajustement pour le changement de siècle
DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = "1" + CMDCLE OF WAR702RS &
    IF CMDCLE OF WAR702RS[1:1] < "8" &
    ELSE "0" + CMDCLE OF WAR702RS

SORT ON CIECLE OF WAR702RS &
     ON D_CMDCLE_TRI &
     ON REQCLE OF WAR702RS &
     ON REQCLENUM OF WAR702RS &
     ON RQNSEQ OF ARREQ_NOTE

ITEM T_CDECLE = T_CDECLE + 1 AT REQCLENUM RESET AT D_CMDCLE_TRI TO 1

OUTPUT ARCDE_NOTE        ADD &
                        NOITEMS

  ITEM CIECLE OF ARCDE_NOTE INITIAL CIECLE OF WAR702RS
  ITEM CMDCLE OF ARCDE_NOTE INITIAL CMDCLE OF WAR702RS
  ITEM CMDAJT OF ARCDE_NOTE INITIAL "000"
  ITEM CDECLE OF ARCDE_NOTE INITIAL T_CDECLE * 1000
  ITEM CDNSEQ OF ARCDE_NOTE INITIAL RQNSEQ OF ARREQ_NOTE
  ITEM CDNDSC OF ARCDE_NOTE INITIAL RQNDSC OF ARREQ_NOTE
  ITEM CDNIMP OF ARCDE_NOTE INITIAL RQNIMP OF ARREQ_NOTE

;------------------------------------------------------------------------------
;
; Taritement des réquisitions qui ne possède pas de numéro de commande.
;
REQUEST TRAITE_NON_RESERVE

ACCESS *WAR702CM  &
   ;
   ; Pour la devise du fournisseur
   ;
  LINK  FOUCIECLE  OF WAR702CM, &
        FOUCLE     OF WAR702CM, &
        CIECLE     OF WAR702CM  &
    TO  FOUCIECLE, &
        FOUCLE   , &
        FOCCIECLE   OF VFOC_FOU   &

  LINK  DEVCLE OF VFOC_FOU &
    TO  DEVCLE      OF MCDEVISE   &

  LINK FOUCIECLE OF WAR702CM, &
       "F"                  , &
       FOUCLE    OF WAR702CM, &
       RADCLE    OF WAR702CM  &
    TO REFCIECLE            , &
       REFCLETYP            , &
       REFCLENUM            , &
       RADCLE    OF MCREF_ADRESSE   OPTIONAL &

   ;
   ; Pour la génération du numéro de commande.
   ;
  LINK  CIECLE OF WAR702CM, &
        G_PECCLE[1:2]       &
    TO  CIECLE   , &
        CMSANNCLE  OF ARCMD_SEQ   OPTIONAL

TEMPORARY T_CDECLE      INTEGER SIZE 4 ; Compteur pour les items
TEMPORARY T_CPT         INTEGER SIZE 2 ; Compteur d'item.
TEMPORARY T_CMDCLE      INTEGER SIZE 4 ; Numéro de commande.

TEMPORARY T_MNTTOT           FLOAT SIZE 08
TEMPORARY T_MNTNET           FLOAT SIZE 08
TEMPORARY T_MNTTPS           FLOAT SIZE 08
TEMPORARY T_MNTTVQ           FLOAT SIZE 08
TEMPORARY T_MNTESC           FLOAT SIZE 08
TEMPORARY T_MNTRTI           FLOAT SIZE 08
TEMPORARY T_MNTCTI           FLOAT SIZE 08
TEMPORARY T_MNTENGDES2       FLOAT SIZE 08
TEMPORARY T_HENDSCGEN2   CHARACTER SIZE 40

ITEM T_HENDSCGEN2 = RADNOM OF MCREF_ADRESSE   &
                 IF FOUTYP OF VFOC_FOU <> "D" &
               ELSE " "

SORT ON CIECLE    OF WAR702CM &
     ON ENTCLE    OF WAR702CM &
     ON FOUCIECLE OF WAR702CM &
     ON FOUCLE    OF WAR702CM &
     ON RADCLE    OF WAR702CM &
     ON ADECLE    OF WAR702CM &
     ON REQCLE    OF WAR702CM &
     ON REQCLENUM OF WAR702CM

ITEM T_CDECLE = T_CDECLE + 1 AT REQCLENUM RESET AT ADECLE TO 1
ITEM T_CPT      COUNT                  RESET AT ADECLE
ITEM T_CMDCLE = T_CMDCLE + 1 AT ADECLE RESET AT INITIAL TO CMSNUMSEQ OF ARCMD_SEQ + 1

ITEM T_MNTTOT SUBTOTAL REQMNTTOT OF WAR702CM RESET AT ADECLE
ITEM T_MNTNET SUBTOTAL REQMNTNET OF WAR702CM RESET AT ADECLE
ITEM T_MNTESC SUBTOTAL REQMNTESC OF WAR702CM RESET AT ADECLE
ITEM T_MNTTPS SUBTOTAL REQMNTTPS OF WAR702CM RESET AT ADECLE
ITEM T_MNTTVQ SUBTOTAL REQMNTTVQ OF WAR702CM RESET AT ADECLE
ITEM T_MNTRTI SUBTOTAL REQMNTRTI OF WAR702CM RESET AT ADECLE
ITEM T_MNTCTI SUBTOTAL REQMNTCTI OF WAR702CM RESET AT ADECLE

ITEM T_MNTENGDES2 = ROUND((REQMNTTOT    OF WAR702CM - &
                           REQMNTCTI    OF WAR702CM - &
                           REQMNTRTI    OF WAR702CM)  &
                           * DEVTAU OF MCDEVISE)

OUTPUT ARCMD_DETAIL      ADD &
                        NOITEMS
  ITEM CIECLE       OF ARCMD_DETAIL INITIAL CIECLE       OF WAR702CM
  ITEM CMDCLE       OF ARCMD_DETAIL = G_PECCLE[1:2] + &
                                      ASCII(T_CMDCLE,CMSLONNUM OF ARCMD_SEQ)
  ITEM CMDAJT       OF ARCMD_DETAIL = "000"
  ITEM REQCLE       OF ARCMD_DETAIL INITIAL REQCLE       OF WAR702CM
  ITEM PECCLE       OF ARCMD_DETAIL INITIAL G_PECCLE
  ITEM CDETRP       OF ARCMD_DETAIL INITIAL REQTRP       OF WAR702CM
  ITEM CDEFAB       OF ARCMD_DETAIL INITIAL REQFAB       OF WAR702CM
  ITEM CDEDATLIVPRE OF ARCMD_DETAIL INITIAL REQDATREQ    OF WAR702CM
  ITEM CDELIE       OF ARCMD_DETAIL INITIAL REQLIE       OF WAR702CM
  ITEM CDECLE       OF ARCMD_DETAIL = T_CDECLE * 1000
  ITEM PRDCLE       OF ARCMD_DETAIL INITIAL PRDCLE       OF WAR702CM
  ITEM PRDTYP       OF ARCMD_DETAIL INITIAL PRDTYP       OF WAR702CM
  ITEM CDEQTECOM    OF ARCMD_DETAIL INITIAL REQQTEREQ    OF WAR702CM
  ITEM CDEQTEREC    OF ARCMD_DETAIL INITIAL 0
  ITEM CDEQTEREJ    OF ARCMD_DETAIL INITIAL 0
  ITEM CDETAXTYPTPS OF ARCMD_DETAIL INITIAL "F"
  ITEM CDETAXCODTPS OF ARCMD_DETAIL INITIAL REQTAXCODTPS OF WAR702CM
  ITEM CDETAXTYPTVQ OF ARCMD_DETAIL INITIAL "P"
  ITEM CDETAXCODTVQ OF ARCMD_DETAIL INITIAL REQTAXCODTVQ OF WAR702CM
  ITEM CDETERESCACH OF ARCMD_DETAIL INITIAL REQTERESCACH OF WAR702CM
  ITEM CDESTU       OF ARCMD_DETAIL INITIAL " "
  ITEM CDEDSCPRD    OF ARCMD_DETAIL INITIAL REQDSC       OF WAR702CM
  ITEM CDEPRIUNI    OF ARCMD_DETAIL INITIAL REQPRIUNI    OF WAR702CM
  ITEM CPTCLE       OF ARCMD_DETAIL INITIAL CPTCLE       OF WAR702CM
  ITEM PRJCLE       OF ARCMD_DETAIL INITIAL PRJCLE       OF WAR702CM
  ITEM UNACLE       OF ARCMD_DETAIL INITIAL UNACLE       OF WAR702CM
  ITEM PRACLE       OF ARCMD_DETAIL INITIAL PRACLE       OF WAR702CM
  ITEM IMMCLE       OF ARCMD_DETAIL INITIAL IMMCLE       OF WAR702CM
  ITEM CDEFLGSUP    OF ARCMD_DETAIL INITIAL "0"
  ITEM CDEMNTTOT    OF ARCMD_DETAIL INITIAL REQMNTTOT    OF WAR702CM
  ITEM CDEMNTTPS    OF ARCMD_DETAIL INITIAL REQMNTTPS    OF WAR702CM
  ITEM CDEMNTTVQ    OF ARCMD_DETAIL INITIAL REQMNTTVQ    OF WAR702CM
  ITEM CDEMNTCTI    OF ARCMD_DETAIL INITIAL REQMNTCTI    OF WAR702CM
  ITEM CDEMNTRTI    OF ARCMD_DETAIL INITIAL REQMNTRTI    OF WAR702CM
  ITEM CDEMNTESC    OF ARCMD_DETAIL INITIAL REQMNTESC    OF WAR702CM
  ITEM CDEMNTENG    OF ARCMD_DETAIL INITIAL REQMNTTOT    OF WAR702CM - &
                                            REQMNTCTI    OF WAR702CM - &
                                            REQMNTRTI    OF WAR702CM
  ITEM CDEMNTDES    OF ARCMD_DETAIL INITIAL 0
  ITEM CDEMNTNET    OF ARCMD_DETAIL INITIAL REQMNTNET    OF WAR702CM
  ITEM DEVTAU       OF ARCMD_DETAIL INITIAL DEVTAU       OF MCDEVISE
  ITEM CDEDATIMP    OF ARCMD_DETAIL INITIAL 0

OUTPUT ARCOMMANDE        ADD AT ADECLE &
                        NOITEMS

  ITEM CIECLE       OF ARCOMMANDE INITIAL CIECLE    OF WAR702CM
  ITEM ENTCLE       OF ARCOMMANDE INITIAL ENTCLE    OF WAR702CM
  ITEM ADECLE       OF ARCOMMANDE INITIAL ADECLE    OF WAR702CM
  ITEM CMDCLE       OF ARCOMMANDE = G_PECCLE[1:2] + &
                                    ASCII(T_CMDCLE,CMSLONNUM OF ARCMD_SEQ)
  ITEM CMDAJT       OF ARCOMMANDE INITIAL "000"
  ITEM CMDSTU       OF ARCOMMANDE INITIAL "A"
  ITEM CMDFLGIMP    OF ARCOMMANDE INITIAL "0"
  ITEM PECCLE       OF ARCOMMANDE INITIAL G_PECCLE
  ITEM FOUCIECLE    OF ARCOMMANDE INITIAL FOUCIECLE OF WAR702CM
  ITEM FOUCLE       OF ARCOMMANDE INITIAL FOUCLE    OF WAR702CM
  ITEM REFCLETYP    OF ARCOMMANDE INITIAL "F"
  ITEM RADCLE       OF ARCOMMANDE INITIAL RADCLE    OF WAR702CM
  ITEM CMDDAT       OF ARCOMMANDE INITIAL SYSDATE
  ITEM CMDDATAPP    OF ARCOMMANDE INITIAL REQDATAPP OF WAR702CM
  ITEM CMDUSRCRE    OF ARCOMMANDE INITIAL LOGONID
  ITEM CMDDATCRE    OF ARCOMMANDE INITIAL SYSDATE
  ITEM CMDHRECRE    OF ARCOMMANDE INITIAL SYSTIME / 10000
  ITEM CMDUSRMOD    OF ARCOMMANDE INITIAL LOGONID
  ITEM CMDDATMOD    OF ARCOMMANDE INITIAL SYSDATE
  ITEM CMDHREMOD    OF ARCOMMANDE INITIAL SYSTIME / 10000
  ITEM CMDACH       OF ARCOMMANDE INITIAL ""
  ITEM CMDLIE       OF ARCOMMANDE INITIAL ""
  ITEM CMDCOM       OF ARCOMMANDE INITIAL "" ;REQCOM       OF ARREQUISITION ;SD
  ITEM CMDMNTTOT    OF ARCOMMANDE = T_MNTTOT
  ITEM CMDMNTNET    OF ARCOMMANDE = T_MNTNET
  ITEM CMDCLE       OF ARCMD_DETAIL = G_PECCLE[1:2] + &
                                      ASCII(T_CMDCLE,CMSLONNUM OF ARCMD_SEQ)
  ITEM CMDMNTTPS    OF ARCOMMANDE = T_MNTTPS
  ITEM CMDMNTTVQ    OF ARCOMMANDE = T_MNTTVQ
  ITEM CMDMNTCTI    OF ARCOMMANDE = T_MNTCTI
  ITEM CMDMNTRTI    OF ARCOMMANDE = T_MNTRTI
  ITEM CMDMNTESC    OF ARCOMMANDE = T_MNTESC
  ITEM CMDNBRITE    OF ARCOMMANDE = T_CPT
  ITEM CMDNBRITECMP OF ARCOMMANDE INITIAL 0     
  ITEM CMDNBRIMP    OF ARCOMMANDE INITIAL 0
  ITEM CMDNBRDUP    OF ARCOMMANDE INITIAL 0
  ITEM CMDTYP       OF ARCOMMANDE INITIAL "CM"


OUTPUT *WAR702EG ADD
   ITEM CIECLE       OF WAR702EG FINAL CIECLE      OF WAR702CM
   ITEM ENTCLE       OF WAR702EG FINAL ENTCLE      OF WAR702CM
   ITEM FOUCIECLE    OF WAR702EG FINAL FOUCIECLE   OF WAR702CM
   ITEM FOUCLE       OF WAR702EG FINAL FOUCLE      OF WAR702CM
   ITEM CMDCLE       OF WAR702EG FINAL G_PECCLE[1:2] + &
                                       ASCII(T_CMDCLE,CMSLONNUM OF ARCMD_SEQ)
   ITEM REQDSC       OF WAR702EG FINAL REQDSC      OF WAR702CM
   ITEM CPTCLE       OF WAR702EG FINAL CPTCLE      OF WAR702CM
   ITEM UNACLE       OF WAR702EG FINAL UNACLE      OF WAR702CM
   ITEM PRJCLE       OF WAR702EG FINAL PRJCLE      OF WAR702CM
   ITEM PRACLE       OF WAR702EG FINAL PRACLE      OF WAR702CM
   ITEM IMMCLE       OF WAR702EG FINAL IMMCLE      OF WAR702CM
   ITEM DEVCLE       OF WAR702EG FINAL DEVCLE      OF VFOC_FOU
   ITEM T_HENDSCGEN  OF WAR702EG FINAL T_HENDSCGEN2
   ITEM T_MNTENGDES  OF WAR702EG FINAL T_MNTENGDES2

OUTPUT ARPRD_HISTO   ADD &
       IF PRDTYP OF WAR702CM <> "D" AND &
          PRDTYP OF WAR702CM <> "F" NOITEMS

  ITEM CIECLE       OF ARPRD_HISTO = CIECLE OF WAR702CM
  ITEM PRDCLE       OF ARPRD_HISTO = PRDCLE OF WAR702CM
  ITEM PRDTYP       OF ARPRD_HISTO = PRDTYP OF WAR702CM
  ITEM PRHTIMSTP    OF ARPRD_HISTO = SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  ITEM ENTCLE       OF ARPRD_HISTO = ENTCLE OF WAR702CM
  ITEM PECCLE       OF ARPRD_HISTO = G_PECCLE
  ITEM PRHNUMREF    OF ARPRD_HISTO = G_PECCLE[1:2] + &
                                     ASCII(T_CMDCLE,CMSLONNUM OF ARCMD_SEQ) + "000"
  ITEM PRHDATREF    OF ARPRD_HISTO = SYSDATE
  ITEM PRHTYP       OF ARPRD_HISTO = "01"   ; Commande.
  ITEM PRHQTETRS    OF ARPRD_HISTO = REQQTEREQ OF WAR702CM
  ITEM PRHUSRCRE    OF ARPRD_HISTO = LOGONID
  ITEM PRHDATCRE    OF ARPRD_HISTO = SYSDATE
  ITEM PRHHRECRE    OF ARPRD_HISTO = SYSTIME / 10000
  ITEM PRHITM       OF ARPRD_HISTO INITIAL T_CDECLE * 1000
  ITEM PRHDERCOU    OF ARPRD_HISTO = REQPRIUNI OF WAR702CM
  ITEM FOUCIECLE    OF ARPRD_HISTO = FOUCIECLE OF WAR702CM
  ITEM FOUCLE       OF ARPRD_HISTO = FOUCLE    OF WAR702CM
  ITEM PRHDSC       OF ARPRD_HISTO = RADNOM    OF MCREF_ADRESSE


;
; Mise à jour du dernier numéro utilisé dans le fichier. (# -1)
;
OUTPUT ARCMD_SEQ     ADD UPDATE AT FINAL

  ITEM CIECLE    OF ARCMD_SEQ INITIAL CIECLE OF WAR702CM
  ITEM CMSANNCLE OF ARCMD_SEQ INITIAL G_PECCLE[1:2]
  ITEM CMSNUMSEQ OF ARCMD_SEQ FINAL T_CMDCLE - 1

;
; Mise à jour du SUBFILE pour conserver le numéro de commande généré
; pour la création des notes descriptives.
;
OUTPUT WAR702CM UPDATE
  ITEM CMDCLE OF WAR702CM FINAL G_PECCLE[1:2] + &
                                ASCII(T_CMDCLE,CMSLONNUM OF ARCMD_SEQ)

;
; Subfile pour les impressions.
;
OUTPUT *WAR702IM ADD AT ADECLE
  ITEM CIECLE       OF WAR702IM FINAL CIECLE       OF ARCOMMANDE
  ITEM ENTCLE       OF WAR702IM FINAL ENTCLE       OF ARCOMMANDE
  ITEM FOUCIECLE    OF WAR702IM FINAL FOUCIECLE    OF ARCOMMANDE
  ITEM FOUCLE       OF WAR702IM FINAL FOUCLE       OF ARCOMMANDE
  ITEM REFCLETYP    OF WAR702IM FINAL REFCLETYP    OF ARCOMMANDE
  ITEM RADCLE       OF WAR702IM FINAL RADCLE       OF ARCOMMANDE
  ITEM REQCLE       OF WAR702IM FINAL REQCLE       OF ARCMD_DETAIL
  ITEM CMDCLE       OF WAR702IM FINAL CMDCLE       OF ARCOMMANDE
  ITEM PECCLE       OF WAR702IM FINAL PECCLE       OF ARCOMMANDE
  ITEM CMDDAT       OF WAR702IM FINAL CMDDAT       OF ARCOMMANDE
  ITEM CDEDATLIVPRE OF WAR702IM FINAL CDEDATLIVPRE OF ARCMD_DETAIL
  ITEM CMDNBRITE    OF WAR702IM FINAL CMDNBRITE    OF ARCOMMANDE
  ITEM CMDMNTTOT    OF WAR702IM FINAL CMDMNTTOT    OF ARCOMMANDE


;
; Subfile pour les impressions.
;
OUTPUT *WAR702IM2 ADD
  ITEM CIECLE       OF WAR702IM2 FINAL CIECLE       OF ARCOMMANDE
  ITEM ENTCLE       OF WAR702IM2 FINAL ENTCLE       OF ARCOMMANDE
  ITEM FOUCIECLE    OF WAR702IM2 FINAL FOUCIECLE    OF ARCOMMANDE
  ITEM FOUCLE       OF WAR702IM2 FINAL FOUCLE       OF ARCOMMANDE
  ITEM REFCLETYP    OF WAR702IM2 FINAL REFCLETYP    OF ARCOMMANDE
  ITEM RADCLE       OF WAR702IM2 FINAL RADCLE       OF ARCOMMANDE
  ITEM REQCLE       OF WAR702IM2 FINAL REQCLE       OF ARCMD_DETAIL
  ITEM CDECLE       OF WAR702IM2 FINAL CDECLE       OF ARCMD_DETAIL
  ITEM REQMNTTOT    OF WAR702IM2 FINAL REQMNTTOT    OF WAR702CM
  ITEM REQCLENUM    OF WAR702IM2 FINAL REQCLENUM    OF WAR702CM
  ITEM CMDCLE       OF WAR702IM2 FINAL CMDCLE       OF ARCOMMANDE
  ITEM PECCLE       OF WAR702IM2 FINAL PECCLE       OF ARCOMMANDE
  ITEM CMDDAT       OF WAR702IM2 FINAL CMDDAT       OF ARCOMMANDE
  ITEM CDEDATLIVPRE OF WAR702IM2 FINAL CDEDATLIVPRE OF ARCMD_DETAIL
  ITEM CMDNBRITE    OF WAR702IM2 FINAL CMDNBRITE    OF ARCOMMANDE

;------------------------------------------------------------------------------
;
; Traitement des notes descriptives pour les réquisitions qui ne possèdent
; pas de numéro de commande.
;
REQUEST TRAITE_NON_RESERVE_NOTE

ACCESS *WAR702CM  &
   ;
   ; Pour les notes descriptives.
   ;
  LINK  CIECLE     OF WAR702CM, &
        REQCLE     OF WAR702CM, &
        REQCLENUM     OF WAR702CM  &
    TO  CIECLE   , &
        REQCLE   , &
        REQCLENUM     OF ARREQ_NOTE

TEMPORARY T_CDECLE      INTEGER SIZE 4 ; Compteur pour les items

; Ajustement pour le changement de siècle
DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = "1" + CMDCLE OF WAR702CM &
     IF CMDCLE OF WAR702CM[1:1] < "8" &
     ELSE "0" + CMDCLE OF WAR702CM

SORT ON CIECLE OF WAR702CM &
     ON D_CMDCLE_TRI &
     ON REQCLENUM OF WAR702CM &
     ON RQNSEQ OF ARREQ_NOTE

ITEM T_CDECLE = T_CDECLE + 1 AT REQCLENUM RESET AT D_CMDCLE_TRI TO 1

OUTPUT ARCDE_NOTE        ADD &
                        NOITEMS

  ITEM CIECLE OF ARCDE_NOTE INITIAL CIECLE OF WAR702CM
  ITEM CMDCLE OF ARCDE_NOTE INITIAL CMDCLE OF WAR702CM
  ITEM CMDAJT OF ARCDE_NOTE INITIAL "000"
  ITEM CDECLE OF ARCDE_NOTE = T_CDECLE * 1000
  ITEM CDNSEQ OF ARCDE_NOTE INITIAL RQNSEQ OF ARREQ_NOTE
  ITEM CDNDSC OF ARCDE_NOTE INITIAL RQNDSC OF ARREQ_NOTE
  ITEM CDNIMP OF ARCDE_NOTE INITIAL RQNIMP OF ARREQ_NOTE

;------------------------------------------------------------------------------
;
; Traitement de la mise à jour du numéro de commande sur la ligne de
; réquisition.
;
REQUEST MAJ_LIGNE_REQUISITION

ACCESS *WAR702CM  &
   ;
   ; Pour les notes descriptives.
   ;
  LINK  CIECLE     OF WAR702CM, &
        REQCLE     OF WAR702CM, &
        REQCLENUM     OF WAR702CM  &
    TO  CIECLE   , &
        REQCLE   , &
        REQCLENUM     OF ARREQUISITION

OUTPUT ARREQUISITION UPDATE

  ITEM CMDCLE OF ARREQUISITION = CMDCLE OF WAR702CM


;------------------------------------------------------------------------------
;
; Mise à jour de l'engagement
;
REQUEST MAJ_ENGAGEMENT

ACCESS *WAR702EG  &

  LINK "1", &                           ;
       G_ANN &                          ; Pour le numéro d'écriture.
    TO CIENMC, &                        ;
       HSECLEANN OF  MCHIS_SEQ_ENG   OPTIONAL

TEMPORARY T_HENCLE_ANN INTEGER UNSIGNED SIZE 04
TEMPORARY T_HENCLE_TMP CHARACTER SIZE 09
TEMPORARY T_HENCLE    INTEGER UNSIGNED SIZE 04
TEMPORARY T_NUMSEQ2   INTEGER UNSIGNED SIZE 04
TEMPORARY T_NUMLIG    INTEGER UNSIGNED SIZE 02
TEMPORARY T_MNTENGDES2           FLOAT SIZE 08
TEMPORARY T_MNTENGDES3           FLOAT SIZE 08

; Ajustement pour le changement de siècle
DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = "1" + CMDCLE OF WAR702EG &
    IF CMDCLE OF WAR702EG[1:1] < "8" &
    ELSE "0" + CMDCLE OF WAR702EG

SORT ON D_CMDCLE_TRI, &
        CPTCLE   OF WAR702EG, &
        CIECLE   OF WAR702EG, &
        UNACLE   OF WAR702EG, &
        PRJCLE   OF WAR702EG, &
        PRACLE   OF WAR702EG

ITEM T_NUMSEQ2 INITIAL 0 & ;NCONVERT( G_ANN ) * 10000000 &
                        IF NOT RECORD MCHIS_SEQ_ENG EXIST &
                 ELSE HSENUMSEQ OF MCHIS_SEQ_ENG

ITEM T_HENCLE RESET AT INITIAL TO (T_NUMSEQ2 + 1)

ITEM T_HENCLE COUNT AT D_CMDCLE_TRI

ITEM T_HENCLE_TMP = ASCII(T_HENCLE,9)
ITEM T_HENCLE_ANN = NCONVERT((G_ANN + T_HENCLE_TMP[3:7]))

ITEM T_NUMLIG = T_NUMLIG + 1 RESET AT D_CMDCLE_TRI TO 0

ITEM T_MNTENGDES2 = T_MNTENGDES2 + T_MNTENGDES OF WAR702EG &
                    RESET AT UNACLE TO 0

ITEM T_MNTENGDES3 = T_MNTENGDES3 + T_MNTENGDES OF WAR702EG &
                    RESET AT PRACLE TO 0

OUTPUT MCHISTO_ENG  ADD AT D_CMDCLE_TRI
  ITEM HENCLE      OF MCHISTO_ENG = T_HENCLE_ANN
  ITEM HENCIECLE   OF MCHISTO_ENG INITIAL CIECLE      OF WAR702EG
  ITEM REFCIECLE   OF MCHISTO_ENG INITIAL FOUCIECLE   OF WAR702EG
  ITEM REFCLETYP   OF MCHISTO_ENG INITIAL "F"
  ITEM REFCLENUM   OF MCHISTO_ENG INITIAL FOUCLE      OF WAR702EG
  ITEM HENNUMDOC   OF MCHISTO_ENG INITIAL CMDCLE      OF WAR702EG + "000"
  ITEM SANANN      OF MCHISTO_ENG INITIAL G_PECCLE[1:2]
  ITEM PECCLE      OF MCHISTO_ENG INITIAL G_PECCLE
  ITEM HENDAT      OF MCHISTO_ENG INITIAL SYSDATE
  ITEM HENDATJOU   OF MCHISTO_ENG INITIAL 0
  ITEM HENCODMOD   OF MCHISTO_ENG INITIAL "AR"
  ITEM HENTYPECR   OF MCHISTO_ENG INITIAL "CM"
  ITEM HENDSCGEN   OF MCHISTO_ENG INITIAL T_HENDSCGEN
  ITEM HENDSCSAI   OF MCHISTO_ENG INITIAL REQDSC      OF WAR702EG
  ITEM HENDSCSAI2  OF MCHISTO_ENG INITIAL " "
  ITEM DEVCLE      OF MCHISTO_ENG INITIAL DEVCLE      OF WAR702EG
  ITEM HENCLE1     OF MCHISTO_ENG INITIAL CIECLE      OF WAR702EG
  ITEM HENCLE2     OF MCHISTO_ENG INITIAL CMDCLE      OF WAR702EG
  ITEM HENCLE3     OF MCHISTO_ENG INITIAL "000"

OUTPUT MCHIS_ECR_ENG  ADD AT UNACLE
  ITEM HENCLE       OF MCHIS_ECR_ENG = T_HENCLE_ANN
  ITEM CPTCLE       OF MCHIS_ECR_ENG INITIAL CPTCLE      OF WAR702EG
  ITEM HEECIEINT    OF MCHIS_ECR_ENG INITIAL CIECLE      OF WAR702EG
  ITEM HEEUNAINT    OF MCHIS_ECR_ENG INITIAL UNACLE      OF WAR702EG
  ITEM CIECLE       OF MCHIS_ECR_ENG INITIAL CIECLE      OF WAR702EG
  ITEM UNACLE       OF MCHIS_ECR_ENG INITIAL UNACLE      OF WAR702EG
  ITEM PECCLE       OF MCHIS_ECR_ENG INITIAL G_PECCLE
  ITEM HEENUMDOC2   OF MCHIS_ECR_ENG INITIAL " "

  ITEM HEEMNTDBT    OF MCHIS_ECR_ENG =       T_MNTENGDES2       &
                                          IF T_MNTENGDES2 >= 0  &
                                        ELSE 0

  ITEM HEEMNTCRT    OF MCHIS_ECR_ENG =       ABSOLUTE(T_MNTENGDES2) &
                                          IF T_MNTENGDES2 < 0       &
                                        ELSE 0

  ITEM HEEMNTDBTORI OF MCHIS_ECR_ENG =       T_MNTENGDES2       &
                                          IF T_MNTENGDES2 >= 0  &
                                        ELSE 0

  ITEM HEEMNTCRTORI OF MCHIS_ECR_ENG =       ABSOLUTE(T_MNTENGDES2) &
                                          IF T_MNTENGDES2 < 0       &
                                        ELSE 0

  ITEM HEEFLGINT    OF MCHIS_ECR_ENG INITIAL "0"

OUTPUT MCHIS_PROJET_ENG  ADD AT PRACLE
  ITEM HENCLE    OF MCHIS_PROJET_ENG = T_HENCLE_ANN
  ITEM HPENUMLIG OF MCHIS_PROJET_ENG INITIAL T_NUMLIG
  ITEM CIECLE    OF MCHIS_PROJET_ENG INITIAL CIECLE      OF WAR702EG
  ITEM CPTCLE    OF MCHIS_PROJET_ENG INITIAL CPTCLE      OF WAR702EG
  ITEM PRJCLE    OF MCHIS_PROJET_ENG INITIAL PRJCLE      OF WAR702EG
  ITEM PRACLE    OF MCHIS_PROJET_ENG INITIAL PRACLE      OF WAR702EG
  ITEM IMMCLE    OF MCHIS_PROJET_ENG INITIAL IMMCLE      OF WAR702EG
  ITEM UNACLE    OF MCHIS_PROJET_ENG INITIAL UNACLE      OF WAR702EG
  ITEM PECCLE    OF MCHIS_PROJET_ENG INITIAL G_PECCLE

  ITEM HPEMNTDBT    OF MCHIS_PROJET_ENG =    T_MNTENGDES3       &
                                          IF T_MNTENGDES3 >= 0  &
                                        ELSE 0

  ITEM HPEMNTCRT    OF MCHIS_PROJET_ENG =    ABSOLUTE(T_MNTENGDES3) &
                                          IF T_MNTENGDES3 < 0       &
                                        ELSE 0

  ITEM HPEMNTDBTORI OF MCHIS_PROJET_ENG =    T_MNTENGDES3       &
                                          IF T_MNTENGDES3 >= 0  &
                                        ELSE 0

  ITEM HPEMNTCRTORI OF MCHIS_PROJET_ENG =    ABSOLUTE(T_MNTENGDES3) &
                                          IF T_MNTENGDES3 < 0       &
                                        ELSE 0


OUTPUT ARCOMMANDE  UPDATE AT D_CMDCLE_TRI &
       VIA   CIECLE,               &
             CMDCLE,               &
             CMDAJT                &
       USING CIECLE OF WAR702EG,   &
             CMDCLE OF WAR702EG,   &
             "000"
  ITEM HENCLE    OF ARCOMMANDE = T_HENCLE_ANN


OUTPUT MCHIS_SEQ_ENG  ADD UPDATE AT FINAL
  ITEM CIENMC    OF MCHIS_SEQ_ENG INITIAL "1"
  ITEM HSECLEANN OF MCHIS_SEQ_ENG INITIAL G_ANN
  ITEM HSENUMSEQ OF MCHIS_SEQ_ENG FINAL   ( T_HENCLE  - 1 )


;------------------------------------------------------------------------------
;
; Mise à jour des quantités en demande des produits de type I,N
;
REQUEST MAJ_QTE_DEMANDE

ACCESS *WAR702PR  &

  LINK  CIECLE     OF WAR702PR, &
        REQCLE     OF WAR702PR, &
        REQCLENUM     OF WAR702PR, &
        "C"                     &
    TO  CIECLE   , &
        REQCLE   , &
        REQCLENUM   , &
        REQSTUAPP  OF ARREQUISITION   &

  LINK  CIECLE OF WAR702PR, &
        PRDCLE OF WAR702PR, &
        ENTCLE OF WAR702PR  &
    TO  CIECLE, &
        PRDCLE, &
        ENTCLE OF ARPRD_ENT

TEMPORARY T_QTEREQ FLOAT SIZE 8 ; Qte requise à déduire

SORT ON CIECLE OF WAR702PR &
     ON PRDCLE OF WAR702PR &
     ON ENTCLE OF WAR702PR

ITEM T_QTEREQ SUBTOTAL REQQTEREQ OF WAR702PR RESET AT ENTCLE

OUTPUT ARPRD_ENT UPDATE AT ENTCLE IF PRDTYP OF WAR702PR <> "D" AND &
                                     PRDTYP OF WAR702PR <> "F"

  ITEM PREQTEDEM OF ARPRD_ENT = PREQTEDEM OF ARPRD_ENT - T_QTEREQ


BUILD
