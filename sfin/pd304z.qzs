;/T/ 1.4.5.4 P/O d'achats avec réception à venir.
;
;/P/ Programmeur...: René Bonneau
;    Date Création.: 1994-10-19
;
;    Description...: Liste de base des bons de commande d'achats avec réception
;                    à venir.
;
;    Paramètres....:
;
;    Particularités: AUCUNE
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS PDBON_COMMANDE                      &

  LINK CIECLE          OF PDBON_COMMANDE,  &
       BCMNUMBCM       OF PDBON_COMMANDE   &
    TO CIECLE,                             &
       BCMNUMBCM                           OF PDDET_BON_COMMAN          &

  LINK FOUCLE          OF PDBON_COMMANDE,  &
       FOCCIECLE       OF PDBON_COMMANDE,  &
       FOUCIECLE       OF PDBON_COMMANDE   &
    TO FOUCLE,                             &
       FOCCIECLE,                          &
       FOUCIECLE                           OF VFOC_FOU         OPTIONAL &

  LINK FOUCIECLE       OF VFOC_FOU,        &
       FOUREFTYP       OF VFOC_FOU,        &
       FOUCLE          OF VFOC_FOU,        &
       FOCADRACH       OF VFOC_FOU         &
    TO REFCIECLE,                          &
       REFCLETYP,                          &
       REFCLENUM,                          &
       RADCLE                              OF MCREF_ADRESSE    OPTIONAL


CHOOSE CIECLE PARM


SELECT IF PODCPL OF PDDET_BON_COMMAN = "0"


SORT ON BCMNUMBCM OF PDBON_COMMANDE



;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "P/O d'achats avec réception à venir"
@ELSE
       "%%%%P/O d'achats avec réception à venir"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD304Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST


SKIP 2 &
  TAB 003 "No de P/O" &
  TAB 015 "Fourn" &
  TAB 022 "Nom fournisseur" &
  TAB 060 "Type" &
  TAB 068 "Téléphone" &
  TAB 083 "Projet potentiel" &
SKIP 1 &
  TAB 003 "---------" &
  TAB 015 "-----" &
  TAB 022 "---------------" &
  TAB 060 "----" &
  TAB 068 "---------" &
  TAB 083 "----------------" &
SKIP 2

HEADING AT BCMNUMBCM &
  TAB 001 BCMNUMBCM    OF PDBON_COMMANDE     &
  TAB 015 FOUCLE       OF PDBON_COMMANDE     &
  TAB 022 FOUNOM       OF VFOC_FOU           &
  TAB 060 TPDTYPPRD    OF PDDET_BON_COMMAN   &
  TAB 068 RADTEL       OF MCREF_ADRESSE      &
  TAB 083 BCMPROPOT    OF PDBON_COMMANDE


BUILD pd304z


