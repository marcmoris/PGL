;/T/ Consultation des colonnes         EFICIECLE,EFICLE,EFCNUMCOL
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 8 Mars 1993
;
;    Description..: Permet de visionner et de choisir une
;                   des colonnes active de l'état financier demandé.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gl111 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_EFICIECLE, T_EFICLE, T_EFCNUMCOL

USE gl110.hlp NOLIST
USE ussectmp NOLIST
"GL111"

TEMPORARY T_EFICIECLE CHARACTER SIZE 4
TEMPORARY T_EFICLE    CHARACTER SIZE 4
TEMPORARY T_EFCNUMCOL INTEGER   SIZE 1

FILE GLEFI_COLONNE PRIMARY OCCURS 11
  ACCESS  VIA   EFICIECLE,EFICLE &
          USING T_EFICIECLE, T_EFICLE

  SELECT IF 0 <> SIZE(TRUNCATE(EFCTYPCOL OF GLEFI_COLONNE))

;
; Nom des colonnes
;
TEMPORARY T_CODASC      INT  SIZE 1 ; Code ascii de la lettre (A À P)
TEMPORARY T_NOMCOL      CHAR SIZE 1 OCCURS WITH GLEFI_COLONNE

USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP 1

ALIGN(3,3,6)(,,8) (,,13)

CLUSTER OCCURS WITH GLEFI_COLONNE

FIELD T_NOMCOL &
      HIDDEN LABEL ">>"
FIELD EFCTYPCOL OF GLEFI_COLONNE
FIELD EFCENTCOL OF GLEFI_COLONNE FOR 1,27

CLUSTER

;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;
;
; Affiche les lettres de A à P pour les colonnes.
;
;
PROCEDURE OUTPUT T_NOMCOL
BEGIN
  ;
  ; Le code ASCII pour la lettre A est 65, c'est pour cette raison que je
  ; part avec le code ascii 64.
  ;
  LET T_CODASC = 64 + EFCNUMCOL OF GLEFI_COLONNE
  LET FIELDTEXT = CHAR(T_CODASC)
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_EFCNUMCOL = EFCNUMCOL OF GLEFI_COLONNE
  RETURN
END

BUILD
;
;*****************************************
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;* >> x xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx *
;*****************************************
