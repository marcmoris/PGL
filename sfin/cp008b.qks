;/T/ Paiements - Imputation
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ PRJgrammeur..: Alain Côté / Adapté pour les CP par Thomas BRENNEUR
;    Date Création: 30 Juin 1993
;
;    Description..: Saisie de l'imputation pour une dépense directe.
;
;/M/ François Déry, 30 avril 1999
;       Modifier l'accèss au fichier de séquence car il a
;       été converti en format interbase au lieu d'indexé.
;
;/M/ Adatation....: Marc Morissette 22 Octobre 1993
;
;    Description..: Ajout du flg Facturable Oui/non et du numéro de client
;                   Ajout du code d'équipement
;                   Modification de l'ordre de saisie des champs
;/A/ Adaptation : Nancy Marceau, 24 mars 1994
;
;    Description: Afficher les valeurs par défaut du projet, du sous-projet et
;                 de l'équipement qui ont été définis dans la maintenance des
;                 paramètres du Prix de revient.
;
;/M/ Modifié par: Guy Chabot le 12 Avril 1994
;    Impact.....: Prendre par défaut l'équipement mentionné dans la table
;                 GPPROJETS selon le projet saisie.
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp008b ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU  , &
                        T_CIENOM     , &
                        D_LCPATRJOU  , &
                        D_DEVCLE     , &
                        CPPAIEMENT   , &
                        VFOC_FOU     , &
                        MCREF_ADRESSE

TEMPORARY T_CPTPECDEBI_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CPTPECDEBA_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_PECCLE_SIE2 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CCUPECDEBI_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CCUPECDEBA_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_PECCLE_SIE1 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle


USE ussectmp   NOLIST
    "CP008B"

USE cp008b.hlp NOLIST

USE usactecr   NOLIST

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE CPPAM_SEQ IN DICT
                                                ; Le IN DICT est pour unix
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_RAD_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE MCRAD_SEQ IN DICT
                                                ; Le IN DICT est pour unix

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.

TEMPORARY T_CLICIECLE CHARACTER SIZE 4 RESET AT STARTUP ; Compagnie du client.

DEFINE    D_LCPATRJOU CHARACTER SIZE 1  ; Si le lot est autorisé.
DEFINE    D_DEVCLE    CHARACTER SIZE 3  ; Devise de la référence.

;-------------------------------------------------------------------------------
;
; Relations mâitres :
;
;       Paiement    = Paiement en cours de création ou modification
;       Adresse     = Si on fait un déboursé directe sur un fournisseur
;                     divers, alors il faut ajouter une nouvelle adresse
;       Fournisseur = Pour avoir le type de fournisseur et ses codes de taxes

FILE CPPAIEMENT       MASTER

FILE MCREF_ADRESSE    MASTER

FILE VFOC_FOU         MASTER

;-------------------------------------------------------------------------------
;
FILE CPPAM_IMPUTATION PRIMARY &
                      OCCURS 14 &
                      NOITEMS

  ACCESS  VIA     CIECLE   ,&
                  LBQCPTENC, &
                  PAMCLE     &
          USING   CIECLE    OF CPPAIEMENT, &
                  LBQCPTENC OF CPPAIEMENT, &
                  PAMCLE    OF CPPAIEMENT  &
          ORDERBY CIECLE   , &
                  LBQCPTENC, &
                  PAMCLE   , &
                  CPITIMSTP

  ITEM CIECLE     OF CPPAM_IMPUTATION INITIAL CIECLE    OF CPPAIEMENT
  ITEM LBQCPTENC  OF CPPAM_IMPUTATION INITIAL LBQCPTENC OF CPPAIEMENT
  ITEM PAMCLE     OF CPPAM_IMPUTATION FINAL   PAMCLE    OF CPPAIEMENT
  ITEM CPITIMSTP  OF CPPAM_IMPUTATION INITIAL SYSDATE * 1000000 + &
                                              ROUND(SYSTIME / 100)
  ITEM CPICODDC   OF CPPAM_IMPUTATION INITIAL "D"
  ITEM CLICIECLE  OF CPPAM_IMPUTATION INITIAL T_CLICIECLE
;-------------------------------------------------------------------------------
;
; Notion de charte de compte et pour avoir la devise du G/L.
;
FILE MCHOLDING        REFERENCE

  ACCESS  VIA   CIENMC &
          USING "1"

;-------------------------------------------------------------------------------
;
; Validation de la charte de compte.
;
FILE MCCHARTE_UNA     REFERENCE

  ACCESS  VIA   CPTCLE, &
                CIECLE, &
                UNACLE &
          USING CPTCLE OF CPPAM_IMPUTATION, &
                T_CIECLEMNU               , &
                UNACLE OF CPPAM_IMPUTATION

;-------------------------------------------------------------------------------
;
; Validation de l'unité administrative.
;
FILE MCUNITE_ADM      REFERENCE &
                      OCCURS WITH CPPAM_IMPUTATION

  ACCESS  VIA   CIECLE, &
                UNACLE &
          USING T_CIECLEMNU, &
                UNACLE OF CPPAM_IMPUTATION

;-------------------------------------------------------------------------------
;
; Validation du compte comptable.
;
FILE VCPT_DSC         REFERENCE &
                      OCCURS WITH CPPAM_IMPUTATION

  ACCESS  VIA   CPTCLE &
          USING CPTCLE OF CPPAM_IMPUTATION

;-------------------------------------------------------------------------------
;
; Validation des codes de taxes fédéral.
;
FILE MCTAXE           REFERENCE &
                      ALIAS A_TAX_TPS

  ACCESS  VIA   TAXCLETYP, &
                TAXCLECOD &
          USING CPITAXTYPTPS OF CPPAM_IMPUTATION, &
                CPITAXCODTPS OF CPPAM_IMPUTATION

;-------------------------------------------------------------------------------
;
; Validation des codes de taxes provincial.
;
FILE MCTAXE           REFERENCE &
                      ALIAS A_TAX_TVP

  ACCESS  VIA   TAXCLETYP, &
                TAXCLECOD &
          USING CPITAXTYPTVQ OF CPPAM_IMPUTATION, &
                CPITAXCODTVQ OF CPPAM_IMPUTATION

;-------------------------------------------------------------------------------
;
; Validation du projet.
;
FILE GPPROJETS         REFERENCE &
                      OCCURS WITH CPPAM_IMPUTATION

  ACCESS VIA CIECLE, &
             PRJCLE  &
         USING T_CIECLEMNU, &
               PRJCLE OF CPPAM_IMPUTATION

;-------------------------------------------------------------------------------
;
; Validation de l'activité.
;
FILE GPPRO_ACTIVITE       REFERENCE

  ACCESS VIA CIECLE, &
             PRJCLE, &
             PRACLE  &
         USING T_CIECLEMNU, &
               PRJCLE OF CPPAM_IMPUTATION, &
               PRACLE OF CPPAM_IMPUTATION


;-------------------------------------------------------------------------------
; Valeurs par défaut à utiliser pour le projet, sous-projet et équipement.
;
FILE GPPARAMETRE REFERENCE &
                 OCCURS WITH CPPAM_IMPUTATION

  ACCESS VIA   CIECLE &
         USING T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Valide l'immobilisation et affiche la description.
;
TEMPORARY T_IMMCLE CHARACTER SIZE 12 ; Popup sur les immobilisations.

FILE IMIMMOBILISATION REFERENCE

  ACCESS  VIA   CIECLE, &
                IMMCLE &
          USING T_CIECLEMNU , &
                IMMCLE OF CPPAM_IMPUTATION

  SELECT IF IMMSTU OF IMIMMOBILISATION = "A"




;
; Validation du numéro de client et on a besoin de certaines informations sur
; le client , comme les termes d'encaissements, le statut, le type...
;
FILE VCLC_CLI         REFERENCE
  ACCESS VIA CLICIECLE, &
             CLICLE   , &
             CIECLE     &
         USING T_CLICIECLE,&
               CLICLE OF CPPAM_IMPUTATION,&
               T_CIECLEMNU

TEMPORARY T_CLICLE    CHARACTER SIZE  6 ; Popup sur les clients

;-------------------------------------------------------------------------------
;
; Numéro de séquence des adresses pour les fournisseurs divers.
;
FILE MCRAD_SEQ        DESIGNER &
                      TRANSACTION GEN_RAD_SEQ

;-------------------------------------------------------------------------------
;
; Numérotation du numéro de paiement, le numéro est attribué par compagnie
; et par compte d'encaisse.
;
FILE CPPAM_SEQ  DESIGNER &
                TRANSACTION GEN_NUM_SEQ

  ACCESS  VIA   CIECLE   , &
                LBQCPTENC  &
          USING T_CIECLEMNU, &
                LBQCPTENC OF CPPAIEMENT

  ITEM CIECLE     OF CPPAM_SEQ INITIAL T_CIECLEMNU
  ITEM LBQCPTENC  OF CPPAM_SEQ INITIAL LBQCPTENC OF CPPAIEMENT

;-------------------------------------------------------------------------------
;
; Calcul de l'écart entre le montant brut et la sommation de l'imputation.
;
DEFINE D_ECART FLOAT SIZE 8 = PAMMNT    OF CPPAIEMENT - &
                              PAMCUMMNT OF CPPAIEMENT


TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes et unité adm.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes

TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur les unités adm.

TEMPORARY T_PRJCLE    CHARACTER SIZE 8 ; Popup sur les projets.
TEMPORARY T_PRJSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.

TEMPORARY T_PRACLE    CHARACTER SIZE 3 ; Popup sur les activités.
TEMPORARY T_PRASTUPAT CHARACTER SIZE 5 ; Popup sur les activités.

TEMPORARY T_TAXCLETYP CHARACTER SIZE 1 ; Popup sur les codes de taxes
TEMPORARY T_TAXCLECOD CHARACTER SIZE 2 ; Popup sur les codes de taxes
TEMPORARY T_TAXMODPAT CHARACTER SIZE 5 ; Popup sur les codes de taxes

;
; Pour le calcul des taxes.
;
TEMPORARY T_FLGMOD       CHARACTER SIZE 1 OCCURS WITH CPPAM_IMPUTATION
                                          ; Flag pour déceler s'il y eu modif.
TEMPORARY T_TAXMODTPSOLD CHARACTER SIZE 1 OCCURS WITH CPPAM_IMPUTATION
                                          ; Pour diminuer le bon cumul de taxe.
TEMPORARY T_TAXMODTVQOLD CHARACTER SIZE 1 OCCURS WITH CPPAM_IMPUTATION
                                          ; Pour diminuer le bon cumul de taxe.


;
; Temporaires necessaires au calcul des taxes
;
USE ustaxtmp NOLIST

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Imputation " &
@ELSE
      " Charge " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST


ALIGN(,3,19) (,98,114)

FIELD PAMCLE    OF CPPAIEMENT &
LABEL "No paiement...:"&
        FIXED

FIELD PAMMNT    OF CPPAIEMENT &
        FIXED &
@IF FRANCAIS
        LABEL "Montant total.:"
@ELSE
        LABEL "Total amount..:"
@ENDIF

FIELD PAMMNTTPS OF CPPAIEMENT &
LABEL "Montant de TPS:"&
        DISPLAY &
        PREDISPLAY

FIELD PAMCUMMNT OF CPPAIEMENT &
LABEL "Cum. mnt brut.:"&
        DISPLAY &
        PREDISPLAY

FIELD PAMMNTTVQ OF CPPAIEMENT &
LABEL "Montant de TVQ:"&
        DISPLAY &
        PREDISPLAY

FIELD D_ECART &
        DISPLAY &
        PREDISPLAY &
@IF FRANCAIS
        LABEL "Solde à vent..:" &
@ELSE
        LABEL "Balance.......:" &
@ENDIF
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

SKIP

DRAW ,1 TO ,132

SKIP 1

TITLE &
@IF FRANCAIS
"Projet  SSpro Immobilis.  Unité    Compte       Montant  TF TP         TPS         TVQ Fac. Client"&
@ENDIF
  AT ,3

ALIGN (3,2,3) (,,12) (,,16) (,,29) (,,38) (,,45) (,,60) (,,63) (,,66)(,,78)(,,90)(,,94)

CLUSTER OCCURS WITH CPPAM_IMPUTATION ID BASE 05

FIELD PRJCLE       OF CPPAM_IMPUTATION &
        HIDDEN LABEL " "

FIELD PRACLE       OF CPPAM_IMPUTATION

FIELD IMMCLE       OF CPPAM_IMPUTATION &
        LOOKUP ON IMIMMOBILISATION &
            MESSAGE 491 ; Cette immobilisation n'existe pas ou est inactive.

FIELD UNACLE       OF CPPAM_IMPUTATION &
        DEFAULT PRAUNADEP OF GPPRO_ACTIVITE &
        LOOKUP ON MCUNITE_ADM &
          MESSAGE 472 ; Ce numéro d'unité administrative n'existe pas.

FIELD CPTCLE       OF CPPAM_IMPUTATION  &
        REQUIRED &
        LOOKUP ON VCPT_DSC  &
          MESSAGE 411 ; Ce compte est inexistant.


FIELD CPIMNT       OF CPPAM_IMPUTATION &
  DEFAULT D_ECART

FIELD CPITAXCODTPS OF CPPAM_IMPUTATION &
        DUPLICATE

FIELD CPITAXCODTVQ OF CPPAM_IMPUTATION &
        DUPLICATE

FIELD CPIMNTTPS    OF CPPAM_IMPUTATION

FIELD CPIMNTTVQ    OF CPPAM_IMPUTATION

FIELD CPIFLGFAC    OF CPPAM_IMPUTATION SIZE 3

FIELD CLICLE       OF CPPAM_IMPUTATION REQUIRED &
        LOOKUP ON VCLC_CLI &
          MESSAGE 624  ; Ce client n'existe pas.


CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST

  IF HLDCHTCLI OF MCHOLDING = "1"
  THEN LET T_CLICIECLE = T_CIECLEMNU
  ELSE LET T_CLICIECLE = HLDCHRSUB OF MCHOLDING

END


;-------------------------------------------------------------------------------
;
;
; Averti l'usager si l'éciture ne balance pas.
;
;
PROCEDURE EXIT
BEGIN
  IF PAMMNT OF CPPAIEMENT <> PAMCUMMNT OF CPPAIEMENT
  THEN WARNING 459 ; L'imputation ne balance pas.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les comptes.
;
;
PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = PECCLE OF CPPAIEMENT
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
  ;
  ; Force l'éxécution du LOOKUP.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(CPTCLE OF CPPAM_IMPUTATION))
  THEN LET FIELDTEXT = CPTCLE OF CPPAM_IMPUTATION
END


;-------------------------------------------------------------------------------
;
;
; Validation si le compte est actif.
;
;
PROCEDURE EDIT CPTCLE
BEGIN
  ;
  ; Note: La période de fin, est la première période d'inactivité.
  ;

  ; Ajustement pour le changement de siècle
  IF PECCLE OF CPPAIEMENT[1:1] < "8"
  THEN LET T_PECCLE_SIE2 = "1" + PECCLE OF CPPAIEMENT
  ELSE LET T_PECCLE_SIE2 = "0" + PECCLE OF CPPAIEMENT

  ; Ajustement pour le changement de siècle
  IF CPTPECDEBA OF VCPT_DSC[1:1] < "8"
  THEN LET T_CPTPECDEBA_SIE = "1" + CPTPECDEBA OF VCPT_DSC
  ELSE LET T_CPTPECDEBA_SIE = "0" + CPTPECDEBA OF VCPT_DSC

  ; Ajustement pour le changement de siècle
  IF CPTPECDEBI OF VCPT_DSC[1:1] < "8"
  THEN LET T_CPTPECDEBI_SIE = "1" + CPTPECDEBI OF VCPT_DSC
  ELSE LET T_CPTPECDEBI_SIE = "0" + CPTPECDEBI OF VCPT_DSC

  IF T_PECCLE_SIE2 >= T_CPTPECDEBA_SIE AND &
     ( &
       T_PECCLE_SIE2 < T_CPTPECDEBI_SIE OR &
       CPTPECDEBI OF VCPT_DSC = "" &
     )
  THEN NULL
  ELSE ERROR 473 ; Ce compte est inactif.

  IF CPTTYP OF VCPT_DSC <> "R"
  THEN ERROR 474 ; On ne peut pas utliser ce compte dans une imputation.

  IF     DEVCLE OF VCPT_DSC <> D_DEVCLE            &
     AND DEVCLE OF VCPT_DSC <> DEVCLE OF MCHOLDING
  THEN ERROR 578 ; Compte interdit due à la devise du compte.

  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 475 ; La combinaison compte/unité adm n'existe pas.
    END
    ELSE BEGIN
      ;
      ; Si la charte est Exclusive et que la période fait partie de
      ; l'intervalle de période de la charte alors c'est une erreur.
      ;
      ; Si la charte est inclusive et que la période ne fait pas partie de
      ; l'intervalle de période de la charte alors c'est une erreur.
      ;

      ; Ajustement pour le changement de siècle
      IF PECCLE OF CPPAIEMENT[1:1] < "8"
      THEN LET T_PECCLE_SIE1 = "1" + PECCLE OF CPPAIEMENT
      ELSE LET T_PECCLE_SIE1 = "0" + PECCLE OF CPPAIEMENT

      ; Ajustement pour le changement de siècle
      IF CCUPECDEBA OF MCCHARTE_UNA[1:1] < "8"
      THEN LET T_CCUPECDEBA_SIE = "1" + CCUPECDEBA OF MCCHARTE_UNA
      ELSE LET T_CCUPECDEBA_SIE = "0" + CCUPECDEBA OF MCCHARTE_UNA

      ; Ajustement pour le changement de siècle
      IF CCUPECDEBI OF MCCHARTE_UNA[1:1] < "8"
      THEN LET T_CCUPECDEBI_SIE = "1" + CCUPECDEBI OF MCCHARTE_UNA
      ELSE LET T_CCUPECDEBI_SIE = "0" + CCUPECDEBI OF MCCHARTE_UNA

      IF ( &
           T_PECCLE_SIE1 >= T_CCUPECDEBA_SIE AND &
           ( &
             T_PECCLE_SIE1 < T_CCUPECDEBI_SIE OR &
             CCUPECDEBI OF MCCHARTE_UNA = " " &
           ) &
         )
      THEN BEGIN
        IF HLDCHTCPTRLT OF MCHOLDING = "E"
        THEN ERROR 476 ; La combinaison compte/unité administrative est inactive.
      END
      ELSE BEGIN
        IF HLDCHTCPTRLT OF MCHOLDING = "I"
        THEN ERROR 476 ; La combinaison compte/unité administrative est inactive.
      END
    END
  END

END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT UNACLE
BEGIN
  ;
  ; Popup sur les unités administrative.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = CPTCLE OF CPPAM_IMPUTATION
    LET T_PECCLE = PECCLE OF CPPAIEMENT
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
  ;
  ; Force l'éxécution du LOOKUP.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(UNACLE OF CPPAM_IMPUTATION))
  THEN LET FIELDTEXT = UNACLE OF CPPAM_IMPUTATION
END


;-------------------------------------------------------------------------------
;
; Permet d'avoir comme valeur par défaut le solde à ventiler et de calculer
; le restant à ventiler. Force l'exécution de la procédure EDIT
;
PROCEDURE INPUT CPIMNT
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) AND CPIMNT = 0
  THEN LET FIELDTEXT = ASCII(D_ECART,10)[1:8] + "." + ASCII(D_ECART,10)[9:10]
END

;-------------------------------------------------------------------------------
;
;
; Mise à jour du cumulatif de l'imputation.
;
;
PROCEDURE EDIT CPIMNT
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec NOLIST

  LET PAMCUMMNT OF CPPAIEMENT = PAMCUMMNT OF CPPAIEMENT - &
                                 OLDVALUE(CPIMNT OF CPPAM_IMPUTATION) + &
                                 FIELDVALUE
  ;
  ; Initialise le flag à Oui dans la procédure EDIT. Car si l'usager saisie
  ; une valeur la procédure EDIT est exécuté.
  ;
  LET T_FLGMOD = "1"
END


;-------------------------------------------------------------------------------
;
;
; Affiche l'écart.
;
;
PROCEDURE PROCESS CPIMNT
BEGIN
  DISPLAY PAMCUMMNT OF CPPAIEMENT
  DISPLAY D_ECART
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran de consultation des codes de taxe.
;
;
PROCEDURE INPUT CPITAXCODTPS
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TAXCLETYP = CPITAXTYPTPS OF CPPAM_IMPUTATION
    LET T_TAXCLECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TAXMODPAT = "@"
    RUN SCREEN mc111 MODE F PASSING T_TAXCLETYP, &
                                    T_TAXCLECOD, &
                                    T_TAXMODPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNC(T_TAXCLECOD)
  END
  ;
  ; Si on fait un chèque à un fournisseur, alors
  ; on utilise ses codes de taxes. Pour un employé, il n'y a pas
  ; de codes de taxes par défaut
  ;
  IF     0 = SIZE( FIELDTEXT ) &
     AND REFCLETYP OF CPPAIEMENT = "F" &
     AND NEWRECORD OF CPPAM_IMPUTATION &
     AND CPITAXCODTPS OF CPPAM_IMPUTATION = "" &
     AND FOCTAXCODTPS OF VFOC_FOU <> ""
  THEN LET FIELDTEXT = FOCTAXCODTPS OF VFOC_FOU
END


;-------------------------------------------------------------------------------
;
;
; Validation du code de taxe fédéral.
;
;
PROCEDURE EDIT CPITAXCODTPS
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT))
  THEN BEGIN
    GET A_TAX_TPS OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 421 ; Ce code de taxe est inexistant.

  END
  ;
  ; Initialise le flag à Oui dans la procédure EDIT. Car si l'usager saisie
  ; une valeur la procédure EDIT est exécuté.
  ;
  LET T_FLGMOD = "1"
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran de consultation des codes de taxe.
;
;
PROCEDURE INPUT CPITAXCODTVQ
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TAXCLETYP = CPITAXTYPTVQ OF CPPAM_IMPUTATION
    LET T_TAXCLECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TAXMODPAT = "@"
    RUN SCREEN mc111 MODE F PASSING T_TAXCLETYP, &
                                    T_TAXCLECOD, &
                                    T_TAXMODPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNC(T_TAXCLECOD)
  END
  ;
  ; Si on fait un chèque à un fournisseur, alors
  ; on utilise ses codes de taxes. Pour un employé, il n'y a pas
  ; de codes de taxes par défaut
  ;
  IF     0 = SIZE( FIELDTEXT ) &
     AND REFCLETYP OF CPPAIEMENT = "F" &
     AND NEWRECORD OF CPPAM_IMPUTATION &
     AND CPITAXCODTVQ OF CPPAM_IMPUTATION = "" &
     AND FOCTAXCODTVQ OF VFOC_FOU <> ""
  THEN LET FIELDTEXT = FOCTAXCODTVQ OF VFOC_FOU
  ;
  ; Force l'éxécution de la procédure edit, car il y a inter-relation entre
  ; deux champs(taxe).
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     T_FLGMOD = "1"
  THEN LET FIELDTEXT = CPITAXCODTVQ OF CPPAM_IMPUTATION
END


;-------------------------------------------------------------------------------
;
;
; Validation du code de taxe provincial
;
;
PROCEDURE EDIT CPITAXCODTVQ
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT))
  THEN BEGIN
    GET A_TAX_TVP OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 421 ; Ce code de taxe est inexistant.

    IF TAXMOD    OF A_TAX_TVP = "I" AND &
       TAXFLGNET OF A_TAX_TVP = "2" AND &
       TAXMOD    OF A_TAX_TPS = "S"
    THEN ERROR 480 ; TVQ incluse et TPS en sus sont incompatible.
  END
END

;-------------------------------------------------------------------------------
;
; Calcul du CTI = remboursement de tps.
;
PROCEDURE INTERNAL CALCUL_CTI
BEGIN
  ;
  ; Il faut diminuer le cumul de taxe du paiement.
  ;
  LET PAMMNTCTI OF CPPAIEMENT = PAMMNTCTI OF CPPAIEMENT - &
                                   CPIMNTCTI OF CPPAM_IMPUTATION
  ;
  LET CPIMNTCTI OF CPPAM_IMPUTATION = 0

  ;
  ; Calcul du remboursement fédéral.
  ;
  LET CPIMNTCTI OF CPPAM_IMPUTATION = &
                ROUND( CPIMNTTPS OF CPPAM_IMPUTATION * TAXPCTCTI OF A_TAX_TPS )
  ;
  ; Il faut augmenter le cumul de taxe du paiement.
  ;
  LET PAMMNTCTI OF CPPAIEMENT = PAMMNTCTI OF CPPAIEMENT + &
                                   CPIMNTCTI OF CPPAM_IMPUTATION
END

;-------------------------------------------------------------------------------
;
; Calcul du RTI = remboursement de tvq.
;
PROCEDURE INTERNAL CALCUL_RTI
BEGIN
  ;
  ; Il faut diminuer le cumul de taxe du paiement.
  ;
  LET PAMMNTRTI OF CPPAIEMENT = PAMMNTRTI OF CPPAIEMENT - &
                                   CPIMNTRTI OF CPPAM_IMPUTATION
  ;
  LET CPIMNTRTI OF CPPAM_IMPUTATION = 0

  ;
  ; Calcul du remboursement fédéral.
  ;
  LET CPIMNTRTI OF CPPAM_IMPUTATION = &
                ROUND( CPIMNTTVQ OF CPPAM_IMPUTATION * TAXPCTCTI OF A_TAX_TVP )
  ;
  ; Il faut augmenter le cumul de taxe du paiement.
  ;
  LET PAMMNTRTI OF CPPAIEMENT = PAMMNTRTI OF CPPAIEMENT + &
                                   CPIMNTRTI OF CPPAM_IMPUTATION
END

;-------------------------------------------------------------------------------
;
; Calcul des taxes.
;
PROCEDURE PROCESS CPITAXCODTVQ
BEGIN
  ;
  ; Enlève les anciennes valeurs des totaux
  ;
  LET PAMMNTTPS OF CPPAIEMENT = PAMMNTTPS OF CPPAIEMENT - &
                                   CPIMNTTPS OF CPPAM_IMPUTATION
  LET PAMMNTTVQ OF CPPAIEMENT = PAMMNTTVQ OF CPPAIEMENT - &
                                   CPIMNTTVQ OF CPPAM_IMPUTATION
  ;
  ; Si le mode de taxation est ENSUS, il faut mettre à jour le cumulatif brut
  ; de la ventilation.
  ;
  IF T_TAXMODTPSOLD = "S"
  THEN LET PAMCUMMNT OF CPPAIEMENT   = PAMCUMMNT OF CPPAIEMENT   - &
                                          CPIMNTTPS    OF CPPAM_IMPUTATION
  IF T_TAXMODTVQOLD = "S"
  THEN LET PAMCUMMNT OF CPPAIEMENT   = PAMCUMMNT OF CPPAIEMENT   - &
                                          CPIMNTTVQ    OF CPPAM_IMPUTATION
  ;
  LET CPIMNTTPS OF CPPAM_IMPUTATION = 0
  LET CPIMNTTVQ OF CPPAM_IMPUTATION = 0
  ;
  ; On prend le montant brut de l'imputation pour le calcul des taxes.
  ;
  LET TZ_MNTTXB = CPIMNT OF CPPAM_IMPUTATION
  ;
  ; Use standart pour le calcul des taxes
  ;
  USE ustaxcal NOLIST

  LET CPIMNTTPS OF CPPAM_IMPUTATION = TZ_MNTTPS
  LET CPIMNTTVQ OF CPPAM_IMPUTATION = TZ_MNTTVP

  ;
  ; Si le mode de taxation est "EN SUS", il faut mettre à jour le cumulatif brut
  ; de la ventilation.
  ;
  IF TAXMOD OF A_TAX_TPS = "S"
  THEN LET PAMCUMMNT OF CPPAIEMENT   = PAMCUMMNT OF CPPAIEMENT   + &
                                          CPIMNTTPS    OF CPPAM_IMPUTATION
  IF TAXMOD OF A_TAX_TVP = "S"
  THEN LET PAMCUMMNT OF CPPAIEMENT   = PAMCUMMNT OF CPPAIEMENT   + &
                                          CPIMNTTVQ    OF CPPAM_IMPUTATION
  ;
  ; Calcul des montants de taxe remboursable (CTI & RTI)
  ;
  DO INTERNAL CALCUL_CTI
  DO INTERNAL CALCUL_RTI

  ;
  ; Mise à jour des montants totaux
  ;
  LET PAMMNTTPS OF CPPAIEMENT = PAMMNTTPS OF CPPAIEMENT + &
                                   CPIMNTTPS OF CPPAM_IMPUTATION
  LET PAMMNTTVQ OF CPPAIEMENT = PAMMNTTVQ OF CPPAIEMENT + &
                                   CPIMNTTVQ OF CPPAM_IMPUTATION
  ;
  DISPLAY CPIMNTTPS OF CPPAM_IMPUTATION
  DISPLAY CPIMNTTVQ OF CPPAM_IMPUTATION
  DISPLAY PAMMNTTPS OF CPPAIEMENT
  DISPLAY PAMMNTTVQ OF CPPAIEMENT
  DISPLAY PAMCUMMNT OF CPPAIEMENT
  DISPLAY D_ECART
  ;
  ; Ré-initialise le flag à non, ce flag nous indique si le montant de l'impu-
  ; tation ou le code de tps a été modifié.
  ;
  LET T_FLGMOD = "0"
  ;
  ; Il faut conserver l'ancienne valeur qui a servi à incrémenter le cumul
  ; des montants brut(incluant taxe). Il faut prendre les informations du
  ; code de taxe précédemment saisie, c'est pour cette raison que le traitement
  ; est fait dans cette procédure.
  ;
  LET T_TAXMODTPSOLD = TAXMOD OF A_TAX_TPS
  LET T_TAXMODTVQOLD = TAXMOD OF A_TAX_TVP
END


;-------------------------------------------------------------------------------
;
;
; L'usager peut modifier le montant de taxe.
;
;
PROCEDURE EDIT CPIMNTTPS
BEGIN

  DO INTERNAL CALCUL_CTI

  LET PAMMNTTPS OF CPPAIEMENT = PAMMNTTPS OF CPPAIEMENT - &
                                 OLDVALUE(CPIMNTTPS OF CPPAM_IMPUTATION) + &
                                 FIELDVALUE
  ;
  ; Si le mode de taxation est ENSUS, il faut mettre à jour le cumulatif brut
  ; de la ventilation.
  ;
  IF TAXMOD OF A_TAX_TPS = "S"
  THEN LET PAMCUMMNT OF CPPAIEMENT = PAMCUMMNT OF CPPAIEMENT - &
                                      OLDVALUE(CPIMNTTPS OF CPPAM_IMPUTATION) + &
                                      FIELDVALUE
END


;-------------------------------------------------------------------------------
;
;
; On affiche les cumulatifs situé en-tête.
;
;
PROCEDURE PROCESS CPIMNTTPS
BEGIN
  DISPLAY PAMMNTTPS OF CPPAIEMENT
  DISPLAY PAMCUMMNT OF CPPAIEMENT
  DISPLAY D_ECART
END


;-------------------------------------------------------------------------------
;
;
; L'usager peut modifier le montant de taxe.
;
;
PROCEDURE EDIT CPIMNTTVQ
BEGIN

  DO INTERNAL CALCUL_RTI

  LET PAMMNTTVQ OF CPPAIEMENT = PAMMNTTVQ OF CPPAIEMENT - &
                                 OLDVALUE(CPIMNTTVQ OF CPPAM_IMPUTATION) + &
                                 FIELDVALUE
  ;
  ; Si le mode de taxation est ENSUS, il faut mettre à jour le cumulatif brut
  ; de la ventilation.
  ;
  IF TAXMOD OF A_TAX_TVP = "S"
  THEN LET PAMCUMMNT OF CPPAIEMENT = PAMCUMMNT OF CPPAIEMENT - &
                                      OLDVALUE(CPIMNTTVQ OF CPPAM_IMPUTATION) + &
                                      FIELDVALUE
END


;-------------------------------------------------------------------------------
;
;
; On affiche les cumulatifs situé en-tête.
;
;
PROCEDURE PROCESS CPIMNTTVQ
BEGIN
  DISPLAY PAMMNTTVQ OF CPPAIEMENT
  DISPLAY PAMCUMMNT OF CPPAIEMENT
  DISPLAY D_ECART
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les projets.
;
;
PROCEDURE INPUT PRJCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJSTUPAT = "A"
    LET T_PRJCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp104 MODE F PASSING T_CIECLEMNU, &
                                    T_PRJCLE   , &
                                    T_PRJSTUPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_PRJCLE)
  END

  ;
  ; Par défaut on utilise le projet défini dans GPPARAMETRE. S'il n'y en a pas
  ; on force la validation.
  ;
  IF     0 = SIZE( FIELDTEXT ) &
     AND PRJCLE    OF CPPAM_IMPUTATION = ""
  THEN BEGIN
    IF PROCLE    OF GPPARAMETRE <> ""
    THEN LET FIELDTEXT = PROCLE OF GPPARAMETRE
    ELSE LET FIELDTEXT = PRJCLE OF CPPAM_IMPUTATION
  END

END


;-------------------------------------------------------------------------------
;
;
; Validation du projet, le projet est optionel dans l'imputation.
;
;
PROCEDURE EDIT PRJCLE
BEGIN
  GET GPPROJETS OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 512 ; Ce projet n'existe pas.

  IF PRJSTU OF GPPROJETS <> "A"
  THEN ERROR 514 ; Ce projet n'est pas actif.
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT PRACLE
BEGIN
  ;
  ; Popup sur les activités.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJCLE    = PRJCLE OF CPPAM_IMPUTATION
    LET T_PRASTUPAT = "A"
    LET T_PRACLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp108 MODE F PASSING T_CIECLEMNU, &
                                    T_PRJCLE   , &
                                    T_PRACLE   , &
                                    T_PRASTUPAT &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_PRACLE)
  END
  ;
  ; Par défaut on utilise le sous-projet défini dans GPPARAMETRE. S'il n'y en a pas
  ; on force la validation.
  ;
  IF     0 = SIZE( FIELDTEXT ) &
     AND PRACLE    OF CPPAM_IMPUTATION = ""
  THEN BEGIN
     IF ACTCLE    OF GPPARAMETRE <> ""
     THEN LET FIELDTEXT = ACTCLE OF GPPARAMETRE
     ELSE LET FIELDTEXT = PRACLE OF CPPAM_IMPUTATION
  END


  ;
  ; Force la validation de l'activité, car l'usager peut modifier le numéro de
  ; compagnie ou le numéro de projet sur le ligne.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(PRACLE OF CPPAM_IMPUTATION))
  THEN LET FIELDTEXT = PRACLE OF CPPAM_IMPUTATION
END


;-------------------------------------------------------------------------------
;
;
; Validation de l'activité, l'activité est optionel dans l'imputation.
;
;
PROCEDURE EDIT PRACLE
BEGIN
  GET GPPRO_ACTIVITE OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 471 ; Cette activité n'existe pas.

  IF PRASTU OF GPPRO_ACTIVITE <> "A"
  THEN ERROR 520 ; Cette activité est inactive.
END


PROCEDURE INPUT IMMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IMMCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN im102 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_IMMCLE  WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_IMMCLE )
  END
  ;
  ; Par défaut on utilise la catégorie définie dans GPPARAMETRE. S'il n'y en a pas
  ; on force la validation.
  ;
  IF     0 = SIZE( FIELDTEXT ) &
     AND 0 = SIZE(TRUNCATE(IMMCLE OF CPPAM_IMPUTATION))
  THEN BEGIN
     IF 0 <> SIZE(TRUNCATE(PRAIMMCLE OF GPPRO_ACTIVITE))
     THEN LET FIELDTEXT = PRAIMMCLE OF GPPRO_ACTIVITE
     ELSE LET FIELDTEXT = IMMCLE OF CPPAM_IMPUTATION
  END

END

PROCEDURE EDIT IMMCLE
BEGIN
   IF IMMTYP OF IMIMMOBILISATION <> "E"
   THEN ERROR 1503
END


PROCEDURE INPUT CPIFLGFAC
BEGIN
  USE usflginp NOLIST
END

PROCEDURE PROCESS CPIFLGFAC
BEGIN
  IF CPIFLGFAC = "0"
  THEN BEGIN
    LET CLICLE OF CPPAM_IMPUTATION = ""
    DISPLAY CLICLE
  END
END

PROCEDURE OUTPUT CPIFLGFAC
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les client.
;
;
PROCEDURE INPUT CLICLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLICLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cr102 MODE F PASSING T_CLICIECLE, T_CIECLEMNU, T_CLICLE &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNC(T_CLICLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du client.
;
;
PROCEDURE EDIT CLICLE
BEGIN
  IF CLISTU OF VCLC_CLI = "I"
  THEN ERROR 632 ; Le client est inactif.
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE APPEND
BEGIN

  USE ussecent NOLIST

  ;
  ; Si notion de projet.
  ;
  IF HLDNTNPRO OF MCHOLDING = "1"
  THEN BEGIN
    ACCEPT PRJCLE    OF CPPAM_IMPUTATION
    INFORMATION = PRJDSC1 OF GPPROJETS
    ACCEPT PRACLE    OF CPPAM_IMPUTATION
    INFORMATION = PRADSC1 OF GPPRO_ACTIVITE
    ACCEPT IMMCLE    OF CPPAM_IMPUTATION
    INFORMATION = IMMDSC  OF IMIMMOBILISATION
  END
  ELSE BEGIN
    LET PRJCLE OF CPPAM_IMPUTATION = " "
    LET PRACLE OF CPPAM_IMPUTATION = " "
    LET IMMCLE OF CPPAM_IMPUTATION = " "
  END

  ACCEPT UNACLE    OF CPPAM_IMPUTATION
  INFORMATION = UNANOM OF MCUNITE_ADM
  EDIT UNACLE    OF CPPAM_IMPUTATION


  ACCEPT CPTCLE    OF CPPAM_IMPUTATION
  INFORMATION = CPTDSCABR OF VCPT_DSC

  ACCEPT CPIMNT       OF CPPAM_IMPUTATION
  ACCEPT CPITAXCODTPS OF CPPAM_IMPUTATION
  ACCEPT CPITAXCODTVQ OF CPPAM_IMPUTATION

  ACCEPT CPIFLGFAC    OF CPPAM_IMPUTATION
  IF CPIFLGFAC    OF CPPAM_IMPUTATION = "1"
  THEN BEGIN
    ACCEPT CLICLE OF CPPAM_IMPUTATION
    INFORMATION = CLINOM OF VCLC_CLI
  END
END


;-------------------------------------------------------------------------------
;
;
; La procédure entry est généré car la procédure append est généré.
;
;
PROCEDURE ENTRY
BEGIN
  FOR CPPAM_IMPUTATION
  BEGIN
    PERFORM APPEND
  END
END


;-------------------------------------------------------------------------------
;
;
; Cette procédure est identique à la procédure APPEND.
;
;
PROCEDURE DESIGNER 05
BEGIN
  ;
  ; Si notion de projet.
  ;
  IF HLDNTNPRO OF MCHOLDING = "1"
  THEN BEGIN
    ACCEPT PRJCLE    OF CPPAM_IMPUTATION
    INFORMATION = PRJDSC1 OF GPPROJETS
    ACCEPT PRACLE    OF CPPAM_IMPUTATION
    INFORMATION = PRADSC1 OF GPPRO_ACTIVITE
    ACCEPT IMMCLE    OF CPPAM_IMPUTATION
    INFORMATION = IMMDSC  OF IMIMMOBILISATION
  END
  ELSE BEGIN
    LET PRJCLE OF CPPAM_IMPUTATION = " "
    LET PRACLE OF CPPAM_IMPUTATION = " "
    LET IMMCLE OF CPPAM_IMPUTATION = " "
  END


  ACCEPT UNACLE    OF CPPAM_IMPUTATION
  INFORMATION = UNANOM OF MCUNITE_ADM
  EDIT UNACLE    OF CPPAM_IMPUTATION

  ACCEPT CPTCLE    OF CPPAM_IMPUTATION
  INFORMATION = CPTDSCABR OF VCPT_DSC

  ACCEPT CPIMNT       OF CPPAM_IMPUTATION
  ACCEPT CPITAXCODTPS OF CPPAM_IMPUTATION
  ACCEPT CPITAXCODTVQ OF CPPAM_IMPUTATION

  IF CPITAXCODTPS OF CPPAM_IMPUTATION <> ""
  THEN ACCEPT CPIMNTTPS    OF CPPAM_IMPUTATION

  IF CPITAXCODTVQ OF CPPAM_IMPUTATION <> ""
  THEN ACCEPT CPIMNTTVQ    OF CPPAM_IMPUTATION

  ACCEPT CPIFLGFAC    OF CPPAM_IMPUTATION
  IF CPIFLGFAC    OF CPPAM_IMPUTATION = "1"
  THEN BEGIN
    ACCEPT CLICLE OF CPPAM_IMPUTATION
    INFORMATION = CLINOM OF VCLC_CLI
  END
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE POSTFIND
BEGIN
  FOR CPPAM_IMPUTATION
  BEGIN
    ;
    ; Il faut conserver l'ancienne valeur qui a servi à incrémenter le cumul
    ; des montants brut(incluant taxe). Il faut prendre les informations du
    ; code de taxe précédemment saisie, c'est pour cette raison que le traitement
    ; est fait dans cette procédure.
    ;
    LET T_TAXMODTPSOLD = TAXMOD OF A_TAX_TPS
    LET T_TAXMODTVQOLD = TAXMOD OF A_TAX_TVP
  END
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR CPPAM_IMPUTATION
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF CPPAM_IMPUTATION &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF CPPAM_IMPUTATION &
       AND NOT NEWRECORD     OF CPPAM_IMPUTATION &
       AND NOT DELETEDRECORD OF CPPAM_IMPUTATION &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; L'imputation est non modifiable aprés journalisation.
    ;
    IF (    NEWRECORD     OF CPPAM_IMPUTATION &
         OR ALTEREDRECORD OF CPPAM_IMPUTATION &
         OR DELETEDRECORD OF CPPAM_IMPUTATION &
       ) &
       AND PAMDATJOU OF CPPAIEMENT <> 0
    THEN ERROR 458 ; Impossible de mettre à jour une transaction journalisée.
    ;
    ; Mise à jour du timbre de modification si l'usager change l'imputation
    ;
    IF    NOT NEWRECORD OF CPPAIEMENT &
      AND ALTEREDRECORD OF CPPAM_IMPUTATION
    THEN BEGIN
      LET PAMDATMOD OF CPPAIEMENT = SYSDATE
      LET PAMHREMOD OF CPPAIEMENT = SYSTIME / 10000
      LET PAMUSRMOD OF CPPAIEMENT = LOGONID
    END
  END

  ;
  ; L'imputation est non modifiable Si le lot est autorisé.
  ;
  IF     D_LCPATRJOU = "1" &
     AND PAMDATJOU OF CPPAIEMENT = 0
  THEN ERROR 544 ; Impossible de mettre à jour , le lot est déjà autorisé
END
;
; La procédure UPDATE est utilisée à cause des numéros séquentiels sur
; l'adresse et le numéro de paiement.
;
PROCEDURE UPDATE
BEGIN
  ;
  ; On incrémente l'adresse pour les fournisseurs divers
  ;
  IF    NEWRECORD OF CPPAIEMENT       &
    AND REFCLETYP OF CPPAIEMENT = "F" &
    AND FOUTYP    OF VFOC_FOU   = "D" &
    AND PAMDEPDIR OF CPPAIEMENT = "1"
  THEN BEGIN
    START TRANSACTION GEN_RAD_SEQ
    GET MCRAD_SEQ VIA REFCIECLE, &
                      REFCLETYP, &
                      REFCLENUM  &
                  USING REFCIECLE OF CPPAIEMENT, &
                        REFCLETYP OF CPPAIEMENT, &
                        REFCLENUM OF CPPAIEMENT  &
                  OPTIONAL
    LET REFCIECLE OF MCRAD_SEQ = REFCIECLE OF CPPAIEMENT
    LET REFCLETYP OF MCRAD_SEQ = REFCLETYP OF CPPAIEMENT
    LET REFCLENUM OF MCRAD_SEQ = REFCLENUM OF CPPAIEMENT
    LET RASNUMSEQ OF MCRAD_SEQ = RASNUMSEQ OF MCRAD_SEQ + 1
    LET RADCLE    OF CPPAIEMENT = RASNUMSEQ OF MCRAD_SEQ
    PUT MCRAD_SEQ
    COMMIT TRANSACTION GEN_RAD_SEQ
  END

  ;
  ; On obtient un numéro de paiement pour le nouveau paiement
  ;
  IF NEWRECORD OF CPPAIEMENT
  THEN BEGIN
    START TRANSACTION GEN_NUM_SEQ
    GET CPPAM_SEQ OPTIONAL
    LET PSENUMSEQ OF CPPAM_SEQ = PSENUMSEQ OF CPPAM_SEQ + 1
    LET PAMCLE OF CPPAIEMENT &
                = ASCII(PSENUMSEQ OF CPPAM_SEQ,PSELONNUM OF CPPAM_SEQ)
    PUT CPPAM_SEQ
    COMMIT TRANSACTION GEN_NUM_SEQ
  END

  PUT CPPAIEMENT
  PUT MCREF_ADRESSE

  FOR CPPAM_IMPUTATION
  BEGIN
    PUT CPPAM_IMPUTATION
  END
END

PROCEDURE POSTUPDATE
BEGIN
  DISPLAY PAMCLE OF CPPAIEMENT
END

;-------------------------------------------------------------------------------
;
;
; Met à jour les cumulatifs dans le paiement.
;
;
PROCEDURE DELETE
BEGIN
  IF NOT DELETEDRECORD OF CPPAM_IMPUTATION
  THEN BEGIN
    ;
    ; Enlève les anciennes valeurs des totaux
    ;
    LET PAMMNTTPS OF CPPAIEMENT = PAMMNTTPS OF CPPAIEMENT - &
                                  CPIMNTTPS OF CPPAM_IMPUTATION
    LET PAMMNTCTI OF CPPAIEMENT = PAMMNTCTI OF CPPAIEMENT - &
                                  CPIMNTCTI OF CPPAM_IMPUTATION
    LET PAMMNTTVQ OF CPPAIEMENT = PAMMNTTVQ OF CPPAIEMENT - &
                                  CPIMNTTVQ OF CPPAM_IMPUTATION
    LET PAMMNTRTI OF CPPAIEMENT = PAMMNTRTI OF CPPAIEMENT - &
                                  CPIMNTRTI OF CPPAM_IMPUTATION
    ;
    LET PAMCUMMNT OF CPPAIEMENT = PAMCUMMNT OF CPPAIEMENT - &
                                   CPIMNT OF CPPAM_IMPUTATION
    IF TAXMOD OF A_TAX_TPS = "S"
    THEN LET PAMCUMMNT OF CPPAIEMENT= PAMCUMMNT OF CPPAIEMENT - &
                                       CPIMNTTPS OF CPPAM_IMPUTATION
    IF TAXMOD OF A_TAX_TVP = "S"
    THEN LET PAMCUMMNT OF CPPAIEMENT= PAMCUMMNT OF CPPAIEMENT - &
                                       CPIMNTTVQ OF CPPAM_IMPUTATION
  END
  DELETE CPPAM_IMPUTATION
  DISPLAY PAMMNTTPS OF CPPAIEMENT
  DISPLAY PAMMNTTVQ OF CPPAIEMENT
  DISPLAY PAMCUMMNT OF CPPAIEMENT
  DISPLAY D_ECART
END

BUILD
@IF FORMAT
        CP008B    ;/T/ Paiements - Imputation    93-10-22

        xxxx                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                              x
        ************************************************************ Imputation ************************************************************
        * No paiement...: xxxxxxx                        Montant total.: xxxxxxxxxxxxxx                                                    *
        * Montant de TPS: xxxxxxxxxxx                    Cum. mnt brut.: xxxxxxxxxxxxxx                                                    *
        * Montant de TVQ: xxxxxxxxxxx                    Ecart.........: xxxxxxxxxxxxxx                                                    *
        ********************************************************************************                                                   *
        * Projet  SSpro Équipement Unité    Compte        Montant  TF TP         TPS         TVQ                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        * xxxxxxxx xxx  xxxxxxxxxx xxxxxxxx xxxxxx  xxxxxxxxxxxxxx xx xx xxxxxxxxxxx xxxxxxxxxxx                                           *
        ************************************************************************************************************************************
@ENDIF
