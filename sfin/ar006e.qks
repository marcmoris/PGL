;/T/ Adresse fournisseur divers
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 23 mars 1994
;
;    Adapter par..: Guy Chabot ( Réquisition )
;
;    Description..: Saisie de l'adresse du fournisseurs divers pour la 
;                   réquisition.
;
;-------------------------------------------------------------------------------
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar006e SLAVE &
              NOMODE NOACTION FIELDMARK &
              FROM 5,10 TO 18,69 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY, CHANGE &
              RECEIVING MCREF_ADRESSE

USE ar006e.hlp NOLIST

USE ussectmp NOLIST     ; Variable pour la sécurité 
"AR006E"                ; Nom de l'écran

;------------------------------------------------------------------------------

FILE MCREF_ADRESSE     MASTER

;------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tous les écrans

DRAW 1,1 TO 14,60

TITLE &
@IF FRANCAIS
      " Adresse - fournisseur divers " &
@ELSE
      " Address - Miscellaneous Suppliers " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(2,3,19)


FIELD RADNOM    OF MCREF_ADRESSE &
        HIDDEN &
        DISPLAY &
        FOR 2,30
 
FIELD RADNOMABR OF MCREF_ADRESSE &
        HIDDEN &
        DEFAULT RADNOM OF MCREF_ADRESSE
 
FIELD RADADR1   OF MCREF_ADRESSE &
        HIDDEN

FIELD RADADR2   OF MCREF_ADRESSE ID SAME NOLABEL

FIELD RADADR3   OF MCREF_ADRESSE ID SAME NOLABEL

FIELD RADADR4   OF MCREF_ADRESSE ID SAME NOLABEL

FIELD RADCP     OF MCREF_ADRESSE HIDDEN

FIELD RADTEL    OF MCREF_ADRESSE HIDDEN

FIELD RADENRTPS OF MCREF_ADRESSE HIDDEN

FIELD RADENRTVQ OF MCREF_ADRESSE HIDDEN

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Obligation de saisir le nom du fournisseur divers.
;
PROCEDURE INPUT RADNOM
BEGIN
  LET FIELDTEXT = TRUNCATE( FIELDTEXT )
END
;-------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de code postal
;
PROCEDURE INPUT RADCP
BEGIN
  USE uscpfmt NOLIST  ;Formate le numéro de code postal 
END

;------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de téléphone
;
PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain   
END

;-------------------------------------------------------------------------------
;
; Procédure ENTRY standard plus le verbe RETURN à la fin. L'option AUTORETURN
; ne fonctionne pas avec un écran SLAVE.
;
;
PROCEDURE ENTRY
BEGIN
  ;
  ; Valide si l'usager peut faire de la saisie.
  ;
  USE ussecent NOLIST
  DISPLAY RADNOM    OF MCREF_ADRESSE
  ACCEPT RADNOMABR OF MCREF_ADRESSE
  ACCEPT RADADR1   OF MCREF_ADRESSE
  ACCEPT RADADR2   OF MCREF_ADRESSE
  ACCEPT RADADR3   OF MCREF_ADRESSE
  ACCEPT RADADR4   OF MCREF_ADRESSE
  ACCEPT RADCP     OF MCREF_ADRESSE
  ACCEPT RADTEL    OF MCREF_ADRESSE
  ACCEPT RADENRTPS OF MCREF_ADRESSE
  ACCEPT RADENRTVQ OF MCREF_ADRESSE
  RETURN
END

;-------------------------------------------------------------------------------
;
; La mise à jour va être fait dans l'écran des commandes.
;
;
PROCEDURE PREUPDATE
BEGIN
  RETURN
END

BUILD
@IF FORMAT
         *************** Adresse - fournisseur divers ***************
         *                                                          *
         * Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
         * Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                     *
         * Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
         *                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
         *                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
         *                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
         * Code Postal...: xxxxxxx                                  *
         * Téléphone.....: xxxxxxxxxxxxx                            *
         * Enreg. TPS....: xxxxxxxxxxx                              *
         * Enreg. TVP....: xxxxxxxxxxxxxxxx                         *
         ************************************************************
@ENDIF                                                                
