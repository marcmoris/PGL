;/T/ Note de crédit sur facture
;
;/M/ François Déry, 26 mai 1999
;       Conversion unxi/interbase.
;
SCREEN if017 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND,ENTRY,CHANGE &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF017"

USE if017.hlp NOLIST

USE usactecr  NOLIST
ACTIONMENU LABEL "Traitement"
  MENUITEM LABEL "Impression de la note de crédit" ACTION DESIGNER D001
  MENUITEM LABEL "Duplicata de la note de crédit " ACTION DESIGNER D002
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE IFSEQUENCE IN DICT
                                                ; Le IN DICT est pour unix

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie


FILE IFNOT_CRE_MAITRE PRIMARY
        ITEM CIECLE OF IFNOT_CRE_MAITRE INIT T_CIECLE
        ITEM NCMFIN OF IFNOT_CRE_MAITRE INIT "N"
        ITEM NCMANNUL OF IFNOT_CRE_MAITRE INIT "N"
        ITEM NCMIMP OF IFNOT_CRE_MAITRE INIT "N"
        ITEM NCMTOTALVENT OF IFNOT_CRE_MAITRE FINAL NCMFACTOT
        ITEM NCMFACTOTCAN OF IFNOT_CRE_MAITRE FINAL NCMTOTALCANVENT
        ITEM NCMTRANSFERT OF IFNOT_CRE_MAITRE INIT "N"

FILE IFFACTURE REFERENCE
  ACCESS VIA   CIECLE, &
               FCECLE  &
         USING T_CIECLE, &
               FCECLE OF IFNOT_CRE_MAITRE

FILE IFCONTRAT REFERENCE
  ACCESS VIA   CIECLE, &
               CLTCLE, &
               CRRCLENUM, &
               CRRCLEANN  &
         USING T_CIECLE, &
               CLTCLE OF IFFACTURE, &
               CRRCLENUM OF IFFACTURE, &
               CRRCLEANN OF IFFACTURE

FILE IFCLIENT         REFERENCE
  ACCESS VIA   CIECLE, &
               CLTCLE  &
         USING T_CIECLE, &
               CLTCLE OF IFFACTURE

FILE CRCLIENT         REFERENCE
  ACCESS VIA   CLICIECLE, &
               CLICLE &
         USING CLICIECLE OF IFCLIENT, &
               CLICLE OF IFCLIENT

;
; Information du client par compagnie
;
FILE CRCLI_CIE        REFERENCE
  ACCESS VIA   CLICIECLE, &
               CLICLE, &
               CIECLE  &
         USING CLICIECLE OF IFCLIENT, &
               CLICLE OF IFCLIENT, &
               T_CIECLE
;
; % de taxe fédéral
;
FILE VTAX_DSC         REFERENCE ALIAS A_TAXFED
  ACCESS VIA   TAXCLETYP, &
               TAXCLECOD &
         USING CLCTAXTYPTPS OF CRCLI_CIE, &
               CLCTAXCODTPS OF CRCLI_CIE
;
; % de taxe provincial
;
FILE VTAX_DSC         REFERENCE ALIAS A_TAXPRO
  ACCESS VIA   TAXCLETYP, &
               TAXCLECOD &
         USING CLCTAXTYPTVP OF CRCLI_CIE, &
               CLCTAXCODTVP OF CRCLI_CIE

FILE IFSEQUENCE DESIGNER &
                TRANSACTION GEN_NUM_SEQ

FILE MCPERIODE_CTB  DESIGNER
 SELECT IF (NCMDAT OF IFNOT_CRE_MAITRE <= PECDATFIN OF MCPERIODE_CTB AND &
            NCMDAT OF IFNOT_CRE_MAITRE >= PECDATDEB OF MCPERIODE_CTB)
FILE MCDEVISE DESIGNER

ITEM CRRCLENUM OF IFNOT_CRE_MAITRE FINAL CRRCLENUM OF IFFACTURE
ITEM CRRCLEANN OF IFNOT_CRE_MAITRE FINAL CRRCLEANN OF IFFACTURE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMP TMP-FIN CHARACTER SIZE 1 INIT NCMFIN OF IFNOT_CRE_MAITRE
TEMP TMP-NCMANNUL CHARACTER SIZE 1 INIT NCMANNUL OF IFNOT_CRE_MAITRE

DEF T-SOUS-TOTAL   FLOAT SIZE 4 = NCMFACTOT OF IFNOT_CRE_MAITRE &
    + NCMMNTTPSFED OF IFNOT_CRE_MAITRE

DEF T-TOTAL-CREDIT FLOAT SIZE 4 = T-SOUS-TOTAL + NCMMNTTVQPRO &
    OF IFNOT_CRE_MAITRE

DEFINE D_IMPNOT CHARACTER SIZE 7   = "IF326A" ; Impression de note de crédit
DEFINE D_DUPNOT CHARACTER SIZE 7   = "IF327" ; duplicata de note de crédit
;
; Liste des paramètres.
;
DEFINE D_LISPAR CHARACTER SIZE 204 = "F_CIECLEMNU     |F_CIENOM        |" + &
                                     "CRECLE          |"
DEFINE D_INDLON CHARACTER SIZE 112 = "001004|005040|045005"
DEFINE D_LISVAL CHARACTER SIZE 300 = T_CIECLE &
                                   + DZ_CIENOM    &
                                   + ASCII( CRECLE OF IFNOT_CRE_MAITRE , 5 )
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Note de crédit pour facture au complet " &
      CENTERED AT ,1
SKIP TO LINE 4
ALIGN (2,5,17)(,24,30)(,39,52)(,58,68)(,71,78)
FIELD CRECLE OF IFNOT_CRE_MAITRE &
        HIDDEN ID 1 &
        LABEL "No credit..:" &
        DISPLAY
FIELD NCMDAT OF IFNOT_CRE_MAITRE &
        NOCHANGE &
        DEFAULT SYSDATE &
        LABEL "Date:"
FIELD NCMFIN OF IFNOT_CRE_MAITRE &
        LABEL "Code fin:"
FIELD NCMIMP OF IFNOT_CRE_MAITRE &
        LABEL "Code imp:" &
        DISPLAY
FIELD NCMANNUL OF IFNOT_CRE_MAITRE &
        LABEL "Annul:" &
        DISPLAY
SKIP

ALIGN (2,5,17)(,24,36)(,,38)(,,40)(,43,54)
FIELD  FCECLE OF IFNOT_CRE_MAITRE &
        HIDDEN ID 2 &
        NOCHANGE &
        REQUIRED &
        LABEL "No facture:"
FIELD CLTCLE OF IFFACTURE &
        LABEL "No contrat.:" &
        DISPLAY
FIELD CRRCLENUM OF IFFACTURE &
        DISPLAY
FIELD CRRCLEANN OF IFFACTURE &
        DISPLAY
FIELD FCEDAT OF IFFACTURE &
        LABEL "Date fact.:" &
        DISPLAY
SKIP

ALIGN(2,5,17)(,43,54)(,64,77)
FIELD PECCLE OF IFNOT_CRE_MAITRE &
        HIDDEN ID 3 &
        LABEL "No Période:" &
        DISPLAY
FIELD LCRCLE OF IFNOT_CRE_MAITRE &
        LABEL "No Lot....:" &
        DISPLAY
FIELD NCMTRANSFERT OF IFNOT_CRE_MAITRE &
        LABEL "Transférée.:" &
        DISPLAY
SKIP 1
ALIGN (,5,17) (,43,54)
FIELD CLINOMABR OF CRCLIENT &
        LABEL "Nom client.:" &
        DISPLAY
FIELD CLILNG OF CRCLIENT &
        LABEL "Langue....:" &
        DISPLAY

ALIGN (,5,11)(,43,54)
FIELD FCEADR1 OF IFFACTURE  &
        LABEL "Adr.:"  &
        DISPLAY
FIELD DEVCLE OF CRCLIENT &
        LABEL "Devise....:" &
        DISPLAY
FIELD FCEADR2 OF IFFACTURE &
        LABEL "Fact:" &
        DISPLAY
FIELD CONCONDITION1 OF IFCONTRAT &
        LABEL "Conditions:" &
        DISPLAY
FIELD FCEADR3 OF IFFACTURE &
        NOLABEL &
        DISPLAY
FIELD CONCONDITION2 OF IFCONTRAT &
        NOLABEL &
        DISPLAY
FIELD FCECP OF IFFACTURE &
        NOLABEL &
        DISPLAY
SKIP

ALIGN(2,5,60)
FIELD NCMFACTOT OF IFNOT_CRE_MAITRE &
        HIDDEN ID 4 &
        LABEL "Note de credit :"
TITLE "Montant........:" AT ,43
SKIP

ALIGN(,43,47)(56,57,59)
FIELD TAXPCT OF A_TAXFED &
        LABEL "TPS " &
        NOID &
        DISPLAY &
        PICTURE "^^^.^^%"
FIELD NCMMNTTPSFED OF IFNOT_CRE_MAITRE &
        HIDDEN ID 5 &
        LABEL " " &
        PICTURE " ^^,^^^.^^"
SKIP
TITLE "__________" AT ,59
SKIP

ALIGN(,43,59)
FIELD T-SOUS-TOTAL &
        LABEL "Sous total.....:" &
        NOID &
        DISPLAY &
        PICTURE "^^^,^^^.^^"
SKIP

ALIGN(,5,25)(,43,47)(56,57,59)
FIELD CLCEXPTVP OF CRCLI_CIE &
        LABEL "No exemp TX prov. :" &
        DISPLAY
FIELD TAXPCT OF A_TAXPRO &
        LABEL "TVQ " &
        NOID &
        DISPLAY &
        PICTURE "^^^.^^%"
FIELD NCMMNTTVQPRO OF IFNOT_CRE_MAITRE &
        HIDDEN ID 6 &
        LABEL " " &
        PICTURE " ^^,^^^.^^"
SKIP
TITLE "__________" AT ,59
SKIP

ALIGN(,43,59)
FIELD T-TOTAL-CREDIT &
        LABEL "Total du credit:" &
        DISPLAY PIC "^^^,^^^.^^"
SKIP

ALIGN(2,5,19)
FIELD NCMCOM1 OF IFNOT_CRE_MAITRE &
        HIDDEN ID 7 &
        LABEL "Commentaires:"
FIELD NCMCOM2 OF IFNOT_CRE_MAITRE &
        ID SAME &
        NOLABEL
CLUSTER

PROCEDURE EDIT FCECLE OF IFNOT_CRE_MAITRE
BEGIN
  GET IFFACTURE VIA   CIECLE, FCECLE &
                USING T_CIECLE, FCECLE OF IFNOT_CRE_MAITRE OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR "*E* Cette facture n'existe pas..."
  ELSE BEGIN
    IF FCEFIN = "N"
    THEN ERROR "*E* Cette facture n'est pas finale..."
    IF FCEANNUL = "O"
    THEN ERROR "*E* Cette facture est deja annulee..."
  END
END
;
; Impression de la note de crédit
;
PROCEDURE DESIGNER D001
BEGIN
  IF ALTEREDRECORD OF IFNOT_CRE_MAITRE
  THEN ERROR "*E* Faire une mise a jour avant l'impression de la note de crédit"

  IF NCMIMP OF IFNOT_CRE_MAITRE = "O"
  THEN ERROR "*E* Cette note de crédit est déjà imprimée"

  IF     NCMFIN   OF IFNOT_CRE_MAITRE <> "O" &
     AND NCMANNUL OF IFNOT_CRE_MAITRE <> "O"
  THEN ERROR "*E* La note de crédit doit être finale pour être imprimée."

  RUN SCREEN gs091 &
             MODE E &
             PASSING D_IMPNOT, &
                     D_LISPAR, &
                     D_INDLON, &
                     D_LISVAL
  LET NCMIMP OF IFNOT_CRE_MAITRE = "O"
  PUT IFNOT_CRE_MAITRE
  DISPLAY NCMIMP OF IFNOT_CRE_MAITRE
END
;
; Duplicata de la note de crédit
;
PROCEDURE DESIGNER D002
BEGIN
  IF ALTEREDRECORD OF IFNOT_CRE_MAITRE
  THEN ERROR "*E* Faire une mise a jour avant l'impression de la note de crédit"

  IF    (     NCMFIN OF IFNOT_CRE_MAITRE = "O" &
          AND NCMIMP OF IFNOT_CRE_MAITRE = "O" &
        ) &
     OR NCMANNUL OF IFNOT_CRE_MAITRE = "O"
  THEN BEGIN
    RUN SCREEN gs091 &
             MODE E &
             PASSING D_DUPNOT, &
                     D_LISPAR, &
                     D_INDLON, &
                     D_LISVAL
  END
  ELSE ERROR "*E* La note de crédit doit être finale/imprimée ou annulée."
END

PROCEDURE PREUPDATE
BEGIN
  IF TMP-FIN = "O"
  THEN ERROR "*E* Mise a jour interdite car note de credit finale"
  IF TMP-NCMANNUL = "O"
  THEN ERROR "*E* La note de credit est annulee"
  IF NCMFIN = "O" AND NCMANNUL = "O"
  THEN ERROR "*E* On ne peut pas annuler une note de credit finale"

  GET CRCLIENT OPTIONAL
  IF ACCESSOK
  THEN LET DEVCLE OF IFNOT_CRE_MAITRE = DEVCLE OF CRCLIENT

  GET MCDEVISE VIA DEVCLE USING DEVCLE OF IFNOT_CRE_MAITRE OPTIONAL
  IF ACCESSOK
  THEN LET DEVTAU OF IFNOT_CRE_MAITRE = DEVTAU OF MCDEVISE

  IF DEVCLE OF IFNOT_CRE_MAITRE = "CAN" OR DEVCLE OF IFNOT_CRE_MAITRE = ""
  THEN LET NCMTOTALCANVENT = NCMFACTOT OF IFNOT_CRE_MAITRE
  ELSE LET NCMTOTALCANVENT = ROUND(DEVTAU OF IFNOT_CRE_MAITRE * &
                                   NCMFACTOT OF IFNOT_CRE_MAITRE)

  IF NEWRECORD OF IFNOT_CRE_MAITRE
  THEN BEGIN
    START TRANSACTION GEN_NUM_SEQ
    GET IFSEQUENCE VIA   CIECLE, SEQCODE &
                   USING T_CIECLE, "FAC#" OPTIONAL
    LET CIECLE  OF ifSEQUENCE = T_CIECLE
    LET SEQCODE OF IFSEQUENCE = "FAC#"
    LET SEQNO   OF IFSEQUENCE = SEQNO OF IFSEQUENCE + 1

    LET CRECLE  OF IFNOT_CRE_MAITRE = SEQNO OF IFSEQUENCE

    PUT IFSEQUENCE
  END
END

PROCEDURE POSTUPDATE
BEGIN
  DISPLAY CRECLE OF IFNOT_CRE_MAITRE
  LET TMP-NCMANNUL = NCMANNUL OF IFNOT_CRE_MAITRE
END

;
; SAISIE DU MONTANT DE TPS POUR MODIFICATION
;
PROCEDURE DESIGNER 5
BEGIN
  ACCEPT NCMMNTTPSFED
  DISPLAY T-SOUS-TOTAL
  LET NCMMNTTVQPRO OF IFNOT_CRE_MAITRE = ROUND(T-SOUS-TOTAL &
      * TAXPCT OF A_TAXPRO )
  DISPLAY NCMMNTTVQPRO OF IFNOT_CRE_MAITRE
  DISPLAY T-TOTAL-CREDIT
END

;
; SAISIE DU MONTANT DE TPS POUR MODIFICATION
;
PROCEDURE DESIGNER 6
BEGIN
  ACCEPT NCMMNTTVQPRO OF IFNOT_CRE_MAITRE
  DISPLAY T-TOTAL-CREDIT
END





PROCEDURE PATH
  BEGIN
    REQUEST CRECLE OF IFNOT_CRE_MAITRE
    IF PROMPTOK
      THEN LET PATH = 1

    IF PATH = 0
      THEN BEGIN
        REQUEST FCECLE OF IFNOT_CRE_MAITRE
        IF PROMPTOK
          THEN LET PATH = 4
        END

    IF PATH = 0
      THEN BEGIN
        LET PATH = 5
        END

    END

PROCEDURE FIND
  BEGIN
    IF PATH = 1
      THEN GET IFNOT_CRE_MAITRE VIA CIECLE, CRECLE USING T_CIECLE, CRECLE OF IFNOT_CRE_MAITRE
    IF PATH = 4
      THEN GET IFNOT_CRE_MAITRE VIA CIECLE, FCECLE USING T_CIECLE, FCECLE OF IFNOT_CRE_MAITRE
    IF PATH = 5
      THEN GET IFNOT_CRE_MAITRE VIA CIECLE USING T_CIECLE
    LET TMP-FIN = NCMFIN OF IFNOT_CRE_MAITRE
    END

PROCEDURE INTERNAL CALCUL-TAXE
BEGIN
  LET NCMMNTTPSFED OF IFNOT_CRE_MAITRE =ROUND(NCMFACTOT OF IFNOT_CRE_MAITRE &
      * TAXPCT OF A_TAXFED )
  DISPLAY NCMMNTTPSFED OF IFNOT_CRE_MAITRE
  DISPLAY T-SOUS-TOTAL
  LET NCMMNTTVQPRO OF IFNOT_CRE_MAITRE = ROUND(T-SOUS-TOTAL &
      * TAXPCT OF A_TAXPRO)
  DISPLAY NCMMNTTVQPRO OF IFNOT_CRE_MAITRE
  DISPLAY T-TOTAL-CREDIT
END

PROCEDURE DESIGNER 4
BEGIN
  ACCEPT NCMFACTOT
  DO INTERNAL CALCUL-TAXE
END

PROCEDURE ENTRY
  BEGIN
    DISPLAY CRECLE OF IFNOT_CRE_MAITRE
    ACCEPT NCMDAT OF IFNOT_CRE_MAITRE

    GET MCPERIODE_CTB SEQUENTIAL OPTIONAL
    LET PECCLE OF IFNOT_CRE_MAITRE = PECCLE OF MCPERIODE_CTB
    ACCEPT NCMFIN OF IFNOT_CRE_MAITRE
    DISPLAY NCMIMP OF IFNOT_CRE_MAITRE
    ACCEPT FCECLE OF IFNOT_CRE_MAITRE
    DISPLAY CLTCLE OF IFFACTURE
    DISPLAY CRRCLENUM OF IFFACTURE
    DISPLAY CRRCLEANN OF IFFACTURE
    DISPLAY FCEDAT OF IFFACTURE
    DISPLAY PECCLE OF IFNOT_CRE_MAITRE
    DISPLAY LCRCLE OF IFNOT_CRE_MAITRE
    DISPLAY NCMTRANSFERT OF IFNOT_CRE_MAITRE
    DISPLAY CLINOMABR OF CRCLIENT
    DISPLAY CLILNG OF CRCLIENT
    DISPLAY FCEADR1 OF IFFACTURE
    DISPLAY DEVCLE OF CRCLIENT
    DISPLAY FCEADR2 OF IFFACTURE
    DISPLAY CONCONDITION1 OF IFCONTRAT
    DISPLAY FCEADR3 OF IFFACTURE
    DISPLAY CONCONDITION2 OF IFCONTRAT
    DISPLAY FCECP OF IFFACTURE
    DISPLAY TAXPCT OF A_TAXFED
    DISPLAY TAXPCT OF A_TAXPRO
    DISPLAY CLCEXPTVP OF CRCLI_CIE
    ACCEPT NCMFACTOT OF IFNOT_CRE_MAITRE
    DO INTERNAL CALCUL-TAXE
    ACCEPT NCMCOM1 OF IFNOT_CRE_MAITRE
    ACCEPT NCMCOM2 OF IFNOT_CRE_MAITRE
    END

BUILD
@IF FORMAT
@ENDIF
