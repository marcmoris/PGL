;/T/ Liste des cédules d'entretien
;
;/P/ Programmeur..: Alain Côté
;    Date création: 2 mars 1994
;
;/V3.02.00/ Alain Côté, 2 mars 1994, Immobilisation
;
;    Description...: Ce rapport imprime la liste des immobilisations dont
;                    un entretien est prévue dans la plage de date
;                    paramètrée.
;
;
;    Paramétres....: No. cie   (%CUF_CIECLEMNU)
;                    Date debut (%DUDATDEB)
;                    Date fin   (%DUDATFIN)
;                    Nom cie   (%DUF_CIENOM)
;
;    Tris..........: Par numéro d'immobilisation.
;
;    Particularités: Les immobilisations doivent être actives pour être
;                    sélectionnés.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

;
; Pour enlever la deuxième ligne de titre.
;
SET NOBLANKS


ACCESS IMIMM_CEDULE &
  LINK CIECLE OF IMIMM_CEDULE , &
       IMMCLE OF IMIMM_CEDULE   &
    TO CIECLE , &
       IMMCLE OF IMIMMOBILISATION

CHOOSE CIECLE PARM

DEFINE D_DATDEB  DATE  = PARM
DEFINE D_DATFIN  DATE  = PARM

SELECT IMIMM_CEDULE      IF     CEDDATPRE OF IMIMM_CEDULE >= D_DATDEB &
                            AND CEDDATPRE OF IMIMM_CEDULE <= D_DATFIN

SELECT IMIMMOBILISATION  IF IMMSTU OF IMIMMOBILISATION = "A"


;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des cédules d'entretien"
@ELSE
       "%%%Liste des cédules d'entretien"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM306"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


SORT ON CIECLE    OF IMIMMOBILISATION &
     ON IMMCLE    OF IMIMMOBILISATION &
     ON CEDDATPRE OF IMIMM_CEDULE

USE ustitstd NOLIST
SKIP &
  TAB 051 &
@IF FRANCAIS
          "entre le" &
@ELSE
          "entre le" &
@ENDIF
  TAB 060 D_DATDEB &
  TAB 069 &
@IF FRANCAIS
          "et le" &
@ELSE
          "et le" &
@ENDIF
  TAB 075 D_DATFIN &
SKIP 2 &
  TAB 058 &
"------ Date -------" &
SKIP &
  TAB 002 &
@IF FRANCAIS
"  No. Immo.         Description  immobilisation          Prévue   Effectuée             Description entretien" &
@ELSE
"%%%o. Immo.         Description  immobilisation          Prévue   Effectuée             Description entretien" &
@ENDIF
SKIP &
  TAB 002 &
"------------  ----------------------------------------  --------  ---------    ----------------------------------------" &
SKIP 2



REPORT &
   TAB 002 IMMCLE OF IMIMMOBILISATION &
             PRINT AT IMMCLE &
   TAB 016 IMMDSC OF IMIMMOBILISATION &
             PRINT AT IMMCLE &
   TAB 058 CEDDATPRE OF IMIMM_CEDULE   &
   TAB 068 CEDDATREE OF IMIMM_CEDULE   &
   TAB 081 CEDCOM    OF IMIMM_CEDULE


FOOTING AT IMMCLE &
  SKIP 2


BUILD im306z
@IF FORMAT

XX999X                                        XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                    PAGE: 9999
99/99/99 99:99 XXXXXXXXXXXX                        Liste des cédules d'entretien
                                    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
                                                  entre le 99/99/99 et le 99/99/99

                                                         ------ Date -------
   No. Immo.         Description  immobilisation          Prévue   Effectuée             Description entretien
 ------------  ----------------------------------------  --------  ---------    ----------------------------------------
 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

 xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                                         99/99/99  99/99/99     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
@ENDIF
