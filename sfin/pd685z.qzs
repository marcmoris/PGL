;/T/ Détail du bon de livraison.
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-19
;
;    Description...: Détail Facture du bon de livraison
;
;    Paramètres....: ...
;
;    Particularités: ...
;/M/ Programmeur...: REMY DEMERS
;    Date modif....: 1999-03-01
;
;    Description...: arrondissement des montant
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date modification.: 06 mars 2000
;    Description.......: Afficher la description associée au type de granit. Tenir compte que la description 3 et 4 sont possibles.
;
;    Référence.........: Demande de changement 000020.
;
;    Signet............: MP-00-03-06_D20.
;
;    --------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par...: Marc Poulin
;    Date..........: 27 septembre 2000.
;    Description...: Ajustement du calcul de la surface en fonction du type de diagrame (T-triangulaire ou R-Régulier). Dans le
;                    cas des diagrammes de type "triangulaire", diviser la surface par deux.
;
;    Référence.....: Demande de changement 000096.
;
;    Signet........: MP-00-09-27_D96.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDBON_LIVRAISON &

  LINK CIECLE           OF PDBON_LIVRAISON,  &
       EXPNUMEXP        OF PDBON_LIVRAISON,  &
       BLINUMBLI        OF PDBON_LIVRAISON   &
    TO CIECLE,                               &
       EXPNUMEXP,                            &
       BLINUMBLI        OF PDLIVRE_CAISSON   &

  LINK CIECLE           OF PDBON_LIVRAISON,  &
       CAINUMCAI        OF PDLIVRE_CAISSON   &
    TO CIECLE,                               &
       CAINUMCAI        OF PDCAISSON         &

  LINK CIECLE           OF PDBON_LIVRAISON,  &
       PJTABR           OF PDBON_LIVRAISON,  &
       PJTANN           OF PDCAISSON, &
       COMNUMCOMINT     OF PDCAISSON &
    TO CIECLE,                               &
       PJTABR,                               &
       PJTANN,                               &
       COMNUMCOMINT     OF PDCOMMAN_INTERNE  &

  LINK PJTABR           OF PDBON_LIVRAISON, &
       CIECLE           OF PDCOMMAN_INTERNE   &
    TO PJTABR,                                &
       CIECLE           OF PDPROJET           &

  LINK ("----", &
       CLICLE       OF PDBON_LIVRAISON , &
       CIECLE       OF PDCOMMAN_INTERNE)   &
  TO   CLICIECLE                        , &
       CLICLE                           , &
       CIECLE       OF VCLC_CLI           &

  LINK CIECLE           OF PDBON_LIVRAISON,  &
       CAINUMCAI        OF PDLIVRE_CAISSON   &
    TO CIECLE,                               &
       CAINUMCAI        OF PDEMBAL_TRANCHE   OPTIONAL &

  LINK CIECLE           OF PDEMBAL_TRANCHE,  &
       TRANUMTRA002     OF PDEMBAL_TRANCHE   &
    TO CIECLE,                               &
       TRANUMTRA002     OF PDTRANCHE OPTIONAL &

  LINK (CIECLE           OF PDEMBAL_TRANCHE,  &
        PJTABR           OF PDEMBAL_TRANCHE,  &
        PJTANN           OF PDEMBAL_TRANCHE,  &
        COMNUMCOMINT     OF PDEMBAL_TRANCHE,  &
        DCINUMLIG        OF PDEMBAL_TRANCHE,  &
        "TR")                           &
    TO CIECLE,                          &
       PJTABR,                          &
       PJTANN,                          &
       COMNUMCOMINT,                    &
       DCINUMLIG,                       &
       TPDTYPPRD        OF PDDETAIL_C_I      ALIAS A_DCI_TRANCHE OPTIONAL &

  LINK CIECLE           OF PDBON_LIVRAISON,  &
       CAINUMCAI        OF PDLIVRE_CAISSON   &

    TO CIECLE,                               &
       CAINUMCAI        OF PDEMBAL_DIAG      OPTIONAL &

  LINK CIECLE           OF PDBON_LIVRAISON  ,&
       DIANUM002        OF PDEMBAL_DIAG      &
    TO CIECLE,                               &
       DIANUM002    OF PDDIAGRAMME       OPTIONAL &

;  LINK CIECLE           OF PDBON_LIVRAISON,  &
;       COMNUMCOM        OF PDDIAGRAMME,      &
;       DCINUMLIG        OF PDDIAGRAMME       &
;    TO CIECLE,                               &
;       COMNUMCOM,                            &
;       DCINUMLIG        OF PDDETAIL_C_I      ALIAS A_DCI_DIAGRAMME OPTIONAL &


  LINK (CIECLE           OF PDDIAGRAMME,&
        PJTABR           OF PDDIAGRAMME,&
        PJTANN           OF PDDIAGRAMME,&
        COMNUMCOMINT     OF PDDIAGRAMME,&
        DCINUMLIG        OF PDDIAGRAMME,&

        "DI")                           &
    TO CIECLE,                          &
       PJTABR,                          &
       PJTANN,                          &
       COMNUMCOMINT,                    &
       DCINUMLIG,                       &

       TPDTYPPRD        OF PDDETAIL_C_I      ALIAS A_DCI_DIAGRAMME OPTIONAL &


  LINK (CIECLE           OF PDBON_LIVRAISON, &
        "M2",                                &
        "P2")                                &
    TO CIECLE,                               &
       CUMCODUNMINT,                         &
       CUMCODUNMEXT OF PDCONVERT_UN_M        &

  LINK CIECLE    OF PDDIAGRAMME,&
       TGRCODGRA OF PDDIAGRAMME &
   TO  CIECLE ,&
       TGRCODGRA OF PDTYPE_GRANIT ALIAS A_TYPE_DIAG OPTIONAL &

  LINK CIECLE    OF PDTRANCHE ,&
       TGRCODGRA OF PDTRANCHE &
   TO  CIECLE ,&
       TGRCODGRA OF PDTYPE_GRANIT ALIAS A_TYPE_TRAN OPTIONAL &

  LINK CIECLE    OF PDDIAGRAMME ,&
       TYFCODFIN OF PDDIAGRAMME   &
  TO  CIECLE ,&
       TYFCODFIN OF PDTYPE_FINI ALIAS A_TYPE_DIAG2 OPTIONAL &

  LINK CIECLE    OF PDTRANCHE ,&
       TYFCODFIN OF PDTRANCHE &
   TO  CIECLE ,&
       TYFCODFIN OF PDTYPE_FINI ALIAS A_TYPE_TRAN2 OPTIONAL &

  LINK CIECLE           OF PDBON_LIVRAISON,  &
       EXPNUMEXP        OF PDBON_LIVRAISON,  &
       BLINUMBLI        OF PDBON_LIVRAISON   &
    TO CIECLE,                               &
       EXPNUMEXP,                            &
       BLINUMBLI        OF PDCONTENEUR OPT



CHOOSE CIECLE    PARM , &
       EXPNUMEXP PARM , &
       BLINUMBLI PARM

SELECT A_DCI_TRANCHE IF RECORD PDTRANCHE EXISTS
SELECT A_DCI_DIAGRAMME IF RECORD PDDIAGRAMME EXISTS

;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le rapport
;


DEFINE D_NUMPIE CHARACTER SIZE 7 = TRANUMTRA002 OF PDTRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                             ELSE DIANUM002 OF PDDIAGRAMME

DEFINE D_COMCOM CHAR SIZE 7 = COMNUMCOM OF PDEMBAL_TRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                             ELSE COMNUMCOM OF PDEMBAL_DIAG


DEFINE D_UNMPRX_1 CHARACTER SIZE 02 = DCIUNMVEN OF A_DCI_TRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                                 ELSE DCIUNMVEN OF A_DCI_DIAGRAMME


; ----------------------------------------------------------------------------------------------------------------
; MP-00-09-27_D96.

define d_surpie_mx_t float size 08 = emtsurmc2ven of pdembal_tranche &
                                  if tpdtypprd of pdcaisson = "TR"   &
                                else diasurmc2 of pddiagramme

define d_surpie_mx   float size 08 = emtsurmc2ven of pdembal_tranche                                          &
                                  if tpdtypprd of pdcaisson = "TR" and d_unmprx_1 = "M2"                      &
                                else round((emtsurmc2ven of pdembal_tranche * cumfaccon of pdconvert_un_m),5) &
                                  if tpdtypprd of pdcaisson = "TR"                                            &
                                else diasurmc2 of pddiagramme                                                 &
                                  if d_unmprx_1 = "M2"                                                        &
                                else round((diasurmc2 of pddiagramme * cumfaccon of pdconvert_un_m),5)

define d_surpie_px   float size 08 = round((d_surpie_mx * cumfaccon of pdconvert_un_m),5)

define d_surpie_m2_t float size 08 = d_surpie_mx_t / 2 if compridet of pdcomman_interne = "0" and diatypdia of pddiagramme = "T" else d_surpie_mx_t
define d_surpie_m2   float size 08 = d_surpie_mx   / 2 if compridet of pdcomman_interne = "0" and diatypdia of pddiagramme = "T" else d_surpie_mx
define d_surpie_p2   float size 08 = d_surpie_px   / 2 if compridet of pdcomman_interne = "0" and diatypdia of pddiagramme = "T" else d_surpie_px

define d_etoile  character size 01 = "*" if compridet of pdcomman_interne = "0" and diatypdia of pddiagramme = "T"

define d_remarque character size 100 = " * --> Indique que la pièce est de type Triangulaire (dans ce cas, la surface est réduite de moitié)" &
                                    if comcodlngcor of pdcomman_interne = "F"                                                                 &
                                  else " * --> Triangal pieces"

; ----------------------------------------------------------------------------------------------------------------


DEFINE D_UNFRAN CHARACTER SIZE 02 = "M2" IF (D_UNMPRX_1 = "M2" AND  COMCODLNGCOR OF PDCOMMAN_INTERNE = "F") &
ELSE "SM" IF ( D_UNMPRX_1 = "M2" AND  COMCODLNGCOR OF PDCOMMAN_INTERNE <> "F") &
ELSE "P2" IF ( D_UNMPRX_1 = "P2" AND  COMCODLNGCOR OF PDCOMMAN_INTERNE = "F") &
ELSE "SF" IF (D_UNMPRX_1 = "P2" AND COMCODLNGCOR OF PDCOMMAN_INTERNE <> "F")


DEFINE D_QTE      FLOAT     SIZE 08 = 1.00 IF TPDTYPPRD OF PDCAISSON = "TR" &
                                           ELSE EMDQTEPRD OF PDEMBAL_DIAG

DEFINE D_UNMPRX_2 CHARACTER SIZE 02 = DCIUNMVEN OF A_DCI_TRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                                 ELSE DCIUNMVEN OF A_DCI_DIAGRAMME

DEFINE D_UNMPRX_M2 CHARACTER SIZE 02 = "M2" IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                            ELSE "SM"

DEFINE D_UNMPRX_P2 CHARACTER SIZE 02 = "P2" IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                            ELSE "SF"

DEFINE D_SURTOT_M2 FLOAT    SIZE 08 = round((D_SURPIE_M2 * D_QTE),5)
DEFINE D_SURTOT_M2_T FLOAT    SIZE 08 = round((D_SURPIE_M2_T * D_QTE),5)

DEFINE D_SURTOT_P2 FLOAT    SIZE 08 = round((D_SURTOT_M2 * CUMFACCON OF PDCONVERT_UN_M),5)

DEFINE D_LONG     INTEGER   SIZE 04 = EMTLONVEN OF PDEMBAL_TRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                          ELSE DIALON OF PDDIAGRAMME
;ELSE EMDLONVEN OF PDEMBAL_DIAG

DEFINE D_HAUT     INTEGER   SIZE 04 = EMTHAUVEN OF PDEMBAL_TRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                          ELSE DIAHAU OF PDDIAGRAMME
;ELSE EMDHAUVEN OF PDEMBAL_DIAG

DEFINE D_EPAI     INTEGER   SIZE 04 = EMTEPAVEN OF PDEMBAL_TRANCHE IF TPDTYPPRD OF PDCAISSON = "TR" &
                                                          ELSE DIAEPA OF PDDIAGRAMME
;ELSE EMDEPAVEN OF PDEMBAL_DIAG

DEFINE D_GRA CHARACTER SIZE 30 = TGRDSCFRA001 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "1" &
                            ELSE TGRDSCFRA001 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "1" &
                            ELSE TGRDSCANG001 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "1" &
                            ELSE TGRDSCANG001 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "1" &
                            ELSE TGRDSCFRA002 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "2" &
                            ELSE TGRDSCFRA002 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "2" &
                            ELSE TGRDSCANG002 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "2" &
                            ELSE TGRDSCANG002 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "2" &

                            ELSE TGRDSCFRA003 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "3" & ; MP-00-03-06_D20.
                            ELSE TGRDSCFRA003 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "3" & ; MP-00-03-06_D20.
                            ELSE TGRDSCANG003 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "3" & ; MP-00-03-06_D20.
                            ELSE TGRDSCANG003 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "3" & ; MP-00-03-06_D20.

                            ELSE TGRDSCFRA004 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "4" & ; MP-00-03-06_D20.
                            ELSE TGRDSCFRA004 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "4" & ; MP-00-03-06_D20.
                            ELSE TGRDSCANG004 OF A_TYPE_TRAN  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPGRA OF A_DCI_TRANCHE   = "4" & ; MP-00-03-06_D20.
                            ELSE TGRDSCANG004 OF A_TYPE_DIAG  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPGRA OF A_DCI_DIAGRAMME = "4"   ; MP-00-03-06_D20.

DEFINE D_FIN CHARACTER SIZE 15 = TYFDSCFRA001 OF A_TYPE_TRAN2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPFIN OF A_DCI_TRANCHE   = "1" &
                            ELSE TYFDSCFRA001 OF A_TYPE_DIAG2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPFIN OF A_DCI_DIAGRAMME = "1" &
                            ELSE TYFDSCANG001 OF A_TYPE_TRAN2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPFIN OF A_DCI_TRANCHE   = "1" &
                            ELSE TYFDSCANG001 OF A_TYPE_DIAG2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPFIN OF A_DCI_DIAGRAMME = "1" &
                            ELSE TYFDSCFRA002 OF A_TYPE_TRAN2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPFIN OF A_DCI_TRANCHE   = "2" &
                            ELSE TYFDSCFRA002 OF A_TYPE_DIAG2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPFIN OF A_DCI_DIAGRAMME = "2" &
                            ELSE TYFDSCANG002 OF A_TYPE_TRAN2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "TR" AND DCITYPFIN OF A_DCI_TRANCHE   = "2" &
                            ELSE TYFDSCANG002 OF A_TYPE_DIAG2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A" AND TPDTYPPRD OF PDCAISSON = "DI" AND DCITYPFIN OF A_DCI_DIAGRAMME = "2" 

DEFINE D_SUBTOT_M2   CHARACTER SIZE 20 = "Sous-total M2  : " IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                             ELSE "Sub-total SM   : "

DEFINE D_SUBTOT_P2   CHARACTER SIZE 20 = "Sous-total P2  : " IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                             ELSE "Sub-total SF   : "

DEFINE D_TXTSOUSTOTAL CHARACTER SIZE 20 = "Sous-total " IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
ELSE "Sub-total "

DEFINE D_GRATOT_M2   CHARACTER SIZE 20 = "Grand-total M2 : " IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                             ELSE "Total SM       : "

DEFINE D_GRATOT_P2   CHARACTER SIZE 20 = "Grand-total P2 : " IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                             ELSE "Total SF     2 : "
DEFINE D_TXTTOTAL CHARACTER SIZE 20 = "Total "

DEFINE D_PROJET      CHARACTER SIZE 15 = "Num. projet...:" IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                           ELSE "Project no....:"

DEFINE D_CLIENT      CHARACTER SIZE 15 = "Num. client...:" IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                           ELSE "%%%. client...:"

DEFINE D_ENTETE_0    CHARACTER SIZE 132 =  "Lig Granite                         Fini              Epai "&
                                           IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                           ELSE &
                                           "Lig Granit                          Finish            Thick "


DEFINE D_ENTETE_1    CHARACTER SIZE 132 =  "   Piece    Long Haut      Surface U/M     Prix U/M        Qte    Surface U/M       Total  U/M" &
                                           IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                           ELSE &
                                           "   Piece    Lengh High     Surface U/M     Price U/M        Qty     Surface U/M     Total  U/M "


DEFINE D_UMSOUSTOTAL CHARACTER SIZE 2 = D_UNMPRX_M2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                            ELSE D_UNMPRX_P2

DEFINE D_SOUSTOTAL INTEGER SIZE 4 = D_SURTOT_M2 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                            ELSE D_SURTOT_P2


DEFINE D_DCINUMLIG CHAR SIZE 4 = DCINUMLIG OF A_DCI_TRANCHE IF RECORD &
                    A_DCI_TRANCHE EXIST ELSE DCINUMLIG OF A_DCI_DIAGRAMME


DEFINE D_CHADEPAI     CHAR SIZE 4 = ASCII(D_EPAI)

DEFINE D_TRI CHAR SIZE 60 = D_COMCOM + D_DCINUMLIG + D_GRA + D_FIN + D_CHADEPAI

DEFINE D_PRIUNITAIRE FLOAT SIZE 8 = DIAMNTPRXPIE OF PDDIAGRAMME IF RECORD A_DCI_DIAGRAMME &
                                    EXIST AND DIAMNTPRXPIE OF PDDIAGRAMME <> 0 ELSE &
                                    DCIPRIVEN OF A_DCI_DIAGRAMME IF RECORD A_DCI_DIAGRAMME EXIST ELSE &
                                    DCIPRIVEN OF A_DCI_TRANCHE



DEFINE D_PRIUNITAIRE_T FLOAT SIZE 8 = DIAMNTPRXPIE OF PDDIAGRAMME IF RECORD A_DCI_DIAGRAMME &
                                    EXIST AND DIAMNTPRXPIE OF PDDIAGRAMME <> 0 ELSE &
                                    DCIPRIVEN OF A_DCI_DIAGRAMME IF D_UNMPRX_1 = "M2" AND RECORD A_DCI_DIAGRAMME EXIST ELSE &
                                    ROUND((DCIPRIVEN OF A_DCI_DIAGRAMME * CUMFACCON OF PDCONVERT_UN_M),2) &
                                                                                      IF RECORD A_DCI_DIAGRAMME EXIST ELSE &
                                    DCIPRIVEN OF A_DCI_TRANCHE IF D_UNMPRX_1 = "M2" ELSE &
                                    ROUND((DCIPRIVEN OF A_DCI_TRANCHE * CUMFACCON OF PDCONVERT_UN_M),2)

DEFINE D_UNMPRIUNITAIRE CHAR SIZE 2 = "PE" IF RECORD A_DCI_DIAGRAMME &
                                    EXIST AND DIAMNTPRXPIE OF PDDIAGRAMME <> 0 ELSE &
                                    "M2" IF (D_UNMPRX_1 = "M2" AND  COMCODLNGCOR OF PDCOMMAN_INTERNE = "F") &
                                    ELSE "SM" IF ( D_UNMPRX_1 = "M2" AND  COMCODLNGCOR OF PDCOMMAN_INTERNE <> "F") &
                                    ELSE "P2" IF ( D_UNMPRX_1 = "P2" AND  COMCODLNGCOR OF PDCOMMAN_INTERNE = "F") &
                                    ELSE "SF" IF (D_UNMPRX_1 = "P2" AND COMCODLNGCOR OF PDCOMMAN_INTERNE <> "F")


DEFINE D_PRITOTAL FLOAT SIZE 8 = ROUND((D_PRIUNITAIRE_T * D_QTE),5) IF D_UNMPRIUNITAIRE ="PE" &
                                 ELSE ROUND((D_PRIUNITAIRE_T * D_SURTOT_M2_T),5)


SORT ON EXPNUMEXP OF PDBON_LIVRAISON &
     ON BLINUMBLI OF PDBON_LIVRAISON &
     ON D_TRI                        &
     ON D_NUMPIE

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Détail de la facture" IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                                             ELSE "Invoice detail"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ASCII(EXPNUMEXP OF PDBON_LIVRAISON) + &
                                     "-" + (BLINUMBLI OF PDBON_LIVRAISON) + &
                                     "-" + (BLIAMM    OF PDBON_LIVRAISON)
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD685"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitstd45 NOLIST
  SKIP 2 &
  TAB 001 D_PROJET &
  TAB 017 PJTABR OF PDBON_LIVRAISON &
  TAB 025 PJTNOM OF PDPROJET &
  SKIP &
  TAB 001 D_CLIENT &
  TAB 017 CLICLE OF PDBON_LIVRAISON &
  TAB 025 CLINOM OF VCLC_CLI &
  TAB 080 "Container No.:" &
  TAB 096 CTNNUMCON OF PDCONTENEUR &
  SKIP 2 &
  TAB 001 D_ENTETE_0 &
  SKIP &
  TAB 001 D_ENTETE_1 &
  SKIP 2


HEADING AT D_TRI &
SKIP &
  TAB 001 D_DCINUMLIG PRINT AT D_TRI &
  TAB 005 D_GRA PRINT AT D_TRI &
  TAB 037 D_FIN PRINT AT D_TRI &
  TAB 054 D_EPAI PICTURE "^^^^" PRINT AT D_TRI &
  TAB 070 d_comcom PRINT AT D_TRI &
SKIP

REPORT &
  tab 001 d_etoile & ; MP-00-09-27_D96.
  TAB 004 D_NUMPIE &
  TAB 013 D_LONG PICTURE "^^^^" &
  TAB 019 D_HAUT PICTURE "^^^^" &
  TAB 025 D_SURPIE_M2 PICTURE "^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 036 D_UNFRAN &
  TAB 039 D_PRIUNITAIRE PICTURE " ^^^,^^^.^^"  &
  TAB 050  "$" &
  TAB 053 D_UNMPRIUNITAIRE &
  TAB 058 D_QTE PICTURE "^^^^^" &
  TAB 064 D_SURTOT_M2 PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 077 D_UNFRAN &
  TAB 081 D_PRITOTAL PICTURE " ^^^,^^^.^^" &
   TAB 092  "$" &
  TAB 095 D_UNMPRIUNITAIRE

FOOTING AT D_TRI &
  TAB 025 "----------" &
  TAB 039 "----------" &
  TAB 061 "---" &
  TAB 065 "-----------" &
  TAB 081 "----------" &
SKIP &
  TAB 008 D_TXTSOUSTOTAL &
  TAB 025 D_SURPIE_M2 SUBTOTAL RESET AT D_TRI PICTURE "^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 039 D_PRIUNITAIRE PICTURE " ^^^,^^^.^^" &
  TAB 057 "$" &
  TAB 058 D_QTE SUBTOTAL RESET AT D_TRI PICTURE "^^^^^"  &
  TAB 064 D_SURTOT_M2 SUBTOTAL RESET AT D_TRI PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 081 D_PRITOTAL SUBTOTAL RESET AT D_TRI PICTURE " ^^^,^^^.^^" &
  TAB 092 "$"   &
;  TAB 128 D_UMSOUSTOTAL
SKIP 2


FOOTING AT BLINUMBLI &
 SKIP 2 &
  TAB 025 "----------" &
 ; TAB 039 "----------" &
  TAB 061 "---" &
  TAB 065 "-----------" &
  TAB 081 "----------" &
SKIP &
  TAB 008 D_TXTTOTAL &
  TAB 025 D_SURPIE_M2 SUBTOTAL RESET AT BLINUMBLI PICTURE "^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 039 D_PRIUNITAIRE PICTURE "^^^,^^^.^^" &
  TAB 058 D_QTE SUBTOTAL RESET AT BLINUMBLI PICTURE "^^^^^"  &
  TAB 064 D_SURTOT_M2 SUBTOTAL RESET AT BLINUMBLI PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 081 D_PRITOTAL SUBTOTAL RESET AT BLINUMBLI PICTURE " ^^^,^^^.^^" &
  TAB 092  "$"   &
SKIP PAGE
;  TAB 128 D_UMSOUSTOTAL


page footing skip 2 tab 1 " ___________________________________________________________________________________________________" & ; MP-00-09-27_D96.
             skip 2 tab 1 d_remarque                                                                                               ; MP-00-09-27_D96.


BUILD pd685z

@IF FORMAT

PD685                                                     Granicor inc.                                                   PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX                         Détail du bon de livraison

Num. projet...: XX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
Num. client...: XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num. caisson  Num. pièce  U/M      Surface     Qte  U/M     Surface   Long   Haut   Épai Ganit Fini
   Crate #     Piece #    U/M  Surface/piece   Qty  U/M Surface/total Length High   tick Ganite Finish
 999999999     XXXXXXX    XX   999999.9999   99999  XX  999999.9999   9999   9999   9999  999   99
               XXXXXXX    XX   999999.9999   99999  XX  999999.9999   9999   9999   9999  999   99
               XXXXXXX    XX   999999.9999   99999  XX  999999.9999   9999   9999   9999  999   99
               XXXXXXX    XX   999999.9999   99999  XX  999999.9999   9999   9999   9999  999   99

                      1234567890123456789:   99999  XX  999999.9999
                      1234567890123456789:   99999  XX  999999.9999

 999999999     XXXXXXX     999999.9999   99999   999999.9999   9999   9999   9999  999   99
               XXXXXXX     999999.9999   99999   999999.9999   9999   9999   9999  999   99
               XXXXXXX     999999.9999   99999   999999.9999   9999   9999   9999  999   99
               XXXXXXX     999999.9999   99999   999999.9999   9999   9999   9999  999   99

                           Sous-total:   99999   999999.9999
                      1234567890123456789:   99999  XX  999999.9999

                          Grand total:   99999   999999.9999
                      1234567890123456789:   99999  XX  999999.9999
@ENDIF
