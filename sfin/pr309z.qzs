;/T/ Sommaire des transaction au sous-projet

USE ussetqzs NOLIST
SET FORMFEED

ACCESS GPACT_TRANS  &

  LINK  CIECLE, &
        PROCLE  &
  TO    CIECLE, &
        PROCLE OF GPPROJET  &

  LINK  CIECLE, &
        PROCLE, &
        ACTCLE  &
  TO    CIECLE, &
        PROCLE, &
        ACTCLE OF GPACTIVITE  &

  LINK  CIECLE, &
        UNACLE OF GPACT_TRANS &
  TO    CIECLE, &
        UNACLE OF MCUNITE_ADM &

  LINK  CPTCLE OF GPACT_TRANS &
  TO    CPTCLE OF VCPT_DSC &

  LINK  NACCLE OF VCPT_DSC &
  TO    NACCLE OF MCNATURE_CTB

CHOOSE CIECLE PARM, &
       PROCLE PARM, &
       ACTCLE PARM

DEFINE D-MNT     INTEGER SIZE 4 = 0
DEFINE D-TRDMNT  FLOAT   SIZE 8 = TRDMNT OF GPACT_TRANS

DEFINE D-TRDMNT2 FLOAT   SIZE 8 = TRDMNT OF GPACT_TRANS &

        IF "C" = CPTCODDC ELSE 0 - TRDMNT OF GPACT_TRANS

DEFINE D-TOT     FLOAT   SIZE 8 = D-TRDMNT

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
                       "Sommaire des transactions au sous-projet"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "PR309"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

SORT ON PROCLE &
     ON ACTCLE &
     ON NACCLE &
     ON CPTCLE &
     ON UNACLE

USE ustitstd NOLIST
  KEEP

HEADING AT PROCLE &
  TAB 001 "Projet :" &
  TAB 010 PROCLE   OF GPACT_TRANS &
  TAB 020 PRODSC   OF GPPROJET &
SKIP 1 &
  TAB 020 PRODSC2  OF GPPROJET &
SKIP 3

HEADING AT ACTCLE &
  TAB 001 "Sous-projet :" &
  TAB 015 ACTCLE    OF GPACT_TRANS &
  TAB 020 ACTDSC1   OF GPACTIVITE  &
SKIP 4

REPORT &
  TAB 005 CPTCLE    OF GPACT_TRANS HEAD "Compte"      &
  TAB 012 CPTDSCABR OF VCPT_DSC    HEAD "Description" &
  TAB 033 UNACLE    OF GPACT_TRANS HEAD "Unite^Adm."  &
  TAB 042 UNANOMABR OF MCUNITE_ADM HEAD "Description" &

  TAB 063 D-MNT    &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &
      HEAD "Budget" &

  TAB 080 D-MNT    &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &
      HEAD "Engagement" &

  TAB 097 D-TRDMNT &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &
      HEAD "Réel" &

  TAB 114 D-TOT    &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &
      HEAD "Total"

FOOT AT NACCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
SKIP &
  TAB 015 "Total" NACDSCFRA OF MCNATURE_CTB &

  TAB 063 D-MNT    SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &

  TAB 080 D-MNT    SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &

  TAB 097 D-TRDMNT SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &

  TAB 114 D-TOT    SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP 2

FOOT AT ACTCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
SKIP &
  TAB 015 "Total projet:" &
  TAB 030 PROCLE &
  TAB 040 "Sous-projet:" &
  TAB 055 ACTCLE &
  TAB 063 D-MNT     SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D-MNT     SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D-TRDMNT2 SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 114 D-TRDMNT2 SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP PAGE



FOOT AT PROCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
SKIP &
  TAB 015 "Grand total projet:" &
  TAB 035 PROCLE &
  TAB 063 D-MNT     SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &

  TAB 080 D-MNT     SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      SIGNIF 5 LEAD "<" TRAIL ">" &

  TAB 097 D-TRDMNT2 SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &

  TAB 114 D-TRDMNT2 SUBTOTAL &
      PICTURE " ^^^,^^^,^^^.^^ " &
      LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP PAGE

BUILD pr309z
