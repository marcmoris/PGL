;/T/ Commande , Historique des transactions.
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 25 octobre 1994
;
;    Description..: Affiche les transactions qui affecte la commande.
;
;/M/ Pascal Tremblay, 98-01-20
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar007f MODE LABEL "" AT 1,80 &
              NOACTION FIELDMARK RETAIN &
              FROM 2,1 TO 15,80 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND &
              RECEIVING ARCOMMANDE

USE ussectmp  NOLIST     
    "AR007F"                 

USE ar007f.hlp NOLIST

;-------------------------------------------------------------------------------

FILE    ARCOMMANDE       MASTER

FILE    ARCMD_HISTO      PRIMARY &
                        NOITEM  &
                        OCCURS 9

  ACCESS VIA    CIECLE, &
                CMDCLE, &
                CMDAJT  &
         USING  CIECLE OF ARCOMMANDE, &
                CMDCLE OF ARCOMMANDE, &
                CMDAJT OF ARCOMMANDE  &
        ORDERBY CIECLE, &
                CMDCLE, &
                CMDAJT, &
                CMHTIMSTP

;-------------------------------------------------------------------------------
;
; Pour afficher le type de transaction(BILINGUE).
;
DEFINE    D_CMHTYP CHARACTER SIZE 16 = "CMHTYP"
;
FILE VCBI_DSC          REFERENCE &
                      OCCURS WITH ARCMD_HISTO

  ACCESS VIA    XELNOM, &
                CBICLE  &
         USING  D_CMHTYP, &
                CMHTYP OF ARCMD_HISTO

DRAW 01,01 TO 01,79
DRAW 01,01 TO 14,01
DRAW 14,01 TO 14,80
DRAW 02,80 TO 14,80

USE ushilite NOLIST     

TITLE &
@IF FRANCAIS
      " Historique des transactions " &
@ELSE
      " Transaction History " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           

SKIP

ALIGN (,3,19) 

FIELD CMDCLE OF ARCOMMANDE &
        FIXED

SKIP

TITLE &
"                    Date                           Montant          Montant" &
AT ,2
SKIP

TITLE &
" Type            approbation Référence                brut              net" &
AT ,2

ALIGN (3,2,3) (,,19) (,,31) (,,41) (,,46) (,,63)

CLUSTER OCCURS WITH ARCMD_HISTO ID BASE 10

FIELD CBIDSC OF VCBI_DSC HIDDEN LABEL " " &
        DISPLAY &
        SIZE 15

FIELD CMHDATJOU OF ARCMD_HISTO  FORMAT YYYYMMDD &
        DISPLAY

FIELD CMHCLE2   OF ARCMD_HISTO &
        DISPLAY

FIELD CMHCLE3   OF ARCMD_HISTO &
        DISPLAY

FIELD CMHMNT    OF ARCMD_HISTO &
        DISPLAY &
        PICTURE "^^^,^^^,^^^.^^ " &
        TRAILING "-"

FIELD CMHMNTNET OF ARCMD_HISTO &
        DISPLAY &
        PICTURE "^^^,^^^,^^^.^^ " &
        TRAILING "-"

CLUSTER

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

BUILD
@IF FORMAT
************************* Historique des transactions *************************x
* Commande......: xxxxxxxxx                                                    *
*                    Date                           Montant          Montant   *
* Type            approbation Référence                brut              net   *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxx  xxxxxxxx   xxxxxxxxx xxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx  *
********************************************************************************
 
@ENDIF
