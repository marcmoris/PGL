;/T/ Gestion des Lots
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Alain Côté / Adapté pour les C.P. par Thomas BRENNEUR
;    Date Création: 25 mai 1993
;
;    Description..: Cet écran permet de définir les lots de transactions
;                   pour les payables. Les autorisations de journalisations
;                   sont gérées dans cet écran.
;
;                   Cet écran est utilisé par les lots de facture,les
;                   lots de relevés de dépenses et les lots de paiements la
;                   seul chose qui est différente sont les type de lot
;                   (FA,RD,PP,CH...)
;
;/M/ François Déry, 30 avril 1999
;       Modifier l'accèss au fichier de séquence des lots car il a
;       été converti en format interbase au lieu d'indexé.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date.............: 05 mai 2000
;    Description......: Ajuster le traitement afin de restreindre l'accès à certaines information l'usager utilisateur uniquement.
;
;    Référence........: Stabilisation du module Financier (Comptes à payer) - point 02.
;    
;    Signet...........: MP-00-05-05_SF02.
;
;    *** Attention... cette modification a été désactivée à la demande du client (Nathalie Légaré - Josée Lamothe), le 2000/05/11.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp020 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_PECCLEMNU, &
                       T_PECCLECOU, &
                       T_PECCLEFND, &
                       T_LCPCLEFND

TEMPORARY T_PECCLECOU_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_FIELDTEXT_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

USE ussectmp  NOLIST
    "CP020"

USE cp020.hlp NOLIST

USE usactecr  NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Vérification" ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreens"
  MENUITEM LABEL "Verification" ACTION DESIGNER D001
@ENDIF

;
; Il faut ouvrir une nouvelle transation, car cet écran est appellé comme
; sous-écran dans les transactions(facture, encaissement).
;
USE ustrasse NOLIST
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE CPLCP_SEQ IN DICT
                                                ; Le IN DICT est pour unix

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Nom de la compagnie du menu.

TEMPORARY T_PECCLEMNU CHARACTER SIZE 4 ; Période du menu
TEMPORARY T_PECCLECOU CHARACTER SIZE 4 ; Période courante du module C/P.
TEMPORARY T_PECCLEFND CHARACTER SIZE 4 ; Période de recherche.
TEMPORARY T_LCPCLEFND CHARACTER SIZE 4 ; Numéro de lot de recherche.


;-------------------------------------------------------------------------------
;

FILE CPLOT            PRIMARY
  ITEM CIECLE    OF CPLOT INITIAL T_CIECLEMNU
  ITEM LCPDATCRE OF CPLOT INITIAL SYSDATE
  ITEM LCPHRECRE OF CPLOT INITIAL SYSTIME / 10000
  ITEM LCPUSRCRE OF CPLOT INITIAL LOGONID
  ITEM LCPDATMOD OF CPLOT FINAL   SYSDATE        &
                                        IF NOT NEWRECORD OF CPLOT AND &
                                           ALTEREDRECORD OF CPLOT
  ITEM LCPHREMOD OF CPLOT FINAL   SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF CPLOT AND &
                                           ALTEREDRECORD OF CPLOT
  ITEM LCPUSRMOD OF CPLOT FINAL   LOGONID         &
                                        IF NOT NEWRECORD OF CPLOT AND &
                                           ALTEREDRECORD OF CPLOT

;-------------------------------------------------------------------------------
;
; Pour valider et afficher l'usager.
;
FILE GSUSAGER         DESIGNER &
                      OPEN READ

;-------------------------------------------------------------------------------
;
; Numéro séquentiel des lots.
;
FILE CPLCP_SEQ        DESIGNER &
                      TRANSACTION GEN_NUM_SEQ

;-------------------------------------------------------------------------------
;
; Validation de la période spécifique à la compagnie.
;
FILE MCPERIODE_CIE    REFERENCE

  ACCESS  VIA   CIECLE, &
                PECCLE &
          USING T_CIECLEMNU, &
                PECCLE OF CPLOT

;-------------------------------------------------------------------------------
;
; Validation du type d'écriture.
;
DEFINE    D_LCPTYPECR CHARACTER SIZE 16 = "LCPTYPECR"
TEMPORARY T_LCPTYPECR CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_LCPTYPECR, &
                LCPTYPECR OF CPLOT

;-------------------------------------------------------------------------------
;
; Supression de la sélection si on supprime le lot
;
FILE CPSEL_CAP  DELETE &
                NOITEM

  ACCESS  VIA   CIECLE, &
                PECCLE, &
                LCPCLE &
          USING CIECLE OF CPLOT, &
                PECCLE OF CPLOT, &
                LCPCLE OF CPLOT

;-------------------------------------------------------------------------------

TEMPORARY T_USRCLE    CHARACTER SIZE 12 ; Popup sur les usagers
TEMPORARY T_PECCLE    CHARACTER SIZE  4 ; Popup sur les périodes
TEMPORARY T_MODCLE    CHARACTER SIZE  2 ; Popup sur les périodes

;
; Pour afficher le nom des usagers.
;
TEMPORARY T_LCPUSR_NOM    CHARACTER SIZE 40
TEMPORARY T_LCPUSRVAL_NOM CHARACTER SIZE 40
TEMPORARY T_LCPUSRJOU_NOM CHARACTER SIZE 40
TEMPORARY T_LCPUSRATR_NOM CHARACTER SIZE 40

;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)

;
; Calcul de l'écart entre le montant vérifié et le montant cumulé dans le
; lot.
;
DEFINE D_DIFMNTTRS FLOAT   SIZE 8 = LCPCUMMNTVER OF CPLOT - &
                                    LCPCUMMNT    OF CPLOT
;
; Calcul de l'écart entre le nombre vérifié et le nombre cumulé dans le
; lot.
;
DEFINE D_DIFNBRTRS INTEGER SIZE 2 = LCPNBRTRSVER OF CPLOT - &
                                    LCPNBRTRS    OF CPLOT
;
; Hors balance.
;
DEFINE D_HRSBLC CHARACTER SIZE 20 = &
@IF FRANCAIS
                                    "*** Hors Balance ***" &
@ELSE
                                    "** Out of balance **" &
@ENDIF
                                    IF  0 <> LCPNBRTRSERR OF CPLOT


;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des lots " &
@ELSE
      " Batch Management " &
@ENDIF
      CENTERED AT ,1

SKIP

USE ustitoff NOLIST

ALIGN (19,3,19) (,40,56)

FIELD PECCLE OF CPLOT HIDDEN ID 05 &
LABEL "Période ......:"&
        DEFAULT T_PECCLEMNU &
        NOCHANGE &
        DUPLICATE &
        LOOKUP  ON MCPERIODE_CIE &
                MESSAGE 405 ; Cette période n'est pas définie pour cette compagnie.

FIELD LCPCLE     OF CPLOT &
LABEL "Numéro de lot.:"&
        DISPLAY &
        NUMERIC &
        SIGNIFICANCE 4

SKIP 1

ALIGN (19,3,19)

FIELD LCPDSC     OF CPLOT HIDDEN ID 10 &
LABEL "Description...:"

SKIP 1

ALIGN (19,3,19) (,,22)

FIELD LCPTYPECR  OF CPLOT HIDDEN ID 15 &
LABEL "Type écriture.:"&
        REQUIRED &
        NOCHANGE &
        DUPLICATE &
        LOOKUP ON VCBI_DSC &
          MESSAGE 412 ; Ce type d'écriture est invalide.

FIELD CBIDSC OF VCBI_DSC &
        DISPLAY

ALIGN (19,3,19) (,,32)

FIELD LCPUSR     OF CPLOT HIDDEN ID 20 &
LABEL "Usager........:"&
        DEFAULT LOGONID &
        DUPLICATE &
        LOOKUP ON GSUSAGER &
          VIA   USRCLE &
          USING LCPUSR OF CPLOT &
          MESSAGE 434 ; Cet usager est inexistant ou expiré.

FIELD T_LCPUSR_NOM &
        DISPLAY

SKIP 1

FIELD LCPDATVAL  OF CPLOT  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN ID 25 &
LABEL "Vérifié le....:"&
        DISPLAY
SKIP

FIELD LCPUSRVAL  OF CPLOT HIDDEN ID 30 &
LABEL "Vérifié par...:"&
        DISPLAY

FIELD T_LCPUSRVAL_NOM &
        DISPLAY

SKIP 1

ALIGN (19,3,19) (,,24)

FIELD LCPATRJOU  OF CPLOT HIDDEN ID 35 & 
LABEL "Autorisé......:"&
        NOENTRY &
        SIZE 3

HILITE DISPLAY OFF

FIELD D_HRSBLC &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF D_HRSBLC <> ""

USE ushilite NOLIST

ALIGN (19,3,19) (,,32)

FIELD LCPUSRATR OF CPLOT HIDDEN ID 40 &
LABEL "Autorisé par..:"&
        DISPLAY

FIELD T_LCPUSRATR_NOM &
        DISPLAY

SKIP 1

ALIGN(19,3,19) (,,32)

FIELD LCPDATJOU  OF CPLOT  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN ID 45 &
LABEL "Journalisé le.:"&
        DISPLAY

FIELD LCPHREJOU  OF CPLOT &
LABEL "Journalisé par:"&
        DISPLAY

ALIGN (19,3,19) (,,32)

FIELD LCPUSRJOU OF CPLOT HIDDEN ID 50 &
        DISPLAY

FIELD T_LCPUSRJOU_NOM &
        DISPLAY

SKIP

DRAW ,1 TO ,80

SKIP 1

TITLE &
@IF FRANCAIS
      "    Vérifié            Cumulé             Ecart" &
@ELSE
      "   Verified          Totalled           %%%cart" &
@ENDIF
      AT ,21

SKIP

ALIGN(19,3,19) (,,37) (,,55)

FIELD LCPCUMMNTVER OF CPLOT HIDDEN ID 55 &
@IF FRANCAIS
        LABEL "Montant total.:"
@ELSE
        LABEL "Total amount..:"
@ENDIF

FIELD LCPCUMMNT    OF CPLOT &
        DISPLAY

FIELD D_DIFMNTTRS &
        DISPLAY &
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

ALIGN(19,3,24) (,,42) (,,60)
FIELD LCPNBRTRSVER OF CPLOT HIDDEN ID 60 &
@IF FRANCAIS
        LABEL "Nombre total..:"
@ELSE
        LABEL "Total number..:"
@ENDIF

FIELD LCPNBRTRS    OF CPLOT &
        DISPLAY

FIELD D_DIFNBRTRS &
        DISPLAY &
        PICTURE "^,^^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 2

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
  ;
  ; Si l'écran n'est pas appellé par le menu, alors la saisie,
  ; la destruction et la modification est interdite.
  ;
  IF T_PECCLEFND <> ""  OR T_LCPCLEFND <> ""
  THEN BEGIN
    LET TZ_SECENT = "0"
    LET TZ_SECDEL = "0"
    LET TZ_SECMOD = "0"
  END
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT PECCLE
BEGIN
  ;
  ; Popup sur les périodes.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_MODCLE = "CP"
    RUN SCREEN mc109 PASSING T_CIECLEMNU, T_PECCLE, T_MODCLE MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du statut de la période.
;
;
PROCEDURE EDIT PECCLE
BEGIN
  ; Ajustement pour le changement de siècle
  IF FIELDTEXT[1:1] < "8"
  THEN LET T_FIELDTEXT_SIE = "1" + FIELDTEXT
  ELSE LET T_FIELDTEXT_SIE = "0" + FIELDTEXT

  ; Ajustement pour le changement de siècle
  IF T_PECCLECOU[1:1] < "8"
  THEN LET T_PECCLECOU_SIE = "1" + T_PECCLECOU
  ELSE LET T_PECCLECOU_SIE = "0" + T_PECCLECOU

  IF T_FIELDTEXT_SIE < T_PECCLECOU_SIE
  THEN ERROR 406 ; Cette période est fermée.

  IF FIELDTEXT <> T_PECCLECOU
  THEN WARNING 407 ; Cette période diffère de la période courante.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les types de lot(code bilingue).
;
;
PROCEDURE INPUT LCPTYPECR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LCPTYPECR = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_LCPTYPECR, T_LCPTYPECR
    LET FIELDTEXT = TRUNC(T_LCPTYPECR)
  END
END

PROCEDURE PROCESS LCPTYPECR
BEGIN
  ;
  ; La référence à utiliser est l'"employé" si on crée un lot de relevé de
  ; dépense. Dans tous les autres cas la référence est un fournisseur
  ;
  IF LCPTYPECR OF CPLOT = "RD"
  THEN LET LCPREFTYP OF CPLOT = "E"
  ELSE LET LCPREFTYP OF CPLOT = "F"
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les usagers.
;
;
PROCEDURE INPUT LCPUSR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_USRCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gs104 PASSING T_USRCLE MODE F
    LET FIELDTEXT = TRUNC(T_USRCLE)
  END
END


;------------------------------------------------------------------------------
;
;
; Seul l'usager affecté au lot peut le donner à un autre usager
;
;
PROCEDURE EDIT LCPUSR
BEGIN
  IF NOT ENTRYMODE
  THEN BEGIN
    IF FIELDTEXT <> OLDVALUE(LCPUSR OF CPLOT) AND &
       LOGONID   <> OLDVALUE(LCPUSR OF CPLOT)
    THEN ERROR 432 ; Seul l'usager du lot peut réaffecter celui-ci à un autre usager.
  END
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager du lot(Utilisateur).
;
;
PROCEDURE OUTPUT T_LCPUSR_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LCPUSR OF CPLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager de validation.
;
;
PROCEDURE OUTPUT T_LCPUSRVAL_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LCPUSRVAL OF CPLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; On transpose les valeurs O et N en valeurs 1 et 0.
;
;
PROCEDURE INPUT LCPATRJOU
BEGIN
  USE usflginp NOLIST
END

; Procédure permettant de valider que le lot est validé avant d'être autorisé.
;
PROCEDURE EDIT LCPATRJOU
BEGIN
  IF LCPDATVAL OF CPLOT = 0 AND FIELDTEXT = "1"
  THEN ERROR 581 ; Le lot doit être validé avant de l'autoriser.
END



;-------------------------------------------------------------------------------
;
;
; On conserve dans le lot le nom de l'usager qui a autorisé le lot
;
;
PROCEDURE PROCESS LCPATRJOU
BEGIN
  IF FIELDTEXT = "1"
  THEN LET LCPUSRATR OF CPLOT = LOGONID
  ELSE LET LCPUSRATR OF CPLOT = " "
  DISPLAY LCPUSRATR OF CPLOT
  DISPLAY T_LCPUSRATR_NOM
END


;-------------------------------------------------------------------------------
;
;
; On affiche les valeurs 1 et 0 sous la forme Oui et Non.
;
;
PROCEDURE OUTPUT LCPATRJOU
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager qui a autorisé le lot.
;
;
PROCEDURE OUTPUT T_LCPUSRATR_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LCPUSRATR OF CPLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager qui a journalisé le lot.
;
;
PROCEDURE OUTPUT T_LCPUSRJOU_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LCPUSRJOU OF CPLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; Validation du nombre de décimales saisie par l'usager.
;
;
PROCEDURE EDIT LCPCUMMNTVER
BEGIN
  USE usvaldec NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
; MP-00-05-05_SF02.
;
; Cette information ne peut être modifiée que par l'usager utilisateur du lot.
;
;PROCEDURE DESIGNER 10
;BEGIN
;  IF LOGONID = LCPUSR OF CPLOT
;  THEN ACCEPT LCPDSC OF CPLOT
;END

;-------------------------------------------------------------------------------
; MP-00-05-05_SF02.
;
; Cette information ne peut être modifiée que par l'usager utilisateur du lot.
;
;PROCEDURE DESIGNER 35
;BEGIN
;  IF LOGONID = LCPUSR OF CPLOT
;  THEN ACCEPT LCPATRJOU OF CPLOT
;END

;-------------------------------------------------------------------------------
;
;
; On peut modifier le montant vérifié seulement si l'usager
; est l'utilisateur du lot et qu'il n'y a aucune transaction de saisie
; sur le lot.
;
;
PROCEDURE DESIGNER 55
BEGIN
  IF     LOGONID = LCPUSR OF CPLOT &
     AND LCPNBRTRS OF CPLOT = 0
  THEN ACCEPT LCPCUMMNTVER OF CPLOT
END

;-------------------------------------------------------------------------------
;
;
; On peut modifier le nombre vérifié seulement si l'usager
; est l'utilisateur du lot et qu'il n'y a aucune transaction de saisie
; sur le lot.
;
;
PROCEDURE DESIGNER 60
BEGIN
  IF     LOGONID = LCPUSR OF CPLOT &
     AND LCPNBRTRS OF CPLOT = 0
  THEN ACCEPT LCPNBRTRSVER OF CPLOT
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran   Vérification
;
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN cp020a PASSING CPLOT
END


;-------------------------------------------------------------------------------
;
;
; Le path suivant tient compte de la période et du lot reçu en paramètres.
;
;
PROCEDURE PATH
BEGIN
  IF T_PECCLEFND = ""
  THEN BEGIN
    REQUEST PECCLE OF CPLOT
    IF PROMPTOK
    THEN REQUEST LCPCLE OF CPLOT
    IF PROMPTOK
    THEN LET PATH = 1
    IF PATH = 0
    THEN BEGIN
      REQUEST PECCLE OF CPLOT
      IF PROMPTOK
      THEN LET PATH = 2
    END
    IF PATH = 0
    THEN LET PATH = 3
  END
  ELSE LET PATH = 99
END


;-------------------------------------------------------------------------------
;
;
; Find spécial du au path 99.
;
;
PROCEDURE FIND
BEGIN
  IF PATH = 1
  THEN GET CPLOT VIA CIECLE, &
                     PECCLE, &
                     LCPCLE  &
                 USING T_CIECLEMNU, &
                       PECCLE OF CPLOT, &
                       LCPCLE OF CPLOT
  IF PATH = 2
  THEN GET CPLOT VIA CIECLE, &
                     PECCLE  &
                 USING T_CIECLEMNU, &
                       PECCLE OF CPLOT &
                 ORDERBY CIECLE, &
                         PECCLE, &
                         LCPCLE
  IF PATH = 3
  THEN GET CPLOT VIA CIECLE     &
                 USING T_CIECLEMNU &
                 ORDERBY CIECLE, &
                         PECCLE, &
                         LCPCLE
  IF PATH = 99
  THEN GET CPLOT VIA CIECLE, &
                     PECCLE, &
                     LCPCLE  &
                 USING T_CIECLEMNU, &
                       T_PECCLEFND, &
                       T_LCPCLEFND
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPLOT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CPLOT &
     AND NOT NEWRECORD     OF CPLOT &
     AND NOT DELETEDRECORD OF CPLOT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  IF LCPDATJOU OF CPLOT <> 0
  THEN ERROR 442 ; Mise à jour non autorisée. Ce lot est journalisé.

  IF DELETEDRECORD OF CPLOT AND &
     LCPNBRTRS OF CPLOT <> 0
  THEN ERROR 461 ; Vous ne pouvez pas supprimer un lot qui contient des transactions.

  IF 0 = SIZE( TRUNCATE( LCPCLE OF CPLOT ) )
  THEN BEGIN
    ;
    ; Attribution du numéro de lot selon la cie et la periode
    ; dans la table CPLCP_SEQ se trouve le dernier numero de lot
    ; utilisé.
    ;
    START TRANSACTION GEN_NUM_SEQ
    GET CPLCP_SEQ VIA   CIECLE, &
                        PECCLE  &
                  USING CIECLE OF CPLOT,&
                        PECCLE OF CPLOT &
                  OPTIONAL
    LET CIECLE    OF CPLCP_SEQ = CIECLE    OF CPLOT
    LET PECCLE    OF CPLCP_SEQ = PECCLE    OF CPLOT
    LET LCSNUMSEQ OF CPLCP_SEQ = LCSNUMSEQ OF CPLCP_SEQ + 1

    LET LCPCLE OF CPLOT = ASCII(LCSNUMSEQ OF CPLCP_SEQ,LCSLONNUM OF CPLCP_SEQ)
    PUT CPLCP_SEQ
    COMMIT TRANSACTION GEN_NUM_SEQ
  END
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le numéro de lot nouvellement créé et que l'écran ne s'efface
; pas.
;
;
PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY LCPCLE OF CPLOT
    WARNING = " "
  END
END


BUILD
;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;******************************* Gestion des lots *******************************
;* Période ......: xxxxx                Numéro de lot.: xxxx                    *
;*                                                                              *
;* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               *
;*                                                                              *
;* Type écriture.: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
;* Usager........: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;* Vérifié le....: xxxxxxxx                                                     *
;* Vérifié par...: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;* Autorisé......: xxx  xxxxxxxxxxxxxxxxxxxx                                    *
;* Autorisé par..: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;* Journalisé le.: xxxxxxxx xxxxx                                               *
;* Journalisé par: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;********************************************************************************
;*                       Vérifié            Cumulé             Ecart            *
;* Montant total.: xxxxxxxxxxxxxx    xxxxxxxxxxxxxx    xxxxxxxxxxxxxx           *
;* Nombre total..:      xxxxx             xxxxx             xxxxxx              *
;********************************************************************************

