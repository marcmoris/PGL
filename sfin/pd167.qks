;/T/ Machine opération de surface (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-18
;
;    Description..: Cette table contient les informations de base des
;                   machines servant à éffectuer les opérations de surface.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd167 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD167"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd167.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;
TEMPORARY T_MOPCODMAC CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Machine opération de surface
;

FILE PDMACH_OP_SURF  PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 MOPCODMAC &
         USING   T_CIECLEMNU,                 &
                 MOPCODMAC OF PDMACH_OP_SURF  &
         REQUEST MOPCODMAC OF PDMACH_OP_SURF  &
         ORDERBY CIECLE,   &
                 MOPCODMAC

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 MOPCODMAC

         ITEM CIECLE OF PDMACH_OP_SURF INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Machine opération de surface " &
@ELSE
      " %%%Machine opération de surface " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD MOPCODMAC OF PDMACH_OP_SURF               &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE                       &
        LOOKUP NOTON PDMACH_OP_SURF             &
          VIA   CIECLE,                         &
                MOPCODMAC                       &
          USING T_CIECLEMNU,                    &
                MOPCODMAC OF PDMACH_OP_SURF     &
          MESSAGE 2914 ; Ce code de machine existe déjà.


SKIP 1


FIELD MOPDSC001 OF PDMACH_OP_SURF          &
        HIDDEN ID 10                       &
        REQUIRED


FIELD MOPDSC002 OF PDMACH_OP_SURF          &
        ID SAME                            &
        NOLABEL


FIELD MOPDSC003 OF PDMACH_OP_SURF          &
        ID SAME                            &
        NOLABEL


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs machine opération de surface
;

PROCEDURE INPUT MOPCODMAC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MOPCODMAC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd112 MODE F &
                     PASSING T_MOPCODMAC, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_MOPCODMAC )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDMACH_OP_SURF  &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDMACH_OP_SURF  &
     AND NOT NEWRECORD     OF PDMACH_OP_SURF  &
     AND NOT DELETEDRECORD OF PDMACH_OP_SURF  &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Machine de finition ******************************
*                                                                              *
* Code machine..: xxx                                                          *
*                                                                              *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
