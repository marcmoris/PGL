;/T/ Transfert des cumulatifs de carrières
;
;/M/ François Déry, 21 mai 1999
;       Conversion Unix/interbase. Enlever le request des ajustements
;       (Sert à rien) et faire un seul QTP.
;
; Modifié par.: Guy Chabot le 25 août 1994
;
; Description.: Rendre cette procédure par période comptable en remplacement
;               du mois.
;
USE ussetqts NOLIST

RUN if700t

GLOBAL TEMPORARY G_CIECLE CHARACTER SIZE 4 PARM ; Compagnie

REQUEST SELECTION_PERIODE

ACCESS MCPERIODE_CTB

CHOOSE PECCLE PARM

TEMPORARY T_FLGFIN CHARACTER SIZE 1

  ITEM T_FLGFIN = "0"

SUBFILE WIF700 KEEP &
        INCLUDE G_CIECLE, &
                PECCLE    OF MCPERIODE_CTB, &
                PECDATDEB OF MCPERIODE_CTB, &
                PECDATFIN OF MCPERIODE_CTB, &
                T_FLGFIN

REQUEST AJOUT_CARRIERE_MOIS

ACCESS IFBLOC &
 LINK TO RECORD 1 OF *WIF700 &
 LINK CIECLE OF IFBLOC, &
      "ANCO" &
   TO CIECLE, &
      ANNCODE                   OF IFANNEE_COURANTE

CHOOSE CIECLE ( G_CIECLE )

SELECT IF BLCDAT OF IFBLOC >= PECDATDEB OF WIF700 AND &
          BLCDAT OF IFBLOC <= PECDATFIN OF WIF700

SORT ON CRRCLENUM OF IFBLOC

OUTPUT IFCARRIERE_CUMUL ADD AT CRRCLENUM
 ITEM CIECLE    OF IFCARRIERE_CUMUL INIT CIECLE OF IFBLOC
 ITEM CRRCLENUM OF IFCARRIERE_CUMUL INIT CRRCLENUM OF IFBLOC
 ITEM CRRCLEANN OF IFCARRIERE_CUMUL INIT ANNNO OF IFANNEE_COURANTE
 ITEM PECCLE    OF IFCARRIERE_CUMUL INIT PECCLE OF WIF700

 ITEM CACQTEPROD     OF IFCARRIERE_CUMUL SUB BLCM3TOT OF IFBLOC RESET AT CRRCLENUM
 ITEM CACNBBLOCPROD  OF IFCARRIERE_CUMUL COUNT                  RESET AT CRRCLENUM

REQUEST CUMUL_FACTURE_MOIS

ACCESS *WIF700 &
 LINK G_CIECLE OF WIF700, &
      PECCLE OF WIF700  &
   TO CIECLE, &
      PECCLE                    OF IFFACTURE &
 LINK G_CIECLE OF WIF700, &
      "ANCO" &
   TO CIECLE, &
      ANNCODE                   OF IFANNEE_COURANTE &
 LINK G_CIECLE OF WIF700, &
      FCECLE OF IFFACTURE &
   TO CIECLE, &
      FCECLE                    OF IFFACTURE_DETAIL

SORT ON CRRCLENUM OF IFFACTURE_DETAIL

DEF  TMP-VENTE-FOB INTEGER SIZE 4 = FLOOR(FDEM3PROD * FDEPRIXFOB / 100 + .5 )
DEF  TMP-VENTE-FAS INTEGER SIZE 4 = FLOOR(FDEM3PROD * FDEPRIXFAS / 100 + .5 )

OUTPUT IFCARRIERE_CUMUL ADD UPDATE AT CRRCLENUM &
        VIA   CIECLE, CRRCLENUM, CRRCLEANN, PECCLE &
        USING G_CIECLE OF WIF700, CRRCLENUM OF IFFACTURE_DETAIL, &
              ANNNO OF IFANNEE_COURANTE, PECCLE OF WIF700
 ITEM CIECLE      OF IFCARRIERE_CUMUL INIT CIECLE OF IFFACTURE
 ITEM CRRCLENUM   OF IFCARRIERE_CUMUL INIT CRRCLENUM OF IFFACTURE_DETAIL
 ITEM CRRCLEANN   OF IFCARRIERE_CUMUL INIT ANNNO OF IFANNEE_COURANTE
 ITEM PECCLE      OF IFCARRIERE_CUMUL INIT PECCLE OF WIF700

 ITEM CACQTEFAC     OF IFCARRIERE_CUMUL SUB FDEM3PROD RESET AT CRRCLENUM
 ITEM CACVENTEFOBCN OF IFCARRIERE_CUMUL SUB TMP-VENTE-FOB &
                       IF DEVCLE OF IFFACTURE_DETAIL = "CAN"  RESET AT CRRCLENUM
 ITEM CACVENTEFASCN OF IFCARRIERE_CUMUL SUB TMP-VENTE-FAS &
                       IF DEVCLE OF IFFACTURE_DETAIL = "CAN"  RESET AT CRRCLENUM
 ITEM CACVENTEFOBUS OF IFCARRIERE_CUMUL SUB TMP-VENTE-FOB &
                       IF DEVCLE OF IFFACTURE_DETAIL = "USA"  RESET AT CRRCLENUM
 ITEM CACVENTEFASUS OF IFCARRIERE_CUMUL SUB TMP-VENTE-FAS &
                       IF DEVCLE OF IFFACTURE_DETAIL = "USA"  RESET AT CRRCLENUM

REQUEST CUMUL_CREDIT_DETAIL

ACCESS *WIF700 &
  LINK G_CIECLE OF WIF700, &
       PECCLE   OF WIF700 &
    TO CIECLE, &
       PECCLE                   OF IFNOT_CRE_MAITRE &
  LINK G_CIECLE OF WIF700, &
       "ANCO" &
    TO CIECLE, &
       ANNCODE                  OF IFANNEE_COURANTE &
  LINK G_CIECLE OF WIF700, &
       CRECLE OF IFNOT_CRE_MAITRE &
    TO CIECLE, &
       CRECLE                   OF IFNOTE_CREDIT &
  LINK G_CIECLE OF WIF700, &
       FCECLE OF IFNOT_CRE_MAITRE &
    TO CIECLE, &
       FCECLE                   OF IFFACTURE &
  LINK G_CIECLE OF WIF700, &
       CLTCLE OF IFFACTURE &
    TO CIECLE, &
       CLTCLE                   OF IFCLIENT &
  LINK CLICIECLE OF IFCLIENT, &
       CLICLE OF IFCLIENT &
    TO CLICIECLE, &
       CLICLE                   OF CRCLIENT &
  LINK G_CIECLE OF WIF700, &
       FCECLE OF IFNOT_CRE_MAITRE, &
       CRRCLENUM OF IFNOTE_CREDIT, &
       CRRCLEANN OF IFNOTE_CREDIT, &
       BLCCLE OF IFNOTE_CREDIT &
    TO CIECLE, &
       FCECLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       BLCCLE                   OF IFFACTURE_DETAIL

DEF  TMP-M3-AJUS INTEGER SIZE 4 = FDEM3PROD - CREM3AJUS
DEF  TMP-RECL-FOB INTEGER SIZE 4 = FLOOR((FDEM3PROD - CREM3AJUS) * CREPRIXFOB / 100 + .5 ) &
                                + NCMFACTOT
DEF  TMP-RECL-FAS INTEGER SIZE 4 = FLOOR((FDEM3PROD - CREM3AJUS) * CREPRIXFAS / 100 + .5 )

DEF  TMP-CAR-NO ZONED SIZE 2 = CRRCLENUM OF IFNOT_CRE_MAITRE &
                                        IF NCMFACTOT <> 0 &
                          ELSE CRRCLENUM OF IFNOTE_CREDIT
DEF TMP-TRI INT SIZE 2 = CRRCLENUM OF IFNOT_CRE_MAITRE &
                                        IF NCMFACTOT <> 0 &
                          ELSE CRRCLENUM OF IFNOTE_CREDIT

DEF  TMP-DEV CHARACTER SIZE 3 = DEVCLE OF CRCLIENT IF NCMFACTOT <> 0 &
                           ELSE DEVCLE OF IFNOTE_CREDIT

TEMP T-REC-FOB-CN INT SIZE 4
TEMP T-REC-FOB-US INT SIZE 4
TEMP T-REC-FAS-CN INT SIZE 4
TEMP T-REC-FAS-US INT SIZE 4


SORT ON TMP-TRI

ITEM T-REC-FOB-CN SUB TMP-RECL-FOB IF TMP-DEV = "CAN"  RESET AT TMP-TRI
ITEM T-REC-FOB-US SUB TMP-RECL-FOB IF TMP-DEV = "USA"  RESET AT TMP-TRI
ITEM T-REC-FAS-CN SUB TMP-RECL-FAS IF TMP-DEV = "CAN"  RESET AT TMP-TRI
ITEM T-REC-FAS-US SUB TMP-RECL-FAS IF TMP-DEV = "USA"  RESET AT TMP-TRI


OUTPUT IFCARRIERE_CUMUL ADD UPDATE AT TMP-TRI &
        VIA   CIECLE, CRRCLENUM, CRRCLEANN, PECCLE &
        USING G_CIECLE OF WIF700, TMP-CAR-NO, &
              ANNNO OF IFANNEE_COURANTE, PECCLE OF WIF700
  ITEM CIECLE          OF IFCARRIERE_CUMUL INIT CIECLE OF IFNOT_CRE_MAITRE
  ITEM CRRCLENUM       OF IFCARRIERE_CUMUL INIT TMP-CAR-NO
  ITEM CRRCLEANN       OF IFCARRIERE_CUMUL INIT ANNNO OF IFANNEE_COURANTE
  ITEM PECCLE          OF IFCARRIERE_CUMUL INIT PECCLE OF WIF700

 ITEM CACNBBLRECL  OF IFCARRIERE_CUMUL SUB CREQTEBLAJUS RESET AT TMP-TRI
 ITEM CACQTERECL    OF IFCARRIERE_CUMUL SUB TMP-M3-AJUS RESET AT TMP-TRI
 ITEM CACRECLFOBCN OF IFCARRIERE_CUMUL = CACRECLFOBCN + T-REC-FOB-CN
 ITEM CACRECLFASCN OF IFCARRIERE_CUMUL = CACRECLFASCN + T-REC-FAS-CN
 ITEM CACRECLFOBUS OF IFCARRIERE_CUMUL = CACRECLFOBUS + T-REC-FOB-US
 ITEM CACRECLFASUS OF IFCARRIERE_CUMUL = CACRECLFASUS + T-REC-FAS-US

REQUEST CUMUL_CREDIT_SIMPLE

ACCESS *WIF700 &
  LINK G_CIECLE OF WIF700, &
       PECCLE OF WIF700 &
    TO CIECLE, &
       PECCLE                   OF IFNOT_CRE_MAITRE &
  LINK G_CIECLE OF WIF700, &
       "ANCO" &
    TO CIECLE, &
       ANNCODE                  OF IFANNEE_COURANTE &
  LINK G_CIECLE, &
       FCECLE OF IFNOT_CRE_MAITRE &
    TO CIECLE, &
       FCECLE                   OF IFFACTURE &
  LINK G_CIECLE OF WIF700, &
       CLTCLE OF IFFACTURE &
    TO CIECLE, &
       CLTCLE                   OF IFCLIENT &
  LINK CLICIECLE OF IFCLIENT, &
       CLICLE OF IFCLIENT &
    TO CLICIECLE, &
       CLICLE                   OF CRCLIENT

SELECT IF NCMFACTOT <> 0

TEMP T-FOB-CN INTEGER SIZE 4
TEMP T-FOB-US INTEGER SIZE 4

SORT ON CRRCLENUM OF IFFACTURE

ITEM T-FOB-CN SUB NCMFACTOT IF DEVCLE OF CRCLIENT = "CAN" RESET AT CRRCLENUM
ITEM T-FOB-US SUB NCMFACTOT IF DEVCLE OF CRCLIENT = "USA" RESET AT CRRCLENUM

OUTPUT IFCARRIERE_CUMUL ADD UPDATE AT CRRCLENUM &
        VIA   CIECLE, CRRCLENUM, CRRCLEANN, PECCLE &
        USING G_CIECLE OF WIF700, CRRCLENUM OF IFFACTURE, &
              ANNNO OF IFANNEE_COURANTE, PECCLE OF WIF700
  ITEM CIECLE          OF IFCARRIERE_CUMUL INIT CIECLE OF IFNOT_CRE_MAITRE
  ITEM CRRCLENUM       OF IFCARRIERE_CUMUL INIT CRRCLENUM OF IFFACTURE
  ITEM CRRCLEANN       OF IFCARRIERE_CUMUL INIT ANNNO OF IFANNEE_COURANTE
  ITEM PECCLE          OF IFCARRIERE_CUMUL INIT PECCLE OF WIF700

 ITEM CACAJUSTCN OF IFCARRIERE_CUMUL FINAL CACAJUSTCN OF IFCARRIERE_CUMUL - &
      T-FOB-CN
 ITEM CACAJUSTUS OF IFCARRIERE_CUMUL FINAL CACAJUSTUS OF IFCARRIERE_CUMUL - &
      T-FOB-US
;
; Mise à jour indicateur de fin OK
;
REQUEST MAJ_INDICATEUR

ACCESS *WIF700

OUTPUT *WIF700 UPDATE
  ITEM T_FLGFIN OF WIF700 FINAL "1"

BUILD
