;/T/ Element Facturables - Pour la passerelle
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 11 Nov 1993
;
;    Description..: Écran de saisie des éléments facturable par compagnie
;
;/M/ Marie-Josée Hamel, 96.02.13, ajouter produit et element de tarif
;

;/V/ Écran vérifier pour le passage à l'an 2000

;--------------------------------------------------------------------------
SCREEN cr156 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM, &
                       T_PECCLEMNU

TEMPORARY T_CPTPECDEBI_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CPTPECDEBA_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_PECCLEMNU_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4


USE ussectmp  NOLIST
    "CR156"

USE cr156.hlp NOLIST

USE usactecr  NOLIST


FILE CRELE_FACT PRIMARY OCCURS 17 TIMES
  ACCESS VIA CIECLE, ELECLE USING T_CIECLEMNU, ELECLE OF CRELE_FACT REQUEST ELECLE &
    ORDERBY ELECLE
  ACCESS VIA CIECLE USING T_CIECLEMNU &
    ORDERBY ELECLE


ITEM CIECLE OF CRELE_FACT INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Notion de charte de compte et pour avoir la devise du G/L.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"


;-------------------------------------------------------------------------------
;
; Validation du compte comptable.
;
FILE VCPT_DSC         REFERENCE &
                      OCCURS WITH CRELE_FACT

  ACCESS  VIA   CPTCLE &
          USING CPTCLE OF CRELE_FACT


;-------------------------------
; Fichier des produits / trajets
;
FILE FATRAJET_PRODUIT REFERENCE &
                      OCCURS WITH CRELE_FACT

  ACCESS  VIA   CIECLE,    &
                TRPCLE     &
          USING T_CIECLEMNU           ,  &
                TRPCLE OF CRELE_FACT


;--------------------------------------
; Fichier des éléments de tarifications
;
FILE FAELEMENT_TARIF  REFERENCE &
                      OCCURS WITH CRELE_FACT

  ACCESS  VIA   CIECLE,    &
                ETACLE     &
          USING T_CIECLEMNU           ,  &
                ETACLE OF CRELE_FACT


;------------------------------------
; Fichier des grilles de tarification
;
FILE FAGRILLE_TARIF   DESIGNER OPEN READ


;-------------------------------------------------------------------------------
;

TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes et unité adm.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes

TEMPORARY T_TRPCLE    CHARACTER SIZE 3 ; Popup sur les produits
TEMPORARY T_SEGMUL    CHARACTER SIZE 3 ; Popup sur les produits
TEMPORARY T_TRPTYPPAT CHARACTER SIZE 5 ; Popup sur les produits

TEMPORARY T_ETACLE    CHARACTER SIZE 4 ; Popup sur les elements de tarification


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction


DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Eléments facturables - conversion passerelle " &
@ENDIF
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

TITLE &
@IF FRANCAIS
 "Code Description                         Compte                 Produit Élém." &
@ELSE
 "Code  %%%%%%%%%% "&
@ENDIF
      AT ,3

SKIP

ALIGN(3,2,3)(,,8)(,,44)(,,51)(,,69)(,,75)

CLUSTER OCCUR WITH CRELE_FACT ID BASE 10

FIELD ELECLE OF CRELE_FACT HIDDEN LABEL " " &
  REQUIRED &
  NOCHANGE &
  LOOKUP NOTON CRELE_FACT &
   VIA   CIECLE, &
         ELECLE &
   USING T_CIECLEMNU, &
         ELECLE OF CRELE_FACT &
   MESSAGE 2403 ; Cette element fact exist déjà
FIELD ELEDSC OF CRELE_FACT

FIELD CPTCLE OF CRELE_FACT &
        LOOKUP ON VCPT_DSC  &
          MESSAGE 665 ; Ce numéro de compte n'existe pas.
FIELD CPTDSCABR OF VCPT_DSC &
        DISPLAY SIZE 16

FIELD TRPCLE OF CRELE_FACT

FIELD ETACLE OF CRELE_FACT

CLUSTER



;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les comptes.
;
;
PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = T_PECCLEMNU
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation si le compte est actif.
;
;
PROCEDURE EDIT CPTCLE
BEGIN
  ;
  ; Note: La période de fin, est la première période d'inactivité.
  ;

  ; Ajustement pour le changement de siècle
  IF T_PECCLEMNU[1:1] < "8"
  THEN LET T_PECCLEMNU_SIE = "1" + T_PECCLEMNU
  ELSE LET T_PECCLEMNU_SIE = "0" + T_PECCLEMNU

  ; Ajustement pour le changement de siècle
  IF CPTPECDEBA OF VCPT_DSC[1:1] < "8"
  THEN LET T_CPTPECDEBA_SIE = "1" + CPTPECDEBA OF VCPT_DSC
  ELSE LET T_CPTPECDEBA_SIE = "0" + CPTPECDEBA OF VCPT_DSC

  ; Ajustement pour le changement de siècle
  IF CPTPECDEBI OF VCPT_DSC[1:1] < "8"
  THEN LET T_CPTPECDEBI_SIE = "1" + CPTPECDEBI OF VCPT_DSC
  ELSE LET T_CPTPECDEBI_SIE = "0" + CPTPECDEBI OF VCPT_DSC

  IF T_PECCLEMNU_SIE >= T_CPTPECDEBA_SIE AND &
     ( &
       T_PECCLEMNU_SIE < T_CPTPECDEBI_SIE OR &
       CPTPECDEBI OF VCPT_DSC = "" &
     )
  THEN NULL
  ELSE ERROR 667 ; Ce compte est inactif.

  IF CPTTYP OF VCPT_DSC <> "R"
  THEN ERROR 668 ; On ne peut pas utliser ce compte dans une imputation.

END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les trajets / produits.
;
;
PROCEDURE INPUT TRPCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TRPCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_SEGMUL    = "0|1"
    LET T_TRPTYPPAT = "T|P"
    RUN SCREEN fa102 MODE F PASSING T_CIECLEMNU,T_TRPCLE,T_SEGMUL,T_TRPTYPPAT &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_TRPCLE)
  END

  IF 0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = OLDVALUE(TRPCLE OF CRELE_FACT)

  IF FIELDTEXT <> " "
  THEN BEGIN
       GET FATRAJET_PRODUIT &
           VIA   CIECLE,    &
                 TRPCLE     &
           USING T_CIECLEMNU, &
                 FIELDTEXT OPTIONAL
       IF NOT ACCESSOK
       THEN ERROR 2722
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour l'élement de tarification
;
;
PROCEDURE INPUT ETACLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TRPCLE = TRPCLE OF CRELE_FACT
    LET T_ETACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN fa101 MODE F PASSING T_CIECLEMNU, T_TRPCLE, T_ETACLE &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_ETACLE)
  END

  IF 0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = OLDVALUE(ETACLE OF CRELE_FACT)

  IF FIELDTEXT = " " AND TRPCLE OF CRELE_FACT <> " "
  THEN ERROR 2734

  IF FIELDTEXT <> " " AND TRPCLE OF CRELE_FACT = " "
  THEN BEGIN
       LET FIELDTEXT = " "
       WARNING 2733
  END

  IF FIELDTEXT <> " "
  THEN BEGIN
       GET FAELEMENT_TARIF  &
           VIA   CIECLE,    &
                 ETACLE     &
           USING T_CIECLEMNU, &
                 FIELDTEXT OPTIONAL
       IF NOT ACCESSOK
       THEN ERROR 2719

       GET FAGRILLE_TARIF   &
           VIA   CIECLE,    &
                 TRPCLE,    &
                 ETACLE     &
           USING T_CIECLEMNU,           &
                 TRPCLE OF CRELE_FACT , &
                 FIELDTEXT OPTIONAL
       IF NOT ACCESSOK
       THEN ERROR 2729
  END
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR CRELE_FACT
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF CRELE_FACT &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF CRELE_FACT &
       AND NOT NEWRECORD     OF CRELE_FACT &
       AND NOT DELETEDRECORD OF CRELE_FACT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'une catégorie.
    ;
    IF DELETEDRECORD OF CRELE_FACT
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la catégorie est référée.
      ;
      RUN SCREEN cr156x MODE SAME &
                        PASSING CRELE_FACT, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Eléments facturables *****************************
*                                                                              *
* Code Description                         Compte                 Produit Élém.*
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxx  xxx   xxxx *
********************************************************************************
@ENDIF
