;/T/ Consultation des réceptions        
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 7 Septembre 1994
;
;    Description..: Permet de visionner et de choisir une réception
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar117 NOMODE NOACTION FIELDMARK &
             FROM 11,14 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_FOUCLE, &
                        T_RECCLE 
 
                    

USE ustrasse NOLIST

USE ar117.hlp NOLIST

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Compagnie de la réception
TEMPORARY T_RECCLE    CHARACTER SIZE 12; Code de réception
TEMPORARY T_FOUCLE    CHARACTER SIZE 6 ; Numéro de fournisseur

;-------------------------------------------------------------------------------
;
FILE ARRECEPTION        PRIMARY &
                      OCCURS 10

 ACCESS   VIA     CIECLE,    &
                  RECCLE     &
          USING   T_CIECLE,  &
                  T_RECCLE   &
          ORDERBY CIECLE , &
                  RECCLE   
  
;-------------------------------------------------------------------------------
;
;
;
FILE MCREF_ADRESSE     REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
         USING  FOUCIECLE OF ARRECEPTION, &
                "F"                     , &
                FOUCLE    OF ARRECEPTION, &
                RADCLE    OF ARRECEPTION

;-------------------------------------------------------------------------------
;
; Permet d'avoir l'information du fournisseur pour la cie de transaction.
;
FILE VFOC_FOU            REFERENCE

  ACCESS VIA   FOUCIECLE, &
               FOUCLE   , &
               FOCCIECLE  &
         USING FOUCIECLE OF ARRECEPTION, &
               FOUCLE    OF ARRECEPTION, &
               T_CIECLE


;-------------------------------------------------------------------------------
;
; Pour la présentation de la devise ou non.
;
FILE MCHOLDING           REFERENCE &
                        NOITEM
  ACCESS VIA   CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;

DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING &
          ELSE "/" + DEVCLE OF VFOC_FOU


USE ushilite NOLIST     
DRAW 01,01 TO 13,67

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE "    Réception           Fournisseur             Montant net  " AT ,02

SKIP

ALIGN(3,3,6)(,,19)(,,26)(,,47)(,,62)

CLUSTER OCCURS WITH ARRECEPTION

FIELD RECCLE    OF ARRECEPTION HIDDEN LABEL ">>"
FIELD FOUCLE    OF ARRECEPTION
FIELD RADNOM    OF MCREF_ADRESSE SIZE 20
FIELD RECMNTNET OF ARRECEPTION
FIELD D_DEVCLE

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_RECCLE, "@")
  THEN LET T_RECCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_FOUCLE = FOUCLE OF ARRECEPTION
  LET T_RECCLE = RECCLE OF ARRECEPTION
  RETURN
END

BUILD
@IF FORMAT
             *******************************************************************
             *    Réception           Fournisseur             Montant net      *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             *******************************************************************
@ENDIF
