;/T/ Consultation des diagrammes (Dépisteur)
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1994-10-27
;
;    Description..: Permet de visionner et de choisir un diagramme
;
;    Paramètres...: T_DIANUM002    ---> Numéro du diagramme
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd126 NOMODE NOACTION FIELDMARK    &
             FROM 11,10 TO 23,80          &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND              &
             RECEIVING T_DIANUM002,       &
             T_CIECLEMNU


TEMPORARY T_DIANUM002  CHARACTER SIZE 07
TEMPORARY T_CIECLEMNU  CHARACTER SIZE 04


USE pd126.hlp NOLIST

USE ustrasse NOLIST



;-------------------------------------------------------------------------------
;
; Priorité diagramme
;

FILE PDDIAGRAMME      PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,                &
                 DIANUM002              &
         USING   T_CIECLEMNU,           &
                 T_DIANUM002            &
         ORDERBY CIECLE,                &
                 DIANUM002
;
; Quantité calculé
;
FILE VDIAQTE          REFERENCE OCCURS WITH PDDIAGRAMME
  ACCESS VIA   PJTABR, &
               DIANUMDIA001, &
               DIANUMDIA002, &
               CIECLE &
         USING PJTABR OF PDDIAGRAMME, &
               DIANUMDIA001 OF PDDIAGRAMME, &
               DIANUMDIA002 OF PDDIAGRAMME, &
               CIECLE OF PDDIAGRAMME

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,69       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    Diagr.  Gra Fi Long Haut Épai S *-------- QUANTITÉ ---------* " &
@ELSE
      "%%% Diagr.  Gra Fi Long Haut Épai S *-------- QUANTITÉ ---------* " &
@ENDIF
    AT ,2

SKIP TO LINE 3

TITLE &
@IF FRANCAIS
      "                                      Cmd P dbt P fin   Dbt   Fin  " &
@ELSE
      "%%%                                   Cmd P dbt P fin   Dbt   Fin  " &
@ENDIF
    AT ,2

SKIP TO LINE 4


ALIGN (3,3,6) (,,14) (,,18) (,,21) (,,26) (,,31) (,,36) (,,38) (,,44) (,,50) (,,56) (,,62)


CLUSTER OCCURS WITH PDDIAGRAMME


FIELD DIANUM002        OF PDDIAGRAMME        &
        HIDDEN LABEL ">>"                    &
        DISPLAY


FIELD TGRCODGRA        OF PDDIAGRAMME        &
        DISPLAY


FIELD TYFCODFIN        OF PDDIAGRAMME        &
        DISPLAY


FIELD DIALON           OF PDDIAGRAMME        &
        DISPLAY


FIELD DIAHAU           OF PDDIAGRAMME        &
        DISPLAY


FIELD DIAEPA           OF PDDIAGRAMME        &
        DISPLAY


FIELD DIASTU           OF PDDIAGRAMME        &
        DISPLAY


FIELD DIAQTECMD        OF PDDIAGRAMME        &
        DISPLAY


FIELD DIAQTEPLADBT     OF VDIAQTE            &
        DISPLAY                              &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEPLAFIN     OF VDIAQTE            &
        DISPLAY                              &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEDBT        OF VDIAQTE            &
        DISPLAY                              &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEFIN        OF VDIAQTE            &
        DISPLAY                              &
        PICTURE "^^^^^ " TRAILING "-"


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN

  IF 0 <> INDEX(T_DIANUM002, "@")
  THEN LET T_DIANUM002 = ""

END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN

  LET T_DIANUM002 = DIANUM002 OF PDDIAGRAMME
  RETURN

END

BUILD

@IF FORMAT
         *********************************************************************
         *    Diagr.  Gra Fi Long Haut Épai S *-------- QUANTITÉ ---------*  *
         *                                      Cmd P dbt P fin   Dbt   Fin  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         * >> xxxxxxx xxx xx xxxx xxxx xxxx x xxxxx xxxxx xxxxx xxxxx xxxxx  *
         *********************************************************************
@ENDIF
