;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de projet - Informations complémentaires.
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp001a ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FOR 9 LINES &
              ON LINE 15  &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPROJETS

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "GP001A"            ; Nom de l'écran

USE gp001a.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier d'unité de travail
;------------------------------------
FILE GPPROJETS              MASTER


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPPRO_DESC             PRIMARY  &
                            OCCURS 5 &
                            NOITEMS

;  ACCESS  VIA     CIECLE, &
;                  PRJCLE, &
;                  PDECLE  &
;          USING   CIECLE OF GPPROJETS,    &
;                  PRJCLE OF GPPROJETS,    &
;                  PDECLE OF GPPRO_DESC    &
;          REQUEST PDECLE &
;          ORDERBY PDECLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE  &
          USING   CIECLE OF GPPROJETS, &
                  PRJCLE OF GPPROJETS  &
          ORDERBY PDECLE

  ITEM CIECLE    OF GPPRO_DESC      INITIAL T_CIECLEMNU
  ITEM PRJCLE    OF GPPRO_DESC      INITIAL PRJCLE     OF GPPROJETS



;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


SKIP
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO  9,1
DRAW FROM  3,80 TO  9,80
DRAW FROM  9,1  TO  9,80

SKIP 1

TITLE &
@IF FRANCAIS
      " Informations complémentaires " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP


TITLE &
@IF FRANCAIS
" Séq. Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

SKIP

ALIGN (3,2,3) (,,9)

CLUSTER OCCUR WITH GPPRO_DESC      ID BASE 10

FIELD PDECLE OF GPPRO_DESC                            &
      LABEL " " HIDDEN &
      REQUIRED

FIELD PDEDSC OF GPPRO_DESC

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPPRO_DESC          &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPPRO_DESC          &
     AND NOT NEWRECORD     OF GPPRO_DESC          &
     AND NOT DELETEDRECORD OF GPPRO_DESC          &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************* Informations complémentaires *************************
*                                                                              *
* Séq. Description                                                             *
*xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
********************************************************************************
@ENDIF

