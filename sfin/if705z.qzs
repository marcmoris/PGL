;/T/ Liste des factures du lot de transfert
;
;/P/ Programmeur...: Martin bruyère
;    Date Création.: 1993-03-15
;
;    Description...: Cette liste imprime les factures
;
;    Paramètres....: A venir
;
;    Particularités: À venir
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *W-IF705-FACTURE &
  LINK FTRNUMFAC        OF W-IF705-FACTURE &
    TO FTRNUMFAC        OF PDFACTURE       &
  LINK FTRNUMFAC        OF W-IF705-FACTURE &
    TO FTRNUMFAC        OF PDLOT_FACTURE   &
  LINK CLICIECLE        OF PDFACTURE,      &
       CLICLE           OF PDFACTURE,      &
       CIECLE           OF PDFACTURE       &
    TO CLICIECLE,                          &
       CLICLE,                             &
       CIECLE           OF VCLC_CLI


DEFINE D_NBRTRX INTEGER SIZE 1 = 1


SORT ON LTRNUMLOT OF PDLOT_FACTURE ON FTRNUMFAC OF W-IF705-FACTURE


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
             "Création du lot de transfert"
@ELSE
             "%%%Création du lot de transfert"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF705"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

USE ustitstd NOLIST
SKIP 2


HEADING AT LTRNUMLOT OF PDLOT_FACTURE         &
  TAB 004 "Num. lot......:"                   &
  TAB 021 LTRNUMLOT OF PDLOT_FACTURE          &
  SKIP 3                                      &
  TAB 001 "   Facture    T  Client  Nom                                              Montant"

REPORT &
  TAB 004 FTRNUMFAC        OF W-IF705-FACTURE &
  TAB 015 FTRTYPFAC        OF PDFACTURE &
  TAB 018 CLICLE           OF PDFACTURE &
  TAB 026 CLINOM           OF VCLC_CLI  &
  TAB 068 FTRMNT           OF PDFACTURE SUBTOTAL


FOOTING AT LTRNUMLOT OF PDLOT_FACTURE        &
  SKIP 2                                     &
  TAB 004 "Nbr transaction pour le lot...: " &
  TAB 036 D_NBRTRX SUBTOTAL RESET AT LTRNUMLOT OF PDLOT_FACTURE &
  SKIP                                       &
  TAB 004 "Montant total pour le lot ....: " &
  TAB 036 FTRMNT SUBTOTAL RESET AT LTRNUMLOT OF PDLOT_FACTURE


FINAL FOOTING                                &
  SKIP 2                                     &
  TAB 004 "Nombre de transaction total ..: " &
  TAB 036 D_NBRTRX SUBTOTAL                  &
  SKIP                                       &
  TAB 004 "Montant total.................: " &
  TAB 036 FTRMNT SUBTOTAL

BUILD if705za


@IF FORMAT
12345678901234567890123456789012345678901234567890123456789012345678901234567890
IF705                                         Granicor Inc.                                                               PAGE:    1
95/03/15 20:50 MARBRU                        Liste de vérification de lot de transfert


   Num. lot......:    999999999

   Facture    T  Client  Nom                                              Montant
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
   999999999  x  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-

   Nbr transaction pour le lot...: 9999
   Montant total pour le lot ....: 999,999,999.99-

   Nombre de transaction total ..: 9999
   Montant total.................: 999,999,999.99-

@ENDIF
