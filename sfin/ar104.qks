;/T/ Consultation produit fournisseur  CIECLE, PRDCLE, CDEPRIUNI
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 15 mars 1994
;
;    Description..: Ecran de consultation des produits chez les fournisseurs.
;
;    Paramètres...: CIECLE    ==> - compagnie du produit.
;                                 - segment de la clé de recherche.
;
;                   PRDCLE    ==> - code de produit.   
;                                 - segment de la clé de recherche.
;                      
;                   CDEPRIUNI ==> - dernier coûtant du produit.
;                                 - peut contenir le caractère '@'.
;                                 - contient la valeur de retour.
;
;                          
;    Modifié par..: Guy Chabot 28 septembre 1994                      
;            but..: Rendre le prix unitaire à 4 décimales.              
;                          
;        
;    
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar104 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLE, &
                       T_PRDCLE, &
                       T_CDEPRIUNI

TEMPORARY T_CIECLE       CHARACTER SIZE 4  ; Compagnie
TEMPORARY T_PRDCLE       CHARACTER SIZE 12 ; Code de produit
TEMPORARY T_CDEPRIUNI    CHARACTER SIZE 14 ; Dernier coûtant 

USE ar104.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE ARPRD_FOU         PRIMARY &
                      OCCURS 11
  ACCESS VIA   CIECLE,   &
               PRDCLE    &
         USING T_CIECLE,  &
               T_PRDCLE

;-------------------------------------------------------------------------------
;

; Fichier permettant de déterminer le code de devise du fournisseur.

FILE VFOC_FOU REFERENCE OCCURS WITH ARPRD_FOU
  ACCESS VIA FOUCIECLE            , &
             FOUCLE               , &
             FOCCIECLE              &
      USING FOUCIECLE OF ARPRD_FOU, &
            FOUCLE    OF ARPRD_FOU, &
            CIECLE    OF ARPRD_FOU

; Fichier des devises.

FILE MCDEVISE REFERENCE OCCURS WITH ARPRD_FOU
  ACCESS VIA DEVCLE &
       USING DEVCLE OF VFOC_FOU

; Paramètres pour la compagnie consolidé (holding).

FILE MCHOLDING REFERENCE
  ACCESS VIA CIENMC USING "1"

; Calcul du montant associé au "dernier coût" en tenant compte des devises.

DEFINE D_PDFDERCOU FLOAT SIZE 8 = ROUND(PDFDERCOU OF ARPRD_FOU / DEVTAU OF MCDEVISE) &
                                                           IF DEVCLE OF VFOC_FOU <> DEVCLE OF MCHOLDING          &
                             ELSE PDFDERCOU OF ARPRD_FOU

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tous les écrans
USE usdrwfnd NOLIST     ; Draw pour les écrans de choix et consultation

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO 2

ALIGN(3,3,6)(,,13)
CLUSTER OCCURS WITH ARPRD_FOU

FIELD FOUCLE    OF ARPRD_FOU HIDDEN LABEL ">>"
FIELD D_PDFDERCOU PICTURE "^,^^^,^^^.^^^^ " SIGNIF 7

CLUSTER

;-------------------------------------------------------------------------------
;
;
; Enlève le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CDEPRIUNI, "@")
  THEN LET T_CDEPRIUNI = " "
END

;-------------------------------------------------------------------------------
;
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CDEPRIUNI = ASCII (PDFDERCOU OF ARPRD_FOU,11)[1:7] + "." + &
                    ASCII (PDFDERCOU OF ARPRD_FOU,11)[8:4]
  RETURN
END

BUILD
@IF FORMAT
                                       *****************************************
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       * >> xxxxxx xxxxxxxxxxxxxxx             *
                                       *****************************************
@ENDIF
