;/P/ Liste des blocs expédiés par carrière
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme et pour que le rapport sorte par type de
;                     granit au lieu de par carriere.
;
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC &
 LINK CIECLE OF IFBLOC, &
      EXNCLE OF IFBLOC  &
   TO CIECLE, &
      EXNCLE                    OF IFEXPEDITION OPTIONAL

CHOOSE CIECLE PARM, &
       TGRCODGRA PARM

DEFINE TMP-DAT-DEB DATE = PARM ;-----------PROMPT             &
;------------               "ENTRER LA DATE DE DEBUT (999999): "
DEFINE TMP-DAT-FIN DATE = PARM ;---------PROMPT             &
;----------               "ENTRER LA DATE DE FIN   (999999): "


SELECT IF     EXNDAT OF IFEXPEDITION < TMP-DAT-FIN &
          AND EXNDAT OF IFEXPEDITION > TMP-DAT-DEB


DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""

SORT ON TGRCODGRA &
     ON EXNCLE &
     ON D_NUMBLC &
     ON D_NUMCOM

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" &
                        IF     TMP-DAT-DEB = 0 &
                           AND TMP-DAT-FIN = 0 &
        ELSE "Entre le : " + ASCII( TMP-DAT-DEB , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[7:2] + &
             " et : "      + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF306"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 3 "TYPE"                    &
             TAB 10 "NO"                   &
             TAB 26 "DATE"                 &
             TAB 35 "DIMENSIONS "          &
             TAB 52 "TOTAL"                &
             TAB 64 "T/M"                  &
             TAB 74 "NO"                   &
             TAB 81 "NO"                   &
             TAB 91 "CAT"                  &
             TAB 95 "COUL"                 &
             TAB 100 "QUAL"                 &
             TAB 106 "DATE"                 &
             TAB 117 "NO" &
             TAB 125 "DATE" &
SKIP 1 &
             TAB 3 "GRAN."                  &
             TAB 9  "BLOC"                 &
             TAB 26 "PROD"                 &
             TAB 34 "LONG  HAUT  LARG"       &
             TAB 53 "M3"                   &
             TAB 73 "FACT"                 &
             TAB 81 "COM."                  &
             TAB 106 "DEBUT"                &
             TAB 117 "EXPE" &
             TAB 125 "EXPE" &
SKIP 1 &
             TAB 3 "____"                  &
             TAB 8  "______________"       &
             TAB 24 "________"             &
             TAB 34 "________________"       &
             TAB 51 "_______"              &
             TAB 62 "_______"              &
             TAB 72 "______"               &
             TAB 80 "_________  ___ ____ ____  ________"  &
             TAB 115 "_______"                &
             TAB 123 "__________"                &
SKIP 2

REPORT  TGRCODGRA OF IFBLOC PRINT AT TGRCODGRA OF IFBLOC &
        D_NUMBLC                           &
        BLCDAT OF IFBLOC                   &
        BLCVENDULONG OF IFBLOC             &
        BLCVENDUHAU OF IFBLOC              &
        BLCVENDULAR OF IFBLOC              &
        BLCM3TOT OF IFBLOC                 &
        BLCTFINAL OF IFBLOC                &
        FTRNUMFAC OF IFBLOC                     &
        D_NUMCOM                           &
        CATCLE OF IFBLOC                     &
        CLRCLE OF IFBLOC                   &
        QLTCLE OF IFBLOC                   &
        COEDATDEB OF IFBLOC &
        EXNCLE OF IFBLOC &
        EXNDAT OF IFEXPEDITION

FOOTING AT TGRCODGRA OF IFBLOC &
        TAB 8 "______________"            &
        TAB 48 "__________"         &
        TAB 59 "__________"         &
SKIP 1  &
        TAB 9 COUNT "Blocs"         &
        TAB 30 "Sous-total:"        &
        TAB 50 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
        TAB 61 BLCTFINAL SUBTOTAL PICTURE "^^^^^.^^" &
SKIP 2
FINAL FOOTING  &
        TAB 8 "______________"            &
        TAB 48 "__________"         &
        TAB 59 "__________"         &
SKIP 1  &
        TAB 9 COUNT "Blocs"         &
        TAB 30 "Total:"             &
        TAB 49 BLCM3TOT SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 58 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 2
BUILD  if306z
