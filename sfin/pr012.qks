;/T/ Cummulatif Équipement
;/P/ Programmeur..: Marc Morissette
;    Date Création: 13 Oct 1993
;
;    Description..: Cummulatif projet
;
;/M/ Modifié par..: Guy Chabot le 07 novembre 1994
;            but..: Utiliser une vue au lieu des tables pour la sommation des
;                   projets.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr012 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "PR012"             ; Nom de l'écran

USE pr012.hlp nolist ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Sommaire des transactions  "  ACTION DESIGNER D001
  MENUITEM LABEL "Impression du cumulatif    "  ACTION DESIGNER D002


FILE VUNI01 PRIMARY OCCURS 13 TIMES

SELECT IF "C" <> FODTYP OF VUNI01 OR &
           0  <> (V_MNTCUM1 OF VUNI01 + V_MNTCUM2 OF VUNI01)

FILE GPFORMAT REFERENCE
  ACCESS VIA FORCLE USING FORCLE OF VUNI01

FILE GPUNITE REFERENCE
  ACCESS VIA CIECLE, UNICAT, UNICLE &
    USING T_CIECLEMNU, UNICAT OF VUNI01, UNICLE OF VUNI01

;
;
DEFINE    D_UNICAT CHARACTER SIZE 16 = "UNICAT"
;;TEMPORARY T_UNICAT CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_UNICAT

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_UNICAT, &
                UNICAT OF VUNI01



;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

DEFINE D_XPRNOM CHAR SIZE 7 = "PR316"
DEFINE D_LISPAR CHAR SIZE 204 = "F_CIECLE        |F_UNICAT        |F_UNICLE        |F_FORCLE        |F_CIENOM        "
DEFINE D_INDLON CHAR SIZE 112 = "001004|005002|007008|015005|020040"
DEFINE D_LISVAL CHAR SIZE 300 = CIECLE + UNICAT + UNICLE + FORCLE +T_CIENOM

TEMPORARY T_UNICATFND CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_UNICLEFND CHARACTER SIZE 8 RESET AT STARTUP
TEMPORARY T_FORCLEFND CHARACTER SIZE 5 RESET AT STARTUP

TEMPORARY T_UNICATPOP CHARACTER SIZE 4
TEMPORARY T_UNICAT2   CHARACTER SIZE 2
TEMPORARY T_UNICLEPOP CHARACTER SIZE 8
TEMPORARY T_UNISTUPAT CHARACTER SIZE 5 ; Popup sur les équipements.

;
; Les trois define servent a transformer du numerique en caractere
; tout en conservant le picture.
;
;
DEFINE D_MNT  FLOAT     SIZE  8 = V_MNTCUM1 OF VUNI01 + V_MNTCUM2 OF VUNI01

DEFINE D_MNT1 CHARACTER SIZE 15 = "$" + &
                         ASCII(ABS(D_MNT)/100) + &
                         "." + &
                         ASCII(MOD(ABS(D_MNT),100),2)

DEFINE D_MNT2 CHARACTER SIZE 18 = D_MNT1 IF 100000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:2] + "," + D_MNT1[3:13] &
                          IF 1000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:3] + "," + D_MNT1[4:12] &
                          IF 10000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:4] + "," + D_MNT1[5:11] &
                          IF 100000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:2] + "," + D_MNT1[3:3] + "," + D_MNT1[6:10] &
                          IF 1000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:3] + "," + D_MNT1[4:3] + "," + D_MNT1[7:9] &
                          IF 10000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:4] + "," + D_MNT1[5:3] + "," + D_MNT1[8:8] &
                          IF 100000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:2] + "," + D_MNT1[3:3] + "," + D_MNT1[6:3] + &
                          "," + D_MNT1[9:7] &
                          IF 1000000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:3] + "," + D_MNT1[4:3] + "," + D_MNT1[7:3] + &
                          "," + D_MNT1[10:6]

DEFINE D_MNT3 CHARACTER SIZE 20 = "--------------------" IF FODTYP = "S" &
                        ELSE "" IF FODTYP = "E" &
                        ELSE RJ(PACK("<" + D_MNT2 + ">")) IF D_MNT < 0 &
                        ELSE RJ(D_MNT2)
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Cummulatif Équipement " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP
ALIGN (,2,16)(,,25)
FIELD UNICAT OF VUNI01 LABEL "Catégorie..:" DISPLAY
FIELD CBIDSC OF VCBI_DSC_UNICAT  DISPLAY SIZE 20
FIELD UNICLE OF VUNI01 LABEL "Équipement.:" DISPLAY
FIELD UNIDSC1 OF GPUNITE DISPLAY SIZE 50

FIELD FORCLE OF VUNI01 DISPLAY LABEL "Format.....:"
FIELD FORDSC  OF GPFORMAT     DISPLAY
SKIP 1
TITLE "Description                                          Montant" AT ,2
SKIP
ALIGN (,,2)(,,54)
CLUSTER OCCURS WITH VUNI01
FIELD FODDSC OF VUNI01 DISPLAY
FIELD D_MNT3 DISPLAY
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE DESIGNER D001
BEGIN
  LET T_UNICATFND = UNICAT OF VUNI01
  LET T_UNICLEFND = UNICLE OF VUNI01
  RUN SCREEN pr016 PASSING T_CIECLEMNU, T_CIENOM, T_UNICATFND, T_UNICLEFND MODE F
END

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN gs090 MODE E PASSING D_XPRNOM,D_LISPAR,D_INDLON,D_LISVAL
END

PROCEDURE PATH
BEGIN
  RUN SCREEN pr012a MODE F &
        PASSING T_CIECLEMNU, &
                T_UNICATFND, &
                T_UNICLEFND, &
                T_FORCLEFND
END

PROCEDURE FIND
BEGIN
  FOR VUNI01
  BEGIN
    GET VUNI01 VIA CIECLE , UNICAT , UNICLE , FORCLE &
               ORDERBY CIECLE , UNICAT , UNICLE , FORCLE, FODLIG USING &
               T_CIECLEMNU , T_UNICATFND , T_UNICLEFND , T_FORCLEFND
  END
END

BUILD
