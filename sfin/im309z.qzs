;/T/ Renouvellement des contrats d'assurances
;
;/P/ Programmeur..: Alain Côté
;    Date création: 3 mars 1994
;
;/V3.02.00/ Alain Côté, 3 mars 1994, Immobilisation
;
;    Description...: Ce rapport imprime la liste des immobilisations dont la
;                    date de renouvellement du contrat d'entretien est entre
;                    les dates paramètrées.
;
;    Paramétres....: No. cie            (%CUF_CIECLEMNU)
;                    No. immo.          (%C IMMCLE)
;                    No. fournisseur    (%D FOUCLE)
;                    Date de debut      (%DUDATDEB)
;                    Date de fin        (%DUDATFIN)
;                    Nom cie            (%DUF_CIENOM)
;
;    Tris..........: Par ordre de fournisseur, par date d'échéance et par
;                    numéro d'immobilisation.
;
;    Particularités: Les immobilisations doivent être actives pour être
;                    sélectionnés.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
;
; Pour enlever la deuxième ligne de titre.
;
SET NOBLANKS


ACCESS IMIMMOBILISATION &

  LINK REFCIECLE OF IMIMMOBILISATION , &
       IMMFOUASS OF IMIMMOBILISATION   &
    TO FOUCIECLE , &
       FOUCLE OF CPFOURNISSEUR


CHOOSE CIECLE    PARM, &
       IMMFOUASS PARM , &
       IMMCLE    PARM

DEFINE D_DATDEB  DATE              = PARM
DEFINE D_DATFIN  DATE              = PARM

SELECT IMIMMOBILISATION IF     IMMDATFINASS OF IMIMMOBILISATION >= D_DATDEB &
                           AND IMMDATFINASS OF IMIMMOBILISATION <= D_DATFIN &
                           AND IMMSTU OF IMIMMOBILISATION = "A"

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Renouvellement des contrats d'assurance"
@ELSE
       "%%%Renouvellement des contrats d'assurance"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM309"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


SORT ON CIECLE       OF IMIMMOBILISATION &
     ON IMMFOUASS    OF IMIMMOBILISATION &
     ON IMMDATFINASS OF IMIMMOBILISATION &
     ON IMMCLE       OF IMIMMOBILISATION


USE ustitstd NOLIST
SKIP &
  TAB 051 &
@IF FRANCAIS
          "entre le" &
@ELSE
          "entre le" &
@ENDIF
  TAB 060 D_DATDEB &
  TAB 069 &
@IF FRANCAIS
          "et le" &
@ELSE
          "et le" &
@ENDIF
  TAB 075 D_DATFIN &
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
"                  Fournisseur                          Contact       " &
@ELSE
"                  Fournisseur                          Contact       " &
@ENDIF
SKIP &
  TAB 001 &
"-----------------------------------------------  --------------------" &
SKIP 2


HEADING AT IMMFOUASS &
   TAB 001 IMMFOUASS OF IMIMMOBILISATION &
   TAB 008 FOUNOM    OF CPFOURNISSEUR &
   TAB 050 IMMCNTASS OF IMIMMOBILISATION &
SKIP 2 &
@IF FRANCAIS
@ELSE
@ENDIF
   TAB 008 "Immobilisation                                         Echéance  Montant assuré           Prime" &
SKIP


REPORT &
   TAB 008 IMMCLE       OF IMIMMOBILISATION &
   TAB 021 IMMDSC       OF IMIMMOBILISATION &
   TAB 063 IMMDATFINASS OF IMIMMOBILISATION &
   TAB 074 IMMMNTASS    OF IMIMMOBILISATION &
   TAB 090 IMMMNTPRIASS OF IMIMMOBILISATION

FOOTING AT IMMFOUASS &
  SKIP 2

BUILD im309z
@IF FORMAT

IM309                                         XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                    PAGE: 9999
99/99/99 99:99 XXXXXXXXXXXX                   Renouvellement des contrats d'assurance
                                    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
                                                  entre le xx/xx/xx et le xx/xx/xx

                  Fournisseur                          Contact
-----------------------------------------------  --------------------

xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

       Immobilisation                                         Echéance  Montant assuré           Prime
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99

xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

       Immobilisation                                         Echéance  Montant assuré           Prime
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99

xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

       Immobilisation                                         Echéance  Montant assuré           Prime
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99   99,999,999.99   99,999,999.99

@ENDIF
