;/T/ Dépisteur Bloc de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-05
;
;    Description..: Permet de visionner et de choisir un bloc de granit
;
;    Paramètres...: T_BLONUMGRA001APR ==> Numéro de bloc
;                   T_BLONUMGRA002APR ==> Numéro de bloc
;                   T_BLORECAPR       ==> Numéro de bloc
;                   T_COD             ==> 0 = TOUS  ,
;                                         1 = (R ou T)
;                                         2 = <> T
;                   T_STU             ==> A (Abandonne)
;
;
;    Attention....: La procédure find a été codé
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd121 NOMODE NOACTION FIELDMARK &
             FROM 11,27 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_BLONUMGRA001APR, &
                       T_BLONUMGRA002APR, &
                       T_BLORECAPR,       &
                       T_CHAMP,           &
                       T_COD,             &
                       T_STU,             &
                       T_CIECLEMNU


TEMPORARY T_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_BLONUMGRA002APR  CHARACTER SIZE 05
TEMPORARY T_BLORECAPR        CHARACTER SIZE 01
TEMPORARY T_CHAMP            INTEGER UNSIGNED SIZE 1
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 2
TEMPORARY T_STU              CHARACTER SIZE 1
TEMPORARY T_CIECLEMNU        CHARACTER SIZE 04

TEMPORARY T_LIRE_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_LIRE_BLONUMGRA002APR  CHARACTER SIZE 05
TEMPORARY T_LIRE_BLORECAPR        CHARACTER SIZE 01


USE pd121.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,            &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR          &
         USING   T_CIECLEMNU,       &
                 T_BLONUMGRA001APR, &
                 T_BLONUMGRA002APR, &
                 T_BLORECAPR        &
         ORDERBY CIECLE,            &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR


  SELECT IF T_COD = 0 OR &
           (T_COD = 1 AND (BLOSTU = "I" OR BLOSTU = "R")) OR &
           (T_COD = 2 AND BLOSTU <> "T")


FILE PDCOMMAN_ASSIGN REFERENCE OCCURS WITH PDBLOC

  ACCESS VIA     CIECLE,            &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR          &
         USING   T_CIECLEMNU,                    &
                 BLONUMGRA001APR    OF PDBLOC,   &
                 BLONUMGRA002APR    OF PDBLOC,   &
                 BLORECAPR          OF PDBLOC


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,54       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    No. bloc      Long  Haut  Épa.  Gra S Num. Cmd" &
@ELSE
      "%%% No. bloc      Long  Haut  Épa.  Gra S Num. Cmd" &
@ENDIF
    AT ,2

SKIP TO LINE 3

ALIGN(3,3,6) (,10,11) (,16,17) (,,20) (,,26) (,,32) (,,38) (,,42) (,,44) (,46,47) (,49,50)


CLUSTER OCCURS WITH PDBLOC


FIELD BLONUMGRA001APR  OF PDBLOC           &
        HIDDEN LABEL ">>" DISPLAY


FIELD BLONUMGRA002APR  OF PDBLOC           &
        DISPLAY                            &
        LABEL "-"


FIELD BLORECAPR        OF PDBLOC           &
        DISPLAY                            &
        LABEL "-"


FIELD BLOLONBRU        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL


FIELD BLOHAUBRU        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL


FIELD BLOLARBRU        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL


FIELD TGRCODGRA        OF PDBLOC           &
        DISPLAY                            &
        NOLABEL


FIELD BLOSTU           OF PDBLOC           &
         DISPLAY                           &
         NOLABEL


FIELD PJTABR           OF PDCOMMAN_ASSIGN  &
        DISPLAY                            &
        NOLABEL


FIELD PJTANN           OF PDCOMMAN_ASSIGN  &
        DISPLAY                            &
        BWZ                                &
        LABEL "-"


FIELD COMNUMCOMINT     OF PDCOMMAN_ASSIGN  &
        DISPLAY                            &
        BWZ                                &
        LABEL "-"


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
; Met le statut à "A" (abandonné)

PROCEDURE EXIT
BEGIN
  IF T_BLONUMGRA001APR = " " &
  AND T_BLONUMGRA002APR  = " " &
  AND T_BLORECAPR        = " "
  THEN BEGIN
    LET T_STU = "A"
  END
  ELSE BEGIN
    LET T_STU = " "
  END

  IF 0 <> INDEX(T_BLONUMGRA001APR , "@")
  THEN LET T_BLONUMGRA001APR = ""

  IF 0 <> INDEX(T_BLONUMGRA002APR , "@")
  THEN LET T_BLONUMGRA002APR = ""

  IF 0 <> INDEX(T_BLORECAPR , "@")
  THEN LET T_BLORECAPR = ""

END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE FIND
BEGIN

  IF T_BLONUMGRA001APR <> " " &
  OR T_BLONUMGRA002APR  <> " " &
  OR T_BLORECAPR <> " "
  THEN BEGIN
    LET T_LIRE_BLONUMGRA001APR  = T_BLONUMGRA001APR
    LET T_LIRE_BLONUMGRA002APR  = T_BLONUMGRA002APR
    LET T_LIRE_BLORECAPR        = T_BLORECAPR
    LET T_BLONUMGRA001APR  = " "
    LET T_BLONUMGRA002APR  = " "
    LET T_BLORECAPR        = " "
  END

  ;
  ; Lors de l'appel on a passé seulement le premier parametre pour la recherche
  ;
  IF T_CHAMP = 1
  THEN BEGIN
    FOR PDBLOC
    BEGIN
      GET PDBLOC &
        VIA     CIECLE,           &
                BLONUMGRA001APR   &
        USING   T_CIECLEMNU,            &
                T_LIRE_BLONUMGRA001APR  &
        ORDERBY CIECLE,           &
                BLONUMGRA001APR , &
                BLONUMGRA002APR , &
                BLORECAPR
    END
  END
  ELSE BEGIN
    ;
    ; Lors de l'appel on a passé seulement les deux premier parametre
    ; pour la recherche
    ;
    IF T_CHAMP = 2
    THEN BEGIN
      FOR PDBLOC
      BEGIN
        GET PDBLOC &
          VIA     CIECLE,             &
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR     &
          USING   T_CIECLEMNU,             &
                  T_LIRE_BLONUMGRA001APR , &
                  T_LIRE_BLONUMGRA002APR   &
          ORDERBY CIECLE,             &
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR ,   &
                  BLORECAPR
      END
    END
    ELSE BEGIN
    ;
    ; Lors de l'appel on a passé les trois parametres pour la recherche
    ;
      FOR PDBLOC
      BEGIN
        GET PDBLOC &
          VIA     CIECLE,             &
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR ,   &
                  BLORECAPR           &
          USING   T_CIECLEMNU,             &
                  T_LIRE_BLONUMGRA001APR , &
                  T_LIRE_BLONUMGRA002APR , &
                  T_LIRE_BLORECAPR         &
          ORDERBY CIECLE,             &
                  BLONUMGRA001APR ,   &
                  BLONUMGRA002APR ,   &
                  BLORECAPR
      END
    END
  END
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
  LET T_BLONUMGRA002APR  = BLONUMGRA002APR OF PDBLOC
  LET T_BLORECAPR        = BLORECAPR       OF PDBLOC
  RETURN
END

BUILD

@IF FORMAT

                          ******************************************************
                          *    No. bloc      Long  Haut  Épa.  Gra S Num. Cmd  *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          * >> xxxx-xxxxx-x  xxxx  xxxx  xxxx  xxx x xx-xx-xxx *
                          *                                                    *
                          ******************************************************
@ENDIF
