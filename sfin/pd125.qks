;/T/ Permet de trouver les priorités pour un diagramme
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1994-10-27
;
;    Description..: Permet de visionner et de choisir une priorité pour un
;                   diagramme qui ont des valeurs plus grande que zéro.
;
;    Paramètres...: Commande interne ==> PJTABR
;                                        PJTNUMSEQ
;                                        COMNUMCOMINT
;                   Diagramme        ==> DIANUMDIA002
;                   Priorité         ==> PRICODPRI
;                   Usine secondaire ==> T_USINE_SEC
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd125 NOMODE NOACTION FIELDMARK    &
             FROM 11,35 TO 23,80          &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND              &
             RECEIVING T_PJTABR,          &
                       T_PJTNUMSEQ,       &
                       T_COMNUMCOMINT,    &
                       T_DIANUMDIA002,    &
                       T_PRICODPRI,       &
                       T_USINE_SEC,       &
                       T_CIECLEMNU



USE pd125.hlp NOLIST

USE ustrasse NOLIST


TEMPORARY T_PJTABR          CHARACTER SIZE 02
TEMPORARY T_PJTNUMSEQ       CHARACTER SIZE 03
TEMPORARY T_COMNUMCOMINT    CHARACTER SIZE 03
TEMPORARY T_DIANUMDIA002    CHARACTER SIZE 05
TEMPORARY T_PRICODPRI       CHARACTER SIZE 02
TEMPORARY T_USINE_SEC       CHARACTER SIZE 01
TEMPORARY T_CIECLEMNU       CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Priorité diagramme
;

FILE PDPRIORITE_DIAG  PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,                  &
                 PJTABR,                  &
                 PJTNUMSEQ,               &
                 COMNUMCOMINT,            &
                 DIANUMDIA002             &
         USING   T_CIECLEMNU,             &
                 T_PJTABR,                &
                 T_PJTNUMSEQ,             &
                 T_COMNUMCOMINT,          &
                 T_DIANUMDIA002           &
         ORDERBY CIECLE,                  &
                 PJTABR,                  &
                 PJTNUMSEQ,               &
                 COMNUMCOMINT,            &
                 PRICODPRI


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,46       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    Prio. Diagramme Comman. int." &
@ELSE
      "%%% Prio. Diagramme Comman. int." &
@ENDIF
    AT ,2

SKIP TO LINE 3

TITLE &
@IF FRANCAIS
      "    ----- --------- ------------" &
@ELSE
      "%%% ----- --------- ------------" &
@ENDIF
    AT ,2

SKIP TO LINE 4


ALIGN (3,3,7) (,11,12) (,14,15) (,21,22) (,24,25) (,28,29)


CLUSTER OCCURS WITH PDPRIORITE_DIAG


FIELD PRICODPRI        OF PDPRIORITE_DIAG        &
        HIDDEN LABEL ">>" DISPLAY NUMERIC


FIELD PJTABR           OF PDPRIORITE_DIAG        &
        DISPLAY                                  &
        NOLABEL


FIELD DIANUMDIA002     OF PDPRIORITE_DIAG        &
        DISPLAY                                  &
        LABEL "-"


FIELD PJTABR           OF PDPRIORITE_DIAG        &
        DISPLAY                                  &
        NOLABEL


FIELD PJTNUMSEQ        OF PDPRIORITE_DIAG        &
        DISPLAY                                  &
        BWZ                                      &
        LABEL "-"


FIELD COMNUMCOMINT     OF PDPRIORITE_DIAG        &
        DISPLAY                                  &
        BWZ                                      &
        LABEL "-"


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN

  IF 0 <> INDEX(T_PRICODPRI, "@")
  THEN LET T_PRICODPRI = ""

END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN

  LET T_PRICODPRI = PRICODPRI OF PDPRIORITE_DIAG
  RETURN

END

BUILD

@IF FORMAT
                                  *********************************************
                                  *    Prio. Diagramme Comman. int.           *
                                  *    ----- --------- ------------           *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  * >>  xx   xx-xxxxx  xx-xxx-xxx             *
                                  *********************************************

@ENDIF
