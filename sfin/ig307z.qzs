;/T/ Impression de la liste d'erreur de l'inventaire physique
;    des tranche .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-02
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste d'erreur des tranches de l'inventaire physique
;                   de tranches de Granicor.
;
;    Paramètres....: Compagnie
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST
;
; 1) NUMERO DE TRANCHE EN DOUBLE
;
ACCESS INTRANCHE

CHOOSE CIECLE PARM

SELECT IF TRANUMTRA002 <> "9999999"

SORT ON TRANUMTRA002

DEFINE D-NBRE-TRANCHE INTEGER SIZE 2 = 1

SET SUBFILE AT TRANUMTRA002 NAME WIG307Z1

REP SUMMARY                             &
  CIECLE        OF INTRANCHE        &
  TRANUMTRA002  OF INTRANCHE        &
  D-NBRE-TRANCHE SUB RESET AT TRANUMTRA002

BUILD ig307z1

;===============================================================================
;
ACCESS *WIG307Z1                        &
  LINK TRANUMTRA002     OF WIG307Z1,    &
       CIECLE           OF WIG307Z1     &
    TO TRANUMTRA002,                    &
       CIECLE           OF INTRANCHE

SELECT IF D-NBRE-TRANCHE > 1

SORT ON TRANUMTRA002

DEFINE D-ERREUR      CHAR SIZE 30 = "NUMERO DE TRANCHE EN DOUBLE"

DEFINE D-INSTRUCTION CHAR SIZE 70 = "VEUILLEZ VERIFIER LE NUMERO DE TRANCHE"

SET SUBFILE NAME WIG307Z2

REPORT SUMMARY                          &
  CIECLE        OF WIG307Z1             &
  TRANUMTRA002  OF WIG307Z1             &
  ITRNUMPGE     OF INTRANCHE            &
  ITRNUMLOC     OF INTRANCHE            &
  ITRDATINV     OF INTRANCHE            &
  ITRHREINV     OF INTRANCHE            &
  D-ERREUR                              &
  D-INSTRUCTION                         &
  ITRDESC       OF INTRANCHE

BUILD ig307z2

;================================================================================
;
; VERIFICATION DES TYPES DE FINI, AJOUT DANS WIG307Z2
;
ACCESS INTRANCHE                            &
   LINK TRANUMTRA002    OF INTRANCHE,       &
        CIECLE          OF INTRANCHE        &
     TO TRANUMTRA002,                           &
        CIECLE          OF PDTRANCHE

CHOOSE CIECLE PARM

SORT ON TRANUMTRA002

SELECT IF TRANUMTRA002  OF INTRANCHE <> "9999999" AND &
          TYFCODFIN     OF INTRANCHE <> "" AND &
          TYFCODFIN     OF PDTRANCHE     <> TYFCODFIN OF INTRANCHE

DEFINE D-ERREUR         CHARACTER SIZE 30 = &
                        "FINI INFO(" + TYFCODFIN OF PDTRANCHE + &
                        ") <> FINI INV (" + TYFCODFIN OF INTRANCHE+ ")"

DEFINE D-INSTRUCTION CHAR SIZE 70 = "VEUILLEZ VERIFIER LE FINI DE SURFACE"

SET SUBFILE NAME WIG307Z2 AT TRANUMTRA002 APPEND

REPORT SUMMARY                          &
  CIECLE        OF INTRANCHE            &
  TRANUMTRA002  OF INTRANCHE            &
  ITRNUMPGE     OF INTRANCHE            &
  ITRNUMLOC     OF INTRANCHE            &
  ITRDATINV     OF INTRANCHE            &
  ITRHREINV     OF INTRANCHE            &
  D-ERREUR                              &
  D-INSTRUCTION                         &
  ITRDESC       OF INTRANCHE

BUILD ig307z3

;===============================================================================
;
; NUMERO DE TRANCHE INVALIDE, AJOUT DANS WIG307Z2
;
ACCESS INTRANCHE                            &
   LINK TRANUMTRA002    OF INTRANCHE,       &
        CIECLE          OF INTRANCHE        &
     TO TRANUMTRA002,                           &
        CIECLE          OF PDTRANCHE OPTIONAL

CHOOSE CIECLE PARM

SORT ON TRANUMTRA002

SELECT IF TRANUMTRA002 OF INTRANCHE <> "9999999" AND &
          NOT RECORD PDTRANCHE EXIST

DEFINE D-ERREUR      CHAR SIZE 30 = "NUMERO DE TRANCHE INVALIDE"
DEFINE D-INSTRUCTION CHAR SIZE 70 = &
   "VERIFIER LE NUMERO DE TRANCHE->SI BON PRENDRE CARACTERISTIQUES"

SET SUBFILE NAME WIG307Z2 AT TRANUMTRA002 APPEND

REPORT SUMMARY                          &
  CIECLE        OF INTRANCHE            &
  TRANUMTRA002  OF INTRANCHE            &
  ITRNUMPGE     OF INTRANCHE            &
  ITRNUMLOC     OF INTRANCHE            &
  ITRDATINV     OF INTRANCHE            &
  ITRHREINV     OF INTRANCHE            &
  D-ERREUR                              &
  D-INSTRUCTION                         &
  ITRDESC       OF INTRANCHE

BUILD ig307z4

;==============================================================================
;
; VERIFICATION DU STATUT
;
ACCESS INTRANCHE                                &
   LINK TRANUMTRA002    OF INTRANCHE,           &
        CIECLE          OF INTRANCHE            &
     TO TRANUMTRA002,                           &
        CIECLE          OF PDTRANCHE            &
   LINK TRANUMTRA002    OF INTRANCHE,           &
        CIECLE          OF INTRANCHE            &
     TO TRANUMTRA002,                           &
        CIECLE          OF PDEMBAL_TRANCHE      &
   LINK CAINUMCAI       OF PDEMBAL_TRANCHE,     &
        CIECLE          OF PDEMBAL_TRANCHE      &
     TO CAINUMCAI,                              &
        CIECLE          OF PDCAISSON OPTIONAL

CHOOSE CIECLE PARM

SORT ON TRANUMTRA002

SELECT IF TRASTU OF PDTRANCHE <> "I" or &
          TRASTU OF PDTRANCHE <> "R" or &
          TRASTU OF PDTRANCHE <> "A"

DEFINE D-ERREUR         CHARACTER SIZE 30 = &
                        "STATUT TRANCHE DIFFERENT (" + TRASTU +")"

DEFINE D-INSTRUCTION CHAR SIZE 70 = &
   "CAISSON:" + CAINUMCAI + &
   "-> VERIFIER NO TRANCHE -> SI BON PRENDRE CARACTERISTIQUES"

SET SUBFILE NAME WIG307Z2 AT TRANUMTRA002 APPEND

REPORT SUMMARY                  &
  CIECLE        OF INTRANCHE    &
  TRANUMTRA002  OF INTRANCHE    &
  ITRNUMPGE     OF INTRANCHE    &
  ITRNUMLOC     OF INTRANCHE    &
  ITRDATINV     OF INTRANCHE    &
  ITRHREINV     OF INTRANCHE    &
  D-ERREUR                      &
  D-INSTRUCTION                 &
  ITRDESC       OF INTRANCHE

BUILD ig307z5

;===============================================================================
;
; PDTRANCHE SANS ETIQUETTE
;
ACCESS INTRANCHE

CHOOSE CIECLE PARM, TRANUMTRA002 "9999999"

SORT ON TRANUMTRA002

DEFINE D-ERREUR      CHAR SIZE 30 = "TRANCHE SANS ETIQUETTE"

DEFINE D-INSTRUCTION CHAR SIZE 70 = &
   "PRENDRE CARACTERISTIQUES DE LA TRANCHE ET POSER 'TAG'"

SET SUBFILE NAME WIG307Z2 APPEND

REPORT SUMMARY                  &
  CIECLE        OF INTRANCHE    &
  TRANUMTRA002  OF INTRANCHE    &
  ITRNUMPGE     OF INTRANCHE    &
  ITRNUMLOC     OF INTRANCHE    &
  ITRDATINV     OF INTRANCHE    &
  ITRHREINV     OF INTRANCHE    &
  D-ERREUR                      &
  D-INSTRUCTION                 &
  ITRDESC       OF INTRANCHE

BUILD ig307z6

;==============================================================================
;
; SITE DIFFÉRENT
;
;ACCESS INTRANCHE                            &
;   LINK TRANUMTRA002    OF INTRANCHE,       &
;        CIECLE          OF INTRANCHE        &
;     TO TRANUMTRA002,                           &
;        CIECLE          OF PDTRANCHE

;CHOOSE CIECLE PARM

;SELECT IF TRANUMTRA002 OF INTRANCHE <> "9999999" AND &
;       CIECLE OF INTRANCHE = D-CIECLE AND CIECLE OF PDTRANCHE <> D-CIECLE

;DEFINE D-ERREUR      CHAR SIZE 30 = "USINE DIFFERENTE"
;DEFINE D-INSTRUCTION CHAR SIZE 70 = &
;   "VOIR A TRANSFERER LES TRANCHES OU CORRIGER L'INVENTAIRE"

;SET SUBFILE NAME WIG307Z2 APPEND
;REPORT SUMMARY CIECLE OF INTRANCHE TRANUMTRA002 OF INTRANCHE ITRNUMPGE NO-LOC &
;    ITRDATINV TIME-STAMP D-ERREUR D-INSTRUCTION

;BUILD ig307z7

;===============================================================================

ACCESS *WIG307Z2

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG307"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-ITRNUMLOC CHARACTER SIZE 4 = RJ(ITRNUMLOC)

SORT ON D-ITRNUMLOC     &
     ON ITRNUMPGE       &
     ON ITRDATINV       &
     ON ITRHREINV       &
     ON TRANUMTRA002    &
     ON D-ERREUR

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES D-ERREURS A CORRIGER                                                  ")
DEFINE D-TITRE-CENTRE1 CHARACTER SIZE 80 = &
        CENTRE ("-----INVENTAIRE ----TRANCHES                                                    ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 1 &
  TAB 025 D-TITRE-CENTRE1&
SKIP 3 &
  TAB 005 "                 NUM.  NUMÉRO   " &
SKIP 1 &
  TAB 005 "LOCALISATION     PAGE  TRANCHE  ERREUR(Description)                REMARQUE" &
SKIP 1 &
  TAB 005 "COMMENTAIRE " &
SKIP 1 &
  TAB 005 "------------     ----  -------  -------------------                --------" &
SKIP 2

FOOTING AT D-ITRNUMLOC SKIP PAGE

FOOTING AT TRANUMTRA002 SKIP 3

REPORT &
  TAB 011 D-ITRNUMLOC                   PRINT AT D-ITRNUMLOC    &
  TAB 020 ITRNUMPGE     OF WIG307Z2     PRINT AT ITRNUMPGE      &
  TAB 028 TRANUMTRA002  OF WIG307Z2     PRINT AT TRANUMTRA002   &
  TAB 037 D-ERREUR                                              &
  TAB 072 D-INSTRUCTION                 PRINT AT TRANUMTRA002   &
SKIP                                                            &
  TAB 005 ITRDESC       OF WIG307Z2

BUILD ig307z8

;=============================================================================
;
; SOMMAIRE DES D-ERREURS
;
ACCESS *WIG307Z2

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG307"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-COMPTEUR CHAR SIZE 15 = ASCII(ITRDATINV OF WIG307Z2) + &
                                 ASCII(ITRHREINV OF WIG307Z2) IF &
                                 TRANUMTRA002 OF WIG307Z2 = "9999999" ELSE &
                                 "123456789012345"

SORT ON D-ERREUR ON TRANUMTRA002 ON D-COMPTEUR


DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("SOMMAIRE PAR TYPE D'ERREURS                                                     ")

USE ustitstd52 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4

FOOTING AT D-ERREUR                             &
  D-ERREUR HEAD "DESCRIPTION ERREUR" "---->" COUNT AT D-COMPTEUR &
     HEAD "QUANTITE"

FINAL FOOTING "TOTAL DE " COUNT AT D-COMPTEUR "ERREURS"

BUILD ig307z9

;====================================================================================================================================
;
; SOUS-FICHIER TRANCHES PRESENTES AVANT ET NON APRES
;
ACCESS PDTRANCHE                                &
  LINK TRANUMTRA002     OF PDTRANCHE,           &
       CIECLE           OF PDTRANCHE            &
    TO TRANUMTRA002,                            &
       CIECLE           OF INTRANCHE OPTIONAL   &
  LINK BLONUMGRA001APR  OF PDTRANCHE,           &
       BLONUMGRA002APR  OF PDTRANCHE,           &
       BLORECAPR        OF PDTRANCHE,           &
       CIECLE           OF PDTRANCHE            &
    TO BLONUMGRA001APR,                         &
       BLONUMGRA002APR,                         &
       BLORECAPR,                               &
       CIECLE           OF PDBLOC OPTIONAL

CHOOSE CIECLE PARM

SELECT IF (TRASTU OF PDTRANCHE = "I" OR &
          TRASTU OF PDTRANCHE <> "R" OR &
          TRASTU OF PDTRANCHE <> "A") &
          AND NOT RECORD INTRANCHE EXIST

SET SUBFILE NAME WIG307Z3

REPORT SUMMARY                  &
  CIECLE        OF PDTRANCHE    &
  TRANUMTRA002  OF PDTRANCHE    &
  TYFCODFIN     OF PDTRANCHE    &
  TRALON        OF PDTRANCHE    &
  TRAHAU        OF PDTRANCHE    &
  TRAEPA        OF PDTRANCHE    &
  TGRCODGRA     OF PDBLOC       &
  BLONUMBLOAPR  OF PDBLOC

BUILD ig307z10

;=================================================================================
;
; IMPRESSION DU SOUS-FICHIER PRESENTES AVANT ET NON APRES
;
;set report limit 100

ACCESS *WIG307Z3

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG307"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES ERREURS DE L'INVENTAIRE PHYSIQUE DE TRANCHES                          ")
DEFINE D-TITRE-CENTRE1 CHARACTER SIZE 80 = &
        CENTRE ("ERREUR: PRESENTE AVANT ET NON APRES                                             ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 1 &
  TAB 025 D-TITRE-CENTRE1 &
SKIP 3 &
  TAB 003 "NUMÉRO    CODE   TYPE                                NUMÉRO" &
SKIP &
  TAB 003 "TRANCHE  GRANIT  FINI  LONGUEUR  HAUTEUR  ÉPAISSEUR   BLOC" &
SKIP &
  TAB 003 "-------  ------  ----  --------  -------  ---------  ------" &
SKIP 2

REPORT                                  &
  TAB 003 TRANUMTRA002  OF WIG307Z3     &
  TAB 013 TGRCODGRA     OF WIG307Z3     &
  TAB 021 TYFCODFIN     OF WIG307Z3     &
  TAB 028 TRALON        OF WIG307Z3     &
  TAB 037 TRAHAU        OF WIG307Z3     &
  TAB 047 TRAEPA        OF WIG307Z3     &
  TAB 056 BLONUMBLOAPR  OF WIG307Z3

FINAL  FOOTING &
       SKIP 2 TAB 2 "NOMBRE TOTAL DE TRANCHE TOTAL ......." COUNT
BUILD ig307z11

;===============================================================================
;
; SOUS-FICHIER TRANCHES PRESENTES APRES ET NON AVANT
;
ACCESS INTRANCHE                                &
  LINK TRANUMTRA002     OF INTRANCHE,           &
       CIECLE           OF INTRANCHE            &
    TO TRANUMTRA002,                            &
       CIECLE           OF PDTRANCHE OPTIONAL   ;&
;  LINK BLONUMGRA001APR  OF PDTRANCHE,           &
;       BLONUMGRA002APR  OF PDTRANCHE,           &
;       BLORECAPR        OF PDTRANCHE,           &
;       CIECLE           OF PDTRANCHE            &
;    TO BLONUMGRA001APR,                         &
;       BLONUMGRA002APR,                          &
;       BLORECAPR,                               &
;       CIECLE           OF PDBLOC OPTIONAL

CHOOSE CIECLE PARM

SELECT IF TRANUMTRA002 OF INTRANCHE <> "9999999" AND &
;          TRASTU OF PDTRANCHE = "I" OR &
          NOT RECORD PDTRANCHE EXIST

SET SUBFILE NAME WIG307Z3_2

REPORT SUMMARY                  &
  CIECLE        OF INTRANCHE    &
  TRANUMTRA002  OF INTRANCHE    &
  TYFCODFIN     OF INTRANCHE    &
  ITRNUMLOC     OF INTRANCHE    &
  ITRNUMPGE     OF INTRANCHE    &
  ITRDESC       OF INTRANCHE

BUILD ig307z12

;===============================================================================
;
; IMPRESSION DU SOUS-FICHIER PRESENTES APRES ET NON AVANT
;
ACCESS *WIG307Z3_2

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG307"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES ERREURS DE L'INVENTAIRE PHYSIQUE DE TRANCHES                          ")
DEFINE D-TITRE-CENTRE1 CHARACTER SIZE 80 = &
        CENTRE ("ERREUR: PRESENTE APRES ET NON AVANT                                             ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 1 &
  TAB 025 D-TITRE-CENTRE1 &
SKIP 3 &
  TAB 003 "NUMÉRO                NUMÉRO   TYPE" &
SKIP &
  TAB 003 " PAGE   LOCALISATION  TRANCHE  FINI  COMMENTAIRE" &
SKIP &
  TAB 003 "------  ------------  -------  ----  -----------" &
SKIP 2

REPORT                                  &
  TAB 003 ITRNUMPGE     OF WIG307Z3_2   &
  TAB 015 ITRNUMLOC     OF WIG307Z3_2   &
  TAB 025 TRANUMTRA002  OF WIG307Z3_2   &
  TAB 035 TYFCODFIN     OF WIG307Z3_2   &
  TAB 040 ITRDESC       OF WIG307Z3_2

FINAL  FOOTING &
       SKIP 2 TAB 2 "NOMBRE TOTAL DE TRANCHE TOTAL ......." COUNT

BUILD ig307z13
