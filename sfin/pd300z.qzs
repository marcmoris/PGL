;/T/ 2.3.7 Liste des diagrammes par commande interne
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 22 février 1995
;
;    Description...: Liste des diagrammes pour une commande interne
;
;    Paramètres....: Num. commande interne (un, plusieur, tous )
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------

;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS PDDIAGRAMME                           &
  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE &
  LINK CIECLE           OF PDDIAGRAMME,      &
       PJTABR           OF PDDIAGRAMME,      &
       PJTANN           OF PDDIAGRAMME,      &
       COMNUMCOMINT     OF PDDIAGRAMME       &
  TO   CIECLE,                               &
       PJTABR,                               &
       PJTANN,                               &
       COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
  LINK CIECLE           OF PDDIAGRAMME,      &
       PJTABR           OF PDDIAGRAMME,      &
       PJTANN           OF PDDIAGRAMME,      &
       COMNUMCOMINT     OF PDDIAGRAMME,      &
       DCINUMLIG        OF PDDIAGRAMME       &
  TO   CIECLE,                               &
       PJTABR,                               &
       PJTANN,                               &
       COMNUMCOMINT,                         &
       DCINUMLIG        OF PDDETAIL_C_I      &
  LINK CIECLE           OF PDDIAGRAMME,      &
       TGRCODGRA        OF PDDETAIL_C_I      &
  TO   CIECLE,                               &
       TGRCODGRA        OF PDTYPE_GRANIT     &
  LINK CIECLE           OF PDDIAGRAMME,      &
       TYFCODFIN        OF PDDETAIL_C_I      &
  TO   CIECLE,                               &
       TYFCODFIN        OF PDTYPE_FINI       &
  LINK CLICIECLE        OF PDCOMMAN_INTERNE, &
       CLICLE           OF PDCOMMAN_INTERNE, &
       CIECLE           OF PDCOMMAN_INTERNE  &
  TO   CLICIECLE,                            &
       CLICLE,                               &
       CIECLE           OF VCLC_CLI


CHOOSE CIECLE       PARM , &
       COMNUMCOM    PARM , &
       DIANUMDIA002 PARM


SORT ON COMNUMCOM    OF PDDIAGRAMME      &
     ON DCINUMLIG    OF PDDIAGRAMME      &
     ON DIANUMDIA002 OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = " "
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""


;-------------------------------------------------------------------------------
;
; Numéro du rapport.
;

DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD300"


;-------------------------------------------------------------------------------
;
; Nom de la compagnie.
;

DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


;-------------------------------------------------------------------------------
;
; Définition des éléments variables
;

DEFINE D_DESC_GRANIT   CHARACTER SIZE 40 = TGRDSCFRA001     OF PDTYPE_GRANIT IF DCITYPGRA        OF PDDETAIL_C_I = "1" &
                                                                           ELSE TGRDSCFRA002     OF PDTYPE_GRANIT

DEFINE D_DESC_FINI     CHARACTER SIZE 40 = TYFDSCFRA001     OF PDTYPE_FINI   IF DCITYPFIN        OF PDDETAIL_C_I = "1" &
                                                                           ELSE TYFDSCFRA002     OF PDTYPE_FINI

DEFINE D_DCINUMLIG     NUMERIC = NCONVERT(DCINUMLIG OF PDDETAIL_C_I)


USE ustitstd NOLIST
   SKIP


HEADING AT COMNUMCOM                           &
  SKIP                                         &
  TAB 001 "Num. commande.:"                    &
  TAB 017 COMNUMCOM        OF PDCOMMAN_INTERNE &
  SKIP                                         &
  TAB 001 "Code projet...:"                    &
  TAB 017 PJTABR           OF PDCOMMAN_INTERNE &
  SKIP                                         &
  TAB 001 "Client........:"                    &
  TAB 017 CLINOM           OF VCLC_CLI         &
  SKIP 3


HEADING AT DCINUMLIG                           &
  SKIP 3                                       &
  TAB 001 "Num. ligne....:"                    &
  TAB 017 D_DCINUMLIG PICTURE "^^^"            &
  TAB 021 "Épaisseur.....:"                    &
  TAB 037 DCIEPA           OF PDDETAIL_C_I     &
  SKIP                                         &
  TAB 001 "Type granit...:"                    &
  TAB 017 TGRCODGRA        OF PDDETAIL_C_I     &
  TAB 021 D_DESC_GRANIT                        &
  TAB 062 "Type fini.....:"                    &
  TAB 078 TYFCODFIN        OF PDDETAIL_C_I     &
  TAB 081 D_DESC_FINI                          &
  SKIP 2                                       &
  TAB 001 "Num diagramme  Long   Haut   Épai  Qte cmd P C    D C    P US   D US   P SC   D SC   P C    F C    P US   F US   P SC   F SC"


HEADING AT DIANUMDIA002 &
  TAB 003 DIANUM002        OF PDDIAGRAMME      &
  TAB 016 DIALON           OF PDDIAGRAMME      &
  TAB 023 DIAHAU           OF PDDIAGRAMME      &
  TAB 030 DIAEPA           OF PDDIAGRAMME      &
  TAB 037 DIAQTECMD        OF PDDIAGRAMME      &
  TAB 044 DIAQTEPLADBTGRA  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 051 DIAQTEDBTGRA     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 058 DIAQTEPLADBTUSC  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 065 DIAQTEDBTUSC     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 072 DIAQTEPLADBTSCT  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 079 DIAQTEDBTSCT     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 086 DIAQTEPLAFINGRA  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 093 DIAQTEFINGRA     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 100 DIAQTEPLAFINUSC  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 107 DIAQTEFINUSC     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 114 DIAQTEPLAFINSCT  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"  &
  TAB 121 DIAQTEFINSCT     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING "-"


FOOTING AT COMNUMCOM SKIP PAGE


BUILD pd300z


@IF FORMAT

PD300                                       XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                      PAGE: 9999
AA/MM/JJ HH:MM XXXXXXXXXXXX       XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num. commande.: XX-XX-XXX
Code projet...: XX
Client........: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX


Num. ligne....: 999 Épaisseur.....: 9999
Type granit...: XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Type fini.....: 99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num diagramme  Long   Haut   Épai  Qte cmd P G    D G    P US   D US   P SC   D SC   P G    F G    P US   F US   P SC   F SC
  XXXXXXX      9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999
  XXXXXXX      9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999
  XXXXXXX      9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999   9999


Num. ligne....: 999 Épaisseur.....: 9999
Type granit...: XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Type fini.....: 99  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num diagramme  Long  Haut  Épai  Qte cmd
  XXXXXXX      9999  9999  9999   9999
  XXXXXXX      9999  9999  9999   9999
  XXXXXXX      9999  9999  9999   9999
@ENDIF
