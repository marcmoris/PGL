;/T/ Maintenance des paramètres
;
;/P/ Programmeur..: Alain Côté
;    Date création: 1 septembre 1992
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gs004 ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72

USE usactecr NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-Ecrans"
  MENUITEM LABEL "Valeurs        "      ACTION ID 50
  MENUITEM LABEL "Codes bilingues"      ACTION DESIGNER BILI
@ELSE
ACTIONMENU LABEL "Subscreens"
  MENUITEM LABEL "Values         "      ACTION ID 50
  MENUITEM LABEL "Bilingual Codes"      ACTION DESIGNER BILI
@ENDIF

USE gs004.hlp           ; Use servant à la documentation de l'écran

USE ussectmp NOLIST     ; Variable pour la securite
"GS004"                 ; Nom de l'écran


FILE GSPARAMETRE      PRIMARY
  ACCESS VIA XPANOM &
         REQUEST XPANOM &
         USING XPANOM OF GSPARAMETRE
  ACCESS VIAINDEX XPA_U01 &
         VIA XPANOM &
         USING "@"

;
; Pour conserver l'intégrité des données lors d'une destruction
;
FILE GSXPA_VALUE      DELETE &
                      NOITEM
  ACCESS VIA XPANOM &
         USING XPANOM OF GSPARAMETRE

;
; Code bilingue
;
FILE VCBI_DSC         DESIGNER &
                      OPEN READ

;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE D_XPACAT    CHAR SIZE 16 = "XPACAT"
TEMP   T_XPACAT    CHAR SIZE 4

DEFINE D_XELNOM    CHARACTER SIZE 16 = XPANOM OF GSPARAMETRE

DEFINE D_LANGUE    CHARACTER SIZE 1 = &
@IF FRANCAIS
                                      "F"
@ELSE
                                      "A"
@ENDIF

;
;
DEFINE D_XPATYP    CHARACTER SIZE 10 = &
                               "Date"      IF XPATYP OF GSPARAMETRE = "D" &
                          ELSE "Caractère" IF XPATYP OF GSPARAMETRE = "C" AND &
                                              D_LANGUE = "F" &
                          ELSE "Character" IF XPATYP OF GSPARAMETRE = "C" AND &
                                              D_LANGUE = "A" &
                          ELSE "Numérique" IF XPATYP OF GSPARAMETRE = "N" AND &
                                              D_LANGUE = "F" &
                          ELSE "Numeric"

DEFINE D_FLGSHI    CHARACTER SIZE 10 = &
                             "Majuscule" IF XPAFLGSHI OF GSPARAMETRE = "U" AND &
                                            D_LANGUE = "F" &
                        ELSE "Upshift"   IF XPAFLGSHI OF GSPARAMETRE = "U" AND &
                                            D_LANGUE = "A" &
                        ELSE "Minuscule" IF XPAFLGSHI OF GSPARAMETRE = "D" AND &
                                            D_LANGUE = "F" &
                        ELSE "Downshift" IF XPAFLGSHI OF GSPARAMETRE = "D" AND &
                                            D_LANGUE = "A" &
                        ELSE " "

USE ushilite NOLIST
USE usdrwecr NOLIST


TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF



SKIP 1

ALIGN(19,3,19)
FIELD XPANOM OF GSPARAMETRE HIDDEN ID 5 &
        REQUIRED NOCHANGE &
        LOOKUP NOTON GSPARAMETRE &
          VIA XPANOM &
          USING XPANOM OF GSPARAMETRE &
          MESSAGE 17 ; Ce code existe déjà

SKIP 1

ALIGN(19,3,19) (,,21)
FIELD XPACAT OF GSPARAMETRE HIDDEN ID 10 &
        LOOKUP ON VCBI_DSC &
          VIA XELNOM, CBICLE &
          USING D_XPACAT, XPACAT OF GSPARAMETRE  &
          MESSAGE 17 ; Ce code n'existe pas

FIELD CBIDSC OF VCBI_DSC DISPLAY

SKIP 1

ALIGN(19,3,19) (,,21) (,43,59)
FIELD XPATYP OF GSPARAMETRE HIDDEN ID 15 &
        REQUIRED

FIELD D_XPATYP DISPLAY

FIELD XPASIZ OF GSPARAMETRE ID SAME &
        REQUIRED IF XPATYP OF GSPARAMETRE <> "D"

SKIP TO GROUP 3

FIELD XPANBRDEC OF GSPARAMETRE ID SAME &
        IF XPATYP OF GSPARAMETRE = "N"

ALIGN(,43,59) (,,61)
FIELD XPAFLGSHI OF GSPARAMETRE ID SAME &
        IF XPATYP OF GSPARAMETRE = "C"

FIELD D_FLGSHI DISPLAY

SKIP 1

ALIGN(19,3,19)
FIELD XPAVALDEF OF GSPARAMETRE HIDDEN ID 20

SKIP 1

FIELD XPAPAT OF GSPARAMETRE HIDDEN ID 25
SKIP 1

FIELD XPALBLFRA OF GSPARAMETRE HIDDEN ID 30
FIELD XPALBLANG OF GSPARAMETRE HIDDEN ID 35
SKIP 1

FIELD XPAHLPFRA OF GSPARAMETRE HIDDEN ID 40
FIELD XPAHLPANG OF GSPARAMETRE HIDDEN ID 45

SKIP
ALIGN (3,3)
SUBSCREEN gs004a PASSING GSPARAMETRE NOLABEL HIDDEN ID 50 &
            MODE E AUTO IF XPACAT OF GSPARAMETRE = "4"

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT XPANOM
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; La procédure suivante est nécessaire dans le cas ou l'usager fait une
; recherche ou un sélect sur le code bilingue, la procédure transpose le
; code anglais dans la langue de base seulement si c'est une valeur
; existante.
;
;
PROCEDURE INPUT XPACAT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_XPACAT = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_XPACAT, T_XPACAT
    LET FIELDTEXT = TRUNC(T_XPACAT)
  END

END

;-------------------------------------------------------------------------------
;
PROCEDURE PROCESS XPATYP
BEGIN
  IF FIELDTEXT = "D"
  THEN BEGIN
    LET XPASIZ    OF GSPARAMETRE = 8
    LET XPAFLGSHI OF GSPARAMETRE = ""
    LET XPANBRDEC OF GSPARAMETRE = 0
    DISPLAY XPAFLGSHI OF GSPARAMETRE
    DISPLAY XPANBRDEC OF GSPARAMETRE
    DISPLAY XPASIZ    OF GSPARAMETRE
  END
  IF XPATYP OF GSPARAMETRE = "C"
  THEN BEGIN
    LET XPANBRDEC OF GSPARAMETRE= 0
    DISPLAY XPANBRDEC OF GSPARAMETRE
  END
  IF XPATYP OF GSPARAMETRE = "N"
  THEN BEGIN
    LET XPAFLGSHI OF GSPARAMETRE = ""
    DISPLAY XPAFLGSHI OF GSPARAMETRE
  END
END

;-------------------------------------------------------------------------------
;
; Valide le pattern saisie par l'usager
;
;
PROCEDURE EDIT XPAPAT
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT)) AND &
     NOT VALIDPATTERN(TRUNC(FIELDTEXT))
  THEN ERROR 22 ; Le PATTERN est invalide...
END

;-------------------------------------------------------------------------------
;
;
; Valide le LABEL, le caractère " ne doit se retrouver dans le champ.
; Formatte le LABEL selon les normes.
;
PROCEDURE EDIT XPALBLFRA
BEGIN
  IF 15 = SIZE(TRUNC(FIELDTEXT)) AND &
     ":" <> FIELDTEXT[15:1]
  THEN ERROR 20 ; Le LABEL est trop long..."

  ;
  ; Si l'usager a saisie un : dans le label, on l'enlève pour complèter
  ; avec des points.
  ;
  IF 0 <> IND(FIELDTEXT,":")
  THEN LET FIELDTEXT = FIELDTEXT[1: IND(FIELDTEXT,":") - 1]

  LET FIELDTEXT = (TRUNC(FIELDTEXT) + "..............")[1:14] + ":"

END

;
;
; Valide le LABEL, le caractère " ne doit se retrouver dans le champ.
; Formatte le LABEL selon les normes.
;
PROCEDURE EDIT XPALBLANG
BEGIN
  IF 15 = SIZE(TRUNC(FIELDTEXT)) AND &
     ":" <> FIELDTEXT[15:1]
  THEN ERROR 20; Le LABEL est trop long...

  ;
  ; Si l'usager a saisie un : dans le label, on l'enlève pour complèter
  ; avec des points.
  ;
  IF 0 <> IND(FIELDTEXT,":")
  THEN LET FIELDTEXT = FIELDTEXT[1: IND(FIELDTEXT,":") - 1]

  LET FIELDTEXT = (TRUNC(FIELDTEXT) + "..............")[1:14] + ":"

END

;-------------------------------------------------------------------------------
;
; Cette procédure respecte les conditions de saisie qui sont sur les champs.
;
;
PROCEDURE DESIGNER 15
BEGIN
  ACCEPT XPATYP OF GSPARAMETRE

  IF XPATYP OF GSPARAMETRE <> "D"
  THEN ACCEPT XPASIZ OF GSPARAMETRE

  IF XPATYP OF GSPARAMETRE = "N"
  THEN ACCEPT XPANBRDEC OF GSPARAMETRE

  IF XPATYP OF GSPARAMETRE = "C"
  THEN ACCEPT XPAFLGSHI OF GSPARAMETRE
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran des valeurs possible.
;
;
PROCEDURE DESIGNER 50
BEGIN
  IF XPACAT OF GSPARAMETRE = "4"
  THEN RUN SCREEN gs004a MODE F PASSING GSPARAMETRE
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran des codes bilingues.
;
;
PROCEDURE DESIGNER BILI
BEGIN
  IF XPACAT OF GSPARAMETRE = "3"
  THEN RUN SCREEN gs004b PASSING D_XELNOM MODE F
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GSPARAMETRE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GSPARAMETRE &
     AND NOT NEWRECORD     OF GSPARAMETRE &
     AND NOT DELETEDRECORD OF GSPARAMETRE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
