;/T/ Définition des codes de module
;
;/P/ Programmeur..: Alain Côté
;    Date modif...: 31 août 1992
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gs153 ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72

USE ussectmp NOLIST     ; Variable pour la securite
    "GS153"

USE gs153.hlp           ; Use servant à la documentation de l'écran

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


FILE GSMODULE         PRIMARY &
                      OCCURS 6
  ACCESS VIA XMOCOD &
         REQUEST XMOCOD &
         USING XMOCOD OF GSMODULE
  ACCESS VIAINDEX XMO_U01 &
         VIA XMOCOD &
         USING "@"  &
         ORDERBY XMONUM


USE ushilite NOLIST     ; Hilite pour tout les écrans
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page

TITLE &
@IF FRANCAIS
      " Codes de modules " &
@ELSE
    " Module Codes " &
@ENDIF
    CENTERED AT 2,1
SKIP 1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF

@IF FRANCAIS
  TITLE "Ord"               AT ,5
  TITLE "Code"              AT ,10
  TITLE "Titre français / Titre anglais" AT ,16
@ELSE
  TITLE "Ord"               AT ,5
  TITLE "Code"              AT ,10
  TITLE "French Title   / English Title" AT ,16
@ENDIF

SKIP 1

ALIGN (3,2,5)(,,10)(,,16)

CLUSTER OCCURS WITH GSMODULE ID BASE 10

FIELD XMONUM OF GSMODULE LABEL " " HIDDEN &
        REQUIRED NOCHANGE

FIELD XMOCOD OF GSMODULE REQUIRED &
       LOOKUP NOTON GSMODULE &
         VIA XMOCOD &
         USING XMOCOD OF GSMODULE &
         MESSAGE 17 ; Ce code existe déjà.

FIELD XMOTITFRA OF GSMODULE

ALIGN (,,16)

FIELD XMOTITANG OF GSMODULE

SKIP 1

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT XMONUM
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GSMODULE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF GSMODULE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GSMODULE &
       AND NOT NEWRECORD     OF GSMODULE &
       AND NOT DELETEDRECORD OF GSMODULE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD
