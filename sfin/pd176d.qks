;/T/ 2.2.2 Produits de dimension
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-01
;
;    Description..: Cette écran permet de d'enregistrer les réceptions des
;                   produits de dimension.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd176d ACTIONBAR                   &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM,          &
                       T_FOUNOM,          &
                       PDRECEPTION,       &
                       PDBON_COMMANDE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD176D"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd176d.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom compagnie
TEMPORARY T_FOUNOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom du fournisseur


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
USE usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximun et minimum pour les produits
;
USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Réception
;

FILE PDRECEPTION    MASTER


;-------------------------------------------------------------------------------
;
; Bon de commande
;

FILE PDBON_COMMANDE MASTER


;-------------------------------------------------------------------------------
;
; Détail réception produits de dimension
;

FILE PDREC_PROD_DIMEN PRIMARY &
                      NOITEM

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC,                            &
                 PODNUMLIG                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION,       &
                 PODNUMLIG       OF PDREC_PROD_DIMEN   &
         REQUEST PODNUMLIG       OF PDREC_PROD_DIMEN   &
         ORDERBY CIECLE,   &
                 PODNUMLIG


  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC,                            &
                 PDILON,                               &
                 PDIHAU,                               &
                 PDIEPA,                               &
                 TGRCODGRA,                            &
                 TYFCODFIN                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION,       &
                 PDILON          OF PDREC_PROD_DIMEN,  &
                 PDIHAU          OF PDREC_PROD_DIMEN,  &
                 PDIEPA          OF PDREC_PROD_DIMEN,  &
                 TGRCODGRA       OF PDREC_PROD_DIMEN,  &
                 TYFCODFIN       OF PDREC_PROD_DIMEN   &
         REQUEST PDILON          OF PDREC_PROD_DIMEN,  &
                 PDIHAU          OF PDREC_PROD_DIMEN,  &
                 PDIEPA          OF PDREC_PROD_DIMEN,  &
                 TGRCODGRA       OF PDREC_PROD_DIMEN,  &
                 TYFCODFIN       OF PDREC_PROD_DIMEN   &
         ORDERBY CIECLE,                               &
                 PDILON,                               &
                 PDIHAU,                               &
                 PDIEPA,                               &
                 TGRCODGRA,                            &
                 TYFCODFIN

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION        &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 PODNUMLIG

  ITEM CIECLE       OF PDREC_PROD_DIMEN INITIAL T_CIECLEMNU
  ITEM RCTNUMREC    OF PDREC_PROD_DIMEN INITIAL RCTNUMREC OF PDRECEPTION FIXED
  ITEM BCMNUMBCM    OF PDREC_PROD_DIMEN INITIAL BCMNUMBCM OF PDBON_COMMANDE


;-------------------------------------------------------------------------------
;
; Inscription du produit de dimension dans la base de donnée
;

FILE PDPROD_DIMENSION REFERENCE

  ACCESS VIA     CIECLE,                               &
                 PDILON,                               &
                 PDIHAU,                               &
                 PDIEPA,                               &
                 TGRCODGRA,                            &
                 TYFCODFIN                             &
         USING   T_CIECLEMNU,                          &
                 PDILON          OF PDREC_PROD_DIMEN,  &
                 PDIHAU          OF PDREC_PROD_DIMEN,  &
                 PDIEPA          OF PDREC_PROD_DIMEN,  &
                 TGRCODGRA       OF PDREC_PROD_DIMEN,  &
                 TYFCODFIN       OF PDREC_PROD_DIMEN

;-------------------------------------------------------------------------------
;
; Inscription du produit de dimension dans la base de donnée
;

FILE PDPROD_DIMENSION DESIGNER ALIAS PDI_NEW_PROD_DIM

  ACCESS VIA     CIECLE,                               &
                 PDILON,                               &
                 PDIHAU,                               &
                 PDIEPA,                               &
                 TGRCODGRA,                            &
                 TYFCODFIN                             &
         USING   T_CIECLEMNU,                          &
                 PDILON          OF PDREC_PROD_DIMEN,  &
                 PDIHAU          OF PDREC_PROD_DIMEN,  &
                 PDIEPA          OF PDREC_PROD_DIMEN,  &
                 TGRCODGRA       OF PDREC_PROD_DIMEN,  &
                 TYFCODFIN       OF PDREC_PROD_DIMEN


;-------------------------------------------------------------------------------
;
; Définition de variable temporaire pour le traitement
;

DEFINE D_M2     FLOAT SIZE 8 = PDILON    OF PDPROD_DIMENSION * &
                               PDIHAU    OF PDPROD_DIMENSION

TEMPORARY T_TYPE_PRODUIT CHARACTER SIZE 2 INITIAL "PD" RESET AT STARTUP

TEMPORARY T_OLD_VOL      FLOAT     SIZE 08
TEMPORARY T_NEW_VOL      FLOAT     SIZE 08
TEMPORARY T_OLD_SUR      FLOAT     SIZE 08
TEMPORARY T_NEW_SUR      FLOAT     SIZE 08
TEMPORARY T_PRIUNI       FLOAT     SIZE 08


;-------------------------------------------------------------------------------
;
; Détail du bon de commande
;

FILE PDDET_BON_COMMAN SECONDARY &
                      NOITEM

  ACCESS VIA     CIECLE,                         &
                 BCMNUMBCM,                      &
                 PODNUMLIG                       &
         USING   T_CIECLEMNU,                    &
                 BCMNUMBCM OF PDBON_COMMANDE,    &
                 PODNUMLIG OF PDREC_PROD_DIMEN

  ITEM CIECLE       OF PDDET_BON_COMMAN INITIAL T_CIECLEMNU
  ITEM TGRCODGRA    OF PDREC_PROD_DIMEN INITIAL TGRCODGRA    OF PDDET_BON_COMMAN
  ITEM TYFCODFIN    OF PDREC_PROD_DIMEN INITIAL TYFCODFIN    OF PDDET_BON_COMMAN

  ITEM PODRCU       OF PDDET_BON_COMMAN FINAL "0" IF PODQTERCU OF PDDET_BON_COMMAN = 0 &
                                                  ELSE "1"


;-------------------------------------------------------------------------------
;
; Fichiers de rérérence type de produit
;

FILE PDTYPE_PRODUIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TPDTYPPRD                       &
         USING   T_CIECLEMNU,                    &
                 TPDTYPPRD OF PDDET_BON_COMMAN


;-------------------------------------------------------------------------------
;
; Fichiers de rérérence type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TGRCODGRA                       &
         USING   T_CIECLEMNU,                    &
                 TGRCODGRA OF PDDET_BON_COMMAN


;-------------------------------------------------------------------------------
;
; Fichiers de rérérence type de fini
;

FILE PDTYPE_FINI REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TYFCODFIN                       &
         USING   T_CIECLEMNU,                    &
                 TYFCODFIN OF PDDET_BON_COMMAN


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Réception produits de dimension " &
@ELSE
      " %%%Réception produits de dimension " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 5


ALIGN (,3,19) (,38,56)


FIELD RCTNUMREC OF PDRECEPTION                &
label "No. réception.:"&
        HIDDEN ID 05                          &
        DISPLAY                               &
        PREDISPLAY REFRESH


FIELD RCTDATREC OF PDRECEPTION                 FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date réception:"&
        DISPLAY PREDISPLAY REFRESH


ALIGN (,3,19) (,,38)


FIELD FOUCLE       OF PDBON_COMMANDE          &
label "Fournisseur...:"&
        DISPLAY PREDISPLAY REFRESH


FIELD T_FOUNOM                                &
        DISPLAY PREDISPLAY REFRESH


DRAW 8,2 TO 8,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Produits de dimension " &
@ELSE
      " %%%Produits de dimension " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 10


ALIGN (2,3,19) (37,38,54)


FIELD PODNUMLIG    OF PDREC_PROD_DIMEN            &
label "Numéro ligne..:"&
        HIDDEN ID 25                              &
        DUPLICATE                                 &
        LOOKUP ON FILE PDDET_BON_COMMAN           &
          VIA     CIECLE,                         &
                  BCMNUMBCM,                      &
                  PODNUMLIG,                      &
                  TPDTYPPRD                       &
          USING   T_CIECLEMNU,                    &
                  BCMNUMBCM OF PDBON_COMMANDE,    &
                  PODNUMLIG OF PDREC_PROD_DIMEN,  &
                  T_TYPE_PRODUIT                  &
          MESSAGE 3062 ; Ce numéro de ligne de détail ne correspond pas à un détail pour un produit dimension


FIELD PODPRI       OF PDDET_BON_COMMAN            &
        HIDDEN ID 30                              &
        LABEL "Prix M2 cmd...:"                   &
        DISPLAY REFRESH


SKIP TO LINE 12


ALIGN (,3,19)


FIELD PDILON           OF PDREC_PROD_DIMEN &
label "Longueur......:"&
        DISPLAY


FIELD PDIHAU           OF PDREC_PROD_DIMEN &
label "Hauteur.......:"&
        DISPLAY


FIELD PDIEPA           OF PDREC_PROD_DIMEN &
label "Épaisseur.....:"&
        DISPLAY


ALIGN (,3,19) (,,25)


FIELD TGRCODGRA        OF PDREC_PROD_DIMEN &
label "Code granit...:"&
        DISPLAY                            &
        NUMERIC                            &
        BWZ


FIELD TGRDSCFRA001     OF PDTYPE_GRANIT    &
        DISPLAY


FIELD TYFCODFIN        OF PDREC_PROD_DIMEN &
label "Code fini.....:"&
        DISPLAY                            &
        NUMERIC                            &
        BWZ


FIELD TYFDSCFRA001     OF PDTYPE_FINI      &
        DISPLAY


ALIGN (,3,19) (37,38,54)


FIELD PODQTECMD        OF PDDET_BON_COMMAN &
label "Quantité comm.:"&
        DISPLAY


FIELD RPDQTE           OF PDREC_PROD_DIMEN &
label "Qte recu......:"&
  HIDDEN ID 60                       &
        REQUIRED


ALIGN (,3,19)


FIELD PODQTERCU        OF PDDET_BON_COMMAN &
label "Quantité reçu.:"&
        DISPLAY


ALIGN (,3,19) (,38,54)


FIELD PODSURCMD        OF PDDET_BON_COMMAN &
label "Surface CMD...:"&
        DISPLAY


FIELD PODSURRCU        OF PDDET_BON_COMMAN &
label "Surface RCU...:"&
        DISPLAY REFRESH


ALIGN (,3,19) (37,38,54)


FIELD RPDPRIUNI        OF PDREC_PROD_DIMEN &
label "Prix unitaire.:"&
        HIDDEN ID 82


FIELD PODCPL           OF PDDET_BON_COMMAN &
label "Complet.......:"&
        HIDDEN ID 88                       &
        DEFAULT "0"


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Valide le champs
;

PROCEDURE EDIT PODNUMLIG
BEGIN
  ;
  ; Valide que le champs a été saisit s'il n'y a pas eu de DUPLICATE
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide que la ligne n'est pas déjà complète
  ;
  IF PODCPL OF PDDET_BON_COMMAN = "1"
  THEN BEGIN
    ERROR 2932 ; Cette ligne est déjà complétée
  END
END


;-------------------------------------------------------------------------------
;
; Affectation des champs pour affichage après le lookup
;

PROCEDURE PROCESS PODNUMLIG
BEGIN
  LET PDILON          OF PDREC_PROD_DIMEN = PODLON          OF PDDET_BON_COMMAN
  LET PDIHAU          OF PDREC_PROD_DIMEN = PODHAU          OF PDDET_BON_COMMAN
  LET PDIEPA          OF PDREC_PROD_DIMEN = PODEPA          OF PDDET_BON_COMMAN
  LET TGRCODGRA       OF PDREC_PROD_DIMEN = TGRCODGRA       OF PDDET_BON_COMMAN
  LET TYFCODFIN       OF PDREC_PROD_DIMEN = TYFCODFIN       OF PDDET_BON_COMMAN
  DISPLAY PDILON          OF PDREC_PROD_DIMEN
  DISPLAY PDIHAU          OF PDREC_PROD_DIMEN
  DISPLAY PDIEPA          OF PDREC_PROD_DIMEN
  DISPLAY TGRCODGRA       OF PDREC_PROD_DIMEN
  DISPLAY TYFCODFIN       OF PDREC_PROD_DIMEN
  DISPLAY PODQTECMD       OF PDDET_BON_COMMAN
  DISPLAY PODPRI          OF PDDET_BON_COMMAN
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de calculer le volume des pièces commandées
;

PROCEDURE EDIT RPDQTE
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec4 NOLIST
  ;
  ; Calcul la quantité reçu
  ;
  LET PODQTERCU OF PDDET_BON_COMMAN = PODQTERCU OF PDDET_BON_COMMAN - &
                                      OLDVALUE(RPDQTE OF PDREC_PROD_DIMEN) + &
                                      FIELDVALUE ;(RPDQTE OF PDREC_PROD_DIMEM)
  ;
  ; Calcul de volume d'une pièce de granit
  ;
  LET TZ_LARGEUR   = PDILON           OF PDREC_PROD_DIMEN
  LET TZ_HAUTEUR   = PDIHAU           OF PDREC_PROD_DIMEN
  LET TZ_EPAISSEUR = PDIEPA           OF PDREC_PROD_DIMEN
  DO INTERNAL CALVOL
  ;
  ; Calcul du volume déjà reçu
  ;
  LET T_OLD_VOL    = TZ_VOLUME * OLDVALUE(RPDQTE OF PDREC_PROD_DIMEN)
  LET T_OLD_SUR    = TZ_MC2    * OLDVALUE(RPDQTE OF PDREC_PROD_DIMEN)
  ;
  ; Calcul de volume reçu
  ;
  LET T_NEW_VOL    = TZ_VOLUME * FIELDVALUE ;(RPDQTE OF PDREC_PROD_DIMEN)
  LET T_NEW_SUR    = TZ_MC2    * FIELDVALUE ;(RPDQTE OF PDREC_PROD_DIMEN)
END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu à date
;

PROCEDURE PROCESS RPDQTE
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODSURRCU OF PDDET_BON_COMMAN = PODSURRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_SUR + &
                                      T_NEW_SUR

  DISPLAY PODSURRCU OF PDDET_BON_COMMAN
END


;-------------------------------------------------------------------------------
;
; VALIDE QUE LE CHAMP À ÉTÉ SAISI
;

PROCEDURE INPUT RPDPRIUNI
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    LET TZ_LARGEUR   = PDILON           OF PDREC_PROD_DIMEN
    LET TZ_HAUTEUR   = PDIHAU           OF PDREC_PROD_DIMEN
    LET TZ_EPAISSEUR = PDIEPA           OF PDREC_PROD_DIMEN
    DO INTERNAL CALVOL
    LET T_PRIUNI = TZ_MC2 * PODPRI OF PDDET_BON_COMMAN
    LET FIELDTEXT = ASCII(FLOOR(T_PRIUNI / 1000000)) + "." + ASCII(MOD(T_PRIUNI,1000000))
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE EDIT RPDPRIUNI
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champs est requis
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;

PROCEDURE INPUT PODCPL
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT PODCPL
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Retrouve le détail bon de commande pour pas avoir d'erreur si plus d'une
; destruction pour un même bon de commande.
;

PROCEDURE POSTFIND
BEGIN
  GET PDDET_BON_COMMAN                      &
    VIA     CIECLE,                         &
            BCMNUMBCM,                      &
            PODNUMLIG,                      &
            TPDTYPPRD                       &
    USING   T_CIECLEMNU,                    &
            BCMNUMBCM OF PDBON_COMMANDE,    &
            PODNUMLIG OF PDREC_PROD_DIMEN,  &
            T_TYPE_PRODUIT
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDREC_PROD_DIMEN &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDREC_PROD_DIMEN &
     AND NOT NEWRECORD     OF PDREC_PROD_DIMEN &
     AND NOT DELETEDRECORD OF PDREC_PROD_DIMEN &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Évaluation des champs indiquant une mise à jour du bon de commande.
  ;
  IF NOT NEWRECORD OF PDREC_PROD_DIMEN AND &
     ALTEREDRECORD OF PDREC_PROD_DIMEN
  THEN BEGIN
    LET RCTDATDERMAJ    OF PDRECEPTION = SYSDATE
    LET RCTHREDERMAJ    OF PDRECEPTION = SYSTIME / 10000
    LET RCTUSRDERMAJ    OF PDRECEPTION = LOGONID
  END
  ;
  ; Vérifie que le produit de dimension existe dans la table sinon il le cree
  ;
  GET PDI_NEW_PROD_DIM OPT                             &
         VIA     CIECLE,                               &
                 PDILON,                               &
                 PDIHAU,                               &
                 PDIEPA,                               &
                 TGRCODGRA,                            &
                 TYFCODFIN                             &
         USING   T_CIECLEMNU,                          &
                 PDILON          OF PDREC_PROD_DIMEN,  &
                 PDIHAU          OF PDREC_PROD_DIMEN,  &
                 PDIEPA          OF PDREC_PROD_DIMEN,  &
                 TGRCODGRA       OF PDREC_PROD_DIMEN,  &
                 TYFCODFIN       OF PDREC_PROD_DIMEN
  IF ACCESSOK
  THEN BEGIN
    LET PDIQTE    OF PDI_NEW_PROD_DIM = PDIQTE    OF PDI_NEW_PROD_DIM + &
                                        RPDQTE    OF PDREC_PROD_DIMEN
  END
  ELSE BEGIN
    LET PDILON    OF PDI_NEW_PROD_DIM = PDILON    OF PDREC_PROD_DIMEN
    LET PDIHAU    OF PDI_NEW_PROD_DIM = PDIHAU    OF PDREC_PROD_DIMEN
    LET PDIEPA    OF PDI_NEW_PROD_DIM = PDIEPA    OF PDREC_PROD_DIMEN
    LET TGRCODGRA OF PDI_NEW_PROD_DIM = TGRCODGRA OF PDREC_PROD_DIMEN
    LET TYFCODFIN OF PDI_NEW_PROD_DIM = TYFCODFIN OF PDREC_PROD_DIMEN
    LET PDIQTE    OF PDI_NEW_PROD_DIM = RPDQTE    OF PDREC_PROD_DIMEN
    LET TZ_LARGEUR                 = PDILON OF PDI_NEW_PROD_DIM
    LET TZ_HAUTEUR                 = PDIHAU OF PDI_NEW_PROD_DIM
    LET TZ_EPAISSEUR               = PDILON OF PDI_NEW_PROD_DIM
    DO INTERNAL CALVOL
    LET PDIVOLMC3 OF PDI_NEW_PROD_DIM = TZ_VOLUME
    LET PDISURMC2 OF PDI_NEW_PROD_DIM = TZ_MC2
  END
END


;-------------------------------------------------------------------------------
;
; Procedure de destruction
;

PROCEDURE DELETE
BEGIN
  IF NOT DELETEDRECORD OF PDREC_PROD_DIMEN
  THEN BEGIN
    ;
    ; Décrémente la quantité reçu
    ;
    LET PODQTERCU OF PDDET_BON_COMMAN = PODQTERCU OF PDDET_BON_COMMAN - 1
    ;
    ; Décrémente de volume reçu
    ;
    LET TZ_LARGEUR   = PDILON          OF PDREC_PROD_DIMEN
    LET TZ_HAUTEUR   = PDIHAU          OF PDREC_PROD_DIMEN
    LET TZ_EPAISSEUR = PDIEPA          OF PDREC_PROD_DIMEN
    DO INTERNAL CALVOL
    LET PODSURRCU OF PDDET_BON_COMMAN = PODSURRCU OF PDDET_BON_COMMAN - TZ_MC2
    ;
    ; Initialise les indicateurs de réception
    ;
    IF PODQTERCU OF PDDET_BON_COMMAN = 0
    THEN BEGIN
      LET PODRCU OF PDDET_BON_COMMAN = "0"
    END
    ELSE BEGIN
      LET PODRCU OF PDDET_BON_COMMAN = "1"
    END
    ;
    ; Si la commade était complète alors on indique qu'elle ne le sera plus
    ;
    IF PODCPL OF PDDET_BON_COMMAN = "1"
    THEN BEGIN
      LET PODCPL OF PDDET_BON_COMMAN = "0"
    END
  END
  DELETE PDREC_PROD_DIMEN
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************** Réception produits de dimension ************************
*                                                                              *
* No. réception.: xxxxxxxxx          Date réception:   xxxxxxxx                *
* Fournisseur...: xxxxxx             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*                                                                              *
**************************** Produits de dimension *****************************
*                                                                              *
* Numéro ligne..: xxx                Prix M2 cmd...: xxxxxxxxxxxxxxx           *
*                                                                              *
* Longueur......: xxxx                                                         *
* Hauteur.......: xxxx                                                         *
* Épaisseur.....: xxxx                                                         *
* Code granit...: xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Code fini.....: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Quantité comm.: xxxxxxxxxxxx       Quantité reçu.: xxxxxxxxxxxx              *
* Surface CMD...: xxxxxxxxxx         Surface RCU...: xxxxxxxxxx                *
* Prix unitaire.: xxxxxxxxxxxxxxx    Complet.......: x                         *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
