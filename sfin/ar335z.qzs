;/T/ Impression du bon de commande
;
;/M/ Modification..: Martin Perron
;    Date..........: 10 d\351cembre 1999
;    Description...: Description des conditions de paiements en français ou
;                    en anglais selon le cas (FOULNG of CPFOURNISSEUR)
;
;/M/ Modification..: Martin Perron
;    Date..........: 09 décembre 1999
;    Description...: Enlevé le code du terme net de la commande(FOCTERNET)
;                    et ajouté le no de l'entrepot(ENTCLE) en bas
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 26 mai 1994
;    Description...: Ce programme permet de faire l'impression des bons de
;                    commande. Suite à la première impression, on peut
;                    faire la réimpression du bon de commande ou des duplicatas.
;    Paramètres....: Compagnie          (Compagnie du menu)
;                    Numéro de commande (Borne de début)
;                    Numéro de commande (Borne de fin)
;
;    Particularités: Un compteur de réimpression et de duplicatas est
;                    constamment mis à jour.
;
;/M/ Modification..: Pierre Routhier
;                    13 novembre 1996
;                    - Création d'une macro pour incorporer les codes escapes
;                      selon la demande du CRIM.
;
;/M/ Modification..: Pierre Routhier
;    Date..........: 11 décembre 1996
;    Description...: Sélection dans le rapport ar335z.qzs des items qui ne
;                    sont pas annulés. J'ai arrangé le rapport en conséquence
;                    pour que le total du bon de commande ne contienne pas
;                    le montant des items annulés.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 10 mars 2000.
;    Description.......: Permettre la saisie de 5 numéros de réquisitions "manuelles" supplémentaires - Imprimer ces numéros de
;                        réquisitions sur l'entête du bon de commande.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 16.
;
;    Signet............: MP-00-03-10_S16.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 10 mars 2000.
;    Description.......: La mention "Signature autorisée" ne doit apparaître que sur la dernière page du bon de commande.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 18.
;
;    Signet............: MP-00-03-10_S18.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 29 mars 2000.
;    Description.......: Imprimer en entier les montants en devise étrangères (comme la lire italienne). 
;                        Reformattage du rapport afin de libérer l'espace nécessaire à l'impression de 
;                        ces montants.
;
;    Référence.........: Demande directe 999999.
;
;    Signet............: MP-00-03-29_DD999999.
;
;    ------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 30 janvier 2001
;    Description.......: Ajustements afin de faciliter la validation des charges comptables au niveau des services financiers. Ajouter
;                        en pied de page la liste des items avec leur ventilation comptable (unité et compte administratif).
;
;    Référence.........: Demande de changement 000104.
;
;    Signet............: MP-01-01-30_D104.
;
;    ------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 23 avril 2002
;    Description.......: Ajustements afin d'ajuoter une note de l'Office de langue française.
;
;    Référence.........: Demande de changement directe - Demandeur: Sylvain Boivin.
;
;    Signet............: MP-02-04-23_DD.
;
;-----------------------------------------------------------------------------------------------------------------------------------


USE ussetqzs NOLIST
SET PAGE LENGTH 60 WIDTH 250
SET FORMFEED

;------------------------------------------------------------------------------
;
ACCESS *WAR335F            &
  ;
  ; Adresse de facturation.
  ;
  LINK CIECLE OF WAR335F   &
    TO CIECLE              OF MCCOMPAGNIE               &

  LINK CIECLE OF WAR335F , &
       ENTCLE OF WAR335F   &
    TO CIECLE            , &
       ENTCLE              OF ARENTREPOT                &

  LINK CIECLE OF WAR335F , &
       ENTCLE OF WAR335F , &
       PRDCLE OF WAR335F   &
    TO CIECLE            , &
       ENTCLE            , &
       PRDCLE              OF VPRE_PRD                  &

  LINK FOUCIECLE OF WAR335F, &
       REFCLETYP OF WAR335F, &
       FOUCLE    OF WAR335F, &
       RADCLE    OF WAR335F  &
    TO REFCIECLE           , &
       REFCLETYP           , &
       REFCLENUM           , &
       RADCLE                OF MCREF_ADRESSE   OPTIONAL &

  LINK FOCTERESCACH          &
    TO TERCLE                OF VTER_DSC        OPTIONAL &

  LINK ("CMDTRP",                                        &
         CDETRP OF WAR335F)                              &
      TO XELNOM,                                         &
         CBICLE OF VCBI_DSC  ALIAS A_CBI_CDETRP OPTIONAL &

  LINK ("CMDFAB",                                        &
         CDEFAB OF WAR335F)                              &
      TO XELNOM,                                         &
         CBICLE OF VCBI_DSC  ALIAS A_CBI_CDEFAB OPTIONAL &

  LINK   DEVCLE OF WAR335F                               &
      TO DEVCLE OF MCDEVISE   OPTIONAL                   &

  LINK   FOCTERNET OF WAR335F                            &
      TO TERCLE    OF CPTERME OPTIONAL                   &

  LINK   CIECLE,                                         &
         CMDCLE,                                         &
         CMDAJT ,                                        &
         CDECLE  OF WAR335F                              &
  TO     CIECLE,                                         &
         CMDCLE ,                                        &
         CMDAJT ,                                        &
         CDECLE OF ARCMD_DETAIL OPTIONAL                 &

  LINK   FOUCIECLE            ,                          &
         FOUCLE OF WAR335F                            &
  TO     FOUCIECLE            ,                          &
         FOUCLE OF CPFOURNISSEUR

; MP/Ajout pour qu'on puisse afficher la description du terme en français
; ou en anglais
DEFINE D_TERDSC_LNG CHARACTER SIZE 40 = TERDSCFRA OF CPTERME &
                                     IF FOULNG OF CPFOURNISSEUR = "F" &
                                     ELSE TERDSCANG OF CPTERME

DEFINE D_PRDUNI CHARACTER SIZE 4  = PRDUNI OF ARCMD_DETAIL

DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = "1" + CMDCLE OF WAR335F      &
                                     IF CMDCLE OF WAR335F[1:1] < "8" &
                                   ELSE "0" + CMDCLE OF WAR335F

SORTED ON CIECLE   OF WAR335F            &
       ON D_CMDCLE_TRI                   &
       ON CMDAJT   OF WAR335F RESET PAGE &
       ON CDECLE   OF WAR335F            &
       ON CDNSEQ   OF WAR335F


DEFINE D_COPIE_FLG CHARACTER SIZE 01 = PARM

DEFINE D_COPIE     CHARACTER SIZE 20 = "COPIE MAGASIN"     &
                                    IF  D_COPIE_FLG = "0"  &
                                  ELSE "COPIE FOURNISSEUR"

DEFINE D_CIENOM    CHARACTER SIZE 30 = CIENOM OF MCCOMPAGNIE[1:30]

DEFINE D_CBIDSC_CMDTRP CHARACTER SIZE 30 = CBIDSC OF A_CBI_CDETRP

DEFINE D_CBIDSC_CMDFAB CHARACTER SIZE 30 = CBIDSC OF A_CBI_CDEFAB

DEFINE D_CMDFABLIB2 CHARACTER SIZE 4 = "T.C:" &
                                       IF FOULNG OF WAR335F = "F" ELSE &
                                       "C.T:"

DEFINE D_REPIMP CHARACTER SIZE 5 = "(" + ASCII(CMDNBRIMP OF WAR335F + 1,3) + ")" &
                                   IF G_REIIMP OF WAR335F = "1"  ELSE &
                                    "(" + ASCII(CMDNBRDUP OF WAR335F + 1,3) + ")" &
                                   IF G_DUPIMP OF WAR335F = "1" ELSE ""

DEFINE D_ADRCIE CHARACTER SIZE 170 = TRUNCATE(CIENOM   OF MCCOMPAGNIE) + ", " + &
                                     TRUNCATE(CIEADR1  OF MCCOMPAGNIE) + ", " + &
                                     TRUNCATE(CIEADR2  OF MCCOMPAGNIE) + ", " + &
                                     TRUNCATE(CIEADR3  OF MCCOMPAGNIE) + ", " + &
                                     TRUNCATE(CIEADR4  OF MCCOMPAGNIE) + ", " + &
                                     TRUNCATE(CIECP    OF MCCOMPAGNIE) + ", " + &
                          "TÉL.: " + TRUNCATE(CIETEL   OF MCCOMPAGNIE)        + &
                          " FAX: " + TRUNCATE(CIEFAX   OF MCCOMPAGNIE)

DEFINE DV_EFF_LIG_TOT VARCHAR SIZE 53 = "*******.** |*******.** |*******.** |***********.**" &
                                     IF T_IND_TOT OF WAR335F = 0                             &
                                   ELSE ""

USE usflgdet NOLIST  ; Flag Bilingue (O/N)

DEFINE D_INDDNE    CHARACTER SIZE 03 = DZ_FLGAFFO      IF CMDINDDNE  OF WAR335F = "1" &
                                       ELSE DZ_FLGAFFN IF CMDINDDNE  OF WAR335F = "0"


; ---------------------------------------------------------------
; MP-01-01-30_D104.

define dv_eff_ite varchar size 171 = "       |            |         |       |                                                  |          |              |              |           |           |              " &
                                  if d_copie = "COPIE FOURNISSEUR" &
                                else ""

; ---------------------------------------------------------------
; MP-02-04-23_DD.

define d_rappel_notice character size 162 = "Rappel: Tout texte figurant sur le produit et son emballage et toute documentation relative au produit ou au service doivent au moins avoir une version française." &
                                         IF FOULNG OF WAR335F = "F" &
                                       ELSE "Notice: All inscriptions on products packaging, documentation related to product or service must include a French version.                                        "

; ---------------------------------------------------------------


PAGE HEADING  &
  TAB 001 " " &
SKIP 2 &
  TAB 076 "B O N   D E   C O M M A N D E" &
  TAB 155 "PAGE :"                        &
  TAB 162 SYSPAGE              PIC '^^^'  &
  TAB 166 '/'                             &
  TAB 167 T_NBR_PAG OF WAR335F PIC '^^^'  &
SKIP 1 &
  TAB 149 "DATE (AMJ) :"       &
  TAB 162 SYSDATE              &
          PICTURE "^^^^/^^/^^" &
SKIP 1 &
  TAB 001 "                   -------------------------------------------------------"                          &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1 &
  TAB 001 "                   | NO.     |        NUMÉROS DE          | CONDITIONS PAIEMENT:"                    & ; MP-00-03-10_S16.
  ;TAB 082 FOCTERNET  OF WAR335F                                                                                &
  ;TAB 087 TERDSCFRA  OF CPTERME PIC "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  TAB 081 D_TERDSC_LNG PIC "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  TAB 121 "| DOUANES ET COURTAGE INCLUS (" &
  TAB 151 D_INDDNE               &
  TAB 154 ")                  |" &
SKIP 1 &
  TAB 001 "                   | FOURN.  |    RÉQUISITIONS (1 à 5)    |                                       "  & ; MP-00-03-10_S16.
  TAB 121 "|                             "                                                                      &
  TAB 151 "   " &
  TAB 154 "                   |" &
SKIP 1 &
  TAB 001 "                   -----------                            ----------------" &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1 &
  TAB 001 "                   |" &
  TAB 022 FOUCLE OF WAR335F &
  TAB 030 "| (1)             (2)             (3)             (4)             (5)                      | FAB:"   & ; MP-00-03-10_S16.
  TAB 036 CMDREQCLETMP  OF WAR335F                                                                              & ; MP-00-03-10_S16.
  TAB 052 CMDREQCLETMP2 OF WAR335F                                                                              & ; MP-00-03-10_S16.
  TAB 068 CMDREQCLETMP3 OF WAR335F                                                                              & ; MP-00-03-10_S16.
  TAB 084 CMDREQCLETMP4 OF WAR335F                                                                              & ; MP-00-03-10_S16.
  TAB 100 CMDREQCLETMP5 OF WAR335F                                                                              & ; MP-00-03-10_S16.
  TAB 135 D_CBIDSC_CMDFAB                                                                                       &
  TAB 173 "|" &
SKIP 1 &
  TAB 001 "  ------------------------------------------------------------------------" &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1 &
  TAB 001 "  |   A :"       &
  TAB 011 RADNOM OF MCREF_ADRESSE &
  TAB 059 "| EXPEDIER A :"  &
  TAB 074 ADENOM OF WAR335F &
  TAB 121 "| VIA       :"   &
  TAB 135 D_CBIDSC_CMDTRP   &
  TAB 173 "|" &
SKIP 1 &
  TAB 001 "  |" &
  TAB 011 RADADR1 OF MCREF_ADRESSE &
  TAB 059 "|" &
  TAB 074 ADEADR1 OF WAR335F &
  TAB 121 "|                                                   |" &
SKIP 1 &
  TAB 001 "  |" &
  TAB 011 RADADR2 OF MCREF_ADRESSE &
  TAB 059 "|" &
  TAB 074 ADEADR2 OF WAR335F &
  TAB 121 "-----------------------------------------------------" &
SKIP 1 &
  TAB 001 "  |"                      &
  TAB 011 RADADR3   OF MCREF_ADRESSE &
  TAB 059 "|"                        &
  TAB 074 ADEADR3   OF WAR335F       &
  TAB 121 "| DEVISE    :"            &
  TAB 135 DEVCLE    OF WAR335F       &
  TAB 140 DEVDSCFRA OF MCDEVISE      &
  TAB 173 "|" &
SKIP 1 &
  TAB 001 "  |"                   &
  TAB 011 RADCP  OF MCREF_ADRESSE &
  TAB 020 RADTEL OF MCREF_ADRESSE &
  TAB 059 "|"                     &
  TAB 074 ADECP OF WAR335F        &
  TAB 121 "|"                     &
  TAB 173 "|"                     &
SKIP 1 &
  TAB 001 "  | A/S :" &
  TAB 011 RADCNT OF MCREF_ADRESSE &
  TAB 059 "|" &
  TAB 121 "|" &
  TAB 173 "|" &
SKIP 1 &
  TAB 001 "  ------------------------------------------------------------------------" &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1 &
  TAB 001 "  | NO    | NO         |         |       |                                                  "        & ; MP-00-03-29_DD999999.
  TAB 093 "| DATE     |   PRIX       |    MONTANT   |           |           |              |"                   & ; MP-00-03-29_DD999999.
SKIP 1 &
  TAB 001 "  | ITEM  | INVENTAIRE |   QTE   | UNITE | DESCRIPTION                                      "        & ; MP-00-03-29_DD999999.
  TAB 093 "| REQUISE  |   UNITAIRE   |    NET       |    TPS    |    TVQ    |    TOTAL     |"                   & ; MP-00-03-29_DD999999.
SKIP 1 &
  TAB 001 "  ------------------------------------------------------------------------" &
  TAB 075 "---------------------------------------------------------------------------------------------------"


HEADING AT CDECLE                   &
  TAB 001 "  |"                     &
  TAB 004 CDECLE OF WAR335F         &
  TAB 011 "|"                       &
  TAB 012 PRDCLE OF WAR335F         &
  TAB 024 "|"                       &
  TAB 025 CDEQTECOM OF WAR335F      &
          PICTURE "^^,^^^.^^ "      &
          OUTPUT SCALE -1           &
          SIGNIF 5                  &
  TAB 034 "|"                       &
  TAB 036 D_PRDUNI                  &
  TAB 042 "|"                       &
  TAB 043 CDEDSCPRD OF WAR335F      & ; MP-00-03-29_DD999999.
  TAB 093 "|"                       & ; MP-00-03-29_DD999999.
  TAB 094 CDEDATLIVPRE OF WAR335F   & ; MP-00-03-29_DD999999.
          PICTURE "^^^^/^^/^^"      & ; MP-00-03-29_DD999999.
  TAB 104 "|"                       & ; MP-00-03-29_DD999999.
  TAB 105 CDEPRIUNI OF WAR335F      & ; MP-00-03-29_DD999999.
          PICTURE "^,^^^,^^^.^^^^ " & ; MP-00-03-29_DD999999.
          SIGNIF 7                  & ; MP-00-03-29_DD999999.
  TAB 119 "|"                       & ; MP-00-03-29_DD999999.
  TAB 120 CDEMNTNET OF WAR335F      & ; MP-00-03-29_DD999999.
          PICTURE "^^,^^^,^^^.^^ "  & ; MP-00-03-29_DD999999.
          SIGNIF 5                  & ; MP-00-03-29_DD999999.
  TAB 134 "|"                       & ; MP-00-03-29_DD999999.
  TAB 135 CDEMNTTPS OF WAR335F      & ; MP-00-03-29_DD999999.
          PICTURE "^^^,^^^.^^ "     & ; MP-00-03-29_DD999999.
          SIGNIF 5                  & ; MP-00-03-29_DD999999.
  TAB 146 "|"                       & ; MP-00-03-29_DD999999.
  TAB 147 CDEMNTTVQ OF WAR335F      & ; MP-00-03-29_DD999999.
          PICTURE "^^^,^^^.^^ "     & ; MP-00-03-29_DD999999.
          SIGNIF 5                  & ; MP-00-03-29_DD999999.
  TAB 158 "|"                       & ; MP-00-03-29_DD999999.
  TAB 159 CDEMNTTOT OF WAR335F      & ; MP-00-03-29_DD999999.
          PICTURE "^^,^^^,^^^.^^ "  & ; MP-00-03-29_DD999999.
          SIGNIF 5                  & ; MP-00-03-29_DD999999.
  TAB 173 "|"

REPORT                                                                                        &
  TAB 001 "  |       |            |         |       |"                                        &
  TAB 043 CDNDSC OF WAR335F                                                                   &
  TAB 093 "|          |              |              |           |           |              |"    ; MP-00-03-29_DD999999.


PAGE FOOTING                          &

  ; ---------------------------------------------------------------
  ; MP-01-01-30_D104.

         tab 001 "  |"                & 
         tab 004 dv_eff_ite           &
         tab 173 "|"                  &
    skip 1                            &
         tab 001 "  |items:"          &
         tab 011 t_ite_uc1 of WAR335F &
         tab 004 dv_eff_ite           &
         tab 173 "|"                  &
    skip 1                            &
         tab 001 "  |"                &
         tab 011 t_ite_uc2 of WAR335F &
         tab 170 t_ite_uc3 of WAR335F &
         tab 004 dv_eff_ite           &
         tab 173 "|"                  &
    skip 1                            &
  ; ---------------------------------------------------------------

  TAB 001 "  ------------------------------------------------------------------------" &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1                                &
  TAB 001 "  | NO TAXE FEDERALE :"    &
  TAB 024 CIEENRTPS OF MCCOMPAGNIE    &
  TAB 042 "| NO TAXE PROVINCIALE :"   &
  TAB 066 CIEENRTVQ OF MCCOMPAGNIE    &
  TAB 111 "TOTAUX"                    & ; MP-00-03-29_DD999999.
  TAB 119 "|"                         & ; MP-00-03-29_DD999999.
  TAB 120 CDEMNTNET OF WAR335F        & ; MP-00-03-29_DD999999.
          SUBTOTAL AT CDECLE          & ; MP-00-03-29_DD999999.
          RESET AT CMDAJT             & ; MP-00-03-29_DD999999.
          PICTURE "^^,^^^,^^^.^^ "    & ; MP-00-03-29_DD999999.
          SIGNIF 5                    & ; MP-00-03-29_DD999999.
  TAB 134 "|"                         & ; MP-00-03-29_DD999999.
  TAB 135 CDEMNTTPS OF WAR335F        & ; MP-00-03-29_DD999999.
          SUBTOTAL AT CDECLE          & ; MP-00-03-29_DD999999.
          RESET AT CMDAJT             & ; MP-00-03-29_DD999999.
          PICTURE "^^^,^^^.^^ "       & ; MP-00-03-29_DD999999.
          SIGNIF 5                    & ; MP-00-03-29_DD999999.
  TAB 146 "|"                         & ; MP-00-03-29_DD999999.
  TAB 147 CDEMNTTVQ OF WAR335F        & ; MP-00-03-29_DD999999.
          SUBTOTAL AT CDECLE          & ; MP-00-03-29_DD999999.
          RESET AT CMDAJT             & ; MP-00-03-29_DD999999.
          PICTURE "^^^,^^^.^^ "       & ; MP-00-03-29_DD999999.
          SIGNIF 5                    & ; MP-00-03-29_DD999999.
  TAB 158 "|"                         & ; MP-00-03-29_DD999999.
  TAB 159 CDEMNTTOT OF WAR335F        & ; MP-00-03-29_DD999999.
          SUBTOTAL AT CDECLE          & ; MP-00-03-29_DD999999.
          RESET AT CMDAJT             & ; MP-00-03-29_DD999999.
          PICTURE "^^,^^^,^^^.^^ "    & ; MP-00-03-29_DD999999.
          SIGNIF 5                    & ; MP-00-03-29_DD999999.
  TAB 173 "|"                         &
  TAB 122 DV_EFF_LIG_TOT              &
SKIP 1 &
  TAB 001 "  ------------------------------------------------------------------------"   &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1 &
  TAB 001 "  | CONDITIONS:                                                             " &
  TAB 077 "|  NOTE:                               "                                      &
  TAB 119 "| IMPORTANT:              |                           |"                      & ; MP-00-03-29_DD999999.
SKIP 1 &
  TAB 001 "  | 1. Les termes et conditions de cette commande sont présumés acceptés    " &
  TAB 077 "|  Veuillez indiquer distinctement     "                                      &
  TAB 119 "| Indiquez le numéro de   |                           |"                      &
SKIP 1 &
  TAB 001 "  |    à moins d'avis contraire du fournisseur dans les 10 jours.           " &
  TAB 077 "|  sur vos factures tous montants      "                                      &
  TAB 119 "| commande ci-dessous sur |                           |"                      & ; MP-00-03-29_DD999999.
SKIP 1 &
  TAB 001 "  | 2. Aucun changement ou substitution sans notre autorisation.            " &
  TAB 077 "|  de taxes ainsi que vos numéros      "                                      &
  TAB 119 "| les listes d'emballage, |                           |"                      & ; MP-00-03-29_DD999999.
  TAB 147 "________________________"                                                     & ; MP-00-03-10_S18.
           PRINT AT CMDAJT                                                               & ; MP-00-03-10_S18.
SKIP 1 &
  TAB 001 "  | 3. Nous nous réservons le droit d'annuler si l'expédition ne            " &
  TAB 077 "|  d'inscriptions.                     "                                      &
  TAB 119 "| documents d'expédition  |                           |"                      &
  TAB 150 "Signature autorisée"                                                          & ; MP-00-03-10_S18.
           PRINT AT CMDAJT                                                                                      & ; MP-00-03-10_S18.
SKIP 1 &
  TAB 001 "  |    correspond pas aux quantités, prix, et délai de livraison stipulés.  " &
  TAB 077 "|                                      "                                      &
  TAB 119 "| et factures.            |                           |"                      & ; MP-00-03-29_DD999999.
SKIP 1 &
  TAB 001 "  ------------------------------------------------------------------------"   &
  TAB 075 "---------------------------------------------------------------------------------------------------" &
SKIP 1 &
  TAB 004 D_COPIE            &
  TAB 120 "NO DE COMMANDE:"  &
  TAB 136 CMDCLE OF WAR335F  &
  TAB 150 "CHANGEMENT:"      &
  TAB 162 CMDAJT OF WAR335F  &
SKIP &
  TAB 120 "NO D'ENTREPOT :"  &
  TAB 136 ENTCLE OF WAR335F  &
SKIP 1 &
  TAB 004 D_ADRCIE           &
SKIP 2 &                       ; MP-02-04-23_DD.
  TAB 004 d_rappel_notice    & ; MP-02-04-23_DD.
SKIP 2


FOOTING AT CMDAJT SKIP PAGE


BUILD ar335z


@IF FORMAT

                                                                           B O N   D E   C O M M A N D E                                                  PAGE : 99
                                                                                                                                                    DATE (AMJ) : 9999/99/99
                   ----------------------------------------------------------------------------------------------------------------------------------------------------------
                   | NO.     |        NUMÉROS DE          | CONDITIONS  DE  PAIEMENT   :                                | DOUANES ET COURTAGE INCLUS (   )                  |
                   | FOURN.  |    RÉQUISITIONS (1 à 5)    | SUIVANT MILIEU OU FIN MOIS FACTURATION                      |                 NON INCLUS (   )                  |
                   -----------                            -------------------------------------------------------------------------------------------------------------------
                   |         | (1) xxxxxxxxxx  (2) xxxxxxxxxx  (3) xxxxxxxxxx  (4) xxxxxxxxxx  (5) xxxxxxxxxx           | FAB       :                                       |
  ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------
  |   A :                                                 | EXPEDIER A :                                                | TRANSPORT :                                       |
  |                                                       |                                                             |                                                   |
  |                                                       |                                                             -----------------------------------------------------
  |                                                       |                                                             | VIA       :                                       |
  |                                                       |                                                             -----------------------------------------------------
  | A/S :                                                 |                                                             | DEVISE    :                                       |
  ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------
  | NO    | NO         |         |       |                                                  | DATE     |   PRIX       |   MONTANT    |           |           |              |
  | ITEM  | INVENTAIRE |   QTE   | UNITE | DESCRIPTION                                      | REQUISE  |   UNITAIRE   |   NET        |    TPS    |    TVQ    |    TOTAL     |
  ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |999.999|999999999999| 9,999.99| XXXXX |XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX|9999/99/99|9,999,999.9999|99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  |                                                                                                                                                                         |
  |items:   1,2,3: 9999-999999  4,5,6: 9999-999999  7,8,9: 9999-999999  10,11,12,13,14,15: 9999-999999  16,17,18: 9999-999999  19,20,21: 9999-999999  22,23: 9999-999999    |
  |         24,25,26: 9999-999999  27,28,29,30: 9999-999999  31,32,33: 9999-999999  34,35,36,37,38,39,40,41,42,43,44,45,46,47,48: 9999-999999  49,50,51,52: 9999-999999  ...|
  ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------
  | NO TAXE FEDERALE : XXXXXXXXXXXXXXX   | NO TAXE PROVINCIALE : XXXXXXXXXXXXXXXXX                            TOTAUX  |99,999,999.99 |999,999.99-|999,999.99-|99,999,999.99-|
  ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------
  | CONDITIONS:                                                             |  NOTE:                                  | IMPORTANT:              |                           |
  | 1. Les termes et conditions de cette commande sont présumés acceptés    |  Veuillez indiquer distinctement        | Indiquez le numéro de   |                           |
  |    à moins d'avis contraire du fournisseur dans les 10 jours.           |  sur vos factures tous montants         | commande ci-dessous sur |                           |
  | 2. Aucun changement ou substitution sans notre autorisation.            |  de taxes ainsi que vos numéros         | les listes d'emballage, | _________________________ |
  | 3. Nous nous réservons le droit d'annuler si l'expédition ne            |  d'inscriptions.                        | documents d'expédition  |    Signature autorisée    |
  |    correspond pas aux quantités, prix, et délai de livraison stipulés.  |                                         | et factures.            |                           |
  ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------
   COPIE FOURNISSEUR                                                                                                   NO DE COMMANDE: 999999999     CHANGEMENT: 999
                                                                                                                       NO D'ENTREPOT : 99
   GRANICOR INC., 300 ROTTERDAM, SAINT-AUGUSTIN, QUEBEC, CANADA, G3A 1T4, TÉL.: (418)878-3530 FAX: (418)878-3208

   Rappel: Tout texte figurant sur le produit et son emballage et toute documentation relative au produit ou au service doivent au moins avoir une version française.
@ENDIF
