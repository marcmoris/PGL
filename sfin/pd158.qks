;/T/ Raison d'arrêt (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-07-13
;
;    Description..: Ce panorama permet de saisir les raisons d'arrêt.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd158 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD158"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd158.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour les dépisteurs (pop-up)
;

TEMPORARY T_RAACODARR CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Raison d'arrêt
;

FILE PDRAISON_ARRET PRIMARY NOITEM
  ACCESS VIA     CIECLE,   &
                 RAACODARR &
         USING   T_CIECLEMNU,                &
                 RAACODARR OF PDRAISON_ARRET &
         REQUEST RAACODARR OF PDRAISON_ARRET &
         ORDERBY CIECLE,   &
                 RAACODARR

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 RAACODARR

         ITEM CIECLE OF PDRAISON_ARRET INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Raison d'arrêt " &
@ELSE
      " %%%Raison d'arrêt " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD RAACODARR OF PDRAISON_ARRET           &
        HIDDEN ID 05                        &
        REQUIRED NOCHANGE                   &
        LOOKUP NOTON PDRAISON_ARRET         &
          VIA   CIECLE,                     &
                RAACODARR                   &
          USING T_CIECLEMNU,                &
                RAACODARR OF PDRAISON_ARRET &
          MESSAGE 2909 ; Cette raison existe déjà.


SKIP 1


FIELD RAADSCFRA    OF PDRAISON_ARRET        &
        HIDDEN ID 10                        &
        REQUIRED


SKIP 1


FIELD RAADSCANG    OF PDRAISON_ARRET        &
        HIDDEN ID 15                        &
        REQUIRED                            &
        DEFAULT RAADSCFRA OF PDRAISON_ARRET


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs raison d'arret
;

PROCEDURE INPUT RAACODARR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RAACODARR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd114 MODE F &
                     PASSING T_RAACODARR, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RAACODARR )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRAISON_ARRET &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRAISON_ARRET &
     AND NOT NEWRECORD     OF PDRAISON_ARRET &
     AND NOT DELETEDRECORD OF PDRAISON_ARRET &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************** Raison d'arrêt ********************************
*                                                                              *
* Code arrêt....: xxxx                                                         *
*                                                                              *
* Descr français: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Descr anglais.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
