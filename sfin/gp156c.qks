;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Détail du format de présentation comptable - Formule
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp156c ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPFORMATS  , &
                        GPFOR_DETAILS

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "GP156C"            ; Nom de l'écran

USE gp156c.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


FILE GPFORMATS              MASTER

FILE GPFOR_DETAILS          MASTER


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_SOMME            PRIMARY &
                            NOITEMS &
                            OCCURS 48

  ACCESS  VIA     FRMCLE, &
                  FRDCLE, &
                  FRSREFLIG &
          USING   FRMCLE    OF GPFOR_DETAILS, &
                  FRDCLE    OF GPFOR_DETAILS, &
                  FRSREFLIG OF GPFRD_SOMME    &
          REQUEST FRSREFLIG &
          ORDERBY FRDCLE, &
                  FRSREFLIG

  ACCESS  VIA     FRMCLE, &
                  FRDCLE  &
          USING   FRMCLE    OF GPFOR_DETAILS, &
                  FRDCLE    OF GPFOR_DETAILS  &
          ORDERBY FRDCLE, &
                  FRSREFLIG

  ITEM FRMCLE OF GPFRD_SOMME INITIAL FRMCLE OF GPFOR_DETAILS
  ITEM FRDCLE OF GPFRD_SOMME INITIAL FRDCLE OF GPFOR_DETAILS

;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_DOMAINE          DESIGNER &
                            ALIAS GPFRD_DOMAINE_3


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_DOMAINE          DESIGNER &
                            ALIAS GPFRD_DOMAINE_4


FILE GPFRD_SOMME            DESIGNER &
                            ALIAS GPFRD_SOMME_2

;--------------------------------------------------------------------------
; Validation du type de format de présentation et lecture de sa description
;--------------------------------------------------------------------------
DEFINE    D_FRDTYP CHARACTER SIZE 16 = "FRDTYP"
TEMPORARY T_FRDTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_FRDTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_FRDTYP, &
                FRDTYP OF GPFOR_DETAILS


;-------------------------------------------------------------------------------
;
TEMPORARY T_CPTSOM     INTEGER SIZE 4
TEMPORARY T_FRMCLE   CHARACTER SIZE 4
TEMPORARY T_FRDCLE   CHARACTER SIZE 5

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Détail du format de présentation - Formule " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19)(,,25)

FIELD FRMCLE OF GPFORMATS                             &
      HIDDEN                                          &
      DISPLAY

FIELD FRMDSC OF GPFORMATS                             &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
"Ligne   Type  Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

SKIP

ALIGN (,,3) (,,12) (,,17)

FIELD FRDCLE OF GPFOR_DETAILS                         &
      DISPLAY

FIELD FRDTYP OF GPFOR_DETAILS                         &
      DISPLAY

FIELD FRDDSC OF GPFOR_DETAILS                         &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
"Ligne  Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
"Ligne  Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,23

TITLE &
@IF FRANCAIS
"Ligne  Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,43

TITLE &
@IF FRANCAIS
"Ligne  Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,63

SKIP

ALIGN (3,3,3) (,,14)

CLUSTER OCCUR WITH GPFRD_SOMME VERTICAL AT 11,1 FOR 1,20

FIELD FRSREFLIG OF GPFRD_SOMME                        &
      HIDDEN LABEL " "                                &
      REQUIRED NOCHANGE                               &
      LOOKUP ON GPFRD_DOMAINE_4                       &
                VIA   FRMCLE,                         &
                      FRDCLE                          &
                USING FRMCLE    OF GPFRD_SOMME,       &
                      FRSREFLIG OF GPFRD_SOMME        &
                MESSAGE 874,                          &
          NOTON GPFRD_SOMME                           &
                VIA   FRMCLE,                         &
                      FRDCLE,                         &
                      FRSREFLIG                       &
                USING FRMCLE    OF GPFRD_SOMME,       &
                      FRDCLE    OF GPFRD_SOMME,       &
                      FRSREFLIG OF GPFRD_SOMME        &
                MESSAGE 875

FIELD FRSOPE    OF GPFRD_SOMME                        &
      REQUIRED NOCHANGE

CLUSTER

DRAW 09,01 TO 09,80
DRAW 09,20 TO 23,20
DRAW 09,40 TO 23,40
DRAW 09,60 TO 23,60

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT FRSREFLIG
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FRDCLE = " "
    LET T_FRMCLE = FRMCLE OF GPFOR_DETAILS
    RUN SCREEN gp111 MODE F PASSING T_FRMCLE, T_FRDCLE
    LET FIELDTEXT = TRUNCATE(T_FRDCLE)
  END
END

PROCEDURE EDIT FRSREFLIG
BEGIN
   IF FIELDVALUE >= FRDCLE OF GPFOR_DETAILS
   THEN ERROR 886
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;PROCEDURE INTERNAL MAJ_FORMULE
;BEGIN
;   WHILE RETRIEVING GPFRD_DOMAINE_3 &
;         VIA    FRMCLE,           &
;                FRDCLE,           &
;                FDOREF            &
;         USING  FRMCLE    OF GPFRD_SOMME,   &
;                FRDCLE    OF GPFRD_SOMME,   &
;                FRSREFLIG OF GPFRD_SOMME
;   BEGIN
;
;      LET FDOOPE    OF GPFRD_DOMAINE_3 = FRSOPE    OF GPFRD_SOMME
;      PUT GPFRD_DOMAINE_3
;   END
;END


PROCEDURE INTERNAL AJOUT_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_4 &
         VIA    FRMCLE,             &
                FRDCLE              &
         USING  FRMCLE    OF GPFRD_SOMME, &
                FRSREFLIG OF GPFRD_SOMME
   BEGIN
           LET FRMCLE    OF GPFRD_DOMAINE_3 = FRMCLE      OF GPFRD_DOMAINE_4
           LET FRDCLE    OF GPFRD_DOMAINE_3 = FRDCLE      OF GPFRD_SOMME
           LET FDOREF    OF GPFRD_DOMAINE_3 = FRSREFLIG   OF GPFRD_SOMME
           LET FDOCLE    OF GPFRD_DOMAINE_3 = FDOCLE      OF GPFRD_DOMAINE_4
           LET FDOSEQ    OF GPFRD_DOMAINE_3 = FDOSEQ      OF GPFRD_DOMAINE_4
           LET FRDTYP    OF GPFRD_DOMAINE_3 = "F"
           LET FDOTYP    OF GPFRD_DOMAINE_3 = FDOTYP      OF GPFRD_DOMAINE_4
           LET FDOCPTDEB OF GPFRD_DOMAINE_3 = FDOCPTDEB   OF GPFRD_DOMAINE_4
           LET FDOCPTFIN OF GPFRD_DOMAINE_3 = FDOCPTFIN   OF GPFRD_DOMAINE_4

           IF  FRSOPE OF GPFRD_SOMME     = "+"
           THEN LET FDOOPE    OF GPFRD_DOMAINE_3 = FDOOPE OF GPFRD_DOMAINE_4
           ELSE BEGIN
                IF FDOOPE OF GPFRD_DOMAINE_4 = "+"
                THEN LET FDOOPE OF GPFRD_DOMAINE_3 = "-"
                ELSE LET FDOOPE OF GPFRD_DOMAINE_3 = "+"
           END
           PUT GPFRD_DOMAINE_3 RESET
   END
END


PROCEDURE INTERNAL DETRUIT_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_4 &
         VIA    FRMCLE,             &
                FRDCLE,             &
                FDOREF              &
         USING  FRMCLE    OF GPFRD_SOMME, &
                FRDCLE    OF GPFRD_SOMME, &
                FRSREFLIG OF GPFRD_SOMME
   BEGIN
     DELETE GPFRD_DOMAINE_4
     PUT GPFRD_DOMAINE_4
   END

   LET T_CPTSOM = 0
   WHILE RETRIEVING GPFRD_DOMAINE_4   &
         VIA   FRMCLE,                &
               FRDCLE                 &
         USING FRMCLE OF GPFRD_SOMME, &
               FRDCLE OF GPFRD_SOMME
   BEGIN
      LET T_CPTSOM = T_CPTSOM + 1
   END

   IF T_CPTSOM = 0
   THEN DELETE GPFOR_DETAILS
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GPFRD_SOMME
  BEGIN
     ;
     ; Validation de la destruction
     ;
     IF     DELETEDRECORD OF GPFRD_SOMME         &
        AND TZ_SECDEL = "0"
     THEN ERROR 1
     ;
     ; Validation de la modification.
     ;
     IF     ALTEREDRECORD     OF GPFRD_SOMME         &
        AND NOT NEWRECORD     OF GPFRD_SOMME         &
        AND NOT DELETEDRECORD OF GPFRD_SOMME         &
        AND TZ_SECMOD = "0"
     THEN ERROR 2

     IF DELETEDRECORD OF GPFRD_SOMME
     THEN BEGIN
          GET GPFRD_SOMME_2 &
              VIA   FRMCLE,     &
                    FRSREFLIG   &
              USING FRMCLE OF GPFRD_SOMME, &
                    FRDCLE OF GPFRD_SOMME OPTIONAL
          IF ACCESSOK
          THEN ERROR 872
     END

;     IF     ALTEREDRECORD OF GPFRD_SOMME   AND &
;        NOT NEWRECORD     OF GPFRD_SOMME   AND &
;        NOT DELETEDRECORD OF GPFRD_SOMME
;     THEN DO MAJ_FORMULE

     IF NEWRECORD     OF GPFRD_SOMME
     THEN DO AJOUT_FORMULE

     IF DELETEDRECORD OF GPFRD_SOMME
     THEN DO DETRUIT_FORMULE
  END
END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************** Détail du format de présentation - Formule ******************
*                                                                              *
* Format........: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                         *
*                                                                              *
* Ligne   Type  Description                                                    *
* xxxxx    x    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
********************************************************************************
* Ligne  Opérateur *  Ligne  Opérateur *  Ligne  Opérateur *  Ligne  Opérateur *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
* xxxxx      x     *  xxxxx      x     *  xxxxx      x     *  xxxxx      x     *
********************************************************************************
@ENDIF

