;/T/ Menu Grand Livre consolidé
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 15 avril 1993
;
;    Description..: Menu principal du module Grand Livre consolidé.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN glm11 MENU &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES CHANGE &
             RECEIVING T_FLGHLDOK, &
                       D_CIENOM

USE ussectmp NOLIST
    "GLM11"

USE glm11.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE  1 ; Si le holding est présent.
DEFINE    D_CIENOM   CHARACTER SIZE 40 ; Nom du holdgin

;
; Pour valider la periode comptable.
;
FILE MCPERIODE_CTB    DESIGNER &
                      OPEN READ

;
; Information sur le holdgin.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu.
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Période du menu
TEMPORARY T_PECDATDEB DATE      RESET AT STARTUP
TEMPORARY T_PECDATFIN DATE      RESET AT STARTUP

;
TEMPORARY T_PECCLECOU CHARACTER SIZE 4  RESET AT STARTUP ; Période courante

TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les périodes

;
; La variable suivante indique que nous somme dans le menu du Grand
; Livre du consolidé.
;
TEMPORARY T_FLGMNU    CHARACTER SIZE 1 INITIAL "H" RESET AT STARTUP

;
; Variable passer à l'écran GL020.
;
TEMPORARY T_PECCLEFND CHARACTER SIZE 4
TEMPORARY T_LGLCLEFND CHARACTER SIZE 4
;
; Variable passer à l'écran GL001
;
TEMPORARY T_ECJCLENUMFND CHARACTER SIZE 8
TEMPORARY T_ECJCLESEQFND CHARACTER SIZE 2
;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "GLH@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "GLP@" ; Code pour les procédures


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST ; Hilite pour tout les écrans

DRAW 1,18 TO 3,63
DRAW 2,1  TO 2,18
DRAW 2,63 TO 2,80

DRAW 2,1  TO 7,1
DRAW 7,1  TO 7,80
DRAW 2,80 TO 7,80

DRAW 5,1  TO 5,80

DRAW 7,40 TO 19,40
DRAW 19,1 TO 19,80

TITLE &
@IF FRANCAIS
      "G R A N D   L I V R E   C O N S O L I D E" &
@ELSE
      "G E N E R A L   L E D G E R   C O N S O L I D A T E" &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

FIELD D_CIENOM FIXED NOLABEL DATA AT 4,21

SKIP 1

ALIGN(4,4,13)(,30,42)(,58,68)
FIELD T_PECCLEMNU HIDDEN ID 05 &
        FIXED            &
@IF FRANCAIS
        LABEL "Période:" &
@ELSE
        LABEL "Period.:" &
@ENDIF
        PICTURE "^^-^^" &
        LOOKUP ON MCPERIODE_CTB &
          VIA PECCLE &
          USING T_PECCLEMNU  &
          MESSAGE 222 ; Cette période n'existe pas


FIELD T_PECDATDEB FIXED FORMAT YYYYMMDD PATTERN "####/##/##"&
@IF FRANCAIS
        LABEL "Date début:"
@ELSE
        LABEL "Start Date:"
@ENDIF

FIELD T_PECDATFIN FIXED FORMAT YYYYMMDD PATTERN "####/##/##"&
@IF FRANCAIS
        LABEL "Date fin:"
@ELSE
        LABEL "End Date:"
@ENDIF


SKIP TO 9

ALIGN (2,2)
SUBSCREEN gl020 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_PECCLECOU, &
                        T_FLGMNU   , &
                        T_PECCLEFND, &
                        T_LGLCLEFND  &
@IF FRANCAIS
                LABEL "Gestion des lots      "
@ELSE
                LABEL "Batch Management      "
@ENDIF

SKIP 1

SUBSCREEN gl001 HIDDEN &
                PASSING T_CIECLEMNU    , &
                        T_CIENOM       , &
                        T_PECCLEMNU    , &
                        T_PECCLECOU    , &
                        T_FLGMNU       , &
                        T_ECJCLENUMFND , &
                        T_ECJCLESEQFND   &
@IF FRANCAIS
                LABEL "Journal général       "
@ELSE
                LABEL "General Journal       "
@ENDIF

SKIP TO 9

ALIGN (42,42)

SUBSCREEN glm02 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU     &
@IF FRANCAIS
                LABEL "Etats financiers                "
@ELSE
                LABEL "Financial Statements            "
@ENDIF


SKIP 1

SUBSCREEN gl004 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU     &
@IF FRANCAIS
                LABEL "Analyse de compte               "
@ELSE
                LABEL "Account Analysis                "
@ENDIF


SKIP TO 20

ALIGN(31,31)
SUBSCREEN glm15 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L, &
                        T_CIECLEMNU, &
                        T_PECCLEMNU, &
                        T_CIENOM   , &
                        T_PECDATDEB, &
                        T_PECDATFIN  &
@IF FRANCAIS
                LABEL " Listes et rapports "
@ELSE
                LABEL " Lists and Reports  "
@ENDIF

SUBSCREEN glm15 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P, &
                        T_CIECLEMNU, &
                        T_PECCLEMNU, &
                        T_CIENOM   , &
                        T_PECDATDEB, &
                        T_PECDATFIN  &
@IF FRANCAIS
                LABEL "    Procédures      "
@ELSE
                LABEL "    Procedures      "
@ENDIF


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le holding n'est pas défini on retourne l'usager au menu
  ; principal.
  ;
  IF T_FLGHLDOK = "0"
  THEN RETURN

  USE ussececr NOLIST
  ;
  ; La compagnie du menu contient les caractères de substitution dans le cas
  ; du consolidé.
  ;
  LET T_CIECLEMNU = HLDCHRSUB OF MCHOLDING
  LET T_CIENOM    = LJ(D_CIENOM)
  DISPLAY D_CIENOM

  ;
  ; On va chercher la periode courante du module G/L.
  ;
  GET MCPERIODE_CTB VIA PECSTUGL &
                    USING "C" &
                    OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 229 ; Il n'y a pas de période courante de définie.
  ELSE LET T_PECCLECOU = PECCLE OF MCPERIODE_CTB

  ACCEPT T_PECCLEMNU
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT T_PECCLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc105 PASSING T_PECCLE MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
  ;
  ; Si l'usager ne saisie pas de periode alors on prend la période courante
  ; par défaut.
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) AND &
     0 = SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLECOU
  ;
  ; On force la validation de la période.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLEMNU
END

;-------------------------------------------------------------------------------
;
;
; Validation du statut de la période.
;
;
PROCEDURE EDIT T_PECCLEMNU
BEGIN
  ;
  IF PECSTUGL OF MCPERIODE_CTB = "F"
  THEN ERROR 223 ; La période est fermée.
  ;
  IF PECSTUGL OF MCPERIODE_CTB <> "C"
  THEN WARNING 321 ; La période saisie n'est pas la courante.
END


;-------------------------------------------------------------------------------
;
;
; Conserve les dates de la période et les affiches.
;
;
PROCEDURE PROCESS T_PECCLEMNU
BEGIN
  LET T_PECDATDEB = PECDATDEB OF MCPERIODE_CTB
  LET T_PECDATFIN = PECDATFIN OF MCPERIODE_CTB
  DISPLAY T_PECDATDEB
  DISPLAY T_PECDATFIN
END

PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_PECCLEMNU
END

BUILD
@IF FORMAT


                 **********************************************
****************** G R A N D   L I V R E   C O N S O L I D E  ******************
*                **********************************************                *
*                   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
********************************************************************************
*  Période: xxxxx            Date début: xxxxxxxx          Date fin: xxxxxxxx  *
********************************************************************************
                                       *
 Gestion des lots                      * Etats financiers
                                       *
 Journal général                       * Analyse de compte
                                       *
                                       *
                                       *
                                       *
                                       *
                                       *
                                       *
********************************************************************************
                               Listes et rapports
                                  Procédures


@ENDIF
