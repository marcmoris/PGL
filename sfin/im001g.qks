;/T/ Immobilisation - Historique des achats / déboursés
;
;/P/ Programmeur..: Alain Côté
;    Date création: 23 février 1994
;
;/V3.02.00/ Alain Côté, 23 février 1994, Immobilisation
;
;    Description..: Cet écran sert à la consultation des factures,
;                   ajustements et paiement automatique lies à une
;                   immobilisation.
;
;                   Ces pièces à payer n'ont qu'un lien informatif
;                   avec l'immobilisation.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001g FROM 7,1 TO 23,80 &
              MODE LABEL "" AT 1,80 NOACTION &
              FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND , &
                         CHANGE &
              RECEIVING IMIMMOBILISATION , &
                        T_CIECLEMNU      , &
                        T_CIENOM

;
; Variables pour la sécurité.
;
USE ussectmp NOLIST
    "IM001G"

;
; Documentation de l'écran.
;
USE im001g.hlp NOLIST

USE ustrasse NOLIST       ;Transaction QUERY pour les sous-écran


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie de recherche
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP

;-------------------------------------------------------------------------------
;

FILE IMIMMOBILISATION MASTER

FILE CPCAP_IMM        PRIMARY &
                      OCCURS 12 &
                      NOITEM
  ACCESS VIA CIECLE , &
             IMMCLE   &
         USING CIECLE OF IMIMMOBILISATION , &
               IMMCLE OF IMIMMOBILISATION   &
         ORDERBY CIECLE    , &
                 IMMCLE    , &
                 CAPDAT    , &
                 FOUCIECLE , &
                 FOUCLE    , &
                 CAPCLE


;-------------------------------------------------------------------------------
;
; Pour l'appel de l'écrans CP001.
;
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4
TEMPORARY T_PECCLECOU CHARACTER SIZE 4
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4
TEMPORARY T_FOUCLEFND CHARACTER SIZE 6
TEMPORARY T_CAPCLEFND CHARACTER SIZE 15


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 01,01 TO 01,79
DRAW 01,01 TO 17,01
DRAW 17,01 TO 17,80
DRAW 02,80 TO 17,80

TITLE &
@IF FRANCAIS
      " Historique des achats / Déboursés " &
@ELSE
      " %%%Historique des achats / Déboursés " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

HILITE TITLE OFF

TITLE &
@IF FRANCAIS
"Période Lot Type No. Document      Date     Commentaire               Montant"&
@ELSE
"%%%iode Lot Type No. Document      Date     Commentaire               Montant"&
@ENDIF
      AT ,3

SKIP

ALIGN (4,3,4) (,,10) (,,15) (,,18) (,,34) (,,45) (,,66)

CLUSTER OCCURS WITH CPCAP_IMM ID BASE 01

FIELD PECCLE       OF CPCAP_IMM LABEL " " HIDDEN &
        DISPLAY

FIELD LCPCLE       OF CPCAP_IMM &
        DISPLAY

FIELD CAPTYPECR    OF CPCAP_IMM &
        DISPLAY

FIELD CAPCLE       OF CPCAP_IMM

FIELD CAPDAT       OF CPCAP_IMM  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY

FIELD CIMDSC       OF CPCAP_IMM &
        DISPLAY &
        SIZE 20

FIELD CIMMNT       OF CPCAP_IMM &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT CAPCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    IF   CAPTYPECR OF CPCAP_IMM = "FA" &
      OR CAPTYPECR OF CPCAP_IMM = "CR" &
      OR CAPTYPECR OF CPCAP_IMM = "NC" &
      OR CAPTYPECR OF CPCAP_IMM = "AJ"
    THEN BEGIN
      LET T_FOUCIECLE = FOUCIECLE OF CPCAP_IMM
      LET T_PECCLEMNU = ""
      LET T_PECCLECOU = ""
      LET T_FOUCLEFND = FOUCLE    OF CPCAP_IMM
      LET T_CAPCLEFND = CAPCLE    OF CPCAP_IMM
      RUN SCREEN cp003 MODE F &
                       PASSING T_FOUCIECLE, &
                               T_CIECLEMNU, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_FOUCLEFND, &
                               T_CAPCLEFND
    END
  END
  LET FIELDTEXT = ""
END

BUILD
@IF FORMAT

********************** Historique des achats / Déboursés **********************x
*                                                                              *
* Période Lot Type No. Document      Date   Commentaire                 Montant*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*

  xxxxx xxxx xx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx                                                              *
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*  xxxxx  xxxx xx  xxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx*
*                                                                              *
********************************************************************************
@ENDIF
