;/T/ Conciliation - Vérification
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 22 Juillet 1993
;
;    Description..: Affiche la date,l'heure et l'usager de création et de
;                   modification du relevé de conciliation.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp011a SLAVE &
              FROM 6,1 TO 14,80 &
              MESSAGE ON 24 &
              NOMODE &
              NOACTION FIELDMARK STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND &
              RECEIVING CPCONCIL_CHQ

USE cp011a.hlp

;-------------------------------------------------------------------------------
;
FILE CPCONCIL_CHQ            MASTER

;
; Pour afficher le nom de l'usager de création
;
FILE GSUSAGER         REFERENCE &
                      ALIAS A_USR_CRE
  ACCESS VIA USRCLE &
         USING CCLUSRCRE OF CPCONCIL_CHQ

;
; Pour afficher le nom de l'usager de modification
;
FILE GSUSAGER         REFERENCE &
                      ALIAS A_USR_MOD
  ACCESS VIA USRCLE &
         USING CCLUSRMOD OF CPCONCIL_CHQ

;-------------------------------------------------------------------------------
;
DRAW 1,1 TO 9,80

USE ushilite NOLIST     ; Hilite pour tout les écrans

TITLE &
@IF FRANCAIS
      " Vérification " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19) (,,32)
FIELD CCLDATCRE OF CPCONCIL_CHQ  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN ID 05 &
label "Créé le.......:"
FIELD CCLHRECRE OF CPCONCIL_CHQ

ALIGN(3,3,19) (,,32)
FIELD CCLUSRCRE OF CPCONCIL_CHQ HIDDEN ID 10 &
label "Créé par......:"
FIELD USREMPNOM OF A_USR_CRE

SKIP 1

ALIGN(3,3,19) (,,32)
FIELD CCLDATMOD OF CPCONCIL_CHQ  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN ID 15 &
label "Modifié le....:"
FIELD CCLHREMOD OF CPCONCIL_CHQ

ALIGN(3,3,19) (,,32)
FIELD CCLUSRMOD OF CPCONCIL_CHQ HIDDEN ID 20 &
label "Modifié par...:"
FIELD USREMPNOM OF A_USR_MOD

BUILD

;
;********************************* Vérification *********************************
;*                                                                              *
;* Créé le.......: xxxxxxxx xxxxx                                               *
;* Créé par......: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;* Modifié le....: xxxxxxxx xxxxx                                               *
;* Modifié par...: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;********************************************************************************
