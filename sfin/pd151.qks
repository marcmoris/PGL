;/T/ Type de granit (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-07-04
;
;    Description..: Ce panorama permet de saisir les types de granit
;
;/M/ Programmeur..: Francois Richard
;    Date.........: 21 septembre 1999
;    Description..: Modification pour adapter au module columbia
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd151 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD151"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd151.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 TGRCODGRA &
         USING   T_CIECLEMNU, &
                 TGRCODGRA OF PDTYPE_GRANIT &
         REQUEST TGRCODGRA OF PDTYPE_GRANIT &
         ORDERBY CIECLE,   &
                 TGRCODGRA

  ACCESS VIA     CIECLE           &
         USING   T_CIECLEMNU    &
         ORDERBY CIECLE,      &
                 TGRCODGRA

         ITEM CIECLE OF PDTYPE_GRANIT INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Dureté du granit
;
DEFINE    D_TGRDUR CHARACTER SIZE 16 = "TGRDUR"
TEMPORARY T_TGRDUR CHARACTER SIZE 04

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_TGRDUR

  ACCESS VIA   XELNOM, &
               CBICLE &
         USING D_TGRDUR, &
               TGRDUR OF PDTYPE_GRANIT


;-------------------------------------------------------------------------------
;
; Couleur de granit
;
DEFINE    D_TGRCODCOUGRA CHARACTER SIZE 16 = "TGRCODCOUGRACLE"
TEMPORARY T_TGRCODCOUGRA CHARACTER SIZE 04

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_TGRCODCOUGRA

  ACCESS VIA   XELNOM, &
               CBICLE &
         USING D_TGRCODCOUGRA, &
               TGRCODCOUGRA OF PDTYPE_GRANIT

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL    CHARACTER SIZE 1
TEMPORARY T_TGRCODGRA CHARACTER SIZE 3

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Type de granit " &
@ELSE
      " %%%Type de granit " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,24)
FIELD TGRCODGRA    OF PDTYPE_GRANIT        &
        LABEL "Type...............:"       &
        HIDDEN ID 05                       &
        REQUIRED NOCHANGE                  &
        NUMERIC                            &
        LOOKUP NOTON PDTYPE_GRANIT         &
          VIA   CIECLE,                    &
                TGRCODGRA                  &
          USING T_CIECLEMNU,               &
                TGRCODGRA OF PDTYPE_GRANIT &
          MESSAGE 2901 ; Ce type de granit existe déjà

SKIP 1

ALIGN (2,3,27)
FIELD TGRDSCFRA001 OF PDTYPE_GRANIT        &
        LABEL "Desc. francaise....: 1."    &
        HIDDEN ID 10                       &
        REQUIRED

ALIGN (,24,27)
FIELD TGRDSCFRA002 OF PDTYPE_GRANIT        &
        ID SAME                            &
        LABEL "2."

FIELD TGRDSCFRA003 OF PDTYPE_GRANIT        &
        ID SAME                            &
        LABEL "3."

FIELD TGRDSCFRA004 OF PDTYPE_GRANIT        &
        ID SAME                            &
        LABEL "4."

ALIGN (2,3,27)
FIELD TGRDSCANG001 OF PDTYPE_GRANIT        &
        LABEL "Desc. anglaise.....: 1."    &
        HIDDEN ID 20                       &
        REQUIRED

ALIGN (,24,27)
FIELD TGRDSCANG002 OF PDTYPE_GRANIT        &
        ID SAME                            &
        LABEL "2."

FIELD TGRDSCANG003 OF PDTYPE_GRANIT        &
        ID SAME                            &
        LABEL "3."

FIELD TGRDSCANG004 OF PDTYPE_GRANIT        &
        ID SAME                            &
        LABEL "4."

SKIP 1
ALIGN (2,3,24) (,,27)
FIELD TGRDUR       OF PDTYPE_GRANIT      &
      LABEL "Dureté granit......:"    &         
      HIDDEN ID 35                       &
      LOOKUP ON A_CBI_TGRDUR             &
      MESSAGE 2904 ; Code invalide


FIELD CBIDSC OF A_CBI_TGRDUR &
        DISPLAY

FIELD TGRCODCOUGRA OF PDTYPE_GRANIT        &
        LABEL "Couleur granit.....:"       &
        HIDDEN ID 40                       &
        REQUIRED                           &
        LOOKUP ON A_CBI_TGRCODCOUGRA       &
          MESSAGE 2904 ; Code invalide

FIELD CBIDSC OF A_CBI_TGRCODCOUGRA &
        DISPLAY

SKIP 1
ALIGN (2,3,24) (,43,64)
FIELD TGRCOUGRA    OF PDTYPE_GRANIT      &
      LABEL "Coût granit........:"    &  
      HIDDEN ID 45                       &
      REQUIRED

FIELD TGRCOUGRABRU OF PDTYPE_GRANIT      &
      LABEL "Coût granit brut...:"    &
      REQUIRED

FIELD TGRPOI       OF PDTYPE_GRANIT      &
      LABEL "Poid M3.(kg).......: "      & 
      HIDDEN ID 50                       &
      DEFAULT 27231700.0000

FIELD TGRPOIBRU    OF PDTYPE_GRANIT      &
      LABEL "Poid M3.(kg) brut..: "      &
      DEFAULT 35000000

ALIGN (2,3,24)
FIELD TGRCLADOU    OF PDTYPE_GRANIT      &
      LABEL "Classe douane......: "      &
      HIDDEN ID 55                       &
      REQUIRED

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;
PROCEDURE INPUT TGRCODGRA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TGRCODGRA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd100 MODE F &
                     PASSING T_TGRCODGRA,    &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TGRCODGRA )
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup le champs dureté du granit (codes bilingues)
;
PROCEDURE INPUT TGRDUR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TGRDUR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_TGRDUR , &
                             T_TGRDUR
    LET FIELDTEXT = TRUNCATE( T_TGRDUR )
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup le champs couleur du granit (codes bilingues)
;
PROCEDURE INPUT TGRCODCOUGRA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TGRCODCOUGRA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_TGRCODCOUGRA , &
                             T_TGRCODCOUGRA
    LET FIELDTEXT = TRUNCATE( T_TGRCODCOUGRA )
  END
END

;-------------------------------------------------------------------------------
;
; Valide que le nombre de décimale max = 4
;
PROCEDURE EDIT TGRPOI
BEGIN
  IF FIELDTEXT = " "
  THEN BEGIN
    ERROR 2958 ; Cette valeur est obligatoire
  END
  USE usvaldec4 NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide que le nombre de d\351cimale max = 4
;
PROCEDURE EDIT TGRPOIBRU
BEGIN
  IF FIELDTEXT = " "
  THEN BEGIN
    ERROR 2958 ; Cette valeur est obligatoire
  END
  USE usvaldec4 NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un type de granit
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN pd151x MODE SAME            &
                   PASSING PDTYPE_GRANIT, & 
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3264 ; PD364 Impossible de supprimer ce type de granit,
                     ; il est d\351j\340 utilis\351e
   ELSE DELETE PDTYPE_GRANIT
END
;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDTYPE_GRANIT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDTYPE_GRANIT &
     AND NOT NEWRECORD     OF PDTYPE_GRANIT &
     AND NOT DELETEDRECORD OF PDTYPE_GRANIT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD

