;/T/ Ratios des colonnes G à K
;
;/P/ Programmeur..: Alain Côté
;    Date création: 6 avril 1994
;
;/V3.02.00/ Alain Côté, 5 avril 1994, 199
;
;    Description..: Saisie des ratios pour chaque ligne de détail de l'état
;                   financier.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gl011e ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND   , &
                         CHANGE   &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU   , &
                        T_CBIDSCTYP, &
                        GLETAT_FINANCIER

USE gl011e.hlp NOLIST

USE ussectmp NOLIST
    "GL011E"

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

USE ustrasse NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Procédures"
  MENUITEM LABEL "Saisie de la colonne G          " ACTION DESIGNER D001
  MENUITEM LABEL "Saisie de la colonne H          " ACTION DESIGNER D002
  MENUITEM LABEL "Saisie de la colonne I          " ACTION DESIGNER D003
  MENUITEM LABEL "Saisie de la colonne J          " ACTION DESIGNER D004
  MENUITEM LABEL "Saisie de la colonne K          " ACTION DESIGNER D005
  MENUITEM LABEL "Initialistion d'une colonne     " ACTION DESIGNER D006
@ELSE
ACTIONMENU LABEL "Procedures"
  MENUITEM LABEL "%%%Saisie de la colonne G       " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Saisie de la colonne H       " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Saisie de la colonne I       " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Saisie de la colonne J       " ACTION DESIGNER D004
  MENUITEM LABEL "%%%Saisie de la colonne K       " ACTION DESIGNER D005
  MENUITEM LABEL "%%%Initialistion d'une colonne  " ACTION DESIGNER D006
@ENDIF

;------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Compagnie du menu.
TEMPORARY T_FLGMNU    CHARACTER SIZE  1 ; Provenance du menu.
TEMPORARY T_CBIDSCTYP CHARACTER SIZE 25 ; Desc. du type d'E/F.

;------------------------------------------------------------------------------
;
; Définitions d'un état financier.
;
FILE GLETAT_FINANCIER MASTER

;
; Détail de la définition d'un état financier.
;
FILE GLEFI_DETAIL     PRIMARY &
                      OCCURS 15 &
                      NOITEM

  ACCESS VIA   EFICIECLE , &
               EFICLE      &
         USING EFICIECLE OF GLETAT_FINANCIER , &
               EFICLE OF GLETAT_FINANCIER      &
         ORDERBY EFICIECLE , &
                 EFICLE    , &
                 EFDNUMLIG

  ;
  ; Les ratios sont valident que pour certain type de ligne, le sélect suivant
  ; nous assure que les lignes affichées à l'écran font partie des types de
  ; ligne valide pour les ratios.
  ;
  SELECT IF    "CPT"  = EFDCOD OF GLEFI_DETAIL &
            OR "SDEB" = EFDCOD OF GLEFI_DETAIL &
            OR "SFIN" = EFDCOD OF GLEFI_DETAIL &
            OR "TOT"  = EFDCOD OF GLEFI_DETAIL &
            OR "NOTE" = EFDCOD OF GLEFI_DETAIL &
            OR "JUM"  = EFDCOD OF GLEFI_DETAIL &
            OR "BAL"  = EFDCOD OF GLEFI_DETAIL

;
; Pour afficher le type de colonne en en-tête.
;
TEMPORARY T_EFCNUMCOL INTEGER SIZE 2
;
FILE GLEFI_COLONNE    REFERENCE

  ACCESS  VIA     EFICIECLE , &
                  EFICLE    , &
                  EFCNUMCOL   &
          USING   EFICIECLE OF GLETAT_FINANCIER , &
                  EFICLE    OF GLETAT_FINANCIER , &
                  T_EFCNUMCOL

;
; Pour afficher la description d'une note.
;
FILE GLETAT_FINANCIER REFERENCE &
                      ALIAS A_EFI_NOTE
  ACCESS VIA  EFICIECLE , &
              EFICLE      &
         USING EFICIECLE OF GLEFI_DETAIL , &
               CPTCLE    OF GLEFI_DETAIL

;
; Pour afficher la description du compte sur les lignes.
;
FILE VCPT_DSC2         REFERENCE
  ACCESS VIA CPTCLE &
         USING CPTCLE OF GLEFI_DETAIL

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU      IF T_FLGMNU = "C"
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)

;
; Type des colonnes G à K.
;
TEMPORARY T_TYPCOLG CHARACTER SIZE 4
TEMPORARY T_TYPCOLH CHARACTER SIZE 4
TEMPORARY T_TYPCOLI CHARACTER SIZE 4
TEMPORARY T_TYPCOLJ CHARACTER SIZE 4
TEMPORARY T_TYPCOLK CHARACTER SIZE 4

TEMPORARY T_COLVAL  CHARACTER SIZE 9 INITIAL "G|H|I|J|K" &
                                     RESET AT STARTUP

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Ratios des colonnes G à K " &
@ELSE
      " %%%Ratios des colonnes G à K " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP
ALIGN(,3,19) (,35,51) (,,54)

FIELD EFICLE OF GLETAT_FINANCIER FIXED

FIELD EFITYP OF GLETAT_FINANCIER FIXED

FIELD T_CBIDSCTYP FIXED

SKIP 1

TITLE &
@IF FRANCAIS
      "  No   Code   No                 " &
@ELSE
      "%%%o   Code   No                 " &
@ENDIF
     AT ,3

TITLE "1 3 5 7 " AT ,37
TITLE "G      H      I      J      K" AT ,49

SKIP

TITLE &
@IF FRANCAIS
      " Ligne Tran Compte Texte         " &
@ELSE
      "%%%gne Tran Compte Texte         " &
@ENDIF
     AT ,3

TITLE " 2 4 6 8" AT ,37

ALIGN(,,48) (,,55) (,,62) (,,69) (,,76)

FIELD T_TYPCOLG &
        DISPLAY

FIELD T_TYPCOLH &
        DISPLAY

FIELD T_TYPCOLI &
        DISPLAY

FIELD T_TYPCOLJ &
        DISPLAY

FIELD T_TYPCOLK &
        DISPLAY

SKIP

ALIGN(3,2,3) (,,10) (,,15) (,,22) (,,37) (,,38) (,,39) (,,40) &
     (,,41)  (,,42) (,,43) (,,44) (,,46) (,,53) (,,60) (,,67) (,,74)

CLUSTER OCCURS WITH GLEFI_DETAIL ID BASE 01

FIELD EFDNUMLIG OF GLEFI_DETAIL HIDDEN LABEL " " &
        DISPLAY

FIELD EFDCOD OF GLEFI_DETAIL &
        DISPLAY

FIELD CPTCLE OF GLEFI_DETAIL &
        DISPLAY

FIELD EFDDSC    OF GLEFI_DETAIL &
        FOR 1,13 &
        DISPLAY

HILITE DATA INVERSE

FIELD EFDCOL1   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE HALFTONE

FIELD EFDCOL2   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE

FIELD EFDCOL3   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE HALFTONE

FIELD EFDCOL4   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE

FIELD EFDCOL5   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE HALFTONE

FIELD EFDCOL6   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE

FIELD EFDCOL7   OF GLEFI_DETAIL &
        DISPLAY

HILITE DATA INVERSE HALFTONE

FIELD EFDCOL8   OF GLEFI_DETAIL &
        DISPLAY

USE ushilite NOLIST

FIELD EFDRATG OF GLEFI_DETAIL

FIELD EFDRATH OF GLEFI_DETAIL

FIELD EFDRATI OF GLEFI_DETAIL

FIELD EFDRATJ OF GLEFI_DETAIL

FIELD EFDRATK OF GLEFI_DETAIL

CLUSTER

DRAW 5, 1 TO  5,80
DRAW 5,36 TO 23,36
DRAW 5,45 TO 23,45


;-------------------------------------------------------------------------------
;

; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Affiche le type de la colonne G.
;
;
PROCEDURE OUTPUT T_TYPCOLG
BEGIN
  LET T_EFCNUMCOL = 7
  LET FIELDTEXT = EFCTYPCOL OF GLEFI_COLONNE
END


;-------------------------------------------------------------------------------
;
;
; Affiche le type de la colonne H.
;
;
PROCEDURE OUTPUT T_TYPCOLH
BEGIN
  LET T_EFCNUMCOL = 8
  LET FIELDTEXT = EFCTYPCOL OF GLEFI_COLONNE
END


;-------------------------------------------------------------------------------
;
;
; Affiche le type de la colonne I.
;
;
PROCEDURE OUTPUT T_TYPCOLI
BEGIN
  LET T_EFCNUMCOL = 9
  LET FIELDTEXT = EFCTYPCOL OF GLEFI_COLONNE
END


;-------------------------------------------------------------------------------
;
;
; Affiche le type de la colonne J.
;
;
PROCEDURE OUTPUT T_TYPCOLJ
BEGIN
  LET T_EFCNUMCOL = 10
  LET FIELDTEXT = EFCTYPCOL OF GLEFI_COLONNE
END


;-------------------------------------------------------------------------------
;
;
; Affiche le type de la colonne K.
;
;
PROCEDURE OUTPUT T_TYPCOLK
BEGIN
  LET T_EFCNUMCOL = 11
  LET FIELDTEXT = EFCTYPCOL OF GLEFI_COLONNE
END


;-------------------------------------------------------------------------------
;
; Affiche la description du compte ou la description de la note dans le champ
; texte du détail.
;
;
PROCEDURE OUTPUT EFDDSC
BEGIN
  IF     CPTCLE OF GLEFI_DETAIL <> "" &
     AND 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN

    IF    EFDCOD OF GLEFI_DETAIL = "CPT" &
       OR EFDCOD OF GLEFI_DETAIL = "SDEB" &
       OR EFDCOD OF GLEFI_DETAIL = "SFIN" &
       OR EFDCOD OF GLEFI_DETAIL = "JUM" &
       OR EFDCOD OF GLEFI_DETAIL = "BAL"
    THEN LET FIELDTEXT = CPTDSCABR OF VCPT_DSC2

    IF EFDCOD OF GLEFI_DETAIL = "NOTE"
    THEN LET FIELDTEXT = EFITITEF OF A_EFI_NOTE
  END
END


;-------------------------------------------------------------------------------
;
;
; Saisie rapide pour le ratio de la colonne G.
;
;
PROCEDURE DESIGNER D001
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    ACCEPT EFDRATG OF GLEFI_DETAIL
  END
END


;-------------------------------------------------------------------------------
;
;
; Saisie rapide pour le ratio de la colonne H.
;
;
PROCEDURE DESIGNER D002
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    ACCEPT EFDRATH OF GLEFI_DETAIL
  END
END


;-------------------------------------------------------------------------------
;
;
; Saisie rapide pour le ratio de la colonne I.
;
;
PROCEDURE DESIGNER D003
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    ACCEPT EFDRATI OF GLEFI_DETAIL
  END
END


;-------------------------------------------------------------------------------
;
;
; Saisie rapide pour le ratio de la colonne J.
;
;
PROCEDURE DESIGNER D004
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    ACCEPT EFDRATJ OF GLEFI_DETAIL
  END
END


;-------------------------------------------------------------------------------
;
;
; Saisie rapide pour le ratio de la colonne K.
;
;
PROCEDURE DESIGNER D005
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    ACCEPT EFDRATK OF GLEFI_DETAIL
  END
END


;-------------------------------------------------------------------------------
;
;
; Appel du sous-écran pour initialiser une colonne.
;
;
PROCEDURE DESIGNER D006
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    IF ALTEREDRECORD OF GLEFI_DETAIL
    THEN ERROR 272 ; Faire une mise à jour avant d'aller au sous-écran.
  END
  RUN SCREEN gl011g MODE E &
                    PASSING GLETAT_FINANCIER , &
                            T_COLVAL
  SEVERE 288 ; Faire une recherche pour visualiser le résultat.
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GLEFI_DETAIL
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF    DELETEDRECORD OF GLEFI_DETAIL &
      AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GLEFI_DETAIL &
       AND NOT NEWRECORD     OF GLEFI_DETAIL &
       AND NOT DELETEDRECORD OF GLEFI_DETAIL &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************** Ratios des colonnes G à K ***************************
* Code E/F......: xxxx            Type E/F......: x  xxxxxxxxxxxxxxxxxxxxxxxxx *
********************************************************************************
*   No   Code   No                 *1 3 5 7 *   G      H      I      J      K  *
*  Ligne Tran Compte Texte         * 2 4 6 8*  xxxx   xxxx   xxxx   xxxx   xxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
* xxxxxx xxxx xxxxxx xxxxxxxxxxxxx *xxxxxxxx*xxxxxx xxxxxx xxxxxx xxxxxx xxxxxx*
********************************************************************************
@ENDIF
