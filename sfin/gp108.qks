;/T/ Consultation des activités        CIECLE, PRJCLE, PRACLE, PRASTUPAT
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 8 mars 1995
;
;    Description..: Affiche les codes d'activités par projet.
;
;    Paramètres...: CIECLE    ==> - numéro de compagnie
;                                 - segment de la clé de recherche
;
;                   PRJCLE    ==> - numéro de projet
;                                 - segment de la clé de recherche
;
;                   PRACLE    ==> - numéro d'activités
;                                 - segment de la clé de recherche
;                                 - peut contenir la valeur @
;                                 - contient la valeur de retour
;
;                   PRASTUPAT ==> - Statut de l'activités
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp108 NOMODE NOACTION FIELDMARK &
             FROM 11,05 TO 23,75 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU, &
                       T_PRJCLE, &
                       T_PRACLE, &
                       T_PRASTUPAT

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_PRJCLE    CHARACTER SIZE 8
TEMPORARY T_PRACLE    CHARACTER SIZE 3
TEMPORARY T_PRASTUPAT CHARACTER SIZE 5

USE gp108.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE GPPRO_ACTIVITE   PRIMARY &
                      OCCURS 11
  ACCESS VIA CIECLE, &
             PRJCLE, &
             PRACLE  &
         USING T_CIECLEMNU, &
               T_PRJCLE   , &
               T_PRACLE     &
         ORDERBY PRACLE

  SELECT IF &
    MATCHPATTERN(PRASTU OF GPPRO_ACTIVITE, TRUNCATE(T_PRASTUPAT))

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 1,1 TO 13,71

USE ustitoff NOLIST

SKIP TO 2

ALIGN (3,3,6)(,,10)
CLUSTER OCCURS WITH GPPRO_ACTIVITE

FIELD PRACLE    OF GPPRO_ACTIVITE HIDDEN LABEL ">>"
FIELD PRADSC1   OF GPPRO_ACTIVITE DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_PRACLE = PRACLE OF GPPRO_ACTIVITE
  RETURN
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_PRACLE, "@")
  THEN LET T_PRACLE = ""
END

BUILD ;
;
;                                       *****************************************
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       *****************************************
