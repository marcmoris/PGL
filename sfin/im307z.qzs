;/T/ Garanties venant à échéance
;
;/P/ Programmeur..: Alain Côté
;    Date création: 2 mars 1994
;
;/V3.02.00/ Alain Côté, 2 mars 1994, Immobilisation
;
;    Description...: Ce rapport imprime la liste des immobilisations dont la
;                    garantie vient à échéance dans le nombre de jours
;                    paramètré.
;
;
;    Paramétres....: No. cie   (%CUF_CIECLEMNU)
;                    Jours échéance (%DUJRSECH)
;                    Nom cie   (%DUF_CIENOM)
;
;    Tris..........: Par ordre de catégorie et par numéro d'immobilisation.
;
;    Particularités: Les immobilisations doivent être actives pour être
;                    sélectionnés.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
;
; Pour enlver la deuxième ligne de titre.
;
SET NOBLANKS

ACCESS IMIMMOBILISATION &
  LINK REFCIECLE OF IMIMMOBILISATION , &
       FOUCLE    OF IMIMMOBILISATION &
    TO FOUCIECLE , &
       FOUCLE OF CPFOURNISSEUR


CHOOSE CIECLE PARM

DEFINE D_NBRJRS  INTEGER   SIZE 4 = PARM PROMPT "D'ici (ex 30Jours):"

SELECT IMIMMOBILISATION &
                IF     0 <= ( DAYS( IMMDATFINGAR OF IMIMMOBILISATION ) - &
                              DAYS( SYSDATE) ) &
                   AND D_NBRJRS >= ( DAYS(IMMDATFINGAR OF IMIMMOBILISATION) - &
                                     DAYS(SYSDATE) ) &
                   AND IMMSTU OF IMIMMOBILISATION = "A"


;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Garanties venant à échéance"
@ELSE
       "%%%Garanties venant à échéance"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM307"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


SORT ON CIECLE OF IMIMMOBILISATION &
     ON FOUCLE OF IMIMMOBILISATION &
     ON IMMCLE OF IMIMMOBILISATION


USE ustitstd NOLIST
SKIP &
  TAB 059 &
@IF FRANCAIS
          "d'ici" &
@ELSE
          "d'ici" &
@ENDIF
  TAB 065 D_NBRJRS &
            PICTURE "^^^" &
  TAB 069 &
@IF FRANCAIS
         "jours" &
@ELSE
         "jours" &
@ENDIF
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
"                  Fournisseur                          Contact         " &
@ELSE
"                  Fournisseur                          Contact         " &
@ENDIF
SKIP &
  TAB 001 &
"-----------------------------------------------  --------------------  "


HEADING AT FOUCLE &
   TAB 001 FOUCLE    OF IMIMMOBILISATION &
   TAB 008 FOUNOM    OF CPFOURNISSEUR    &
   TAB 050 IMMCNTENT OF IMIMMOBILISATION &
SKIP 2 &
   TAB 008 &
@IF FRANCAIS
          "Immobilisation                                         Echéance  Utilisateur           Localisation" &
@ELSE
          "Immobilisation                                         Echéance  Utilisateur           Localisation" &
@ENDIF
SKIP 2


REPORT &
   TAB 008 IMMCLE OF IMIMMOBILISATION &
   TAB 021 IMMDSC OF IMIMMOBILISATION &
   TAB 063 IMMDATFINGAR OF IMIMMOBILISATION &
   TAB 073 IMMUTL OF IMIMMOBILISATION &
   TAB 095 IMMLOC OF IMIMMOBILISATION


FOOTING AT FOUCLE &
 SKIP 2


BUILD im307z
@IF FORMAT

IM307                                         XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                    PAGE: 9999
99/99/99 99:99 XXXXXXXXXXXX                         Garanties venant à échéance
                                    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
                                                          d'ici 999 jours

                  Fournisseur                          Contact
-----------------------------------------------  --------------------
xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

       Immobilisation                                         Echéance  Utilisateur           Localisation
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

       Immobilisation                                         Echéance  Utilisateur           Localisation
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx

       Immobilisation                                         Echéance  Utilisateur           Localisation
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
       xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx
@ENDIF
