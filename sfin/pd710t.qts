;/T/ 2.4.3 Enregistrer les opérations de surface - PROCÉDURE DE VALIDATION
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-03-30
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les opérations de surface.
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; set process nolimit



RUN pd710t


USE usdimprd NOLIST   ; Dimension min et max pour les pièces de granit


;-------------------------------------------------------------------------------
;
; Valide les lignes de détail du rapport d'opération de surface
;

REQUEST VALIDE_DETAIL_RAPPORT


ACCESS PDOPERATION_SURF                      &
  LINK CIECLE           OF PDOPERATION_SURF, &
       OSUNUMRAP        OF PDOPERATION_SURF  &
    TO CIECLE,                               &
       OSUNUMRAP        OF PDDET_OPER_SURF   &
  LINK CIECLE           OF PDOPERATION_SURF, &
       TRANUMTRA002     OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       TRANUMTRA002     OF PDTRANCHE         OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       DOSLON           OF PDDET_OPER_SURF,  &
       DOSHAU           OF PDDET_OPER_SURF,  &
       DOSEPA           OF PDDET_OPER_SURF,  &
       TGRCODGRA        OF PDDET_OPER_SURF,  &
       DOSCODFIN001     OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       PDILON,                               &
       PDIHAU,                               &
       PDIEPA,                               &
       TGRCODGRA,                            &
       TYFCODFIN        OF PDPROD_DIMENSION  OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       MOPCODMAC        OF PDOPERATION_SURF  &
    TO CIECLE,                               &
       MOPCODMAC        OF PDMACH_OP_SURF    &
  LINK CIECLE           OF PDOPERATION_SURF, &
       DOSCODFIN001     OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       TYFCODFIN        OF PDTYPE_FINI       ALIAS TYF_AVANT OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       DOSCODFIN002     OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       TYFCODFIN        OF PDTYPE_FINI       ALIAS TYF_APRES OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       TGRCODGRA        OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       TGRCODGRA        OF PDTYPE_GRANIT     OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       MOPCODMAC        OF PDOPERATION_SURF, &
       DOSCODFIN001     OF PDDET_OPER_SURF,  &
       DOSCODFIN002     OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       MOPCODMAC,                            &
       MFPCODFINAVT,                         &
       MFPCODFINAPR     OF PDMACH_OP_S_F_P   OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       OSUNUMRAP        OF PDOPERATION_SURF  &
    TO CIECLE,                               &
       OSUNUMRAP        OF PDOPERATEURS_O_S  OPTIONAL &
  LINK CIECLE           OF PDOPERATION_SURF, &
       OSUNUMRAP        OF PDDET_OPER_SURF,  &
       TRANUMTRA002     OF PDDET_OPER_SURF   &
    TO CIECLE,                               &
       OSUNUMRAP,                            &
       TRANUMTRA002     OF PDDET_OPER_SURF   ALIAS DOS_DOUBLE OPTIONAL


CHOOSE CIECLE PARM , OSUNUMRAP PARM


SELECT PDOPERATION_SURF IF OSUSTU OF PDOPERATION_SURF <> "T"


SELECT PDDET_OPER_SURF  IF DOSSTU OF PDDET_OPER_SURF <> "M" &
                           AND &
                           DOSSTU OF PDDET_OPER_SURF <> "D"

SELECT DOS_DOUBLE IF DOSNUMLIG OF PDDET_OPER_SURF <> DOSNUMLIG OF DOS_DOUBLE &
                     AND &
                     DOSSTU OF DOS_DOUBLE <> "D"


SORT ON OSUNUMRAP OF PDOPERATION_SURF &
     ON DOSNUMLIG OF PDDET_OPER_SURF


TEMPORARY T_MESSAGE CHARACTER SIZE 80


;-------------------------------------------------------------------------------
;
; Code d'erreur pour le rapport
;


TEMPORARY T_ERREUR_1 CHARACTER SIZE 01
TEMPORARY T_ERREUR_2 CHARACTER SIZE 01
TEMPORARY T_ERREUR_3 CHARACTER SIZE 01
TEMPORARY T_ERREUR_4 CHARACTER SIZE 01
TEMPORARY T_ERREUR_5 CHARACTER SIZE 01
TEMPORARY T_ERREUR_6 CHARACTER SIZE 01
TEMPORARY T_ERREUR_7 CHARACTER SIZE 01
TEMPORARY T_ERREUR_8 CHARACTER SIZE 01
TEMPORARY T_ERREUR_9 CHARACTER SIZE 01


ITEM T_ERREUR_1 = "1" IF NOT RECORD PDOPERATEURS_O_S EXISTS ELSE "0"
ITEM T_ERREUR_2 INITIAL "0"
ITEM T_ERREUR_3 INITIAL "0"
ITEM T_ERREUR_4 INITIAL "0"
ITEM T_ERREUR_5 INITIAL "0"
ITEM T_ERREUR_6 INITIAL "0"
ITEM T_ERREUR_7 INITIAL "0"
ITEM T_ERREUR_8 INITIAL "0"
ITEM T_ERREUR_9 INITIAL "0"


ITEM T_MESSAGE = &
     "." IF DOSACCERR OF PDDET_OPER_SURF = "1" &
            OR DOSSTU OF PDDET_OPER_SURF = "V" &
            OR DOSSTU OF PDDET_OPER_SURF = "T" &
ELSE "Ce numéro de tranche n'existe pas : " + TRANUMTRA002 OF PDTRANCHE &
     IF TRANUMTRA002 OF PDDET_OPER_SURF <> "" AND NOT RECORD PDTRANCHE EXISTS &
ELSE "Le statut de la tranche ne permet pas cette opération : " + TRANUMTRA002 OF PDTRANCHE &
     IF TRASTU OF PDTRANCHE <> "I" &
        AND TRASTU OF PDTRANCHE <> "R" &
        AND TRASTU OF PDTRANCHE <> "A" &
        AND RECORD PDTRANCHE EXISTS &
ELSE "Le type de granit n'existe pas : " + TGRCODGRA OF PDDET_OPER_SURF &
     IF NOT RECORD PDTYPE_GRANIT EXISTS &
ELSE "La longueur n'est pas dans les limites permises : " + ASCII(DOSLON OF PDDET_OPER_SURF) &
     IF (TPDTYPPRD        OF PDDET_OPER_SURF = "TR" AND &
         (DOSLON          OF PDDET_OPER_SURF > G_DIM_MAX_LAR_TR OR &
          DOSLON          OF PDDET_OPER_SURF < G_DIM_MIN_LAR_TR)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "DI" AND &
         (DOSLON          OF PDDET_OPER_SURF > G_DIM_MAX_LAR_DI OR &
          DOSLON          OF PDDET_OPER_SURF < G_DIM_MIN_LAR_DI)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "PD" AND &
         (DOSLON          OF PDDET_OPER_SURF > G_DIM_MAX_LAR_PD OR &
          DOSLON          OF PDDET_OPER_SURF < G_DIM_MIN_LAR_PD)) &
ELSE "La hauteur n'est pas dans les limites permises: " + ASCII(DOSHAU OF PDDET_OPER_SURF) &
     IF (TPDTYPPRD        OF PDDET_OPER_SURF = "TR" AND &
         (DOSHAU          OF PDDET_OPER_SURF > G_DIM_MAX_HAU_TR OR &
          DOSHAU          OF PDDET_OPER_SURF < G_DIM_MIN_HAU_TR)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "DI" AND &
         (DOSHAU           OF PDDET_OPER_SURF > G_DIM_MAX_HAU_DI OR &
          DOSHAU           OF PDDET_OPER_SURF < G_DIM_MIN_HAU_DI)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "PD" AND &
         (DOSHAU           OF PDDET_OPER_SURF > G_DIM_MAX_HAU_PD OR &
          DOSHAU           OF PDDET_OPER_SURF < G_DIM_MIN_HAU_PD)) &
ELSE "L'épaisseur n'est pas dans les limites permises: " + ASCII(DOSEPA OF PDDET_OPER_SURF) &
     IF (TPDTYPPRD        OF PDDET_OPER_SURF = "TR" AND &
         (DOSEPA           OF PDDET_OPER_SURF > G_DIM_MAX_EPA_TR OR &
          DOSEPA           OF PDDET_OPER_SURF < G_DIM_MIN_EPA_TR)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "DI" AND &
         (DOSEPA           OF PDDET_OPER_SURF > G_DIM_MAX_EPA_DI OR &
          DOSEPA           OF PDDET_OPER_SURF < G_DIM_MIN_EPA_DI)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "PD" AND &
         (DOSEPA           OF PDDET_OPER_SURF > G_DIM_MAX_EPA_PD OR &
          DOSEPA           OF PDDET_OPER_SURF < G_DIM_MIN_EPA_PD)) &
ELSE "L'épaisseur résultante peut être qu'avec le niveleur" &
     IF (MOPCODMAC        OF PDOPERATION_SURF <> "NIV" &
        AND &
        DOSEPARES OF PDDET_OPER_SURF <> 0) &
        AND &
        (MOPCODMAC        OF PDOPERATION_SURF <> "NIV" &
        AND &
        DOSEPARES OF PDDET_OPER_SURF <> DOSEPA OF PDDET_OPER_SURF) &
ELSE "L'épaisseur resultante n'est pas dans les limites permises: " + ASCII(DOSEPARES OF PDDET_OPER_SURF) &
     IF (TPDTYPPRD        OF PDDET_OPER_SURF = "TR" AND &
         (DOSEPARES        OF PDDET_OPER_SURF > G_DIM_MAX_EPA_TR OR &
          DOSEPARES        OF PDDET_OPER_SURF < G_DIM_MIN_EPA_TR)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "DI" AND &
         (DOSEPARES        OF PDDET_OPER_SURF > G_DIM_MAX_EPA_DI OR &
          DOSEPARES        OF PDDET_OPER_SURF < G_DIM_MIN_EPA_DI)) &
        OR &
        (TPDTYPPRD        OF PDDET_OPER_SURF = "PD" AND &
         (DOSEPARES        OF PDDET_OPER_SURF > G_DIM_MAX_EPA_PD OR &
          DOSEPARES        OF PDDET_OPER_SURF < G_DIM_MIN_EPA_PD)) &
ELSE "L'épaisseur resultante est plus grande que l'épaisseur du départ" &
     IF DOSEPARES        OF PDDET_OPER_SURF > DOSEPA           OF PDDET_OPER_SURF &
ELSE "Le type de fini avant n'existe pas: " + DOSCODFIN001 OF PDDET_OPER_SURF &
     IF NOT RECORD TYF_AVANT EXISTS &
ELSE "Le type de fini après n'existe pas: " + DOSCODFIN002 OF PDDET_OPER_SURF &
     IF NOT RECORD TYF_APRES EXISTS &
ELSE "Le fini avant et après n'est pas disponible pour cette machine : " + &
      DOSCODFIN001 OF PDDET_OPER_SURF + " " + DOSCODFIN002 OF PDDET_OPER_SURF &
     IF NOT RECORD PDMACH_OP_S_F_P EXISTS &
ELSE "La tranche à été traiter plus d'une fois pour ce rapport" &
     IF RECORD DOS_DOUBLE EXISTS &
        AND &
        TRANUMTRA002 OF DOS_DOUBLE = TRANUMTRA002 OF PDDET_OPER_SURF &
        AND &
        0 <> SIZE(TRUNCATE(TRANUMTRA002 OF DOS_DOUBLE)) &
ELSE "."


SUBFILE W-PD710-PARAMETRE AT OSUNUMRAP &
  INCLUDE CIECLE    OF PDOPERATION_SURF, &
          OSUNUMRAP OF PDOPERATION_SURF


SUBFILE W-PD710-MESSAGE KEEP &
  INCLUDE CIECLE    OF PDOPERATION_SURF, &
          OSUNUMRAP OF PDOPERATION_SURF, &
          DOSNUMLIG OF PDDET_OPER_SURF, &
          T_MESSAGE, &
          T_ERREUR_1, &
          T_ERREUR_2, &
          T_ERREUR_3, &
          T_ERREUR_4, &
          T_ERREUR_5, &
          T_ERREUR_6, &
          T_ERREUR_7, &
          T_ERREUR_8, &
          T_ERREUR_9


OUTPUT PDDET_OPER_SURF UPDATE IF DOSSTU OF PDDET_OPER_SURF <> "T"
  ITEM DOSSTU OF PDDET_OPER_SURF FINAL "V" IF T_MESSAGE = "." AND DOSSTU OF PDDET_OPER_SURF = "E"


;-------------------------------------------------------------------------------
;
; Valide le rapport d'opération de surface
;

REQUEST VALIDE_RAPPORT


ACCESS *W-PD710-PARAMETRE                     &
  LINK CIECLE           OF W-PD710-PARAMETRE, &
       OSUNUMRAP        OF W-PD710-PARAMETRE  &
    TO CIECLE,                                &
       OSUNUMRAP        OF PDOPERATION_SURF   &
  LINK CIECLE           OF W-PD710-PARAMETRE, &
       OSUNUMRAP        OF PDOPERATION_SURF   &
    TO CIECLE,                                &
       OSUNUMRAP        OF PDDET_OPER_SURF    OPTIONAL &
  LINK CIECLE           OF W-PD710-PARAMETRE, &
       OSUNUMRAP        OF PDOPERATION_SURF   &
    TO CIECLE,                                &
       OSUNUMRAP        OF PDOPERATEURS_O_S   OPTIONAL


TEMPORARY T_ERREUR CHARACTER SIZE 1


ITEM T_ERREUR = "E" IF T_ERREUR = "E" &
                       OR &
                       (DOSSTU OF PDDET_OPER_SURF <> "V" &
                        AND &
                        DOSSTU OF PDDET_OPER_SURF <> "T" &
                        AND &
                        DOSSTU OF PDDET_OPER_SURF <> "M" &
                        AND &
                        DOSSTU OF PDDET_OPER_SURF <> "D") &
                       OR &
                       NOT RECORD PDOPERATEURS_O_S EXISTS &
                    ELSE "C" IF OSUSTU OF PDOPERATION_SURF = "C" &
                               ; OR &
                    ELSE "T" IF OSUSTU OF PDOPERATION_SURF = "T" &
                    ELSE "V"


SORT ON OSUNUMRAP OF W-PD710-PARAMETRE


OUTPUT PDOPERATION_SURF UPDATE AT OSUNUMRAP
  ITEM OSUSTU OF PDOPERATION_SURF FINAL T_ERREUR


BUILD pd710t
