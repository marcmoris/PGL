;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Fiche de projet
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp001 ACTIONBAR &
             MODE LABEL "" AT 2,132 &
             NOACTION &
             from 1,1 to 23,132 &
             FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CIENOM


TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie - client
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_PRJCLE    CHARACTER SIZE 8  RESET AT STARTUP ; Numero de projet
TEMPORARY T_CLICLE2   CHARACTER SIZE 6  RESET AT STARTUP ; Numéro de client
TEMPORARY T_RADCLE2   INTEGER UNSIGNED SIZE 2 RESET AT STARTUP
TEMPORARY T_ERRCOP    CHARACTER SIZE 1  RESET AT STARTUP

USE ussectmp NOLIST     ; Variable pour la securite
    "GP001"             ; Nom de l'écran

USE gp001.hlp NOLIST    ; Use servant à la documentation de l'écran
                        ; Ne pas oublier de créer un fichier vide dans [exefra], [exeang]


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Informations complémentaires" ACTION DESIGNER D001
  MENUITEM LABEL "Client supplémentaire       " ACTION DESIGNER D002
  MENUITEM LABEL "Suivi comptable             " ACTION DESIGNER D003
  MENUITEM LABEL "Suivi d'opération           " ACTION DESIGNER D004
  MENUITEM LABEL "Piste de vérification       " ACTION DESIGNER D005
  MENUITEM LABEL "Copie de sous-projet        " ACTION DESIGNER D006
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "Informations complémentaires" ACTION DESIGNER D001
  MENUITEM LABEL "Client supplémentaire       " ACTION DESIGNER D002
  MENUITEM LABEL "Suivi comptable             " ACTION DESIGNER D003
  MENUITEM LABEL "Suivi d'opération           " ACTION DESIGNER D004
  MENUITEM LABEL "Piste de vérification       " ACTION DESIGNER D005
  MENUITEM LABEL "Copie de sous-projet        " ACTION DESIGNER D006
@ENDIF

;------------------------------------
; Fichier de projets
;------------------------------------
FILE GPPROJETS              PRIMARY &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE  &
          USING   T_CIECLEMNU, &
                  PRJCLE OF GPPROJETS &
          REQUEST PRJCLE &
          ORDERBY PRJCLE

  ACCESS  VIA     CIECLE, &
                  PRJTYP  &
          USING   T_CIECLEMNU, &
                  PRJTYP OF GPPROJETS &
          REQUEST PRJTYP &
          ORDERBY PRJCLE

  ACCESS  VIA     CIECLE, &
                  CLICLE  &
          USING   T_CIECLEMNU, &
                  CLICLE OF GPPROJETS &
          REQUEST CLICLE &
          ORDERBY PRJCLE

  ACCESS  VIA     CIECLE, &
                  PRJRES  &
          USING   T_CIECLEMNU, &
                  PRJRES OF GPPROJETS &
          REQUEST PRJRES &
          ORDERBY PRJCLE

  ACCESS  VIA     CIECLE &
          USING   T_CIECLEMNU &
          ORDERBY PRJCLE

  ITEM CIECLE    OF GPPROJETS       INITIAL T_CIECLEMNU
  ITEM CLICIECLE OF GPPROJETS       INITIAL T_CLICIECLE
  ITEM PRJUSRCRE OF GPPROJETS       INITIAL LOGONID
  ITEM PRJDATCRE OF GPPROJETS       INITIAL SYSDATE
  ITEM PRJHRECRE OF GPPROJETS       INITIAL SYSTIME
  ITEM PRJDATMOD OF GPPROJETS       FINAL   SYSDATE        &
                                       IF NOT NEWRECORD OF GPPROJETS       AND &
                                          ALTEREDRECORD OF GPPROJETS
  ITEM PRJHREMOD OF GPPROJETS       FINAL   SYSTIME &
                                       IF NOT NEWRECORD OF GPPROJETS       AND &
                                          ALTEREDRECORD OF GPPROJETS
  ITEM PRJUSRMOD OF GPPROJETS       FINAL   LOGONID         &
                                       IF NOT NEWRECORD OF GPPROJETS       AND &
                                          ALTEREDRECORD OF GPPROJETS


;---------------------------------
; Fichier des clients
;---------------------------------
FILE GPPRO_CLIENT           SECONDARY &
                            NOITEMS


;---------------------------------
; Fichier des sous-projets
;---------------------------------
FILE GPPRO_ACTIVITE         SECONDARY &
                            NOITEMS   &
                            ALIAS GPPRO_ACTIVITE_ADD

;---------------------------------
; Fichier des sous-projets
;---------------------------------
FILE GPPRA_CLIENT           SECONDARY &
                            NOITEMS   &
                            ALIAS GPPRA_CLIENT_ADD

;---------------------------------
; Fichier des sous-projets
;---------------------------------
FILE GPPRO_ACTIVITE         DESIGNER  &
                            NOITEMS   &
                            ALIAS GPPRO_ACTIVITE_3  &
                            OPEN 3 READ SHARE

;---------------------------------
; Fichier des clients / compagnies
;---------------------------------
FILE VCLC_CLI               REFERENCE

  ACCESS  VIA   CLICIECLE, &
                CLICLE,    &
                CIECLE     &
          USING CLICIECLE OF GPPROJETS, &
                CLICLE    OF GPPROJETS, &
                CIECLE    OF GPPROJETS


;---------------------------------
; Fichier des références d'adresse
;---------------------------------
TEMPORARY T_CLICLE CHARACTER SIZE 6
FILE MCREF_ADRESSE          REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
          USING CLICIECLE OF GPPROJETS, &
                "C"                   , &
                CLICLE    OF GPPROJETS, &
                1


;-------------------------------------------------------------------
; Validation du type de projet et lecture de sa description
;-------------------------------------------------------------------
DEFINE    D_PRJTYP CHARACTER SIZE 16 = "PRJTYP"
TEMPORARY T_PRJTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRJTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRJTYP, &
                PRJTYP OF GPPROJETS


;-------------------------------------------------------------------
; Validation de la catégorie du projet et lecture de sa description
;-------------------------------------------------------------------
DEFINE    D_PRJCAT CHARACTER SIZE 16 = "PRJCAT"
TEMPORARY T_PRJCAT CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRJCAT

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRJCAT, &
                PRJCAT OF GPPROJETS


;-----------------------------------------------------------------------
; Validation du type de cargaison du projet et lecture de sa description
;-----------------------------------------------------------------------
DEFINE    D_PRJCRG CHARACTER SIZE 16 = "CRGCLE"
TEMPORARY T_PRJCRG CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRJCRG

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRJCRG, &
                PRJCRG OF GPPROJETS


;-------------------------------------------------------------------
; Validation du responsable du projet et lecture de sa description
;-------------------------------------------------------------------
DEFINE    D_PRJRES CHARACTER SIZE 16 = "RESCLE"
TEMPORARY T_PRJRES CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRJRES

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRJRES, &
                PRJRES OF GPPROJETS


;----------------------------------------------------------
; Validation du statut du projet et lecture de sa description
;----------------------------------------------------------
DEFINE    D_PRJSTU CHARACTER SIZE 16 = "PRJSTU"
TEMPORARY T_PRJSTU CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRJSTU

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRJSTU, &
                PRJSTU OF GPPROJETS



;--------------------------------------------------
; Fichier de destruction des descriptions / projets
;--------------------------------------------------
FILE GPPRO_DESC             DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;---------------------------------------------
; Fichier de destruction des clients / projets
;---------------------------------------------
FILE GPPRO_CLIENT           DELETE &
                            ALIAS GPPRO_CLIENT_2 &
                            OPEN 1 &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;----------------------------------------------------
; Fichier de destruction du budget comptable / projet
;----------------------------------------------------
FILE GPPRO_BDG_CTB          DELETE  &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;------------------------------------------------------
; Fichier de destruction du budget d'opération / projet
;------------------------------------------------------
FILE GPPRO_BDG_UNT          DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;---------------------------------------------
; Fichier de destruction des sous-projets
;---------------------------------------------
FILE GPPRO_ACTIVITE         DELETE  &
                            NOITEMS &
                            OPEN 1

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;--------------------------------------------------
; Fichier de destruction des clients / sous-projets
;--------------------------------------------------
FILE GPPRA_CLIENT           DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;-------------------------------------------------------
; Fichier de destruction des descriptions / sous-projets
;-------------------------------------------------------
FILE GPPRA_DESC             DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS



;---------------------------------------------------------
; Fichier de destruction du budget comptable / sous-projet
;---------------------------------------------------------
FILE GPPRA_BDG_CTB          DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS


;-----------------------------------------------------------
; Fichier de destruction du budget d'opération / sous-projet
;-----------------------------------------------------------
FILE GPPRA_BDG_UNT          DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPROJETS, &
                PRJCLE    OF GPPROJETS



;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr132 NOLIST  ; Draw pour les écrans pleine page
USE ustitstd132 NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Fiche de projet " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(3,3,19)(,45,61)(,,64)

FIELD PRJCLE OF GPPROJETS                             &
label "Projet........:" &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LOOKUP NOTON GPPROJETS                          &
        VIA   CIECLE,                                 &
              PRJCLE                                  &
        USING T_CIECLEMNU,                            &
              PRJCLE OF GPPROJETS                     &
        MESSAGE 832; Ce projet existe déjà

FIELD PRJSTU OF GPPROJETS                             &
label "Statut projet.:" &
      ID SAME                                         &
      DEFAULT "A"                                     &
      LOOKUP ON MCCODE_BUIL_PRJSTU                    &
        MESSAGE 833; Ce statut de projet est invalide

FIELD CBIDSC    OF MCCODE_BUIL_PRJSTU                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 15

SKIP 1

ALIGN (3,3,19)
FIELD PRJDSC1 OF GPPROJETS                            &
label "Description...:" &
      HIDDEN

FIELD PRJDSC2 OF GPPROJETS                            &
      LABEL " "                                       &
      HIDDEN

SKIP 1

ALIGN (3,3,19) (,,24)
FIELD PRJCAT OF GPPROJETS                             &
label "Catégorie.....:" &
      HIDDEN                                          &
      LOOKUP ON MCCODE_BUIL_PRJCAT                    &
        MESSAGE 834; Cette catégorie de projet est inexistante

FIELD CBIDSC    OF MCCODE_BUIL_PRJCAT                 &
      ID SAME                                         &
      DISPLAY

SKIP 1

ALIGN (3,3,19) (,,24) (,40,56) (,,61)
FIELD PRJTYP OF GPPROJETS                             &
label "Type projet...:" &
      HIDDEN                                          &
      REQUIRED                                        &
      LOOKUP ON MCCODE_BUIL_PRJTYP                    &
        MESSAGE 835; Ce type de projet est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_PRJTYP                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 15

FIELD PRJCRG OF GPPROJETS                             &
label "Type cargaison:" &
      ID SAME                                         &
      REQUIRED                                        &
      LOOKUP ON MCCODE_BUIL_PRJCRG                    &
        MESSAGE 865; Ce type de cargaison est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_PRJCRG                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 18


SKIP 1

ALIGN (3,3,19) (,,28) (,,69)
FIELD CLICLE OF GPPROJETS                             &
label "Client........:" &
      HIDDEN                                          &
      LOOKUP ON VCLC_CLI                              &
        MESSAGE 836,                                  &
             ON MCREF_ADRESSE                         &
        MESSAGE 836; Ce client est inexistant

FIELD CLINOM OF VCLC_CLI                              &
      ID SAME                                         &
      DISPLAY

FIELD DEVCLE OF VCLC_CLI                              &
      ID SAME                                         &
      DISPLAY                                         &
      PICTURE "/^^^"

ALIGN (,3,19) (,,28)
FIELD RADCLE OF GPPROJETS                             &
label "No adresse....:" &
      ID SAME                                         &
      BWZ                                             &
      DISPLAY

FIELD RADADR1 OF MCREF_ADRESSE                        &
      ID SAME                                         &
      DISPLAY

ALIGN (3,3,19) (,,28)
FIELD PRJRES OF GPPROJETS                             &
label "Responsable...:" &
      HIDDEN                                          &
      REQUIRED                                        &
      LOOKUP ON MCCODE_BUIL_PRJRES                    &
        MESSAGE 837; Ce responsable est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_PRJRES                 &
      ID SAME                                         &
      DISPLAY

ALIGN (3,3,19)
FIELD PRJANN OF GPPROJETS                             &
label "Année.........:" &
      HIDDEN                                          &
      DEFAULT NCON(ASC(SYSDATE)[1:4])

SKIP 1

ALIGN (3,3,49) (,,60) (,,72) (,,82)
FIELD PRJDATPOSEST OF GPPROJETS                        FORMAT YYYYMMDD &
      HIDDEN                                          &
      PATTERN "(####/##/##)|(!0)"                       &
      LABEL "Date / heure positionnement estimée, réelle.:" &
      IF PRJTYP OF GPPROJETS = "MT"

FIELD PRJHREPOSEST OF GPPROJETS                       &
      ID SAME                                         &
      IF PRJTYP OF GPPROJETS = "MT"

FIELD PRJDATPOSREE OF GPPROJETS                        FORMAT YYYYMMDD &
      PATTERN "(####/##/##)|(!0)"                       &
      ID SAME                                         &
      NOENTRY

FIELD PRJHREPOSREE OF GPPROJETS                       &
      ID SAME                                         &
      NOENTRY

FIELD PRJDATDEPEST OF GPPROJETS                        FORMAT YYYYMMDD &
   PATTERN  "####/##/##" &
      HIDDEN                                          &
      LABEL "Date / heure départ estimée, réelle.........:" &
      REQUIRED

FIELD PRJHREDEPEST OF GPPROJETS                       &
      ID SAME                                         &
      REQUIRED

FIELD PRJDATDEPREE OF GPPROJETS                        FORMAT YYYYMMDD &
   PATTERN  "####/##/##" &
      ID SAME                                         &
      NOENTRY

FIELD PRJHREDEPREE OF GPPROJETS                       &
      ID SAME                                         &
      NOENTRY

FIELD PRJDATFINEST OF GPPROJETS                        FORMAT YYYYMMDD &
   PATTERN  "####/##/##" &
      HIDDEN                                          &
      LABEL "Date / heure fin estimée, réelle............:" &
      REQUIRED

FIELD PRJHREFINEST OF GPPROJETS                       &
      ID SAME                                         &
      REQUIRED

FIELD PRJDATFINREE OF GPPROJETS                        FORMAT YYYYMMDD &
   PATTERN  "####/##/##" &
      ID SAME                                         &
      NOENTRY

FIELD PRJHREFINREE OF GPPROJETS                       &
      ID SAME                                         &
      NOENTRY


ALIGN (,3,49) (,,60)
FIELD PRJPBCDATAPP OF GPPROJETS                        FORMAT YYYYMMDD &
   PATTERN  "####/##/##" &
      LABEL "Date d'approbation du budget comptable......:" &
      DISPLAY

FIELD PRJPBCUSRAPP OF GPPROJETS                       &
      DISPLAY

FIELD PRJPBUDATAPP OF GPPROJETS                        FORMAT YYYYMMDD &
   PATTERN  "####/##/##" &
      LABEL "Date d'approbation du budget d'opération....:" &
      DISPLAY

FIELD PRJPBUUSRAPP OF GPPROJETS                       &
      DISPLAY

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRJTYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJTYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRJTYP, T_PRJTYP
    LET FIELDTEXT = TRUNCATE(T_PRJTYP)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRJSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRJSTU, T_PRJSTU
    LET FIELDTEXT = TRUNCATE(T_PRJSTU)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRJCAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJCAT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRJCAT, T_PRJCAT
    LET FIELDTEXT = TRUNCATE(T_PRJCAT)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRJCRG
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJCRG = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRJCRG, T_PRJCRG
    LET FIELDTEXT = TRUNCATE(T_PRJCRG)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRJRES
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJRES = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRJRES, T_PRJRES
    LET FIELDTEXT = TRUNCATE(T_PRJRES)
  END
END




;-------------------------------------------------------------------------------
;
; Appel du popup pour les clients.
;
;
PROCEDURE INPUT CLICLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLICLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp103 PASSING T_CLICLE,     &
                             T_CLICIECLE,  &
                             T_CIECLEMNU MODE F
    LET FIELDTEXT = TRUNC(T_CLICLE)
  END
END

PROCEDURE PROC CLICLE
BEGIN
   IF CLICLE OF GPPROJETS <> " "
   THEN LET RADCLE OF GPPROJETS = RADCLE OF MCREF_ADRESSE
END


;--------------------------------------------
; Date de positionnement estimée
;
PROCEDURE INPUT PRJDATPOSEST
BEGIN
  IF PRJTYP  OF GPPROJETS <> "MT" AND &
     0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    LET FIELDTEXT = "0"
    WARNING 863 ;Date et heure pos. ne doit pas être saisi; Le type de projet
                ;n'est pas "MARITIME".
  END
END


;--------------------------------------------
; Heure de positionnement estimée
;
PROCEDURE INPUT PRJHREPOSEST
BEGIN
  IF PRJTYP  OF GPPROJETS <> "MT" AND &
     0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    LET FIELDTEXT = "0"
    WARNING 863 ;Date et heure pos. ne doit pas être saisi; Le type de projet
                ;n'est pas "MARITIME".
  END
END


;--------------------------------------------
; Date de positionnement réel
;
PROCEDURE INPUT PRJDATPOSREE
BEGIN
  IF PRJTYP  OF GPPROJETS <> "MT" AND &
     0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    LET FIELDTEXT = "0"
    WARNING 863 ;Date et heure pos. ne doit pas être saisi; Le type de projet
                ;n'est pas "MARITIME".
  END
END


;--------------------------------------------
; Heure de positionnement réel.
;
PROCEDURE INPUT PRJHREPOSREE
BEGIN
  IF PRJTYP  OF GPPROJETS <> "MT" AND &
     0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    LET FIELDTEXT = "0"
    WARNING 863 ;Date et heure pos. ne doit pas être saisi; Le type de projet
                ;n'est pas "MARITIME".
  END
END


;--------------------------------------------
; Date de départ estimée
;
PROCEDURE EDIT PRJDATDEPEST
BEGIN
   IF FIELDVALUE < PRJDATPOSEST OF GPPROJETS AND &
               0<> PRJDATPOSEST OF GPPROJETS
   THEN ERROR 838; La date de départ estimée doit être supérieure ou égale
                 ; à la date de pos. estimée.
END

;--------------------------------------------
; Date de départ réelle
;
PROCEDURE EDIT PRJDATDEPREE
BEGIN
   IF FIELDVALUE < PRJDATPOSREE OF GPPROJETS AND &
               0<> PRJDATPOSREE OF GPPROJETS
   THEN ERROR 839; La date de départ réelle doit être supérieure ou égale
                 ; à la date de pos. réelle.
END

;--------------------------------------------
; Date de fin estimée
;
PROCEDURE EDIT PRJDATFINEST
BEGIN
   IF FIELDVALUE < PRJDATDEPEST OF GPPROJETS AND &
               0<> PRJDATDEPEST OF GPPROJETS
   THEN ERROR 840; La date de fin estimée doit être supérieure ou égale
                 ; à la date de départ estimée.
END

;--------------------------------------------
; Date de fin réelle
;
PROCEDURE EDIT PRJDATFINREE
BEGIN
   IF FIELDVALUE < PRJDATDEPREE OF GPPROJETS AND &
               0<> PRJDATDEPREE OF GPPROJETS
   THEN ERROR 841; La date de fin réelle doit être supérieure ou égale
                 ; à la date de départ réelle.
END


;--------------------------------------------
; Année du projet
;
PROCEDURE EDIT PRJANN
BEGIN

   IF FIELDVALUE < NCON(ASC(SYSDATE)[1:4]) OR &
      FIELDVALUE > 2100
   THEN ERROR 849; Année invalide.
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran des informations complémentaires.
;
PROCEDURE DESIGNER D001
BEGIN
  IF NEWRECORD     OF GPPROJETS OR &
     ALTEREDRECORD OF GPPROJETS OR &
     DELETEDRECORD OF GPPROJETS
  THEN ERROR 2507

  RUN SCREEN gp001a PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPROJETS    &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de client supplémentaire.
;
PROCEDURE DESIGNER D002
BEGIN
  IF CLICLE OF GPPROJETS = " "
  THEN ERROR 843; Création du client supplémentaire impossible; Il y a aucun
                ; client principal

  IF NEWRECORD     OF GPPROJETS OR &
     ALTEREDRECORD OF GPPROJETS OR &
     DELETEDRECORD OF GPPROJETS
  THEN ERROR 2507

  LET T_PRJCLE = PRJCLE OF GPPROJETS
  LET T_CLICLE2= CLICLE OF GPPROJETS
  LET T_RADCLE2= RADCLE OF GPPROJETS

  RUN SCREEN gp001b PASSING &
                        T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PRJCLE   , &
                        T_CLICLE2  , &
                        T_RADCLE2    &
                MODE F

  LET CLICLE OF GPPROJETS = T_CLICLE2
  LET RADCLE OF GPPROJETS = T_RADCLE2

  IF CLICLE OF GPPROJETS = " "
  THEN DELETE GPPRO_CLIENT_2

  DISPLAY CLICLE
  DISPLAY CLINOM
  DISPLAY DEVCLE
  DISPLAY RADCLE
  DISPLAY RADADR1
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de suivi comptable.
;
PROCEDURE DESIGNER D003
BEGIN
  IF NEWRECORD     OF GPPROJETS OR &
     ALTEREDRECORD OF GPPROJETS OR &
     DELETEDRECORD OF GPPROJETS
  THEN ERROR 2507

  RUN SCREEN gp001e PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPROJETS    &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de suivi d'opération.
;
PROCEDURE DESIGNER D004
BEGIN
  IF NEWRECORD     OF GPPROJETS OR &
     ALTEREDRECORD OF GPPROJETS OR &
     DELETEDRECORD OF GPPROJETS
  THEN ERROR 2507

  RUN SCREEN gp001c PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPROJETS    &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de piste de vérification.
;
PROCEDURE DESIGNER D005
BEGIN
  IF NEWRECORD     OF GPPROJETS OR &
     ALTEREDRECORD OF GPPROJETS OR &
     DELETEDRECORD OF GPPROJETS
  THEN ERROR 2507

  RUN SCREEN gp001h PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPROJETS    &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de copie de sous-projets.
;
PROCEDURE DESIGNER D006
BEGIN
  IF NEWRECORD     OF GPPROJETS OR &
     ALTEREDRECORD OF GPPROJETS OR &
     DELETEDRECORD OF GPPROJETS
  THEN ERROR 2507

  LET T_ERRCOP = "N"

  WHILE RETRIEVING GPPRO_ACTIVITE_3      &
          VIA     CIECLE,                &
                  PRJCLE                 &
          USING   CIECLE OF GPPROJETS,   &
                  PRJCLE OF GPPROJETS
  BEGIN
     IF PRACLE OF GPPRO_ACTIVITE_3 <> "000"
     THEN LET T_ERRCOP = "O"
  END

  IF T_ERRCOP = "O"
  THEN WARNING 864 ; Le projet doit possèder aucun sous-projet autre qui
                   ; celui primaire (000).

  ELSE RUN SCREEN gp001i PASSING &
                         T_CIECLEMNU, &
                         T_CIENOM   , &
                         GPPROJETS    &
                  MODE E
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPPROJETS &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPPROJETS &
     AND NOT NEWRECORD     OF GPPROJETS &
     AND NOT DELETEDRECORD OF GPPROJETS &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Destruction du projet
  ;
  IF DELETEDRECORD OF GPPROJETS
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le projet est
    ; référée dans la définition MCHIS_PROJET, MCHIS_UNITE,
    ; CPCAP_IMPUTATION, CRCAR_IMPUTATION, CPPAM_IMPUTATION et
    ; CRDPO_IMPUTATION.
    ;
    RUN SCREEN gp001x MODE SAME &
                      PASSING GPPROJETS, &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END

   IF PRJTYP       OF GPPROJETS <> "MT" AND &
     (PRJDATPOSEST OF GPPROJETS <> 0     OR &
      PRJHREPOSEST OF GPPROJETS <> 0     OR &
      PRJDATPOSREE OF GPPROJETS <> 0     OR &
      PRJHREPOSREE OF GPPROJETS <> 0)
   THEN ERROR 863 ;Date et heure pos. ne doit pas être saisi; Le type de projet
                  ;n'est pas "MARITIME".

   IF PRJDATDEPEST OF GPPROJETS < PRJDATPOSEST OF GPPROJETS AND &
      0 <> PRJDATPOSEST OF GPPROJETS
   THEN ERROR 838; La date de départ estimée doit être supérieure ou égale
                 ; à la date de pos. estimée.

   IF PRJDATDEPREE OF GPPROJETS < PRJDATPOSREE OF GPPROJETS AND &
      0 <> PRJDATPOSREE OF GPPROJETS
   THEN ERROR 839; La date de départ réelle doit être supérieure ou égale
                 ; à la date de pos. réelle.

   IF PRJDATFINEST OF GPPROJETS < PRJDATDEPEST OF GPPROJETS AND &
      0 <> PRJDATDEPEST OF GPPROJETS
   THEN ERROR 840; La date de fin estimée doit être supérieure ou égale
                 ; à la date de départ estimée.

   IF PRJDATFINREE OF GPPROJETS < PRJDATDEPREE OF GPPROJETS AND &
      0 <> PRJDATDEPREE OF GPPROJETS
   THEN ERROR 841; La date de fin réelle doit être supérieure ou égale
                 ; à la date de départ réelle.

  IF CLICLE OF GPPROJETS <> " " AND &
     NOT DELETEDRECORD OF GPPROJETS
  THEN BEGIN
       GET GPPRO_CLIENT VIA   CIECLE, &
                              PRJCLE, &
                              PRCCLE   &
                        USING CIECLE OF GPPROJETS, &
                              PRJCLE OF GPPROJETS, &
                              1 OPT
       IF NOT ACCESSOK
       THEN BEGIN
            LET CIECLE OF GPPRO_CLIENT = CIECLE OF GPPROJETS
            LET PRJCLE OF GPPRO_CLIENT = PRJCLE OF GPPROJETS
            LET PRCCLE OF GPPRO_CLIENT = 1
       END

       LET CLICIECLE OF GPPRO_CLIENT = CLICIECLE OF GPPROJETS
       LET CLICLE    OF GPPRO_CLIENT = CLICLE    OF GPPROJETS
       LET RADCLE    OF GPPRO_CLIENT = RADCLE    OF GPPROJETS
  END

  IF NEWRECORD OF GPPROJETS
  THEN BEGIN
    GET GPPRO_ACTIVITE_ADD &
          VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   T_CIECLEMNU, &
                  PRJCLE OF GPPROJETS, &
                  "000" OPT
    IF NOT ACCESSOK
    THEN BEGIN
       LET PRACLE       OF GPPRO_ACTIVITE_ADD  = "000"
       LET PRJCLE       OF GPPRO_ACTIVITE_ADD  = PRJCLE         OF GPPROJETS
       LET PRASTU       OF GPPRO_ACTIVITE_ADD  = PRJSTU         OF GPPROJETS
       LET PRATYP       OF GPPRO_ACTIVITE_ADD  = PRJTYP         OF GPPROJETS
       LET PRACRG       OF GPPRO_ACTIVITE_ADD  = PRJCRG         OF GPPROJETS
       LET PRARES       OF GPPRO_ACTIVITE_ADD  = PRJRES         OF GPPROJETS
       LET PRADSC1      OF GPPRO_ACTIVITE_ADD  = PRJDSC1        OF GPPROJETS
       LET PRADSC2      OF GPPRO_ACTIVITE_ADD  = PRJDSC2        OF GPPROJETS
       LET PRAANN       OF GPPRO_ACTIVITE_ADD  = PRJANN         OF GPPROJETS
       LET PRADATPOSEST OF GPPRO_ACTIVITE_ADD  = PRJDATPOSEST   OF GPPROJETS
       LET PRAHREPOSEST OF GPPRO_ACTIVITE_ADD  = PRJHREPOSEST   OF GPPROJETS
       LET PRADATDEPEST OF GPPRO_ACTIVITE_ADD  = PRJDATDEPEST   OF GPPROJETS
       LET PRAHREDEPEST OF GPPRO_ACTIVITE_ADD  = PRJHREDEPEST   OF GPPROJETS
       LET PRADATFINEST OF GPPRO_ACTIVITE_ADD  = PRJDATFINEST   OF GPPROJETS
       LET PRAHREFINEST OF GPPRO_ACTIVITE_ADD  = PRJHREFINEST   OF GPPROJETS
       LET PRADATPOSREE OF GPPRO_ACTIVITE_ADD  = PRJDATPOSREE   OF GPPROJETS
       LET PRAHREPOSREE OF GPPRO_ACTIVITE_ADD  = PRJHREPOSREE   OF GPPROJETS
       LET PRADATDEPREE OF GPPRO_ACTIVITE_ADD  = PRJDATDEPREE   OF GPPROJETS
       LET PRAHREDEPREE OF GPPRO_ACTIVITE_ADD  = PRJHREDEPREE   OF GPPROJETS
       LET PRADATFINREE OF GPPRO_ACTIVITE_ADD  = PRJDATFINREE   OF GPPROJETS
       LET PRAHREFINREE OF GPPRO_ACTIVITE_ADD  = PRJHREFINREE   OF GPPROJETS
       LET FRMCLE       OF GPPRO_ACTIVITE_ADD  = FRMCLE         OF GPPROJETS
       LET CIECLE       OF GPPRO_ACTIVITE_ADD  = CIECLE         OF GPPROJETS
       LET CLICIECLE    OF GPPRO_ACTIVITE_ADD  = CLICIECLE      OF GPPROJETS
       LET PRAUSRCRE    OF GPPRO_ACTIVITE_ADD  = PRJUSRCRE      OF GPPROJETS
       LET PRADATCRE    OF GPPRO_ACTIVITE_ADD  = PRJDATCRE      OF GPPROJETS
       LET PRAHRECRE    OF GPPRO_ACTIVITE_ADD  = PRJHRECRE      OF GPPROJETS
       LET CLICLE       OF GPPRO_ACTIVITE_ADD  = CLICLE         OF GPPROJETS
    END

    IF CLICLE OF GPPROJETS <> " "
    THEN BEGIN
         GET GPPRA_CLIENT_ADD VIA   CIECLE, &
                                    PRJCLE, &
                                    PRACLE, &
                                    PCLCLE  &
                              USING CIECLE OF GPPROJETS, &
                                    PRJCLE OF GPPROJETS, &
                                    "000",               &
                                    1 OPTIONAL
         IF NOT ACCESSOK
         THEN BEGIN
              LET CIECLE    OF GPPRA_CLIENT_ADD = CIECLE OF GPPROJETS
              LET PRJCLE    OF GPPRA_CLIENT_ADD = PRJCLE OF GPPROJETS
              LET PRACLE    OF GPPRA_CLIENT_ADD = "000"
              LET PCLCLE    OF GPPRA_CLIENT_ADD = 1
              LET CLICIECLE OF GPPRA_CLIENT_ADD = CLICIECLE OF GPPROJETS
              LET CLICLE    OF GPPRA_CLIENT_ADD = CLICLE    OF GPPROJETS
              LET RADCLE    OF GPPRA_CLIENT_ADD = RADCLE    OF GPPROJETS
         END
    END
  END
END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                  x
******************************* Fiche de projet *******************************
* Projet........: xxxxxxxx                  Statut projet.: x  xxxxxxxxxxxxxxx*
*                                                                             *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                                                                             *
* Catégorie.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxx                              *
*                                                                             *
* Type projet...: xx   xxxxxxxxxxxxxxx Type cargaison: xxxx xxxxxxxxxxxxxxxxxx*
*                                                                             *
* Client........: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx      *
* No adresse....: xxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
* Responsable...: xxxx     xxxxxxxxxxxxxxxxxxxxxxxxx                          *
* Année.........: xxxx                                                        *
*                                                                             *
* Date / heure positionnement estimée, réelle.: xxxxxxxx xxxxx xxxxxxxx xxxxx *
* Date / heure départ estimée, réelle.........: xxxxxxxx xxxxx xxxxxxxx xxxxx *
* Date / heure fin estimée, réelle............: xxxxxxxx xxxxx xxxxxxxx xxxxx *
* Date d'approbation du budget comptable......: xxxxxxxx xxxxxxxxxxxx         *
* Date d'approbation du budget d'opération....: xxxxxxxx xxxxxxxxxxxx         *
*******************************************************************************
@ENDIF

