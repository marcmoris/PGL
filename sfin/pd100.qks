;/T/ Dépisteur Code de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-07
;
;    Description..: Permet de visionner et de choisir un code de granit
;
;    Paramètres...: T_TGRCODGRA ==> - code
;                                   - segment de la clé de recherche
;                                   - peut contenir la valeur @
;                                   - contient la valeur de retour.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd100 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_TGRCODGRA,    &
                       T_CIECLEMNU


TEMPORARY T_CIECLEMNU CHARACTER SIZE 04
TEMPORARY T_TGRCODGRA CHARACTER SIZE 04


USE pd100.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT PRIMARY &
                      OCCURS 11

  ACCESS VIA    CIECLE,         &
                TGRCODGRA       &
         USING  T_CIECLEMNU,    &
                T_TGRCODGRA     &
         ORDERBY CIECLE,        &
                 TGRCODGRA


;-------------------------------------------------------------------------------
;
;
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
USE usdrwfnd NOLIST     ; Draw pour les écrans de choix et consultations

USE ustitoff NOLIST     ; Pour placer le TITLE OFF

SKIP TO 2

ALIGN(3,3,6)(,,11)

CLUSTER OCCURS WITH PDTYPE_GRANIT

FIELD TGRCODGRA    OF PDTYPE_GRANIT HIDDEN LABEL ">>" DISPLAY
FIELD TGRDSCFRA001 OF PDTYPE_GRANIT DISPLAY SIZE 30

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_TGRCODGRA, "@")
  THEN LET T_TGRCODGRA = ""
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_TGRCODGRA = TGRCODGRA OF PDTYPE_GRANIT
  RETURN
END


BUILD


@IF FORMAT

                                       *****************************************
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       *****************************************
@ENDIF

