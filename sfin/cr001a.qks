;/T/ Clients - Informations par compagnies
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 3 juin 1993
;
;    Description..: Saisie des informations par compagnie pour la client.
;                   Si la charte de client est universel, alors l'usager
;                   doit saisir un numéro de compagnie dans cet écran, sinon
;                   c-a-d que la charte de client est par compagnie, alors
;                   la compagnie est la compagnie du menu. Dans ce cas il y a
;                   un lient de un pour un avec le client.
;
;
;    Particularités:
;
;    Tenir compte du paramètre charte client (HLDCHTCLI) dans la table MCHOLDING
;        universelle-----> - represente non à la charte client (HLDCHTCLI=0)
;                          - CLICIECLE égale au caractère de substitution.
;        par compagnie---> - represente oui à la charte client (HLDCHTCLI=1)
;                          - CLICIECLE égale la compagnie du menu.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr001a ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        CRCLIENT

USE ussectmp   NOLIST
    "CR001A"

USE cr001a.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CLICIECLE CHARACTER SIZE  4  ; Compagnie du client.
TEMPORARY T_CIECLEMNU CHARACTER SIZE  4  ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40  ; Nom de la compagnie du menu

;-------------------------------------------------------------------------------
;

;
; Informations sur le holding.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"

FILE CRCLIENT         MASTER

;
; Information par compagnies.
;
FILE CRCLI_CIE        PRIMARY &
                      NOITEM

  ITEM CLICIECLE OF CRCLI_CIE INITIAL CLICIECLE OF CRCLIENT
  ITEM CLICLE    OF CRCLI_CIE INITIAL CLICLE    OF CRCLIENT
  ;
  ; On initialise la compagnie de CRCLI_CIE, si la charte de client est par
  ; compagnie, c'est à dire non-universel.
  ;
  ITEM CIECLE    OF CRCLI_CIE INITIAL T_CIECLEMNU &
                                        IF HLDCHTCLI OF MCHOLDING = "1"
  ;
  ; Lors de la création d'un client, le numéro d'adresse est toujours
  ; à 1 = La première adresse. Sauf pour les client divers, qui n'ont
  ; pas d'adresse permanente.
  ;
  ITEM CLCADRETACPT OF CRCLI_CIE INITIAL 1 IF CLITYP OF CRCLIENT <> "D"
  ITEM CLCADRFAC    OF CRCLI_CIE INITIAL 1 IF CLITYP OF CRCLIENT <> "D"
  ITEM CLCADRLIV    OF CRCLI_CIE INITIAL 1 IF CLITYP OF CRCLIENT <> "D"

;
; Destruction des statistiques du client-compagnie.
;
FILE CRCLI_STAT       DELETE &
                      NOITEM
  ACCES VIA CLICIECLE , &
            CLICLE    , &
            CIECLE      &
        USING CLICIECLE OF CRCLIENT , &
              CLICLE    OF CRCLIENT , &
              CIECLE    OF CRCLI_CIE

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      REFERENCE
  ACCESS VIA CIECLE &
         USING CIECLE OF CRCLI_CIE

;
; Validation du compte à recevoir.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_CAR
  ACCESS VIA CPTCLE &
         USING CLCCPTCAR OF CRCLI_CIE

;
; Validation du compte d'encaisse.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_ENC
  ACCESS VIA CPTCLE &
         USING LBQCPTENC OF CRCLI_CIE

;
; Validation du compte d'encaisse.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_ESC
  ACCESS VIA CPTCLE &
         USING CLCCPTESC OF CRCLI_CIE

;
; Validation du code de taxe fédérale.
;
FILE VTAX_DSC         REFERENCE &
                      ALIAS A_TAX_TPS
  ACCESS VIA TAXCLETYP, &
             TAXCLECOD &
         USING CLCTAXTYPTPS OF CRCLI_CIE, &
               CLCTAXCODTPS OF CRCLI_CIE

;
; Validation du code de taxe provinciale.
;
FILE VTAX_DSC         REFERENCE &
                      ALIAS A_TAX_TVP
  ACCESS VIA TAXCLETYP, &
             TAXCLECOD &
         USING CLCTAXTYPTVP OF CRCLI_CIE, &
               CLCTAXCODTVP OF CRCLI_CIE

;
; Validation du représentant.
;
DEFINE    D_CLCREP CHARACTER SIZE 16 = "CLCREP"
TEMPORARY T_CLCREP CHARACTER SIZE  4
;
FILE VCBI_DSC         REFERENCE &
                      ALIAS A_CBI_CLCREP
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_CLCREP, &
               CLCREP OF CRCLI_CIE

;
; Validation du terme net.
;
FILE VTME_DSC         REFERENCE &
                      ALIAS A_TME_NET
  ACCESS VIA TMECLE &
         USING CLCTERNET OF CRCLI_CIE

;
; Validation du terme escompte 1.
;
FILE VTME_DSC         REFERENCE &
                      ALIAS A_TME_ESC1
  ACCESS VIA TMECLE &
         USING CLCTERESC1 OF CRCLI_CIE

;
; Validation du terme net.
;
FILE VTME_DSC         REFERENCE &
                      ALIAS A_TME_ESC2
  ACCESS VIA TMECLE &
         USING CLCTERESC2 OF CRCLI_CIE

;
; Validation du terme de frais d'administration.
;
FILE VTME_DSC         REFERENCE &
                      ALIAS A_TME_FRS
  ACCESS VIA TMECLE &
         USING CLCTERFRS OF CRCLI_CIE

;
; Validation de l'adresse d'état de compte.
;
FILE MCREF_ADRESSE    REFERENCE &
                      ALIAS A_RAD_ETACPT
  ACCESS VIA REFCIECLE, &
             REFCLETYP, &
             REFCLENUM, &
             RADCLE &
         USING CLICIECLE    OF CRCLIENT, &
               CLIREFTYP    OF CRCLIENT, &
               CLICLE       OF CRCLIENT, &
               CLCADRETACPT OF CRCLI_CIE

;
; Validation de l'adresse de facturation.
;
FILE MCREF_ADRESSE    REFERENCE &
                      ALIAS A_RAD_FAC
  ACCESS VIA REFCIECLE, &
             REFCLETYP, &
             REFCLENUM, &
             RADCLE &
         USING CLICIECLE OF CRCLIENT, &
               CLIREFTYP OF CRCLIENT, &
               CLICLE    OF CRCLIENT, &
               CLCADRFAC OF CRCLI_CIE

;
; Validation de l'adresse de livraison.
;
FILE MCREF_ADRESSE    REFERENCE &
                      ALIAS A_RAD_LIV
  ACCESS VIA REFCIECLE, &
             REFCLETYP, &
             REFCLENUM, &
             RADCLE &
         USING CLICIECLE OF CRCLIENT, &
               CLIREFTYP OF CRCLIENT, &
               CLICLE    OF CRCLIENT, &
               CLCADRLIV OF CRCLI_CIE

;
; Pour les comptes par défaut.
;
FILE MCCOMPTE         DESIGNER &
                      OPEN READ

;
; Pour initialiser la relation CRCLI_CIE...
;
FILE CRCLI_CIE        DESIGNER &
                      OPEN READ &
                      ALIAS A_CLC_DEFAUT


FILE MCCOMPTE ALIAS A_MCCOMPTE  DESIGNER

FILE MCPERIODE_CIE DESIGNER
;-------------------------------------------------------------------------------
;

TEMPORARY T_CIECLE    CHARACTER SIZE  4 ; Popup sur les compagnies

TEMPORARY T_CPTCLE    CHARACTER SIZE  6 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE  4 ; Popup sur les comptes
TEMPORARY T_CPTTYPPAT CHARACTER SIZE  1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE  8 ; Popup sur les comptes

TEMPORARY T_TAXCLETYP CHARACTER SIZE  1 ; Popup sur les taxes
TEMPORARY T_TAXCLECOD CHARACTER SIZE  2 ; Popup sur les taxes
TEMPORARY T_TAXMODPAT CHARACTER SIZE  5 ; Popup sur les taxes

TEMPORARY T_TMECLE    CHARACTER SIZE  4 ; Popup sur terme d'encaissement.
TEMPORARY T_TMECATPAT CHARACTER SIZE 11 ; Popup sur terme d'encaissement.

TEMPORARY T_REFCIECLE CHARACTER        SIZE 4 ; Popup sur les adresses.
TEMPORARY T_REFCLETYP CHARACTER        SIZE 1 ; Popup sur les adresses.
TEMPORARY T_REFCLENUM CHARACTER        SIZE 6 ; Popup sur les adresses.
TEMPORARY T_TYPADR    CHARACTER        SIZE 3 ; Popup sur les adresses.
TEMPORARY T_RADCLE    INTEGER UNSIGNED SIZE 2 ; Popup sur les adresses.

TEMPORARY T_FLGDEL    CHARACTER        SIZE 1 ; Flag pour le sous-écran de
                                              ; destruction

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Informations par compagnies " &
@ELSE
      " Information by companies " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN (3,3,19) (,,26)

FIELD CLICLE OF CRCLIENT HIDDEN ID 05 &
        FIXED

FIELD CLINOM OF CRCLIENT &
        FIXED

FIELD CIECLE OF CRCLI_CIE HIDDEN ID 10 &
        REQUIRED &
        NOCHANGE &
        IF HLDCHTCLI OF MCHOLDING = "0" &
        LOOKUP ON MCCOMPAGNIE &
          MESSAGE 604, & ; cette compagnie n'existe pas
        NOTON CRCLI_CIE  &
          VIA CLICIECLE, &
              CLICLE   , &
              CIECLE     &
          USING CLICIECLE OF CRCLI_CIE, &
                CLICLE    OF CRCLI_CIE, &
                CIECLE    OF CRCLI_CIE  &
          MESSAGE 677 ; cette compagnie est deja referer

FIELD CIENOM OF MCCOMPAGNIE &
        DISPLAY

SKIP 1

;
; On fait deux fois le lookup noton, car le champs CIECLE n'est pas toujours
; saisie. Tout dépend du flag sur la charte de client.
;
FIELD CLCCPTCAR OF CRCLI_CIE HIDDEN ID 15 &
        REQUIRED &
        PATTERN "@" & ; BUG POWERHOUSE, il a faut mettre un pattern, sinon
                      ; la valeur retourné par le popup sur les comptes est
                      ; rejeté.
        LOOKUP ON A_CPT_CAR &
          MESSAGE 665, &   ; compte inexistant
        NOTON CRCLI_CIE  &
          VIA CLICIECLE, &
              CLICLE   , &
              CIECLE     &
          USING CLICIECLE OF CRCLI_CIE, &
                CLICLE    OF CRCLI_CIE, &
                CIECLE    OF CRCLI_CIE  &
          MESSAGE 677 ; cette compagnie est deja referer

FIELD CPTDSC OF A_CPT_CAR &
        DISPLAY

FIELD LBQCPTENC OF CRCLI_CIE HIDDEN ID 20 &
        PATTERN "@" & ; BUG POWERHOUSE, il a faut mettre un pattern, sinon
                      ; la valeur retourné par le popup sur les comptes est
                      ; rejeté.
        REQUIRED &
        LOOKUP ON A_CPT_ENC &
          MESSAGE 665; compte inexistant

FIELD CPTDSC OF A_CPT_ENC &
        DISPLAY

FIELD CLCCPTESC OF CRCLI_CIE HIDDEN ID 22 &
        PATTERN "@" & ; BUG POWERHOUSE, il a faut mettre un pattern, sinon
                      ; la valeur retourné par le popup sur les comptes est
                      ; rejeté.
        REQUIRED &
        LOOKUP ON A_CPT_ESC &
          MESSAGE 665; compte inexistant

FIELD CPTDSC OF A_CPT_ESC &
        DISPLAY

ALIGN(3,3,19) (,,22) (,44,60)

FIELD CLCTAXCODTPS OF CRCLI_CIE HIDDEN ID 25

FIELD TAXDSC OF A_TAX_TPS &
        DISPLAY

FIELD CLCEXPTPS    OF CRCLI_CIE

FIELD CLCTAXCODTVP OF CRCLI_CIE HIDDEN ID 30

FIELD TAXDSC OF A_TAX_TVP &
        DISPLAY

FIELD CLCEXPTVP    OF CRCLI_CIE

ALIGN (3,3,19) (,,26)
FIELD CLCREP    OF CRCLI_CIE &
        HIDDEN ID 35        &
        LOOKUP ON A_CBI_CLCREP &
          MESSAGE 2401 ; ce représentant est inexistante

FIELD CBIDSC OF A_CBI_CLCREP &
        DISPLAY

ALIGN (3,3,19)

FIELD CLCLIMCRT OF CRCLI_CIE HIDDEN ID 35

SKIP
DRAW ,1 TO ,80

TITLE &
@IF FRANCAIS
      " Termes " &
@ELSE
      " Terms " &
@ENDIF
      AT ,1 CENTERED
SKIP

ALIGN (3,3,19) (,,24)

FIELD CLCTERNET  OF CRCLI_CIE HIDDEN ID 40

FIELD TMEDSC OF A_TME_NET &
        DISPLAY

FIELD CLCTERFRS  OF CRCLI_CIE HIDDEN ID 45

FIELD TMEDSC OF A_TME_FRS &
        DISPLAY

FIELD CLCTERESC1 OF CRCLI_CIE HIDDEN ID 50

FIELD TMEDSC OF A_TME_ESC1 &
        DISPLAY

FIELD CLCTERESC2 OF CRCLI_CIE HIDDEN ID 55

FIELD TMEDSC OF A_TME_ESC2 &
        DISPLAY

SKIP

DRAW ,1 TO ,80

TITLE &
@IF FRANCAIS
      " Adresses " &
@ELSE
      " Address " &
@ENDIF
      AT ,1 CENTERED
SKIP

FIELD CLCADRETACPT OF CRCLI_CIE HIDDEN ID 60 &
        NOENTRY &
        LOOKUP ON A_RAD_ETACPT &
          MESSAGE 625 ; cette adresse est inexistante

FIELD RADNOM OF A_RAD_ETACPT &
        DISPLAY

FIELD CLCADRFAC    OF CRCLI_CIE HIDDEN ID 65 &
        NOENTRY &
        LOOKUP ON A_RAD_FAC &
          MESSAGE 625 ; cette adresse est inexistante

FIELD RADNOM OF A_RAD_FAC &
        DISPLAY

FIELD CLCADRLIV    OF CRCLI_CIE HIDDEN ID 70 &
        NOENTRY &
        LOOKUP ON A_RAD_LIV &
          MESSAGE 625 ; cette adresse est inexistante

FIELD RADNOM OF A_RAD_LIV &
        DISPLAY


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT CIECLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 MODE F PASSING T_CIECLE
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; On initialise la relation CRCLI_CIE, selon la première compagnie qui a été
; saisie pour toutes les autres compagnies qui sont saisie par la suite. Le
; but est de rendre la saisie plus rapide.
; Cela s'applique seulement pour une charte de client par compagnie.
;
PROCEDURE PROCESS CIECLE
BEGIN
  GET A_CLC_DEFAUT VIA CLICIECLE, &
                       CLICLE     &
                   USING CLICIECLE OF CRCLIENT, &
                         CLICLE    OF CRCLIENT  &
                   ORDERBY CLICIECLE, &
                           CLICLE   , &
                           CIECLE     &
                   OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    LET CLCCPTCAR    OF CRCLI_CIE = CLCCPTCAR    OF A_CLC_DEFAUT
    LET LBQCPTENC    OF CRCLI_CIE = LBQCPTENC    OF A_CLC_DEFAUT
    LET CLCCPTESC    OF CRCLI_CIE = CLCCPTESC    OF A_CLC_DEFAUT
    LET CLCTAXCODTPS OF CRCLI_CIE = CLCTAXCODTPS OF A_CLC_DEFAUT
    LET CLCEXPTPS    OF CRCLI_CIE = CLCEXPTPS    OF A_CLC_DEFAUT
    LET CLCTAXCODTVP OF CRCLI_CIE = CLCTAXCODTVP OF A_CLC_DEFAUT
    LET CLCEXPTVP    OF CRCLI_CIE = CLCEXPTVP    OF A_CLC_DEFAUT
    LET CLCTERNET    OF CRCLI_CIE = CLCTERNET    OF A_CLC_DEFAUT
    LET CLCTERFRS    OF CRCLI_CIE = CLCTERFRS    OF A_CLC_DEFAUT
    LET CLCTERESC1   OF CRCLI_CIE = CLCTERESC1   OF A_CLC_DEFAUT
    LET CLCTERESC2   OF CRCLI_CIE = CLCTERESC2   OF A_CLC_DEFAUT

    DISPLAY CLCCPTCAR    OF CRCLI_CIE
    DISPLAY CPTDSC       OF A_CPT_CAR
    DISPLAY LBQCPTENC    OF CRCLI_CIE
    DISPLAY CPTDSC       OF A_CPT_ENC
    DISPLAY CLCCPTESC    OF CRCLI_CIE
    DISPLAY CPTDSC       OF A_CPT_ESC
    DISPLAY CLCTAXCODTPS OF CRCLI_CIE
    DISPLAY TAXDSC       OF A_TAX_TPS
    DISPLAY CLCEXPTPS    OF CRCLI_CIE
    DISPLAY CLCTAXCODTVP OF CRCLI_CIE
    DISPLAY TAXDSC       OF A_TAX_TVP
    DISPLAY CLCEXPTVP    OF CRCLI_CIE
    DISPLAY CLCTERNET    OF CRCLI_CIE
    DISPLAY TMEDSC       OF A_TME_NET
    DISPLAY CLCTERFRS    OF CRCLI_CIE
    DISPLAY TMEDSC       OF A_TME_FRS
    DISPLAY CLCTERESC1   OF CRCLI_CIE
    DISPLAY TMEDSC       OF A_TME_ESC1
    DISPLAY CLCTERESC2   OF CRCLI_CIE
    DISPLAY TMEDSC       OF A_TME_ESC2
  END
END


;-------------------------------------------------------------------------------
;
;
; Comptes à recevoir, Popup sur les comptes.
;
;
PROCEDURE INPUT CLCCPTCAR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "CR"
    LET T_PECCLE    = " "
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
  ;
  ; On met par défaut le premier comptes à recevoir du plan comptable
  ; dans la devise du client.
  ;
  IF ENTRYMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 = SIZE(TRUNCATE(CLCCPTCAR OF CRCLI_CIE))
  THEN BEGIN
    GET MCCOMPTE VIA CPTCAT, &
                     DEVCLE  &
                 USING "CR", &
                       DEVCLE OF CRCLIENT &
                 OPTIONAL
    IF ACCESSOK
    THEN LET FIELDTEXT = CPTCLE OF MCCOMPTE
  END
  ;
  ; Il faut mettre l'ancienne valeur dans le fieldtext à cause de l'option
  ; REQUIRED sur le champs, tout cela est du à la procédure PROCESS CIECLE.
  ;
  IF     ENTRYMODE &
     AND 0 = SIZE(FIELDTEXT) &
     AND 0 <> SIZE(TRUNCATE(CLCCPTCAR OF CRCLI_CIE))
  THEN LET FIELDTEXT = CLCCPTCAR OF CRCLI_CIE
END


;-------------------------------------------------------------------------------
;
;
; Validation du Compte à recevoir.
;
;
PROCEDURE EDIT CLCCPTCAR
BEGIN
  IF DEVCLE OF CRCLIENT <> DEVCLE OF A_CPT_CAR
  THEN ERROR 711 ;Le compte doit être dans la devise du client.

  IF CPTCAT OF A_CPT_CAR <> "CR"
  THEN ERROR 712 ;Le compte doit être un C.A.R

END
;--------------------
PROCEDURE PROCESS CLCCPTCAR
BEGIN
  GET A_MCCOMPTE VIA CPTCAT,CPTCLE USING "CR",CLCCPTCAR OF CRCLI_CIE OPT
  IF ACCESSOK
  THEN BEGIN
     GET MCPERIODE_CIE VIA CIECLE,PCCSTUCR USING T_CIECLEMNU,"C" OPT
     IF ACCESSOK
     THEN BEGIN
       IF CPTPECDEBA OF A_MCCOMPTE = "" OR &
          (CPTPECDEBI OF A_MCCOMPTE <> "" AND &
          CPTPECDEBI OF A_MCCOMPTE < PECCLE OF MCPERIODE_CIE)
       THEN BEGIN
           ERROR "Compte inactif"
           ACCEPT CLCCPTCAR OF CRCLI_CIE
      END
    END
     ELSE BEGIN
        ERROR "Période courante C/R non trouve"
        ACCEPT CLCCPTCAR OF CRCLI_CIE
    END
  END
  ELSE BEGIN
      ERROR "Compte non trouve dans la charte"
      ACCEPT CLCCPTCAR OF CRCLI_CIE
  END
END

;-------------------------------------------------------------------------------
;
;
; Comptes d'encaisse, Popup sur les comptes.
;
;
PROCEDURE INPUT LBQCPTENC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "EN"
    LET T_PECCLE    = " "
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
  ;
  ; On met par défaut le premier compte d'encaisse du plan comptable.
  ;
  IF ENTRYMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 = SIZE(TRUNCATE(LBQCPTENC OF CRCLI_CIE))
  THEN BEGIN
    GET MCCOMPTE VIA CPTCAT &
                 USING "EN" &
                 OPTIONAL
    IF ACCESSOK
    THEN LET FIELDTEXT = CPTCLE OF MCCOMPTE
  END
  ;
  ; Il faut mettre l'ancienne valeur dans le fieldtext à cause de l'option
  ; REQUIRED sur le champs, tout cela est du à la procédure PROCESS CIECLE.
  ;
  IF     ENTRYMODE &
     AND 0 = SIZE(FIELDTEXT) &
     AND 0 <> SIZE(TRUNCATE(LBQCPTENC OF CRCLI_CIE))
  THEN LET FIELDTEXT = LBQCPTENC OF CRCLI_CIE
END


;-------------------------------------------------------------------------------
;
;
; Validation du compte d'encaisse.
;
;
PROCEDURE EDIT LBQCPTENC
BEGIN
  IF CPTCAT OF A_CPT_ENC <> "EN"
  THEN ERROR 697 ;Le compte doit être un compte d'encaisse.

  IF     DEVCLE OF A_CPT_ENC <> DEVCLE OF MCHOLDING &
     AND DEVCLE OF A_CPT_ENC <> DEVCLE OF CRCLIENT
  THEN ERROR 726 ; Compte interdit due à la devise du compte.
END


;-------------------------------------------------------------------------------
;
;
; Comptes d'escompte, Popup sur les comptes.
;
;
PROCEDURE INPUT CLCCPTESC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "ES"
    LET T_PECCLE    = " "
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
  ;
  ; On met par défaut le premier compte d'escompte du plan comptable.
  ;
  IF ENTRYMODE AND &
     0 = SIZE( FIELDTEXT ) AND &
     0 = SIZE( TRUNCATE( CLCCPTESC OF CRCLI_CIE ) )
  THEN BEGIN
    GET MCCOMPTE VIA CPTCAT &
                 USING "ES" &
                 OPTIONAL
    IF ACCESSOK
    THEN LET FIELDTEXT = CPTCLE OF MCCOMPTE
  END
  ;
  ; Il faut mettre l'ancienne valeur dans le fieldtext à cause de l'option
  ; REQUIRED sur le champs, tout cela est du à la procédure PROCESS CIECLE.
  ;
  IF     ENTRYMODE &
     AND 0 = SIZE(FIELDTEXT) &
     AND 0 <> SIZE(TRUNCATE(CLCCPTESC OF CRCLI_CIE))
  THEN LET FIELDTEXT = CLCCPTESC OF CRCLI_CIE
END


;-------------------------------------------------------------------------------
;
;
; Validation du compte d'escompte.
;
;
PROCEDURE EDIT CLCCPTESC
BEGIN
  IF CPTCAT OF A_CPT_ESC <> "ES"
  THEN ERROR 617 ;Le compte doit être un compte d'escompte.

  IF     DEVCLE OF A_CPT_ESC <> DEVCLE OF MCHOLDING &
     AND DEVCLE OF A_CPT_ESC <> DEVCLE OF CRCLIENT
  THEN ERROR 726 ; Compte interdit due à la devise du compte.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les codes de taxes fédérales.
;
;
PROCEDURE INPUT CLCTAXCODTPS
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TAXCLETYP = "F"
    LET T_TAXCLECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TAXMODPAT = "@"
    RUN SCREEN mc111 MODE F PASSING T_TAXCLETYP, &
                                    T_TAXCLECOD, &
                                    T_TAXMODPAT
    LET FIELDTEXT = TRUNC(T_TAXCLECOD)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du code de TPS, le code est optionel.
;
;
PROCEDURE EDIT CLCTAXCODTPS
BEGIN
  GET A_TAX_TPS OPTIONAL
  IF 0 <> SIZE( TRUNCATE(FIELDTEXT) ) AND &
     NOT ACCESSOK
  THEN ERROR 672; Ce code de taxe n'existe pas.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les codes de taxes provincial.
;
;
PROCEDURE INPUT CLCTAXCODTVP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TAXCLETYP = "P"
    LET T_TAXCLECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TAXMODPAT = "@"
    RUN SCREEN mc111 MODE F PASSING T_TAXCLETYP, &
                                    T_TAXCLECOD, &
                                    T_TAXMODPAT
    LET FIELDTEXT = TRUNC(T_TAXCLECOD)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du code de TVP, le code est optionel.
;
;
PROCEDURE EDIT CLCTAXCODTVP
BEGIN
  GET A_TAX_TVP OPTIONAL
  IF 0 <> SIZE( TRUNCATE(FIELDTEXT) ) AND &
     NOT ACCESSOK
  THEN ERROR 672; Ce code de taxe n'existe pas.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les catégories de client(bilingue).
;
;
PROCEDURE INPUT CLCREP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLCREP = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_CLCREP, T_CLCREP
    LET FIELDTEXT = TRUNC(T_CLCREP)
  END
END


;-------------------------------------------------------------------------------
;
;
; Terme net, Popup sur les termes d'encaissements.
;
;
PROCEDURE INPUT CLCTERNET
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TMECATPAT = "NET"
    RUN SCREEN cr104 MODE F PASSING T_TMECLE, &
                                    T_TMECATPAT
    LET FIELDTEXT = TRUNCATE( T_TMECLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du terme net, le code est optionel.
;
;
PROCEDURE EDIT CLCTERNET
BEGIN
  GET A_TME_NET OPTIONAL

  IF 0 <> SIZE( TRUNCATE(FIELDTEXT) )
  THEN BEGIN
    IF NOT ACCESSOK
    THEN ERROR 639 ; Ce terme est inexistant

    IF TMECAT OF A_TME_NET <> "NET"
    THEN ERROR 640 ; la categorie du terme ne s'applique pas
  END
END


;-------------------------------------------------------------------------------
;
;
; Terme Frais d'administration, Popup sur les termes d'encaissements.
;
;
PROCEDURE INPUT CLCTERFRS
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TMECATPAT = "FRS"
    RUN SCREEN cr104 MODE F PASSING T_TMECLE, &
                                    T_TMECATPAT
    LET FIELDTEXT = TRUNCATE( T_TMECLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du terme Frais d'administration, le code est optionel.
;
;
PROCEDURE EDIT CLCTERFRS
BEGIN
  GET A_TME_FRS OPTIONAL

  IF 0 <> SIZE( TRUNCATE(FIELDTEXT) )
  THEN BEGIN
    IF NOT ACCESSOK
    THEN ERROR 639 ; Ce terme est inexistant

    IF TMECAT OF A_TME_FRS <> "FRS"
    THEN ERROR 640 ; la categorie du terme ne s'applique pas
  END
END


;-------------------------------------------------------------------------------
;
;
; Terme Escompte 1, Popup sur les termes d'encaissements.
;
;
PROCEDURE INPUT CLCTERESC1
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TMECATPAT = "ESC"
    RUN SCREEN cr104 MODE F PASSING T_TMECLE, &
                                    T_TMECATPAT
    LET FIELDTEXT = TRUNCATE( T_TMECLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du terme escompte 1, le code est optionel.
;
;
PROCEDURE EDIT CLCTERESC1
BEGIN
  GET A_TME_ESC1 OPTIONAL

  IF 0 <> SIZE( TRUNCATE(FIELDTEXT) )
  THEN BEGIN
    IF NOT ACCESSOK
    THEN ERROR 639 ; Ce terme est inexistant

    IF TMECAT OF A_TME_ESC1 <> "ESC"
    THEN ERROR 640 ; la categorie du terme ne s'applique pas
  END
END


;-------------------------------------------------------------------------------
;
;
; Terme Escompte 2, Popup sur les termes d'encaissements.
;
;
PROCEDURE INPUT CLCTERESC2
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TMECATPAT = "ESC"
    RUN SCREEN cr104 MODE F PASSING T_TMECLE, &
                                    T_TMECATPAT
    LET FIELDTEXT = TRUNCATE( T_TMECLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du terme escompte 2, le code est optionel.
;
;
PROCEDURE EDIT CLCTERESC2
BEGIN
  GET A_TME_ESC2 OPTIONAL

  IF 0 <> SIZE( TRUNCATE(FIELDTEXT) )
  THEN BEGIN
    IF NOT ACCESSOK
    THEN ERROR 639 ; Ce terme est inexistant

    IF TMECAT OF A_TME_ESC2 <> "ESC"
    THEN ERROR 640 ; la categorie du terme ne s'applique pas
  END
END


;-------------------------------------------------------------------------------
;
;
; Adresse de l'état de compte, Popup sur les adresses.
;
;
PROCEDURE INPUT CLCADRETACPT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFCIECLE = CLICIECLE OF CRCLIENT
    LET T_REFCLETYP = CLIREFTYP OF CRCLIENT
    LET T_REFCLENUM = CLICLE    OF CRCLIENT
    LET T_TYPADR    = "ETA"
    LET T_RADCLE    = CLCADRETACPT OF CRCLI_CIE
    RUN SCREEN mc108 MODE F PASSING T_REFCIECLE, &
                                    T_REFCLETYP, &
                                    T_REFCLENUM, &
                                    T_TYPADR   , &
                                    T_RADCLE
    LET FIELDTEXT = ASCII(T_RADCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de l'adresse de L'état de compte.
;
;
PROCEDURE EDIT CLCADRETACPT
BEGIN
  IF RADETACPTCR OF A_RAD_ETACPT <> "1"
  THEN ERROR 654; le type d'adresse est incompatible
END


;-------------------------------------------------------------------------------
;
;
; Adresse de facturation, Popup sur les adresses.
;
;
PROCEDURE INPUT CLCADRFAC
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFCIECLE = CLICIECLE OF CRCLIENT
    LET T_REFCLETYP = CLIREFTYP OF CRCLIENT
    LET T_REFCLENUM = CLICLE    OF CRCLIENT
    LET T_TYPADR    = "FAC"
    LET T_RADCLE    = CLCADRFAC OF CRCLI_CIE
    RUN SCREEN mc108 MODE F PASSING T_REFCIECLE, &
                                    T_REFCLETYP, &
                                    T_REFCLENUM, &
                                    T_TYPADR   , &
                                    T_RADCLE
    LET FIELDTEXT = ASCII(T_RADCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de l'adresse de facturation.
;
;
PROCEDURE EDIT CLCADRFAC
BEGIN
  IF RADFACCR OF A_RAD_FAC <> "1"
  THEN ERROR 654; le type d'adresse est incompatible
END


;-------------------------------------------------------------------------------
;
;
; Adresse de livraison, Popup sur les adresses.
;
;
PROCEDURE INPUT CLCADRLIV
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFCIECLE = CLICIECLE OF CRCLIENT
    LET T_REFCLETYP = CLIREFTYP OF CRCLIENT
    LET T_REFCLENUM = CLICLE    OF CRCLIENT
    LET T_TYPADR    = "LIV"
    LET T_RADCLE    = CLCADRLIV OF CRCLI_CIE
    RUN SCREEN mc108 MODE F PASSING T_REFCIECLE, &
                                    T_REFCLETYP, &
                                    T_REFCLENUM, &
                                    T_TYPADR   , &
                                    T_RADCLE
    LET FIELDTEXT = ASCII(T_RADCLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Validation de l'adresse de livraison.
;
;
PROCEDURE EDIT CLCADRLIV
BEGIN
  IF RADLIVCR OF A_RAD_LIV <> "1"
  THEN ERROR 654; le type d'adresse est incompatible
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Recherche de la compagnie en fonction du type de charte client.
;
; Si la charte du client est universelle, alors on demande à
; l'usager d'entrer un numéro de compagnie pour retrouver l'information
; de CRCLI_CIE. Mais si la charte est par compagnie, alors on va
; automatiquement chercher l'enregistrement de CRCLI_CIE correspondant
; à la compagnie du menu.
;
;
PROCEDURE PATH
BEGIN
  IF HLDCHTCLI OF MCHOLDING = "0"
  THEN BEGIN
    REQUEST CIECLE OF CRCLI_CIE
    IF PROMPTOK
    THEN LET PATH = 1
    ELSE LET PATH = 2
  END
  ELSE LET PATH = 99
END
;
;
PROCEDURE FIND
BEGIN
  IF PATH = 1
  THEN GET CRCLI_CIE VIA CLICIECLE , &
                         CLICLE    , &
                         CIECLE      &
                     USING CLICIECLE OF CRCLIENT , &
                           CLICLE    OF CRCLIENT , &
                           CIECLE    OF CRCLI_CIE

  IF PATH = 2
  THEN GET CRCLI_CIE VIA  CLICIECLE , &
                          CLICLE      &
                     USING CLICIECLE OF CRCLIENT , &
                           CLICLE    OF CRCLIENT   &
                     ORDERBY CLICIECLE , &
                             CLICLE    , &
                             CIECLE

  IF PATH = 99
  THEN GET CRCLI_CIE VIA CLICIECLE , &
                         CLICLE    , &
                         CIECLE      &
                     USING CLICIECLE OF CRCLIENT , &
                           CLICLE    OF CRCLIENT , &
                           T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CRCLI_CIE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CRCLI_CIE &
     AND NOT NEWRECORD     OF CRCLI_CIE &
     AND NOT DELETEDRECORD OF CRCLI_CIE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Le timbre de modification est mis à jour si l'usager modifie
  ; la table CRCLI_CIE.
  ;
  IF        NEWRECORD OF CRCLI_CIE &
     OR ALTEREDRECORD OF CRCLI_CIE &
     OR DELETEDRECORD OF CRCLI_CIE
  THEN BEGIN
    LET CLIDATMOD OF CRCLIENT = SYSDATE
    LET CLIHREMOD OF CRCLIENT = SYSTIME / 10000
    LET CLIUSRMOD OF CRCLIENT = LOGONID
  END
  ;
  ; Destruction du client-compagnie.
  ;
  IF DELETEDRECORD OF CRCLI_CIE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le client-compagnie est référé.
    ;
    RUN SCREEN cr001ax MODE SAME &
                       PASSING CRCLI_CIE , &
                               T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END

BUILD

@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************* Informations par compagnies **************************
* Client........: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
* Compagnie.....: xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
*                                                                              *
* Compte CAR....: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
* Cpt encaisse..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
* Cpt d'escompte: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
* Code taxe TPS.: xx xxxxxxxxxxxxxxxxxxxx  Exemption TPS.: xxxxxxxxxxxxxxx     *
* Code taxe TVQ.: xx xxxxxxxxxxxxxxxxxxxx  Exemption TVQ.: xxxxxxxxxxxxxxx     *
* Représentant..: xxx    xxxxxxxxxxxxxxxxxxxxxxxxx                             *
* Limite crédit.: xxxxxxxxxxx                                                  *
************************************ Termes ************************************
* Terme net.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Frais adm.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Escompte 1....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Escompte 2....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*********************************** Adresses ***********************************
* Etat compte...: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Facturation...: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Livraison.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
********************************************************************************
@ENDIF
