;/T/ Selection des blocs 
;
;/P/ Programmeur..: Francois Richard
;    Date.........: 1999-10-20
;    Description..: Ecran qui sert a assigner les bloc a chaque ligne de detail
;                   des commande interne de la gestion des bloc. 
;
;/M/ Programmeur..: Francois Richard
;    Date.........: 2000-01-05
;    Description..: Lorsqu'un bloc est assigne a un projet multiple, 
;                   il faut le placer le client de la commande sur la
;                   carte de bloc
;
;-------------------------------------------------------------------------------

SCREEN pd210q ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              ACTIVITIES FIND,CHANGE        &  
              AUTOUPDATE                    &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        T_ECRAPP,           &
                        T_CLICLE,           & 
                        PDDETAIL_C_I 
                        

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD210Q"

;-------------------------------------------------------------------------------
;
; Panorama d'aide
;
;USE pd210q.hlp NOLIST

;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;

USE ustrasse NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Carte des bloc              " ACTION DESIGNER D001 MARK
@ENDIF

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_ECRAPP    CHARACTER SIZE 5  RESET AT STARTUP ; ecran apellant
TEMPORARY T_CLICLE    CHARACTER SIZE 6  RESET AT STARTUP ; No client

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Commande interne
;
FILE PDDETAIL_C_I MASTER

;-------------------------------------------------------------------------------
;
; Détail de la commande
;
FILE IFBLOC PRIMARY OCCURS 14 NOITEM 

  ACCESS VIA     CIECLE,                    &
                 TGRCODGRA                  &
         USING   T_CIECLEMNU,               &
                 TGRCODGRA OF PDDETAIL_C_I  &
         ORDERBY CIECLE,                    &
                 TGRCODGRA,                 &  
                 IFCCLE,                    & 
                 BLCCLE,                    &
                 BLCRED  

       SELECT IF ( &
                   PJTABR OF IFBLOC = PJTABR OF PDDETAIL_C_I &
                   OR &
                   0 = SIZE(TRUNC(PJTABR OF IFBLOC)) & 
                 ) &
                 AND &
                 ( &
                   ( &
                     PJTANN OF IFBLOC = PJTANN OF PDDETAIL_C_I &
                     AND &
                     COMNUMCOMINT OF IFBLOC = COMNUMCOMINT OF PDDETAIL_C_I &
                   ) &
                   OR &
                   ( &
                     PJTANN OF IFBLOC = PJTANN OF PDDETAIL_C_I &
                     AND &
                     0 = SIZE(TRUNCATE(COMNUMCOMINT OF IFBLOC)) &
                   ) &
                   OR &
                   ( &
                     0 = SIZE(TRUNCATE(PJTANN OF IFBLOC)) &
                     AND &
                     0 = SIZE(TRUNCATE(COMNUMCOMINT OF IFBLOC)) &
                   ) & 
                 ) &
                 AND &
                 ( &
                   CATCLE OF IFBLOC = CATCLE OF PDDETAIL_C_I &
                   OR &
                   0 = SIZE(TRUNCATE(CATCLE OF PDDETAIL_C_I)) &
                 ) &
                 AND &
                 ( &
                   CLRCLE OF IFBLOC = CLRCLE OF PDDETAIL_C_I &
                   OR &
                   0 = SIZE(TRUNCATE(CLRCLE OF PDDETAIL_C_I)) &
                 ) &
                 AND &
                 ( &
                   QLTCLE OF IFBLOC = QLTCLE OF PDDETAIL_C_I &
                   OR &
                   0 = SIZE(TRUNCATE(QLTCLE OF PDDETAIL_C_I)) &
                 ) &
                 AND &
                 0 = SIZE(TRUNCATE(DCINUMLIG OF IFBLOC))    
                   
;------------------------------------------------------------------------------
;
; Sert a aller chercher le total des bloc en metre cube
;
FILE IFBLOC DESIGNER ALIAS A_IFBLOC

;------------------------------------------------------------------------------
;
; Commande interne
;
FILE PDCOMMAN_INTERNE DESIGNER

;------------------------------------------------------------------------------
;
; Projets
;
FILE PDPROJET DESIGNER

;-------------------------------------------------------------------------------
;
; Variable servant à l'affichage
;
DEFINE D_COUTBL    FLOAT SIZE 8 = &
     ((BLCMNTM3 OF IFBLOC + BLCMNTEXPM3 OF IFBLOC) &
      * BLCM3TOT OF IFBLOC) / 100

;------------------------------------------------------------------------------
;
; Variables servant a appelle if001
;
TEMPORARY T_TGRCODGRA_ORI    CHARACTER SIZE 3
TEMPORARY T_IFCCLE_ORI       CHARACTER SIZE 2
TEMPORARY T_BLCCLE_ORI       CHARACTER SIZE 6
TEMPORARY T_BLCRED_ORI       CHARACTER SIZE 1                                     
;------------------------------------------------------------------------------
;
; Variable servant a mettre en hilite une ligne complete
;
TEMPORARY T_BLANC1 CHARACTER SIZE 3 RESET AT STARTUP
TEMPORARY T_BLANC2 CHARACTER SIZE 7 RESET AT STARTUP
TEMPORARY T_BLANC3 CHARACTER SIZE 7 RESET AT STARTUP
TEMPORARY T_BLANC4 CHARACTER SIZE 6 RESET AT STARTUP
TEMPORARY T_BLANC5 CHARACTER SIZE 8 RESET AT STARTUP
TEMPORARY T_BLANC6 CHARACTER SIZE 3 RESET AT STARTUP

TEMPORARY T_TOTBLOC FLOAT SIZE 10
DEFINE D_LABEL1 CHARACTER SIZE 1 = "-"
DEFINE D_LABEL2 CHARACTER SIZE 1 = "-"

;------------------------------------------------------------------------------
;
; Temporaire servant a savoir si la ligne de cluster a ete selectionne
;
TEMPORARY T_SELECT CHARACTER SIZE 1 OCCURS WITH IFBLOC

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Assignation des blocs " &
@ELSE
      " %%%Assignation des blocs " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4

ALIGN (,3,19) (,21,22) (,24,25) (,35,51)

FIELD PJTABR           OF PDDETAIL_C_I     &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD PJTANN           OF PDDETAIL_C_I     &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD COMNUMCOMINT     OF PDDETAIL_C_I     &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD DCINUMLIG        OF PDDETAIL_C_I     &
        LABEL "Numero ligne..:"            &
        DISPLAY                             


DRAW 5,1 TO 5,80


SKIP TO LINE  7


TITLE &
@IF FRANCAIS
     " No bloc         Longeur    Hauteur    Largeur   Dimension(M3)   Cout total" &
@ENDIF
      AT ,2


SKIP TO LINE 8

ALIGN (02,02,03)(,,06)(,,08)(,,09)(,,15)(,,16)(,,17)(,,20)(,,24)(,,31)(,,35) &
      (,,42)(,,46)(,,52)(,,58)(,,66)(,,77)

CLUSTER OCCURS WITH IFBLOC  

FIELD TGRCODGRA OF IFBLOC                       &
      HILITE DISPLAY INVERSE IF T_SELECT = "1"  &
      ID 01                                     &
      LABEL " "                                 &
      DISPLAY                                   &
      HIDDEN                            
 
FIELD IFCCLE OF IFBLOC                         &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY                   

HILITE DATA DEFAULT

FIELD D_LABEL1                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD BLCCLE OF IFBLOC                         &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY                 

HILITE DATA DEFAULT

FIELD D_LABEL2                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD BLCRED OF IFBLOC                          &
      HILITE DISPLAY INVERSE IF T_SELECT = "1"  & 
      DISPLAY                 

HILITE DATA DEFAULT

FIELD T_BLANC1                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD BLCVENDULONG OF IFBLOC                   &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      LABEL " "                                & 
      ID SAME                                  & 
      DISPLAY

HILITE DATA DEFAULT

FIELD T_BLANC2                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD BLCVENDUHAU OF IFBLOC                    &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      LABEL " "                                &
      DISPLAY 

HILITE DATA DEFAULT

FIELD T_BLANC3                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD BLCVENDULAR OF IFBLOC                    &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      LABEL " "                                &
      ID SAME                                  &
      DISPLAY

HILITE DATA DEFAULT

FIELD T_BLANC4                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD BLCM3TOT OF IFBLOC                       &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      LABEL " "                                &
      ID SAME                                  &
      DISPLAY

HILITE DATA DEFAULT

FIELD T_BLANC5                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

HILITE DATA UNDERLINE HALFTONE

FIELD D_COUTBL                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      PICTURE " ^^^,^^^.^^"                    &
      DISPLAY                                

HILITE DATA DEFAULT

FIELD T_BLANC6                                 &
      HILITE DISPLAY INVERSE IF T_SELECT = "1" &
      DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1 
BEGIN

  IF T_SELECT = "1"
  THEN LET T_SELECT = "0"
  ELSE BEGIN

    LET T_SELECT = "1"

    ; Valide pour savoir si la quantite du bloc selectionne + la quantite
    ; des autres blocs deja assigne + la quantite des blocs deja selectionnees
    ; depasse la quantite commande inscrite sur la ligne de detail

    ; Variable servant a cumuler le total en m3 de blocs de la ligne de
    ; commande + les blocs selectionnes de l'ecran
    LET T_TOTBLOC = 0 

    ; Calcul des blocs deja assignes
    WHILE RETRIEVING A_IFBLOC                      &
         VIA     CIECLE,                           &
                 PJTABR,                           &
                 PJTANN,                           &
                 COMNUMCOMINT,                     &
                 DCINUMLIG                         &
         USING   T_CIECLEMNU,                      &
                 PJTABR           OF PDDETAIL_C_I, &
                 PJTANN           OF PDDETAIL_C_I, &
                 COMNUMCOMINT     OF PDDETAIL_C_I, &
                 DCINUMLIG        OF PDDETAIL_C_I
    BEGIN
      LET T_TOTBLOC = T_TOTBLOC + (BLCM3TOT OF A_IFBLOC / 100)
    END

    ; Ajout au total des blocs les blocs selectionnes a l'ecran
    FOR IFBLOC
    BEGIN
      IF T_SELECT = "1" 
      THEN LET T_TOTBLOC = T_TOTBLOC + (BLCM3TOT OF IFBLOC / 100)
    END 
 
    ; Si la quantite totale depasse la quantite commande inscrite sur la
    ; ligne de detail alors il y a erreur
    IF T_TOTBLOC > DCIQTECMD OF PDDETAIL_C_I
    THEN BEGIN
      LET T_SELECT = "0"
      WARNING 3294 ; PD394E La quantite assigne depasse la quantite commande
    END
  END

  DISPLAY TGRCODGRA OF IFBLOC
  DISPLAY IFCCLE OF IFBLOC
  DISPLAY D_LABEL1
  DISPLAY BLCCLE OF IFBLOC
  DISPLAY D_LABEL2
  DISPLAY BLCRED OF IFBLOC
  DISPLAY T_BLANC1
  DISPLAY BLCVENDULONG OF IFBLOC
  DISPLAY T_BLANC2
  DISPLAY BLCVENDUHAU OF IFBLOC
  DISPLAY T_BLANC3
  DISPLAY BLCVENDULAR OF IFBLOC
  DISPLAY T_BLANC4
  DISPLAY BLCM3TOT OF IFBLOC
  DISPLAY T_BLANC5
  DISPLAY D_COUTBL
  DISPLAY T_BLANC6  

END

;------------------------------------------------------------------------------
;
; Appel de l'ecran if001
;
PROCEDURE DESIGNER D001
BEGIN

 LET T_TGRCODGRA_ORI = TGRCODGRA OF IFBLOC
 LET T_IFCCLE_ORI = IFCCLE OF IFBLOC
 LET T_BLCCLE_ORI = BLCCLE OF IFBLOC
 LET T_BLCRED_ORI = BLCRED OF IFBLOC

 RUN SCREEN if001 MODE F         & 
        PASSING T_CIECLEMNU,     &
                T_CIENOM,        &
                T_TGRCODGRA_ORI, &
                T_IFCCLE_ORI,    &
                T_BLCCLE_ORI,    &
                T_BLCRED_ORI 

END

;------------------------------------------------------------------------------
;
; - Mise a jour des blocs selectionnes pour les attaches a la commande
; - Mise a jour des lignes de commandes dans le cas ou cette derniere
;   est en insertion
;
PROCEDURE PREUPDATE
BEGIN

  ; Initilisation des variables de la ligne de commande a zero
  IF T_ECRAPP = "ifm01"
  THEN BEGIN
    ; Attention ne pas enlever cette ligne car elle risque de servir
    ; a nouveau
    ;LET DCIPRIVEN OF PDDETAIL_C_I = 0
    ;LET DCIPRIUNMPRD OF PDDETAIL_C_I = 0
    LET DCIQTEUNMPRD OF PDDETAIL_C_I = 0
  END 
 
  FOR DISPLAYED IFBLOC
  BEGIN
    IF T_SELECT = "1"
    THEN BEGIN

      ; Initialisation des variables du bloc pour le
      ; rattache a la commande
      LET PJTABR OF IFBLOC = PJTABR OF PDDETAIL_C_I
      LET PJTANN OF IFBLOC = PJTANN OF PDDETAIL_C_I
      LET COMNUMCOMINT OF IFBLOC = COMNUMCOMINT OF PDDETAIL_C_I
      LET DCINUMLIG OF IFBLOC = DCINUMLIG OF PDDETAIL_C_I
      LET COEDATDEB OF IFBLOC = SYSDATE
      LET CLICLE OF IFBLOC = T_CLICLE
      LET BLCDATASSIG OF IFBLOC = SYSDATE
      LET IFLCLE OF IFBLOC = IFLCLE OF PDDETAIL_C_I

      IF T_ECRAPP = "ifm01"
      THEN BEGIN 

          ; Attention ne pas enlever cette ligne car elle risque de servir
          ; a nouveau
          ;LET DCIPRIVEN OF PDDETAIL_C_I = &
          ;    DCIPRIVEN OF PDDETAIL_C_I + &
          ; (((BLCMNTM3 OF IFBLOC + BLCMNTEXPM3 OF IFBLOC) &
          ; * BLCM3TOT OF IFBLOC) / 100)

          ;LET DCIPRIUNMPRD OF PDDETAIL_C_I = DCIPRIVEN OF PDDETAIL_C_I

          LET DCIQTEUNMPRD OF PDDETAIL_C_I = &
          DCIQTEUNMPRD OF PDDETAIL_C_I + (BLCM3TOT OF IFBLOC / 100)
      END

      ; Si le projet est un projet multiple alors le client de la carte 
      ; de bloc sera celui de la commande
      GET PDPROJET                        &
        VIA   CIECLE,                     &
              PJTABR,                     &
              PJTANN                      &
        USING CIECLE OF PDCOMMAN_INTERNE, &
              PJTABR OF PDCOMMAN_INTERNE, &
              PJTANN OF PDCOMMAN_INTERNE  &
        OPTIONAL   
     
      ; Si c'est un projet multiple 
      IF PJTPROMUL OF PDPROJET = "1" 
      THEN BEGIN
 
         ; Va chercher la commande interne
         GET PDCOMMAN_INTERNE                 &
           VIA   CIECLE,                      &
                 PJTABR,                      &
                 PJTANN,                      &
                 COMNUMCOMINT                 &
           USING CIECLE OF PDDETAIL_C_I,      &
                 PJTABR OF PDDETAIL_C_I,      &
                 PJTANN OF PDDETAIL_C_I,      &
                 COMNUMCOMINT OF PDDETAIL_C_I &
           OPTIONAL
     
        ; Assignation du client sur la carte de bloc 
        LET CLICLE OF IFBLOC = CLICLE OF PDCOMMAN_INTERNE   
      END     
    END 
  END
END

BUILD DETAIL LIST
