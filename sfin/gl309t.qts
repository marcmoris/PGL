;/T/ Balance de vérification
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 5 mai 1993
;
;    Description...: Le rapport présente les transactions sommaires sous
;                    forme de balance de vérification. Ainsi les données sont
;                    présentées pour une période et de manière cumulée jusqu'à
;                    cette même période.
;
;    Paramètres....: Compagnie
;                    Période CTB
;/M/ REMY DEMERS 26 MARS 1999
;    CORRECTION DU SELECT QUI NE FONCTIONNAIT PAS POUR L'AN 00
;-------------------------------------------------------------------------------


USE ussetqts NOLIST   ; permet de faire les SET REPORT DEV....

RUN gl309t

REQUEST BALANCE_VERFICATION

ACCESS MCSOLDE_ANNUEL &
  LINK CPTCLE OF MCSOLDE_ANNUEL      &
    TO CPTCLE OF VCPT_DSC OPTIONAL   &
  LINK NACCLE OF VCPT_DSC            &
    TO NACCLE OF VNAC_DSC OPTIONAL   &
  LINK CIECLE OF MCSOLDE_ANNUEL    , &
       UNACLE OF MCSOLDE_ANNUEL      &
    TO CIECLE                      , &
       UNACLE OF MCUNITE_ADM OPTIONAL



CHOOSE CIECLE PARM


DEFINE D_PECCLE CHARACTER SIZE 4 = PARM


SELECT MCSOLDE_ANNUEL IF SANANN OF MCSOLDE_ANNUEL = D_PECCLE[1:2]


TEMPORARY T_DBT_SANCUMREE    FLOAT SIZE 8  ;Total de la période (débit)
TEMPORARY T_CRT_SANCUMREE    FLOAT SIZE 8  ;Total de la période (crédit)
TEMPORARY T_DBT_SANCUMANN    FLOAT SIZE 8  ;Total (débit)  jusqu'à la période sélection + solde ouverture
TEMPORARY T_CRT_SANCUMANN    FLOAT SIZE 8  ;Total (crédit) jusqu'à la période sélection + solde ouverture
TEMPORARY T_DBT_TOTSANCUMREE FLOAT SIZE 8  ;Si le solde du débit  (T_DBT_SANCUMREE) est positif alors on prend ce montant.
                                           ;Si le solde du crédit (T_CRT_SANCUMREE) est négatif alors on prend le résultat de
                                           ;(0 - T_CRT_SANCUMREE) (Pour la période sélectionné)
TEMPORARY T_CRT_TOTSANCUMREE FLOAT SIZE 8  ;Si le solde du crédit (T_CRT_SANCUMREE) est positif alors on prend ce montant.
                                           ;Si le solde du débit  (T_DBT_SANCUMREE) est négatif alors on prend le résultat de
                                           ;(0 - T_DBT_SANCUMREE) (Pour la période sélectionné)
TEMPORARY T_DBT_TOTSANCUMANN FLOAT SIZE 8  ;Si le solde du débit  (T_DBT_SANCUMANN) est positif alors on prend ce montant.
                                           ;Si le solde du crédit (T_CRT_SANCUMANN) est négatif alors on prend le résultat de
                                           ;(0 - T_CRT_SANCUMANN) (Pour le cumulatif)
TEMPORARY T_CRT_TOTSANCUMANN FLOAT SIZE 8  ;Si le solde du crédit (T_CRT_SANCUMANN) est positif alors on prend ce montant.
                                           ;Si le solde du débit  (T_DBT_SANCUMANN) est négatif alors on prend le résultat de
                                           ;(0 - T_DBT_SANCUMANN) (Pour le cumulatif)


;--------------------------------------------------------------------------------
; Cumul annuel pour un compte DEBIT (CPTCODDC= D au niveau de la vue VCPT_DSC) .
;       -On imprime que le cumul qui correspond à la période saisie en
;        paramètres + Solde ouverture .
;--------------------------------------------------------------------------------
ITEM T_DBT_SANCUMANN  = 0 IF CPTCODDC OF VCPT_DSC = "C" &
 ELSE SANSLDOUV OF MCSOLDE_ANNUEL + SANCUMREE1 OF MCSOLDE_ANNUEL

ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE2 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "02" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE3 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "03" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE4 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "04" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE5 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "05" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE6 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "06" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE7 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "07" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE8 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "08" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE9 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "09" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE10 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "10" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE11 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "11" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE12 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "12" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE13 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "13" AND CPTCODDC OF VCPT_DSC = "D")
ITEM T_DBT_SANCUMANN  = T_DBT_SANCUMANN + SANCUMREE14 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "14" AND CPTCODDC OF VCPT_DSC = "D")

;--------------------------------------------------------------------------------
; Cumul annuel pour un compte CREDIT (CPTCODDC= C au niveau ).
;       -On imprime que le cumul qui correspond à la période saisie en
;        paramètres + Solde ouverture .
;--------------------------------------------------------------------------------
ITEM T_CRT_SANCUMANN  = 0 IF CPTCODDC OF VCPT_DSC = "D" &
 ELSE SANSLDOUV OF MCSOLDE_ANNUEL + SANCUMREE1 OF MCSOLDE_ANNUEL

ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE2 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "02" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE3 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "03" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE4 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "04" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE5 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "05" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE6 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "06" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE7 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "07" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE8 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "08" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE9 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "09" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE10 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "10" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE11 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "11" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE12 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "12" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE13 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "13" AND CPTCODDC OF VCPT_DSC = "C")
ITEM T_CRT_SANCUMANN  = T_CRT_SANCUMANN + SANCUMREE14 OF MCSOLDE_ANNUEL &
     IF (D_PECCLE[3:2] >= "14" AND CPTCODDC OF VCPT_DSC = "C")

;--------------------------------------------------------------------------------
; Imprimer le montant correspondant à la période saisie en paramètres
; pour un compte DEBIT.
;--------------------------------------------------------------------------------
ITEM T_DBT_SANCUMREE = 0 IF CPTCODDC OF VCPT_DSC = "C" &
  ELSE SANCUMREE1  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "01" ) &
  ELSE SANCUMREE2  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "02" ) &
  ELSE SANCUMREE3  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "03" ) &
  ELSE SANCUMREE4  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "04" ) &
  ELSE SANCUMREE5  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "05" ) &
  ELSE SANCUMREE6  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "06" ) &
  ELSE SANCUMREE7  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "07" ) &
  ELSE SANCUMREE8  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "08" ) &
  ELSE SANCUMREE9  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "09" ) &
  ELSE SANCUMREE10 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "10" ) &
  ELSE SANCUMREE11 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "11" ) &
  ELSE SANCUMREE12 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "12" ) &
  ELSE SANCUMREE13 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "13" ) &
  ELSE SANCUMREE14 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "14" ) &
  ELSE 0
;--------------------------------------------------------------------------------
; Imprimer le montant correspondant à la période saisie en paramètres
; pour un compte CREDIT.
;--------------------------------------------------------------------------------
ITEM T_CRT_SANCUMREE = 0 IF CPTCODDC OF VCPT_DSC = "D" &
  ELSE SANCUMREE1  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "01" ) &
  ELSE SANCUMREE2  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "02" ) &
  ELSE SANCUMREE3  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "03" ) &
  ELSE SANCUMREE4  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "04" ) &
  ELSE SANCUMREE5  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "05" ) &
  ELSE SANCUMREE6  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "06" ) &
  ELSE SANCUMREE7  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "07" ) &
  ELSE SANCUMREE8  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "08" ) &
  ELSE SANCUMREE9  OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "09" ) &
  ELSE SANCUMREE10 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "10" ) &
  ELSE SANCUMREE11 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "11" ) &
  ELSE SANCUMREE12 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "12" ) &
  ELSE SANCUMREE13 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "13" ) &
  ELSE SANCUMREE14 OF MCSOLDE_ANNUEL IF (D_PECCLE[3:2] = "14" ) &
  ELSE 0

;--------------------------------------------------------------------------------
;  Pour la période sélectionné : Si le solde du débit  (T_DBT_SANCUMREE) est positif
;                                alors on prend ce montant.
;                                Si le solde du crédit (T_CRT_SANCUMREE) est négatif
;                                alors on prend le résultat de (0 - T_CRT_SANCUMREE) )
;--------------------------------------------------------------------------------
ITEM T_DBT_TOTSANCUMREE  = T_DBT_SANCUMREE IF CPTCODDC OF VCPT_DSC = "D" AND T_DBT_SANCUMREE > 0 ELSE  &
                           (0 - T_CRT_SANCUMREE) IF CPTCODDC OF VCPT_DSC = "C" AND T_CRT_SANCUMREE < 0 &
                           ELSE 0

;--------------------------------------------------------------------------------
;  Pour la période sélectionné : Si le solde du Crédit (T_CRT_SANCUMREE) est positif
;                                alors on prend ce montant.
;                                Si le solde du Débit  (T_DBT_SANCUMREE) est négatif
;                                alors on prend le résultat de (0 - T_DBT_SANCUMREE) )
;--------------------------------------------------------------------------------
ITEM T_CRT_TOTSANCUMREE  =  T_CRT_SANCUMREE IF CPTCODDC OF VCPT_DSC = "C" AND T_CRT_SANCUMREE > 0 ELSE &
                            (0 - T_DBT_SANCUMREE) IF CPTCODDC OF VCPT_DSC = "D" AND T_DBT_SANCUMREE < 0 &
                            ELSE 0

;--------------------------------------------------------------------------------
;  Pour le cumulatif des périodes : Si le solde du Débit  (T_DBT_SANCUMANN) est positif
;                                   alors on prend ce montant.
;                                   Si le solde du Crédit (T_CRT_SANCUMANN) est négatif
;                                   alors on prend le résultat de (0 - T_CRT_SANCUMANN) )
;--------------------------------------------------------------------------------
ITEM T_DBT_TOTSANCUMANN  = T_DBT_SANCUMANN IF CPTCODDC OF VCPT_DSC = "D" AND T_DBT_SANCUMANN > 0 ELSE  &
                           (0 - T_CRT_SANCUMANN) IF CPTCODDC OF VCPT_DSC = "C" AND T_CRT_SANCUMANN < 0 &
                           ELSE 0
;--------------------------------------------------------------------------------
;  Pour le cumulatif des périodes : Si le solde du Crédit (T_CRT_SANCUMANN) est positif
;                                   alors on prend ce montant.
;                                   Si le solde du Débit  (T_DBT_SANCUMANN) est négatif
;                                   alors on prend le résultat de (0 - T_DBT_SANCUMANN) )
;--------------------------------------------------------------------------------
ITEM T_CRT_TOTSANCUMANN  = T_CRT_SANCUMANN IF CPTCODDC OF VCPT_DSC = "C" AND T_CRT_SANCUMANN > 0 ELSE  &
                           (0 - T_DBT_SANCUMANN) IF CPTCODDC OF VCPT_DSC = "D" AND T_DBT_SANCUMANN < 0 &
                           ELSE 0



SUBFILE WGL309A KEEP INCLUDE&
  CIECLE    OF MCSOLDE_ANNUEL ,  &
  SANANN    OF MCSOLDE_ANNUEL ,  &
  D_PECCLE                    ,  &
  CPTCLE    OF MCSOLDE_ANNUEL ,  &
  UNACLE    OF MCSOLDE_ANNUEL ,  &
  NACCLE    OF VCPT_DSC       ,  &
  NACDSC    OF VNAC_DSC       ,  &
  CPTDSCABR  OF VCPT_DSC      ,  &
  UNANOMABR  OF MCUNITE_ADM   ,  &
  T_DBT_TOTSANCUMREE          ,  &
  T_CRT_TOTSANCUMREE          ,  &
  T_DBT_TOTSANCUMANN          ,  &
  T_CRT_TOTSANCUMANN

BUILD gl309t
