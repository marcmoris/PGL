;/T/ Vérification des paiements annulés
;
;/P/ Programmeur...:  Thomas BRENNEUR
;    Date création.:  3 Aout 1993
;
;    Description...:  Liste de Verification des lots de paiements annulés
;
;    Paramètres....:  Compagnie
;                     Periode CTB (Une).
;                     No Lot (Un, Plusieurs ou tous).
;
;    Particularités:  Met à jour la date de vérification du lot.
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; permet de faire les SET REPORT NOLIMIT....

RUN cp306t

;--------------------------------------------------------------------------------
;
; Lecture des annulation de paiements à imprimer
;
;
REQUEST RECHERCHE_PAIEMENT

;
; Lecture des lots de paiements
;

ACCESS                        CPLOT &

;
; On retrouve les paiements par le lot.
;

  LINK  CIECLE    OF CPLOT     , &
        PECCLE    OF CPLOT     , &
        LCPCLE    OF CPLOT       &
    TO  CIECLE                 , &
        PECCLE                 , &
        LCPCLE                  OF CPPAIEMENT

CHOOSE CIECLE    PARM, &  ; Compagnie
       PECCLE    PARM, &  ; Période
       LCPCLE    PARM, &  ; Lot
       LCPTYPECR "CA"     ; Type de lot "Annulation de paiements"


SORTED ON CIECLE OF CPLOT, &
          LCPCLE OF CPLOT

SUBFILE WCP306A &
    KEEP &
    INCLUDE CIECLE       OF CPLOT                       , &
            PECCLE       OF CPLOT                       , &
            LCPCLE       OF CPLOT                       , &
            LCPDSC       OF CPLOT                       , &
            LCPDATJOU    OF CPLOT                       , &
            LCPNBRTRS    OF CPLOT                       , &
            LCPNBRTRSVER OF CPLOT                       , &
            LCPCUMMNT    OF CPLOT                       , &
            LCPNBRTRSERR OF CPLOT                       , &
            LCPCUMMNTVER OF CPLOT                       , &
            REFCIECLE    OF CPPAIEMENT                  , &
            REFCLETYP    OF CPPAIEMENT                  , &
            REFCLENUM    OF CPPAIEMENT                  , &
            RADCLE       OF CPPAIEMENT                  , &
            LBQCPTENC    OF CPPAIEMENT                  , &
            PAMCLE       OF CPPAIEMENT                  , &
            PAMPAMCLEANN OF CPPAIEMENT                  , &
            PAMDAT       OF CPPAIEMENT                  , &
            PAMMNT       OF CPPAIEMENT                  , &
            PAMCUMMNT    OF CPPAIEMENT

;
; Mise à jour de la date de vérification dans le lot s'il n'est pas journalisé
;
OUTPUT  CPLOT &
        UPDATE AT LCPCLE &
        IF LCPDATJOU OF CPLOT = 0

  ITEM LCPDATVAL OF CPLOT FINAL SYSDATE
  ITEM LCPUSRVAL OF CPLOT FINAL LOGONID


BUILD cp306t
