;/T/ Charte comptable
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 12 mai 1993
;
;    Description...: Ce rapport présente la liste charte des comptes
;
;
;    Particularités:
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....


ACCESS MCCHARTE_UNA &
  LINK CIECLE OF MCCHARTE_UNA      , &
       UNACLE OF MCCHARTE_UNA        &
    TO CIECLE                      , &
       UNACLE OF MCUNITE_ADM OPTIONAL&
  LINK CPTCLE OF MCCHARTE_UNA        &
    TO CPTCLE OF VCPT_DSC OPTIONAL   &
  LINK CIECLE OF MCCHARTE_UNA        &
    TO CIECLE OF MCCOMPAGNIE OPTIONAL


CHOOSE CIECLE PARM, & ; Numéro de compagnie
       UNACLE PARM    ; Numéro d'unité administrative

DEFINE D_PECACT CHARACTER SIZE 04 = PARM

; Ajustement pour le changement de siècle
DEFINE D_CCUPECDEBI_SIE CHARACTER SIZE 5 = "1" + CCUPECDEBI OF MCCHARTE_UNA &
         IF CCUPECDEBI OF MCCHARTE_UNA[1:1] < "8" &
         ELSE "0" + CCUPECDEBI OF MCCHARTE_UNA

; Ajustement pour le changement de siècle
DEFINE D_PECACT_SIE CHARACTER SIZE 5 = "1" + D_PECACT &
         IF D_PECACT[1:1] < "8" &
         ELSE "0" + D_PECACT

SELECT MCCHARTE_UNA IF 0 = SIZE(TRUNCATE(D_PECACT)) &
                       OR ( D_CCUPECDEBI_SIE > D_PECACT_SIE &
                            AND 0 <> SIZE(TRUNCATE(D_PECACT)) )   &
                       OR ( 0 = SIZE(TRUNCATE(CCUPECDEBI OF MCCHARTE_UNA)) &
                            AND 0 <> SIZE(TRUNCATE(D_PECACT)) )

 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &        ; titre1  du rapport
@IF FRANCAIS
                "Charte comptable"
@ELSE
                "%%%Charte comptable"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "      ; titre2  du rapport
 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "MC307"  ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM     ; Nom de la compagnie


SORT ON CIECLE OF MCCHARTE_UNA , &              ; Compagnie
        UNACLE OF MCCHARTE_UNA , &              ; Unité adm.
        CPTCLE OF MCCHARTE_UNA                  ; Compte


USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP 2 &
@IF FRANCAIS
  TAB 002 "Compagnie.....:" &
@ELSE
  TAB 002 "%%%pagnie.....:" &
@ENDIF
  TAB 018 CIECLE OF MCCHARTE_UNA &
  TAB 029 CIENOM OF MCCOMPAGNIE &
SKIP &
@IF FRANCAIS
  TAB 002 "Unité adm.....:" &
@ELSE
  TAB 002 "%%%té adm.....:" &
@ENDIF
  TAB 018 UNACLE OF MCCHARTE_UNA &
  TAB 029 UNANOM OF MCUNITE_ADM &
SKIP 2 &
@IF FRANCAIS
  TAB 007 "                                                    Période         " &
SKIP &
  TAB 007 "Compte   Description                            active   inactive   " &
@ELSE
  TAB 007 "%%%                                                 Période         " &
SKIP &
  TAB 007 "%%%pte   Description                            active   inactive   " &
@ENDIF
SKIP &
  TAB 007 "------   ----------------------------------    --------  --------   " &
SKIP

REPORT &
  TAB 007 CPTCLE     OF MCCHARTE_UNA &
  TAB 016 CPTDSC     OF VCPT_DSC     &
  TAB 056 CCUPECDEBA OF MCCHARTE_UNA &
  TAB 065 CCUPECDEBI OF MCCHARTE_UNA

FOOTING AT UNACLE &
SKIP PAGE


BUILD mc307z
@IF FORMAT

MC307                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                              Charte comptable
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

 Compagnie.....: xxxx       xxxxxxxxxxxxxxxxxxxx
 Unité adm.....: xxxxxxxx   xxxxxxxxxxxxxxxxxxxx

                                                          Période
      Compte   Description                            active   inactive
      ------   ----------------------------------    --------  --------
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
      xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx
@ENDIF
