;/T/ 2.2.2 Défaut de bloc (Bloc)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-08-29
;
;    Description..: Cette écran permet de d'enregistrer les défauts de bloc.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd176e ACTIONBAR                   &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM,          &
                       PDBLOC

;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD176E"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd176e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom compagnie

;-------------------------------------------------------------------------------
;
; Variable pour l'appel des pop-up
;
TEMPORARY T_DBLCODDEF CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Bloc
;
FILE PDBLOC MASTER


;-------------------------------------------------------------------------------
;
; Détail réception bloc
;
FILE PDBLOC_DEFAUTS PRIMARY &
                    NOITEM  &
                    OCCURS 10

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR                             &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC,            &
                 BLORECAPR       OF PDBLOC             &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ITEM CIECLE          OF PDBLOC_DEFAUTS INITIAL T_CIECLEMNU
  ITEM BLONUMGRA001APR OF PDBLOC_DEFAUTS INITIAL BLONUMGRA001APR OF PDBLOC
  ITEM BLONUMGRA002APR OF PDBLOC_DEFAUTS INITIAL BLONUMGRA002APR OF PDBLOC
  ITEM BLORECAPR       OF PDBLOC_DEFAUTS INITIAL BLORECAPR       OF PDBLOC
  ITEM BLONUMBLOAPR    OF PDBLOC_DEFAUTS INITIAL BLONUMGRA001APR OF PDBLOC + &
                                                 BLONUMGRA002APR OF PDBLOC + &
                                                 BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Fichiers de rérérence type de produit
;

FILE PDDEFAUT_DE_BLOC REFERENCE

  ACCESS VIA     CIECLE,                         &
                 DBLCODDEF                       &
         USING   T_CIECLEMNU,                    &
                 DBLCODDEF OF PDBLOC_DEFAUTS     &
         ORDERBY CIECLE,                         &
                 DBLCODDEF


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Blocs " &
@ELSE
      " %%%Blocs " &
@ENDIF
      CENTERED AT ,1


TITLE "-" AT 5,24

TITLE "-" AT 5,30


SKIP TO LINE 5

ALIGN (,3,19)(,,25)(,,31)(37,38,54)


FIELD BLONUMGRA001APR OF PDBLOC &;_DEFAUTS       &
        PREDISPLAY                            &
        DISPLAY                               &
        LABEL "No bloc.......:"               &
        NUMERIC                               &
        PREDISPLAY REFRESH

FIELD BLONUMGRA002APR OF PDBLOC &;_DEFAUTS       &
        PREDISPLAY                            &
        DISPLAY                               &
        NUMERIC                               &
        PREDISPLAY REFRESH

FIELD BLORECAPR       OF PDBLOC &;_DEFAUTS       &
        DISPLAY                               &
        PREDISPLAY REFRESH


DRAW 7,1 TO 7,80

SKIP TO LINE 7

TITLE &
@IF FRANCAIS
      " Défauts " &
@ELSE
      " %%%Défauts " &
@ENDIF
      CENTERED AT ,1

SKIP TO LINE 9

TITLE &
@IF FRANCAIS
      "Code de défaut  Description " &
@ELSE
      "%%%Code de défaut  Description " &
@ENDIF
      AT  ,14

SKIP TO LINE 11

ALIGN (2,,19) (,,30)

CLUSTER OCCURS WITH PDBLOC_DEFAUTS ID BASE 5

FIELD DBLCODDEF    OF PDBLOC_DEFAUTS          &
        HIDDEN                                &
        REQUIRED                              &
        LOOKUP ON FILE PDDEFAUT_DE_BLOC       &
          VIA     CIECLE,                     &
                  DBLCODDEF                   &
          USING   T_CIECLEMNU,                &
                  DBLCODDEF OF PDBLOC_DEFAUTS &
          MESSAGE 2954 ,                      & ; Ce code de défaut n'existe pas
               NOTON FILE PDBLOC_DEFAUTS      &
          VIA     CIECLE,                               &
                  BLONUMGRA001APR,                      &
                  BLONUMGRA002APR,                      &
                  BLORECAPR,                            &
                  DBLCODDEF                             &
          USING   T_CIECLEMNU,                          &
                  BLONUMGRA001APR OF PDBLOC_DEFAUTS,    &
                  BLONUMGRA002APR OF PDBLOC_DEFAUTS,    &
                  BLORECAPR       OF PDBLOC_DEFAUTS,    &
                  DBLCODDEF       OF PDBLOC_DEFAUTS     &
          MESSAGE 2955 ; Ce code de défaut existe déjà pour ce bloc

FIELD DBLDSCFRA    OF PDDEFAUT_DE_BLOC        &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du pop-up pour le champ
;

PROCEDURE INPUT DBLCODDEF
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DBLCODDEF = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd115 MODE F &
                     PASSING T_DBLCODDEF, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DBLCODDEF )
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  FOR PDBLOC_DEFAUTS
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDBLOC_DEFAUTS &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDBLOC_DEFAUTS &
       AND NOT NEWRECORD     OF PDBLOC_DEFAUTS &
       AND NOT DELETEDRECORD OF PDBLOC_DEFAUTS &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Évaluation des champs indiquant une mise à jour du bloc.
    ;
    IF NEWRECORD OF PDBLOC_DEFAUTS &
       OR &
       ALTEREDRECORD OF PDBLOC_DEFAUTS
    THEN BEGIN
      LET BLODATDERMAJ     OF PDBLOC         = SYSDATE
      LET BLOHREDERMAJ     OF PDBLOC         = SYSTIME / 10000
      LET BLOUSRDERMAJ     OF PDBLOC         = LOGONID
    END
  END
END


BUILD DETAIL LIST

@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Blocs *************************************
*                                                                              *
* No bloc.......: xxxxx-xxxxx-x                                                *
*                                                                              *
*********************************** Défauts ************************************
*                                                                              *
*            Code de défaut  Description                                       *
*                                                                              *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                 xx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
