;/T/ Gestion des acheteurs
;
;/P/ Réalisé par.......: Marc Poulin.
;    Date..............: 08 mars 2000.
;    Description.......: Création d'une gestion des acheteurs afin de cibler plus rapidement ceux qui initient les commandes et qui les réceptionnent.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 14.
;    
;    Signet............: MP-00-03-08_S14.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN ar158 ACTIONBAR                         &
             MODE LABEL "" AT 2,80             &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72      &
             RECEIVING T_CIECLEMNU,            &
                       T_CIENOM

USE ussectmp  NOLIST
    "AR158"

USE ar158.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom du holding

TEMPORARY T_USRCLE    CHARACTER SIZE 12   RESET AT STARTUP
;-------------------------------------------------------------------------------
;
; Table des utilisateurs
;
FILE ARACHETEUR PRIMARY      &
                   OCCURS 17 &
                   NOITEMS

  ACCESS  VIA ACHCLE               &
        USING ACHCLE OF ARACHETEUR &
      REQUEST ACHCLE               &
      ORDERBY ACHCLE 

  ACCESS SEQUENTIAL   &
         ORDERBY ACHCLE

;-------------------------------------------------------------------------------
;
; Affichage du nom associé au code d'acheteur.
;
FILE GSUSAGER REFERENCE    OPEN 1
  ACCESS VIA USRCLE             &
       USING ACHCLE OF ARACHETEUR

;-------------------------------------------------------------------------------
;
; Validation du code d'acheteur.
;
FILE GSUSAGER DESIGNER ALIAS A_GSUSAGER  OPEN 2

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE "Acheteur" CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

TITLE  "Code          Nom" AT 5,5

SKIP TO 6

ALIGN(5,3,5)(,,19)

CLUSTER OCCUR WITH ARACHETEUR ID BASE 10

FIELD ACHCLE OF ARACHETEUR               &
      LABEL " "                          &
      HIDDEN                             &
      REQUIRED                           &
      NOCHANGE                           &
      LOOKUP NOTON ARACHETEUR            &
         VIA ACHCLE                      &
       USING ACHCLE OF ARACHETEUR        &
     MESSAGE 0017, & ; Ce code existe déjà
     ON A_GSUSAGER                       &
         VIA USRCLE                      &
       USING ACHCLE                      &
     MESSAGE 0018 ; GS018 *E* Ce code n'existe pas.

FIELD USREMPNOM OF GSUSAGER              &
      DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Validation de l'autorisation d'entrer des données
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END
;-------------------------------------------------------------------------------
;
; Gérer le code d'acheteur.
;
PROCEDURE INPUT ACHCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_USRCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gs104 MODE F PASSING T_USRCLE
    LET FIELDTEXT = T_USRCLE
  END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR ARACHETEUR
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF DELETEDRECORD OF ARACHETEUR &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification
    ;
    IF    ALTEREDRECORD     OF ARACHETEUR &
      AND NOT NEWRECORD     OF ARACHETEUR &
      AND NOT DELETEDRECORD OF ARACHETEUR &
      AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'une catégorie.
    ;
    IF DELETEDRECORD OF ARACHETEUR
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si le code d'acheteur est référé.
      ;
      RUN SCREEN ar158x MODE SAME &
                        PASSING ARACHETEUR, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT
********************************** Acheteur ************************************
*                                                                              *
*   Code          Nom                                                          *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
********************************************************************************
@ENDIF
