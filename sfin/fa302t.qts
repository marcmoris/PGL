;/T/ Rapport de statistiques de ventes
;
;/P/ Programmeur...: Steeve Duguay
;    Date Création.: 3 mai 1995
;
;    Description...: Ce programme permet de produire le rapport de statistique
;                    de ventes par date.
;
;    Paramètres....: Compagnie          (Compagnie du menu)
;                    Date de facture    (Borne de début)
;                    Date de facture    (Borne de fin)
;                    Produit
;                    Élément de tarification
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; set process nolimit

RUN fa302t

;------------------------------------------------------
; Partie qui extrait les historiques de passerelle et
; qui cumul les quantités et montant par produit et élément
; pour chaque date.
REQUEST EXTRAIRE_HIST

ACCESS FAFAR_HIST &
       LINK CIECLE,    &
            FHIFARCLE  &
         TO CIECLE,    &
            FARCLE OF FACPT_A_REC &
       LINK CIECLE,    &
            FHIETACLE  &
         TO CIECLE,    &
            ETACLE OF FAELEMENT_TARIF

CHOOSE CIECLE    PARM,       & ; Compagnie du menu
       FHIANN    PARM,       & ; Année
       FHIDAT    PARM RANGE, & ; Date de la facture
       FHITRPCLE PARM,       & ; Numéro de produit
       FHIETACLE PARM          ; Numéro de élément

DEFINE D_FLGORI CHARACTER SIZE 1 = PARM          ; Passerelle oui/non

SELECT FAFAR_HIST IF (FHIETACLE OF FAFAR_HIST <> "0000" AND &
                      FHITRPCLE OF FAFAR_HIST <> "H11") AND &
                     (   (    D_FLGORI = "1" &
                          AND FHIORI OF FAFAR_HIST = D_FLGORI ) &
                      OR D_FLGORI = "0")

; On ne sélectionne pas les factures annulées
;
SELECT FACPT_A_REC IF FARSTU OF FACPT_A_REC <> "X"


SORT ON CIECLE    OF FAFAR_HIST   &
     ON FHITRPCLE OF FAFAR_HIST   &
     ON FHIDAT    OF FAFAR_HIST   &
     ON FHIETACLE OF FAFAR_HIST

TEMPORARY T_TYPTRX   CHARACTER SIZE 01
TEMPORARY T_NUMTRX   CHARACTER SIZE 01
TEMPORARY T_QTEFAC1      FLOAT SIZE 08
TEMPORARY T_MNTFAC1      FLOAT SIZE 08
TEMPORARY T_MNTAUT1      FLOAT SIZE 08
TEMPORARY T_QTEFAC2      FLOAT SIZE 08
TEMPORARY T_MNTFAC2      FLOAT SIZE 08
TEMPORARY T_MNTAUT2      FLOAT SIZE 08
TEMPORARY T_MNTCAL       FLOAT SIZE 08
TEMPORARY T_ETATYP   CHARACTER SIZE 01
TEMPORARY T_SEGMUL     INTEGER SIZE 04

ITEM T_TYPTRX   = "2"

ITEM T_SEGMUL   = 1

ITEM T_NUMTRX   = "1"

ITEM T_ETATYP   = "1" IF ETATYP OF FAELEMENT_TARIF = "P" ELSE &
                  "2" IF ETATYP OF FAELEMENT_TARIF = "V" ELSE &
                  "3"

ITEM T_MNTCAL   = ROUND(((FHIQTEFAC OF FAFAR_HIST * FHIPRIUNI OF FAFAR_HIST) - &
                          FHIMNTESC OF FAFAR_HIST) / 100)

ITEM T_QTEFAC1  = T_QTEFAC1 + FHIQTEFAC OF FAFAR_HIST &
                  RESET AT FHIETACLE TO 0

ITEM T_MNTFAC1  = T_MNTFAC1 + T_MNTCAL &
                  RESET AT FHIETACLE TO 0

ITEM T_MNTAUT1  = T_MNTAUT1 + T_MNTCAL &
               IF ETATYP OF FAELEMENT_TARIF = "A" &
                  RESET AT FHIETACLE TO 0

SUBFILE WFA302A KEEP AT FHIETACLE INCLUDE &
        CIECLE         OF FAFAR_HIST,     &
        FHITRPCLE      OF FAFAR_HIST,     &
        FHIETACLE      OF FAFAR_HIST,     &
        FHIDSC         OF FAFAR_HIST,     &
        FHIDAT         OF FAFAR_HIST,     &
        T_QTEFAC1,                        &
        T_MNTFAC1,                        &
        T_TYPTRX,                         &
        T_NUMTRX,                         &
        T_SEGMUL,                         &
        T_ETATYP

OUTPUT *WFA302A ADD  AT FHIETACLE ALIAS SF1 IF T_MNTAUT1 <> 0
  ITEM  CIECLE     OF SF1 FINAL CIECLE    OF FAFAR_HIST
  ITEM  FHITRPCLE  OF SF1 FINAL FHITRPCLE OF FAFAR_HIST
  ITEM  FHIETACLE  OF SF1 FINAL FHIETACLE OF FAFAR_HIST
  ITEM  FHIDAT     OF SF1 FINAL FHIDAT    OF FAFAR_HIST
  ITEM  T_QTEFAC1  OF SF1 FINAL 0
  ITEM  T_MNTFAC1  OF SF1 FINAL T_MNTAUT1
  ITEM  T_TYPTRX   OF SF1 FINAL "2"
  ITEM  T_ETATYP   OF SF1 FINAL "2"
  ITEM  T_NUMTRX   OF SF1 FINAL "1"
  ITEM  T_SEGMUL   OF SF1 FINAL 1

;-----------------------------
; Trouver le nombre de segment
;
REQUEST SEGEMENT

ACCESS *WFA302A &
       LINK CIECLE,   &
            FHITRPCLE &
         TO CIECLE,   &
            TRPCLE OF FASEGMENT

SELECT WFA302A IF T_ETATYP <> "3" ; À cause des doubles mis dans t_etatyp = "2"

SORT ON CIECLE    &
     ON FHITRPCLE &
     ON FHIDAT    &
     ON FHIETACLE

TEMPORARY T_SEGMUL2    INTEGER SIZE 4

ITEM T_SEGMUL2 = T_SEGMUL2 + 1            &
                 RESET AT FHIETACLE TO 0

OUTPUT WFA302A UPDATE
  ITEM T_SEGMUL OF WFA302A FINAL T_SEGMUL2


;-----------------------------
; Trouver le nombre de segment
;
REQUEST SEGEMENT

ACCESS *WFA302A

SORT ON CIECLE    &
     ON FHITRPCLE &
     ON T_SEGMUL D

TEMPORARY T_SEGMUL2    INTEGER SIZE 4
TEMPORARY T_COMPTEUR   INTEGER SIZE 4

ITEM T_COMPTEUR= T_COMPTEUR + 1 RESET AT FHITRPCLE TO 0
ITEM T_SEGMUL2 = T_SEGMUL     IF T_COMPTEUR = 1

OUTPUT WFA302A UPDATE
  ITEM T_SEGMUL OF WFA302A FINAL T_SEGMUL2


;-------------------------------------------
; Cumuler par type d'élément de tarification
;
REQUEST CUMUL_TYPE

ACCESS *WFA302A

SORT ON CIECLE    &
     ON FHITRPCLE &
     ON FHIDAT    &
     ON T_ETATYP  &
     ON FHIETACLE

TEMPORARY T_QTESUM1      FLOAT SIZE 08
TEMPORARY T_MNTSUM1      FLOAT SIZE 08
TEMPORARY T_QTEGTO1      FLOAT SIZE 08
TEMPORARY T_MNTGTO1      FLOAT SIZE 08
TEMPORARY T_QTESEG1      FLOAT SIZE 08
TEMPORARY T_MNTSEG1      FLOAT SIZE 08
TEMPORARY T_QTEGTS1      FLOAT SIZE 08
TEMPORARY T_MNTGTS1      FLOAT SIZE 08

ITEM T_QTESUM1  = T_QTESUM1 + T_QTEFAC1      &
               IF T_ETATYP OF WFA302A <> "3" &
                  RESET AT T_ETATYP TO 0

ITEM T_QTESEG1  = T_QTESEG1 + (T_QTEFAC1 * T_SEGMUL) &
               IF T_ETATYP OF WFA302A <> "3"         &
                  RESET AT T_ETATYP TO 0

ITEM T_QTEGTO1  = T_QTEGTO1 + T_QTEFAC1 &
               IF T_ETATYP OF WFA302A = "1" OR &
                  T_ETATYP OF WFA302A = "2"    &
                  RESET AT FHIDAT TO 0

ITEM T_QTEGTS1  = T_QTEGTS1 + (T_QTEFAC1 * T_SEGMUL) &
               IF T_ETATYP OF WFA302A = "1" OR &
                  T_ETATYP OF WFA302A = "2"    &
                  RESET AT FHIDAT TO 0

ITEM T_MNTSUM1  = T_MNTSUM1 + T_MNTFAC1 &
                  RESET AT T_ETATYP TO 0

ITEM T_MNTSEG1  = T_MNTSEG1 + T_MNTFAC1 &
                  RESET AT T_ETATYP TO 0

ITEM T_MNTGTO1  = T_MNTGTO1 + T_MNTFAC1 &
               IF T_ETATYP OF WFA302A = "1" OR &
                  T_ETATYP OF WFA302A = "2"    &
                  RESET AT FHIDAT TO 0

ITEM T_MNTGTS1  = T_MNTGTS1 + T_MNTFAC1 &
               IF T_ETATYP OF WFA302A = "1" OR &
                  T_ETATYP OF WFA302A = "2"    &
                  RESET AT FHIDAT TO 0

OUTPUT *WFA302A ADD  AT T_ETATYP ALIAS SF1
  ITEM  CIECLE     OF SF1 FINAL CIECLE     OF WFA302A
  ITEM  FHITRPCLE  OF SF1 FINAL FHITRPCLE  OF WFA302A
  ITEM  FHIETACLE  OF SF1 FINAL " "
  ITEM  FHIDAT     OF SF1 FINAL FHIDAT     OF WFA302A
  ITEM  T_QTEFAC1  OF SF1 FINAL T_QTESUM1
  ITEM  T_MNTFAC1  OF SF1 FINAL T_MNTSUM1
  ITEM  T_TYPTRX   OF SF1 FINAL "2"
  ITEM  T_ETATYP   OF SF1 FINAL T_ETATYP   OF WFA302A
  ITEM  T_NUMTRX   OF SF1 FINAL "2"

OUTPUT *WFA302A ADD  AT T_ETATYP ALIAS SF2
  ITEM  CIECLE     OF SF2 FINAL CIECLE     OF WFA302A
  ITEM  FHITRPCLE  OF SF2 FINAL FHITRPCLE  OF WFA302A
  ITEM  FHIETACLE  OF SF2 FINAL " "
  ITEM  FHIDAT     OF SF2 FINAL FHIDAT     OF WFA302A
  ITEM  T_QTEFAC1  OF SF2 FINAL T_QTESEG1
  ITEM  T_MNTFAC1  OF SF2 FINAL T_MNTSEG1
  ITEM  T_TYPTRX   OF SF2 FINAL "2"
  ITEM  T_ETATYP   OF SF2 FINAL T_ETATYP   OF WFA302A
  ITEM  T_NUMTRX   OF SF2 FINAL "3"

OUTPUT *WFA302A ADD  AT FHIDAT ALIAS SF3
  ITEM  CIECLE     OF SF3 FINAL CIECLE     OF WFA302A
  ITEM  FHITRPCLE  OF SF3 FINAL FHITRPCLE  OF WFA302A
  ITEM  FHIETACLE  OF SF3 FINAL " "
  ITEM  FHIDAT     OF SF3 FINAL FHIDAT     OF WFA302A
  ITEM  T_QTEFAC1  OF SF3 FINAL T_QTEGTO1
  ITEM  T_MNTFAC1  OF SF3 FINAL T_MNTGTO1
  ITEM  T_TYPTRX   OF SF3 FINAL "2"
  ITEM  T_ETATYP   OF SF3 FINAL "4"
  ITEM  T_NUMTRX   OF SF3 FINAL "1"

OUTPUT *WFA302A ADD  AT FHIDAT ALIAS SF4
  ITEM  CIECLE     OF SF4 FINAL CIECLE     OF WFA302A
  ITEM  FHITRPCLE  OF SF4 FINAL FHITRPCLE  OF WFA302A
  ITEM  FHIETACLE  OF SF4 FINAL " "
  ITEM  FHIDAT     OF SF4 FINAL FHIDAT     OF WFA302A
  ITEM  T_QTEFAC1  OF SF4 FINAL T_QTEGTS1
  ITEM  T_MNTFAC1  OF SF4 FINAL T_MNTGTS1
  ITEM  T_TYPTRX   OF SF4 FINAL "2"
  ITEM  T_ETATYP   OF SF4 FINAL "4"
  ITEM  T_NUMTRX   OF SF4 FINAL "2"


;------------------------------------------------
; Cumul pour le total par produit
;
REQUEST CUMUL_PRODUIT

ACCESS *WFA302A

SORT ON CIECLE    &
     ON FHITRPCLE &
     ON T_ETATYP  &
     ON T_NUMTRX  &
     ON FHIETACLE

TEMPORARY T_QTESUM1     FLOAT SIZE 08
TEMPORARY T_MNTSUM1     FLOAT SIZE 08

ITEM T_QTESUM1 = T_QTESUM1 + T_QTEFAC1 &
                 RESET AT FHIETACLE TO 0

ITEM T_MNTSUM1 = T_MNTSUM1 + T_MNTFAC1 &
                 RESET AT FHIETACLE TO 0

OUTPUT *WFA302A ADD  AT FHIETACLE ALIAS SF1
  ITEM  CIECLE     OF SF1 FINAL CIECLE     OF WFA302A
  ITEM  FHITRPCLE  OF SF1 FINAL FHITRPCLE  OF WFA302A
  ITEM  FHIETACLE  OF SF1 FINAL FHIETACLE  OF WFA302A
  ITEM  FHIDAT     OF SF1 FINAL 0
  ITEM  T_QTEFAC1  OF SF1 FINAL T_QTESUM1
  ITEM  T_MNTFAC1  OF SF1 FINAL T_MNTSUM1
  ITEM  T_TYPTRX   OF SF1 FINAL "2"
  ITEM  T_ETATYP   OF SF1 FINAL T_ETATYP   OF WFA302A
  ITEM  T_NUMTRX   OF SF1 FINAL T_NUMTRX   OF WFA302A


;------------------------
; Préparation du sommaire
;
REQUEST SOMMAIRE

ACCESS *WFA302A

SORT ON CIECLE    &
     ON FHIDAT    &
     ON T_ETATYP  &
     ON T_NUMTRX  &
     ON FHIETACLE

TEMPORARY T_QTESUM1     FLOAT SIZE 08
TEMPORARY T_MNTSUM1     FLOAT SIZE 08

ITEM T_QTESUM1 = T_QTESUM1 + T_QTEFAC1 &
                 RESET AT FHIETACLE TO 0

ITEM T_MNTSUM1 = T_MNTSUM1 + T_MNTFAC1 &
                 RESET AT FHIETACLE TO 0

OUTPUT *WFA302A ADD  AT FHIETACLE ALIAS SF1
  ITEM  CIECLE     OF SF1 FINAL CIECLE     OF WFA302A
  ITEM  FHITRPCLE  OF SF1 FINAL " "
  ITEM  FHIETACLE  OF SF1 FINAL FHIETACLE  OF WFA302A
  ITEM  FHIDAT     OF SF1 FINAL FHIDAT     OF WFA302A
  ITEM  T_QTEFAC1  OF SF1 FINAL T_QTESUM1
  ITEM  T_MNTFAC1  OF SF1 FINAL T_MNTSUM1
  ITEM  T_TYPTRX   OF SF1 FINAL "1"
  ITEM  T_ETATYP   OF SF1 FINAL T_ETATYP   OF WFA302A
  ITEM  T_NUMTRX   OF SF1 FINAL T_NUMTRX   OF WFA302A

BUILD fa302t
