;/T/ 5.2 Type de produit (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-07-28
;
;    Description..: Ce panorama permet de saisir les types de produit
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd200 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD200"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd200.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Variable servant à l'appel du/des dépisteur(s) (pop-up)
;
TEMPORARY T_TPDTYPPRD    CHARACTER SIZE 04 ; Type de produit
TEMPORARY T_CPTCLE       CHARACTER SIZE 06 ; Compte
TEMPORARY T_UNACLE       CHARACTER SIZE 08 ; Unité administrative
TEMPORARY T_CPTTYPPAT    CHARACTER SIZE 01 ; Pattern type de compte
TEMPORARY T_CPTCATPAT    CHARACTER SIZE 08 ; Pattern catégorie du compte
TEMPORARY T_PECCLE       CHARACTER SIZE 04 ; Période
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04 ; Unité mesure


;-------------------------------------------------------------------------------
;
; Type de produit
;

FILE PDTYPE_PRODUIT PRIMARY NOITEM
  ACCESS VIA     CIECLE,     &
                 TPDTYPPRD   &
         USING   T_CIECLEMNU,                &
                 TPDTYPPRD OF PDTYPE_PRODUIT &
         REQUEST TPDTYPPRD OF PDTYPE_PRODUIT &
         ORDERBY CIECLE,     &
                 TPDTYPPRD

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 TPDTYPPRD

   ITEM CIECLE OF PDTYPE_PRODUIT INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Compte comptable
;

FILE MCCOMPTE REFERENCE

  ACCESS VIA     CPTCLE &
         USING   CPTCLE OF PDTYPE_PRODUIT


;-------------------------------------------------------------------------------
;
; Unités administratives
;

FILE MCUNITE_ADM REFERENCE

  ACCESS VIA     CIECLE,     &
                 UNACLE      &
         USING   T_CIECLEMNU,&
                 UNACLE OF PDTYPE_PRODUIT


;-------------------------------------------------------------------------------
;
;
;

FILE PDUNITE_MESURE REFERENCE

  ACCESS VIA     CIECLE,     &
                 UNMCODUNM   &
         USING   T_CIECLEMNU,&
                 UNMCODUNM OF PDTYPE_PRODUIT


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Type de produit " &
@ELSE
      " %%%Type de produit " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF

SKIP TO LINE 5

ALIGN (2,3,19)

FIELD TPDTYPPRD    OF PDTYPE_PRODUIT       &
        HIDDEN ID 05                       &
        REQUIRED NOCHANGE                  &
        LOOKUP NOTON PDTYPE_PRODUIT        &
          VIA   CIECLE,                    &
                TPDTYPPRD                  &
          USING T_CIECLEMNU,               &
                TPDTYPPRD OF PDTYPE_PRODUIT &
          MESSAGE 2963 ; Ce code de produit n'existe pas


FIELD TPDDSC001 OF PDTYPE_PRODUIT          &
        HIDDEN ID 10                       &
        REQUIRED


ALIGN (2,,19)


FIELD TPDDSC002 OF PDTYPE_PRODUIT          &
;        HIDDEN ID 15                       &
       ID SAME &
        NOLABEL

ALIGN (2,3,19) (,,28)


FIELD CPTCLE           OF PDTYPE_PRODUIT   &
        HIDDEN ID 20                       &
        REQUIRED                           &
        LOOKUP ON MCCOMPTE                 &
          MESSAGE 2965 ; Ce numéro de compte n'existe pas


FIELD CPTDSCFRA        OF MCCOMPTE         &
        DISPLAY


FIELD UNACLE           OF PDTYPE_PRODUIT   &
        HIDDEN ID 25                       &
        REQUIRED                           &
        LOOKUP ON MCUNITE_ADM              &
          MESSAGE  3083 ; Cette unité administrative n'existe pas


FIELD UNANOM           OF MCUNITE_ADM      &
        DISPLAY


FIELD UNMCODUNM        OF PDTYPE_PRODUIT   &
        HIDDEN ID 30                       &
        REQUIRED                           &
        LOOKUP ON PDUNITE_MESURE           &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas


FIELD UNMDSCFRA        OF PDUNITE_MESURE   &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs type de produit
;

PROCEDURE INPUT TPDTYPPRD
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TPDTYPPRD = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd118 MODE F &
                      PASSING T_TPDTYPPRD, &
                              T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TPDTYPPRD )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs
;

PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F &
                      PASSING T_CPTCLE,    &
                              T_CPTTYPPAT, &
                              T_CPTCATPAT, &
                              T_PECCLE
    LET FIELDTEXT = TRUNCATE( T_CPTCLE )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;

PROCEDURE INPUT UNACLE
BEGIN
  ;
  ; Popup sur les unités administrative.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104                &
               MODE F               &
               PASSING T_CIECLEMNU, &
                       T_UNACLE
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE INPUT UNMCODUNM
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDTYPE_PRODUIT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDTYPE_PRODUIT &
     AND NOT NEWRECORD     OF PDTYPE_PRODUIT &
     AND NOT DELETEDRECORD OF PDTYPE_PRODUIT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD detail list

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   xall
******************************* Type de produit ********************************all
*                                                                              *all
* Type produit..: xx                                                           *all
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *all
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *all
* Compte........: xxxxxx                                                       *all
* Unité adm.....: xxxxxxxx                                                     *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
*                                                                              *all
********************************************************************************all
@ENDIF
