;/T/ Termes, test de date
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 17 mai 1993
;
;
;    Description..: Ce sous-écran sert à effectuer des tests pour le calcul
;                   des date due pour les termes d'encaissement.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN cr153a NOMODE NOACTION FIELDMARK &
              FROM 5,8 TO 13,72 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY

USE cr153a.hlp

;-------------------------------------------------------------------------------
;
; Variables saisie ou affiché à l'écran.
;
TEMPORARY T_TMECLE_TMP    CHARACTER SIZE 4
TEMPORARY T_DAT_TMP       DATE

;-------------------------------------------------------------------------------
;
; Validation du code de terme.
;
FILE VTME_DSC         REFERENCE
  ACCESS VIA TMECLE &
         USING T_TMECLE_TMP
;
; Pour le calcul de la date.
;
FILE CRTERME          REFERENCE
  ACCESS VIA TMECLE &
         USING T_TMECLE_TMP


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_TMECLE    CHARACTER SIZE  4 ; Popup sur terme d'encaissement.
TEMPORARY T_TMECATPAT CHARACTER SIZE 11 ; Popup sur terme d'encaissement.
;
; Variable temporaire pour le calcul des dates.
;
USE ustertmp NOLIST

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST

DRAW 1,1 TO 9,65

TITLE &
@IF FRANCAIS
      " Test - Calcul de date " &
@ELSE
      " %%%TEST " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

USE ustitoff NOLIST

ALIGN (3,3,19) (,,24)

FIELD T_TMECLE_TMP HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Code de terme.:" &
@ELSE
        LABEL "Term code.....:" &
@ENDIF
        UPSHIFT  &
        REQUIRED &
        LOOKUP ON VTME_DSC &
          MESSAGE 639 ; Ce code de taxe n'existe pas

FIELD TMEDSC OF VTME_DSC &
        DISPLAY

SKIP 1

ALIGN (3,3,19)

FIELD T_DAT_TMP HIDDEN ID  10 FORMAT YYYYMMDD PATTERN "####/##/##"&
        REQUIRED &
@IF FRANCAIS
        LABEL "Date cible....:"
@ELSE
        LABEL "Target date...:"
@ENDIF

SKIP 1

FIELD TZ_DATRES HIDDEN ID 15 FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY &
@IF FRANCAIS
        LABEL "Date due......:"
@ELSE
        LABEL "Due date......:"
@ENDIF


;-------------------------------------------------------------------------------
;
;
; Popup sur les termes d'encaissements.
;
;
PROCEDURE INPUT T_TMECLE_TMP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TMECATPAT = "@"
    RUN SCREEN cr104 MODE F PASSING T_TMECLE, &
                                    T_TMECATPAT
    LET FIELDTEXT = TRUNCATE( T_TMECLE )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul de la date due.
;
USE ustmecal NOLIST


;-------------------------------------------------------------------------------
;
;
PROCEDURE PROCESS T_DAT_TMP
BEGIN
  LET TZ_DATCAL = T_DAT_TMP
  DO INTERNAL TER_DATE_DUE
  DISPLAY TZ_DATRES
END

BUILD

;       ********************* Test - Calcul de date *********************
;       *                                                               *
;       * Code de terme.: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;       *                                                               *
;       * Date cible....: xxxxxxxx                                      *
;       *                                                               *
;       * Date due......: xxxxxxxx                                      *
;       *                                                               *
;       *****************************************************************
