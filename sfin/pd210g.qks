;/T/ 2.3.1 Enregistrer la commande interne. (Crédit)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-01
;
;    Description..: Cette unité de traitement permet de traiter les
;                   informations sur le crédit de la commande interne.
;
;    ----------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 18 février 2002
;    Référence.........: Demande de changement D200215 - Demandeur (Josée Robitaille & Anik Lupien).
;
;    Signet............: MP-02-02-18_D200215.
;
;-------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210g ACTIONBAR                    &
             MODE LABEL "" AT 2,80         &
             NOACTION                      &
             SLAVE                         &
             FIELDMARK RETAIN STARTUP      &
             HELP POPUP FROM 4,9 TO 20,72  &
             RECEIVING T_CIECLEMNU,        &
                       T_CIENOM,           &
                       PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD210G"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd210g.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour l'appel des dépisteurs
;

TEMPORARY T_TMECATPAT CHARACTER SIZE  11; Popup sur les termes d'encaissement.
TEMPORARY T_TMECLE    CHARACTER SIZE  4 ; Popup sur les termes d'encaissement.


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE MASTER


;-------------------------------------------------------------------------------
;
; Terme de paiement
;

FILE CRTERME REFERENCE

  ACCESS VIA     TMECLE &
         USING   TMECLE OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------

FILE CRCLI_CIE DESIGNER

;-----------------------------------------------------------------------------
;
FILE CPFOURNISSEUR  REFERENCE
  ACCESS VIA FOUCIECLE   , &
             FOUCLE        &
       USING "----"      , &
             FOUCLE OF PDCOMMAN_INTERNE


;-----------------------------------------------------------------------------
; MP-02-02-18_D200215.

FILE CPFOURNISSEUR  REFERENCE ALIAS A_CPFOURNISSEUR
  ACCESS VIA FOUCIECLE   , &
             FOUCLE        &
       USING "----"      , &
             FOUCLE2 OF PDCOMMAN_INTERNE


FILE CPCPT_A_PAYER DESIGNER


TEMPORARY T_FOUCLE         CHARACTER SIZE 06  RESET AT STARTUP ; Code de fournisseur
TEMPORARY T_FOUCLE2        CHARACTER SIZE 06  RESET AT STARTUP ; Code de fournisseur 2
TEMPORARY T_USINE_SEC      CHARACTER SIZE 01  RESET AT STARTUP ; Code de sécurité

;-----------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

TITLE &
@IF FRANCAIS
      " Crédit " &
@ELSE
      " %%%Crédit " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        FIXED


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        FIXED


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        FIXED


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        FIXED


DRAW 5,2 TO 5,79


SKIP TO LINE 6


ALIGN (2,3,19) (,,24)


FIELD TMECLE           OF PDCOMMAN_INTERNE &
label "Code terme....:"&
        HIDDEN ID 15                       &
        LOOKUP ON CRTERME                  &
          MESSAGE 3084 ; Ce terme de paiement n'existe pas

FIELD TMEDSCFRA        OF CRTERME          &
        DISPLAY


ALIGN (2,3,19)


FIELD COMCTR001        OF PDCOMMAN_INTERNE &
label "Crédit........:"&
        HIDDEN ID 20                       &
        FOR , 58


ALIGN (2,,19)


FIELD COMCTR002        OF PDCOMMAN_INTERNE &
        ID SAME                            &
        FOR , 58


FIELD COMCTR003        OF PDCOMMAN_INTERNE &
        ID SAME                            &
        FOR , 58


FIELD COMCTR004        OF PDCOMMAN_INTERNE &
        ID SAME                            &
        FOR , 58


FIELD COMCTR005        OF PDCOMMAN_INTERNE &
        ID SAME                            &
        FOR , 58


FIELD COMCTR006        OF PDCOMMAN_INTERNE &
        ID SAME                            &
        FOR , 58

SKIP 1

; ---------------------------------------------------
; MP-02-02-18_D200215.

ALIGN (2,3,24) (,,31) (,55,75)
FIELD FOUCLE OF PDCOMMAN_INTERNE           &
      HIDDEN ID 21                         &
      REQUIRED                             &  
      LABEL "Agent 1............:"         

FIELD FOUNOMABR OF CPFOURNISSEUR           &
      DISPLAY

FIELD COMCALAUT        OF PDCOMMAN_INTERNE &
      LABEL "Calcul automatique:"          &
      HIDDEN ID SAME                       &
      UPSHIFT                              &
      DEFAULT "0"                          &
      SIZE 3

ALIGN (2,3,24) (,43,63)
FIELD COMMNT01         OF PDCOMMAN_INTERNE &
      LABEL "Commissions à payer:"         &
      HIDDEN ID 22                         

FIELD COMMNT02         OF PDCOMMAN_INTERNE &
      LABEL "Commissions payées:"          &
      ID SAME                         

SKIP

ALIGN (2,3,24) (,,31) (,55,75)
FIELD FOUCLE2  OF PDCOMMAN_INTERNE         &
      HIDDEN ID 23                         &
      REQUIRED                             &  
      UPSHIFT                              &
      LABEL "Agent 2............:"         

FIELD FOUNOMABR OF A_CPFOURNISSEUR         &
      DISPLAY

FIELD COMCALAUT2       OF PDCOMMAN_INTERNE &
      LABEL "Calcul automatique:"          &
      HIDDEN ID SAME                       &
      UPSHIFT                              &
      DEFAULT "0"                          &
      SIZE 3

ALIGN (2,3,24) (,43,63)
FIELD COMMNT012        OF PDCOMMAN_INTERNE &
      LABEL "Commissions à payer:"         &
      HIDDEN ID 24                         &
      INPUT SCALE 2                        &
      PICTURE "^^^,^^^,^^^.^^ "            &
      SIGNIF 5                             &
      TRAILING SIGN "-"

FIELD COMMNT022        OF PDCOMMAN_INTERNE &
      LABEL "Commissions payées:"          &
      ID SAME                              &
      INPUT SCALE 2                        &
      PICTURE "^^^,^^^,^^^.^^ "            &
      SIGNIF 5                             &
      TRAILING SIGN "-"

; ---------------------------------------------------

SKIP 1

ALIGN (2,3,19) (,22,38) (,49,65)


FIELD COMATR           OF PDCOMMAN_INTERNE &
label "Autorisation..:"&
        HIDDEN ID 25


FIELD COMDATMAJATR     OF PDCOMMAN_INTERNE  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date autorisa.:"&
        DISPLAY


FIELD COMUSRMAJATR     OF PDCOMMAN_INTERNE &
label "Usager autori.:"&
        DISPLAY


FIELD COMTER           OF PDCOMMAN_INTERNE &
label "Terminé.......:"&
        HIDDEN ID 30


FIELD COMDATTER        OF PDCOMMAN_INTERNE  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date terminé..:"&
        DISPLAY


FIELD COMUSRTER        OF PDCOMMAN_INTERNE &
label "Usager terminé:"&
        DISPLAY

; ---------------------------------------------------
; MP-02-02-18_D200215.

SKIP

ALIGN (2,3,19)

FIELD COMNUMLETCRECLI  OF PDCOMMAN_INTERNE &
      LABEL "Num ltr client:"              &
      HIDDEN ID 40

ALIGN (2,3,19) (,60,70)

FIELD COMNUMLETCREGRA  OF PDCOMMAN_INTERNE &
      LABEL "Num ltr comp..:"              &
      ID SAME

FIELD COMDATECHLETCRE  OF PDCOMMAN_INTERNE &
      FORMAT YYYYMMDD                      &
      PATTERN "####/##/##"                 &
      LABEL "Dern.livr:"                   &
      ID SAME


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
; MP-02-02-18_D200215.
;
; Traitement pour les indicateurs dont la valeur est Oui ou Non.
; Le procédure INPUT transpose les valeurs sous un code universel soit 1 et 0.
;

PROCEDURE INPUT COMCALAUT    OF PDCOMMAN_INTERNE
BEGIN
  USE usflginp NOLIST
END

PROCEDURE OUTPUT COMCALAUT   OF PDCOMMAN_INTERNE
BEGIN
  USE usflgout3 NOLIST
END

PROCEDURE INPUT COMCALAUT2  OF PDCOMMAN_INTERNE
BEGIN
  USE usflginp NOLIST
END

PROCEDURE OUTPUT COMCALAUT2 OF PDCOMMAN_INTERNE
BEGIN
  USE usflgout3 NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;

PROCEDURE INPUT TMECLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_TMECATPAT = "NET"
    RUN SCREEN cr104 MODE F PASSING T_TMECLE, &
                                    T_TMECATPAT
    LET FIELDTEXT = TRUNC(T_TMECLE)
  END
  ELSE BEGIN
  GET CRCLI_CIE OPT                          &
         VIA   CLICIECLE                     ,&
               CLICLE                        ,&
               CIECLE                         &
         USING CLICIECLE OF PDCOMMAN_INTERNE ,&
               CLICLE    OF PDCOMMAN_INTERNE ,&
               T_CIECLEMNU
   IF ACCESSOK
   THEN LET TMECLE OF PDCOMMAN_INTERNE = CLCTERNET OF CRCLI_CIE
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;

PROCEDURE INPUT COMATR
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Change le statut de la commande si la commande est autorisé
;

PROCEDURE PROCESS COMATR
BEGIN
  IF COMATR OF PDCOMMAN_INTERNE = "1"
  THEN BEGIN
    LET COMSTU OF PDCOMMAN_INTERNE = "P"
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;

PROCEDURE OUTPUT COMATR
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;

PROCEDURE INPUT COMTER
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;

PROCEDURE OUTPUT COMTER
BEGIN
  USE usflgout NOLIST
END


;------------------------------------------------------------------------------
;
; Pop-up sur l'agent 1
;
PROCEDURE INPUT FOUCLE OF PDCOMMAN_INTERNE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd117 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_FOUCLE   , &
                             T_USINE_SEC  
    LET FIELDTEXT = TRUNCATE( T_FOUCLE )
  END
END

PROCEDURE EDIT FOUCLE OF PDCOMMAN_INTERNE
BEGIN
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    IF FIELDTEXT = FOUCLE2 OF PDCOMMAN_INTERNE
    THEN ERROR "PD997E Les codes d'agent 1 et 2 doivent être différents"

    GET CPFOURNISSEUR VIA FOUCIECLE                 , &
                          FOUCLE                      &
                    USING "----"                    , &
                          FOUCLE OF PDCOMMAN_INTERNE  OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR "PD999E Ce code d'agent n'existe pas"
  END

  IF  0 <> SIZE(TRUNCATE(OLDVALUE(FOUCLE OF PDCOMMAN_INTERNE))) AND FIELDTEXT <> OLDVALUE(FOUCLE OF PDCOMMAN_INTERNE)
  THEN BEGIN
    WHILE RETRIEVING CPCPT_A_PAYER VIA CAPCIECLE                           , &
                                       FOUCLE                              , &
                                       PJTABR                              , &
                                       PJTANN                              , &
                                       COMNUMCOMINT                          &
                                 USING CIECLE          OF PDCOMMAN_INTERNE , &
                                       OLDVALUE(FOUCLE OF PDCOMMAN_INTERNE), &
                                       PJTABR          OF PDCOMMAN_INTERNE , &
                                       PJTANN          OF PDCOMMAN_INTERNE , &
                                       COMNUMCOMINT    OF PDCOMMAN_INTERNE
             
    IF CAPDATJOU OF CPCPT_A_PAYER <> 0
    THEN ERROR "PD998E Modification impossible - agent associé à une facture C/P journalisée"
  END
END

;------------------------------------------------------------------------------
; MP-02-02-18_D200215.
;
; Pop-up sur l'agent 2
;
PROCEDURE INPUT FOUCLE2 OF PDCOMMAN_INTERNE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd117 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_FOUCLE2  , &
                             T_USINE_SEC  
    LET FIELDTEXT = TRUNCATE( T_FOUCLE2 )
  END
END

PROCEDURE EDIT FOUCLE2 OF PDCOMMAN_INTERNE
BEGIN
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    IF FIELDTEXT = FOUCLE OF PDCOMMAN_INTERNE
    THEN ERROR "PD997E Les codes d'agent 1 et 2 doivent être différents"

    GET CPFOURNISSEUR VIA FOUCIECLE                 , &
                          FOUCLE                      &
                    USING "----"                    , &
                          FOUCLE2 OF PDCOMMAN_INTERNE  OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR "PD999E Ce code d'agent n'existe pas"
  END

  IF  0 <> SIZE(TRUNCATE(OLDVALUE(FOUCLE2 OF PDCOMMAN_INTERNE))) AND FIELDTEXT <> OLDVALUE(FOUCLE2 OF PDCOMMAN_INTERNE)
  THEN BEGIN
    WHILE RETRIEVING CPCPT_A_PAYER VIA CAPCIECLE                            , &
                                       FOUCLE                               , &
                                       PJTABR                               , &
                                       PJTANN                               , &
                                       COMNUMCOMINT                           &
                                 USING CIECLE           OF PDCOMMAN_INTERNE , &
                                       OLDVALUE(FOUCLE2 OF PDCOMMAN_INTERNE), &
                                       PJTABR           OF PDCOMMAN_INTERNE , &
                                       PJTANN           OF PDCOMMAN_INTERNE , &
                                       COMNUMCOMINT     OF PDCOMMAN_INTERNE
             
    IF CAPDATJOU OF CPCPT_A_PAYER <> 0
    THEN ERROR "PD998E Modification impossible - agent associé à une facture C/P journalisée"
  END
END

;-------------------------------------------------------------------------------
;
; Mise à jour du time AUTORISÉ
;

PROCEDURE DESIGNER 25
BEGIN
  ACCEPT  COMATR           OF PDCOMMAN_INTERNE
  IF "1" = COMATR OF PDCOMMAN_INTERNE
  THEN BEGIN
    LET     COMDATMAJATR     OF PDCOMMAN_INTERNE = SYSDATE
    LET     COMUSRMAJATR     OF PDCOMMAN_INTERNE = LOGONID
    LET     COMHREMAJATR     OF PDCOMMAN_INTERNE = SYSTIME / 10000
  END
  DISPLAY COMDATMAJATR     OF PDCOMMAN_INTERNE
  DISPLAY COMUSRMAJATR     OF PDCOMMAN_INTERNE
END


;-------------------------------------------------------------------------------
;
; Mise à jour du time TERMINÉ
;

PROCEDURE DESIGNER 30
BEGIN
  ACCEPT COMTER OF PDCOMMAN_INTERNE
  
  IF "1" = COMTER OF PDCOMMAN_INTERNE
  THEN BEGIN
    LET COMDATTER OF PDCOMMAN_INTERNE = SYSDATE
    LET COMUSRTER OF PDCOMMAN_INTERNE = LOGONID
    LET COMHRETER OF PDCOMMAN_INTERNE = SYSTIME / 10000
    LET COMSTU    OF PDCOMMAN_INTERNE = "T"
  END
  ELSE LET COMSTU OF PDCOMMAN_INTERNE = "P"

  DISPLAY COMDATTER OF PDCOMMAN_INTERNE
  DISPLAY COMUSRTER OF PDCOMMAN_INTERNE
END


BUILD

@IF FORMAT
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************************************************************
* Num. commande.: xx-xx-xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
* Code terme....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Crédit........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   *
*                                                                              *
* Agent 1............: xxxxxx xxxxxxxxxxxxxxxxxxxx    Calcul automatique: xxx  *
* Commissions à payer: xxxxxxxxxxxxxxx    Commissions payées: xxxxxxxxxxxxxxx  *
* Agent 2............: xxxxxx xxxxxxxxxxxxxxxxxxxx    Calcul automatique: xxx  *
* Commissions à payer: xxxxxxxxxxxxxxx    Commissions payées: xxxxxxxxxxxxxxx  *
*                                                                              *
* Autorisation..: x  Date autorisa.: xxxxxxxxxx Usager autori.: xxxxxxxxxxxx   *
* Terminé.......: x  Date terminé..: xxxxxxxxxx Usager terminé: xxxxxxxxxxxx   *
* Num ltr client: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Num ltr comp..: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Dern.livr:xxxxxxxxxx*
********************************************************************************
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
@ENDIF
