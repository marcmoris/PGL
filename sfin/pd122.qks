;/T/ Dépisteur Tranche de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-05
;
;    Description..: Permet de visionner et de choisir une tranche de granit
;
;    Paramètres...: T_TRANUMTRA002  ==> Numéro de tranche
;                   T_COD           ==> 0 = Tous   ,   1 = (I ou R)
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd122 NOMODE NOACTION FIELDMARK &
             FROM 11,29 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_TRANUMTRA002, &
                       T_COD,          &
                       T_CIECLEMNU


TEMPORARY T_TRANUMTRA002     CHARACTER SIZE 07
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 02
TEMPORARY T_CIECLEMNU        CHARACTER SIZE 04

USE pd122.hlp NOLIST

USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,            &
                 TRANUMTRA002       &
         USING   T_CIECLEMNU,       &
                 T_TRANUMTRA002     &
         ORDERBY CIECLE,            &
                 TRANUMTRA002

  SELECT IF T_COD = 0 OR &
           (T_COD = 1 AND (TRASTU OF PDTRANCHE = "I" OR &
                           TRASTU OF PDTRANCHE = "R"))


;-------------------------------------------------------------------------------
;
; Récupérer l'information sur les assignations
;

FILE PDCOMMAN_ASSIGN REFERENCE

   ACCESS VIA   CIECLE,          &
                TRANUMTRA002     &
          USING T_CIECLEMNU,     &
                TRANUMTRA002 OF PDTRANCHE


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,52       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    Tranche   Long Haut Épa. Gra Fi S Num. Cmd " &
@ELSE
      "%%% Tranche   Long Haut Épa. Gra Fi S Num. Cmd " &
@ENDIF
    AT ,2

SKIP TO LINE 3

ALIGN (3,3,6) (,,16) (,,21) (,,26) (,,31) (,,35) (,,38) (,,40)


CLUSTER OCCURS WITH PDTRANCHE


FIELD TRANUMTRA002     OF PDTRANCHE        &
        HIDDEN LABEL ">>" DISPLAY


FIELD TRALON           OF PDTRANCHE        &
        DISPLAY


FIELD TRAHAU           OF PDTRANCHE        &
        DISPLAY


FIELD TRAEPA           OF PDTRANCHE        &
        DISPLAY


FIELD TGRCODGRA        OF PDTRANCHE        &
        DISPLAY


FIELD TYFCODFIN        OF PDTRANCHE        &
        DISPLAY


FIELD TRASTU           OF PDTRANCHE        &
        DISPLAY


FIELD COMNUMCOM        OF PDCOMMAN_ASSIGN  &
        DISPLAY


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_TRANUMTRA002 , "@")
  THEN LET T_TRANUMTRA002 = ""
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_TRANUMTRA002     = TRANUMTRA002 OF PDTRANCHE
  RETURN
END

BUILD

@IF FORMAT

                            ****************************************************
                            *    Tranche   Long Haut Épa. Gra Fi S Num. Cmd    *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            * >> xxxxxxxxx xxxx xxxx xxxx xxx xx x xx-xx-xxx   *
                            ****************************************************
@ENDIF
