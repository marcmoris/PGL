;/T/ Detail des transactions au projet pour compte/unite
;;

;/V/ Écran vérifier pour le passage à l'an 2000

;;
SCREEN pr010b ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND  &
             RECEIVING T_CIECLEMNU ,&
                       T_CIENOM, &
                       T_PROCLE

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_PROCLE    CHARACTER SIZE 8  RESET AT STARTUP

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "PR010B"             ; Nom de l'écran

USE pr010b.hlp nolist ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

FILE VHIP_01 PRIMARY OCCURS 4 NOITEMS
  ACCESS VIA CIECLE, PROCLE &
     USING T_CIECLEMNU,&
           T_PROCLE  &
   ORDERBY PECCLE, HISNUMDOC

FILE GPPROJET REFERENCE
  ACCESS VIA CIECLE, PROCLE USING T_CIECLEMNU, PROCLE OF VHIP_01

FILE VCPT_DSC REFERENCE
  ACCESS VIA CPTCLE USING CPTCLE OF VHIP_01

FILE MCUNITE_ADM REFERENCE
  ACCESS VIA CIECLE, UNACLE USING T_CIECLEMNU, UNACLE OF VHIP_01

FILE MCREFERENCE REFERENCE
  ACCESS VIA REFCIECLE, REFCLETYP, REFCLENUM &
       USING REFCIECLE OF VHIP_01, REFCLETYP OF VHIP_01, REFCLENUM OF VHIP_01




;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

DEFINE D_MNT     INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT  IF CPTCODDC = "C" &
                             ELSE HIPMNTDBT - HIPMNTCRT

DEFINE D_DSCGEN  CHARACTER SIZE 40 = &
   HISDSCGEN OF VHIP_01 IF HISDSCGEN OF VHIP_01 <> "" &
                      ELSE REFNOM OF MCREFERENCE
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Détail des transactions au projet compte/unité " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP
ALIGN (,2,13)(,,25)
FIELD PROCLE   OF VHIP_01 DISPLAY LABEL "Projet...:"
FIELD PRODSC   OF GPPROJET     DISPLAY SIZE 50
FIELD CPTCLE   OF VHIP_01 DISPLAY LABEL "Compte...:"
FIELD CPTDSC   OF VCPT_DSC     DISPLAY
FIELD UNACLE   OF VHIP_01 DISPLAY LABEL "Unite Adm:"
FIELD UNANOM   OF MCUNITE_ADM  DISPLAY
SKIP 1
TITLE "Sous              Période  " AT ,2
SKIP
TITLE "Projet Equipement   Ctb.    Adm.    # Reference        Montant    Source Lot" AT ,2
ALIGN (,,3)(,,9)(,,11)(,,21)(,,28)(,,36)(,,53)(,,71)(,,76)
CLUSTER OCCURS WITH VHIP_01
FIELD ACTCLE    OF VHIP_01 DISPLAY
FIELD UNICAT    OF VHIP_01 DISPLAY
FIELD UNICLE    OF VHIP_01 DISPLAY
FIELD PECCLE    OF VHIP_01      DISPLAY
FIELD HISNUMDOC OF VHIP_01      DISPLAY
FIELD D_MNT                     DISPLAY PICTURE "^^^,^^^,^^^.^^ " TRAIL "-" SIGNIF 5
FIELD HISCODMOD OF VHIP_01      DISPLAY
FIELD HISNUMLOT OF VHIP_01      DISPLAY
SKIP
FIELD D_DSCGEN  DISPLAY
SKIP
FIELD HISDSCSAI OF VHIP_01      DISPLAY
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


BUILD
