;/T/ 2.3.5 Gérer les replacement avec et sans charges
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-14 (Jour de la Saint-Valantin)
;
;    Description..: Cette écran permet de gérer les remplacements
;                   avec ou sans charge.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd207 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             ACTIVITIES CHANGE,FIND,ENTRY &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_REMPLACE IN DICT


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD207"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd207.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D001
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_PJTABR001    CHARACTER SIZE 02 RESET AT STARTUP
TEMPORARY T_PJTABR002    CHARACTER SIZE 02 RESET AT STARTUP


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;

TEMPORARY T_RRMCODREM    CHARACTER SIZE 04
TEMPORARY T_PJTABR       CHARACTER SIZE 02
TEMPORARY T_PJTANN       CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 03
TEMPORARY T_COMNUMCOM    CHARACTER SIZE 07
TEMPORARY T_DIANUM001    CHARACTER SIZE 07
TEMPORARY T_DIANUM002    CHARACTER SIZE 07


;-------------------------------------------------------------------------------
;
; Remplacement
;

FILE PDREMPLACEMENT PRIMARY &
                    NOITEM


  ACCESS VIA    CIECLE,          &
                REMNUMREM        &
         USING   T_CIECLEMNU,                         &
                 REMNUMREM        OF PDREMPLACEMENT   &
         REQUEST REMNUMREM        OF PDREMPLACEMENT   &
         ORDERBY CIECLE,         &
                 REMNUMREM


  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 REMNUMREM


  ITEM   CIECLE           OF PDREMPLACEMENT INITIAL T_CIECLEMNU
  ITEM   REMDATCRE        OF PDREMPLACEMENT INITIAL SYSDATE
  ITEM   REMHRECRE        OF PDREMPLACEMENT INITIAL SYSTIME / 10000
  ITEM   REMUSRCRE        OF PDREMPLACEMENT INITIAL LOGONID

  ITEM   REMDATMAJ        OF PDREMPLACEMENT FINAL   SYSDATE &
                                            IF NOT NEWRECORD OF PDREMPLACEMENT &
                                               AND &
                                               ALTEREDRECORD OF PDREMPLACEMENT

  ITEM   REMHREMAJ        OF PDREMPLACEMENT FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF PDREMPLACEMENT &
                                               AND &
                                               ALTEREDRECORD OF PDREMPLACEMENT

  ITEM   REMUSRMAJ        OF PDREMPLACEMENT FINAL   LOGONID &
                                            IF NOT NEWRECORD OF PDREMPLACEMENT &
                                               AND &
                                               ALTEREDRECORD OF PDREMPLACEMENT


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les remplacements
;

FILE PDSEQ_REMPLACE  DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Détail remplacement
;

FILE PDDET_REMPLACE        DETAIL &
                           OCCURS 8 &
                           NOITEM

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                         &
                 PJTABR           OF PDREMPLACEMENT,  &
                 PJTANN           OF PDREMPLACEMENT,  &
                 COMNUMCOMINT     OF PDREMPLACEMENT   &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DIANUMDIA002


  ITEM   CIECLE           OF PDDET_REMPLACE   INITIAL T_CIECLEMNU
  ITEM   REMNUMREM        OF PDDET_REMPLACE   INITIAL REMNUMREM OF PDREMPLACEMENT

  ITEM   PJTABR           OF PDDET_REMPLACE   FINAL   PJTABR           OF PDREMPLACEMENT
  ITEM   PJTANN           OF PDDET_REMPLACE   FINAL   PJTANN           OF PDREMPLACEMENT
  ITEM   COMNUMCOMINT     OF PDDET_REMPLACE   FINAL   COMNUMCOMINT     OF PDREMPLACEMENT


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE REFERENCE

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                         &
                 PJTABR           OF PDREMPLACEMENT,  &
                 PJTANN           OF PDREMPLACEMENT,  &
                 COMNUMCOMINT     OF PDREMPLACEMENT

  ITEM   COMNUMCOM    OF PDREMPLACEMENT FINAL COMNUMCOM   OF PDCOMMAN_INTERNE
  ITEM   PJTNUMSEQ    OF PDREMPLACEMENT FINAL PJTNUMSEQ   OF PDCOMMAN_INTERNE
  ITEM   PJTNUMPRO    OF PDREMPLACEMENT FINAL PJTNUMPRO   OF PDCOMMAN_INTERNE

  ITEM   COMNUMCOM    OF PDDET_REMPLACE FINAL COMNUMCOM   OF PDCOMMAN_INTERNE
  ITEM   PJTNUMSEQ    OF PDDET_REMPLACE FINAL PJTNUMSEQ   OF PDCOMMAN_INTERNE
  ITEM   PJTNUMPRO    OF PDDET_REMPLACE FINAL PJTNUMPRO   OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Diagramme père
;

FILE PDDIAGRAMME REFERENCE &
                 OCCURS WITH PDDET_REMPLACE &
                 ALIAS A_DIA_PERE


  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DIANUM002        &
         USING   T_CIECLEMNU,                         &
                 PJTABR           OF PDREMPLACEMENT,  &
                 PJTANN           OF PDREMPLACEMENT,  &
                 COMNUMCOMINT     OF PDREMPLACEMENT,  &
                 DIANUM001        OF PDDET_REMPLACE


;-------------------------------------------------------------------------------
;
; Diagramme fils
;

FILE PDDIAGRAMME REFERENCE &
                 OCCURS WITH PDDET_REMPLACE &
                 ALIAS A_DIA_FILS


  ACCESS VIA     CIECLE ,         &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DIANUM002        &
         USING   T_CIECLEMNU,                         &
                 PJTABR           OF PDREMPLACEMENT,  &
                 PJTANN           OF PDREMPLACEMENT,  &
                 COMNUMCOMINT     OF PDREMPLACEMENT,  &
                 DIANUM002        OF PDDET_REMPLACE


;-------------------------------------------------------------------------------
;
; Raison remplacement
;

FILE PDRAISON_REMPLAC REFERENCE OCCURS WITH PDDET_REMPLACE

  ACCESS VIA     CIECLE,          &
                 RRMCODREM        &
         USING   T_CIECLEMNU,     &
                 RRMCODREM        OF PDDET_REMPLACE


;-------------------------------------------------------------------------------
;
; PROJET
;

FILE PDPROJET REFERENCE

  ACCESS VIA     CIECLE,          &
                 PJTABR           &
         USING   T_CIECLEMNU,     &
                 PJTABR           OF PDREMPLACEMENT


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Remplacement " &
@ELSE
      " %%%Remplacement " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD REMNUMREM        OF PDREMPLACEMENT   &
label "Num. séquence.:"&
        HIDDEN ID 05                       &
        DISPLAY                            &
        REFRESH


ALIGN (2,3,19) (21,21,22) (24,24,25) (,,30)


FIELD PJTABR           OF PDREMPLACEMENT   &
        HIDDEN ID 10                       &
        REFRESH                            &
        REQUIRED                           &
        LABEL "Num. commande.:"            &
        LOOKUP ON PDPROJET                 &
          MESSAGE 3074 ; Le code de projet n'existe pas


FIELD PJTANN           OF PDREMPLACEMENT   &
        ID SAME                            &
        LABEL "-"                          &
        DISPLAY


FIELD COMNUMCOMINT     OF PDREMPLACEMENT   &
        ID SAME                            &
        LABEL "-"                          &
        NUMERIC                            &
        LOOKUP ON PDCOMMAN_INTERNE         &
          MESSAGE 3081 ; Ce numéro de commande n'existe pas


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        HIDDEN                             &
        DISPLAY


ALIGN (02,03,19)


FIELD REMDSC001        OF PDREMPLACEMENT   &
label "Description...:"&
        HIDDEN ID 25


ALIGN (,,19)


FIELD REMDSC002        OF PDREMPLACEMENT   &
        ID SAME


FIELD REMDSC003        OF PDREMPLACEMENT   &
        ID SAME



DRAW 11,2  TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Diagrammes " &
@ELSE
      " %%%Diagrammes " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
      "Diagra. Diagra. Date     Raison remplacement          C Qte    Prix pièce" &
@ELSE
      "%%%Diagra. Diagra. Date     Raison remplacement          C Qte    Prix piècece
" &
@ENDIF
      AT ,3


SKIP TO LINE 14


ALIGN (02,02,3) (,,11) (,,19) (,,30) (,,35) (,,57) (,,59) (,,64)


CLUSTER OCCURS WITH PDDET_REMPLACE ID BASE 50


FIELD DIANUM001        OF PDDET_REMPLACE   &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED                           &
        LOOKUP ON A_DIA_PERE               &
          MESSAGE 2998 ; Ce numéro de diagramme n'existe pas



FIELD DIANUM002        OF PDDET_REMPLACE   &
        HIDDEN                             &
        REQUIRED                           &
        LOOKUP ON A_DIA_FILS               &
          MESSAGE 2998 ; Ce numéro de diagramme n'existe pas


FIELD DRMDATREM        OF PDDET_REMPLACE    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        HIDDEN                             &
        REQUIRED                           &
        DUPLICATE


FIELD RRMCODREM        OF PDDET_REMPLACE   &
        HIDDEN                             &
        REQUIRED                           &
        DUPLICATE                          &
        LOOKUP ON PDRAISON_REMPLAC         &
          VIA     CIECLE,                  &
                  RRMCODREM                &
          USING   T_CIECLEMNU,                       &
                  RRMCODREM        OF PDDET_REMPLACE &
          MESSAGE 2976 ; Cette raison n'existe pas


FIELD RRMDSC001        OF PDRAISON_REMPLAC &
        HIDDEN                             &
        DISPLAY                            &
        SIZE 21


FIELD DRMTYPCHG        OF PDDET_REMPLACE   &
        HIDDEN                             &
        DUPLICATE                          &
        REQUIRED


FIELD DRMQTEREM        OF PDDET_REMPLACE   &
        HIDDEN                             &
        REQUIRED


FIELD DRMPRIUNI        OF PDDET_REMPLACE   &
        HIDDEN                             &
        REQUIRED


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT PJTABR OF PDREMPLACEMENT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN

    LET T_PJTABR       = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PJTANN       = " "
    LET T_COMNUMCOMINT = " "
    LET T_COMNUMCOM    = " "

    RUN SCREEN pd130 MODE F &
                     PASSING T_PJTABR, &
                             T_PJTANN, &
                             T_COMNUMCOMINT, &
                             T_COMNUMCOM, &
                             T_CIECLEMNU


    LET     PJTANN       OF PDREMPLACEMENT = TRUNCATE( T_PJTANN )
    LET     COMNUMCOMINT OF PDREMPLACEMENT = TRUNCATE( T_COMNUMCOMINT )
    LET     COMNUMCOM    OF PDREMPLACEMENT = TRUNCATE( T_COMNUMCOM )
    ;
    ; Doit être le dernier champ initialisé sinon on risque de perdre la valeur
    ;
    LET     FIELDTEXT                      = TRUNCATE( T_PJTABR )
  END
END


;-------------------------------------------------------------------------------
;
; assigne le code de projet pour les diagrammes père et fils
;

PROCEDURE PROCESS PJTABR
BEGIN
  LET     T_PJTABR001 = PJTABR OF PDREMPLACEMENT
  LET     T_PJTABR002 = PJTABR OF PDREMPLACEMENT
  LET PJTANN OF PDREMPLACEMENT = PJTANN OF PDPROJET
  DISPLAY PJTABR       OF PDREMPLACEMENT
  DISPLAY PJTANN       OF PDREMPLACEMENT
  DISPLAY COMNUMCOMINT OF PDREMPLACEMENT
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT COMNUMCOMINT OF PDREMPLACEMENT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN

    LET T_PJTABR       = PJTABR OF PDREMPLACEMENT
    LET T_PJTANN       = PJTANN OF PDREMPLACEMENT
    LET T_COMNUMCOMINT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_COMNUMCOM    = " "

    RUN SCREEN pd130 MODE F &
                     PASSING T_PJTABR, &
                             T_PJTANN, &
                             T_COMNUMCOMINT, &
                             T_COMNUMCOM,&
                             T_CIECLEMNU


    LET     COMNUMCOM    OF PDREMPLACEMENT = TRUNCATE( T_COMNUMCOM )
    ;
    ; Doit être le dernier champ initialisé sinon on risque de perdre la valeur
    ;
    LET     FIELDTEXT                      = TRUNCATE( T_COMNUMCOMINT )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ et ajoute le code de projet si l'utilisateur
; ne l'a pas saisit.
;

PROCEDURE INPUT DIANUM001
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DIANUM001 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd126 MODE F &
                     PASSING T_DIANUM001, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DIANUM001 )
  END
  ;
  ; Ajout du code de projet si l'utilisateur ne l'a pas saisit
  ;
  IF 5 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    LET FIELDTEXT = PJTABR OF PDREMPLACEMENT + FIELDTEXT
  END
END


;-------------------------------------------------------------------------------
;
; Valide le champ
;

PROCEDURE EDIT DIANUM001
BEGIN
  IF FIELDTEXT[1:2] <> PJTABR OF PDREMPLACEMENT
  THEN BEGIN
    ERROR 3149 ; Le code de projet du diagramme ne correspond pas à celui de la commande
  END
END


;-------------------------------------------------------------------------------
;
; Initialise le champ DIANUMDIA001
;

PROCEDURE PROCESS DIANUM001
BEGIN
  LET DIANUMDIA001 OF PDDET_REMPLACE = DIANUM001 OF PDDET_REMPLACE[3:5]
END


;-------------------------------------------------------------------------------
;
; Ajoute le code de projet si l'utilisateru ne l'a pas saisit
;

PROCEDURE INPUT DIANUM002
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DIANUM002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd126 MODE F &
                     PASSING T_DIANUM002, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DIANUM002 )
  END
  ;
  ; Ajout du code de projet si l'utilisateur ne l'a pas saisit
  ;
  IF 5 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    LET FIELDTEXT = PJTABR OF PDREMPLACEMENT + FIELDTEXT
  END
END


;-------------------------------------------------------------------------------
;
; Valide le champ
;

PROCEDURE EDIT DIANUM002
BEGIN
  IF FIELDTEXT[1:2] <> PJTABR OF PDREMPLACEMENT
  THEN BEGIN
    ERROR 3149 ; Le code de projet du diagramme ne correspond pas à celui de la commande
  END
  ;
  ; Valide que le diagramme n'a pas déjà été facturer
  ;
  IF 0 <> DIAQTEFAC OF A_DIA_FILS
  THEN BEGIN
    ERROR 3210 ; On ne peut utiliser un diagramme déjà facturé pour un remplacement
  END
END


;-------------------------------------------------------------------------------
;
; Initialise le champ DIANUMDIA002
;

PROCEDURE PROCESS DIANUM002
BEGIN
  LET DIANUMDIA002 OF PDDET_REMPLACE = DIANUM002 OF PDDET_REMPLACE[3:5]
END


;-------------------------------------------------------------------------------
;
; Validation de la date de remplacement
;

PROCEDURE EDIT DRMDATREM
BEGIN
  IF FIELDVALUE < COMDATCMD OF PDCOMMAN_INTERNE
  THEN  BEGIN
    ERROR 3100 ; La date saisie doit être plus grande que la date de la commande interne
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT RRMCODREM
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RRMCODREM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd106 MODE F &
                     PASSING T_RRMCODREM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RRMCODREM )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;

PROCEDURE INPUT DRMTYPCHG
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT DRMTYPCHG
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Validation de la quantité
;

PROCEDURE EDIT DRMQTEREM
BEGIN
  IF FIELDVALUE > DIAQTECMD        OF A_DIA_PERE
  THEN BEGIN
    ERROR 3101 ; La quantité ne doit pas être suppérieur à la quantité commandé
  END
END


;----------------------------------------------------------------------
;
; Valide le nombre de décimale
;

PROCEDURE EDIT DRMPRIUNI
BEGIN
  USE usvaldec2 NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Saisie de la description du remplacement
;

PROCEDURE DESIGNER 25
BEGIN
  ACCEPT REMDSC001        OF PDREMPLACEMENT
  ACCEPT REMDSC002        OF PDREMPLACEMENT
  ACCEPT REMDSC003        OF PDREMPLACEMENT
END


;-------------------------------------------------------------------------------
;
; Affecte les valeurs des champs temporaire
;

PROCEDURE POSTFIND
BEGIN
  LET     T_PJTABR001 = PJTABR OF PDREMPLACEMENT
  LET     T_PJTABR002 = PJTABR OF PDREMPLACEMENT
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDREMPLACEMENT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDREMPLACEMENT &
     AND NOT NEWRECORD     OF PDREMPLACEMENT &
     AND NOT DELETEDRECORD OF PDREMPLACEMENT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = REMNUMREM OF PDREMPLACEMENT
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_REMPLACE OPTIONAL &
      VIA     CIECLE            &
      USING   T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_REMPLACE = T_CIECLEMNU
    END
    LET     SQRNUMSEQ        OF PDSEQ_REMPLACE   = SQRNUMSEQ        OF PDSEQ_REMPLACE  + 1
    LET     REMNUMREM        OF PDREMPLACEMENT   = SQRNUMSEQ        OF PDSEQ_REMPLACE
    PUT     PDSEQ_REMPLACE
    DISPLAY REMNUMREM        OF PDREMPLACEMENT
  END
END


;-------------------------------------------------------------------------------
;
; Vérification
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd207a &
              PASSING T_CIECLEMNU,    &
                      T_CIENOM,       &
                      PDREMPLACEMENT  &
              MODE F
END


BUILD


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************* Remplacement *********************************
*                                                                              *
* Num. séquence.: xxxxxxxxxxx                                                  *
* Num. commande.: xx-xx-xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
********************************** Diagrammes **********************************
*                                                                              *
* Diagra. Diagra. Date     Raison remplacement          C Qte  Prix unit.      *
* xxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
* xxxxxxx xxxxxxx xxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxx x xxxx xxxxxxxxxxxxxxx *
*                                                                              *
********************************************************************************
@ENDIF
