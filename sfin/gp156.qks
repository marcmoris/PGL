;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Format de présentation
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp156 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "GP156"             ; Nom de l'écran

USE gp156.hlp NOLIST    ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Détail du format            " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Détail du format         " ACTION DESIGNER D001
@ENDIF

;------------------------------------
; Fichier d'unité de travail
;------------------------------------
FILE GPFORMATS              PRIMARY &
                            NOITEMS

  ACCESS  VIA     FRMCLE &
          USING   FRMCLE OF GPFORMATS &
          REQUEST FRMCLE &
          ORDERBY FRMCLE

  ACCESS  VIA     FRMTYP &
          USING   FRMTYP OF GPFORMATS &
          REQUEST FRMTYP &
          ORDERBY FRMCLE

  ACCESS  SEQUENTIAL &
          ORDERBY FRMCLE



;--------------------------------------------------------------------------
; Validation du type de format de présentation et lecture de sa description
;--------------------------------------------------------------------------
DEFINE    D_FRMTYP CHARACTER SIZE 16 = "FRMTYP"
TEMPORARY T_FRMTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_FRMTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_FRMTYP, &
                FRMTYP OF GPFORMATS


;----------------------------------------------
; Fichier de desctruction des détails de format
;----------------------------------------------
FILE GPFOR_DETAILS          DELETE

  ACCESS  VIA   FRMCLE  &
          USING FRMCLE OF GPFORMATS



;----------------------------------------------
; Fichier de desctruction des détails de format
;----------------------------------------------
FILE GPFRD_DOMAINE          DELETE

  ACCESS  VIA   FRMCLE  &
          USING FRMCLE OF GPFORMATS



;----------------------------------------------
; Fichier de desctruction des détails de format
;----------------------------------------------
FILE GPFRD_SOMME            DELETE

  ACCESS  VIA   FRMCLE  &
          USING FRMCLE OF GPFORMATS



;-------------------------------------------------------------------------------
;
TEMPORARY T_TITRE   CHARACTER SIZE 70

DEFINE DZ_CIECLE    CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM    CHARACTER SIZE 40 = CENTER( T_CIENOM )


DRAW 3,01 TO 16,80

USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Format de présentation " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19)(,46,62)(,,64)

FIELD FRMCLE OF GPFORMATS                             &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LOOKUP NOTON GPFORMATS                          &
        VIA   FRMCLE                                  &
        USING FRMCLE OF GPFORMATS                     &
        MESSAGE 826; Ce format de présentation existe déjà

FIELD FRMTYP OF GPFORMATS                             &
      ID SAME                                         &
      REQUIRED NOCHANGE                               &
      LOOKUP ON MCCODE_BUIL_FRMTYP                    &
        MESSAGE 827; Ce type de format de présentation est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_FRMTYP                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 15

ALIGN (3,3,19)
FIELD FRMDSC OF GPFORMATS                             &
      HIDDEN                                          &
      REQUIRED

SKIP 1

TITLE &
@IF FRANCAIS
      " Titre du format de présentation " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

SKIP 1

ALIGN (5,3,5)
FIELD FRMTIT1 OF GPFORMATS                            &
      LABEL " "                                       &
      HIDDEN

FIELD FRMTIT2 OF GPFORMATS                            &
      LABEL " "                                       &
      HIDDEN

FIELD FRMTIT3 OF GPFORMATS                            &
      LABEL " "                                       &
      HIDDEN

FIELD FRMTIT4 OF GPFORMATS                            &
      LABEL " "                                       &
      HIDDEN

FIELD FRMTIT5 OF GPFORMATS                            &
      LABEL " "                                       &
      HIDDEN



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT FRMTYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FRMTYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_FRMTYP, T_FRMTYP
    LET FIELDTEXT = TRUNCATE(T_FRMTYP)
  END
END


PROCEDURE EDIT FRMTIT1
BEGIN
   LET T_TITRE   = FIELDTEXT
   LET FIELDTEXT = CENTER( T_TITRE )
END

PROCEDURE EDIT FRMTIT2
BEGIN
   LET T_TITRE   = FIELDTEXT
   LET FIELDTEXT = CENTER( T_TITRE )
END

PROCEDURE EDIT FRMTIT3
BEGIN
   LET T_TITRE   = FIELDTEXT
   LET FIELDTEXT = CENTER( T_TITRE )
END

PROCEDURE EDIT FRMTIT4
BEGIN
   LET T_TITRE   = FIELDTEXT
   LET FIELDTEXT = CENTER( T_TITRE )
END

PROCEDURE EDIT FRMTIT5
BEGIN
   LET T_TITRE   = FIELDTEXT
   LET FIELDTEXT = CENTER( T_TITRE )
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran de détail du format de présentation.
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN gp156a PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPFORMATS    &
                MODE F
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPFORMATS       &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPFORMATS       &
     AND NOT NEWRECORD     OF GPFORMATS       &
     AND NOT DELETEDRECORD OF GPFORMATS       &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Format de présentation ***************************
*                                                                             *
*Format........: xxxx                      Type..........: x xxxxxxxxxxxxxxx  *
*Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                             *
*                       Titre du format de présentation                       *
*                                                                             *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                             *
*                                                                             *
*                                                                             *
*                                                                             *
*                                                                             *
*                                                                             *
*                                                                             *
*                                                                             *
*                                                                             *
*******************************************************************************
@ENDIF

