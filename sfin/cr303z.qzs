;/T/ Vente par catégorie et par client
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Alain Côté
;    Date Création.: 29 juillet 1993
;
;    Description...: Pour consulter les ventes par catégorie et par client
;                    et cela par année financière.
;
;    Paramètres....: Année (une seule)
;                    Catégorie (1, plusieurs,tous)
;
;
;------------------------------------------------------------------------------

USE ussetqzs NOLIST


;
;
; Dans la première étape on demande les catégories en paramètre, c'est pour
; cette raison que l'on accède la table client en premier.
;
;
ACCESS VCLC_CLI

CHOOSE CLICIECLE PARM , & ; Compagnie de la charte client
       CIECLE    PARM , & ; Compagnie du menu
       CLICAT    PARM     ; Catégorie

DEFINE D_ANNFIN CHARACTER SIZE 2 = PARM ; Année financière

SET SUBFILE NAME WCR303

REPORT SUMMARY &
  CLICIECLE OF VCLC_CLI &
  CLICLE    OF VCLC_CLI &
  CLINOM    OF VCLC_CLI &
  CLICAT    OF VCLC_CLI &
  CIECLE    OF VCLC_CLI &
  D_ANNFIN

BUILD cr303za


;-------------------------------------------------------------------------------
;
;
; Lecture des statistiques selon la compagnie et l'année financière reçu
; de l'étape précédente.
;
;
;
ACCESS *WCR303 &
  ;
  ; Statistique.
  ;
  LINK CLICIECLE OF WCR303 , &
       CLICLE    OF WCR303 , &
       CIECLE    OF WCR303 , &
       D_ANNFIN  OF WCR303   &
    TO CLICIECLE , &
       CLICLE    , &
       CIECLE    , &
       CLSANN OF     CRCLI_STAT &
  ;
  ; Catégorie, pour avoir la description.
  ;
  LINK ( "CLICAT" , &
         CLICAT OF WCR303 ) &
    TO XELNOM , &
       CBICLE OF     VCBI_DSC OPTIONAL



;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
             "Ventes par catégorie et par client"
@ELSE
             "%%%Ventes par catégorie et par client"
@ENDIF


DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CR303"

;
; Compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; On fait le total annuel des statistiques pour l'impression.
;
DEFINE D_CLSCUMFAC FLOAT SIZE 8 = CLSCUMFAC01 OF CRCLI_STAT + &
                                  CLSCUMFAC02 OF CRCLI_STAT + &
                                  CLSCUMFAC03 OF CRCLI_STAT + &
                                  CLSCUMFAC04 OF CRCLI_STAT + &
                                  CLSCUMFAC05 OF CRCLI_STAT + &
                                  CLSCUMFAC06 OF CRCLI_STAT + &
                                  CLSCUMFAC07 OF CRCLI_STAT + &
                                  CLSCUMFAC08 OF CRCLI_STAT + &
                                  CLSCUMFAC09 OF CRCLI_STAT + &
                                  CLSCUMFAC10 OF CRCLI_STAT + &
                                  CLSCUMFAC11 OF CRCLI_STAT + &
                                  CLSCUMFAC12 OF CRCLI_STAT + &
                                  CLSCUMFAC13 OF CRCLI_STAT
;
DEFINE D_CLSCUMESC FLOAT SIZE 8 = CLSCUMESC01 OF CRCLI_STAT + &
                                  CLSCUMESC02 OF CRCLI_STAT + &
                                  CLSCUMESC03 OF CRCLI_STAT + &
                                  CLSCUMESC04 OF CRCLI_STAT + &
                                  CLSCUMESC05 OF CRCLI_STAT + &
                                  CLSCUMESC06 OF CRCLI_STAT + &
                                  CLSCUMESC07 OF CRCLI_STAT + &
                                  CLSCUMESC08 OF CRCLI_STAT + &
                                  CLSCUMESC09 OF CRCLI_STAT + &
                                  CLSCUMESC10 OF CRCLI_STAT + &
                                  CLSCUMESC11 OF CRCLI_STAT + &
                                  CLSCUMESC12 OF CRCLI_STAT + &
                                  CLSCUMESC13 OF CRCLI_STAT
;
DEFINE D_CLSNBRJRSDPO FLOAT SIZE 8 = CLSNBRJRSDPO01 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO02 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO03 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO04 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO05 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO06 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO07 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO08 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO09 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO10 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO11 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO12 OF CRCLI_STAT + &
                                     CLSNBRJRSDPO13 OF CRCLI_STAT
;
DEFINE D_CLSNBRDPO FLOAT SIZE 8 = CLSNBRDPO01 OF CRCLI_STAT + &
                                  CLSNBRDPO02 OF CRCLI_STAT + &
                                  CLSNBRDPO03 OF CRCLI_STAT + &
                                  CLSNBRDPO04 OF CRCLI_STAT + &
                                  CLSNBRDPO05 OF CRCLI_STAT + &
                                  CLSNBRDPO06 OF CRCLI_STAT + &
                                  CLSNBRDPO07 OF CRCLI_STAT + &
                                  CLSNBRDPO08 OF CRCLI_STAT + &
                                  CLSNBRDPO09 OF CRCLI_STAT + &
                                  CLSNBRDPO10 OF CRCLI_STAT + &
                                  CLSNBRDPO11 OF CRCLI_STAT + &
                                  CLSNBRDPO12 OF CRCLI_STAT + &
                                  CLSNBRDPO13 OF CRCLI_STAT


; Ajustement pour le changement de siècle
DEFINE D_CLSANN_SIE CHARACTER SIZE 3 = "1" + CLSANN OF CRCLI_STAT[1:2] &
         IF CLSANN OF CRCLI_STAT[1:1] < "8" &
         ELSE "0" + CLSANN OF CRCLI_STAT[1:2]

SORT ON D_CLSANN_SIE & ; Année financière.
     ON CLICAT OF WCR303     & ; Catégorie
     ON CLICLE OF WCR303       ; Numéro de client


;
; Impression de l'en-tête.
;
USE ustitstd NOLIST
  SKIP 2 &
@IF FRANCAIS
  TAB 001 "Année financière:" &
@ELSE
  TAB 001 "Fiscal Year     :" &
@ENDIF
  TAB 019 CLSANN OF CRCLI_STAT &
SKIP 2 &

  TAB 001 &
@IF FRANCAIS
"                                                                                                          Montant      Délai moyen" &
@ELSE
"%%%                                                                                                       Montant      Délai moyen" &
@ENDIF
 SKIP &
  TAB 001 &
@IF FRANCAIS
"           Catégorie                                  Client                             Vente            escompte     encaissement" &
@ELSE
"%%%        Catégorie                                  Client                             Vente            escompte     encaissement" &
@ENDIF
 SKIP &

  TAB 001 &
" ------------------------------   -----------------------------------------------   ---------------   ---------------  ------------"



HEADING AT CLICAT &
  SKIP PAGE



REPORT &
  TAB 002 CLICAT OF WCR303   PRINT AT CLICAT &
  TAB 007 CBIDSC OF VCBI_DSC PRINT AT CLICAT &
  TAB 035 CLICLE OF WCR303                   &
  TAB 042 CLINOM OF WCR303                   &
  TAB 085 D_CLSCUMFAC                        &
            PICTURE "^^^,^^^,^^^.^^ "        &
            TRAILING SIGN "-"                &
            SIGNIFICANCE 5                   &
  TAB 103 D_CLSCUMESC                        &
            PICTURE "^^^,^^^,^^^.^^ "        &
            TRAILING SIGN "-"                &
            SIGNIFICANCE 5                   &
  TAB 123 D_CLSNBRJRSDPO RATIO D_CLSNBRDPO   &
            PICTURE "^,^^^ "                 &
            TRAILING SIGN "-"                &
            OUTPUT SCALE 0                   &
            SIGNIFICANCE 2



FOOTING AT CLICAT &
 SKIP 2 &
  TAB 085 "---------------" &
  TAB 103 "---------------" &
  TAB 123 "------"          &
 SKIP 1 &

@IF FRANCAIS
  TAB 054 "Total catégorie:" &
@ELSE
  TAB 054 "Category Total :" &
@ENDIF
  TAB 085 D_CLSCUMFAC    SUBTOTAL     &
            PICTURE "^^^,^^^,^^^.^^ " &
            TRAILING SIGN "-"         &
            SIGNIFICANCE 5            &
  TAB 103 D_CLSCUMESC    SUBTOTAL     &
            PICTURE "^^^,^^^,^^^.^^ " &
            TRAILING SIGN "-"         &
            SIGNIFICANCE 5            &
  TAB 123 D_CLSNBRJRSDPO RATIO D_CLSNBRDPO &
            PICTURE "^,^^^ "          &
            TRAILING SIGN "-"         &
            OUTPUT SCALE 0            &
            SIGNIFICANCE 2            &
 SKIP 2



FOOTING AT D_CLSANN_SIE           &
 SKIP 2                     &
  TAB 085 "---------------" &
  TAB 103 "---------------" &
  TAB 123 "------"          &
 SKIP 1                     &
  TAB 054 "Grand total    :"          &
  TAB 085 D_CLSCUMFAC  SUBTOTAL       &
            PICTURE "^^^,^^^,^^^.^^ " &
            TRAILING SIGN "-"         &
            SIGNIFICANCE 5            &
  TAB 103 D_CLSCUMESC  SUBTOTAL       &
            PICTURE "^^^,^^^,^^^.^^ " &
            TRAILING SIGN "-"         &
            SIGNIFICANCE 5            &
  TAB 123 D_CLSNBRJRSDPO RATIO D_CLSNBRDPO &
            PICTURE "^,^^^ "          &
            TRAILING SIGN "-"         &
            OUTPUT SCALE 0            &
            SIGNIFICANCE 2            &
 SKIP 1 &
  TAB 085 "===============" &
  TAB 103 "===============" &
  TAB 123 "======"


BUILD cr303zb

@IF FORMAT

CR303                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                     Ventes par catégorie et par client
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Année financière: 99

                                                                                                          Montant      Délai moyen
           Catégorie                                  Client                             Vente            escompte     encaissement
 ------------------------------   -----------------------------------------------   ---------------   ---------------  ------------
 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

                                                                                    ---------------   ---------------     ------
                                                     Grand total    :               999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ===============   ===============     ======
@ENDIF
