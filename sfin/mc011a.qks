;/T/ Copie d'une charte comptable
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 2 avril 1993
;
;
;    Description..: Sous-écran pour la copie d'une charte de compte. La charte
;                   d'unité adm. d'origine doit exister et celui de destination
;                   ne doit pas exister.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN mc011a NOMODE NOACTION FIELDMARK &
              FROM 6,15 TO 16,64 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              AUTOUPDATE &
              ACTIVITIES ENTRY

USE ussectmp NOLIST     ; Variable pour la securite
    "MC011A"

USE mc011a.hlp NOLIST


;-------------------------------------------------------------------------------
;
;
;
; Validation de la compagnie d'origine.
;
FILE MCCOMPAGNIE      DESIGNER &
                      ALIAS A_CIE_ORIG &
                      OPEN READ

;
; Validation de la compagnie de destination.
;
FILE MCCOMPAGNIE      DESIGNER &
                      ALIAS A_CIE_DEST &
                      OPEN READ
;
; Validation de l'unité adm. d'origine.
;
FILE MCUNITE_ADM      DESIGNER &
                      ALIAS A_UNA_ORIG &
                      OPEN READ

;
; Validation de l'unité adm. de destination.
;
FILE MCUNITE_ADM      DESIGNER &
                      ALIAS A_UNA_DEST &
                      OPEN READ
;
; Charte de compte d'origine.
;
FILE MCCHARTE_UNA     DESIGNER &
                      ALIAS A_CCU_ORIG &
                      OPEN READ

;
; Charte de compte de destination.
;
FILE MCCHARTE_UNA     DESIGNER &
                      ALIAS A_CCU_DEST &
                      OPEN 2


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_CIECLE_ORI CHARACTER SIZE 4 ; Compagnie d'origine
TEMPORARY T_UNACLE_ORI CHARACTER SIZE 8 ; Unité adm. d'origine
TEMPORARY T_CIECLE_DES CHARACTER SIZE 4 ; Compagnie de destination
TEMPORARY T_UNACLE_DES CHARACTER SIZE 8 ; Unité adm. de destination

TEMPORARY T_CIECLE     CHARACTER SIZE 4 ; Popup compagnie et unité adm.
TEMPORARY T_UNACLE     CHARACTER SIZE 8 ; Popup unité administrative

;-------------------------------------------------------------------------------
;
;
USE ushilite NOLIST     ; Hilite pour tout les écrans


DRAW 1,1 TO 11,50

TITLE &
@IF FRANCAIS
      " Copie d'une charte " &
@ELSE
      " Chart copy " &
@ENDIF
      CENTERED AT 1,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

DRAW ,1 TO ,50

TITLE &
@IF FRANCAIS
      " Origine " &
@ELSE
      " Source " &
@ENDIF
      CENTERED AT ,1
SKIP

ALIGN(2,3,19) (,,28)
FIELD T_CIECLE_ORI HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Companie......:" &
@ENDIF
        REQUIRED &
        LOOKUP ON A_CIE_ORIG &
          VIA CIECLE &
          USING T_CIECLE_ORI &
          MESSAGE 120 ; Cette compagnie n'existe pas.

FIELD CIENOMABR OF A_CIE_ORIG &
        DISPLAY

FIELD T_UNACLE_ORI ID SAME &
@IF FRANCAIS
        LABEL "Unité adm.....:" &
@ELSE
        LABEL "Unit adm......:" &
@ENDIF
        REQUIRED &
        LOOKUP ON A_UNA_ORIG &
          VIA CIECLE, &
              UNACLE  &
          USING T_CIECLE_ORI, &
                T_UNACLE_ORI  &
          MESSAGE 116 ; Cette unité administrative n'existe pas.

FIELD UNANOMABR OF A_UNA_ORIG &
        DISPLAY

SKIP 1

DRAW ,1 TO ,50

TITLE &
@IF FRANCAIS
      " Destination " &
@ELSE
      " Target " &
@ENDIF
      CENTERED AT ,1
SKIP

ALIGN(2,3,19) (,,28)
FIELD T_CIECLE_DES HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Companie......:" &
@ENDIF
        REQUIRED &
        LOOKUP ON A_CIE_DEST &
          VIA CIECLE &
          USING T_CIECLE_DES &
          MESSAGE 120 ; Cette compagnie n'existe pas.

FIELD CIENOMABR OF A_CIE_DEST &
        DISPLAY

FIELD T_UNACLE_DES ID SAME &
@IF FRANCAIS
        LABEL "Unité adm.....:" &
@ELSE
        LABEL "Unit adm......:" &
@ENDIF
        REQUIRED &
        LOOKUP ON A_UNA_DEST &
          VIA CIECLE, &
              UNACLE  &
          USING T_CIECLE_DES, &
                T_UNACLE_DES  &
          MESSAGE 116 ; Cette unité administrative n'existe pas.

FIELD UNANOMABR OF A_UNA_DEST &
        DISPLAY


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLE_ORI
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 MODE F PASSING T_CIECLE
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administrative.
;
;
PROCEDURE INPUT T_UNACLE_ORI
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLE_ORI
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLE,T_UNACLE
    LET FIELDTEXT = TRUNC(T_UNACLE)
  END
  ;
  ; Force le LOOKUP
  ;
  IF     NOT FINDMODE &
     AND 0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = T_UNACLE_ORI
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLE_DES
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 MODE F PASSING T_CIECLE
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administrative.
;
;
PROCEDURE INPUT T_UNACLE_DES
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = T_CIECLE_ORI
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLE,T_UNACLE
    LET FIELDTEXT = TRUNC(T_UNACLE)
  END
  ;
  ; Force le LOOKUP
  ;
  IF     NOT FINDMODE &
     AND 0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = T_UNACLE_DES
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'unité d'origine est différente de l'unité de destination.
;
;
PROCEDURE EDIT T_UNACLE_DES
BEGIN
  IF T_CIECLE_ORI = T_CIECLE_DES AND &
     T_UNACLE_ORI = T_UNACLE_DES
  THEN ERROR 136 ; On ne peut pas spécifier la même unité adm. (orig.,dest)
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Si l'usager n'a pas saisie tous les champs on retourne à l'écran
  ; précédent sans faire de traitement.
  ;
  IF T_CIECLE_ORI = "" OR &
     T_UNACLE_ORI = "" OR &
     T_UNACLE_DES = "" OR &
     T_UNACLE_DES = ""
  THEN RETURN

  ;
  ; Lecture de la charte d'origine.
  ;
  WHILE RETRIEVING A_CCU_ORIG VIA CIECLE, &
                                  UNACLE  &
                              USING T_CIECLE_ORI, &
                                    T_UNACLE_ORI
  BEGIN
    ;
    ; On ajoute la charte de compte de destination seulement si elle
    ; n'est pas présente.
    ;
    GET A_CCU_DEST VIA CIECLE, &
                       UNACLE, &
                       CPTCLE  &
                   USING T_CIECLE_DES, &
                         T_UNACLE_DES, &
                         CPTCLE OF A_CCU_ORIG &
                   OPTIONAL
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE     OF A_CCU_DEST = T_CIECLE_DES
      LET UNACLE     OF A_CCU_DEST = T_UNACLE_DES
      LET CPTCLE     OF A_CCU_DEST = CPTCLE     OF A_CCU_ORIG
      LET CCUPECDEBA OF A_CCU_DEST = CCUPECDEBA OF A_CCU_ORIG
      LET CCUPECDEBI OF A_CCU_DEST = CCUPECDEBI OF A_CCU_ORIG
      PUT A_CCU_DEST RESET
    END
  END
  RETURN
END


BUILD
@IF FORMAT


              *************** Copie d'une charte ***************
              *                                                *
              ******************** Origine *********************
              * Compagnie.....: xxxx     xxxxxxxxxxxxxxxxxxxx  *
              * Unité adm.....: xxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
              *                                                *
              ****************** Destination *******************
              * Compagnie.....: xxxx     xxxxxxxxxxxxxxxxxxxx  *
              * Unité adm.....: xxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
              *                                                *
              **************************************************
@ENDIF
