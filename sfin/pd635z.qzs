;/T/ Résumé des diagrammes à finir par commande interne
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-17
;
;    Description...: Ce rapport permet d'imprimer la liste des diagrammes à
;                    finir pour une commande interne.
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDPRIORITE_DIAG                  &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       COMNUMCOM    OF PDPRIORITE_DIAG, &
       PRICODPRI    OF PDPRIORITE_DIAG  &
    TO CIECLE,                          &
       COMNUMCOM,                       &
       PRICODPRI    OF PDPRIORITE       &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       DIANUM002    OF PDPRIORITE_DIAG  &
    TO CIECLE,                          &
       DIANUM002    OF PDDIAGRAMME      &

  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       COMNUMCOM    OF PDPRIORITE_DIAG  &
    TO CIECLE,                          &
       COMNUMCOM    OF PDCOMMAN_INTERNE &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       DIANUM002    OF PDDIAGRAMME      &
    TO CIECLE,                          &
       DIANUM002    OF PDUNI_TRAV_DIA   OPTIONAL &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       UTRNUMUNITRV OF PDUNI_TRAV_DIA   &
    TO CIECLE,                          &
       UTRNUMUNITRV OF PDUNITE_TRAVAIL  &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       UTRCODUNMPRD OF PDUNITE_TRAVAIL  &
    TO CIECLE,                          &
       UNMCODUNM    OF PDUNITE_MESURE


CHOOSE CIECLE    PARM , &
       COMNUMCOM PARM , &
       DIANUM002 PARM , &
       PRICODPRI PARM , &
       DCINUMLIG PARM



SELECT IF (DIAQTECMD OF PDDIAGRAMME > DIAQTEEMB OF PDDIAGRAMME) and &
           (0 <> DIAQTEPLADBTGRA OF VDIAQTE)


SORT ON COMNUMCOM OF PDPRIORITE_DIAG &
     ON PRICODPRI    OF PDPRIORITE_DIAG &
     ON UTRNUMUNITRV OF PDUNI_TRAV_DIA


;-------------------------------------------------------------------------------
;
; Définition de variable nécessaire au rapport
;
DEFINE D_QTE_PIE_PRD   INTEGER SIZE 04 = DIAQTEPLAFINGRA OF VDIAQTE - DIAQTEFINGRA OF VDIAQTE

DEFINE D_QTE_UTR_PRD     FLOAT SIZE 08 = UTDQTEPRD OF PDUNI_TRAV_DIA - UTDQTEUTRPRD OF PDUNI_TRAV_DIA &
                                           IF 0 < (UTDQTEPRD OF PDUNI_TRAV_DIA - UTDQTEUTRPRD OF PDUNI_TRAV_DIA) &
                                           ELSE 0

DEFINE D_QTE_UTR_PRD_2   FLOAT SIZE 08 = (DIAQTECMD OF PDDIAGRAMME - DIAQTEEMB OF PDDIAGRAMME) * UTDQTEPRD OF PDUNI_TRAV_DIA &
                                           IF 0 < (DIAQTECMD OF PDDIAGRAMME - DIAQTEEMB OF PDDIAGRAMME) * UTDQTEPRD OF PDUNI_TRAV_DIA &
                                           ELSE 0.00

DEFINE D_DIFF FLOAT SIZE 8 = (DIAQTEPLAFINGRA OF VDIAQTE - DIAQTEEMB OF PDDIAGRAMME) * UTDQTEPRD OF PDUNI_TRAV_DIA

DEFINE D_QTE_UTR_A_FINIR FLOAT SIZE 08 = D_QTE_PIE_PRD * UTDQTEPRD OF PDUNI_TRAV_DIA &
                                           IF (DIAQTEFINGRA OF VDIAQTE > DIAQTEEMB OF PDDIAGRAMME) &
                                           ELSE D_DIFF

DEFINE D_QTE_CMD         FLOAT SIZE 08 = DIAQTEPLADBTGRA OF VDIAQTE
DEFINE D_SUR_DIA         FLOAT SIZE 08 = DIASURMC2 OF PDDIAGRAMME

DEFINE D_SUR_CMD         FLOAT SIZE 08 = D_SUR_DIA * D_QTE_CMD

DEFINE D_SUR_A_FINIR     FLOAT SIZE 08 = D_SUR_DIA * D_QTE_PIE_PRD


DEF D_COMNOM    CHAR SIZE 20 = COMNOM OF PDCOMMAN_INTERNE
DEF D_UTRDSC001 CHAR SIZE 20 = UTRDSC001 OF PDUNITE_TRAVAIL
DEFINE D_UTDQTEPRD FLOAT SIZE 8 = DIAQTEPLADBTGRA OF VDIAQTE * UTDQTEPRD OF PDUNI_TRAV_DIA

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Résumé des diagrammes à finir par commande interne"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD635Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitgor200_45 NOLIST
  SKIP &


  TAB 001 &
"                                                                        Qte    Qte       Qte U/T     Qte U/T     Surface   Surface" &
  SKIP &
  TAB 001 &
"Commande                         Priorite U/T Description          U/M  Cmd  À finir    Commandé     À finir    Commandé   À finir" &
  SKIP 2

FOOTING AT UTRNUMUNITRV &
  TAB 001 PJTABR OF PDPRIORITE_DIAG        PRINT AT COMNUMCOM &
  TAB 003 "-"                              PRINT AT COMNUMCOM &
  TAB 004 COMNUMCOMINT OF PDPRIORITE_DIAG  PRINT AT COMNUMCOM &
  TAB 009 D_COMNOM                         PRINT AT COMNUMCOM &
  TAB 031 PRICODPRI OF PDPRIORITE_DIAG     PRINT AT PRICODPRI &
  TAB 034 PRIDATFIN OF PDPRIORITE          PRINT AT PRICODPRI &
  TAB 043 UTRNUMUNITRV  OF PDUNI_TRAV_DIA &
  TAB 046 D_UTRDSC001      &
  TAB 068 UTRCODUNMPRD  OF PDUNITE_TRAVAIL &
  TAB 072 DIAQTEPLADBTGRA OF VDIAQTE SUBTOTAL &
                        PICTURE "^^^^^" &
  TAB 080 D_QTE_PIE_PRD SUBTOTAL PICTURE "^^^^^" &
  TAB 086 D_UTDQTEPRD SUBTOTAL &
  TAB 098 D_QTE_UTR_A_FINIR SUBTOTAL PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 110 D_SUR_CMD         SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 121 D_SUR_A_FINIR     SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  SKIP 2


BUILD pd635z

@IF FORMAT

PD635Z                                                      GRANICOR INC                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX              Résumé des diagrammes à finir par commande interne

Num. commande.: XX-999 COMMANDE DE TEST POUR MARBRU                   Tolérence
Priorité......: 99  AA/MM/JJ                                          Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
                                                                      Critères esthé:


                                                  Qte      Qte        Qte U/T       Qte U/T      Surface      Surface
U/T Description                              U/M  Cmd  À finir       Commandé       À finir     Commandé      À finir
XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX 99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999
XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX 99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999
XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX 99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999
XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX 99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999
XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XX 99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999

Sous-total pour la priorité                     99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999

Sous-total pour la commande                     99999   99999    999,999.9999  999,999.9999  999999.9999  999999.9999

@ENDIF
