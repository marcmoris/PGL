;/T/ Liste des réquisitions d'achat / Détaillée par fournisseur
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 2 mai 1994
;
;    Description...: Ce rapport présente les réquisitions d'achat pour
;                    les entrepôts désirés.
;
;    Paramètres....: Compagnie 
;                    Entrepôt           (Un, plusieurs ou tous)
;                    Numéro réquisition (Borne de début, <retour>=tous)
;                    Numéro réquisition (Borne de fin)
;                    Statut             (P=préliminaire, A=approuvée, 
;                                        C=complète,X=annulée,<retour>=tous)    
;                     
;
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; set process nolimit


RUN ar320t

;-------------------------------------------------------------------------------
; Cette procédure permet d'extraire toutes les réquisitions qui répondent
; aux critères de sélection demandés.
;
REQUEST EXTRAIRE_REQUISITION

ACCESS ARREQUISITION    &

  LINK CIECLE    OF ARREQUISITION , &
       REQCLE    OF ARREQUISITION , &
       FOUCIECLE OF ARREQUISITION , &
       FOUCLE    OF ARREQUISITION   &
    TO CIECLE                    , &
       REQCLE                    , &
       FOUCIECLE                 , &
       FOUCLE    OF ARREQ_FOU   OPTIONAL

CHOOSE CIECLE PARM,       &  ; Compagnie du menu
       ENTCLE PARM,       &  ; Entrepôt
       REQCLE PARM RANGE     ; Numéro de réquisition

DEFINE D_REQSTUAPP CHARACTER SIZE 6 = PARM UPSHIFT

SELECT ARREQUISITION IF (0 <> INDEX(D_REQSTUAPP,REQSTUAPP)) OR (D_REQSTUAPP = "")

SORT ON CIECLE    OF ARREQUISITION  &  ; Compagnie
     ON ENTCLE    OF ARREQUISITION  &  ; Entrepôt
     ON REQCLE    OF ARREQUISITION  &  ; Numéro réquisition
     ON REQSTUAPP OF ARREQUISITION     ; Statut réquisition

TEMPORARY T_FOUCIECLE   CHARACTER SIZE 04
TEMPORARY T_FOUCLE      CHARACTER SIZE 06
TEMPORARY T_REFCLETYP   CHARACTER SIZE 01
TEMPORARY T_RADCLE      INTEGER UNSIGNED SIZE 2

ITEM T_FOUCIECLE = FOUCIECLE OF ARREQ_FOU
ITEM T_FOUCLE    = FOUCLE    OF ARREQ_FOU
ITEM T_REFCLETYP = REFCLETYP OF ARREQ_FOU
ITEM T_RADCLE    = RADCLE    OF ARREQ_FOU

SUBFILE WAR320A KEEP                &
  INCLUDE CIECLE         OF ARREQUISITION, &
          ENTCLE         OF ARREQUISITION, &
          REQSTUAPP      OF ARREQUISITION, &
          REQCLE         OF ARREQUISITION, &
          REQCLENUM      OF ARREQUISITION, &
          REQFLGSOU      OF ARREQUISITION, &
          FOUCLE         OF ARREQUISITION, &
          REQDATAPP      OF ARREQUISITION, &
          REQDATREQ      OF ARREQUISITION, &
          PRDCLE         OF ARREQUISITION, &
          REQDSC         OF ARREQUISITION, &
          REQQTEREQ      OF ARREQUISITION, &
          REQPRIUNI      OF ARREQUISITION, &
          REQMNTTPS      OF ARREQUISITION, &
          REQMNTTVQ      OF ARREQUISITION, &
          REQMNTESC      OF ARREQUISITION, &
          REQMNTTOT      OF ARREQUISITION, &
          CPTCLE         OF ARREQUISITION, &
          UNACLE         OF ARREQUISITION, &
          PRJCLE         OF ARREQUISITION, &
          PRACLE         OF ARREQUISITION, &
          IMMCLE         OF ARREQUISITION, &
          FOUCLE         OF ARREQUISITION, &
          ADECLE         OF ARREQUISITION, &
          D_REQSTUAPP             , & 
          T_FOUCIECLE             , &
          T_FOUCLE                , &
          T_REFCLETYP             , &
          T_RADCLE                , &
          RQFNBRITE OF ARREQ_FOU, &
          RQFMNTTOT OF ARREQ_FOU

BUILD 
