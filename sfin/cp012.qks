;/T/ Numérotation des chèques
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 23 Juillet 1993
;
;    Description..: Numérotation des numéro physique des chèques.
;
;                   Lors de l'impression des chèques, on ne leur
;                   assigne pas de numéro physique (numéro préimprimé).
;                   Après avoir imprimé ses chèques, l'usager utilise cet
;                   écran (CP012) pour entrer dans le systèmes les numéros
;                   physiques utilisés par cette impression.
;
;                   Le principe est le suivant :
;
;                      . On demande la période, le lot et le compte d'encaisse,
;                        c'est les mêmes paramètres que pour le programme
;                        d'impression des chèques;
;
;                      . On demande l'intervalle de chèques à numéroter;
;
;                      . On demande le premier numéro pré-imprimé utilisé
;                        par l'impression;
;
;                      . Et on va attribuer un numéro physique à tous les
;                        chèques de l'intervalle en tenant compte du nombre
;                        de pages imprimé par le chèque;
;
;                      . Lors de cette assignation, on doit aussi s'assurer
;                        qu'il n'y ait aucun chèque portant déja ce numéro
;                        physique.
;
;
;/V3.00.01/ Alain Côté, 9 février 1994, 212
;
;/M/ Marie-Josée Hamel, 96.03.06, mettre le dernier numéro pré-imprimé du chq
;                                 sur le cheque et non pas le premier lors qu'il
;                                 a plusieurs pages. ;;MJH
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp012  ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY, FIND &
              RECEIVING   T_CIECLEMNU, &
                          T_CIENOM

USE ussectmp  NOLIST
    "CP012"

USE cp012.hlp NOLIST

USE usactecr NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-Ecrans"
  MENUITEM LABEL "Visualisation paiement          " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreens"
  MENUITEM LABEL "Payment Consultation            " ACTION DESIGNER D001
@ENDIF

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 RESET AT STARTUP
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP

;-------------------------------------------------------------------------------
;
; Historique de numérotation des paiements
;
;
FILE CPRENUM_PAM      PRIMARY

  ACCESS  REQUEST PECCLE    OF CPRENUM_PAM , &
                  LCPCLE    OF CPRENUM_PAM , &
                  LBQCPTENC OF CPRENUM_PAM   &
          VIA     CIECLE    , &
                  PECCLE    , &
                  LCPCLE    , &
                  LBQCPTENC   &
          USING   CIECLE    OF CPRENUM_PAM , &
                  PECCLE    OF CPRENUM_PAM , &
                  LCPCLE    OF CPRENUM_PAM , &
                  LBQCPTENC OF CPRENUM_PAM   &
          ORDERBY CIECLE    , &
                  PECCLE    , &
                  LCPCLE    , &
                  LBQCPTENC , &
                  RENTIMSTP

  ACCESS  REQUEST PECCLE    OF CPRENUM_PAM , &
                  LCPCLE    OF CPRENUM_PAM   &
          VIA     CIECLE    , &
                  PECCLE    , &
                  LCPCLE      &
          USING   CIECLE    OF CPRENUM_PAM , &
                  PECCLE    OF CPRENUM_PAM , &
                  LCPCLE    OF CPRENUM_PAM   &
          ORDERBY CIECLE    , &
                  PECCLE    , &
                  LCPCLE    , &
                  LBQCPTENC , &
                  RENTIMSTP

  ACCESS  REQUEST PECCLE    OF CPRENUM_PAM &
          VIA     CIECLE    , &
                  PECCLE      &
          USING   CIECLE    OF CPRENUM_PAM , &
                  PECCLE    OF CPRENUM_PAM   &
          ORDERBY CIECLE    , &
                  PECCLE    , &
                  LCPCLE    , &
                  LBQCPTENC , &
                  RENTIMSTP

  ACCESS  VIA     CIECLE &
          USING   CIECLE    OF CPRENUM_PAM &
          ORDERBY CIECLE    , &
                  PECCLE    , &
                  LCPCLE    , &
                  LBQCPTENC , &
                  RENTIMSTP

  ITEM CIECLE     OF CPRENUM_PAM INITIAL T_CIECLEMNU
  ;
  ; Date et heure du jour, sans les centièmes de seconde.
  ;
  ITEM RENTIMSTP  OF CPRENUM_PAM INITIAL SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  ITEM RENUSR     OF CPRENUM_PAM INITIAL LOGONID
  ITEM RENDAT     OF CPRENUM_PAM INITIAL SYSDATE

;
; Validation du compte d'encaisse.
;
FILE MCLIVRET_BQ      REFERENCE
  ACCESS  VIA   CIECLE    , &
                LBQCPTENC   &
          USING CIECLE    OF CPRENUM_PAM , &
                LBQCPTENC OF CPRENUM_PAM

;
; Recherche de la description du compte d'encaisse
;
FILE VCPT_DSC         REFERENCE
  ACCESS  VIA   CPTCLE &
          USING LBQCPTENC OF CPRENUM_PAM

;
; Pour afficher le nom de l'usager.
;
FILE GSUSAGER         REFERENCE
  ACCESS VIA USRCLE &
         USING RENUSR OF CPRENUM_PAM

;
; Validation & consultation des paiements et mise à jour du numéro pré-imprimé.
;
FILE CPPAIEMENT       DESIGNER

;
; Valide si le numéro de pré-imprimé existe déjà en dehors de l'intervalle
; à numéroter. On utilise un "select" sur le fichier pour la performance, car
; si on accède la table avec la compagnie et le compte de banque Interbase
; essaie d'utiliser deux index.
;
FILE CPPAIEMENT       DESIGNER &
                      ALIAS A_PAM_EXISTE &
                      OPEN READ SHARE
  SELECT IF     CIECLE    OF A_PAM_EXISTE = CIECLE    OF CPRENUM_PAM &
            AND LBQCPTENC OF A_PAM_EXISTE = LBQCPTENC OF CPRENUM_PAM &
            AND (    PAMCLE OF A_PAM_EXISTE < RENPAMDEB OF CPRENUM_PAM &
                  OR PAMCLE OF A_PAM_EXISTE > RENPAMFIN OF CPRENUM_PAM )

;-------------------------------------------------------------------------------

DEFINE    DZ_CIECLE   CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_PECCLE      CHARACTER SIZE 4 ; Popup sur les périodes/lot.
TEMPORARY T_MODCLE      CHARACTER SIZE 2 ; Popup sur les périodes
TEMPORARY T_LCPCLE      CHARACTER SIZE 4 ; Popup sur les lots
TEMPORARY T_TYPECR      CHARACTER SIZE 2 ; Popup sur les lots
TEMPORARY T_LBQCPTENC   CHARACTER SIZE 6 ; Popup sur les comptes d'encaisse.
TEMPORARY T_PAMCLE      CHARACTER SIZE 7 ; Popup sur les paiements.
TEMPORARY T_PAMNUMPIM   CHARACTER SIZE 8 ; Numéro de pré-imprimé.
TEMPORARY T_PAMCLEDEB   CHARACTER SIZE 7 ; Premier # chèque par défaut.
TEMPORARY T_PAMCLEFIN   CHARACTER SIZE 7 ; Dernier # chèque par défaut.
TEMPORARY T_FLGUPD      CHARACTER SIZE 1 ; Le flag suivante indique d'exécuter
                                         ; la procédure TRAITE_PAIEMENT en mode
                                         ; de validation ou mise à jour.

TEMPORARY T_NUMPIM      INTEGER UNSIGNED SIZE 4

TEMPORARY T_LCPCLE_SIL    CHARACTER SIZE 1 ; Validation période et lot.
TEMPORARY T_NBRTRSLOT_SIL CHARACTER SIZE 1 ; Pour activer le calcul du nombre
                                           ; de paiement du lot.


;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Numérotation des chèques " &
@ELSE
      " Check numbering " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

ALIGN( 19,3,19) (,40,56)

FIELD PECCLE OF CPRENUM_PAM ID 05 HIDDEN &
label "Période ......:" &
        REQUIRED

FIELD LCPCLE OF CPRENUM_PAM &
label "Numéro de lot.:" &
        REQUIRED &
        NUMERIC  &
        SIGNIFICANCE 4

FIELD T_LCPCLE_SIL &
        SILENT &
        LOOKUP ON CPPAIEMENT &
          VIA CIECLE , &
              PECCLE , &
              LCPCLE   &
          USING CIECLE OF CPRENUM_PAM , &
                PECCLE OF CPRENUM_PAM , &
                LCPCLE OF CPRENUM_PAM   &
          MESSAGE 436 ; Ce numéro de lot n'existe pas.

SKIP 1

ALIGN(,3,19) (,,28)

FIELD LBQCPTENC OF CPRENUM_PAM &
label "Cpt encaisse..:" &
        DEFAULT LBQCPTENC OF CPPAIEMENT &
        LOOKUP ON MCLIVRET_BQ &
          MESSAGE 569 ; Ce compte n'est pas un compte d'encaisse

FIELD CPTDSC OF VCPT_DSC &
      DISPLAY

FIELD T_NBRTRSLOT_SIL &
        SILENT

SKIP 1

ALIGN(19,3,19)

FIELD RENNBRTRSLOT OF CPRENUM_PAM ID 10 HIDDEN &
label "Nombre total..:" &
        DISPLAY

SKIP 1

ALIGN(19,3,19) (,,32)

FIELD RENUSR OF CPRENUM_PAM ID 15 HIDDEN &
label "Usager........:" &
        DISPLAY

FIELD USREMPNOM OF GSUSAGER &
        DISPLAY

SKIP 1

FIELD RENDAT OF CPRENUM_PAM  FORMAT YYYYMMDD PATTERN "####/##/##" ID 20 HIDDEN &
label "Date..........:" &
        DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Premier       Dernier" &
@ELSE
      "  First          Last" &
@ENDIF
      AT ,19

SKIP

ALIGN (19,3,19) (,,33)

FIELD RENPAMDEB OF CPRENUM_PAM ID 25 HIDDEN &
label "No de chèque..:" &
        PATTERN "#>"         &
        LOOKUP ON CPPAIEMENT &
          VIA   CIECLE   , &
                LBQCPTENC, &
                PAMCLE     &
          USING CIECLE    OF CPRENUM_PAM , &
                LBQCPTENC OF CPRENUM_PAM , &
                RENPAMDEB OF CPRENUM_PAM   &
          MESSAGE 462 ; Ce paiement n'existe pas.

FIELD RENPAMFIN OF CPRENUM_PAM &
        PATTERN "#>"         &
        LOOKUP ON CPPAIEMENT &
          VIA   CIECLE    , &
                LBQCPTENC , &
                PAMCLE      &
          USING CIECLE    OF CPRENUM_PAM , &
                LBQCPTENC OF CPRENUM_PAM , &
                RENPAMFIN OF CPRENUM_PAM   &
         MESSAGE 462 ; Ce paiement n'existe pas.

SKIP 1

FIELD RENNUMPIMDEB OF CPRENUM_PAM ID 30 HIDDEN &
label "No pré-imprimé:" &
        PATTERN "#>" &
        REQUIRED

FIELD RENNUMPIMFIN OF CPRENUM_PAM &
        DISPLAY

SKIP 1

FIELD RENNBRTRSTRA OF CPRENUM_PAM ID 35 HIDDEN &
label "Nbr de chèques:" &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT PECCLE
BEGIN
  ;
  ; Popup sur les périodes.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_MODCLE = "CP"
    RUN SCREEN mc109 MODE F &
                     PASSING T_CIECLEMNU , &
                             T_PECCLE    , &
                             T_MODCLE
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les lots.
;
;
PROCEDURE INPUT LCPCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = PECCLE OF CPRENUM_PAM
    LET T_TYPECR = "CH"
    LET T_LCPCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cp103 MODE F &
                     PASSING T_TYPECR    , &
                             T_CIECLEMNU , &
                             T_PECCLE    , &
                             T_LCPCLE
    LET FIELDTEXT = TRUNCATE( T_LCPCLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les comptes d'encaisse disponibles
;
;
PROCEDURE INPUT LBQCPTENC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LBQCPTENC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc110 PASSING T_CIECLEMNU, T_LBQCPTENC MODE F
    LET FIELDTEXT = TRUNCATE(T_LBQCPTENC)
  END
END


;-------------------------------------------------------------------------------
;
;
; On compte le nombre de paiements du lot, le premier numéro de paiement du lot
; et le dernier numéro de paiement du lot.
;
;
PROCEDURE PROCESS T_NBRTRSLOT_SIL
BEGIN
  LET RENNBRTRSLOT OF CPRENUM_PAM = 0
  LET T_PAMCLEDEB = " "
  LET T_PAMCLEFIN = " "
  WHILE RETRIEVING CPPAIEMENT VIA CIECLE    , &
                                  LBQCPTENC , &
                                  PECCLE    , &
                                  LCPCLE      &
                               USING CIECLE    OF CPRENUM_PAM , &
                                     LBQCPTENC OF CPRENUM_PAM , &
                                     PECCLE    OF CPRENUM_PAM , &
                                     LCPCLE    OF CPRENUM_PAM   &
                               ORDERBY CIECLE    , &
                                       LBQCPTENC , &
                                       PAMCLE
  BEGIN
    IF     T_PAMCLEDEB = " " &
       AND PAMFLGIMP OF CPPAIEMENT = "1"
    THEN LET T_PAMCLEDEB = PAMCLE OF CPPAIEMENT

    LET T_PAMCLEFIN = PAMCLE OF CPPAIEMENT
    LET RENNBRTRSLOT OF CPRENUM_PAM = RENNBRTRSLOT OF CPRENUM_PAM + 1
  END
  DISPLAY RENNBRTRSLOT OF CPRENUM_PAM
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT RENPAMDEB
BEGIN
  ;
  ; Popup sur les paiements du lot.
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PAMCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cp012a PASSING CPRENUM_PAM , &
                              T_PAMCLE      &
                     MODE F
    LET FIELDTEXT = TRUNCATE(T_PAMCLE)
  END
  ;
  ; Valeur par défaut.
  ;
  IF     NOT FINDMODE &
     AND 0 = SIZE( FIELDTEXT ) &
     AND "" = TRUNCATE( RENPAMDEB OF CPRENUM_PAM )
  THEN LET FIELDTEXT = T_PAMCLEDEB
  ;
  ; On force l'exécution de la procedure edit
  ;
  IF    NOT FINDMODE &
    AND 0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = RENPAMDEB OF CPRENUM_PAM
  ;
  ; On complète le numéro de chèque avec des zéros.
  ;
  IF 0 <> SIZE( FIELDTEXT )
  THEN LET FIELDTEXT = ASCII( NCONVERT(FIELDTEXT) , &
                              SIZE( RENPAMDEB OF CPRENUM_PAM ) - 1 )
END


;-------------------------------------------------------------------------------
;
;
; Ce doit être un chèque imprimé, pas annulé, ni déja numéroté.
;
;
PROCEDURE EDIT RENPAMDEB
BEGIN
  IF PAMFLGIMP OF CPPAIEMENT = "0"
  THEN ERROR 503 ; Ce paiement n'a pas encore été imprimé.

  IF PAMCODANN OF CPPAIEMENT = "1"
  THEN ERROR 470 ; Ce paiement est annulé.

  IF    PECCLE OF CPPAIEMENT <> PECCLE OF CPRENUM_PAM &
     OR LCPCLE OF CPPAIEMENT <> LCPCLE OF CPRENUM_PAM
  THEN ERROR 557 ; Ce paiement n'appartient pas à ce lot.

  IF PAMNUMPIM OF CPPAIEMENT <> ""
  THEN WARNING 556 ; Ce paiement est déja numéroté.
END


;-------------------------------------------------------------------------------
;
; Consultation des paiements à numéroter - FIN DE L'INTERVALLE
;
;
PROCEDURE INPUT RENPAMFIN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PAMCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cp012a PASSING CPRENUM_PAM , &
                              T_PAMCLE      &
                      MODE F
    LET FIELDTEXT = TRUNCATE(T_PAMCLE)
  END
  ;
  ; Valeur par défaut.
  ;
  IF NOT FINDMODE &
     AND 0 = SIZE( FIELDTEXT ) &
     AND "" = TRUNCATE( RENPAMFIN OF CPRENUM_PAM )
  THEN LET FIELDTEXT = T_PAMCLEFIN
  ;
  ; On force l'exécution de la procedure edit
  ;
  IF    NOT FINDMODE &
    AND 0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = RENPAMFIN OF CPRENUM_PAM
  ;
  ; On complète le numéro de chèque avec des zéros.
  ;
  IF 0 <> SIZE( FIELDTEXT )
  THEN LET FIELDTEXT = ASCII( NCONVERT(FIELDTEXT) , &
                              SIZE( RENPAMFIN OF CPRENUM_PAM ) - 1 )
END


;-------------------------------------------------------------------------------
;
;
; Ce doit être un chèque imprimé, mais pas annulé, et le
; numéro de paiement doit être supérieur au numéro de début de l'intervalle.
;
;
PROCEDURE EDIT RENPAMFIN
BEGIN
  IF PAMFLGIMP OF CPPAIEMENT = "0"
  THEN ERROR 503 ; Ce paiement n'a pas encore été imprimé.

  IF PAMCODANN OF CPPAIEMENT = "1"
  THEN ERROR 470 ; Ce paiement est annulé.

  IF    PECCLE OF CPPAIEMENT <> PECCLE OF CPRENUM_PAM &
     OR LCPCLE OF CPPAIEMENT <> LCPCLE OF CPRENUM_PAM
  THEN ERROR 557 ; Ce paiement n'appartient pas à ce lot.

  IF FIELDTEXT < RENPAMDEB OF CPRENUM_PAM
  THEN ERROR 467 ; Le numéro de fin doit être plus grand que celui de début.

  IF PAMNUMPIM OF CPPAIEMENT <> ""
  THEN WARNING 556 ; Ce paiement est déja numéroté.

END


;-------------------------------------------------------------------------------
;
;
; Numérotation des paiements sélectionnés
;
; On parcours l'intervalle demandé. On interromp le traitement si on trouve un
; paiement portant déja le numéro calculé.
;
;
PROCEDURE INTERNAL TRAITE_PAIEMENT
BEGIN
  LET RENNBRTRSTRA OF CPRENUM_PAM = 0
  LET T_NUMPIM = NCONVERT (RENNUMPIMDEB OF CPRENUM_PAM)
  WHILE RETRIEVING CPPAIEMENT &
        VIA     CIECLE    , &
                LBQCPTENC , &
                PECCLE    , &
                LCPCLE      &
        USING   CIECLE    OF CPRENUM_PAM , &
                LBQCPTENC OF CPRENUM_PAM , &
                PECCLE    OF CPRENUM_PAM , &
                LCPCLE    OF CPRENUM_PAM   &
        ORDERBY CIECLE   , &
                LBQCPTENC, &
                PAMCLE
  BEGIN
    IF    PAMCLE OF CPPAIEMENT >= RENPAMDEB OF CPRENUM_PAM &
      AND PAMCLE OF CPPAIEMENT <= RENPAMFIN OF CPRENUM_PAM &
      AND PAMFLGIMP OF CPPAIEMENT = "1"
    THEN BEGIN

      LET T_NUMPIM = T_NUMPIM + PAMNBRPAG OF CPPAIEMENT - 1   ;;MJH
      LET T_PAMNUMPIM = ASCII (T_NUMPIM, LBQLONNUM OF MCLIVRET_BQ)
      ;
      ; On vérifie si le numéro de pré-imprimé existe déjà, on utilise un
      ; "select" sur le fichier pour la performance, car si on accède la
      ; table avec la compagnie et le compte de bancque Interbase essaie
      ; d'utiliser deux index.
      ;
      GET A_PAM_EXISTE VIA PAMNUMPIM   &
                       USING T_PAMNUMPIM &
                       OPTIONAL
      IF ACCESSOK
      THEN ERROR 573 ; Ce numéro physique est déja utilisé. Annulation de la numérotation.
      ;
      ; On met à jour le paiement seulement si cette procédure est appelé par
      ; lors de la mise à jour(update).
      ;
      IF T_FLGUPD = "U"
      THEN BEGIN
        LET PAMNUMPIM OF CPPAIEMENT = T_PAMNUMPIM
        PUT CPPAIEMENT
      END
      LET T_NUMPIM = T_NUMPIM + 1                             ;;MJH
      ;
      ; Nombre chèque traités.
      ;
      LET RENNBRTRSTRA OF CPRENUM_PAM = RENNBRTRSTRA OF CPRENUM_PAM + 1
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; On complète le numéro de pré-imprimé selon la longueur déterminé dans la
; table MCLIVRET_BQ.
;
;
PROCEDURE INPUT RENNUMPIMDEB
BEGIN
  IF     LBQLONNUM OF MCLIVRET_BQ > SIZE( FIELDTEXT ) &
     AND 0 <> SIZE( FIELDTEXT )
  THEN LET FIELDTEXT = ASCII( NCONVERT( FIELDTEXT ), LBQLONNUM OF MCLIVRET_BQ )
  ;
  ; On force l'exécution du traitement suivant.
  ;
  IF     NOT FINDMODE &
     AND 0 = SIZE( FIELDTEXT )
  THEN LET FIELDTEXT = RENNUMPIMDEB OF CPRENUM_PAM
END


;-------------------------------------------------------------------------------
;
;
; Compte le nombre de chèque selon l'interval.
;
;
PROCEDURE PROCESS RENNUMPIMDEB
BEGIN
  ;
  ; Le flag suivante indique d'exécuter la procédure TRAITE_PAIEMENT en mode
  ; de validation.
  ;
  LET T_FLGUPD = "V"
  DO INTERNAL TRAITE_PAIEMENT
  LET RENNUMPIMFIN OF CPRENUM_PAM = T_PAMNUMPIM
  DISPLAY RENNUMPIMFIN OF CPRENUM_PAM
  DISPLAY RENNBRTRSTRA OF CPRENUM_PAM
END


;-------------------------------------------------------------------------------
;
; Consultation des paiements sélectionnés par l'intervalle
;
;
PROCEDURE DESIGNER D001
BEGIN
    LET T_PAMCLE = "@"
    RUN SCREEN cp012a PASSING CPRENUM_PAM , &
                              T_PAMCLE      &
                      MODE F
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPRENUM_PAM &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la mise à jour(Entrée, Modification)
  ;
  IF     ALTEREDRECORD     OF CPRENUM_PAM &
     AND NOT NEWRECORD     OF CPRENUM_PAM &
     AND NOT DELETEDRECORD OF CPRENUM_PAM &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Le traitement de renumerotation est effectué qu'une seule fois.
  ; Si l'enregistrement est déja daté c'est que le traitement à déja
  ; été effectué.
  ;
  IF NEWRECORD OF CPRENUM_PAM
  THEN BEGIN
    ;
    ; Le flag suivante indique d'exécuter la procédure TRAITE_PAIEMENT en mode
    ; de mise à jour.
    ;
    LET T_FLGUPD = "U"
    DO INTERNAL TRAITE_PAIEMENT
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*************************** Numérotation des chèques ***************************
*                                                                              *
* Période ......: xxxxx                Numéro de lot.: xxxx                    *
*                                                                              *
* Cpt encaisse..: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                 *
*                                                                              *
* Nombre total..: xxxxx                                                        *
*                                                                              *
* Usager........: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
* Date..........: xxxxxxxx                                                     *
*                                                                              *
*                 Premier       Dernier                                        *
* No de chèque..: xxxxxxx       xxxxxxx                                        *
*                                                                              *
* No pré-imprimé: xxxxxxxx      xxxxxxxx                                       *
*                                                                              *
* Nbr de chèques: xxxxx                                                        *

*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
