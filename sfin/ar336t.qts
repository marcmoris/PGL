;/T/ Impression de la soumission
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 13 juin 1994
;
;/M/ Modifié par...: Guy Chabot 21 septembre 1994
;            but...: Adaptation de la notion multi-fournisseur.
;
;    Description...: Ce programme permet de faire l'impression des soumissions.
;
;    Paramètres....: Compagnie             (Compagnie du menu)
;                    Numéro de réquisition (Borne de début)
;                    Numéro de réquisition (Borne de fin)
;
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; set process nolimit

RUN ar336t

REQUEST EXTRAIRE_REQUISITIONS

ACCESS ARREQUISITION    &

  LINK  CIECLE OF ARREQUISITION, &
        REQCLE OF ARREQUISITION, &
        REQCLENUM OF ARREQUISITION  &
    TO  CIECLE, &
        REQCLE, &
        REQCLENUM OF ARREQ_SOUMISSION   &

  LINK FOUCIECLE OF ARREQ_SOUMISSION, &
       FOUCLE    OF ARREQ_SOUMISSION, &
       CIECLE    OF ARREQ_SOUMISSION  &
    TO FOUCIECLE, &
       FOUCLE,    &
       FOCCIECLE OF VFOC_FOU   &

  LINK CIECLE    OF ARREQUISITION, &
       REQCLE    OF ARREQUISITION, &
       REQCLENUM OF ARREQUISITION, &
       "1"                        &
    TO CIECLE, &
       REQCLE, &
       REQCLENUM, &
       RQNIMP    OF ARREQ_NOTE   OPTIONAL

CHOOSE CIECLE PARM,       & ; Compagnie du menu
       REQCLE PARM RANGE, & ; Numéro de réquisition
       REQFLGSOU "1"

DEFINE D_FOUCLE       CHARACTER SIZE 6 = PARM PROMPT "Fournisseur : "

SELECT ARREQ_SOUMISSION IF FOUCLE OF ARREQ_SOUMISSION = D_FOUCLE OR D_FOUCLE=" "

TEMPORARY T_NBRLIG    INTEGER SIZE 4
TEMPORARY T_FLGDRN    CHARACTER SIZE 1

SORT ON CIECLE    OF ARREQUISITION     &
     ON FOUCIECLE OF ARREQ_SOUMISSION &
     ON FOUCLE    OF ARREQ_SOUMISSION &
     ON RADCLE    OF ARREQ_SOUMISSION &
     ON ADECLE    OF ARREQUISITION     &
     ON REQCLE    OF ARREQUISITION     &
     ON REQCLENUM OF ARREQUISITION     &
     ON RQNSEQ    OF ARREQ_NOTE

ITEM T_NBRLIG  COUNT RESET AT RADCLE

SUBFILE WAR336A KEEP &
  INCLUDE CIECLE       OF ARREQUISITION  , &
          ENTCLE       OF ARREQUISITION  , &
          ADECLE       OF ARREQUISITION  , &
          REQCLE       OF ARREQUISITION  , &
          REQDATREQ    OF ARREQUISITION  , &
          FOUCIECLE    OF ARREQ_SOUMISSION , &
          FOUCLE       OF ARREQ_SOUMISSION , &
          REFCLETYP    OF ARREQUISITION, &
          RADCLE       OF ARREQ_SOUMISSION , &
          FOCTERESCACH OF VFOC_FOU   , &
          CMDTRP    OF VFOC_FOU   , &
          CMDFAB    OF VFOC_FOU   , & 
          DEVCLE       OF VFOC_FOU   , &
          FOULNG       OF VFOC_FOU   , &
          PRDCLE       OF ARREQUISITION , &
          REQCLENUM    OF ARREQUISITION , &
          REQDSC       OF ARREQUISITION , &
          REQQTEREQ    OF ARREQUISITION , &
          RQNSEQ       OF ARREQ_NOTE   , &
          RQNDSC       OF ARREQ_NOTE   , &
          T_FLGDRN                     , &
          T_NBRLIG  

;-------------------------------------------------------------------------------
;
;
; Cette étape met à jour le flag qui indique le dernier record de la commande.
;
;
REQUEST MAJ_DERNIER_LIG

ACCESS *WAR336A

SORTED ON CIECLE    OF WAR336A &      ; Compagnie 
       ON FOUCIECLE OF WAR336A &      ; Compagnie fournisseur
       ON FOUCLE    OF WAR336A &      ; Numéro du fournisseur
       ON RADCLE    OF WAR336A &      ; Adresse
       ON ADECLE    OF WAR336A &      
       ON REQCLE    OF WAR336A &      ; Réquisition 
       ON REQCLENUM OF WAR336A &      ; Produit 
       ON RQNSEQ    OF WAR336A

OUTPUT WAR336A UPDATE &
               AT ADECLE 
  ITEM T_FLGDRN OF WAR336A FINAL "1"


BUILD
