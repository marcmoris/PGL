;/T/ Valeurs par paramètre
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 1 septembre 1992
;
;    Description..: Définition des valeurs possible pour un paramètre.
;                   Sert dans l'écran de lancement des listes et rapports.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gs004a ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING GSPARAMETRE

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

USE gs004a.hlp NOLIST
USE ussectmp NOLIST     ; Variable pour la securite
"GS004A"                ; Nom de l'écran

FILE GSPARAMETRE      MASTER

FILE GSXPA_VALUE      PRIMARY &
                      OCCURS 17 &
                      NOITEMS
  ACCESS VIAINDEX XPV_U01 &
         VIA XPANOM &
         USING XPANOM OF GSPARAMETRE

  ITEM XPANOM OF GSXPA_VALUE INITIAL XPANOM OF GSPARAMETRE

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page

TITLE &
@IF FRANCAIS
       " Valeurs possible " &
@ELSE
       " Values " &
@ENDIF
       CENTERED AT 2,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


ALIGN (,3,19)
FIELD XPANOM OF GSPARAMETRE FIXED

SKIP 1

TITLE &
@IF FRANCAIS
       "Code        Description Française   Description anglaise" &
@ELSE
       "Code        French description      English description" &
@ENDIF
AT ,8

CLUSTER OCCURS WITH GSXPA_VALUE ID BASE 10
ALIGN(8,6,8) (,,20) (,,44)

FIELD XPVCLE OF GSXPA_VALUE LABEL " " HIDDEN &
        REQUIRED NOCHANGE &
        LOOKUP NOTON GSXPA_VALUE &
          VIA XPANOM, XPVCLE &
          USING XPANOM OF GSXPA_VALUE, &
                XPVCLE OF GSXPA_VALUE &
          MESSAGE 17 ; Ce code existe déjà.

FIELD XPVDSCFRA OF GSXPA_VALUE &
        REQUIRED

FIELD XPVDSCANG OF GSXPA_VALUE &
        DEFAULT XPVDSCFRA OF GSXPA_VALUE
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie(ENTRY).
;
;
PROCEDURE INPUT XPVCLE
BEGIN
  USE ussecent NOLIST
END

BUILD
