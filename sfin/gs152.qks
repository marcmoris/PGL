;/T/ Définition des queue batch
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 31 août 1992
;
;    Description..: Définition des queues batch pour les programmes.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gs152 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72

USE ussectmp NOLIST
"GS152"

USE gs152.hlp NOLIST

USE usactecr NOLIST


FILE GSQUEUE_BATCH    PRIMARY OCCURS 18 NOITEMS
  ACCESS VIA XQBCLE &
         USING XQBCLE OF GSQUEUE_BATCH &
         REQUEST XQBCLE
  ACCESS VIAINDEX XQB_U01 &
         VIA XQBCLE &
         USING "@"

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = ""
DEFINE DZ_CIENOM CHARACTER SIZE 40 = ""

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP


TITLE &
@IF FRANCAIS
       " File d'attente " &
@ELSE
       " Queue batch " &
@ENDIF
       CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE &
      "Code  Description Française Description anglaise Queue de traitement Priorité" &
     AT 4,3

CLUSTER OCCURS WITH GSQUEUE_BATCH ID BASE 10
ALIGN(3,2,3) (,,9) (,,31) (,,59) (,,75)

FIELD XQBCLE OF GSQUEUE_BATCH  LABEL " " HIDDEN &
        REQUIRED NOCHANGE &
        LOOKUP NOTON GSQUEUE_BATCH &
          VIA XQBCLE &
          USING XQBCLE OF GSQUEUE_BATCH &
          MESSAGE 17 ; Ce code existe déjà.

FIELD XQBDSCFRA OF GSQUEUE_BATCH &
        REQUIRED

FIELD XQBDSCANG OF GSQUEUE_BATCH &
        DEFAULT XQBDSCFRA OF GSQUEUE_BATCH

FIELD XQBNAM OF GSQUEUE_BATCH &
        REQUIRED
FIELD XQBPRI OF GSQUEUE_BATCH

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie(ENTRY).
;
;
PROCEDURE INPUT XQBCLE
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  FOR GSQUEUE_BATCH
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF GSQUEUE_BATCH &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GSQUEUE_BATCH &
       AND NOT NEWRECORD     OF GSQUEUE_BATCH &
       AND NOT DELETEDRECORD OF GSQUEUE_BATCH &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD
