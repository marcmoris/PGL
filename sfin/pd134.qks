;/T/ Dépisteur numéro de bon de livraison
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-12-05
;
;    Description..: Permet de visionner et de choisir un bon de livraison
;
;    Paramètres...: T_EXPNUMEXP ==> Numéro d'expédition
;                   T_BLINUMBLI ==> Numéro de bon de livraison
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd134 NOMODE NOACTION FIELDMARK &
             FROM 11,39 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_EXPNUMEXP, &
                       T_BLINUMBLI,&
                       T_CIECLEMNU


TEMPORARY T_EXPNUMEXP CHARACTER SIZE 09
TEMPORARY T_BLINUMBLI CHARACTER SIZE 02
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd134.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Expédition
;

FILE PDBON_LIVRAISON PRIMARY OCCURS 10

  ACCESS VIA     CIECLE,                           &
                 EXPNUMEXP                         &
         USING   T_CIECLEMNU,NCONVERT(T_EXPNUMEXP) &
         ORDERBY CIECLE,                           &
                 EXPNUMEXP,                        &
                 BLINUMBLI

SELECT IF MATCHPATTERN(TRUNCATE(BLINUMBLI OF PDBON_LIVRAISON) , TRUNCATE(T_BLINUMBLI))


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,41 ; Remplace le DRAW standard

USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2


TITLE &
@IF FRANCAIS
      "    Expedition   Date       Commande " &
@ELSE
      "%%% Expedition   Date       Commande " &
@ENDIF
      AT ,2


SKIP


;ALIGN (03,03,06) (,15,16) (,,19) (,,28)
ALIGN (03,03,06) (,15,16) (,,19) (,,30)


CLUSTER OCCURS WITH PDBON_LIVRAISON


FIELD EXPNUMEXP OF PDBON_LIVRAISON HIDDEN LABEL ">>" DISPLAY SIZE 9


FIELD BLINUMBLI OF PDBON_LIVRAISON LABEL "-" HIDDEN NUM


FIELD BLIDAT OF PDBON_LIVRAISON  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN


FIELD COMNUMCOM OF PDBON_LIVRAISON HIDDEN


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_BLINUMBLI , "@")
  THEN BEGIN
    LET T_BLINUMBLI = ""
  END
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_BLINUMBLI = BLINUMBLI OF PDBON_LIVRAISON
  RETURN
END

BUILD


@IF FORMAT

**************************************
*    Expedition   Date     Commande  *
* >> xxxxxxxxx-xx AA/MM/JJ xx-xx-xxx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         **************************************
@ENDIF
