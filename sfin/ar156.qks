;/T/ Gestion des utilisateurs
;
;/P/ Programmeur..: Patrick Langlois
;    Date Création: 24 août 1999
;
;    Description..: Définition des codes d'utilisateur. Utilisé dans l'écran
;                   des mouvement d'inventaire
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar156 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "AR156"

USE ar156.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom du holding

;-------------------------------------------------------------------------------
;
; Table des utilisateurs
;
FILE ARUTILISATEUR PRIMARY &
                   OCCURS 17 &
                   NOITEMS

  ACCESS  VIA     UTLCLE &
          USING   UTLCLE OF ARUTILISATEUR &
          REQUEST UTLCLE &
          ORDERBY UTLCLE 

  ACCESS  SEQUENTIAL &
          ORDERBY UTLCLE

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Utilisateur " &
@ELSE
      "%%%" &
@ENDIF
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

TITLE &
@IF FRANCAIS
    "Code        Nom                   Prénom" &
@ELSE
    "%%%" &
@ENDIF
  AT 5,5
SKIP TO 6

ALIGN(5,3,5)(,,17)(,,39)

CLUSTER OCCUR WITH ARUTILISATEUR ID BASE 10

FIELD UTLCLE OF ARUTILISATEUR &
        LABEL " " HIDDEN &
        REQUIRED NOCHANGE &
        LOOKUP NOTON ARUTILISATEUR &
          VIA   UTLCLE &
          USING UTLCLE OF ARUTILISATEUR &
          MESSAGE 0017 ; Ce code existe déjà

FIELD UTLNOM  OF ARUTILISATEUR &
      REQUIRED

FIELD UTLPNM  OF ARUTILISATEUR &
      REQUIRED

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END



;-------------------------------------------------------------------------------
;
; Validation de l'autorisation d'entrer des données
;
PROCEDURE INPUT UTLCLE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR ARUTILISATEUR
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF DELETEDRECORD OF ARUTILISATEUR &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification
    ;
    IF    ALTEREDRECORD     OF ARUTILISATEUR &
      AND NOT NEWRECORD     OF ARUTILISATEUR &
      AND NOT DELETEDRECORD OF ARUTILISATEUR &
      AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'une catégorie.
    ;
    IF DELETEDRECORD OF ARUTILISATEUR
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la catégorie de produit est référée.
      ;
      RUN SCREEN ar156x MODE SAME &
                        PASSING ARUTILISATEUR, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT
********************************* Utilisateur **********************************
*                                                                              *
*   Code        Nom                   Prénom                                   *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
*   xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx                     *
********************************************************************************
@ENDIF
