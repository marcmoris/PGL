;/T/ Devises
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 24 février 1992
;
;    Description..: Gestion des codes de devises
;
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc003 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_FLGHLDOK, &
                       T_CIENOM

USE ussectmp NOLIST     ; Variable pour la securite
    "MC003"             ; Nom de l'écran

USE mc003.hlp NOLIST

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Historique des taux        " ACTION DESIGNER D001

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le Holding est présent.
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding


;-------------------------------------------------------------------------------
;

FILE MCDEVISE         PRIMARY &
                      OCCURS 15
  ACCESS REQUEST DEVCLE OF MCDEVISE &
         VIA     DEVCLE &
         USING   DEVCLE OF MCDEVISE &
         ORDERED
  ACCESS SEQUENTIAL &
         ORDERBY DEVCLE

;-------------------------------------------------------------------------------

TEMPORARY T_DEVTAU    FLOAT SIZE 8 OCCURS WITH MCDEVISE
TEMPORARY T_DEVOLDTAU FLOAT SIZE 8 OCCURS WITH MCDEVISE
TEMPORARY T_DEVDATMAJ INT   SIZE 4 OCCURS WITH MCDEVISE

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Devises " &
@ELSE
      " Currencies " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

@IF FRANCAIS
TITLE &
 "     ----------Description----------   Taux           Ancien       Date" &
  AT ,3
@ELSE
TITLE &
 "     ----------Description----------  Current          Old         Date" &
  AT ,3
@ENDIF

SKIP
@IF FRANCAIS
TITLE &
 "Code Française       Anglaise         Courant          Taux        M.A.J." &
  AT ,3
@ELSE
TITLE &
 "Code French          English           Rate            Rate        Update" &
  AT ,3
@ENDIF

SKIP

ALIGN(3,2,3)(,,8)(,,24)(,,40)(,,54)(,,68);(,,74)
CLUSTER OCCURS WITH MCDEVISE ID BASE 05
FIELD DEVCLE OF MCDEVISE &
  HIDDEN LABEL " " &
  NOCHANGE &
  REQUIRED &
  LOOKUP NOTON MCDEVISE &
    VIA DEVCLE USING DEVCLE OF MCDEVISE &
    MESSAGE 107 ; Ce code de devise existe déjà
FIELD DEVDSCFRA OF MCDEVISE REQUIRED
FIELD DEVDSCANG OF MCDEVISE REQUIRED
FIELD DEVTAU    OF MCDEVISE REQUIRED
FIELD DEVOLDTAU OF MCDEVISE DISPLAY
FIELD DEVDATMAJ OF MCDEVISE  FORMAT YYYYMMDD PATTERN "####/##/##" DISPLAY
CLUSTER

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le Holding a été défini, alors on applique la sécurité standard, sinon
  ; on a accès à l'écran sans restriction.
  ;
  IF T_FLGHLDOK = "1"
  THEN BEGIN
    USE ussececr NOLIST
  END
END


;-------------------------------------------------------------------------------
;
;
; Si l'usager modifi le taux alors on conserve l'ancien taux et la date de mise
; à jour, si l'usager resaisie le même taux après un modification alors on
; on remet l'ancien taux de la recherche.
;
;
PROCEDURE PROCESS DEVTAU
BEGIN
  IF DEVTAU OF MCDEVISE <> T_DEVTAU
  THEN BEGIN
    LET DEVOLDTAU OF MCDEVISE = T_DEVTAU
    LET DEVDATMAJ OF MCDEVISE = SYSDATE
  END
  ELSE BEGIN
    ;
    ; On remet l'informations avant modification.
    ;
    LET DEVOLDTAU OF MCDEVISE = T_DEVOLDTAU
    LET DEVDATMAJ OF MCDEVISE = T_DEVDATMAJ
  END
  DISPLAY DEVOLDTAU OF MCDEVISE
  DISPLAY DEVDATMAJ OF MCDEVISE
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT DEVCLE
BEGIN
  USE ussecent NOLIST
END

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN mc003a PASSING T_CIENOM MODE F
END
;-------------------------------------------------------------------------------
;
;
;
PROCEDURE POSTFIND
BEGIN
  FOR MCDEVISE
  BEGIN
    LET T_DEVTAU    = DEVTAU    OF MCDEVISE
    LET T_DEVOLDTAU = DEVOLDTAU OF MCDEVISE
    LET T_DEVDATMAJ = DEVDATMAJ OF MCDEVISE
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE POSTUPDATE
BEGIN
  FOR MCDEVISE
  BEGIN
    LET T_DEVTAU    = DEVTAU    OF MCDEVISE
    LET T_DEVOLDTAU = DEVOLDTAU OF MCDEVISE
    LET T_DEVDATMAJ = DEVDATMAJ OF MCDEVISE
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR MCDEVISE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF MCDEVISE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF MCDEVISE &
       AND NOT NEWRECORD     OF MCDEVISE &
       AND NOT DELETEDRECORD OF MCDEVISE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'un code de devise.
    ;
    IF DELETEDRECORD OF MCDEVISE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la devise est référée.
      ;
      RUN SCREEN mc003x MODE SAME &
                        PASSING MCDEVISE , &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Devises ************************************
*                                                                              *
*      ----------Description----------   Taux      Ancien     Date             *
* Code Française       Anglaise         Courant     Taux      M.A.J.           *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
* xxx  xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxx  xxxxxxxxx  xxxxxxxx          *
*                                                                              *
********************************************************************************
@ENDIF
