;/T/ Listes, rapports et procédures A/R
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 22 avril 1994
;
;    Description..: Menu général pour la présentation des choix de
;                   listes et rapports ou de procédures pour le module
;                   Achats et Réceptions.
;
;/M/ Pascal Tremblay, 98-01-21
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN arm05 &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND &
             RECEIVING D_XMOCOD , &
                       T_CIECLEMNU, &
                       T_FOUCIECLE, &
                       T_PECCLEMNU, &
                       T_CIENOM   , &
                       T_PECDATDEB, &
                       T_PECDATFIN

USE ussectmp NOLIST ; Variables pour la sécurité
    "ARM05"

USE arm05.hlp NOLIST

DEFINE    D_XMOCOD    CHARACTER SIZE 4  ; Code pour rapports ou procédures
TEMPORARY T_IMP       CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_MODEIMP   CHARACTER SIZE 1 ;Mode d'impression(Direct ou Normal)
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4
TEMPORARY T_CIENOM    CHARACTER SIZE 40
TEMPORARY T_PECDATFIN DATE
TEMPORARY T_PECDATDEB DATE

;-------------------------------------------------------------------------------
;
; Code de module.
;
FILE GSMODULE          PRIMARY


;
; Programmes liés au code de module.
;
FILE GSPROGRAMME       DETAIL &
                      NOITEM &
                      OCCURS 13


;-------------------------------------------------------------------------------
;
TEMPORARY T_XMONUM INTEGER   SIZE 2 RESET AT STARTUP INIT 0 ; Ordre des rapports
;
; Nom de la compagnie centrée.
;
DEFINE D_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)
;
; Titre du code de module.
;
DEFINE D_XMOTIT    CHARACTER SIZE 70 = &
@IF FRANCAIS
                                      XMOTITFRA OF GSMODULE
@ELSE
                                      XMOTITANG OF GSMODULE
@ENDIF

;
; Pour passer le le code du programme à l'écran paramètre.
;
DEFINE D_XPRNOM CHARACTER SIZE 7 = XPRNOM OF GSPROGRAMME

;
; Liste des paramètres.
;
DEFINE D_LISPAR char      size 204 =  &
  "F_CIECLEMNU     |F_PECCLEMNU     |F_CIENOM        |F_FOUCIECLE     |" 

DEFINE D_INDLON CHARACTER SIZE 112 =  &
  "001004|005004|009040|049004"
 
DEFINE D_LISVAL CHARACTER SIZE 300 = T_CIECLEMNU &
                                   + T_PECCLEMNU &
                                   + D_CIENOM &
                                   + T_FOUCIECLE

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 2,1  TO 2,19
DRAW 2,62 TO 2,80
DRAW 1,19 TO 3,62

DRAW 2,1  TO 7,1
DRAW 7,1  TO 7,80
DRAW 2,80 TO 7,80
DRAW 5,1  TO 5,80

DRAW 9, 1 TO 23,1
DRAW 9,80 TO 23,80
DRAW 23,1 TO 23,80


TITLE &
@IF FRANCAIS
      "A C H A T S  E T  R E C E P T I O N S" &
@ELSE
      "%%%A C H A T S  E T  R E C E P T I O N S" &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

HILITE DISPLAY HALFTONE

FIELD D_CIENOM &
        FIXED NOLABEL DATA AT 4,21

SKIP 1

;ALIGN(,4,13)(,30,42)(,60,70)
ALIGN(,4,13)(,29,41)(,58,68)

FIELD T_PECCLEMNU FIXED &
        PICTURE "^^-^^" &
@IF FRANCAIS
        LABEL "Période:"
@ELSE
        LABEL "Period :"
@ENDIF

FIELD T_PECDATDEB FIXED FORMAT YYYYMMDD &
@IF FRANCAIS
        LABEL "Date début:"
@ELSE
        LABEL "Start Date:"
@ENDIF

FIELD T_PECDATFIN FIXED FORMAT YYYYMMDD &
@IF FRANCAIS
        LABEL "Date fin:"
@ELSE
        LABEL "End Date:"
@ENDIF

USE ushilite NOLIST

SKIP TO 8

ALIGN(,,2)(,,5)

FIELD T_XMONUM &
        DISPLAY &
        PICTURE "^^"

FIELD D_XMOTIT &
        DISPLAY

SKIP TO 10

HILITE DISPLAY HALFTONE

ALIGN (7,4,7) (,,69)

CLUSTER OCCURS WITH GSPROGRAMME ID BASE 01
@IF FRANCAIS
FIELD XPRDSCFRA OF GSPROGRAMME HIDDEN LABEL " "
@ELSE
FIELD XPRDSCANG OF GSPROGRAMME HIDDEN LABEL " "
@ENDIF

FIELD XPRNOM    OF GSPROGRAMME 

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les codes de modules.
;
;
PROCEDURE INPUT T_XMONUM
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    RUN SCREEN gs105 MODE F &
                     PASSING D_XMOCOD , &
                             T_XMONUM
    LET FIELDTEXT = ASCII(T_XMONUM)
  END
END

;-------------------------------------------------------------------------------
;
;
; Pour centrer le titre du module, regroupement de rapports.
;
;
PROCEDURE OUTPUT D_XMOTIT
BEGIN
  LET FIELDTEXT = CENTER(FIELDTEXT)
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  IF XPRNOM OF GSPROGRAMME <> ""
  THEN BEGIN
    LET T_IMP     = XIMCOD OF GSPROGRAMME
    LET T_MODEIMP = "N"
    RUN SCREEN gs090 MODE E PASSING &
                                    D_XPRNOM, &
                                    D_LISPAR, &
                                    D_INDLON, &
                                    D_LISVAL
  END
END
;
; Pour PowerHouse Client

PROCEDURE DESIGNER PH01
BEGIN
  IF XPRNOM OF GSPROGRAMME <> ""
  THEN BEGIN
    LET T_IMP     = XIMCOD OF GSPROGRAMME
    LET T_MODEIMP = "N"
    RUN SCREEN gs090 MODE E PASSING T_IMP, &
                                    T_MODEIMP, &
                                    D_XPRNOM, &
                                    D_LISPAR, &
                                    D_INDLON, &
                                    D_LISVAL
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE PATH
BEGIN
  ;
  ; Lorsqu'on arrive dans l'écran la première fois le numéro de module
  ; est zéro.
  ;
  IF T_XMONUM = 0
  THEN LET PATH = 2
  ELSE BEGIN
    REQUEST T_XMONUM
    IF PROMPTOK
    THEN LET PATH = 1
  END
  IF PATH = 0
  THEN LET PATH = 2
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE FIND
BEGIN
  ;
  ; Recherche sur un numéro de sous-module.
  ;
  IF PATH = 1
  THEN GET GSMODULE VIA XMOCOD, &
                        XMONUM  &
                    USING D_XMOCOD, &
                          T_XMONUM  &
                    ORDERED
  ;
  ; Recherche sur un module.
  ;
  IF PATH = 2
  THEN GET GSMODULE VIA XMOCOD &
                    USING D_XMOCOD &
                    ORDERBY XMOCOD, &
                            XMONUM
  ;
  ; On met une valeur dans cette variable, pour que quand l'usager
  ; fait une recherche on lui demande un numéro de module.
  ;
  LET T_XMONUM = XMONUM OF GSMODULE
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE DETAIL FIND
BEGIN
  FOR GSPROGRAMME
  BEGIN
    GET GSPROGRAMME VIA     XMOCOD &
                    USING   XMOCOD OF GSMODULE &
                    ORDERBY XPRRNG &
                    OPTIONAL
  END
END

BUILD 
@IF FORMAT
                  ********************************************
*******************  A C H A T S  E T  R E C E P T I O N S   *******************
*                 ********************************************                 *
*                   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
********************************************************************************
*  Période: xxxxx            Date début: xxxxxxxx          Date fin: xxxxxxxx  *
********************************************************************************
 xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
*                                                                              *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
*     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx    *
********************************************************************************
@ENDIF
