;/T/ Priorité diagramme
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-11-08
;
;    Description..: Permet de visionner et de choisir une priorité pour
;                   un diagramme.
;
;    Paramètres...: T_PJTABR        ==> Abréviation
;                   T_PJTANN        ==> Année de la commande interne
;                   T_COMNUMCOMINT  ==> Numéro de séquence de la commande
;                                       interne
;                   T_USINE_SEC     ==> usine secondaire Oui, Non
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd123 NOMODE NOACTION FIELDMARK    &
             FROM 11,1 TO 23,80           &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND              &
             RECEIVING T_DIANUM002,       &
                       T_PRICODPRI,       &
                       T_QTEPLADBT,       &
                       T_CIECLEMNU



TEMPORARY T_DIANUM002    CHARACTER SIZE 07
TEMPORARY T_PRICODPRI    CHARACTER SIZE 02
TEMPORARY T_QTEPLADBT    INTEGER   SIZE 04
TEMPORARY T_CIECLEMNU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Documentation d'aide pour l'écran
;

USE pd123.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse NOLIST



;-------------------------------------------------------------------------------
;
; Priorité diagramme
;

FILE PDPRIORITE_DIAG PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,        &
                 DIANUM002      &
         USING   T_CIECLEMNU,   &
                 T_DIANUM002    &
         ORDERBY CIECLE,        &
                 DIANUM002,     &
                 PRICODPRI


;  SELECT IF T_PRICODPRI = " " &
;            OR &
;            T_PRICODPRI = PRICODPRI OF PDPRIORITE_DIAG


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,80       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    Diagramme Priorité Num. cmd     Dbt G  Fin G  Dbt U  Fin U  Dbt S  Fin S " &
@ELSE
      "%%% Diagramme Priorité Num. cmd     Dbt G  Fin G  Dbt U  Fin U  Dbt S  Fin S " &
@ENDIF
    AT ,2


SKIP


ALIGN (3,3,6) (,,19) (,,25) (,,38) (,,45) (,,52) (,,59) (,,66) (,,73)


CLUSTER OCCURS WITH PDPRIORITE_DIAG


FIELD DIANUM002        OF PDPRIORITE_DIAG        &
        HIDDEN LABEL ">>" DISPLAY


FIELD PRICODPRI OF PDPRIORITE_DIAG               &
        DISPLAY NUMERIC


FIELD COMNUMCOM        OF PDPRIORITE_DIAG        &
        DISPLAY


FIELD PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG        &
        DISPLAY


FIELD PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG        &
        DISPLAY


FIELD PDGQTEPLACPEUSC  OF PDPRIORITE_DIAG        &
        DISPLAY


FIELD PDGQTEPLAFINUSC  OF PDPRIORITE_DIAG        &
        DISPLAY


FIELD PDGQTEPLACPESCT  OF PDPRIORITE_DIAG        &
        DISPLAY


FIELD PDGQTEPLAFINSCT  OF PDPRIORITE_DIAG        &
        DISPLAY


CLUSTER


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE INITIALIZE
BEGIN
  LET T_QTEPLADBT = 0
END


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_PRICODPRI, "@")
  THEN LET T_PRICODPRI = ""
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_PRICODPRI = PRICODPRI        OF PDPRIORITE_DIAG
  LET T_QTEPLADBT = PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG
  RETURN
END

BUILD

@IF FORMAT

********************************************************************************
*    Diagramme Priorité Num. cmd     Dbt G  Fin G  Dbt U  Fin U  Dbt S  Fin S  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
* >> xxxxxxx      xx    xxxxxxxxx    xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  xxxxx  *
*                                                                              *
********************************************************************************
@ENDIF
