;/T/ 2.2.2 Détail réception (Tranche)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-08-22
;
;    Description..: Cette écran permet de d'enregistrer la réception des bons
;                   de commandes pour les tranches.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd176b ACTIONBAR                   &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM,          &
                       T_FOUNOM,          &
                       PDRECEPTION,       &
                       PDBON_COMMANDE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD176B"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd176b.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Défauts de tranche              " ACTION DESIGNER D001
  MENUITEM LABEL "Vérification tranche            " ACTION DESIGNER D002
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Défauts de tranche           " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Vérification tranche         " ACTION DESIGNER D002
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_TRANUMTRA002 CHAR SIZE 7
TEMPORARY T_FLG CHAR SIZE 1
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_FOUNOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom du fournisseur
TEMPORARY T_PODNUMLIG CHARACTER SIZE 3
TEMPORARY T_BCMNUMBCM INTEGER SIZE 4
TEMPORARY T_TGRCODGRA        CHAR SIZE 3
TEMPORARY T_TYFCODFIN        CHAR SIZE 2
TEMPORARY T_PODLON           INTE SIZE 4
TEMPORARY T_PODHAU           INTE SIZE 4
TEMPORARY T_PODEPA           INTE SIZE 4

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_TRASTU          CHARACTER SIZE 16 = "TRASTU"
TEMPORARY T_TRASTU          CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;

USE usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximun et minimun
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_TRALON       CHARACTER SIZE 04 RESET AT STARTUP
TEMPORARY T_TRAHAU       CHARACTER SIZE 04 RESET AT STARTUP
TEMPORARY T_TRAEPA       CHARACTER SIZE 04 RESET AT STARTUP


;-------------------------------------------------------------------------------
;
; Réception
;

FILE PDRECEPTION MASTER


;-------------------------------------------------------------------------------
;
; Bon de commande
;

FILE PDBON_COMMANDE MASTER


;-------------------------------------------------------------------------------
;
; Détail réception de tranche
;

FILE PDREC_TRANCHE PRIMARY &
                   NOITEM

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC,                            &
                 TRANUMTRA002                          &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION,       &
                 TRANUMTRA002    OF PDREC_TRANCHE      &
         REQUEST TRANUMTRA002    OF PDREC_TRANCHE      &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 TRANUMTRA002

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION        &
         ORDERBY CIECLE,                               &
                RCTNUMREC



  ITEM CIECLE       OF PDREC_TRANCHE INITIAL T_CIECLEMNU
  ITEM RCTNUMREC    OF PDREC_TRANCHE INITIAL RCTNUMREC OF PDRECEPTION
  ITEM RTRQTE       OF PDREC_TRANCHE INITIAL 1

  ITEM TRTPRIMC2VEN OF PDREC_TRANCHE FINAL   0.00

;-------------------------------------------------------------------------------
;
; Inscription de la tranche dans la base de donnée
;

FILE PDTRANCHE SECONDARY &
               NEED 1    &
               NOITEM

  ACCESS VIA   CIECLE,         &
               TRANUMTRA002    &
         USING T_CIECLEMNU,    &
               TRANUMTRA002    OF PDREC_TRANCHE

  ITEM CIECLE          OF PDTRANCHE INITIAL T_CIECLEMNU
  ITEM TRANUMTRA001    OF PDTRANCHE INITIAL ""
  ITEM TRANUMTRA002    OF PDTRANCHE FINAL   TRANUMTRA002 OF PDREC_TRANCHE
  ITEM TRASTU          OF PDTRANCHE INITIAL "I"
  ITEM TRACODPRD       OF PDTRANCHE INITIAL "A"
  ITEM TRADATCRE       OF PDTRANCHE INITIAL SYSDATE
  ITEM TRAHRECRE       OF PDTRANCHE INITIAL SYSTIME / 10000
  ITEM TRAUSRCRE       OF PDTRANCHE INITIAL LOGONID
  ITEM TRADATMAJ       OF PDTRANCHE INITIAL 0
  ITEM TRAHREMAJ       OF PDTRANCHE INITIAL 0
  ITEM TRAUSRMAJ       OF PDTRANCHE INITIAL " "


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut du bloc)
;


FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_TRASTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_TRASTU,                             &
               TRASTU           OF PDTRANCHE

;-------------------------------------------------------------------------------
;
; Définition de variable temporaire pour le traitement
;
DEFINE D_M2     FLOAT SIZE 8 = TRALON OF PDTRANCHE * &
                               TRAHAU OF PDTRANCHE

TEMPORARY T_TYPE_PRODUIT CHARACTER SIZE 2 INITIAL "TR" RESET AT STARTUP

TEMPORARY T_OLD_VOL FLOAT SIZE 8
TEMPORARY T_OLD_SUR FLOAT SIZE 8


;-------------------------------------------------------------------------------
;
; Détail du bon de commande
;

FILE PDDET_BON_COMMAN SECONDARY &
                      NOITEM
  ACCESS VIA     CIECLE,                         &
                 BCMNUMBCM,                      &
                 PODNUMLIG                       &
         USING   T_CIECLEMNU,                    &
                 BCMNUMBCM OF PDBON_COMMANDE,    &
                 PODNUMLIG OF PDREC_TRANCHE

  ITEM CIECLE       OF PDDET_BON_COMMAN INITIAL T_CIECLEMNU
  ITEM RTRPRIMC2BCM OF PDREC_TRANCHE FINAL   PODPRI       OF PDDET_BON_COMMAN
  ITEM TGRCODGRA    OF PDTRANCHE     INITIAL TGRCODGRA    OF PDDET_BON_COMMAN
  ITEM TYFCODFIN    OF PDTRANCHE     INITIAL TYFCODFIN    OF PDDET_BON_COMMAN

  ITEM PODRCU      OF PDDET_BON_COMMAN FINAL "0" IF PODQTERCU OF PDDET_BON_COMMAN = 0 &
                                                 ELSE "1"

;-------------------------------------------------------------------------------
;
; Fichiers de rérérence type de produit
;

FILE PDTYPE_PRODUIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TPDTYPPRD                       &
         USING   T_CIECLEMNU,                    &
                 TPDTYPPRD OF PDDET_BON_COMMAN


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TGRCODGRA                       &
         USING   T_CIECLEMNU,                    &
                 TGRCODGRA OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TYFCODFIN                       &
         USING   T_CIECLEMNU,                    &
                 TYFCODFIN OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Défauts de tranche
;

FILE PDTRANCHE_DEF      DESIGNER


;-------------------------------------------------------------------------------
;
; Lien assignation commande interne
;

FILE PDCOMMAN_ASSIGN    DESIGNER


;-------------------------------------------------------------------------------
;
; Détail débitage
;

FILE PDDET_DEBITAGE     DESIGNER


;-------------------------------------------------------------------------------
;
; Détail opération de surface
;

FILE PDDET_OPER_SURF    DESIGNER


;-------------------------------------------------------------------------------
;
; Sortie de tranche
;

FILE PDSORTIE_TRANCHE   DESIGNER


;-------------------------------------------------------------------------------
;
; Lien tranche - soumission
;

FILE PDTRANCHE_SOUM     DESIGNER


;-------------------------------------------------------------------------------
;
; Redimension de tranche
;

FILE PDREDIM_TRANCHE    DESIGNER


;-------------------------------------------------------------------------------
;
; Lien tranche - facture
;

FILE PDTRANCHE_FAC      DESIGNER


;-------------------------------------------------------------------------------
;
; Emballage tranche
;

FILE PDEMBAL_TRANCHE    DESIGNER


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Réception de tranche " &
@ELSE
      " %%%Réception de tranche " &
@ENDIF
      CENTERED AT ,1



SKIP TO LINE 4


ALIGN (,3,19) (,38,56)


FIELD RCTNUMREC OF PDRECEPTION                &
label "No. réception.:"&
        DISPLAY                               &
        PREDISPLAY REFRESH


FIELD RCTDATREC OF PDRECEPTION                 FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date réception:"&
        DISPLAY PREDISPLAY REFRESH


ALIGN (,3,19) (,,38)


FIELD FOUCLE       OF PDBON_COMMANDE          &
label "Fournisseur...:"&
        DISPLAY PREDISPLAY REFRESH


FIELD T_FOUNOM                                &
        DISPLAY PREDISPLAY REFRESH


DRAW 7,1 TO 7,80


SKIP TO LINE 7


TITLE &
@IF FRANCAIS
      " Tranches " &
@ELSE
      " %%%Tranches " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 9


ALIGN (2,3,19) (37,38,54)


FIELD PODNUMLIG    OF PDREC_TRANCHE               &
label "Numéro ligne..:"&
        HIDDEN ID 25                              &
        DUPLICATE                                 &
        LOOKUP ON FILE PDDET_BON_COMMAN           &
          VIA     CIECLE,                         &
                  BCMNUMBCM,                      &
                  PODNUMLIG,                      &
                  TPDTYPPRD                       &
          USING   T_CIECLEMNU,                    &
                  BCMNUMBCM OF PDBON_COMMANDE,    &
                  PODNUMLIG OF PDREC_TRANCHE,     &
                  T_TYPE_PRODUIT                  &
          MESSAGE 2937 ; Ce numéro de ligne de détail ne correspond pas à un détail pour une tranche


FIELD PODPRI       OF PDDET_BON_COMMAN            &
        HIDDEN ID 30                              &
        LABEL "Prix M2 cmd...:"                   &
        DISPLAY REFRESH


ALIGN (2,3,19)


FIELD TRANUMTRA002 OF PDREC_TRANCHE           &
        HIDDEN ID 35                          &
        LABEL "No tranche....:"               &
        NUMERIC                               &
        REQUIRED NOCHANGE                     &
        LOOKUP NOTON FILE PDTRANCHE           &
          VIA     CIECLE,                     &
                  TRANUMTRA002                &
          USING   T_CIECLEMNU,                &
                  TRANUMTRA002 OF PDREC_TRANCHE &
          MESSAGE 2939 ; Ce numéro de tranche existe déjà


ALIGN (2,3,19) (,,25)


FIELD TRASTU          OF PDTRANCHE            &
label "Statut........:"&
        HIDDEN ID 45                          &
        DISPLAY


FIELD CBIDSC           OF A_CBI_TRASTU     &
        DISPLAY


ALIGN (,3,19) (,,25)


FIELD TGRCODGRA    OF PDTRANCHE               &
label "Code granit...:"&
        NUMERIC                               &
        BWZ



FIELD TGRDSCFRA001 OF PDTYPE_GRANIT           &
        DISPLAY


FIELD TYFCODFIN    OF PDTRANCHE               &
label "Code fini.....:"&
        NUMERIC                               &
        BWZ



FIELD TYFDSCFRA001 OF PDTYPE_FINI             &
        DISPLAY


ALIGN (2,3,19)


FIELD TRALON       OF PDTRANCHE               &
label "Longueur......:"&
        HIDDEN ID 56                          &
        DUPLICATE


FIELD TRAHAU       OF PDTRANCHE               &
label "Largeur.......:"&
        HIDDEN ID 57                          &
        DUPLICATE


FIELD TRAEPA       OF PDTRANCHE               &
label "Épaisseur.....:" &
        HIDDEN ID 58                          &
        DUPLICATE


ALIGN (,3,19)


FIELD TRASURMC2        OF PDTRANCHE           &
label "Total m2......:"&
        DISPLAY


ALIGN (,3,19) (37,38,54)


FIELD PODQTECMD OF PDDET_BON_COMMAN           &
label "Quantité comm.:"&
        DISPLAY


FIELD PODQTERCU OF PDDET_BON_COMMAN           &
label "Quantité reçu.:"&
        HIDDEN ID 80                          &
        DISPLAY


ALIGN (,3,19) (,38,54)


FIELD PODSURCMD OF PDDET_BON_COMMAN           &
label "Volume cmd....:"&
        DISPLAY


FIELD PODSURRCU OF PDDET_BON_COMMAN           &
label "Volume rcu....:"&
        DISPLAY REFRESH


ALIGN (2,3,19) (37,38,54)


FIELD RTRPRIMC2 OF PDREC_TRANCHE              &
label "Prix m2.......:"&
        HIDDEN ID 82                          ;&
;        DEFAULT PODPRI OF PDDET_BON_COMMAN


FIELD PODCPL OF PDDET_BON_COMMAN              &
label "Complet.......:"&

        HIDDEN ID 88


FIELD TRTNUMTRAFOU OF PDREC_TRANCHE &
label "No tranche f..:"&
        HIDDEN ID 90


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
  LET T_TRALON = "    "
  LET T_TRAHAU = "    "
  LET T_TRAEPA = "    "
END


;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST

;----------------------------------------------------------------------------
PROCEDURE INPUT PODNUMLIG
BEGIN
 LET T_BCMNUMBCM = BCMNUMBCM OF PDBON_COMMANDE
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PODNUMLIG = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd176i MODE F &
                     PASSING T_PODNUMLIG   , &
                             T_BCMNUMBCM, &
                             T_TGRCODGRA ,&
                             T_TYFCODFIN ,&
                             T_PODLON    ,&
                             T_PODHAU    ,&
                             T_PODEPA    ,&
                             T_FLG,       &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_PODNUMLIG )
  END
END
;-------------------------------------------------------------------------------
;
; Valide le champ
;

PROCEDURE EDIT PODNUMLIG
BEGIN
  ;
  ; Valide que le champs a été saisit s'il n'y a pas eu de DUPLICATE
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide que la ligne n'est pas déjà complète
  ;
  IF PODCPL OF PDDET_BON_COMMAN = "1"
  THEN BEGIN
    ERROR 2932 ; Cette ligne est déjà complétée
  END
END


;-------------------------------------------------------------------------------
;
; incrémente la quantité reçu (en saisie seulement)
;

PROCEDURE PROCESS PODNUMLIG
BEGIN
  ;
  ; Incrémente la quantité reçu (En saisie seulement)
  ;
  IF ENTRYMODE
  THEN BEGIN
    LET PODQTERCU OF PDDET_BON_COMMAN = PODQTERCU OF PDDET_BON_COMMAN + 1
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option duplicate
;

PROCEDURE INPUT TRALON
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    IF 0 <> SIZE(TRUNCATE(T_TRALON))
    THEN BEGIN
      LET FIELDTEXT = T_TRALON
    END
  END
  ELSE BEGIN
    LET T_TRALON = FIELDTEXT
  END
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions à payer du bloc
; N.B.: on ne peut pas avoir de dimension à payer plus grande que les dimensions
;       nettes.
;

PROCEDURE EDIT TRALON
BEGIN
  ;
  ; Valide que le champs a été saisit
  ;
  IF 0 = FIELDVALUE
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide les dimensions
  ;
  IF FIELDVALUE < T_DIM_MIN_LON_TR OR &
     FIELDVALUE > T_DIM_MAX_LON_TR
  THEN BEGIN
   ERROR 2947 ; La dimension est hors des limite
  END
  ELSE BEGIN
    ;
    ; Calcul de volume (old)
    ;
    LET TZ_LARGEUR   = OLDVALUE(TRALON OF PDTRANCHE)
    LET TZ_HAUTEUR   = TRAHAU OF PDTRANCHE
    LET TZ_EPAISSEUR = TRAEPA OF PDTRANCHE
    DO INTERNAL CALVOL
    LET T_OLD_VOL    = TZ_VOLUME
    LET T_OLD_SUR    = TZ_MC2
    ;
    ; Calcul de volume (new)
    ;
    LET TZ_LARGEUR   = FIELDVALUE ; TRALON OF PDTRANCHE
    LET TZ_HAUTEUR   = TRAHAU OF PDTRANCHE
    LET TZ_EPAISSEUR = TRAEPA OF PDTRANCHE
    DO INTERNAL CALVOL
    LET TRASURMC2 OF PDTRANCHE = TZ_MC2
    LET TRAVOLMC3 OF PDTRANCHE = TZ_VOLUME
    DISPLAY TRASURMC2 OF PDTRANCHE
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu
;

PROCEDURE PROCESS TRALON
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODSURRCU OF PDDET_BON_COMMAN = PODSURRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_SUR  +                    &
                                      TZ_MC2
  IF NOT ENTRYMODE
  THEN BEGIN
    DISPLAY PODSURRCU OF PDDET_BON_COMMAN
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option duplicate
;

PROCEDURE INPUT TRAHAU
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    IF 0 <> SIZE(TRUNCATE(T_TRAHAU))
    THEN BEGIN
      LET FIELDTEXT = T_TRAHAU
    END
  END
  ELSE BEGIN
    LET T_TRAHAU = FIELDTEXT
  END
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions à payer du bloc
; N.B.: on ne peut pas avoir de dimension à payer plus grande que les dimensions
;       nettes.
;

PROCEDURE EDIT TRAHAU
BEGIN
  ;
  ; Valide que le champs a été saisit s'il n'y a pas eu de DUPLICATE
  ;
  IF 0 = FIELDVALUE
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide les dimensions
  ;
  IF FIELDVALUE < T_DIM_MIN_HAU_TR OR &
     FIELDVALUE > T_DIM_MAX_HAU_TR
  THEN BEGIN
   ERROR 2947 ; La dimension est hors des limite
  END
  ELSE BEGIN
    ;
    ; Calcul de volume (old)
    ;
    LET T_OLD_VOL = 0.0000
    IF CHANGEMODE
    THEN BEGIN
      LET TZ_LARGEUR   = TRALON OF PDTRANCHE
      LET TZ_HAUTEUR   = OLDVALUE(TRAHAU OF PDTRANCHE)
      LET TZ_EPAISSEUR = TRAEPA OF PDTRANCHE
      DO INTERNAL CALVOL
      LET T_OLD_VOL    = TZ_VOLUME
      LET T_OLD_SUR    = TZ_MC2
    END
    ;
    ; Calcul de volume (new)
    ;
    LET TZ_LARGEUR   = TRALON OF PDTRANCHE
    LET TZ_HAUTEUR   = FIELDVALUE ; TRAHAU OF PDTRANCHE
    LET TZ_EPAISSEUR = TRAEPA OF PDTRANCHE
    DO INTERNAL CALVOL
    LET TRASURMC2 OF PDTRANCHE = TZ_MC2
    LET TRAVOLMC3 OF PDTRANCHE = TZ_VOLUME
    DISPLAY TRASURMC2 OF PDTRANCHE
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu
;

PROCEDURE PROCESS TRAHAU
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODSURRCU OF PDDET_BON_COMMAN = PODSURRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_SUR  +                    &
                                      TZ_MC2
  IF NOT ENTRYMODE
  THEN BEGIN
    DISPLAY PODSURRCU OF PDDET_BON_COMMAN
  END
END




;-------------------------------------------------------------------------------
;
; Simule l'option duplicate
;

PROCEDURE INPUT TRAEPA
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    IF 0 <> SIZE(TRUNCATE(T_TRAEPA))
    THEN BEGIN
      LET FIELDTEXT = T_TRAEPA
    END
  END
  ELSE BEGIN
    LET T_TRAEPA = FIELDTEXT
  END
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions à payer du bloc
; N.B.: on ne peut pas avoir de dimension à payer plus grande que les dimensions
;       nettes.
;

PROCEDURE EDIT TRAEPA
BEGIN
  ;
  ; Valide que le champs a été saisit s'il n'y a pas eu de DUPLICATE
  ;
  IF 0 = FIELDVALUE
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide les dimensions
  ;
  IF FIELDVALUE < T_DIM_MIN_EPA_TR OR &
     FIELDVALUE > T_DIM_MAX_EPA_TR
  THEN BEGIN
   ERROR 2947 ; La dimension est hors des limite
  END
  ELSE BEGIN
    ;
    ; Calcul du dans l'intervale de l'industrie
    ;
    IF FIELDVALUE > PODEPA       OF PDDET_BON_COMMAN + 2 OR &
       FIELDVALUE < PODEPA       OF PDDET_BON_COMMAN - 2
    THEN BEGIN
      ERROR 3093 ; Cette dimension est hors limite
    END
    ;
    ; Calcul de volume (old)
    ;
    LET TZ_LARGEUR   = TRALON OF PDTRANCHE
    LET TZ_HAUTEUR   = TRAHAU OF PDTRANCHE
    LET TZ_EPAISSEUR = OLDVALUE(TRAEPA OF PDTRANCHE)
    DO INTERNAL CALVOL
    LET T_OLD_VOL    = TZ_VOLUME
    LET T_OLD_SUR    = TZ_MC2
    ;
    ; Calcul de volume (new)
    ;
    LET TZ_LARGEUR   = TRALON OF PDTRANCHE
    LET TZ_HAUTEUR   = TRAHAU OF PDTRANCHE
    LET TZ_EPAISSEUR = FIELDVALUE ; TRAEPA OF PDTRANCHE
    DO INTERNAL CALVOL
    LET TRASURMC2 OF PDTRANCHE = TZ_MC2
    LET TRAVOLMC3 OF PDTRANCHE = TZ_VOLUME
    DISPLAY TRASURMC2 OF PDTRANCHE
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu
;

PROCEDURE PROCESS TRAEPA
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODSURRCU OF PDDET_BON_COMMAN = PODSURRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_SUR  +                    &
                                      TZ_MC2
  IF NOT ENTRYMODE
  THEN BEGIN
    DISPLAY PODSURRCU OF PDDET_BON_COMMAN
  END
END


;-------------------------------------------------------------------------------
;
; Informe l'utilisateur que la QTE RCU > QTE CMD
;

PROCEDURE OUTPUT PODSURRCU
BEGIN
  IF NOT FINDMODE
  THEN BEGIN
  IF PODSURRCU OF PDDET_BON_COMMAN > PODSURCMD OF PDDET_BON_COMMAN
  THEN BEGIN
    WARNING 2936 ; la quantité reçu est plus grande que la quantité commandé
  END
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;

PROCEDURE INPUT PODCPL
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT PODCPL
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Retrouve le détail bon de commande pour pas avoir d'erreur si plus d'une
; destruction pour un même bon de commande.
;

PROCEDURE POSTFIND
BEGIN
  GET PDDET_BON_COMMAN                      &
    VIA     CIECLE,                         &
            BCMNUMBCM,                      &
            PODNUMLIG,                      &
            TPDTYPPRD                       &
    USING   T_CIECLEMNU,                    &
            BCMNUMBCM OF PDBON_COMMANDE,    &
            PODNUMLIG OF PDREC_TRANCHE,     &
            T_TYPE_PRODUIT
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDREC_TRANCHE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDREC_TRANCHE &
     AND NOT NEWRECORD     OF PDREC_TRANCHE &
     AND NOT DELETEDRECORD OF PDREC_TRANCHE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Affectation de l'enregistrement PDTRANCHE
  ;
  LET TZ_LARGEUR   = TRALON OF PDTRANCHE
  LET TZ_HAUTEUR   = TRAHAU OF PDTRANCHE
  LET TZ_EPAISSEUR = TRAEPA OF PDTRANCHE
  DO INTERNAL CALVOL
  LET TRASURMC2 OF PDTRANCHE = TZ_MC2
  LET TRAVOLMC3 OF PDTRANCHE = TZ_VOLUME
  ;
  ;   Évaluation des champs indiquant une mise à jour du bon de commande.
  ;
  IF NOT NEWRECORD OF PDREC_TRANCHE AND &
     ALTEREDRECORD OF PDREC_TRANCHE
  THEN BEGIN
    LET RCTDATDERMAJ    OF PDRECEPTION = SYSDATE
    LET RCTHREDERMAJ    OF PDRECEPTION = SYSTIME / 10000
    LET RCTUSRDERMAJ    OF PDRECEPTION = LOGONID
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE INTERNAL RECHERCHE_TRANCHE
BEGIN
  ;
  ; Recherche si la tranche a été assigné à une commande
  ;
  GET PDCOMMAN_ASSIGN    OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Assignation commande interne") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a été débité
  ;
  GET PDDET_DEBITAGE     OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Débitage") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a reçu un fini de surface
  ;
  GET PDDET_OPER_SURF    OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Fini de surface") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a été assigne à une sortie
  ;
  GET PDSORTIE_TRANCHE   OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Sortie de tranche") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a été assigné à une soumission
  ;
  GET PDTRANCHE_SOUM     OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Soumission") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a été redimensionné
  ;
  GET PDREDIM_TRANCHE    OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Redimension de tranche") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a été facturé
  ;
  GET PDTRANCHE_FAC      OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Facture") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si la tranche a été emballé
  ;
  GET PDEMBAL_TRANCHE    OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Emballage") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
END


;-------------------------------------------------------------------------------
;
; Procedure de destruction
;

PROCEDURE DELETE
BEGIN
  IF NOT DELETEDRECORD OF PDREC_TRANCHE
  THEN BEGIN
    ;
    ; Valide le statut de la tranche avant la destruction
    ;
    GET PDTRANCHE &
      VIA   CIECLE,         &
            TRANUMTRA002    &
      USING T_CIECLEMNU,    &
            TRANUMTRA002    OF PDREC_TRANCHE
    IF ACCESSOK
    THEN BEGIN
      IF TRASTU OF PDTRANCHE <> "I"
      THEN BEGIN
        ERROR 3068 ; Le statut ne permet pas cette opération
      END
    END
    ELSE BEGIN
      ERROR = "PROBLÈME TECHNIQUE (1) - La tranche n'existe pas"
    END
    ;
    ; Recherche si le bloc a déjà été utilisé
    ;
    DO INTERNAL RECHERCHE_TRANCHE
    ;
    ; Décrémente la quantité reçu
    ;
    LET PODQTERCU OF PDDET_BON_COMMAN = PODQTERCU OF PDDET_BON_COMMAN - 1
    ;
    ; Décrémente de volume reçu
    ;
    LET TZ_LARGEUR   = TRALON OF PDTRANCHE
    LET TZ_HAUTEUR   = TRAHAU OF PDTRANCHE
    LET TZ_EPAISSEUR = TRAEPA OF PDTRANCHE
    DO INTERNAL CALVOL
    LET PODSURRCU OF PDDET_BON_COMMAN = PODSURRCU OF PDDET_BON_COMMAN - TZ_MC2
    ;
    ; Initialise les indicateurs de réception
    ;
    IF PODQTERCU OF PDDET_BON_COMMAN = 0
    THEN BEGIN
      LET PODRCU OF PDDET_BON_COMMAN = "0"
    END
    ELSE BEGIN
      LET PODRCU OF PDDET_BON_COMMAN = "1"
    END
    ;
    ; Si la commade était complète alors on indique qu'elle ne le sera plus
    ;
    IF PODCPL OF PDDET_BON_COMMAN = "1"
    THEN BEGIN
      LET PODCPL OF PDDET_BON_COMMAN = "0"
    END
  END
  WHILE RETRIEVING PDTRANCHE_DEF &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  BEGIN
    DELETE PDTRANCHE_DEF
  END
  DELETE PDREC_TRANCHE
  DELETE PDTRANCHE
END


;-------------------------------------------------------------------------------
;
; Sous-écran de défauts de tranche
;

PROCEDURE DESIGNER D001
BEGIN
 LET TRANUMTRA002 OF PDTRANCHE = TRANUMTRA002    OF PDREC_TRANCHE
  RUN SCREEN  pd176g &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        PDTRANCHE       &
              MODE SAME
END

;-------------------------------------------------------------------------------
;
; Consultation des date de création et de modification de tranche
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd176h &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        PDTRANCHE       &
              MODE SAME
END
;------------------------------------------------------------------------
PROCEDURE INPUT TGRCODGRA
BEGIN
 IF TRASTU OF PDTRANCHE <> "I"
 THEN ERROR = "LE STATUS DE LA TRANCHE NE LE PERMET PAS"
END
;------------------------------------------------------------------------
PROCEDURE INPUT TYFCODFIN
BEGIN
  GET PDDET_OPER_SURF    OPTIONAL &
    VIA   CIECLE,      &
          TRANUMTRA002 &
    USING T_CIECLEMNU, &
          TRANUMTRA002 OF PDREC_TRANCHE
  IF ACCESSOK
  THEN BEGIN
    ERROR = "OPERATION DE SURFACE"
  END
END
;------------------------------------------------------------------------
PROCEDURE ENTRY
BEGIN
     DISPLAY RCTNUMREC    OF PDRECEPTION
     DISPLAY RCTDATREC    OF PDRECEPTION
     DISPLAY FOUCLE       OF PDBON_COMMANDE
     DISPLAY T_FOUNOM
     ACCEPT  PODNUMLIG    OF PDREC_TRANCHE
     IF T_FLG = "1"
     THEN BEGIN
       LET TGRCODGRA OF PDTRANCHE = T_TGRCODGRA
       LET TYFCODFIN OF PDTRANCHE = T_TYFCODFIN
       LET TRALON    OF PDTRANCHE = T_PODLON
       LET TRAHAU    OF PDTRANCHE = T_PODHAU
       LET TRAEPA    OF PDTRANCHE = T_PODEPA
       DISPLAY TGRCODGRA OF PDTRANCHE
       DISPLAY TYFCODFIN OF PDTRANCHE
       DISPLAY TRALON    OF PDTRANCHE
       DISPLAY TRAHAU    OF PDTRANCHE
       DISPLAY TRAEPA    OF PDTRANCHE
       ACCEPT  TRANUMTRA002 OF PDREC_TRANCHE
       DISPLAY PODPRI       OF PDDET_BON_COMMAN
       DISPLAY TRASTU       OF PDTRANCHE
       DISPLAY TGRDSCFRA001 OF PDTYPE_GRANIT
       DISPLAY TYFDSCFRA001 OF PDTYPE_FINI
       DISPLAY TRASURMC2    OF PDTRANCHE
       DISPLAY PODQTECMD    OF PDDET_BON_COMMAN
       DISPLAY PODQTERCU    OF PDDET_BON_COMMAN
       DISPLAY PODSURCMD    OF PDDET_BON_COMMAN
       DISPLAY PODSURRCU    OF PDDET_BON_COMMAN
       ACCEPT  RTRPRIMC2    OF PDREC_TRANCHE
       ACCEPT  PODCPL       OF PDDET_BON_COMMAN
       ACCEPT  TRTNUMTRAFOU OF PDREC_TRANCHE
     END
     ELSE BEGIN
       DISPLAY PODPRI       OF PDDET_BON_COMMAN
       ACCEPT  TRANUMTRA002 OF PDREC_TRANCHE
       DISPLAY TRASTU       OF PDTRANCHE
       DISPLAY CBIDSC       OF A_CBI_TRASTU
       ACCEPT TGRCODGRA    OF PDTRANCHE
       DISPLAY TGRDSCFRA001 OF PDTYPE_GRANIT
       ACCEPT TYFCODFIN    OF PDTRANCHE
       DISPLAY TYFDSCFRA001 OF PDTYPE_FINI
       ACCEPT  TRALON       OF PDTRANCHE
       ACCEPT  TRAHAU       OF PDTRANCHE
       ACCEPT  TRAEPA       OF PDTRANCHE
       DISPLAY TRASURMC2    OF PDTRANCHE
       DISPLAY PODQTECMD    OF PDDET_BON_COMMAN
       DISPLAY PODQTERCU    OF PDDET_BON_COMMAN
       DISPLAY PODSURCMD    OF PDDET_BON_COMMAN
       DISPLAY PODSURRCU    OF PDDET_BON_COMMAN
       ACCEPT  RTRPRIMC2    OF PDREC_TRANCHE
       ACCEPT  PODCPL       OF PDDET_BON_COMMAN
       ACCEPT  TRTNUMTRAFOU OF PDREC_TRANCHE
     END
END

BUILD DETAIL LIST

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Réception de tranche *****************************
* No. réception.: xxxxxxxxx          Date réception:   xxxxxxxx                *
* Fournisseur...: xxxxxx             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*********************************** Tranches ***********************************
* Numéro ligne..: xxx                Prix M2 cmd...: xxxxxxxxxxxxxxx           *
* No tranche....: xxxxxxxxx                                                    *
* Statut........: x                                                            *
* Code granit...: xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Code fini.....: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Localisation..: xxx                                                          *
* Largeur.......: xxxxx                                                        *
* Longueur......: xxxxx                                                        *
* Épaisseur.....: xxxxx                                                        *
* Total m2......: xxxxxxxxxxxx                                                 *
* Quantité comm.: xxxxxxxxxx         Quantité reçu.: xxxxxxxxxx                *
* Volume cmd....: xxxxxxxxxxxx       Volume rcu....: xxxxxxxxxxxx              *
* Prix m2.......: xxxxxxxxxxxxxxx    Complet.......: x                         *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
