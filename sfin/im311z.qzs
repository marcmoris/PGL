;/T/ Historique des achats / déboursés
;
;/P/ Programmeur..: Alain Côté
;    Date création: 3 mars 1994
;
;/V3.02.00/ Alain Côté, 3 mars 1994, Immobilisation
;
;    Description...: Ce rapport imprime l'historique des achats et déboursés
;                    pour un ou plusieurs immobilisations.
;
;
;    Paramétres....: No. cie   (%CUF_CIECLEMNU)
;                    No. immo  (%C IMMCLE)
;                    Date debut (%D DATDEB)
;                    Date fin   (%D DATFIN)
;                    Nom cie   (%DUF_CIENOM)
;
;    Tris..........: Par numéro d'immobilisation et par date.
;
;
;-------------------------------------------------------------------------------


USE ussetqzs NOLIST


ACCESS CPCAP_IMM &
  LINK FOUCIECLE OF CPCAP_IMM , &
       FOUCLE    OF CPCAP_IMM   &
    TO FOUCIECLE , &
       FOUCLE OF CPFOURNISSEUR &

  LINK CIECLE OF CPCAP_IMM , &
       IMMCLE OF CPCAP_IMM   &
    TO CIECLE , &
       IMMCLE OF IMIMMOBILISATION

CHOOSE CIECLE PARM, &
       IMMCLE PARM

DEFINE D_DATDEB  DATE = PARM
DEFINE D_DATFIN  DATE = PARM

SELECT CPCAP_IMM IF     CAPDAT OF CPCAP_IMM >= D_DATDEB &
                    AND (    CAPDAT OF CPCAP_IMM <= D_DATFIN &
                          OR D_DATFIN = 0 )


;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Historique des achats / déboursés"
@ELSE
       "%%%Historique des achats / déboursés"
@ENDIF

DEFINE D_NOM2_1   CHARACTER SIZE 30 = &
@IF FRANCAIS
                                "du " + ASCII(D_DATDEB)[3:2] + &
                                 "/"  + ASCII(D_DATDEB)[5:2] + "/" + &
                                 ASCII(D_DATDEB)[7:2] &
                                   IF D_DATDEB <> 0 &
                            ELSE "du début"
@ELSE
@ENDIF

DEFINE D_NOM2_2   CHARACTER SIZE 30 = &
@IF FRANCAIS
                                 "au " + ASCII(D_DATFIN)[3:2] + &
                                 "/"  + ASCII(D_DATFIN)[5:2] + "/" + &
                                 ASCII(D_DATFIN)[7:2] &
                                   IF D_DATFIN <> 0 &
                            ELSE "à ce jour"
@ELSE
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = TRUNCATE( D_NOM2_1 ) + " " + &
                                      TRUNCATE( D_NOM2_2 )

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM311"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM



SORT ON CIECLE OF CPCAP_IMM &
     ON IMMCLE OF CPCAP_IMM &
     ON CAPDAT OF CPCAP_IMM

USE ustitstd NOLIST
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
"                  Fournisseur                   Période Lot  No. document      Date   Commentaire                        Montant" &
@ELSE
"%%%               Fournisseur                   Période Lot  No. document      Date   Commentaire                        Montant" &
@ENDIF
SKIP &
  TAB 001 &
"----------------------------------------------- ------- ---- --------------- -------- ------------------------------ --------------"


HEADING AT IMMCLE &
  TAB 001 " " &
SKIP &
  TAB 001 &
@IF FRANCAIS
          "Immobilisation:" &
@ELSE
          "%%%obilisation:" &
@ENDIF
  TAB 017 IMMCLE OF IMIMMOBILISATION &
  TAB 030 IMMDSC OF IMIMMOBILISATION &
SKIP 2



REPORT &
  TAB 001 FOUCLE OF CPCAP_IMM &
  TAB 008 FOUNOM OF CPFOURNISSEUR &
  TAB 050 PECCLE OF CPCAP_IMM &
  TAB 057 LCPCLE OF CPCAP_IMM &
  TAB 062 CAPCLE OF CPCAP_IMM &
  TAB 078 CAPDAT OF CPCAP_IMM &
  TAB 087 CIMDSC OF CPCAP_IMM &
  TAB 118 CIMMNT OF CPCAP_IMM &
  RESERVE 3 LINES


FOOTING AT IMMCLE &
  TAB 118 "--------------" &
SKIP 1 &
  TAB 087 &
@IF FRANCAIS
       "Total de l'immobilisation:" &
@ELSE
       "%%%al de l'immobilisation:" &
@ENDIF
  TAB 118 CIMMNT OF CPCAP_IMM &
            SUBTOTAL &
SKIP 2


BUILD im311z
@IF FORMAT

IM311                                         XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                    PAGE: 9999
99/99/99 99:99 XXXXXXXXXXXX                      Historique des achats / déboursés
                                                      du xx/xx/xx au xx/xx/xx

                  Fournisseur                   Période Lot  No. document      Date   Commentaire                        Montant
----------------------------------------------- ------- ---- --------------- -------- ------------------------------ --------------

Immobilisation: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
                                                                                                                     --------------
                                                                                      Total de l'immobilisation:     99,999,999.99-


Immobilisation: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
                                                                                                                     --------------
                                                                                      Total de l'immobilisation:     99,999,999.99-


Immobilisation: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
xxxxxx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99-99  9999 XXXXXXXXXXXXXXX 99/99/99 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999.99-
                                                                                                                     --------------
                                                                                      Total de l'immobilisation:     99,999,999.99-
@ENDIF
