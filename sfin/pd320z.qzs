;/T/ Liste des ventes par type de granit
;
;/P/ Programmeur...: Patrick Langlois
;    Date Création.: 28 Mai 1999
;
;    Description...: Liste des ventes par type de granit.
;
;    Paramètres....: Compagnie,type de granit, type de fini, date début, date fin
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST
SET FORMFEED NOBLANKS


;
; Plus rapide avec une vue
;
ACCESS V_VNTGRA  &
  LINK CIECLE  , &
       TYFCODFIN &
    TO CIECLE  , &
       TYFCODFIN    OF  PDTYPE_FINI OPT


CHOOSE CIECLE    PARM, &
       TGRCODGRA PARM, &
       TYFCODFIN PARM, &
       EXPDATEXP PARM RANGE


SORT ON CIECLE   , &
        TGRCODGRA, &
        TYFCODFIN, &
        TRAEPA

DEFINE D_DATDEB DATE = PARM
DEFINE D_DATFIN DATE = PARM


DEFINE D_DATDEBCHR CHARACTER SIZE 10 = ASCII(D_DATDEB)[1:4] + "/" + &
                                       ASCII(D_DATDEB)[5:2] + "/" + &
                                       ASCII(D_DATDEB)[7:2]


DEFINE D_DATFINCHR CHARACTER SIZE 10 = ASCII(D_DATFIN)[1:4] + "/" + &
                                       ASCII(D_DATFIN)[5:2] + "/" + &
                                       ASCII(D_DATFIN)[7:2]

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Ventes par type de granit"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Date de début: " + D_DATDEBCHR + &
                                      "   Date de fin: " + D_DATFINCHR

DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "pd320z"      ; numéro du rapport

DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

DEFINE D_VNT          FLOAT SIZE 8 = (DCIPRIUNMPRD * EMTSURMC2VEN)
DEFINE D_EMTSURMC2VEN FLOAT SIZE 8 = EMTSURMC2VEN * 100

DEFINE D_FOT2 CHARACTER SIZE 50 = "(" + TRUNC(TGRDSCFRA001) + ")"


USE ustitstd NOLIST
  SKIP 3 &
 TAB 002 "Nom granit                 Fini                      Ep.    M2 / Fini       Ventes $$" &
  SKIP 1 &
 TAB 002 "-------------------------  ------------------------  ----  ------------  --------------"




FOOTING AT TRAEPA     &
SKIP 1                &
 TAB 002 TGRCODGRA    PRINT AT TGRCODGRA &
 tab 006 TGRDSCFRA001 PRINT AT TGRCODGRA &
 TAB 029 TYFCODFIN    PRINT AT TYFCODFIN &
 TAB 032 TYFDSCFRA001 PRINT AT TYFCODFIN &
 TAB 055 TRAEPA                          &
 TAB 061 D_EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^" &
 TAB 075 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^"


FOOTING AT TYFCODFIN  &
SKIP 1                &
 TAB 061 "------------  --------------" &
 SKIP 1 &
 TAB 061 D_EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^" &
 TAB 075 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" &
SKIP 3


FOOTING AT TGRCODGRA  &
SKIP 2                &
 TAB 061 "============  ==============" &
 SKIP 1 &
 TAB 006 "TOTAL "     &
 TAB 012 D_FOT2       &
 TAB 061 D_EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^" &
 TAB 075 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" &
SKIP 1 &
 TAB 061 "============  ==============" &
SKIP 2 &
TAB 002 "_______________________________________________________________________________________" &
SKIP 3


FOOTING AT CIECLE     &
SKIP 2                &
 TAB 061 "************  **************" &
 SKIP 1 &
 TAB 006 "GRAND TOTAL" &
 TAB 061 D_EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^" &
 TAB 075 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" &
 SKIP 1 &
 TAB 061 "************  **************"





@IF FORMAT
                                                           GRANICOR INC.                                                  PAGE:    1
99/05/28 14:24 patlan                                Ventes par type de granit
                                         Date de début: 1998/11/01   Date de fin:1999/05/02


 Nom granit                 Fini                      Ep.    M2 / Fini       Ventes $$
 -------------------------  ------------------------  ----  ------------  --------------
 011 CAMBRIEN               00 BRUT                     20         77.15        7,681.28
                                                        22        144.80       14,043.57
                                                       200         14.28        6,917.11
                                                            ------------  --------------
                                                                  236.23       28,641.97


                            01 BRULÉ                    22        218.79       23,504.02
                                                        32        170.95       18,740.71
                                                        40         24.48        3,989.53
                                                            ------------  --------------
                                                                  414.22       46,234.26


                            02 POLI                     20        166.01       18,437.43
                                                        21        196.39       19,194.00
                                                        22     19,447.42    1,985,902.92
                                                        31        235.48       32,152.94
                                                        32     13,167.33    1,581,741.10
                                                            ------------  --------------
                                                               33,212.63    3,637,428.40


                            03 POLI MAT                 22        547.60       54,012.17
                                                        32        765.00       79,793.86
                                                            ------------  --------------
                                                                1,312.60      133,806.03


                            04 POLI\BRULÉ               22          3.31          369.56
                                                            ------------  --------------
                                                                    3.31          369.56


                            05 JET DE SABLE             22        133.95       15,413.67
                                                            ------------  --------------
                                                                  133.95       15,413.67


                                                            ============  ==============
     TOTAL (CAMBRIEN)                                          35,312.93    3,861,893.89
                                                            ============  ==============

 _______________________________________________________________________________________


 090 VERT LAURENTIDE        02 POLI                     21        153.48       12,782.01
                                                        22        237.09       21,377.01
                                                        31        174.33       16,701.75
                                                        32        273.07       25,333.02
                                                            ------------  --------------
                                                                  837.97       76,193.79
                                                           GRANICOR INC.                                                  PAGE:    2
99/05/28 14:24 patlan                                Ventes par type de granit
                                         Date de début: 1998/11/01   Date de fin:1999/05/02


 Nom granit                 Fini                      Ep.    M2 / Fini       Ventes $$
 -------------------------  ------------------------  ----  ------------  --------------
                                                            ============  ==============
     TOTAL (VERT LAURENTIDE)                                      837.97       76,193.79
                                                            ============  ==============

 _______________________________________________________________________________________


                                                            ************  **************
     GRAND TOTAL                                               36,150.90    3,938,087.68
                                                            ************  **************
@ENDIF


BUILD pd320z
