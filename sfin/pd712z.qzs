;/T/ 2.4.4. Enregistrer le débitage - PROCÉDURE DE VALIDATION
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-03-31
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le débitage des pièce de granit.
;
;/M/ Programmeur..: Sébastien Chabot
;    Date Modif...: 18 Septembre 1996
;    Description..: Modification selon la demande du client (gra01-9311-305)
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *W-PD712M &

  LINK CIECLE OF W-PD712M,&
       DEBNUMRAP OF W-PD712M &
    TO CIECLE,&
       DEBNUMRAP OF PDDEBITAGE &

  LINK CIECLE OF W-PD712M,&
       DEBNUMRAP OF W-PD712M, &
       DDENUMLIG OF W-PD712M &
    TO CIECLE,&
       DEBNUMRAP, &
       DDENUMLIG OF PDDET_DEBITAGE



;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de validation - débitage"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD712"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


;
; Différence entre les deux dates
;
DEFINE DZ_DIFSECTOT_DEB   FLOAT SIZE 8 = DEBDURSECOPE OF PDDEBITAGE
DEFINE DZ_DIFSECTOT_ARRET FLOAT SIZE 8 = DEBDURSECARR OF PDDEBITAGE
DEFINE DZ_DIFSECTOT_OPER  FLOAT SIZE 8 = DEBDURSECOPE    OF PDDEBITAGE - &
                                         DEBDURSECARR OF PDDEBITAGE
;
; Différence de jour
;
DEFINE DZ_DIFJOU_DEB   FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT_DEB / (60 * 60 * 24))
DEFINE DZ_DIFJOU_ARRET FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT_ARRET / (60 * 60 * 24))
DEFINE DZ_DIFJOU_OPER  FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT_OPER / (60 * 60 * 24))
;
;
;
DEFINE DZ_DIFHRE_DEB   FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_DEB   - (DZ_DIFJOU_DEB   * 60 * 60 * 24)) / (60 * 60 ))
DEFINE DZ_DIFHRE_ARRET FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_ARRET - (DZ_DIFJOU_ARRET * 60 * 60 * 24)) / (60 * 60 ))
DEFINE DZ_DIFHRE_OPER  FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_OPER  - (DZ_DIFJOU_OPER  * 60 * 60 * 24)) / (60 * 60 ))
;
;
;
DEFINE DZ_DIFMIN_DEB   FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_DEB   - ( (DZ_DIFJOU_DEB   * 60 * 60 * 24) + (DZ_DIFHRE_DEB   * 60 * 60) ) ) / 60)
DEFINE DZ_DIFMIN_ARRET FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_ARRET - ( (DZ_DIFJOU_ARRET * 60 * 60 * 24) + (DZ_DIFHRE_ARRET * 60 * 60) ) ) / 60)
DEFINE DZ_DIFMIN_OPER  FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_OPER  - ( (DZ_DIFJOU_OPER  * 60 * 60 * 24) + (DZ_DIFHRE_OPER  * 60 * 60) ) ) / 60)
;
;
;
DEFINE DZ_DIFSEC_DEB   FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_DEB   - ((DZ_DIFJOU_DEB   * 60 * 60 * 24) + (DZ_DIFHRE_DEB   * 60 * 60) + (DZ_DIFMIN_DEB   * 60))))
DEFINE DZ_DIFSEC_ARRET FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_ARRET - ((DZ_DIFJOU_ARRET * 60 * 60 * 24) + (DZ_DIFHRE_ARRET * 60 * 60) + (DZ_DIFMIN_ARRET * 60))))
DEFINE DZ_DIFSEC_OPER  FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_OPER  - ((DZ_DIFJOU_OPER  * 60 * 60 * 24) + (DZ_DIFHRE_OPER  * 60 * 60) + (DZ_DIFMIN_OPER  * 60))))
;
;
;
DEFINE DZ_DIFTIM_DEB      CHARACTER SIZE 6 = ASCII(DZ_DIFHRE_DEB,2)   + ":" + ASCII(DZ_DIFMIN_DEB,2)
DEFINE DZ_DIFTIM_ARRET    CHARACTER SIZE 6 = ASCII(DZ_DIFHRE_ARRET,2) + ":" + ASCII(DZ_DIFMIN_ARRET,2)
DEFINE DZ_DIFTIM_OPER     CHARACTER SIZE 6 = ASCII(DZ_DIFHRE_OPER,2)  + ":" + ASCII(DZ_DIFMIN_OPER,2)
;



DEFINE D_TRI     CHARACTER SIZE 132 = ASCII(DEBNUMRAP OF W-PD712M,10) + &
                                      ASCII(DDENUMLIG OF W-PD712M,03) + &
                                      T_MESSAGE OF W-PD712M


DEFINE D_ERREUR  CHARACTER SIZE 100 = &
            "le numéro d'opérateur est invalide"                                            IF T_ERREUR_1 = "1" &
       ELSE "Le rapport des opérateurs n'est pas complêt"                                   IF T_ERREUR_2 = "1" &
       ELSE "Le code de machine n'existe pas "                                              IF T_ERREUR_3 = "1" &
       ELSE "L'heure de début d'opérateur n'est pas compris dans l'intervalle du rapport"   IF T_ERREUR_4 = "1" &
       ELSE "de début d'opérateur n'est pas compris dans l'intervalle du rapport"           IF T_ERREUR_5 = "1" &
       ELSE "L'heure de début d'arrêt n'est pas compris dans l'intervalle du rapport "      IF T_ERREUR_6 = "1" &
       ELSE "L'heure de fin d'arrêt n'est pas compris dans l'intervalle du rapport "        IF T_ERREUR_7 = "1" &
       ELSE "Raison d'arret existe pas "                                                    IF T_ERREUR_8 = "1" &
       ELSE " "                                                                             IF T_ERREUR_9 = "1" &
       ELSE " "


SORT ON DEBNUMRAP        OF W-PD712M &
     ON DDENUMLIG        OF W-PD712M &
     ON D_TRI


USE ustitstd45 NOLIST
SKIP


HEADING AT DEBNUMRAP &
  SKIP PAGE &
  TAB 001 "Num. rapport..:" &
  TAB 017 DEBNUMRAP OF W-PD712M PICTURE "^^^^^^^" &
  TAB 028  "Code machine..:" &
  TAB 044 MDECODMAC OF PDDEBITAGE &
  TAB 055 "Date product..:" &
  TAB 071 DEBDATPRD OF PDDEBITAGE &
  TAB 082 "Quart travail.:" &
  TAB 098 QRTCODQRT OF PDDEBITAGE &
  SKIP &
  TAB 001 "Hre opération.:" &
  TAB 017 DZ_DIFTIM_DEB     &
  TAB 028 "Hre arrêt.....:" &
  TAB 044 DZ_DIFTIM_ARRET   &
  TAB 055 "Hre total.....:" &
  TAB 071 DZ_DIFTIM_OPER    &
  SKIP 2 &
  TAB 001 "Lig Tranche Diagr.  pr Long Haut Épai Gra Fi   Qte Message" & ; <-- SEBCHA960918
  SKIP


FOOTING AT D_TRI &
  TAB 001 DDENUMLIG        OF W-PD712M &
  TAB 005 TRANUMTRA002     OF PDDET_DEBITAGE  &
  TAB 013 DIANUM002        OF PDDET_DEBITAGE  &
  TAB 021 DDECODPRI        OF PDDET_DEBITAGE  &            ; <-- SEBCHA960918
  TAB 024 DDELON           OF PDDET_DEBITAGE  &
  TAB 029 DDEHAU           OF PDDET_DEBITAGE  &
  TAB 034 DDEEPA           OF PDDET_DEBITAGE  &
  TAB 039 TGRCODGRA        OF PDDET_DEBITAGE  &
  TAB 043 TYFCODFIN        OF PDDET_DEBITAGE  &
  TAB 046 DDEQTEFAB        OF PDDET_DEBITAGE  &
  TAB 052 T_MESSAGE        OF W-PD712M SKIP 1


FOOTING AT DEBNUMRAP &
  SKIP 2 &
  TAB 001 D_ERREUR

BUILD pd712z

@IF FORMAT

PD712                                         GRANICOR INC.                                                               PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX                  Liste de validation - opération de surface

Num. rapport..: 9999999    Code machine..: xxxx

Lig Tranche Diagr.  Long Haut Épai Gra Ty   Qte Message
xxx xxxxxxx xxxxxxx 9999 9999 9999 xxx xx 12345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx xxxxxxx xxxxxxx 9999 9999 9999 xxx xx 12345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx xxxxxxx xxxxxxx 9999 9999 9999 xxx xx 12345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
@ENDIF
