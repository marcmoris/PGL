;/T/ Adresses - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 7 septembre 1993
;
;    Description..: Cet écran vérifie si l'adresse reçu en paramètre est
;                   référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp001dx NOMODE &
               ACTION LABEL " " &
               FROM 23,70 TO 23,80 &
               RECEIVING MCREF_ADRESSE , &
                         T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Adresse supplémentaire.
;
FILE MCREF_ADRESSE    MASTER

;
; Comptes à payer.
;
FILE CPCPT_A_PAYER    REFERENCE

;
; Paiements.
;
FILE CPPAIEMENT       REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ;
  GET CPCPT_A_PAYER VIA REFCIECLE , &
                        REFCLETYP , &
                        REFCLENUM , &
                        RADCLE      &
                    USING REFCIECLE OF MCREF_ADRESSE , &
                          REFCLETYP OF MCREF_ADRESSE , &
                          REFCLENUM OF MCREF_ADRESSE , &
                          RADCLE    OF MCREF_ADRESSE   &
                    OPTIONAL
  IF ACCESSOK
  THEN ERROR 477 ; Destruction annulée, l'adresse est référée par un compte
                 ; à payer.
  ;
  GET CPPAIEMENT VIA REFCIECLE , &
                     REFCLETYP , &
                     REFCLENUM , &
                     RADCLE      &
                 USING REFCIECLE OF MCREF_ADRESSE , &
                       REFCLETYP OF MCREF_ADRESSE , &
                       REFCLENUM OF MCREF_ADRESSE , &
                       RADCLE    OF MCREF_ADRESSE   &
                 OPTIONAL
  IF ACCESSOK
  THEN ERROR 575 ; Destruction annulée, l'adresse est référée par un paiement.
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
