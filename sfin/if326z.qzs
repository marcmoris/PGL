;/P/ Impression de la facture
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
USE ussetqzs NOLIST

ACCESS IFNOT_CRE_MAITRE &
  LINK CIECLE OF IFNOT_CRE_MAITRE, &
       FCECLE OF IFNOT_CRE_MAITRE  &
    TO CIECLE, &
       FCECLE                           OF IFFACTURE &
  LINK CIECLE OF IFNOT_CRE_MAITRE, &
       CLTCLE OF IFFACTURE, &
       CRRCLENUM OF IFFACTURE, &
       CRRCLEANN OF IFFACTURE, &
       FCECAT OF IFFACTURE, &
       COEDATDEB OF IFFACTURE &
    TO CIECLE, &
       CLTCLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       CATCLE, &
       COEDATDEB                        OF IFCONTRAT_LIGNE &
  LINK CIECLE OF IFFACTURE, &
       CLTCLE OF IFFACTURE  &
    TO CIECLE, &
       CLTCLE                           OF IFCLIENT &
  LINK CLICIECLE OF IFCLIENT, &
       CLICLE OF IFCLIENT &
    TO CLICIECLE, &
       CLICLE                           OF CRCLIENT &
  LINK ( CLICIECLE OF IFCLIENT, &
         CLICLE OF IFCLIENT, &
         CLIREFTYP OF CRCLIENT, &
         1 ) &
    TO REFCIECLE, &
       REFCLENUM, &
       REFCLETYP, &
       RADCLE                           OF MCREF_ADRESSE &
  LINK CLICIECLE OF IFCLIENT, &
       CLICLE OF IFCLIENT, &
       CIECLE OF IFCLIENT &
    TO CLICIECLE, &
       CLICLE, &
       CIECLE                           OF CRCLI_CIE OPTIONAL &
  LINK CLCTAXTYPTPS OF CRCLI_CIE, &
       CLCTAXCODTPS OF CRCLI_CIE  &
    TO TAXCLETYP, &
       TAXCLECOD                        OF VTAX_DSC ALIAS A_TAXFED OPTIONAL &
  LINK CLCTAXTYPTVP OF CRCLI_CIE, &
       CLCTAXCODTVP OF CRCLI_CIE  &
    TO TAXCLETYP, &
       TAXCLECOD                        OF VTAX_DSC ALIAS A_TAXPRO OPTIONAL &
  LINK CIECLE OF IFFACTURE, &
       CLTCLE OF IFFACTURE, &
       CRRCLENUM OF IFFACTURE, &
       CRRCLEANN OF IFFACTURE  &
    TO CIECLE, &
       CLTCLE, &
       CRRCLENUM, &
       CRRCLEANN                        OF IFCONTRAT &
  LINK CIECLE OF IFNOT_CRE_MAITRE &
    TO CIECLE                           OF MCCOMPAGNIE

CHOOSE CIECLE PARM, CRECLE PARM

DEFINE D_FLGIMP CHARACTER SIZE 1 = PARM

SELECT IF     (NCMFIN = "O" OR NCMANNUL = "O") &
          AND (    ( NCMIMP <> "O" AND D_FLGIMP = "0" ) &
                OR ( NCMIMP = "O" AND D_FLGIMP = "1" ) &
              )

DEF TMP-SOUS-TOTAL  = NCMFACTOT + NCMMNTTPSFED OF IFNOT_CRE_MAITRE
DEF TMP-GRAND-TOTAL = TMP-SOUS-TOTAL + NCMMNTTVQPRO OF IFNOT_CRE_MAITRE
DEF TMP-LABEL-TPS CHARACTER SIZE 5 = "TPS "
DEF TMP-LABEL-TVQ CHARACTER SIZE 5 = "TVQ "
DEF TMP-LABEL-FIN CHARACTER SIZE 30 = "TOTAL DE CETTE NOTE DE CREDIT" &
    IF CLILNG = "F" ELSE "TOTAL FOR THIS CREDIT NOTE"
DEF TMP-LABEL-SOUS CHARACTER SIZE 15 = "SOUS TOTAL " &
                IF CLILNG = "F" ELSE "SUBTOTAL"


DEF TMP-LABEL-1  CHARACTER SIZE 28 = "NOTE DE CREDIT SUR FACTURE #" &
                  IF  CLILNG = "F"  ELSE  &
                                     "CREDIT NOTE ON INVOICE     #"
DEF TMP-LABEL-2  CHARACTER SIZE 13 =       "COMMENTAIRES:"    &
                  IF CLILNG = "F" ELSE "COMMENTS    :"


SORT ON CLTCLE OF IFFACTURE &
     ON CRRCLENUM OF IFFACTURE &
     ON CRRCLEANN OF IFFACTURE &
     ON FCECAT OF IFFACTURE &
     ON COEDATDEB OF IFFACTURE

PAGE HEADING TAB 3 "." & ; -- IMP -- FONT-LQ   &
SKIP 9  TAB 72 CRECLE      &
SKIP 1 &
        TAB 10 CLINOM     &
SKIP 1 &
        TAB 10 RADADR1 &
        TAB 51 FCEADR1    &
SKIP 1 &
        TAB 10 RADADR2 &
        TAB 51 FCEADR2    &
SKIP 1 &
        TAB 10 RADADR3 &
        TAB 51 FCEADR3    &
SKIP 1 &
        TAB 10 RADCP &
        TAB 51 FCECP  &
SKIP 3  &
        TAB 11 CONCONDITION1 &
SKIP 1 &
        TAB 11 CONCONDITION2 &
SKIP 2 &
        TAB 62 NCMDAT &
SKIP 1 &
        TAB 10 COEREFCLI  &
        TAB 26 "PROV:" CLCEXPTVP &
SKIP 1 &
        TAB 02 "NUMERO INSCRIPTION TPS: " CIEENRTPS &
SKIP 12
REPORT  &
        TAB 12 TMP-LABEL-1     &
        TAB 40 FCECLE           &
SKIP 3 &
        TAB 12 TMP-LABEL-2 &
SKIP 2 &
        TAB 12 NCMCOM1 &
SKIP 1 &
        TAB 12 NCMCOM2 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 40 TMP-LABEL-SOUS &
        TAB 69 NCMFACTOT  FLOAT "$" PIC "^^^^,^^^.^^"      &
SKIP 1 &
        TAB 40 TMP-LABEL-TPS &
        TAB 45 TAXPCT OF A_TAXFED PIC "^^^.^^%"  &
        TAB 69 NCMMNTTPSFED OF IFNOT_CRE_MAITRE FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 69 "___________" &
SKIP 1&
        TAB 69 TMP-SOUS-TOTAL FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1&
        TAB 40 TMP-LABEL-TVQ &
        TAB 45 TAXPCT OF A_TAXPRO PIC "^^^.^^%"  &
        TAB 69 NCMMNTTVQPRO OF IFNOT_CRE_MAITRE FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 40 TMP-LABEL-FIN &
        TAB 69 TMP-GRAND-TOTAL FLOAT "$" PIC "^^^^,^^^.^^"

BUILD if326z
