;/T/ 1.4.5.3 P/O de transfert et de sous-contractants avec réception à venir
;            par commande interne.
;
;/P/ Programmeur...: René Bonneau
;    Date Création.: 1994-10-18
;
;    Description...: Liste de base des bons de commande de transfert et de
;                    sous-contractants avec réception à venir par commande
;                    interne.
;
;    Paramètres....: Numéro de commande interne = COMNUMCOMINT
;
;    Particularités: AUCUNE
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS PDBON_COMMANDE &
  LINK FOUCLE          OF PDBON_COMMANDE,  &
       FOCCIECLE       OF PDBON_COMMANDE   &
    TO FOUCLE,                             &
       FOCCIECLE                           OF VFOC_FOU         OPTIONAL &
  LINK FOUCIECLE       OF VFOC_FOU,        &
       FOUREFTYP       OF VFOC_FOU,        &
       FOUCLE          OF VFOC_FOU,        &
       FOCADRACH       OF VFOC_FOU         &
    TO REFCIECLE,                          &
       REFCLETYP,                          &
       REFCLENUM,                          &
       RADCLE                              OF MCREF_ADRESSE        OPT &

   LINK CIECLE    OF PDBON_COMMANDE ,&
        BCMNUMBCM OF PDBON_COMMANDE &
   TO   CIECLE,&
        BCMNUMBCM OF PDPROD_BON_CMD OPT &
   LINK CIECLE    OF PDPROD_BON_CMD ,&
        DIANUM002 OF PDPROD_BON_CMD &
   TO   CIECLE,&
        DIANUM002 OF PDDIAGRAMME OPT




CHOOSE CIECLE PARM , COMNUMCOM PARM




DEFINE D_BCMCODTYPBCM CHARACTER SIZE 15 = &
                      "Transfert"      IF BCMCODTYPBCM = "T" ELSE &
                      "Sous-traitance" IF BCMCODTYPBCM = "S"

DEFINE SURCMDDBT FLOAT SIZE 8 = DIASURMC2 OF PDDIAGRAMME * PBCQTECMDDBTSCT OF PDPROD_BON_CMD
DEFINE SURRCUDBT FLOAT SIZE 8 = DIASURMC2 OF PDDIAGRAMME * PBCQTERCUDBTSCT OF PDPROD_BON_CMD
DEFINE SURCMDFIN FLOAT SIZE 8 = DIASURMC2 OF PDDIAGRAMME * PBCQTECMDFINSCT OF PDPROD_BON_CMD
DEFINE SURRCUFIN FLOAT SIZE 8 = DIASURMC2 OF PDDIAGRAMME * PBCQTERCUFINSCT OF PDPROD_BON_CMD


SELECT IF PBCQTECMDDBTSCT OF PDPROD_BON_CMD > PBCQTERCUDBTSCT OF PDPROD_BON_CMD


SORT ON COMNUMCOM OF PDBON_COMMANDE &
     ON BCMNUMBCM OF PDBON_COMMANDE



;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "P/O de transfert et sous-contractants par commande interne"
@ELSE
       "%%%%P/O de transfert et sous-contractants/commande interne"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD303Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
SKIP 2 &
    TAB 001 "No de P/O.......:" &
    TAB 018 BCMNUMBCM    OF PDBON_COMMANDE     &
    TAB 060 "Type de P/O.....:" &
    TAB 078 D_BCMCODTYPBCM                     &
SKIP &
    TAB 001 "Com. int........:" &
    TAB 018 COMNUMCOM OF PDBON_COMMANDE &
    TAB 060 "No four.........:" &
    TAB 078 FOUCLE       OF PDBON_COMMANDE     &
SKIP &
    TAB 001 "Nom fournisseur.:" &
    TAB 018 FOUNOM       OF VFOC_FOU           &
SKIP &
    TAB 001 "Téléphone.......:" &
    TAB 018 RADTEL       OF MCREF_ADRESSE      &
    TAB 060 "FAX.............:" &
    TAB 078 RADFAX       OF MCREF_ADRESSE      &
SKIP 2 &
   tab 001 "                    *---Debitage---*    *--- Finition---*  *---Debitage---*  *--- Finition---*" &
SKIP &
   TAB 001 "Diagramme    Prio.  cmd/pce   rcu/pce   cmd/pce   rcu/pce   cmd/m2   rcu/m2   cmd/m2   rcu/m2" &
SKIP 2


REPORT &
TAB 001 DIANUM002 OF PDPROD_BON_CMD &
TAB 015 PRICODPRI OF PDPROD_BON_CMD &
TAB 020 PBCQTECMDDBTSCT OF PDPROD_BON_CMD &
TAB 030 PBCQTERCUDBTSCT OF PDPROD_BON_CMD &
TAB 040 PBCQTECMDFINSCT OF PDPROD_BON_CMD &
TAB 050 PBCQTERCUFINSCT OF PDPROD_BON_CMD &
TAB 060 SURCMDDBT PICTURE "^^,^^^.^^" OUTPUT SCALE 2 &
TAB 070 SURRCUDBT PICTURE "^^,^^^.^^" OUTPUT SCALE 2 &
TAB 080 SURCMDFIN PICTURE "^^,^^^.^^" OUTPUT SCALE 2 &
TAB 090 SURRCUFIN PICTURE "^^,^^^.^^" OUTPUT SCALE 2 &
SKIP

FOOTING AT BCMNUMBCM &
SKIP PAGE

FOOTING AT COMNUMCOM &
SKIP PAGE

BUILD pd303z


