;/T/ Dépisteur ligne de commande interne
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1996-09-17
;
;    Description..: Permet de visionner la ligne de commande interne du
;                   diagramme.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd138 NOMODE NOACTION FIELDMARK &
             FROM 11,27 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_PJTABR,          &
                       T_PJTANN,          &
                       T_COMNUMCOMINT,    &
                       T_DCINUMLIG,       &
                       T_CIECLEMNU


TEMPORARY T_PJTABR           CHARACTER SIZE 02
TEMPORARY T_PJTANN           CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT     CHARACTER SIZE 03
TEMPORARY T_DCINUMLIG        CHARACTER SIZE 03
TEMPORARY T_CIECLEMNU        CHARACTER SIZE 04


USE pd138.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Détail commande interne
;

FILE PDDETAIL_C_I PRIMARY OCCURS 9

  ACCESS VIA     PJTABR,            &
                 PJTANN,            &
                 COMNUMCOMINT,      &
                 DCINUMLIG,         &
                 CIECLE             &
         USING   T_PJTABR,          &
                 T_PJTANN,          &
                 T_COMNUMCOMINT,    &
                 T_DCINUMLIG,       &
                 T_CIECLEMNU        &
         ORDERBY CIECLE,            &
                 PJTABR,            &
                 PJTANN,            &
                 DCINUMLIG

  SELECT IF TPDTYPPRD OF PDDETAIL_C_I = "DI"


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,54       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "    lig gra fi Épa.prix vente u/m Commentaire" &
@ELSE
      "%%% lig gra fi Épa.prix vente u/m Commentaire" &
@ENDIF
    AT ,2

SKIP TO LINE 3

ALIGN(3,3,6) (,,10) (,,14) (,,17) (,,22) (,,31) (,,34)


CLUSTER OCCURS WITH PDDETAIL_C_I


FIELD DCINUMLIG      OF PDDETAIL_C_I     &
        DISPLAY                          &
        HIDDEN LABEL ">>" DISPLAY


FIELD TGRCODGRA      OF PDDETAIL_C_I     &
        DISPLAY                          &
        NOLABEL


FIELD TYFCODFIN      OF PDDETAIL_C_I     &
        DISPLAY                          &
        NOLABEL


FIELD DCIEPA         OF PDDETAIL_C_I     &
        DISPLAY                          &
        NOLABEL


FIELD DCIPRIVEN        OF PDDETAIL_C_I     &
        DISPLAY                            &
        NOLABEL                            &
        PICTURE "^,^^^.^^"                 &
        SIGNIFICANCE 4


FIELD DCIUNMVEN        OF PDDETAIL_C_I     &
        DISPLAY                            &
        NOLABEL

FIELD DCICOM OF PDDETAIL_C_I &
      DISPLAY                &
      NOLABEL                &
      FOR ,18

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_DCINUMLIG , "@")
  THEN LET T_DCINUMLIG = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_DCINUMLIG = DCINUMLIG OF PDDETAIL_C_I
  RETURN
END

BUILD

@IF FORMAT

                          ******************************************************
                          *    Commande  lig gra fi Épa.      prix vente u/m   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          * >> xx-xx-xxx xxx xxx xx xxxx xxxxxxxxxxxxxxxx xx   *
                          *                                                    *
                          ******************************************************
@ENDIF
