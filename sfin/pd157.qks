;/T/ Scie (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-13
;
;    Description..: Cette table contient les informations de base des scies.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd157 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD157"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd157.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES     PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 SCINUMSCI &
         USING   T_CIECLEMNU,         &
                 SCINUMSCI OF PDSCIES &
         REQUEST SCINUMSCI OF PDSCIES &
         ORDERBY CIECLE,   &
                 SCINUMSCI

  ACCESS VIA     CIECLE,   &
                 SCINUMSCI &
         USING   T_CIECLEMNU,                &
                 SCINUMSCI        OF PDSCIES &
         REQUEST SCISEQTRI        OF PDSCIES &
         ORDERBY CIECLE,   &
                 SCISEQTRI

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 SCINUMSCI

          ITEM CIECLE OF PDSCIES INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Scies - type chariot
;

FILE PDSCIES_CHARIOT DETAIL &
                     OCCURS 7 &
                     NOITEM

  ACCESS VIA     CIECLE,   &
                 SCINUMSCI &
         USING   T_CIECLEMNU,         &
                 SCINUMSCI OF PDSCIES &
         ORDERBY CIECLE,    &
                 SCINUMSCI, &
                 TYCCODCHA

  ITEM SCINUMSCI OF PDSCIES_CHARIOT INITIAL SCINUMSCI OF PDSCIES
  ITEM CIECLE OF PDSCIES_CHARIOT INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Type de chariot
;

FILE PDTYPE_CHARIOT REFERENCE OCCURS WITH PDSCIES_CHARIOT

  ACCESS VIA     CIECLE,   &
                 TYCCODCHA &
         USING   T_CIECLEMNU,                 &
                 TYCCODCHA OF PDSCIES_CHARIOT &
         ORDERBY CIECLE,   &
                 TYCCODCHA


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
TEMPORARY T_SCINUMSCI CHARACTER SIZE 04
TEMPORARY T_TYCCODCHA CHARACTER SIZE 04



;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Scies " &
@ELSE
      " %%%Scies " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19) (42,43,59)


FIELD SCINUMSCI OF PDSCIES                      &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE                       &
        LOOKUP NOTON PDSCIES                    &
          VIA   CIECLE,                         &
                SCINUMSCI                       &
          USING T_CIECLEMNU,                    &
                SCINUMSCI OF PDSCIES            &
          MESSAGE 2908 ; Ce code de scie existe déjà


FIELD SCISEQTRI        OF PDSCIES          &
        HIDDEN ID 07                       &
        REQUIRED                           &
        LOOKUP NOTON PDSCIES               &
          VIA   CIECLE,                    &
                SCISEQTRI                  &
          USING T_CIECLEMNU,               &
                SCISEQTRI OF PDSCIES       &
          MESSAGE 2912 ; Cette séquence de tri existe déjà

SKIP 1


FIELD SCIDSC001 OF PDSCIES                 &
        HIDDEN ID 10                       &
        REQUIRED


FIELD SCIDSC002 OF PDSCIES                 &
        ID SAME                            &
        NOLABEL


FIELD SCIDSC003 OF PDSCIES                 &
        ID SAME                            &
        NOLABEL


DRAW 11,2 TO 11,79

SKIP TO LINE 11

TITLE &
@IF FRANCAIS
      " Type de chariot " &
@ELSE
      " %%%Type de chariot " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
      "Code   Description " &
@ELSE
      "%%%Code   Description " &
@ENDIF
      AT ,17


ALIGN (15,16,17) (,,24)


CLUSTER OCCURS WITH PDSCIES_CHARIOT ID BASE 15


FIELD TYCCODCHA OF PDSCIES_CHARIOT                &
        HIDDEN                                    &
        LABEL " "                                 &
        LOOKUP                                    &
          ON PDTYPE_CHARIOT                       &
            VIA     CIECLE,                       &
                    TYCCODCHA                     &
            USING   T_CIECLEMNU,                  &
                    TYCCODCHA OF PDSCIES_CHARIOT  &
            MESSAGE 2969, &  ; Ce type de chariot n'esiste pas
          NOTON PDSCIES_CHARIOT                   &
            VIA     CIECLE,                       &
                    SCINUMSCI,                    &
                    TYCCODCHA                     &
            USING   T_CIECLEMNU,                  &
                    SCINUMSCI OF PDSCIES_CHARIOT, &
                    TYCCODCHA OF PDSCIES_CHARIOT  &
            MESSAGE 2972 ; Cette combinaison existe déjà


FIELD TYCDSCFRA        OF PDTYPE_CHARIOT   &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs
;

PROCEDURE INPUT SCINUMSCI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_SCINUMSCI = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd103 MODE F &
                     PASSING T_SCINUMSCI,&
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_SCINUMSCI )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs
;

PROCEDURE INPUT TYCCODCHA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYCCODCHA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd120 MODE F &
                     PASSING T_TYCCODCHA,&
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYCCODCHA )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDSCIES &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDSCIES &
     AND NOT NEWRECORD     OF PDSCIES &
     AND NOT DELETEDRECORD OF PDSCIES &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
************************************ Scies *************************************
*                                                                              *
* Numéro scie...: xx                      Séquence tri..: xx                   *
*                                                                              *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
******************************* Type de chariot ********************************
*                                                                              *
*               Code   Description                                             *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*               xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
