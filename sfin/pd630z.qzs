;/T/ Liste des diagrammes à finir par commande interne
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-11
;
;    Description...: Ce rapport permet d'imprimer la liste des diagrammes à
;                    finir pour une commande interne.
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDPRIORITE_DIAG                  &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       COMNUMCOM    OF PDPRIORITE_DIAG, &
       PRICODPRI    OF PDPRIORITE_DIAG  &
    TO CIECLE,                          &
       COMNUMCOM,                       &
       PRICODPRI    OF PDPRIORITE       &


  LINK CIECLE       OF PDPRIORITE_DIAG, &
       DIANUM002    OF PDPRIORITE_DIAG  &
    TO CIECLE,                          &
       DIANUM002    OF PDDIAGRAMME      &
  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE &


  LINK CIECLE       OF PDPRIORITE_DIAG, &
       COMNUMCOM    OF PDPRIORITE_DIAG  &
    TO CIECLE,                          &
       COMNUMCOM    OF PDCOMMAN_INTERNE &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       DIANUM002    OF PDDIAGRAMME      &
    TO CIECLE,                          &
       DIANUM002    OF PDUNI_TRAV_DIA   OPTIONAL &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       UTRNUMUNITRV OF PDUNI_TRAV_DIA   &
    TO CIECLE,                          &
       UTRNUMUNITRV OF PDUNITE_TRAVAIL  &

  LINK CIECLE       OF PDPRIORITE_DIAG, &
       UTRCODUNMPRD OF PDUNITE_TRAVAIL  &
    TO CIECLE,                          &
       UNMCODUNM    OF PDUNITE_MESURE


CHOOSE CIECLE    PARM , &
       COMNUMCOM PARM , &
       DIANUM002 PARM , &
       PRICODPRI PARM , &
       DCINUMLIG PARM


SELECT PDCOMMAN_INTERNE IF COMSTU OF PDCOMMAN_INTERNE = "P" &
                           OR &
                           COMSTU OF PDCOMMAN_INTERNE = "C"

SELECT IF (DIAQTECMD OF PDDIAGRAMME > DIAQTEEMB OF PDDIAGRAMME) AND &
           0 <> DIAQTEPLADBTGRA OF VDIAQTE


DEFINE D_TRI CHARACTER SIZE 12 = COMNUMCOM OF PDPRIORITE_DIAG + &
                                 PRICODPRI    OF PDPRIORITE_DIAG + &
                                 UTRNUMUNITRV OF PDUNI_TRAV_DIA


SORT ON COMNUMCOM OF PDPRIORITE_DIAG &
     ON D_TRI &
     ON DIANUM002 OF PDPRIORITE_DIAG


;-------------------------------------------------------------------------------
;
; Définition de variable nécessaire au rapport
;

DEFINE D_ETOILE      CHARACTER SIZE 01 = "*" &
                                           IF DIATYPDIA OF PDDIAGRAMME = "T" &
                                           ELSE " "

DEFINE D_QTE_PIE_PRD   INTEGER SIZE 04 = DIAQTEPLAFINGRA OF VDIAQTE - DIAQTEFINGRA OF VDIAQTE

DEFINE D_QTE_UTR_PRD     FLOAT SIZE 08 = UTDQTEPRD OF PDUNI_TRAV_DIA - UTDQTEUTRPRD OF PDUNI_TRAV_DIA &
                                           IF 0 < (UTDQTEPRD OF PDUNI_TRAV_DIA - UTDQTEUTRPRD OF PDUNI_TRAV_DIA) &
                                           ELSE 0

DEFINE D_QTE_UTR_PRD_2   FLOAT SIZE 08 = (DIAQTECMD OF PDDIAGRAMME - DIAQTEEMB OF PDDIAGRAMME) * UTDQTEPRD OF PDUNI_TRAV_DIA &
                                           IF 0 < (DIAQTECMD OF PDDIAGRAMME - DIAQTEEMB OF PDDIAGRAMME) * UTDQTEPRD OF PDUNI_TRAV_DIA &
                                           ELSE 0.00

DEFINE D_DIFF FLOAT SIZE 8 = (DIAQTEPLAFINGRA OF VDIAQTE - DIAQTEEMB OF PDDIAGRAMME) * UTDQTEPRD OF PDUNI_TRAV_DIA

DEFINE D_QTE_UTR_A_FINIR FLOAT SIZE 08 = D_QTE_PIE_PRD * UTDQTEPRD OF PDUNI_TRAV_DIA &
                                           IF (DIAQTEFINGRA OF VDIAQTE > DIAQTEEMB OF PDDIAGRAMME) &
                                           ELSE D_DIFF
;DEFINE D_QTE_UTR_A_FINIR FLOAT SIZE 08 = D_QTE_UTR_PRD &
;                                           IF D_QTE_UTR_PRD < D_QTE_UTR_PRD_2 &
;                                           ELSE D_QTE_UTR_PRD_2

DEFINE D_UTDQTEPRD FLOAT SIZE 8 = DIAQTEPLADBTGRA OF VDIAQTE * UTDQTEPRD OF PDUNI_TRAV_DIA
;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste des diagrammes à finir par commande interne"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD630Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitstd45 NOLIST
  SKIP


HEADING AT COMNUMCOM &
  SKIP PAGE


HEADING AT D_TRI &
  SKIP 3 &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDPRIORITE_DIAG &
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDPRIORITE_DIAG &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 001 "Priorité......:" &
  TAB 017 PRICODPRI OF PDPRIORITE_DIAG &
  TAB 021 PRIDATFIN OF PDPRIORITE &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 001 "Unité travail.:" &
  TAB 017 UTRNUMUNITRV  OF PDUNI_TRAV_DIA &
  TAB 021 UTRDSC001     OF PDUNITE_TRAVAIL &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP &
  TAB 001 "U/M production:" &
  TAB 017 UTRCODUNMPRD OF PDUNITE_TRAVAIL &
  TAB 021 UNMDSCFRA OF PDUNITE_MESURE &
  SKIP 2 &
  TAB 001 "          *- Qte pièce *  *--- Qte unité/travail ---*" &
  SKIP &
  TAB 001 "Diag.  T    Cmd À finir            Cmd        À finir   Reste à faire                                          Gra   Fi   Épai" &
  SKIP 2


REPORT &
  TAB 001 DIANUMDIA002 OF PDDIAGRAMME &
  TAB 008 D_ETOILE &
  TAB 011 DIAQTEPLADBTGRA OF VDIAQTE &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 019 D_QTE_PIE_PRD PICTURE "^^^^^" &
  TAB 027 D_UTDQTEPRD PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 042 D_QTE_UTR_A_FINIR PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 057 "____________________________________________________" &
  TAB 112 TGRCODGRA OF PDDIAGRAMME &
  TAB 118 TYFCODFIN OF PDDIAGRAMME &
  TAB 123 DIAEPA OF PDDIAGRAMME &
  SKIP 2

FOOTING AT D_TRI &
  TAB 001 "Total: " &
  TAB 011 DIAQTEPLADBTGRA OF VDIAQTE     SUB RESET AT D_TRI &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 019 D_QTE_PIE_PRD                  SUB RESET AT D_TRI PICTURE "^^^^^" &
  TAB 027 D_UTDQTEPRD                 SUB RESET AT D_TRI PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 042 D_QTE_UTR_A_FINIR              SUB RESET AT D_TRI PICTURE "^^^,^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
SKIP PAGE

BUILD pd630z

@IF FORMAT

PD630Z                                                      GRANICOR INC                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX              Liste des diagrammes à finir par commande interne

Num. commande.: XX-999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
Priorité......: 99  AA/MM/JJ                                          Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
Unité travail.: XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX          Critères esthé:
U/M production: XX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

          *- Qte pièce *  *--- Qte unité/travail ---*
Diag.  T    Cmd À finir            Cmd        À finir   Reste à faire                                          Gra   Fi   Épai

XXXXX  *   99999  99999   9999999.9999   9999999.9999   ____________________________________________________   999   99     99

XXXXX  *   99999  99999   9999999.9999   9999999.9999   ____________________________________________________   999   99     99

XXXXX  *   99999  99999   9999999.9999   9999999.9999   ____________________________________________________   999   99     99

XXXXX  *   99999  99999   9999999.9999   9999999.9999   ____________________________________________________   999   99     99

XXXXX  *   99999  99999   9999999.9999   9999999.9999   ____________________________________________________   999   99     99

@ENDIF
