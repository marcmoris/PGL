;/T/ Blocs appartenant a un ligne de commande
;
;/P/ Programmeur..: Bernard Samson
;    Date.........: 1999-10-15
;    Description..: Ecran qui sert a consulter les blocs attache a une ligne
;                   de commande
;
;-------------------------------------------------------------------------------

SCREEN pd210p ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              AUTOUPDATE                    &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        T_CLICLE,           &
                        PDDETAIL_C_I 

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD210P"

;-------------------------------------------------------------------------------
;
; Panorama d'aide
;
;USE pd210p.hlp NOLIST

;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;

USE ustrasse NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Carte des blocs             " ACTION DESIGNER D001 MARK
ACTIONMENU LABEL "Procédures"
  MENUITEM LABEL "Dé-assignation bloc" ACTION DESIGNER P001 MARK
@ENDIF

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_CLICLE    CHARACTER SIZE 06 RESET AT STARTUP ; No client

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Commande interne
;
FILE PDDETAIL_C_I MASTER

;-------------------------------------------------------------------------------
;
; Détail de la commande
;
FILE IFBLOC PRIMARY OCCURS 14  NODELETE NOITEM

  ACCESS VIA     CIECLE,                           &
                 PJTABR,                           &
                 PJTANN,                           &
                 COMNUMCOMINT,                     &
                 DCINUMLIG                         &
         USING   T_CIECLEMNU,                      &
                 PJTABR           OF PDDETAIL_C_I, &
                 PJTANN           OF PDDETAIL_C_I, &
                 COMNUMCOMINT     OF PDDETAIL_C_I, &
                 DCINUMLIG        OF PDDETAIL_C_I  & 
         ORDERBY CIECLE,                           &
                 TGRCODGRA,                        &
                 IFCCLE,                           &
                 BLCCLE,                           &
                 BLCRED                        

;-----------------------------------------------------------------------------
;
; Commande
;
FILE PDCOMMAN_INTERNE REFERENCE

ACCESS VIA     CIECLE,                           &
               PJTABR,                           &
               PJTANN,                           &
               COMNUMCOMINT                      &
       USING   T_CIECLEMNU,                      &
               PJTABR           OF PDDETAIL_C_I, &
               PJTANN           OF PDDETAIL_C_I, &
               COMNUMCOMINT     OF PDDETAIL_C_I 


;-------------------------------------------------------------------------------
;
;Variable servant à l'affichage
;
DEFINE D_COUTBL FLOAT SIZE 8 = &
     ((BLCMNTM3 OF IFBLOC + BLCMNTEXPM3 OF IFBLOC) &
      * BLCM3TOT OF IFBLOC) / 100

;------------------------------------------------------------------------------
;
; Variables servant a appelle if001
;
TEMPORARY T_TGRCODGRA_ORI    CHARACTER SIZE 3
TEMPORARY T_IFCCLE_ORI       CHARACTER SIZE 2
TEMPORARY T_BLCCLE_ORI       CHARACTER SIZE 6
TEMPORARY T_BLCRED_ORI       CHARACTER SIZE 1     

TEMPORARY T_ECRAPP   CHARACTER SIZE 5  RESET AT STARTUP ; ecran apellant                                
;-------------------------------------------------------------------------------

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
      " Blocs assignés " &
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4

ALIGN (,3,19) (,21,22) (,24,25) (,35,51)

FIELD PJTABR           OF PDDETAIL_C_I     &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD PJTANN           OF PDDETAIL_C_I     &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD COMNUMCOMINT     OF PDDETAIL_C_I     &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED

FIELD DCINUMLIG        OF PDDETAIL_C_I     &
        LABEL "Numero ligne..:"            &
        DISPLAY                             


DRAW 5,1 TO 5,80


SKIP TO LINE  7


TITLE &
@IF FRANCAIS
     " No bloc         Longeur    Hauteur    Largeur   Dimension(M3)   Cout total" &
@ENDIF
      AT ,2


SKIP TO LINE 8

ALIGN (02,02,03) (,,06) (,08,09) (,15,16) (,,20) (,,31) (,,42) (,,52) (,,66)

CLUSTER OCCURS WITH IFBLOC  

FIELD TGRCODGRA        OF IFBLOC           &
        ID 01                              &
        LABEL " "                          &
        DISPLAY                            &
        HIDDEN 
 
FIELD IFCCLE           OF IFBLOC           &
        DISPLAY

FIELD BLCCLE           OF IFBLOC           &
        DISPLAY                            &
        LABEL "-"

FIELD BLCRED           OF IFBLOC           &
        DISPLAY                            &
        LABEL "-" 

FIELD BLCVENDULONG     OF IFBLOC           &
        LABEL " "                          & 
        ID SAME                            &
        DISPLAY

FIELD BLCVENDUHAU      OF IFBLOC           &
        LABEL " "                          &
        DISPLAY 

FIELD BLCVENDULAR      OF IFBLOC           &
        LABEL " "                          &
        ID SAME                            &
        DISPLAY

FIELD BLCM3TOT         OF IFBLOC           &
        LABEL " "                          &
        ID SAME                            &
        DISPLAY

FIELD D_COUTBL                             &
        LABEL " "                          &
        ID SAME                            &
        PICTURE " ^^^,^^^.^^"              &
        FLOAT "$"                          &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran if001
;
PROCEDURE DESIGNER D001
BEGIN

 LET T_TGRCODGRA_ORI = TGRCODGRA OF IFBLOC
 LET T_IFCCLE_ORI = IFCCLE OF IFBLOC
 LET T_BLCCLE_ORI = BLCCLE OF IFBLOC
 LET T_BLCRED_ORI = BLCRED OF IFBLOC

 RUN SCREEN if001 MODE F         & 
        PASSING T_CIECLEMNU,     &
                T_CIENOM,        &
                T_TGRCODGRA_ORI, &
                T_IFCCLE_ORI,    &
                T_BLCCLE_ORI,    &
                T_BLCRED_ORI 

 PUSH FIND
END

;------------------------------------------------------------------------------
;
; De-assignation d'un bloc
;
PROCEDURE DESIGNER P001
BEGIN

  IF 0 = EXNCLE OF IFBLOC
  THEN BEGIN 

    LET COMNUMCOMINT OF IFBLOC = " "
    LET DCINUMLIG OF IFBLOC = " "
    LET COEDATDEB OF IFBLOC = 0
    LET IFLCLE OF IFBLOC = " "

    LET BLCIFLORI    OF IFBLOC = "0"  ; Indiquer que le lot pourra etre
                                      ; creer a l'origine via la carte de blocs.
    LET BLCFOUORI    OF IFBLOC = "0"  ; Indiquer que le fournisseur pourra
                                      ; etre creer a l'origine via la carte
                                      ; de blocs.
    PUSH UPDATE 

  END
  ELSE ERROR 3278 ; PD378 Impossible de de-assigner ce bloc

END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Assignation des blocs
;
PROCEDURE ENTRY
BEGIN

  ; Des blocs peuvent etre assigne seulement si le statut t de la commande
  ; est eagel a "C"
  IF COMSTU OF PDCOMMAN_INTERNE = "C"
  THEN BEGIN
    LET T_ECRAPP = " "
    RUN SCREEN pd210q                    &
             PASSING T_CIECLEMNU,        &
                     T_CIENOM,           &
                     T_ECRAPP,           &     
                     T_CLICLE,           & 
                     PDDETAIL_C_I        &
            MODE F
  END
  ELSE WARNING 3283 ; PD383 Impossible d'insérer un bloc pour cette commande 

  PUSH FIND
END

;------------------------------------------------------------------------------
;
; Repart la recherche pour remettre l'ecran a jour
;
PROCEDURE POSTUPDATE
BEGIN
  PUSH FIND
END

BUILD DETAIL LIST
