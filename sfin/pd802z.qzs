;/T/ Liste de vérification de saisie des diagrammes
;
;/P/ Programmeur.....: Martin Bruyère
;    Date création...: 1996-06-14
;    Description.....: Liste de vérification de saisie des diagrammes
;
;-------------------------------------------------------------------------------
CANCEL CLEAR


USE ussetqzs NOLIST


ACCESS PDDIAGRAMME                     &

  LINK TGRCODGRA    OF PDDIAGRAMME   , & 
       CIECLE       OF PDDIAGRAMME     &
    TO TGRCODGRA                     , & 
       CIECLE       OF PDTYPE_GRANIT   &

  LINK CIECLE       OF PDDIAGRAMME   , &
       DIANUM002    OF PDDIAGRAMME     &
    TO CIECLE,                         &
       DIANUM002    OF PDUNI_TRAV_DIA  OPTIONAL


CHOOSE CIECLE PARM , COMNUMCOM PARM


DEFINE D_DATE_DEB DATE            = PARM PROMPT "DATE DEBUT....:"
DEFINE D_HRE_DEB  INTEGER SIZE 08 = PARM PROMPT "HEURE DEBUT...:"
DEFINE D_DATE_FIN DATE            = PARM PROMPT "DATE FIN......:"
DEFINE D_HRE_FIN  INTEGER SIZE 08 = PARM PROMPT "HEURE FIN.....:"


;-------------------------------------------------------------------------------
;
; Sélection selon l'interval saisi
;

SELECT IF (DIADATCRE OF PDDIAGRAMME > D_DATE_DEB &
           OR (DIADATCRE OF PDDIAGRAMME = D_DATE_DEB AND DIAHRECRE OF PDDIAGRAMME >= D_HRE_DEB  ))&
          AND &
          (DIADATCRE OF PDDIAGRAMME < D_DATE_FIN &
           OR (DIADATCRE OF PDDIAGRAMME = D_DATE_FIN AND DIAHRECRE OF PDDIAGRAMME <= D_HRE_FIN ))

DEF D-UTRNUMUNITRV INT SIZE 2 = NCONVERT(UTRNUMUNITRV OF PDUNI_TRAV_DIA)

SORT ON COMNUMCOM    OF PDDIAGRAMME    &
     ON DIADATCRE    OF PDDIAGRAMME    &
     ON DIAHRECRE    OF PDDIAGRAMME    &
     ON DIANUM002    OF PDDIAGRAMME    &
     ON D-UTRNUMUNITRV

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de vérification de saisie des diagrammes" ; Premier titre du rapport

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Commande interne: " + COMNUMCOM OF PDDIAGRAMME[1:2] + &
                                      "-" +  COMNUMCOM OF PDDIAGRAMME[3:2] + &
                                      "-" +  COMNUMCOM OF PDDIAGRAMME[5:3]             ; Deuxiemme titre du rapport

DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD802"                                          ; Numéro du rapport

DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM                                             ; Nom de la compagnie

USE ustitstd45 NOLIST
  SKIP 2  &
  TAB 001 "                                                                                QTE      UNITÉ         QTE               QTE      " &
  SKIP    &
  TAB 001 "              GRANIT                      FINI  DIAGRAMME  LONG  HAUT  EPAIS  REQUISE   TRAVAIL   PRODUCTION U.M.     VENTE U.M.  " &
  SKIP    &
  TAB 001 "----------------------------------------  ----  ---------  ----  ----  -----  -------   -------  ----------------  ---------------" &
  SKIP

REPORT &
  TAB 001 TGRDSCFRA001 OF PDTYPE_GRANIT PRINT AT DIANUM002 &
  TAB 044 TYFCODFIN    OF PDDIAGRAMME   PRINT AT DIANUM002 &
  TAB 049 DIANUM002    OF PDDIAGRAMME   PRINT AT DIANUM002 &
  TAB 060 DIALON       OF PDDIAGRAMME   PRINT AT DIANUM002 &
  TAB 066 DIAHAU       OF PDDIAGRAMME   PRINT AT DIANUM002 &
  TAB 072 DIAEPA       OF PDDIAGRAMME   PRINT AT DIANUM002 &
  TAB 080 DIAQTECMD    OF PDDIAGRAMME   PRINT AT DIANUM002 &
  TAB 091 UTRNUMUNITRV OF PDUNI_TRAV_DIA                   &
  TAB 098 UTDQTEPRD    OF PDUNI_TRAV_DIA                   &
  TAB 111 UTDUNMPRD    OF PDUNI_TRAV_DIA                   &
  TAB 116 UTDQTEPRI    OF PDUNI_TRAV_DIA                   &
  TAB 129 UTDUNMVEN    OF PDUNI_TRAV_DIA                   


FOOTING AT COMNUMCOM SKIP PAGE

BUILD pd802z

@IF FORMAT

                                                                                QTE      UNITÉ        QTE               QTE
              GRANIT                      FINI  DIAGRAMME  LONG  HAUT  EPAIS  REQUISE   TRAVAIL   PRODUCTION U.M.     VENTE U.M.
----------------------------------------  ----  ---------  ----  ----  -----  -------   -------  ----------------  ---------------
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXSSSSSSSSS   01   XXXXXXX    9999  9999  9999    99999-     XXX    ^^^,^^^.^^^^ XX   ^^^,^^^.^^^^ XX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXSSSSSSSSS   01   XXXXXXX    9999  9999  9999    99999-     XXX    ^^^,^^^.^^^^ XX   ^^^,^^^.^^^^ XX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXSSSSSSSSS   01   XXXXXXX    9999  9999  9999    99999-     XXX    ^^^,^^^.^^^^ XX   ^^^,^^^.^^^^ XX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXSSSSSSSSS   01   XXXXXXX    9999  9999  9999    99999-     XXX    ^^^,^^^.^^^^ XX   ^^^,^^^.^^^^ XX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXSSSSSSSSS   01   XXXXXXX    9999  9999  9999    99999-     XXX    ^^^,^^^.^^^^ XX   ^^^,^^^.^^^^ XX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXSSSSSSSSS   01   XXXXXXX    9999  9999  9999    99999-     XXX    ^^^,^^^.^^^^ XX   ^^^,^^^.^^^^ XX

@ENDIF
