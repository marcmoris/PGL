;/P/ Rapport sur les comptes prix de revient (court)
;;
USE ussetqzs NOLIST

ACCESS *WPR306A &
  LINK REFCIECLE, REFCLETYP, REFCLENUM TO &
       REFCIECLE, REFCLETYP, REFCLENUM OF MCREFERENCE OPTIONAL &
  LINK CIECLE, PROCLE TO &
       CIECLE, PROCLE OF GPPROJET OPTIONAL  &
  LINK G-CPTCLE1 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE1 OPTIONAL &
  LINK G-CPTCLE2 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE2 OPTIONAL &
  LINK G-CPTCLE3 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE3 OPTIONAL &
  LINK G-CPTCLE4 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE4 OPTIONAL



DEFINE D-DESC1 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE1
DEFINE D-DESC2 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE2
DEFINE D-DESC3 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE3
DEFINE D-DESC4 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE4

DEFINE D_DSCGEN  CHARACTER SIZE 40 = &
   HISDSCGEN OF WPR306A IF HISDSCGEN OF WPR306A <> "" &
                      ELSE REFNOM OF MCREFERENCE



DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
  "Rapport sur les comptes au prix de revient"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "PR306"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

DEFINE D_MNT1 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE1 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT2 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE2 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT3 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE3 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT4 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE4 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT

DEFINE D-MNT1 INTEGER SIZE 4 = D_MNT1 IF CPTCLE = G-CPTCLE1 &
                               ELSE 0
DEFINE D-MNT2 INTEGER SIZE 4 = D_MNT2 IF CPTCLE = G-CPTCLE2 &
                               ELSE 0
DEFINE D-MNT3 INTEGER SIZE 4 = D_MNT3 IF CPTCLE = G-CPTCLE3 &
                               ELSE 0
DEFINE D-MNT4 INTEGER SIZE 4 = D_MNT4 IF CPTCLE = G-CPTCLE4 &
                               ELSE 0
DEFINE D-TOT  INTEGER SIZE 4 = D-MNT1 + D-MNT2 + D-MNT3 + D-MNT4

SORT ON CIECLE &
     ON PROCLE &
     ON REFCLENUM &
     ON HISDAT &
     ON HISNUMDOC

USE ustit220 NOLIST
  TAB 001 "Projet :" &
  TAB 010 PROCLE OF WPR306A &
  TAB 020 PRODSC OF GPPROJET &
SKIP 2 &
  TAB 001 "# Client/" &
  TAB 010 "Nom" &
  TAB 045 "Date" &
  TAB 055 "# facture" &
  TAB 075 "Commentaires" &
  TAB 130 D-DESC1 &
  TAB 146 D-DESC2 &
  TAB 162 D-DESC3 &
  TAB 178 D-DESC4 &
  TAB 200 "Total" &
  TAB 220 "Source" &
SKIP &
  TAB 001 "# fourn." &
  TAB 130 "(" &
  TAB 131 G-CPTCLE1 &
  TAB 137 ")" &
  TAB 146 "(" &
  TAB 147 G-CPTCLE2 &
  TAB 153 ")" &
  TAB 162 "(" &
  TAB 163 G-CPTCLE3 &
  TAB 169 ")" &
  TAB 173 "(" &
  TAB 179 G-CPTCLE4 &
  TAB 185 ")" &
SKIP 2

FOOT AT HISNUMDOC &
  TAB 001 REFCLENUM   PRINT AT REFCLENUM &
  TAB 010 REFNOM &
  TAB 045 HISDAT &
  TAB 055 HISNUMDOC &
  TAB 075 D_DSCGEN &
  TAB 130 D-MNT1 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 145 D-MNT2 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 160 D-MNT3 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 175 D-MNT4 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 190 D-TOT  SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 210 HISCODMOD &
SKIP &
  TAB 075 HISDSCSAI &
SKIP &
  TAB 075 HISDSCSAI2

FOOT AT PROCLE &
  TAB 130 "--------------" &
  TAB 145 "--------------" &
  TAB 160 "--------------" &
  TAB 175 "--------------" &
  TAB 190 "--------------" &
SKIP &
  TAB 001 "Total" &
  TAB 007 PROCLE &
  TAB 130 D-MNT1 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 145 D-MNT2 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 160 D-MNT3 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 175 D-MNT4 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 190 D-TOT  SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
SKIP PAGE

BUILD pr306z
