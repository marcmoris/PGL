;/T/ Digrammes sur plus d'une priorité.
;
;/A/ Auteur..........: Marc Poulin
;    Date création...: 2000-05-10
;    Description.....: Identifier les diagrammes dont les quantitées débitées ou à débiter
;                      sont réparties sur plus d'une priorité.
;
;-----------------------------------------------------------------

CANCEL CLEAR

USE ussetqzs NOLIST

acc *wpd806a

sort on ciecle       of wpd806a &
     on pjtabr       of wpd806a &
     on pjtann       of wpd806a &
     on comnumcomint of wpd806a &
     on comstu       of wpd806a &
     on dianum002    of wpd806a

define d_cbidsc   character size 10 = cbidsc of wpd806a

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Identification des digrammes ayant plus d'une priorité" ; Premier titre du rapport
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "                                                      ; Deuxiemme titre du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD806ZA"                                                ; Numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM                                                     ; Nom de la compagnie


USE ustitstd45 NOLIST
  SKIP 2                                                              &
 tab 001 "                Commande    Statut     Numéro    Nombre de" &
  SKIP                                                                &
 tab 001 " Projet  Année  interne    commande   diagramme  priorités" &
  SKIP                                                                &
 tab 001 " ------  -----  --------  ----------  ---------  ---------" &
  SKIP 2 

report                                     &
  tab 002 pjtabr       of wpd806a          &
          print at pjtabr                  &
  tab 010 pjtann       of wpd806a          &
          print at pjtann                  &
  tab 017 comnumcomint of wpd806a          &
          print at comnumcomint            &
  tab 027 d_cbidsc                         &
          print at comnumcomint            &
  tab 039 dianum002    of wpd806a          &
  tab 050 t_count      of wpd806a          

footing at pjtann                          &
   skip 2                                  

final footing                              &
   skip 3                                  &
  tab 002 "Nombre de diagrammes sur plus d'une priorité : " count


build pd806za

;/T/ Vérification quantités planifiés versus quantités commandées
;
;/A/ Auteur..........: Marc Poulin
;    Date création...: 2000-02-17
;    Description.....: Vérification si les quantités planifiées
;                      balancent avec les quantités commandées.
;
;-----------------------------------------------------------------
CANCEL CLEAR

USE ussetqzs NOLIST

acc pddiagramme link pjtabr        of pddiagramme     , &
                     dianumdia001  of pddiagramme     , &
                     dianumdia002  of pddiagramme     , &
                     ciecle        of pddiagramme       &
                  to pjtabr                           , &
                     dianumdia001                     , &
                     dianumdia002                     , &
                     ciecle        of vdiaqte           &

                link  pjtabr       of pddiagramme     , &
                      pjtann       of pddiagramme     , &
                      comnumcomint of pddiagramme     , &
                      ciecle       of pddiagramme       &
                  to  pjtabr                          , &
                      pjtann                          , &
                      comnumcomint                    , &
                      ciecle       of pdcomman_interne  &

                link ("COMSTU"                        , &
                      COMSTU      of pdcomman_interne)  &
                  to  XELNOM                          , &
                      CBICLE      of vcbi_dsc         

choose ciecle parm, dianum002 parm

select pdcomman_interne if comstu of pdcomman_interne = "P" or &
                           comstu of pdcomman_interne = "C"

select if diaqtecmd of pddiagramme <> diaqtepladbtgra of vdiaqte + &
                                      diaqtepladbtusc of vdiaqte + &
                                      diaqtepladbtsct of vdiaqte
sort on ciecle          of pddiagramme      &
     on pjtabr          of pddiagramme      &
     on pjtann          of pddiagramme      &
     on comnumcomint    of pddiagramme      &
     on comstu          of pdcomman_interne &
     on dianum002       of pddiagramme      

define d_qte_pl_tot integer   size 08 = diaqtepladbtgra of vdiaqte + &
                                        diaqtepladbtusc of vdiaqte + &
                                        diaqtepladbtsct of vdiaqte

define d_cbidsc     character size 10 = cbidsc of vcbi_dsc

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Vérification quantités planifiés versus quantités commandées" ; Premier titre du rapport
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "                                                            ; Deuxiemme titre du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD806ZB"                                                      ; Numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM                                                           ; Nom de la compagnie


USE ustitstd45 NOLIST
  SKIP 2                                                                                &
 tab 001 "                Commande    Statut     Numéro    Quantité       Quantité    " &
  SKIP                                                                                  &
 tab 001 " Projet  Année  interne    commande   diagramme  commandée  planifiée totale" &
  SKIP                                                                                  &
 tab 001 " ------  -----  --------  ----------  ---------  ---------  ----------------" &
  SKIP 2 

report                                     &
  tab 002 pjtabr       of pddiagramme      &
          print at pjtabr                  &
  tab 010 pjtann       of pddiagramme      &
          print at pjtann                  &
  tab 017 comnumcomint of pddiagramme      &
          print at comnumcomint            &
  tab 027 d_cbidsc                         &
          print at comnumcomint            &
  tab 039 dianum002    of pddiagramme      &
  tab 050 diaqtecmd    of pddiagramme      &
          pic "^^^^^^^^^" signif 1         &
  tab 068 d_qte_pl_tot                     &
          pic "^^^^^^^^^" signif 1         

footing at pjtann                          &
   skip 2                                  

final footing                              &
   skip 3                                  &
  tab 002 "Nombre de diagrammes hors balance : " count


build pd806zb
