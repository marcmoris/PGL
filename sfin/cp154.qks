;/T/ Maintenance des intervalles
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 18 mai 1993
;
;    Description..: Maintenance des intervalles pour les listes de l'âges des
;                   compte et de la prévision des déboursés.
;
;                   Les deux listes n'ont pas le même nombre de colonnes, c'est
;                   pour cette raison que pour l'âge des comptes on ne peut
;                   pas accéder à toutes les colonnes.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp154 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp NOLIST
    "CP154"

USE cp154.hlp

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------
;
;
FILE CPINTERVALLE     PRIMARY
  ACCESS REQUEST CIVTYP, &
                 CIVCOD  &
         VIA     CIECLE, &
                 CIVTYP, &
                 CIVCOD  &
         USING   T_CIECLEMNU           , &
                 CIVTYP OF CPINTERVALLE, &
                 CIVCOD OF CPINTERVALLE

  ACCESS REQUEST CIVTYP  &
         VIA     CIECLE, &
                 CIVTYP  &
         USING   T_CIECLEMNU           , &
                 CIVTYP OF CPINTERVALLE  &
         ORDERBY CIECLE, &
                 CIVTYP, &
                 CIVCOD

  ACCESS VIA     CIECLE &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE, &
                 CIVTYP, &
                 CIVCOD

  ITEM CIECLE OF CPINTERVALLE INITIAL T_CIECLEMNU

;
; Validation du type de liste(bilingue).
;
DEFINE    D_CIVTYP CHARACTER SIZE 16 = "CIVTYP"
TEMPORARY T_CIVTYP CHARACTER SIZE 4

;
; Validation du type de liste.
;
FILE VCBI_DSC         REFERENCE
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_CIVTYP, &
               CIVTYP OF CPINTERVALLE


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST   ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Maintenance des intervalles " &
@ELSE
      " %%%Maintenance des intervalles " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1

ALIGN (3,3,19) (,,21)

FIELD CIVTYP OF CPINTERVALLE HIDDEN ID 05 &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON VCBI_DSC &
          VIA XELNOM, &
              CBICLE &
          USING D_CIVTYP, &
                CIVTYP OF CPINTERVALLE &
          MESSAGE 682 ; Code invalide

FIELD CBIDSC OF VCBI_DSC DISPLAY

FIELD CIVCOD OF CPINTERVALLE HIDDEN ID 10 &
        REQUIRED &
        LOOKUP NOTON CPINTERVALLE &
          VIA CIECLE, &
              CIVTYP, &
              CIVCOD  &
          USING CIECLE OF CPINTERVALLE, &
                CIVTYP OF CPINTERVALLE, &
                CIVCOD OF CPINTERVALLE  &
          MESSAGE 564 ; Ce code existe déjà

SKIP

ALIGN(3,3,19)

FIELD CIVDSC OF CPINTERVALLE HIDDEN ID 12 &
        REQUIRED
SKIP

DRAW ,1 TO ,80
TITLE &
@IF FRANCAIS
      " Intervalle en jour " &
@ELSE
      "%%%Intervalle en jour " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

TITLE &
@IF FRANCAIS
      "Titre         >=      <=" &
@ELSE
      "Title         >=      <=" &
@ENDIF
      AT ,19

SKIP

ALIGN (3,3,19) (,,32) (,,40)
FIELD CIVCOLTIT1 OF CPINTERVALLE HIDDEN ID 15 &
        REQUIRED

FIELD CIVDEB1    OF CPINTERVALLE &
        DISPLAY

FIELD CIVFIN1    OF CPINTERVALLE &
        REQUIRED

SKIP 1

FIELD CIVCOLTIT2 OF CPINTERVALLE ID SAME &
        REQUIRED

FIELD CIVDEB2    OF CPINTERVALLE &
        DISPLAY

FIELD CIVFIN2    OF CPINTERVALLE &
        REQUIRED

SKIP 1

FIELD CIVCOLTIT3 OF CPINTERVALLE ID SAME &
        REQUIRED

FIELD CIVDEB3    OF CPINTERVALLE &
        DISPLAY

FIELD CIVFIN3    OF CPINTERVALLE &
        REQUIRED

SKIP 1

FIELD CIVCOLTIT4 OF CPINTERVALLE ID SAME &
        REQUIRED

FIELD CIVDEB4    OF CPINTERVALLE &
        DISPLAY

FIELD CIVFIN4    OF CPINTERVALLE &
        REQUIRED

SKIP 1

FIELD CIVCOLTIT5 OF CPINTERVALLE ID SAME &
        REQUIRED

FIELD CIVDEB5    OF CPINTERVALLE &
        DISPLAY


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT CIVTYP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIVTYP = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_CIVTYP, T_CIVTYP
    LET FIELDTEXT = TRUNC(T_CIVTYP)
  END
END


;-------------------------------------------------------------------------------
;
;
; L'âge des comptes a moins de colonne sur la liste.
;
;
PROCEDURE PROCESS CIVTYP
BEGIN
  IF CIVTYP OF CPINTERVALLE = "1"
  THEN BEGIN
    LET CIVFIN4 OF CPINTERVALLE = 0
    LET CIVDEB5 OF CPINTERVALLE = 0
    DISPLAY CIVFIN4 OF CPINTERVALLE
    DISPLAY CIVDEB5 OF CPINTERVALLE
  END
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT CIVFIN1
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(CIVFIN1 OF CPINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT CIVFIN1
BEGIN
  IF FIELDVALUE <= CIVDEB1 OF CPINTERVALLE
  THEN ERROR 565 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS CIVFIN1
BEGIN
  LET CIVDEB2 OF CPINTERVALLE = CIVFIN1 OF CPINTERVALLE + 1
  DISPLAY CIVDEB2 OF CPINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT CIVFIN2
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(CIVFIN2 OF CPINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT CIVFIN2
BEGIN
  IF FIELDVALUE <= CIVDEB2 OF CPINTERVALLE
  THEN ERROR 565 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS CIVFIN2
BEGIN
  LET CIVDEB3 OF CPINTERVALLE = CIVFIN2 OF CPINTERVALLE + 1
  DISPLAY CIVDEB3 OF CPINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT CIVFIN3
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(CIVFIN3 OF CPINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT CIVFIN3
BEGIN
  IF FIELDVALUE <= CIVDEB3 OF CPINTERVALLE
  THEN ERROR 565 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS CIVFIN3
BEGIN
  LET CIVDEB4 OF CPINTERVALLE = CIVFIN3 OF CPINTERVALLE + 1
  DISPLAY CIVDEB4 OF CPINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT CIVFIN4
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(CIVFIN4 OF CPINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT CIVFIN4
BEGIN
  IF FIELDVALUE <= CIVDEB4 OF CPINTERVALLE
  THEN ERROR 565 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS CIVFIN4
BEGIN
  LET CIVDEB5 OF CPINTERVALLE = CIVFIN4 OF CPINTERVALLE + 1
  DISPLAY CIVDEB5 OF CPINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPINTERVALLE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CPINTERVALLE &
     AND NOT NEWRECORD     OF CPINTERVALLE &
     AND NOT DELETEDRECORD OF CPINTERVALLE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END


BUILD

;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;************************* Maintenance des intervalles **************************
;*                                                                              *
;* Type..........: x xxxxxxxxxxxxxxxxxxxxxxxxx                                  *
;* Code..........: x                                                            *
;* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;****************************** Intervalle en jour ******************************
;*                                                                              *
;*                 Titre         >=      <=                                     *
;* Colonne 1.....: xxxxxxxxxxx  xxx     xxx                                     *
;*                                                                              *
;* Colonne 2.....: xxxxxxxxxxx  xxx     xxx                                     *
;*                                                                              *
;* Colonne 3.....: xxxxxxxxxxx  xxx     xxx                                     *
;*                                                                              *
;* Colonne 4.....: xxxxxxxxxxx  xxx     xxx                                     *
;*                                                                              *
;* Colonne 5.....: xxxxxxxxxxx  xxx                                             *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;********************************************************************************
