;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de sous-projet - Budget d'unité de travail
;
;/M/François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction pour unix.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp002c ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FOR 11 LINES &
              ON LINE 13  &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE


TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

TEMPORARY T_CIECLEMNU    CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM       CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_OLDVALUE     CHARACTER SIZE 1

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans


TRANSACTION T_PAU_VISUEL COMMIT ON UPDATE &
                         READ WRITE &
                         RESERVING FOR SHARE WRITE GPPRA_BDG_UNT IN DICT

USE ussectmp NOLIST     ; Variable pour la securite
    "GP002C"            ; Nom de l'écran

USE gp002c.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier d'unité de travail
;------------------------------------
FILE GPPRO_ACTIVITE         MASTER


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPPRA_BDG_UNT          PRIMARY  &

     TRANSACTION T_PAU_VISUEL FOR PROCESS, QUERY, UPDATE &

                            OCCURS 5 &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE, &
                  UNTCLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE, &
                  UNTCLE OF GPPRA_BDG_UNT   &
          REQUEST UNTCLE OF GPPRA_BDG_UNT   &
          ORDERBY UNTCLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE  &
          ORDERBY UNTCLE

  ITEM CIECLE    OF GPPRA_BDG_UNT   INITIAL T_CIECLEMNU
  ITEM PRJCLE    OF GPPRA_BDG_UNT   INITIAL PRJCLE     OF GPPRO_ACTIVITE
  ITEM PRACLE    OF GPPRA_BDG_UNT   INITIAL PRACLE     OF GPPRO_ACTIVITE
  ITEM PAUUSRBDG OF GPPRA_BDG_UNT   INITIAL LOGONID
  ITEM PAUDATBDG OF GPPRA_BDG_UNT   INITIAL SYSDATE



FILE GPPRO_BDG_UNT          DESIGNER   &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  UNTCLE  &
          USING   CIECLE OF GPPRA_BDG_UNT,  &
                  PRJCLE OF GPPRA_BDG_UNT,  &
                  UNTCLE OF GPPRA_BDG_UNT UNIQUE

;------------------------------
; Fichier des unités de travail
;------------------------------
TEMPORARY T_UNTCLE     CHARACTER SIZE 4

FILE GPUNITE_TRAVAIL        REFERENCE

  ACCESS  VIA     UNTCLE  &
          USING   UNTCLE OF GPPRA_BDG_UNT


;------------------------------
; Fichier des unités de mesures
;------------------------------
FILE GPUNITE_MESURE         REFERENCE

  ACCESS  VIA     UNMCLE  &
          USING   UNMCLE OF GPUNITE_TRAVAIL


;-------------------------------------------------------------------------------
;

TEMPORARY T_OLDMNTBDG        FLOAT SIZE 8  OCCURS WITH GPPRA_BDG_UNT
TEMPORARY T_OLDMNTBDGREV     FLOAT SIZE 8  OCCURS WITH GPPRA_BDG_UNT

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


SKIP
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO  11,1
DRAW FROM  3,80 TO  11,80
DRAW FROM  11,1  TO  11,80

SKIP 1

TITLE &
@IF FRANCAIS
      " Budget d'unité de travail " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,32)
FIELD PRAPAUDATAPP  OF GPPRO_ACTIVITE                  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
      LABEL "Date approb...:" &
      DISPLAY

FIELD PRAPAUUSRAPP  OF GPPRO_ACTIVITE                 &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
" Unité de travail" &
@ELSE
      " %%% " &
@ENDIF
      AT ,2

TITLE &
@IF FRANCAIS
"Budget initial    Budget courant" &
@ELSE
      " %%% " &
@ENDIF
      AT ,45

SKIP

ALIGN (3,2,3) (,,8) (,,39) (,,43) (,,61)

CLUSTER OCCUR WITH GPPRA_BDG_UNT   ID BASE 10

FIELD UNTCLE OF GPPRA_BDG_UNT                         &
      LABEL " " HIDDEN                                &
      REQUIRED NOCHANGE                               &
      LOOKUP ON GPUNITE_TRAVAIL                       &
        MESSAGE 866, &
          NOTON GPPRA_BDG_UNT                         &
        VIA   CIECLE,                                 &
              PRJCLE,                                 &
              PRACLE,                                 &
              UNTCLE                                  &
        USING T_CIECLEMNU,                            &
              PRJCLE OF GPPRO_ACTIVITE,               &
              PRACLE OF GPPRO_ACTIVITE,               &
              UNTCLE OF GPPRA_BDG_UNT                 &
        MESSAGE 867; Cette unité de travail est déjà  &
                   ; référée pour ce budget.

FIELD UNTDSC OF GPUNITE_TRAVAIL                       &
      DISPLAY                                         &
      ID SAME

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      ID SAME                                         &
      PICTURE "/^^"

FIELD PAUBDG OF GPPRA_BDG_UNT                         &
      ID SAME                                         &
      IF PRAPAUDATAPP  OF GPPRO_ACTIVITE = 0          &
      VALUE 0 TO 999999999.9999

FIELD PAUBDGREV OF GPPRA_BDG_UNT                      &
      ID SAME                                         &
      IF PRAPAUDATAPP  OF GPPRO_ACTIVITE <> 0         &
      VALUE 0 TO 999999999.9999

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les unités de travail.
;
;
PROCEDURE INPUT UNTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNTCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp107 MODE F PASSING T_UNTCLE
    LET FIELDTEXT = TRUNCATE(T_UNTCLE)
  END
END

PROCEDURE EDIT UNTCLE
BEGIN
   IF UNTSTU OF GPUNITE_TRAVAIL <> "A"
   THEN ERROR 868; Le statut de l'unité de travail doit être actif.
END



PROCEDURE INPUT PAUBDG
BEGIN
   IF 0=SIZE(FIELDTEXT)
   THEN BEGIN
        LET FIELDTEXT = "1"
        LET T_OLDVALUE= "O"
   END
   ELSE LET T_OLDVALUE = "N"
END

PROCEDURE EDIT PAUBDG
BEGIN
   IF T_OLDVALUE = "O"
   THEN LET FIELDVALUE = OLDVALUE(PAUBDG OF GPPRA_BDG_UNT)

   IF PRAPAUDATAPP  OF GPPRO_ACTIVITE <> 0  AND &
      T_OLDVALUE = "N"
   THEN BEGIN
        LET FIELDVALUE = OLDVALUE(PAUBDG OF GPPRA_BDG_UNT)
        WARNING 871 ; Cet élément ne peut être modifié dû au statut du budget
   END
END



PROCEDURE INPUT PAUBDGREV
BEGIN
   IF 0=SIZE(FIELDTEXT)
   THEN BEGIN
        LET FIELDTEXT = "1"
        LET T_OLDVALUE= "O"
   END
   ELSE LET T_OLDVALUE = "N"
END

PROCEDURE EDIT PAUBDGREV
BEGIN
   IF T_OLDVALUE = "O"
   THEN LET FIELDVALUE = OLDVALUE(PAUBDGREV OF GPPRA_BDG_UNT)

   IF PRAPAUDATAPP  OF GPPRO_ACTIVITE = 0 AND &
      T_OLDVALUE = "N"
   THEN BEGIN
        LET FIELDVALUE = OLDVALUE(PAUBDGREV OF GPPRA_BDG_UNT)
        WARNING 871 ; Cet élément ne peut être modifié dû au statut du budget
   END
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GPPRA_BDG_UNT
  BEGIN
     ;
     ; Validation de la destruction
     ;
     IF     DELETEDRECORD OF GPPRA_BDG_UNT       &
        AND TZ_SECDEL = "0"
     THEN ERROR 1
     ;
     ; Validation de la modification.
     ;
     IF     ALTEREDRECORD     OF GPPRA_BDG_UNT       &
        AND NOT NEWRECORD     OF GPPRA_BDG_UNT       &
        AND NOT DELETEDRECORD OF GPPRA_BDG_UNT       &
        AND TZ_SECMOD = "0"
     THEN ERROR 2

     ;
     ; Destruction du budget par unite de travail
     ;
     IF DELETEDRECORD OF GPPRA_BDG_UNT
     THEN BEGIN
        ;
        ; On initialise le flag de retour pour savoir si le sous-écran a
        ; bien fonctionné.
        ;
        LET T_FLGDEL = ""

        RUN SCREEN gp002cx MODE SAME &
                           PASSING GPPRO_ACTIVITE, &
                                   GPPRA_BDG_UNT,  &
                                   T_FLGDEL
        ;
        ; Si la valeur de retour égale 1, cela indique que le sous-écran
        ; n'a trouvé aucune référence pour l'identifiant demandé.
        ;
        IF T_FLGDEL = ""
        THEN ERROR " "
     END


     GET  GPPRO_BDG_UNT &
          VIA     CIECLE, &
                  PRJCLE, &
                  UNTCLE  &
          USING   CIECLE OF GPPRA_BDG_UNT,  &
                  PRJCLE OF GPPRA_BDG_UNT,  &
                  UNTCLE OF GPPRA_BDG_UNT OPTIONAL
     IF NOT ACCESSOK
     THEN BEGIN
          LET CIECLE OF GPPRO_BDG_UNT = CIECLE OF GPPRA_BDG_UNT
          LET PRJCLE OF GPPRO_BDG_UNT = PRJCLE OF GPPRA_BDG_UNT
          LET UNTCLE OF GPPRO_BDG_UNT = UNTCLE OF GPPRA_BDG_UNT
     END

     IF    (NEWRECORD OF GPPRA_BDG_UNT       OR &
            ALTEREDRECORD OF GPPRA_BDG_UNT) AND &
        NOT DELETEDRECORD OF GPPRA_BDG_UNT
     THEN BEGIN
          LET PBUBDG       OF GPPRO_BDG_UNT = PBUBDG       OF GPPRO_BDG_UNT + &
                                              PAUBDG       OF GPPRA_BDG_UNT - &
                                              T_OLDMNTBDG

          LET PBUBDGREV    OF GPPRO_BDG_UNT = PBUBDGREV    OF GPPRO_BDG_UNT + &
                                              PAUBDGREV    OF GPPRA_BDG_UNT - &
                                              T_OLDMNTBDGREV
     END

     IF DELETEDRECORD OF GPPRA_BDG_UNT
     THEN BEGIN
          LET PBUBDG       OF GPPRO_BDG_UNT = PBUBDG       OF GPPRO_BDG_UNT - &
                                              T_OLDMNTBDG

          LET PBUBDGREV    OF GPPRO_BDG_UNT = PBUBDGREV    OF GPPRO_BDG_UNT - &
                                              T_OLDMNTBDGREV
     END

     LET PBUUSRBDG OF GPPRO_BDG_UNT = LOGONID
     LET PBUDATBDG OF GPPRO_BDG_UNT = SYSDATE
     PUT GPPRO_BDG_UNT
  END
END


PROCEDURE POSTUPDATE
BEGIN
   FOR GPPRA_BDG_UNT
   BEGIN
      LET T_OLDMNTBDG    = PAUBDG    OF GPPRA_BDG_UNT
      LET T_OLDMNTBDGREV = PAUBDGREV OF GPPRA_BDG_UNT
   END
END


PROCEDURE POSTFIND
BEGIN
   FOR GPPRA_BDG_UNT
   BEGIN
      LET T_OLDMNTBDG    = PAUBDG    OF GPPRA_BDG_UNT
      LET T_OLDMNTBDGREV = PAUBDGREV OF GPPRA_BDG_UNT
   END
END

BUILD
@IF FORMAT
************************** Budget d'unité de travail **************************x
* Date..........: xxxxxxxx xxxxxxxxxxxx                                        *
*                                                                              *
* Unité de travail                          Budget initial    Buget courant    *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxx xxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx  *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxx xxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx  *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxx xxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx  *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxx xxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx  *
* xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxx xxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx  *
********************************************************************************
@ENDIF

