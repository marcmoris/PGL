;/T/ Consultation Code Form     XQBCLE
;
;/P/ Programmeur..: Chantal Martineau
;    Date Création: 17-septembre-1992
;
;    Description..: Permet de visionner et de choisir Queue Batch
;
;/M/ François Déry, 11 mai 1999
;       Ajout d'un paramètre pour retourner le nom logique ou le code selon
;       le type d'appel.
;
;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gs110 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_XQBCLE, T_FLGQUE

USE gs110.hlp NOLIST
USE ussectmp NOLIST
"GS110"

TEMPORARY T_XQBCLE   CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_FLGQUE   CHARACTER SIZE 1 RESET AT STARTUP

FILE GSQUEUE_BATCH PRIMARY OCCURS 11
 ACCESS VIA XQBCLE USING T_XQBCLE

;-------------------------------------------------------------------------------
; Recherche de la langue de l'usager
;
FILE GSUSAGER REFERENCE
  ACCESS VIA USRCLE USING LOGONID


DEFINE D_XQBDSC CHARACTER SIZE 20 = &
@IF FRANCAIS
        XQBDSCFRA OF GSQUEUE_BATCH
@ELSE
        XQBDSCANG OF GSQUEUE_BATCH
@ENDIF

USE ushilite NOLIST
USE usdrwfnd NOLIST

@IF FRANCAIS
 TITLE " File d'attente " CENTERED AT 1,1
@ELSE
 TITLE " %%% " CENTERED AT 1,1
@ENDIF

USE ustitoff NOLIST

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH GSQUEUE_BATCH

FIELD XQBCLE    OF GSQUEUE_BATCH HIDDEN LABEL ">>"
FIELD D_XQBDSC DISPLAY

CLUSTER

;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_XQBCLE, "@")
  THEN LET T_XQBCLE = ""
END

PROCEDURE DESIGNER 1
BEGIN
  IF T_FLGQUE = "1"
  THEN LET T_XQBCLE = XQBNAM OF GSQUEUE_BATCH ; Pour écran gs160
  ELSE LET T_XQBCLE = XQBCLE OF GSQUEUE_BATCH
  RETURN
END

BUILD
