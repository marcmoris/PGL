;/T/ Immobilisation - Suivi des écritures
;
;/P/ Programmeur..: Alain Côté
;    Date création: 22 février 1994
;
;/V3.02.00/ Alain Côté, 22 février 1994, Immobilisation
;
;    Description..: Cet écran sert exclusivement à la consultation de
;                   l'historique des amortissements et de aliénation pour les
;                   différentes périodes comptables.
;
;                   Chaque ligne d'historique correspond une écriture
;                   comptable effectuée au journal général.
;
;
;-----------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001f FROM 7,1 TO 23,80 &
              MODE LABEL "" AT 1, 80 NOACTION &
              FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND , &
                         CHANGE &
              RECEIVING IMIMMOBILISATION , &
                        T_CIECLEMNU      , &
                        T_CIENOM

;
; Variables pour la sécurité.
;
USE ussectmp NOLIST
    "IM001F"

;
; Documentation de l'écran.
;
USE im001f.hlp NOLIST

USE ustrasse NOLIST       ;Transaction QUERY pour les sous-écran


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie de recherche
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP

;-------------------------------------------------------------------------------
;
FILE IMIMMOBILISATION MASTER

;
;
FILE IMIMM_SUIVI      PRIMARY &
                      OCCURS 12 &
                      NOITEM
  ACCESS VIA   CIECLE , &
               IMMCLE  &
         USING CIECLE OF IMIMMOBILISATION , &
               IMMCLE OF IMIMMOBILISATION   &
         ORDERBY CIECLE    DESCENDING, &
                 IMMCLE    DESCENDING, &
                 PECCLE    DESCENDING, &
                 ECJCLENUM DESCENDING

;-------------------------------------------------------------------------------
;
; Pour l'appel de l'écran GL001 du Grand Livre.
;
TEMPORARY T_PECCLEMNU    CHARACTER SIZE 4
TEMPORARY T_PECCLECOU    CHARACTER SIZE 4
TEMPORARY T_FLGMNU       CHARACTER SIZE 1
TEMPORARY T_ECJCIECLEFND CHARACTER SIZE 4
TEMPORARY T_ECJCLENUMFND CHARACTER SIZE 8
TEMPORARY T_ECJCLESEQFND CHARACTER SIZE 2

;
; Solde.
;
DEFINE D_VALCTB  INTEGER SIZE 4  = IMIMNTNONAMT OF IMIMM_SUIVI - &
                                   IMIMNTAMT    OF IMIMM_SUIVI


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 01,01 TO 01,79
DRAW 01,01 TO 17,01
DRAW 17,01 TO 17,80
DRAW 02,80 TO 17,80

TITLE &
@IF FRANCAIS
      " Suivi des écritures " &
@ELSE
      " %%%Suivi des écritures " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

HILITE TITLE OFF

TITLE &
@IF FRANCAIS
      "Numéro            Montant" &
@ELSE
      "Numéro            Montant" &
@ENDIF
      AT ,18
SKIP

TITLE &
@IF FRANCAIS
   "Période     écriture        non amorti   Amortissement           Solde" &
@ELSE
   "%%%iode     écriture        non amorti   Amortissement           Solde" &
@ENDIF
      AT ,5

SKIP

ALIGN (6,5,6) (,,16) (,,24) (,,30) (,,46) (,,62)

CLUSTER OCCURS WITH IMIMM_SUIVI ID BASE 05

FIELD PECCLE       OF IMIMM_SUIVI LABEL " " HIDDEN &
        DISPLAY

FIELD ECJCLENUM    OF IMIMM_SUIVI

FIELD ECJCLESEQ    OF IMIMM_SUIVI &
        DISPLAY

FIELD IMIMNTNONAMT OF IMIMM_SUIVI &
        DISPLAY

FIELD IMIMNTAMT    OF IMIMM_SUIVI &
        DISPLAY

FIELD D_VALCTB &
        DISPLAY &
        PICTURE "^^,^^^,^^^.^^" &
        SIGNIFICANCE 4

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Appel de l'écran du journal général du module Grand Livre.
;
;
PROCEDURE INPUT ECJCLENUM
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLEMNU    = ""
    LET T_PECCLECOU    = ""
    LET T_FLGMNU       = "C"
    LET T_ECJCIECLEFND = CIECLE    OF IMIMM_SUIVI
    LET T_ECJCLENUMFND = ECJCLENUM OF IMIMM_SUIVI
    LET T_ECJCLESEQFND = ECJCLESEQ OF IMIMM_SUIVI
    RUN SCREEN gl001 MODE F &
                     PASSING T_ECJCIECLEFND , &
                             T_CIENOM       , &
                             T_PECCLEMNU    , &
                             T_PECCLECOU    , &
                             T_FLGMNU       , &
                             T_ECJCLENUMFND , &
                             T_ECJCLESEQFND
  END
  LET FIELDTEXT = ""
END

BUILD
@IF FORMAT

***************************** Suivi des écritures *****************************x
*                                                                              *
*                Numéro            Montant                                     *
*   Période     écriture        non amorti   Amortissement           Solde     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
*    xxxxx     xxxxxxxxxx    xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx     *
********************************************************************************
@ENDIF
