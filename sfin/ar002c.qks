;/T/ Gestion des produits - Catalogue
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 7 mars 1994
;
;    Description..: Cet écran permet de faire la gestion du catalogue
;                   fournisseur des produits. Les informations fournies
;                   par cet écran proviennent de la mise à jour faite
;                   lors de transactions chez le fournisseur. Seul le numéro 
;                   de produit chez le fournisseur est modifiable dans cet écran. 
;-------------------------------------------------------------------------------
;/M/ Pascal Tremblay, 98-01-20
;
;    Ajustement pour le changement de siècle
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar002c &
             ACTIONBAR & 
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             FOR 11 LINES &
             ON LINE 13  &  
             ACTIVITIES FIND, CHANGE &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING  ARPRODUIT, &
                        T_FOUCIECLE 
                       
USE ustrasse NOLIST      ; Pour les sous-écrans

USE ussectmp NOLIST      ; Variable pour la securité
    "AR002C"             ; Nom de l'écran

USE ar002c.hlp NOLIST  ; Use servant à la documentation de l'écran

USE usactecr NOLIST                      

;-------------------------------------------------------------------------------
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4  ; Compagnie fournisseur
;-------------------------------------------------------------------------------

;
; Produit
;
FILE ARPRODUIT  MASTER

;
; Produit / fournisseur
;
FILE ARPRD_FOU     PRIMARY NOITEM OCCURS 6
  ACCESS VIA   CIECLE,               &
               PRDCLE,               &   
               FOUCLE                &
         USING CIECLE OF ARPRODUIT,  &
               PRDCLE OF ARPRODUIT,  &
               FOUCLE OF ARPRD_FOU  &
         REQUEST FOUCLE    &  
         ORDERBY CIECLE,   &
                 PRDCLE,   &
                 PDFDATDERACH DESCENDING     

  ACCESS VIA   CIECLE,              &
               PRDCLE               &  
         USING CIECLE OF ARPRODUIT, &
               PRDCLE OF ARPRODUIT  &
         ORDERBY CIECLE,   &
                 PRDCLE,   &
                 PDFDATDERACH DESCENDING     


;-------------------------------------------------------------------------------
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie du menu
TEMPORARY T_FOUCLE    CHARACTER SIZE 6  ; Numéro du fournisseur
TEMPORARY T_FOCCIECLE CHARACTER SIZE 4  ; Compagnie du menu écran dépisteur 

DEFINE D_DELMYNLIV INTEGER SIZE 4 = &
  (PDFNBRJRSCMDLIV OF ARPRD_FOU / PDFNBRLIV OF ARPRD_FOU)


;-------------------------------------------------------------------------------
;

; Fichier permettant de déterminer le code de devise du fournisseur.

FILE VFOC_FOU REFERENCE OCCURS WITH ARPRD_FOU
  ACCESS VIA FOUCIECLE            , &
             FOUCLE               , &
             FOCCIECLE              &
      USING FOUCIECLE OF ARPRD_FOU, &
            FOUCLE    OF ARPRD_FOU, &
            CIECLE    OF ARPRD_FOU

; Fichier des devises.

FILE MCDEVISE REFERENCE OCCURS WITH ARPRD_FOU
  ACCESS VIA DEVCLE &
       USING DEVCLE OF VFOC_FOU

; Paramètres pour la compagnie consolidé (holding).

FILE MCHOLDING REFERENCE
  ACCESS VIA CIENMC USING "1"

; Calcul du montant associé au "dernier coût" en tenant compte des devises.

DEFINE D_PDFDERCOU FLOAT SIZE 8 = ROUND(PDFDERCOU OF ARPRD_FOU / DEVTAU OF MCDEVISE) &
                                                           IF DEVCLE OF VFOC_FOU <> DEVCLE OF MCHOLDING          &
                             ELSE PDFDERCOU OF ARPRD_FOU

DEFINE D_PDFVALCOM FLOAT SIZE 8 = (D_PDFDERCOU * PDFQTECOM OF ARPRD_FOU/100000)

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tous les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO  11,1
DRAW FROM  3,80 TO  11,80
DRAW FROM  11,1  TO 11,80


SKIP 1
TITLE &
@IF FRANCAIS
      " Fournisseur du produit " &
@ELSE
      " %%%Fournisseur du produit " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 
TITLE "             # Produit chez   Dernier    Dernier     Qté en     Valeur en  Dél"  AT ,2
SKIP 
TITLE " Fourn. Dev   fournisseur      achat     coûtant    commande    commande   liv"  AT ,2
SKIP

CLUSTER OCCURS WITH ARPRD_FOU ID BASE 10 

ALIGN (2,3,3) (,,10) (,,14) (,,30) (,,41) (,,53) (,,65) (,,77)
SKIP

FIELD FOUCLE OF ARPRD_FOU              &
        HIDDEN                         &
        NOCHANGE                       &
        LABEL " "                      &
        LOOKUP ON ARPRD_FOU            &
              VIA CIECLE            ,  &
                  FOUCLE               &
            USING CIECLE OF ARPRD_FOU, &
                  FOUCLE OF ARPRD_FOU  &  
          MESSAGE 1347                 ; Ce fournisseur n'existe pas.

FIELD DEVCLE OF VFOC_FOU               &
      DISPLAY

FIELD PDFPRDCLEFOU OF ARPRD_FOU

FIELD PDFDATDERACH OF ARPRD_FOU        &
      FORMAT YYYYMMDD                  &
      DISPLAY 

FIELD D_PDFDERCOU                      &
      PICTURE "^^^,^^^.^^^"            &
      SIGNIF 5                         &
      OUTPUT SCALE -1

FIELD PDFQTECOM OF ARPRD_FOU           &
      DISPLAY                          &
      PICTURE "^^^,^^^.^^^"

FIELD D_PDFVALCOM  DISPLAY             &
      PICTURE "^^^,^^^.^^ "            &
      TRAILING SIGN "-"                &
      BWZ  

FIELD D_DELMYNLIV DISPLAY              &
      PICTURE "^^^"                    &
      BWZ  

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
; Ecran de consultation (dépisteur) des fournisseurs du produit
;
;
PROCEDURE INPUT FOUCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_FOCCIECLE = CIECLE OF ARPRODUIT
    RUN SCREEN cp104 MODE F PASSING T_FOUCIECLE, T_FOUCLE, T_FOCCIECLE
    LET FIELDTEXT = TRUNCATE(T_FOUCLE)
  END
END

;
; Procédure servant à modifier la première occurrence dans
; Powerhouse Client
;

PROCEDURE DESIGNER PH01
BEGIN
  ACCEPT PDFPRDCLEFOU OF ARPRD_FOU 
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR ARPRD_FOU
  BEGIN
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF ARPRD_FOU &
       AND NOT NEWRECORD     OF ARPRD_FOU &
       AND NOT DELETEDRECORD OF ARPRD_FOU &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END

END

BUILD


@IF FORMAT
**************************** Fournisseur du produit ****************************
*             # Produit chez   Dernier    Dernier     Qté en     Valeur en  Dél*
* Fourn. Dev   fournisseur      achat     coûtant    commande    commande   liv*
* xxxxxx xxx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxx*
* xxxxxx xxx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxx*
* xxxxxx xxx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxx*
* xxxxxx xxx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxx*
* xxxxxx xxx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxx*
* xxxxxx xxx xxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxxxxxxxxxx xxx*
********************************************************************************
@ENDIF
