;/T/ 2.4.5. Enregistrer la finition
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-06-15
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   la finition des pièces de granit.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;/M/ REMY DEMERS AJOUT DE LA PROCEDURE UPDATE AFIN DE PERMETTRE LA
;                MISE A JOUR DE PLUSIEURS LIGNES SEMBLABLES
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN pd187  ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM

;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;
TRANSACTION NO_SEQ READ WRITE RESERVING FOR PROTECTED WRITE PDSEQ_FINITION IN DICT

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD187"

;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd187.hlp NOLIST

;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Arrêt finition                  " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Arrêt finition               " ACTION DESIGNER D001
@ENDIF

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
; Variable pour l'appel des dépisteur (pop-up)
;
TEMPORARY T_OPENUMEMP    CHARACTER SIZE 06
TEMPORARY T_RAACODARR    CHARACTER SIZE 04
TEMPORARY T_DIANUM002    CHARACTER SIZE 07
TEMPORARY T_UTRNUMUNITRV CHARACTER SIZE 04
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04
TEMPORARY T_MFICODMAC    CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;
TEMPORARY T_SILENT CHARACTER SIZE 01

;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calcul d'un intervalle de temps
;
USE usdiftim NOLIST

;-------------------------------------------------------------------------------
;
; Finition
;
FILE PDFINITION       PRIMARY &
                      NOITEM &
                      NODELETE

  ACCESS VIA     CIECLE,          &
                 FINNUMRAP        &
         USING   T_CIECLEMNU,                         &
                 FINNUMRAP        OF PDFINITION       &
         REQUEST FINNUMRAP        OF PDFINITION       &
         ORDERBY CIECLE,          &
                 FINNUMRAP

  ACCESS VIA     CIECLE,          &
                 FINDATPRD,       &
                 QRTCODQRT,       &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,                         &
                 FINDATPRD        OF PDFINITION,      &
                 QRTCODQRT        OF PDFINITION,      &
                 OPENUMEMP        OF PDFINITION       &
         REQUEST FINDATPRD        OF PDFINITION,      &
                 QRTCODQRT        OF PDFINITION,      &
                 OPENUMEMP        OF PDFINITION       &
         ORDERBY CIECLE,          &
                 FINNUMRAP


  ACCESS VIA     CIECLE,          &
                 FINDATPRD,       &
                 QRTCODQRT        &
         USING   T_CIECLEMNU ,                        &
                 FINDATPRD        OF PDFINITION,      &
                 QRTCODQRT        OF PDFINITION       &
         REQUEST FINDATPRD        OF PDFINITION,      &
                 QRTCODQRT        OF PDFINITION       &
         ORDERBY CIECLE,          &
                 FINNUMRAP


  ACCESS VIA     CIECLE,          &
                 FINDATPRD        &
         USING   T_CIECLEMNU,                         &
                 FINDATPRD        OF PDFINITION       &
         REQUEST FINDATPRD        OF PDFINITION       &
         ORDERBY FINNUMRAP


  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 FINNUMRAP

   ITEM CIECLE OF PDFINITION INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Détail finition
;
FILE PDDET_FINITION DETAIL OCCURS 11 NOITEM
  ACCESS VIA     CIECLE,          &
                 FINNUMRAP        &
         USING   T_CIECLEMNU,                         &
                 FINNUMRAP        OF PDFINITION       &
         ORDERBY CIECLE,          &
                 FINNUMRAP,       &
                 DFINUMLIG

  ITEM CIECLE    OF PDDET_FINITION INITIAL T_CIECLEMNU
  ITEM FINNUMRAP OF PDDET_FINITION INITIAL FINNUMRAP OF PDFINITION FIXED

;-------------------------------------------------------------------------------
;
; Unité travail - diagramme
;
FILE PDUNI_TRAV_DIA SECONDARY &
                    OCCURS WITH PDDET_FINITION &
                    NOITEM &
                    NODELETE

  ACCESS VIA   CIECLE,       &
               DIANUM002,    &
               UTRNUMUNITRV  &
         USING T_CIECLEMNU,                    &
               DIANUM002    OF PDDET_FINITION, &
               UTRNUMUNITRV OF PDDET_FINITION

  ITEM CIECLE       OF PDUNI_TRAV_DIA INITIAL T_CIECLEMNU
  ITEM DIANUM002    OF PDUNI_TRAV_DIA INITIAL DIANUM002     OF PDDET_FINITION
  ITEM UTRNUMUNITRV OF PDUNI_TRAV_DIA INITIAL UTRNUMUNITRV  OF PDDET_FINITION
  ITEM DFIQTEPRD    OF PDDET_FINITION SUM INTO UTDQTEUTRPRD OF PDUNI_TRAV_DIA

FILE PDUNI_TRAV_DIA REFERENCE ALIAS A_PDUNI_TRAV_DIA
    ACCESS VIA  CIECLE                         , &
                DIANUM002                      , &
                UTRNUMUNITRV                     &
         USING  T_CIECLEMNU                    , &
                DIANUM002    OF PDDET_FINITION , &
                UTRNUMUNITRV OF PDDET_FINITION

FILE PDUNI_TRAV_DIA DESIGNER ALIAS B_PDUNI_TRAV_DIA
    ACCESS VIA  CIECLE                         , &
                DIANUM002                      , &
                UTRNUMUNITRV                     &
         USING  T_CIECLEMNU                    , &
                DIANUM002    OF PDDET_FINITION , &
                UTRNUMUNITRV OF PDDET_FINITION

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour la finition
;
FILE PDSEQ_FINITION  DESIGNER &
                     TRANSACTION NO_SEQ

;-------------------------------------------------------------------------------
;
; Détail de finition (Ancien pour valider)
;
FILE PDDET_FINITION DESIGNER ALIAS DFI_OLD

;-------------------------------------------------------------------------------
;
FILE PDFINITION DESIGNER ALIAS PDFINITION_VALIDE

  ACCESS VIA     CIECLE,          &
                 FINDATPRD,       &
                 QRTCODQRT,       &
                 MFICODMAC        &
         USING   T_CIECLEMNU,                         &
                 FINDATPRD        OF PDFINITION,      &
                 QRTCODQRT        OF PDFINITION,      &
                 MFICODMAC        OF PDFINITION

;-------------------------------------------------------------------------------
;
; finition - Validation des plages horraires pour le rapport
;
FILE PDFINITION DESIGNER ALIAS A_FIN_FINITION

  ACCESS VIA     CIECLE,          &
                 FINDATPRD,       &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,                         &
                 FINDATPRD        OF PDFINITION,      &
                 OPENUMEMP        OF PDFINITION

  ;
  ; Sélectionne si une des deux bornes du nouvel enregistrement se retrouve
  ; parmis l'intervale déjà enregistré.
  ;

  SELECT IF ( (FINHREDEB OF PDFINITION >= FINHREDEB OF A_FIN_FINITION    &
               AND                                                       &
               FINHREDEB OF PDFINITION <= FINHREFIN OF A_FIN_FINITION )  &
             OR                                                          &
              (FINHREFIN OF PDFINITION >= FINHREDEB OF A_FIN_FINITION    &
               AND                                                       &
               FINHREFIN OF PDFINITION <= FINHREFIN OF A_FIN_FINITION )) &
            OR                                                           &
              (FINHREDEB OF PDFINITION <= FINHREDEB OF A_FIN_FINITION    &
               AND                                                       &
               FINHREFIN OF PDFINITION >= FINHREFIN OF A_FIN_FINITION )


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME REFERENCE OCCURS WITH PDDET_FINITION

  ACCESS VIA   CIECLE,      &
               DIANUM002    &
         USING T_CIECLEMNU, &
               DIANUM002 OF PDDET_FINITION


;-------------------------------------------------------------------------------
;
; Machine de finition
;

FILE PDMACH_DE_FINIT REFERENCE OCCURS WITH PDFINITION

  ACCESS VIA     CIECLE,          &
                 MFICODMAC        &
         USING   T_CIECLEMNU,     &
                 MFICODMAC        OF PDFINITION


;-------------------------------------------------------------------------------
;
; Unité de travail
;

FILE PDUNITE_TRAVAIL REFERENCE OCCURS WITH PDDET_FINITION

  ACCESS VIA     CIECLE,           &
                 UTRNUMUNITRV      &
         USING   T_CIECLEMNU ,     &
                 UTRNUMUNITRV     OF PDDET_FINITION


;-------------------------------------------------------------------------------
;
; Détail unité de travail
;

FILE PDDET_UNIT_TRAV REFERENCE

  ACCESS VIA     CIECLE,            &
                 UTRNUMUNITRV       &
         USING   T_CIECLEMNU,       &
                 UTRNUMUNITRV     OF PDDET_FINITION


;-------------------------------------------------------------------------------
;
; Unité de mesure
;

FILE PDUNITE_MESURE REFERENCE OCCURS WITH PDUNITE_TRAVAIL

  ACCESS VIA     CIECLE,             &
                 UNMCODUNM           &
         USING   T_CIECLEMNU,        &
                 UTRCODUNMPRD     OF PDUNITE_TRAVAIL


;-------------------------------------------------------------------------------
;
; Opérateur
;

FILE PDOPERATEUR REFERENCE

  ACCESS VIA     CIECLE,           &
                 OPENUMEMP         &
         USING   T_CIECLEMNU,      &
                 OPENUMEMP        OF PDFINITION


;-------------------------------------------------------------------------------
;
; Opérateur
;

FILE PDOPERATEUR DESIGNER ALIAS A_OPE_FIC


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDFINITION


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

DEFINE    D_OPENOM CHARACTER SIZE 40 = "Inconnu au système" &
                                    IF   OPEPNM OF PDOPERATEUR IS NULL &
                                         OR &
                                         OPENOM OF PDOPERATEUR IS NULL &
                                    ELSE TRUNCATE( OPEPNM OF PDOPERATEUR ) + &
                                         " " + &
                                         TRUNCATE( OPENOM OF PDOPERATEUR )

TEMPORARY T_NECOUP CHARACTER SIZE 1
TEMPORARY T_NOLIG  INTEGER SIZE 04 RESET AT STARTUP


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Finition " &
@ELSE
      " %%%Finition " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (2,3,19)


FIELD FINNUMRAP        OF PDFINITION       &
label "Numéro rapport:"&
        HIDDEN ID 05                       &
        DISPLAY


FIELD FINDATPRD        OF PDFINITION        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        HIDDEN ID 10                       &
        DEFAULT SYSDATE


ALIGN (2,3,19) (,,26)


FIELD OPENUMEMP        OF PDFINITION       &
label "Numéro employé:"&
        HIDDEN ID 15                       &
        NUMERIC                            &
        REQUIRED


FIELD D_OPENOM                             &
        DISPLAY


ALIGN (2,3,19) (,,21)


FIELD QRTCODQRT        OF PDFINITION       &
label "Quart travail.:"&
        HIDDEN ID 20


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY SIZE 19


ALIGN (2,3,19) ; (,,23) (,41,57) (,,60)

FIELD MFICODMAC        OF PDFINITION   &
label "Code machine.:"&
        HIDDEN ID 25                            &
        REQUIRED                           &
        LOOKUP ON PDMACH_DE_FINIT          &
          VIA     CIECLE,                  &
                  MFICODMAC                &
          USING   T_CIECLEMNU,                &
                  MFICODMAC OF PDFINITION &
          MESSAGE 2915 ; Ce code de machine n'existe pas



SKIP TO LINE 4


ALIGN (42,43,59)


FIELD FINHREDEB        OF PDFINITION       &
label "Hre début.....:"&
        HIDDEN ID 30


FIELD FINHREFIN        OF PDFINITION       &
label "Hre fin.......:"&
        HIDDEN ID 35


DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Détail finition " &
@ELSE
      " %%%Détail finition " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
    "Lig  UnT                          Unité mesure     Diagr.  Qte D     Qte F " &
@ELSE
    "Lig  UnT                          Unité mesure     Diagr.  Qte D     Qte F " &
@ENDIF
      AT ,2


ALIGN (2,2,2)(,,7)(,,12)(,,30)(,,34)(,,52)(,,61)(,,67)


CLUSTER OCCURS WITH PDDET_FINITION


FIELD DFINUMLIG        OF PDDET_FINITION   &
        HIDDEN                        size 3     &
        LABEL " "                          &
        NOCHANGE                           &
        LOOKUP NOTON PDDET_FINITION        &
          VIA    CIECLE,                   &
                 FINNUMRAP,                &
                 DFINUMLIG                 &
          USING  T_CIECLEMNU,                &
                 FINNUMRAP OF PDFINITION,    &
                 DFINUMLIG OF PDDET_FINITION &
          MESSAGE 3055 ; Ce numéro de ligne existe déjà


FIELD UTRNUMUNITRV     OF PDDET_FINITION       &
        HIDDEN                        &
        REQUIRED                           &
        LOOKUP ON PDUNITE_TRAVAIL          &
          VIA     CIECLE,                  &
                  UTRNUMUNITRV             &
          USING   T_CIECLEMNU,               &
                  UTRNUMUNITRV OF PDDET_FINITION &
          MESSAGE 2925 ; Cette unité n'existe pas

FIELD UTRDSC001        OF PDUNITE_TRAVAIL  &
        DISPLAY                            &
        SIZE 16

FIELD UTRCODUNMPRD     OF PDUNITE_TRAVAIL  &
        DISPLAY

FIELD UNMDSCFRA        OF PDUNITE_MESURE   &
        DISPLAY                            &
        SIZE 16


FIELD DIANUM002        OF PDDET_FINITION         &
        HIDDEN                                   &
        PATTERN "((^|#)>)"                       &
        LOOKUP  ON A_PDUNI_TRAV_DIA              &
           VIA  CIECLE                         , &
                DIANUM002                      , &
                UTRNUMUNITRV                     &
         USING  T_CIECLEMNU                    , &
                DIANUM002    OF PDDET_FINITION , &
                UTRNUMUNITRV OF PDDET_FINITION   &
       MESSAGE  3173      ; Cette unité de travail n'est pas
                          ; requis pour ce diagramme

FIELD DFIQTEDIA        OF PDDET_FINITION   &
        HIDDEN                size 4       &
        BWZ                                &
        IF 0 <> SIZE(TRUNCATE(DIANUM002 OF PDDET_FINITION))


FIELD DFIQTEPRD        OF PDDET_FINITION   &
        HIDDEN                             &
        OUTPUT SCALE 4                     &
        SIGNIFICANCE 6                     &
        PICTURE "^^^,^^^.^^^^"             &
        BWZ                                &
        REQUIRED

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs (codes bilingues)
;

PROCEDURE INPUT QRTCODQRT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    ;
    ; Appel du dépisteur pour le champs (codes bilingues)
    ;
    LET T_QRTCODQRT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd119 MODE F &
                     PASSING D_QRTCODQRT , &
                             T_QRTCODQRT,  &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_QRTCODQRT )
  END
  ELSE BEGIN
    ;
    ; Assigne une valeur par défaut pour le quart de travail
    ;
    IF 0 = SIZE(FIELDTEXT)
    THEN BEGIN
      LET FIELDTEXT = "J"
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'heure de début est plus petite que l'heure de fin
;

PROCEDURE EDIT FINHREFIN
BEGIN
  IF FIELDVALUE < FINHREDEB OF PDFINITION
  THEN BEGIN
    ERROR 3007 ; L'heure de début doit être plus petite que l'heure de fin
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;

PROCEDURE INPUT OPENUMEMP OF PDFINITION
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_OPENUMEMP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd110 MODE F &
                     PASSING T_OPENUMEMP, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_OPENUMEMP )
  END
END


;-------------------------------------------------------------------------------
;
; Valide le numéro d'opérateur
;

PROCEDURE EDIT OPENUMEMP OF PDFINITION
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    LET T_OPENUMEMP = ASCII(NCONVERT(FIELDTEXT),6)
    GET A_OPE_FIC OPTIONAL   &
      VIA     CIECLE,        &
              OPENUMEMP      &
      USING   T_CIECLEMNU,   &
              T_OPENUMEMP
    IF NOT ACCESSOK
    THEN BEGIN
      WARNING 3057 ; Le numéro d'opérateur n'existe pas
      DISPLAY D_OPENOM
    END
    ELSE BEGIN
      DISPLAY D_OPENOM
    END
  END
  ELSE BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END

PROCEDURE PROCESS OPENUMEMP OF PDFINITION
BEGIN
  LET OPENUMEMP OF PDFINITION = T_OPENUMEMP
END

;-------------------------------------------------------------------------------
;
; Procedure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT DFINUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT) &
     AND &
     DFINUMLIG OF PDDET_FINITION = 0
  THEN BEGIN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE PROCESS DFINUMLIG
BEGIN
  LET T_NOLIG = DFINUMLIG OF PDDET_FINITION
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;

PROCEDURE INPUT MFICODMAC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MFICODMAC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd111 MODE F &
                     PASSING T_MFICODMAC,  &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_MFICODMAC )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;
PROCEDURE INPUT DIANUM002
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DIANUM002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd126 MODE F &
                     PASSING T_DIANUM002,  &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DIANUM002 )
  END
  ;
  ; Validation du diagramme
  ;
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    LET T_DIANUM002 = TRUNCATE(FIELDTEXT)[1:7]
    GET PDDIAGRAMME OPTIONAL &
      VIA   CIECLE,          &
            DIANUM002        &
      USING T_CIECLEMNU,     &
            T_DIANUM002
    IF ACCESSOK
    THEN NULL
    ELSE ERROR 2998 ; Ce numéro de diagramme n'existe pas
  END
  ;
  ; Forcer le EDIT
  ;
  IF 0 =  SIZE(TRUNCATE(FIELDTEXT)) AND &
     0 <> SIZE(TRUNCATE(DIANUM002 OF PDDET_FINITION))
  THEN LET  FIELDTEXT = DIANUM002 OF PDDET_FINITION
END


;-------------------------------------------------------------------------------
;
PROCEDURE EDIT DIANUM002
BEGIN
  GET A_PDUNI_TRAV_DIA OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    IF UTDQTEUTRPRD OF A_PDUNI_TRAV_DIA > UTDQTEUTRCMD OF A_PDUNI_TRAV_DIA
    THEN WARNING "La quantité produite est suppérieur à la quantité à fabriquer"
  END
END


;-------------------------------------------------------------------------------
;
; Initialise les champ de l'enregistrement
;
PROCEDURE PROCESS DIANUM002
BEGIN
  LET PJTABR       OF PDDET_FINITION = DIANUM002 OF PDDET_FINITION[1:2]
  LET DIANUMDIA002 OF PDDET_FINITION = DIANUM002 OF PDDET_FINITION[3:5]
END


;-------------------------------------------------------------------------------
;
; Problème avec PowerHouse
;
PROCEDURE EDIT DFIQTEDIA
BEGIN
  GET PDUNI_TRAV_DIA OPTIONAL
END


;-------------------------------------------------------------------------------
;
; Problème avec PowerHouse
;

PROCEDURE EDIT DFIQTEPRD
BEGIN
  GET PDUNI_TRAV_DIA OPTIONAL
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;

PROCEDURE INPUT UTRNUMUNITRV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UTRNUMUNITRV = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd102 MODE F &
                     PASSING T_UTRNUMUNITRV, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UTRNUMUNITRV )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
  LET T_NOLIG = 0
END


;-------------------------------------------------------------------------------
;
; Recherche le dernier numéro de ligne utilisé
;

PROCEDURE POSTFIND
BEGIN
  GET DFI_OLD   OPTIONAL     &
    VIA     CIECLE,          &
            FINNUMRAP        &
    USING   T_CIECLEMNU,                   &
            FINNUMRAP        OF PDFINITION &
    ORDERBY CIECLE,          &
            FINNUMRAP,       &
            DFINUMLIG        DESCENDING
  IF ACCESSOK
  THEN BEGIN
    LET T_NOLIG = DFINUMLIG OF DFI_OLD
  END
END


;-------------------------------------------------------------------------------
;
; Arrêt finition
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd187a &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDFINITION          &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDFINITION &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDFINITION &
     AND NOT NEWRECORD     OF PDFINITION &
     AND NOT DELETEDRECORD OF PDFINITION &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Calcul de la durée du traitement pour l'opération de surface
  ;
  LET TZ_DATDEB   = FINDATPRD        OF PDFINITION
  LET TZ_TMPDEB   = ASCII(FINHREDEB  OF PDFINITION,4) + "00"
  LET TZ_DATFIN   = FINDATPRD        OF PDFINITION
  LET TZ_TMPFIN   = ASCII(FINHREFIN  OF PDFINITION,4) + "00"

  LET FINDURSEC OF PDFINITION = DZ_DIFSECTOT
  LET FINDURSECOPE OF PDFINITION = FINDURSEC OF PDFINITION

  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = FINNUMRAP OF PDFINITION
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_FINITION OPTIONAL         &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_FINITION = T_CIECLEMNU
    END
    LET SEINUMRAP OF PDSEQ_FINITION = SEINUMRAP OF PDSEQ_FINITION + 1
    LET FINNUMRAP OF PDFINITION     = SEINUMRAP OF PDSEQ_FINITION
    PUT PDSEQ_FINITION
    DISPLAY FINNUMRAP OF PDFINITION
  END
END
;--------------------------------------------------------------
PROCEDURE UPDATE
BEGIN
 PUT PDFINITION
 FOR PDDET_FINITION
  BEGIN
   PUT PDDET_FINITION
   GET B_PDUNI_TRAV_DIA
   PUT B_PDUNI_TRAV_DIA RESET
   PUT PDUNI_TRAV_DIA
  END
END



;-------------------------------------------------------------------------------
PROCEDURE EDIT MFICODMAC
BEGIN
GET PDFINITION_VALIDE OPT
 IF ACCESSOK
 THEN WARNING ="ATTENTION:Machine,Quart,Date existant"
END

;-------------------------------------------------------------------------------
;
; Permet de faire afficher le numéro de séquence
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY FINNUMRAP OF PDFINITION
    WARNING = " "
  END
END


BUILD DETAIL LIST

@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Finition ***********************************
* Numéro rapport: xxxxxxxxx               Hre début.....: xxxxx                *
* Date product..: xxxxxxxxxx              Hre fin.......: xxxxx                *
* Numéro employé: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
* Quart travail.: x xxxxxxxxxxxxxxxxxxx                                        *
* Code machine.:  xxx                                                          *
*                                                                              *
******************************* Détail finition ********************************
*Lig  UnT  Unité mesure          Diagr.                     Qte D     Qte F    *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
*xxx  xxx  xxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxxx  xxxxxxx  xxxx  xxxxxxxxxxxx *
********************************************************************************
@ENDIF
