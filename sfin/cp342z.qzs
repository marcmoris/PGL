;/T/ Achats par catégorie et par fournisseur
;
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Alain Côté / Adapté pour les CP par Thomas BRENNEUR
;    Date Création.: 6 Aout 1993
;
;    Description...: Pour consulter les achats par catégorie et par fournisseur
;                    et cela par année financière.
;
;    Paramètres....: Année (une seule)
;                    Catégorie (1, plusieurs,tous)
;
;
;
;------------------------------------------------------------------------------

USE ussetqzs NOLIST

;
;
; Dans la première étape on demande les catégories en paramètre, c'est pour
; cette raison que l'on accède la table fournisseur en premier.
;
;
ACCESS CPFOURNISSEUR

CHOOSE FOUCIECLE PARM , & ; Compagnie de la charte client
       FOUCAT    PARM     ; Catégorie

DEFINE D_ANNFIN CHARACTER SIZE 2 = PARM ; Année financière
DEFINE D_CIECLE CHARACTER SIZE 4 = PARM ; Compagnie du menu

SET SUBFILE NAME WCP342

REPORT SUMMARY &
  FOUCIECLE OF CPFOURNISSEUR &
  FOUCLE    OF CPFOURNISSEUR &
  FOUNOM    OF CPFOURNISSEUR &
  FOUCAT    OF CPFOURNISSEUR &
  D_CIECLE                   &
  D_ANNFIN

BUILD cp342za


;-------------------------------------------------------------------------------
;
;
; Lecture des statistiques selon la compagnie et l'année financière reçue
; de l'étape précédente.
;
;
;
ACCESS *WCP342 &
  ;
  ; Statistique.
  ;
  LINK FOUCIECLE OF WCP342  , &
       FOUCLE    OF WCP342  , &
       D_CIECLE  OF WCP342  , &
       D_ANNFIN  OF WCP342    &
    TO FOUCIECLE            , &
       FOUCLE               , &
       FOCCIECLE            , &
       FOSANN                   OF CPFOU_STAT &
  ;
  ; Catégorie, pour avoir la description.
  ;
  LINK ( "FOUCAT" , &
          FOUCAT OF WCP342 ) &
    TO XELNOM , &
       CBICLE OF     VCBI_DSC OPTIONAL



;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
             "Achats par catégorie et par fournisseur"
@ELSE
             "%%%Achats par catégorie et par fournisseur"
@ENDIF


DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP342"

;
; Compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; On fait le total annuel des statistiques pour l'impression.
;
DEFINE D_FOSCUMACH FLOAT SIZE 8 = FOSCUMACH01 OF CPFOU_STAT + &
                                  FOSCUMACH02 OF CPFOU_STAT + &
                                  FOSCUMACH03 OF CPFOU_STAT + &
                                  FOSCUMACH04 OF CPFOU_STAT + &
                                  FOSCUMACH05 OF CPFOU_STAT + &
                                  FOSCUMACH06 OF CPFOU_STAT + &
                                  FOSCUMACH07 OF CPFOU_STAT + &
                                  FOSCUMACH08 OF CPFOU_STAT + &
                                  FOSCUMACH09 OF CPFOU_STAT + &
                                  FOSCUMACH10 OF CPFOU_STAT + &
                                  FOSCUMACH11 OF CPFOU_STAT + &
                                  FOSCUMACH12 OF CPFOU_STAT + &
                                  FOSCUMACH13 OF CPFOU_STAT
;
DEFINE D_FOSCUMESC FLOAT SIZE 8 = FOSCUMESC01 OF CPFOU_STAT + &
                                  FOSCUMESC02 OF CPFOU_STAT + &
                                  FOSCUMESC03 OF CPFOU_STAT + &
                                  FOSCUMESC04 OF CPFOU_STAT + &
                                  FOSCUMESC05 OF CPFOU_STAT + &
                                  FOSCUMESC06 OF CPFOU_STAT + &
                                  FOSCUMESC07 OF CPFOU_STAT + &
                                  FOSCUMESC08 OF CPFOU_STAT + &
                                  FOSCUMESC09 OF CPFOU_STAT + &
                                  FOSCUMESC10 OF CPFOU_STAT + &
                                  FOSCUMESC11 OF CPFOU_STAT + &
                                  FOSCUMESC12 OF CPFOU_STAT + &
                                  FOSCUMESC13 OF CPFOU_STAT
;
DEFINE D_FOSNBRJRSPAM FLOAT SIZE 8 = FOSNBRJRSPAM01 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM02 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM03 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM04 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM05 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM06 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM07 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM08 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM09 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM10 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM11 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM12 OF CPFOU_STAT + &
                                     FOSNBRJRSPAM13 OF CPFOU_STAT
;
DEFINE D_FOSNBRPAM FLOAT SIZE 8 = FOSNBRPAM01 OF CPFOU_STAT + &
                                  FOSNBRPAM02 OF CPFOU_STAT + &
                                  FOSNBRPAM03 OF CPFOU_STAT + &
                                  FOSNBRPAM04 OF CPFOU_STAT + &
                                  FOSNBRPAM05 OF CPFOU_STAT + &
                                  FOSNBRPAM06 OF CPFOU_STAT + &
                                  FOSNBRPAM07 OF CPFOU_STAT + &
                                  FOSNBRPAM08 OF CPFOU_STAT + &
                                  FOSNBRPAM09 OF CPFOU_STAT + &
                                  FOSNBRPAM10 OF CPFOU_STAT + &
                                  FOSNBRPAM11 OF CPFOU_STAT + &
                                  FOSNBRPAM12 OF CPFOU_STAT + &
                                  FOSNBRPAM13 OF CPFOU_STAT


; Ajustement pour le changement de siècle
DEFINE D_FOSANN_SIE CHARACTER SIZE 3 = "1" + FOSANN OF CPFOU_STAT &
         IF FOSANN OF CPFOU_STAT[1:1] < "8" &
         ELSE "0" + FOSANN OF CPFOU_STAT

SORT ON D_FOSANN_SIE         & ; Année financière.
     ON FOUCAT OF WCP342     & ; Catégorie
     ON FOUCLE OF WCP342       ; Numéro de fournisseur


;
; Impression de l'en-tête.
;
USE ustitstd NOLIST
  SKIP 2 &
@IF FRANCAIS
    TAB 001 "Année financière:" &
@ELSE
    TAB 001 "Fiscal Year     :" &
@ENDIF
    TAB 019 FOSANN OF CPFOU_STAT &
  SKIP 2 &
    TAB 001 &
@IF FRANCAIS
            "                                                                                                          Montant      Delai moyen" &
@ELSE
            "%%%                                                                                                       Montant      Delai moyen" &
@ENDIF
  SKIP &
    TAB 001 &
@IF FRANCAIS
            "           Catégorie                                Fournisseur                         Achats            escompte       paiement" &
@ELSE
            "%%%        Catégorie                                Fournisseur                         Achats            escompte       paiement" &
@ENDIF
  SKIP &
    TAB 001 &
            " ------------------------------   -----------------------------------------------   ---------------   ---------------  ------------"



HEADING AT FOUCAT &
  SKIP PAGE


REPORT &
    TAB 002 FOUCAT OF WCP342   PRINT AT FOUCAT &
    TAB 007 CBIDSC OF VCBI_DSC PRINT AT FOUCAT &
    TAB 035 FOUCLE OF WCP342                   &
    TAB 042 FOUNOM OF WCP342                   &
    TAB 085 D_FOSCUMACH                        &
            PICTURE "^^^,^^^,^^^.^^ "          &
            TRAILING SIGN "-"                  &
            SIGNIFICANCE 5                     &
    TAB 103 D_FOSCUMESC                        &
            PICTURE "^^^,^^^,^^^.^^ "          &
            TRAILING SIGN "-"                  &
            SIGNIFICANCE 5                     &
    TAB 123 D_FOSNBRJRSPAM RATIO D_FOSNBRPAM   &
            PICTURE "^,^^^ "                   &
            TRAILING SIGN "-"                  &
            OUTPUT SCALE 0                     &
            SIGNIFICANCE 2



FOOTING AT FOUCAT &
  SKIP 2 &
    TAB 085 "---------------" &
    TAB 103 "---------------" &
    TAB 123 "------"          &
  SKIP 1 &
@IF FRANCAIS
    TAB 054 "Total catégorie:" &
@ELSE
    TAB 054 "Category Total :" &
@ENDIF
    TAB 085 D_FOSCUMACH    SUBTOTAL     &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 103 D_FOSCUMESC    SUBTOTAL     &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 123 D_FOSNBRJRSPAM RATIO D_FOSNBRPAM &
            PICTURE "^,^^^ "            &
            TRAILING SIGN "-"           &
            OUTPUT SCALE 0              &
            SIGNIFICANCE 2              &
 SKIP 2


FOOTING AT D_FOSANN_SIE             &
  SKIP 2                      &
    TAB 085 "---------------" &
    TAB 103 "---------------" &
    TAB 123 "------"          &
  SKIP 1                      &
    TAB 054 "Grand total    :"          &
    TAB 085 D_FOSCUMACH  SUBTOTAL       &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 103 D_FOSCUMESC  SUBTOTAL       &
            PICTURE "^^^,^^^,^^^.^^ "   &
            TRAILING SIGN "-"           &
            SIGNIFICANCE 5              &
    TAB 123 D_FOSNBRJRSPAM RATIO D_FOSNBRPAM &
            PICTURE "^,^^^ "            &
            TRAILING SIGN "-"           &
            OUTPUT SCALE 0              &
            SIGNIFICANCE 2              &
  SKIP 1 &
    TAB 085 "===============" &
    TAB 103 "===============" &
    TAB 123 "======"


BUILD cp342zb

@IF FORMAT

CP342                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                   Achats par catégorie et par fournisseur
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Année financière: 99

                                                                                                          Montant      Delai moyen
           Catégorie                                Fournisseur                         Achats            escompte       paiement
 ------------------------------   -----------------------------------------------   ---------------   ---------------  ------------
 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

 xxxx xxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ---------------   ---------------     ------
                                                     Total catégorie:               999,999,999.99-   999,999,999.99-     9,999-

                                                                                    ---------------   ---------------     ------
                                                     Grand total    :               999,999,999.99-   999,999,999.99-     9,999-
                                                                                    ===============   ===============     ======
@ENDIF
