;/T/ Terme d'encaissement
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 17 mai 1993
;
;    Description..: Cet écran permet la gestion des termes d'encaissement
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr153 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEHLD, &
                       T_CIENOMHLD

USE ussectmp  NOLIST
    "CR153"

USE cr153.hlp NOLIST

USE usactecr  NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-Ecrans"
  MENUITEM LABEL "Test - Calcul de date " ACTION DESIGNER CR01
  MENUITEM LABEL "Vérification          " ACTION DESIGNER CR02
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%test               " ACTION DESIGNER CR01
  MENUITEM LABEL "Verification          " ACTION DESIGNER CR02
@ENDIF


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEHLD CHARACTER SIZE 4 ; Numéro de cie du holding.
TEMPORARY T_CIENOMHLD CHARACTER SIZE 40; Nom du holding


;-------------------------------------------------------------------------------
;
;
FILE CRTERME          PRIMARY
  ACCESS REQUEST TMECLE &
         VIA     TMECLE &
         USING   TMECLE OF CRTERME &
         ORDERBY TMECLE

  ACCESS REQUEST TMECAT &
         VIA     TMECAT &
         USING   TMECAT OF CRTERME &
         ORDERBY TMECLE

  ACCESS SEQUENTIAL &
         ORDERBY TMECLE

  ITEM TMEDATCRE OF CRTERME INITIAL SYSDATE
  ITEM TMEHRECRE OF CRTERME INITIAL SYSTIME / 10000
  ITEM TMEUSRCRE OF CRTERME INITIAL LOGONID
  ITEM TMEDATMOD OF CRTERME FINAL   SYSDATE        &
                                            IF NOT NEWRECORD OF CRTERME AND &
                                               ALTEREDRECORD OF CRTERME
  ITEM TMEHREMOD OF CRTERME FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF CRTERME AND &
                                               ALTEREDRECORD OF CRTERME
  ITEM TMEUSRMOD OF CRTERME FINAL   LOGONID         &
                                            IF NOT NEWRECORD OF CRTERME AND &
                                               ALTEREDRECORD OF CRTERME

;
; Codes bilingue pour la catégorie.
;
DEFINE    D_TMECAT    CHARACTER SIZE 16 = "TMECAT"
TEMPORARY T_TMECAT    CHARACTER SIZE 4
;
FILE VCBI_DSC         REFERENCE &
                      ALIAS A_CBI_CAT

;
; Codes bilingue pour le type de calcul.
;
DEFINE    D_TMETYPCAL CHARACTER SIZE 16 = "TMETYPCAL"
TEMPORARY T_TMETYPCAL CHARACTER SIZE 4
;
FILE VCBI_DSC         REFERENCE &
                      ALIAS A_CBI_TYPCAL


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEHLD
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOMHLD )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Termes d'encaissement " &
@ELSE
      " Payment terms " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

ALIGN(3,3,19) (,30,46)(,,50)

FIELD TMECLE       OF CRTERME HIDDEN ID 05 &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON CRTERME &
          VIA TMECLE &
          USING TMECLE OF CRTERME &
          MESSAGE 679 ; ce terme existe deja

FIELD TMECAT       OF CRTERME &
        REQUIRED &
        LOOKUP ON A_CBI_CAT &
          VIA XELNOM, &
              CBICLE  &
          USING D_TMECAT, &
                TMECAT OF CRTERME &
          MESSAGE 680 ; cette categorie n'existe pas

FIELD CBIDSC OF A_CBI_CAT DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Description française" &
@ELSE
      "French description" &
@ENDIF
      AT ,3
SKIP

ALIGN(3,3,19)

FIELD TMEDSCFRA    OF CRTERME HIDDEN ID 10 &
        REQUIRED

FIELD TMEDSCABRFRA OF CRTERME HIDDEN ID 15 &
        DEFAULT TMEDSCFRA OF CRTERME

SKIP 1

TITLE &
@IF FRANCAIS
      "Description anglaise" &
@ELSE
      "English description" &
@ENDIF
      AT ,3

FIELD TMEDSCANG    OF CRTERME HIDDEN ID 20 &
        DEFAULT TMEDSCFRA OF CRTERME

FIELD TMEDSCABRANG OF CRTERME HIDDEN ID 25 &
        DEFAULT TMEDSCANG OF CRTERME

SKIP 1

ALIGN(3,3,19) (,,22)

FIELD TMETYPCAL    OF CRTERME HIDDEN ID 30 &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON A_CBI_TYPCAL &
          VIA XELNOM, &
              CBICLE  &
          USING D_TMETYPCAL, &
                TMETYPCAL OF CRTERME &
          MESSAGE 681 ; ce type de calcul n'existe pas

FIELD CBIDSC OF A_CBI_TYPCAL DISPLAY

SKIP 1

ALIGN(3,3,19) (,30,46) (,57,73)

FIELD TMENBRJRSAJT OF CRTERME HIDDEN ID 35

;
; champ conditionnel au type de terme
;
FIELD TMENBRMOI    OF CRTERME

FIELD TMENBRJRSDEL OF CRTERME

ALIGN(3,3,19)

SKIP 1
;
; champ conditionnel au type de terme
;
FIELD TMEPCT       OF CRTERME HIDDEN ID 45 &
        IF TMECAT OF CRTERME <> "NET"



;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les catégories(bilingue).
;
;
PROCEDURE INPUT TMECAT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMECAT = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TMECAT, T_TMECAT
    LET FIELDTEXT = TRUNC(T_TMECAT)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les types de calcul(bilingue).
;
;
PROCEDURE INPUT TMETYPCAL
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TMETYPCAL = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TMETYPCAL, T_TMETYPCAL
    LET FIELDTEXT = TRUNC(T_TMETYPCAL)
  END
END


;-------------------------------------------------------------------------------
;
;
; Le pourcentage n'est pas accessible pour le terme net.
;
;
PROCEDURE DESIGNER 45
BEGIN
  IF TMECAT OF CRTERME <> "NET"
  THEN ACCEPT TMEPCT OF CRTERME
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER CR01 NODATA &
                        HELP "Test du calcul de date"
BEGIN
  RUN SCREEN cr153a MODE E
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER CR02 &
                        HELP "Vérification des dates..."
BEGIN
  RUN SCREEN cr153b PASSING CRTERME
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CRTERME &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CRTERME &
     AND NOT NEWRECORD     OF CRTERME &
     AND NOT DELETEDRECORD OF CRTERME &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction d'un terme d'encaissement.
  ;
  IF DELETEDRECORD OF CRTERME
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le terme est référé.
    ;
    RUN SCREEN cr153x MODE SAME &
                      PASSING CRTERME , &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END

BUILD

@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Termes d'encaissement *****************************
*                                                                              *
* Code terme....: xxxx       Catégorie.....: xxx xxxxxxxxxxxxxxxxxxxxxxxxx     *
*                                                                              *
* Description française                                                        *
*   Longue......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   Abrégée.....: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Description anglaise                                                         *
*   Longue......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   Abrégée.....: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Type de calcul: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
*                                                                              *
* Nbr.jrs ajust.: xxxx       Nbr. mois.....: xxxx       Nbr.jrs. délai: xxxx   *
*                                                                              *
* Pourcentage...: xxxxxx                                                       *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
