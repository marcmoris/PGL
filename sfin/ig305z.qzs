;/T/ Impression de l'inventaire physique des diagrammes .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-01
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste de validation de l'inventaire physique de
;                   diagramme de Granicor.
;
;    Paramètres....: Compagnie
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INDIAGRAMME

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG305"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEF DPAGEDEB INT SIZE 4 = PARM
DEF DPAGEFIN INT SIZE 4 = PARM

SELECT INDIAGRAMME IF IDINUMPGE OF INDIAGRAMME >= DPAGEDEB AND &
                      IDINUMPGE OF INDIAGRAMME <= DPAGEFIN

SORT ON IDINUMPGE    OF INDIAGRAMME  &
     ON IDIDATINV    OF INDIAGRAMME &
     ON IDIHREINV    OF INDIAGRAMME &
     ON DIANUMDIA002 OF INDIAGRAMME

USE ustitgor132_45 NOLIST
   SKIP 3 &
     TAB 53 "DONNEES DE LA PAGE: " IDINUMPGE OF INDIAGRAMME &
   SKIP 4

HEADING AT IDINUMPGE SKIP PAGE &

  TAB 003 &
;                                                                                                 1         1         1         1
;       1         2         3         4         5         6         7         8         9         0         1         2         3
;34567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
"LOCALISATION  PROJET  DIAGRAMME     QUANTITÉ   COMMENTAIRE" &
SKIP 1 &
  TAB 003 &
"------------  ------  ---------     --------   -----------"

REPORT &
  TAB 007 IDINUMLOC                             &
  TAB 019 PJTABR          OF INDIAGRAMME    &
  TAB 025 DIANUMDIA002    OF INDIAGRAMME    &
  TAB 037 IDIQTEINV       OF INDIAGRAMME    &
  TAB 044 "     " &
  TAB 049 IDIDESC         OF INDIAGRAMME

FOOTING AT IDINUMPGE SKIP 2 &
    "NOMBRE D'ENTREES POUR CETTE PAGE..............:" COUNT

FINAL FOOT SKIP 2 &
    "GRAND TOTAL DE DIAGRAMMES EN INVENTAIRE.......:" &
        IDIQTEINV SUB PICT " ^^^,^^^,^^^.^^^^ " &
  TAB 064 "       "

BUILD ig305z
