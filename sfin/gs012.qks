;/T/ Ecran des descriptions
;
; PAR  : Francois Dery
; DATE : 91/08/21

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gs012 ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING D_XDECODMNE, D_XDENOM

USE usactecr NOLIST

DEF D_XDECODMNE CHAR SIZE 3 ; Mnémonique de la table
DEF D_XDENOM    CHAR SIZE 16; Nom de l'entité, ex: XELNOM

FILE GSDESCRIPTION PRIMARY NOITEM OCCURS 15 TIMES
  ACCESS VIAINDEX XDE_R01 &
         REQUEST  XDECODLNG &
         USING    D_XDECODMNE, D_XDENOM, XDECODLNG
  ITEM XDECODMNE OF GSDESCRIPTION INIT D_XDECODMNE
  ITEM XDENOM    OF GSDESCRIPTION INIT D_XDENOM
  ITEM XDECODLNG OF GSDESCRIPTION INIT FIRST(XDECODLNG OF GSDESCRIPTION)

;
; Les deux fichiers suivant servent à la copie de description à partir d'un
; objet d'origine.
;
FILE GSDESCRIPTION DESIGNER ALIAS XDE-LECT OPEN 1
FILE GSDESCRIPTION DESIGNER ALIAS XDE-PUT NEED ALL

DEF DZNOMECRAN    CHAR SIZE  7 = "GS012"
DEF DZTITLE       CHAR SIZE 78 = "Gestion des descriptions"


TEMP T-NOLIG    INT  SIZE  4 RESET AT STARTUP
TEMP T_NOMORI   CHAR SIZE 16 ; Sert à la copie, nom d'origine


DEFINE D_LANGUE CHAR SIZE 7 = "rançais" IF XDECODLNG OF GSDESCRIPTION = "F" &
                         ELSE "nlais"

;
;                 Information d'aide à l'utilisateur
;
DESCRIPTION OF SCREEN      &
" ",&
" ",&
" ",&
" ",&
" "
USE ushilite NOLIST
USE usdrwecr  NOLIST

@IF FRANCAIS
    TITLE " Documentation d'aide " CENTERED AT 2,1
@ELSE
    TITLE " Help Text " CENTERED AT 2,1
@ENDIF

SKIP TO 4
ALIGN(,3,19)
FIELD XDENOM OF GSDESCRIPTION FIXED

ALIGN(,3,19)(,,24)
FIELD XDECODLNG OF GSDESCRIPTION REQUIRED
FIELD D_LANGUE  DISPLAY &
@IF FRANCAIS
    LABEL "Langue........:"
@ELSE
    LABEL "Language......:"
@ENDIF

SKIP 1

@IF FRANCAIS
    TITLE "No. Ligne"   AT ,3
    TITLE "Description" AT ,15
@ELSE
    TITLE "Line No."    AT ,3
    TITLE "Description" AT ,15
@ENDIF

ALIGN(2,2,3)(,,15)
CLUSTER OCCURS WITH GSDESCRIPTION

FIELD XDENUMLIG OF GSDESCRIPTION LABEL " " HIDDEN
FIELD XDEDSC    OF GSDESCRIPTION
CLUSTER

;
;
; Assigne un numéro de ligne automatiquement.
;
;
PROCEDURE INPUT XDENUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT) AND &
     XDENUMLIG OF GSDESCRIPTION = 0
  THEN LET FIELDTEXT = ASCII(T-NOLIG + 1)
END

;
;
; Incrémente le numéro de ligne.
;
;
PROCEDURE PROCESS XDENUMLIG
BEGIN
  LET T-NOLIG = XDENUMLIG OF GSDESCRIPTION + 1
END

;
;
; Cette procédure sert à copier un description à partir d'un entité d'origine
;
;
PROCEDURE DESIGNER COPY NODATA
BEGIN
  INFO "Entrez l'entité d'origine:"
  PROMPT XDENOM INTO T_NOMORI
  WHILE RETRIEVING XDE-LECT VIAINDEX XDE_R01 USING D_XDECODMNE, T_NOMORI
  BEGIN
    LET XDECODMNE OF XDE-PUT = D_XDECODMNE
    LET XDENOM    OF XDE-PUT = D_XDENOM
    LET XDECODLNG OF XDE-PUT = XDECODLNG OF XDE-LECT
    LET XDENUMLIG OF XDE-PUT = XDENUMLIG OF XDE-LECT
    LET XDEDSC    OF XDE-PUT = XDEDSC    OF XDE-LECT
    PUT XDE-PUT RESET
  END
  DISPLAY XDENOM OF GSDESCRIPTION
END


BUILD
