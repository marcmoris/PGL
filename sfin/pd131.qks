;/T/ 5.2 Charges diverses
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-03-14
;
;    Description..: Permet de visionner et de choisir une charges diverses
;
;    Paramètres...: T_CDICODCHR        ==> Code de charges diverses
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd131  NOMODE NOACTION FIELDMARK &
              FROM 11,38 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND &
              RECEIVING T_CDICODCHR,&
                        T_CIECLEMNU


TEMPORARY T_CDICODCHR        CHARACTER SIZE 05
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04

USE pd131.hlp NOLIST

USE ustrasse NOLIST

;-------------------------------------------------------------------------------
;
; Charges diverses
;

FILE PDCHARGES_DIVERS PRIMARY OCCURS 11

  ACCESS VIA     CIECLE,          &
                 CDICODCHR        &
         USING   T_CIECLEMNU,     &
                 T_CDICODCHR      &
         ORDERBY CIECLE,          &
                 CDICODCHR


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,43       ; Draw pour les écrans de choix et consultations

USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

ALIGN(3,3,6)(,,12)


CLUSTER OCCURS WITH PDCHARGES_DIVERS

FIELD CDICODCHR    OF PDCHARGES_DIVERS HIDDEN LABEL ">>" DISPLAY

@IF FRANCAIS
  FIELD CDIDSCFRA        OF PDCHARGES_DIVERS DISPLAY SIZE 30
@ELSE
  FIELD CDIDSCANG        OF PDCHARGES_DIVERS DISPLAY SIZE 30
@ENDIF


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_CDICODCHR , "@")
  THEN LET T_CDICODCHR = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_CDICODCHR = CDICODCHR OF PDCHARGES_DIVERS
  RETURN
END

BUILD

@IF FORMAT

                                     1234567890123456789012345678901234567890123
                                     2 >> xxxxx 123456789012345678901234567890 *
                                     3 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
                                     4 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     5 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     6 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     7 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     8 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     9 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     0 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     1 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     2 >> xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                     3****************************************
@ENDIF
