;/T/ Consultation des C.A.P.           FOUCIECLE,FOUCLE,TYPECRPAT,FLGSLD,CAPCLE,REFCLENUM,CAPCIECLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 15 avril 1992
;
;    Description..: Permet de visionner et de choisir un compte à payer.
;                   Seul les factures (ou rapports de dépenses) journalisées
;                   seront sélectionnées.
;
;
;    Paramètre....: FOUCIECLE ==> - compagnie du fournisseur.
;                                 - segment de la clé de recherche.
;
;                   FOUCLE    ==> - numéro de fournisseur.
;                                 - segment de la clé de recherche.
;
;                   TYPECRPAT ==> - Type d'écriture.
;                                 - critère de sélection sous la forme de
;                                   PATTERN, ex: "FA|NC"
;
;                   FLGSLD    ==> - Indicateur de sélection.
;                                 - critère de sélection
;                                 - si = 1, facture à payer ou solde <> 0
;                                   si = 0, toutes les factures sans tenir
;                                   compte du solde.
;
;                   CAPCLE    ==> - numéro du compte à payer(facture).
;                                 - segment de la clé de recherche.
;                                 - peut contenir le caractère '@'.
;                                 - contient la valeur de retour.
;
;                   REFCLENUM ==> - Numéro de la référence. Permet de retrouver
;                                   les rapports de dépenses d'un employé.
;                                 - segment de la clé de recherche.
;                                 - peut contenir le caractère '@'.
;
;                   CAPCIECLE     - Numéro de la compagnie du document. Pour
;                                   ne retrouver que les pièces d'une cie.
;                                 - segment de la clé de recherche.
;
;-------------------------------------------------------------------------------
;
;/M/ Date modif...: 09-Mars-1994
;
;    Programmeur..: Guy Chabot
;
;    Description..: Ajout de CAPCIECLE dans le RECEIVING est dans le
;                   ACCESS de la vue pour ne référer que les pièces de
;                   la même compagnie.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp105 NOMODE NOACTION FIELDMARK &
             FROM 11,20 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_FOUCIECLE, &
                        T_FOUCLE, &
                        T_TYPECRPAT, &
                        T_FLGSLD, &
                        T_CAPCLE, &
                        T_REFCLENUM, &
                        T_CAPCIECLE

USE ustrasse NOLIST

USE ussectmp NOLIST
"CP105"

USE cp105.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_FOUCIECLE CHARACTER SIZE 4
TEMPORARY T_FOUCLE    CHARACTER SIZE 6
TEMPORARY T_TYPECRPAT CHARACTER SIZE 30
TEMPORARY T_FLGSLD    CHARACTER SIZE 1
TEMPORARY T_CAPCLE    CHARACTER SIZE 15
TEMPORARY T_REFCLENUM CHARACTER SIZE 6
TEMPORARY T_CAPCIECLE CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
FILE VCAP_REF   PRIMARY &
                OCCURS 10

 ACCESS   VIA     FOUCIECLE, &
                  FOUCLE   , &
                  CAPCIECLE, &
                  CAPCLE   , &
                  REFCLENUM  &
          USING   T_FOUCIECLE, &
                  T_FOUCLE   , &
                  T_CAPCIECLE, &
                  T_CAPCLE   , &
                  T_REFCLENUM  &
          ORDERBY FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE

  SELECT IF &
     MATCHPATTERN(CAPTYPECR OF VCAP_REF,TRUNC(T_TYPECRPAT)) &
     AND (                                    &
            0 <> (  CAPMNT      OF VCAP_REF   &
                  + V_CAPMNTAJT OF VCAP_REF   &
                  + V_CAPMNTCRT OF VCAP_REF   &
                  - V_CAPMNTPYE OF VCAP_REF   &
                  - CAPMNTAVA   OF VCAP_REF   &
                  - V_CAPMNTESC OF VCAP_REF ) &
            OR T_FLGSLD = "0"                 &
         )

;-------------------------------------------------------------------------------

DEFINE    D_SLD_A_PYE FLOAT SIZE 8  &
          = CAPMNT    OF VCAP_REF   &
          + V_CAPMNTAJT OF VCAP_REF &
          + V_CAPMNTCRT OF VCAP_REF &
          - V_CAPMNTPYE OF VCAP_REF &
          - CAPMNTAVA OF VCAP_REF   &
          - V_CAPMNTESC OF VCAP_REF

;-------------------------------------------------------------------------------

USE ushilite NOLIST
HILITE DISPLAY HALFTONE
DRAW 1,1 TO 13,60

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

@IF FRANCAIS
    TITLE "Numéro         Date            Brut        A payer" AT 2,6
@ELSE
    TITLE "Number         Date            Gross       To pay" AT 2,6
@ENDIF

ALIGN(3,3,6) (,,19) (,,30) (,,46)

CLUSTER OCCURS WITH VCAP_REF

FIELD CAPCLE    OF VCAP_REF HIDDEN LABEL ">>" &
        SIZE 12
FIELD CAPDAT    OF VCAP_REF FORMAT YYYYMMDD PATTERN "####/##/##"
FIELD CAPMNT    OF VCAP_REF
FIELD D_SLD_A_PYE &
        PICTURE "^,^^^,^^^.^^ " TRAIL SIGN "-" SIGNIF 5

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CAPCLE, "@")
  THEN LET T_CAPCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CAPCLE = CAPCLE OF VCAP_REF
  RETURN
END

BUILD
@IF FORMAT

                       *********************************************************
                       *    Numéro         Date            Brut        A payer *
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                       *********************************************************
@ENDIF
