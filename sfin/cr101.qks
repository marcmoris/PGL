;/T/ Consultation des lots             CIECLE, LCRTYPECR, PECCLE, LCRCLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 2 juin 1993
;
;    Description..: Permet de visionner et de choisir un lot.
;                   Les lots sont sélectionnés par usager(LOGONID) et il
;                   doivent être journalisé.
;
;
;    Paramètre....: CIECLE ==> numéro de compagnie du menu.
;                              segment de la clé de recherche.
;
;                   LCRTYPECR ==> Type de lot.
;                                 segment de la clé de recherche.
;
;                   PECCLE ==> numéro de période.
;                              segment de la clé de recherche.
;
;                   LCRCLE ==> numéro de lot.
;                              segment de la clé de recherche, peut contenir
;                              le caractére '@'.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN cr101 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU, &
                       T_LCRTYPECR, &
                       T_PECCLE   , &
                       T_LCRCLE

USE cr101.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Numéro de compagnie du menu.
TEMPORARY T_LCRTYPECR CHARACTER SIZE 2
TEMPORARY T_PECCLE    CHARACTER SIZE 4
TEMPORARY T_LCRCLE    CHARACTER SIZE 4


;-------------------------------------------------------------------------------
;
FILE CRLOT            PRIMARY &
                      OCCURS 10

 ACCESS VIA CIECLE, &
            PECCLE, &
            LCRCLE, &
            LCRTYPECR &
        USING T_CIECLEMNU, &
              T_PECCLE   , &
              T_LCRCLE   , &
              T_LCRTYPECR  &
        ORDERBY CIECLE, &
                PECCLE, &
                LCRCLE

 SELECT IF LCRUSR    OF CRLOT = LOGONID AND &
           LCRDATJOU OF CRLOT = 0       AND &
           LCRATRJOU OF CRLOT = "0"


;
; Pour afficher la description du type d'écriture.
;
DEFINE D_LCRTYPECR CHAR SIZE 16 = "LCRTYPECR"
;
FILE VCBI_DSC         REFERENCE &
                      OCCURS WITH CRLOT
  ACCESS VIA XELNOM, &
             CBICLE &
         USING D_LCRTYPECR, &
               LCRTYPECR OF CRLOT


;-------------------------------------------------------------------------------
;
USE usdrwfnd NOLIST
USE ushilite NOLIST

USE ustitoff NOLIST

SKIP TO 2

@IF FRANCAIS
 TITLE "Lot     Type            Date val.  " AT 2,6
@ELSE
 TITLE "Batch   Type            Date val. " AT 2,5
@ENDIF

ALIGN(3,3,6) (,,11) (,,14) (,,30)

CLUSTER OCCURS WITH CRLOT

FIELD LCRCLE    OF CRLOT HIDDEN LABEL ">>"

FIELD LCRTYPECR OF CRLOT

FIELD CBIDSC    OF VCBI_DSC SIZE 15 &
        DISPLAY

FIELD LCRDATVAL OF CRLOT FORMAT YYYYMMDD PATTERN "####/##/##"

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_LCRCLE, "@")
  THEN LET T_LCRCLE = ""
END


;-------------------------------------------------------------------------------
;
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_LCRCLE = LCRCLE OF CRLOT
  RETURN
END

BUILD

;                                       *****************************************
;                                       *    Lot     Type            Date val.  *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       *****************************************
