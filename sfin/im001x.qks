;/T/ Gestion des immobilisations - Validation de la destruction
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 03 décembre 1995
;
;    Description..: Cet écran vérifie si la fiche de projet reçue en
;                   paramètre est référée dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING IMIMMOBILISATION, &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Définitions de projets
;
FILE IMIMMOBILISATION MASTER

FILE CRCAR_IMPUTATION REFERENCE
FILE CPPAM_IMPUTATION REFERENCE
FILE CRDPO_IMPUTATION REFERENCE
FILE GLIMPUTATION     REFERENCE

;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN

  GET CRCAR_IMPUTATION VIA   CIECLE,    &
                             IMMCLE     &
                       USING CIECLE OF IMIMMOBILISATION, &
                             IMMCLE OF IMIMMOBILISATION  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 1623

  GET CPPAM_IMPUTATION VIA   CIECLE,    &
                             IMMCLE     &
                       USING CIECLE OF IMIMMOBILISATION, &
                             IMMCLE OF IMIMMOBILISATION  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 1624

  GET CRDPO_IMPUTATION VIA   CIECLE,    &
                             IMMCLE     &
                       USING CIECLE OF IMIMMOBILISATION, &
                             IMMCLE OF IMIMMOBILISATION  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 1626

  GET GLIMPUTATION     VIA   CIECLE,    &
                             IMMCLE     &
                       USING CIECLE OF IMIMMOBILISATION, &
                             IMMCLE OF IMIMMOBILISATION  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 1625

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
