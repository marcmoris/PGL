;/T/ 5.3 Type de chariot (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-10-04
;
;    Description..: Ce panorama permet de saisir les types de chariot
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd202 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD202"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd202.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;
TEMPORARY T_TYCCODCHA CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Type de chariot
;

FILE PDTYPE_CHARIOT PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 TYCCODCHA &
         USING   T_CIECLEMNU,                &
                 TYCCODCHA OF PDTYPE_CHARIOT &
         REQUEST TYCCODCHA OF PDTYPE_CHARIOT &
         ORDERBY CIECLE,&
                 TYCCODCHA

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 TYCCODCHA

   ITEM CIECLE OF PDTYPE_CHARIOT INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Type de chariot " &
@ELSE
      " %%%Type de chariot " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD TYCCODCHA    OF PDTYPE_CHARIOT        &
        HIDDEN ID 05                        &
        REQUIRED NOCHANGE                   &
        LOOKUP NOTON PDTYPE_CHARIOT         &
          VIA   CIECLE,                     &
                TYCCODCHA                   &
          USING T_CIECLEMNU,                &
                TYCCODCHA OF PDTYPE_CHARIOT &
          MESSAGE 2970 ; Ce type de chariot existe déjà


SKIP 1


FIELD TYCDSCFRA    OF PDTYPE_CHARIOT       &
        HIDDEN ID 10                       &
        REQUIRED


FIELD TYCDSCANG    OF PDTYPE_CHARIOT       &
        HIDDEN ID 20                       &
        REQUIRED


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs
;

PROCEDURE INPUT TYCCODCHA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYCCODCHA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd120 MODE F &
                     PASSING T_TYCCODCHA, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYCCODCHA )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDTYPE_CHARIOT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDTYPE_CHARIOT &
     AND NOT NEWRECORD     OF PDTYPE_CHARIOT &
     AND NOT DELETEDRECORD OF PDTYPE_CHARIOT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Type de chariot ********************************
*                                                                              *
* Type chariot..: xxx                                                          *
*                                                                              *
* Descr. fran...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Descr. angl...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
