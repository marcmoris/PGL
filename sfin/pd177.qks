;/T/ 2.1.1 Enregistrer la carte de bloc
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-06
;
;    Description..: Le but de cette unit'e de traitement est de gérer
;                   l'inventaire de bloc de granit de Granicor.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd177 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             AUTOUPDATE                   &
             ACTIVITIES FIND,CHANGE       &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD177"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd177.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Réception                       " ACTION DESIGNER D001
  MENUITEM LABEL "Bon de commande (P/O)           " ACTION DESIGNER D002
  MENUITEM LABEL "Montage                         " ACTION DESIGNER D003
  MENUITEM LABEL "Sortie                          " ACTION DESIGNER D004
  MENUITEM LABEL "Redimentionnement               " ACTION DESIGNER D005
  MENUITEM LABEL "Sciage                          " ACTION DESIGNER D006
  MENUITEM LABEL "Réservation                     " ACTION DESIGNER D007
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D008
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Réception                    " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Bon de commande (P/O)        " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Montage                      " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Sortie                       " ACTION DESIGNER D004
  MENUITEM LABEL "%%%Redimentionnement            " ACTION DESIGNER D005
  MENUITEM LABEL "%%%Sciage                       " ACTION DESIGNER D006
  MENUITEM LABEL "%%%Réservation                  " ACTION DESIGNER D007
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D008
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_RDBDATRED INT SIZE 4 RESET AT STARTUP

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Clés de recherche pour les popup de consultations et les sous écrans
;

TEMPORARY T_FOUCLE    CHARACTER SIZE 6


TEMPORARY T_FOUNO      CHARACTER SIZE 6  RESET AT STARTUP
TEMPORARY T_FOUNOM     CHARACTER SIZE 40 RESET AT STARTUP

TEMPORARY T_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_BLONUMGRA002APR  CHARACTER SIZE 05
TEMPORARY T_BLORECAPR        CHARACTER SIZE 01
TEMPORARY T_CHAMP            INTEGER UNSIGNED SIZE 01

TEMPORARY T_COD              INTEGER UNSIGNED SIZE 02
TEMPORARY T_STU              CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
;
;

TEMPORARY T_SILENT  CHAR SIZE 01


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC PRIMARY &
            NODELETE

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR                             &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC,            &
                 BLORECAPR       OF PDBLOC             &
         REQUEST BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC,            &
                 BLORECAPR       OF PDBLOC             &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR                       &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC             &
         REQUEST BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC             &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR                       &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC             &
         REQUEST BLONUMGRA001APR OF PDBLOC             &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE                                &
         USING   T_CIECLEMNU                           &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

         ITEM CIECLE OF PDBLOC INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Détail réception bloc
;

FILE PDRECEPTION_BLOC REFERENCE

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR                             &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC,            &
                 BLORECAPR       OF PDBLOC             &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

;------------------------------------------------------------------------
;
FILE PDFACTURE REFERENCE
  ACCESS VIA     CIECLE,                               &
                 FTRNUMFAC                             &
         USING   T_CIECLEMNU,                          &
                 FTRNUMFAC OF PDBLOC

;-------------------------------------------------------------------------------
;
; Réception
;

FILE PDRECEPTION REFERENCE

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC OF PDRECEPTION_BLOC         &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 BCMNUMBCM



;-------------------------------------------------------------------------------
;
; Bon de commande
;

FILE PDBON_COMMANDE REFERENCE

  ACCESS VIA     CIECLE,     &
                 BCMNUMBCM   &
         USING   T_CIECLEMNU,&
                 BCMNUMBCM OF PDRECEPTION


;-------------------------------------------------------------------------------
;
; Détail du bon de commande
;

FILE PDDET_BON_COMMAN REFERENCE

  ACCESS VIA     CIECLE,                         &
                 BCMNUMBCM,                      &
                 PODNUMLIG                       &
         USING   T_CIECLEMNU,                    &
                 BCMNUMBCM OF PDBON_COMMANDE,    &
                 PODNUMLIG OF PDRECEPTION_BLOC


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TGRCODGRA                       &
         USING   T_CIECLEMNU,                    &
                 TGRCODGRA OF PDBLOC


;-------------------------------------------------------------------------------
;
; Fournisseurs
;

FILE VFOC_FOU REFERENCE

  ACCESS VIA     FOUCLE,                      &
                 FOCCIECLE,                   &
                 FOCUSISEC                    &
         USING   FOUCLE    OF PDBON_COMMANDE, &
                 T_CIECLEMNU,                 &
                 "N"


;-------------------------------------------------------------------------------
;
; Détail rapport de montage
;

FILE PDDET_RAP_MONT REFERENCE

  ACCESS VIA     CIECLE,                      &
                 BLONUMGRA001APR,             &
                 BLONUMGRA002APR,             &
                 BLORECAPR                    &
         USING   T_CIECLEMNU,                 &
                 BLONUMGRA001APR OF PDBLOC,   &
                 BLONUMGRA002APR OF PDBLOC,   &
                 BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE REFERENCE

  ACCESS VIA     CIECLE,          &
                 RMSNUMSEQ        &
         USING   T_CIECLEMNU,     &
                 RMSNUMSEQ        OF PDDET_RAP_MONT


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC REFERENCE

  ACCESS VIA     CIECLE,                      &
                 BLONUMGRA001APR,             &
                 BLONUMGRA002APR,             &
                 BLORECAPR                    &
         USING   T_CIECLEMNU,                 &
                 BLONUMGRA001APR OF PDBLOC,   &
                 BLONUMGRA002APR OF PDBLOC,   &
                 BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Sciage bloc
;

FILE PDSCIAGE_BLOCS REFERENCE

  ACCESS VIA     CIECLE,          &
                 RMSNUMSEQ        &
         USING   T_CIECLEMNU,     &
                 RMSNUMSEQ        OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Réservation de bloc
;

FILE PDRESER_BLOCS REFERENCE

  ACCESS VIA     CIECLE,                      &
                 BLONUMGRA001APR,             &
                 BLONUMGRA002APR,             &
                 BLORECAPR                    &
         USING   T_CIECLEMNU,                 &
                 BLONUMGRA001APR OF PDBLOC,   &
                 BLONUMGRA002APR OF PDBLOC,   &
                 BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                      &
                 SCINUMSCI                    &
         USING   T_CIECLEMNU,                 &
                 SCINUMSCI OF PDRAP_MONTAGE



;-------------------------------------------------------------------------------
;
; Sortie de blocs
;

FILE PDSORTIE_BLOCS REFERENCE

  ACCESS VIA     CIECLE,                      &
                 BLONUMGRA001APR,             &
                 BLONUMGRA002APR,             &
                 BLORECAPR                    &
         USING   T_CIECLEMNU,                 &
                 BLONUMGRA001APR OF PDBLOC,   &
                 BLONUMGRA002APR OF PDBLOC,   &
                 BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Sortie de produits
;

FILE PDSORTIE_PRODUIT REFERENCE

  ACCESS VIA     CIECLE,                      &
                 SPRNUMSOR                    &
         USING   T_CIECLEMNU,                 &
                 SPRNUMSOR OF PDSORTIE_BLOCS


;-------------------------------------------------------------------------------
;
; Redimension de bloc
;

FILE PDREDIM_BLOC  REFERENCE

  ACCESS VIA     CIECLE,                      &
                 BLONUMGRA001APR,             &
                 BLONUMGRA002APR,             &
                 BLORECAPR                    &
         USING   T_CIECLEMNU,                 &
                 BLONUMGRA001APR OF PDBLOC,   &
                 BLONUMGRA002APR OF PDBLOC,   &
                 BLORECAPR       OF PDBLOC


;-------------------------------------------------------------------------------
;
; Assignation de bloc pour l'historique
;

FILE PDCOMMAN_ASSIGN REFERENCE

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR                             &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC,            &
                 BLORECAPR       OF PDBLOC
;------------------------------------------------------------------------
FILE PDCOMMAN_INTERNE DESIGNER
  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_ASSIGN, &
                 PJTANN           OF PDCOMMAN_ASSIGN, &
                 COMNUMCOMINT     OF PDCOMMAN_ASSIGN


;-------------------------------------------------------------------------------
;
; Projet
;

FILE PDPROJET REFERENCE

  ACCESS VIA     CIECLE,                      &
                 PJTABR,                      &
                 PJTANN,                      &
                 PJTNUMSEQ                    &
         USING   T_CIECLEMNU,                 &
                 PJTABR          OF PDCOMMAN_ASSIGN, &
                 PJTANN          OF PDCOMMAN_ASSIGN, &
                 PJTNUMSEQ       OF PDCOMMAN_ASSIGN

;-------------------------------------------------------------------------------
;
; Définition de variable temporaire pour le traitement
;
DEFINE D_M3_BRU FLOAT SIZE 8 = (BLOLONBRU OF PDBLOC / 1000 ) * &
                               (BLOHAUBRU OF PDBLOC / 1000 ) * &
                               (BLOLARBRU OF PDBLOC / 1000 )

DEFINE D_M3_NET FLOAT SIZE 8 = (BLOLONNET OF PDBLOC / 1000 ) * &
                               (BLOHAUNET OF PDBLOC / 1000 ) * &
                               (BLOLARNET OF PDBLOC / 1000 )

DEFINE D_M3_PYE FLOAT SIZE 8 = (BLOLONPYE OF PDBLOC / 1000 ) * &
                               (BLOHAUPYE OF PDBLOC / 1000 ) * &
                               (BLOLARPYE OF PDBLOC / 1000 )


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Bloc " &
@ELSE
      " %%%Bloc " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19) (,23,24) (,29,30) (35,36,52) (,56,72)


FIELD BLONUMGRA001APR OF PDBLOC            &
        HIDDEN ID 05                       &
@IF FRANCAIS
        LABEL "Nun. bloc.....:"            &
@ELSE
        LABEL "%%%. bloc.....:"            &
@ENDIF
        AUTONEXT                           &
        NUMERIC                            &
        NOCORRECT                          &
        NOCHANGE


FIELD BLONUMGRA002APR  OF PDBLOC           &
        ID SAME                            &
        LABEL "-"                          &
        AUTONEXT                           &
        NUMERIC                            &
        NOCORRECT                          &
        NOCHANGE


FIELD BLORECAPR        OF PDBLOC           &
        ID SAME                            &
        LABEL "-"                          &
        NOCORRECT                          &
        NOCHANGE


FIELD T_SILENT                             &
        SILENT


FIELD BLOSTU           OF PDBLOC           &
label "Statut........:"&
        HIDDEN ID 20


FIELD BLONUMCARBLO     OF PDBLOC           &
        label "Num. carte....:"&
        HIDDEN ID 23                       &
        DISPLAY                        size 6


SKIP TO LINE 6


ALIGN (,3,19) (,,23)


FIELD TGRCODGRA        OF PDBLOC           &
label "Code granit...:"&
        HIDDEN ID 25                       &
        NUMERIC size 3                            &
        DISPLAY                            &
        BWZ


FIELD TGRDSCFRA001     OF PDTYPE_GRANIT    &
        DISPLAY


FIELD BLONUMLOC         OF PDBLOC          &
        HIDDEN ID 27                        &
        LABEL "Localisation..:"

SKIP TO LINE 9


ALIGN (2,3,19)


FIELD BLOLONBRU        OF PDBLOC           &
label "Longueur brut.:"&
        HIDDEN ID 30   size 4                    &
        DISPLAY

FIELD BLOHAUBRU        OF PDBLOC           &
label "Hauteur brut..:"&
        HIDDEN ID 35  size 4                     &
        DISPLAY

FIELD BLOLARBRU        OF PDBLOC           &
label "Largeur brut..:"&
        HIDDEN ID 40   size 4                    &
        DISPLAY


ALIGN (,3,19)


FIELD BLOVOLMC3BRU     OF PDBLOC           &
label "Volume brut...:"&
        DISPLAY             size 9               &
        OUTPUT SCALE 4


SKIP TO LINE 9


ALIGN (,29,45)


FIELD BLOLONNET        OF PDBLOC           &
label "Longeur nette:"&
        DISPLAY size 4


FIELD BLOHAUNET        OF PDBLOC           &
label "Hauteur nette.:"&
        DISPLAY size 4


FIELD BLOLARNET        OF PDBLOC           &
label "Largeur nette.:"&
        DISPLAY size 4


FIELD BLOVOLMC3NET     OF PDBLOC           &
label "Volume net....:"&
        DISPLAY         size 9                   &
        OUTPUT SCALE 4


SKIP TO LINE 9


ALIGN (,55,71)


FIELD BLOLONPYE        OF PDBLOC           &
label "Longeur payer:"&
        DISPLAY  size 4


FIELD BLOHAUPYE        OF PDBLOC           &
label "Hauteur payer.:"&
        DISPLAY size 4


FIELD BLOLARPYE        OF PDBLOC           &
label "Largeur payer.:"&
        DISPLAY size 4


FIELD BLOVOLMC3PYE     OF PDBLOC           &
label "Volume à payer:"&
        DISPLAY          size 9                  &
        OUTPUT SCALE 4


ALIGN (,3,19) (,,30)


SKIP TO LINE 14


FIELD REBNUMBLOFOU     OF PDRECEPTION_BLOC &
label "No bloc fourn.:"&
        DISPLAY


FIELD FOUNOM       OF VFOC_FOU                &
        DISPLAY                               &
        NOLABEL


DRAW 16,1 TO 16,80


SKIP TO LINE 16


TITLE &
@IF FRANCAIS
      " Historique " &
@ELSE
      " %%%Historique " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 18


ALIGN (,3,19) (,21,22) (,24,25) (,,30)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        DISPLAY


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        LABEL "-"


FIELD COMNUMCOMINT      OF PDCOMMAN_INTERNE &
        LABEL "-"                           &
        DISPLAY

FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY


ALIGN (,3,19) (,40,56)


FIELD RCTDATREC        OF PDRECEPTION       FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date réception:"&
        DISPLAY


FIELD BCMDATCRE        OF PDBON_COMMANDE    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        DISPLAY                            &
        LABEL "Date bon CMD..:"


FIELD RMODATPRD        OF PDRAP_MONTAGE     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date banquage.:"&
        DISPLAY


FIELD SPRDATSOR        OF PDSORTIE_PRODUIT  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
Label "Date sortie...:"&
        DISPLAY


FIELD RDBDATRED        OF PDREDIM_BLOC      FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date redimen..:"&
        DISPLAY


FIELD SCBDATSCI        OF PDSCIAGE_BLOCS    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date sciage...:"&
        DISPLAY

ALIGN (,3,19) (,37,53) (,,67)

FIELD RBLDATRES        OF PDRESER_BLOCS     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date réserv...:"&
        DISPLAY


FIELD FTRDATFAC OF PDFACTURE  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date facture..:"&
      DISPLAY

FIELD FTRNUMFAC OF PDFACTURE &
      DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA001APR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLONUMGRA002APR  = ""
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 1
    LET T_COD              = 0
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA001APR )
  END
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attention cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA002APR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLONUMGRA002APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 2
    LET T_COD              = 0
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDBLOC = TRUNCATE( T_BLONUMGRA001APR )
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLONUMGRA002APR ) ) AND &
     0 =  SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END
END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLORECAPR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLONUMGRA002APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLORECAPR        = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CHAMP            = 3
    LET T_COD              = 0
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDBLOC = TRUNCATE( T_BLONUMGRA001APR )
    LET BLONUMGRA002APR OF PDBLOC = TRUNCATE( T_BLONUMGRA002APR )
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLORECAPR ) ) AND &
     0 = SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
END


;-------------------------------------------------------------------------------
;
; Valide le statut du bloc et permet de l'annuler
;

PROCEDURE EDIT BLOSTU
BEGIN
  IF ((OLDVALUE(BLOSTU OF PDBLOC) = "I") OR &
      (OLDVALUE(BLOSTU OF PDBLOC) = "C") OR &
      (OLDVALUE(BLOSTU OF PDBLOC) = "D"))
  THEN BEGIN
   IF (BLOSTU OF PDBLOC <> "I" AND BLOSTU OF PDBLOC <> "C" AND BLOSTU OF PDBLOC <> "D")
   THEN BEGIN
     ERROR 3068 ; Le statut ne permet pas cette opération
   END
  END
  ELSE
    ERROR 3068
END



;-------------------------------------------------------------------------------
;
; retrouver les liens avec les montages
;

PROCEDURE PROCESS T_SILENT
BEGIN
  GET PDDET_RAP_MONT OPTIONAL
  GET PDRAP_MONTAGE  OPTIONAL
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE POSTFIND
BEGIN
  LET T_FOUNO =  FOUCLE       OF VFOC_FOU
  LET T_FOUNOM = FOUNOM       OF VFOC_FOU
END


;-------------------------------------------------------------------------------
;
; Réception
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd177a &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                      PDRECEPTION ,      &
                      PDRECEPTION_BLOC   &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Bon de commande (P/O)
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd177b &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                      PDBON_COMMANDE,    &
                      PDDET_BON_COMMAN   &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Montage
;

PROCEDURE DESIGNER D003
BEGIN
  RUN SCREEN  pd177c &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                      PDRAP_MONTAGE,     &
                      PDMONTAGE_BLOC,    &
                      PDSCIES            &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Sortie
;

PROCEDURE DESIGNER D004
BEGIN
  RUN SCREEN  pd177d &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                      PDSORTIE_PRODUIT,  &
                      PDSORTIE_BLOCS     &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Redimension
;

PROCEDURE DESIGNER D005
BEGIN
  LET T_RDBDATRED = RDBDATRED OF PDREDIM_BLOC
  RUN SCREEN  pd177e &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      T_FOUNO,            &
                      T_FOUNOM,           &
                      T_RDBDATRED,        &
                      PDBLOC,             &
                      PDRECEPTION,        &
                      PDRECEPTION_BLOC    &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Sciage
;

PROCEDURE DESIGNER D006
BEGIN
  RUN SCREEN  pd177f &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                       PDBLOC,           &
                       PDSCIES,          &
                       PDSCIAGE_BLOCS    &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Réservation
;

PROCEDURE DESIGNER D007
BEGIN
  RUN SCREEN  pd177g &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                      PDBLOC             &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Vérification
;

PROCEDURE DESIGNER D008
BEGIN
  RUN SCREEN  pd177h &
              PASSING T_CIECLEMNU,       &
                      T_CIENOM,          &
                      PDBLOC             &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDBLOC &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDBLOC &
     AND NOT NEWRECORD     OF PDBLOC &
     AND NOT DELETEDRECORD OF PDBLOC &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Évaluation des champs indiquant une mise à jour de la réception
  ;
  IF NOT NEWRECORD OF PDBLOC AND &
     ALTEREDRECORD OF PDBLOC
  THEN BEGIN
    LET BLODATDERMAJ     OF PDBLOC = SYSDATE
    LET BLOHREDERMAJ     OF PDBLOC = SYSTIME / 10000
    LET BLOUSRDERMAJ     OF PDBLOC = LOGONID
  END
END

;---------------------------------------------------------------------------
;
 PROCEDURE FIND
   BEGIN
     IF PATH = 1
       THEN BEGIN
           GET PDBLOC VIA CIECLE , BLONUMGRA001APR , BLONUMGRA002APR &
           , BLORECAPR ORDERBY CIECLE , BLONUMGRA001APR , &
           BLONUMGRA002APR , BLORECAPR USING T_CIECLEMNU , &
           BLONUMGRA001APR OF PDBLOC , BLONUMGRA002APR OF PDBLOC , &
           BLORECAPR OF PDBLOC
           GET PDCOMMAN_ASSIGN OPT &
           VIA     CIECLE,                               &
                   BLONUMGRA001APR,                      &
                   BLONUMGRA002APR,                      &
                   BLORECAPR                             &
           USING   T_CIECLEMNU,                          &
                   BLONUMGRA001APR OF PDBLOC,            &
                   BLONUMGRA002APR OF PDBLOC,            &
                   BLORECAPR       OF PDBLOC
           GET PDCOMMAN_INTERNE  OPT &
           VIA     CIECLE,          &
                   PJTABR,          &
                   PJTANN,          &
                   COMNUMCOMINT     &
           USING   T_CIECLEMNU,                          &
                   PJTABR           OF PDCOMMAN_ASSIGN, &
                   PJTANN           OF PDCOMMAN_ASSIGN, &
                   COMNUMCOMINT     OF PDCOMMAN_ASSIGN

      END
     IF PATH = 2
       THEN BEGIN
           GET PDBLOC VIA CIECLE , BLONUMGRA001APR , BLONUMGRA002APR &
           ORDERBY CIECLE , BLONUMGRA001APR , BLONUMGRA002APR , &
           BLORECAPR USING T_CIECLEMNU , BLONUMGRA001APR OF PDBLOC , &
           BLONUMGRA002APR OF PDBLOC
           GET PDCOMMAN_ASSIGN  OPT &
           VIA     CIECLE,                               &
                   BLONUMGRA001APR,                      &
                   BLONUMGRA002APR,                      &
                   BLORECAPR                             &
           USING   T_CIECLEMNU,                          &
                   BLONUMGRA001APR OF PDBLOC,            &
                   BLONUMGRA002APR OF PDBLOC,            &
                   BLORECAPR       OF PDBLOC
           GET PDCOMMAN_INTERNE  OPT &
           VIA     CIECLE,          &
                   PJTABR,          &
                   PJTANN,          &
                   COMNUMCOMINT     &
           USING   T_CIECLEMNU,                          &
                   PJTABR           OF PDCOMMAN_ASSIGN, &
                   PJTANN           OF PDCOMMAN_ASSIGN, &
                   COMNUMCOMINT     OF PDCOMMAN_ASSIGN

      END
   IF PATH = 3
       THEN BEGIN
           GET PDBLOC VIA CIECLE , BLONUMGRA001APR ORDERBY CIECLE , &
           BLONUMGRA001APR , BLONUMGRA002APR , BLORECAPR USING &
           T_CIECLEMNU , BLONUMGRA001APR OF PDBLOC
           GET PDCOMMAN_ASSIGN OPT &
           VIA     CIECLE,                               &
                   BLONUMGRA001APR,                      &
                   BLONUMGRA002APR,                      &
                   BLORECAPR                             &
           USING   T_CIECLEMNU,                          &
                   BLONUMGRA001APR OF PDBLOC,            &
                   BLONUMGRA002APR OF PDBLOC,            &
                   BLORECAPR       OF PDBLOC
           GET PDCOMMAN_INTERNE  OPT &
           VIA     CIECLE,          &
                   PJTABR,          &
                   PJTANN,          &
                   COMNUMCOMINT     &
           USING   T_CIECLEMNU,                          &
                   PJTABR           OF PDCOMMAN_ASSIGN, &
                   PJTANN           OF PDCOMMAN_ASSIGN, &
                   COMNUMCOMINT     OF PDCOMMAN_ASSIGN

       END
  IF PATH = 4
       THEN BEGIN
          GET PDBLOC VIA CIECLE ORDERBY CIECLE , BLONUMGRA001APR , &
           BLONUMGRA002APR , BLORECAPR USING T_CIECLEMNU
           GET PDCOMMAN_ASSIGN  OPT &
           VIA     CIECLE,                               &
                   BLONUMGRA001APR,                      &
                   BLONUMGRA002APR,                      &
                   BLORECAPR                             &
           USING   T_CIECLEMNU,                          &
                   BLONUMGRA001APR OF PDBLOC,            &
                   BLONUMGRA002APR OF PDBLOC,            &
                   BLORECAPR       OF PDBLOC
           GET PDCOMMAN_INTERNE  OPT &
           VIA     CIECLE,          &
                   PJTABR,          &
                   PJTANN,          &
                   COMNUMCOMINT     &
           USING   T_CIECLEMNU,                          &
                   PJTABR           OF PDCOMMAN_ASSIGN, &
                   PJTANN           OF PDCOMMAN_ASSIGN, &
                   COMNUMCOMINT     OF PDCOMMAN_ASSIGN

       END
  END


BUILD DETAIL LIST

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************* Bloc *************************************
*                                                                              *
* Nun. bloc.....: xxxx-xxxxx-x     Statut........: x   Num. carte....: xxxxxx  *
* Code granit...: xxx        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
* Localisation..: xxxx                                                         *
*                                                                              *
* Longueur brut.: xxxx      Longueur nette: xxxx      Longueur payer: xxxx     *
* Hauteur brut..: xxxx      Hauteur nette.: xxxx      Hauteur payer.: xxxx     *
* Largeur brut..: xxxx      Largeur nette.: xxxx      Largeur payer.: xxxx     *
* Volume brut...: xxxxxxxxx Volume net....: xxxxxxxxx Volume à payer: xxxxxxxxx*
*                                                                              *
* No bloc fourn.: xxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          *
*                                                                              *
********************************** Historique **********************************
*                                                                              *
* Num projet....: xxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
* Date réception: xxxxxxxx             Date bon CMD..: xxxxxxxx                *
* Date banquage.: xxxxxxxx             Date sortie...: xxxxxxxx                *
* Date redimen..: xxxxxxxx             Date sciage...: xxxxxxxx                *
* Date réserv...: xxxxxxxx                                                     *
********************************************************************************
@ENDIF
