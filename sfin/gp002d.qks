;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de sous-projet - Budget comptable
;
;/M/François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction pour unix.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp002d ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FOR 11 LINES &
              ON LINE 13  &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_OLDVALUE     CHARACTER SIZE 1

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

TRANSACTION T_PAC_VISUEL COMMIT ON UPDATE &
                         READ WRITE &
                         RESERVING FOR SHARE WRITE GPPRA_BDG_CTB IN DICT

USE ussectmp NOLIST     ; Variable pour la securite
    "GP002D"            ; Nom de l'écran

USE gp002d.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier d'unité de travail
;------------------------------------
FILE GPPRO_ACTIVITE         MASTER


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPPRA_BDG_CTB          PRIMARY  &

     TRANSACTION T_PAC_VISUEL FOR PROCESS, QUERY, UPDATE &

                            OCCURS 5 &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE, &
                  CPTCLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE, &
                  CPTCLE OF GPPRA_BDG_CTB   &
          REQUEST CPTCLE OF GPPRA_BDG_CTB   &
          ORDERBY CPTCLE, &
                  UNACLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE, &
                  UNACLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE, &
                  UNACLE OF GPPRA_BDG_CTB   &
          REQUEST UNACLE OF GPPRA_BDG_CTB   &
          ORDERBY CPTCLE, &
                  UNACLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE  &
          ORDERBY CPTCLE, &
                  UNACLE

  ITEM CIECLE    OF GPPRA_BDG_CTB   INITIAL T_CIECLEMNU
  ITEM PRJCLE    OF GPPRA_BDG_CTB   INITIAL PRJCLE     OF GPPRO_ACTIVITE
  ITEM PRACLE    OF GPPRA_BDG_CTB   INITIAL PRACLE     OF GPPRO_ACTIVITE
  ITEM PACUSRBDG OF GPPRA_BDG_CTB   INITIAL LOGONID
  ITEM PACDATBDG OF GPPRA_BDG_CTB   INITIAL SYSDATE



FILE GPPRO_BDG_CTB          DESIGNER   &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  CPTCLE, &
                  UNACLE  &
          USING   CIECLE OF GPPRA_BDG_CTB,  &
                  PRJCLE OF GPPRA_BDG_CTB,  &
                  CPTCLE OF GPPRA_BDG_CTB,  &
                  UNACLE OF GPPRA_BDG_CTB


;-------------------------------
; Fichier unité administrative
;-------------------------------
TEMPORARY T_UNACLE    CHARACTER SIZE 8

FILE MCUNITE_ADM            REFERENCE

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE OF GPPRA_BDG_CTB, &
                UNACLE OF GPPRA_BDG_CTB


;-----------------------------------------
; Fichier charte de l'unite administrative
;-----------------------------------------
FILE MCCHARTE_UNA           REFERENCE

  ACCESS  VIA   CPTCLE, &
                CIECLE, &
                UNACLE  &
          USING CPTCLE OF GPPRA_BDG_CTB, &
                CIECLE OF GPPRA_BDG_CTB, &
                UNACLE OF GPPRA_BDG_CTB


;-------------------------------
; Fichier compte / client
;-------------------------------
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes.
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes.
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes.


FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPPRA_BDG_CTB


;-------------------------------------------------------------------------------
;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING REFERENCE
  ACCESS VIA CIENMC USING "1"


;-------------------------------------------------------------------------------
;
TEMPORARY T_OLDMNTBDG        FLOAT SIZE 8  OCCURS WITH GPPRA_BDG_CTB
TEMPORARY T_OLDMNTBDGREV     FLOAT SIZE 8  OCCURS WITH GPPRA_BDG_CTB

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


SKIP
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO  11,1
DRAW FROM  3,80 TO  11,80
DRAW FROM  11,1  TO  11,80

SKIP 1

TITLE &
@IF FRANCAIS
      " Budget comptable " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,32)
FIELD PRAPACDATAPP  OF GPPRO_ACTIVITE                  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
      LABEL "Date approb...:" &
      DISPLAY

FIELD PRAPACUSRAPP  OF GPPRO_ACTIVITE                 &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
" Compte Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,2

TITLE &
@IF FRANCAIS
"Unité      Budget initial   Budget courant" &
@ELSE
      " %%% " &
@ENDIF
      AT ,36

SKIP

ALIGN (3,2,3) (,,10) (,,36) (,,47) (,,64)

CLUSTER OCCUR WITH GPPRA_BDG_CTB   ID BASE 10

FIELD CPTCLE       OF GPPRA_BDG_CTB &
      LABEL " " HIDDEN   &
      REQUIRED NOCHANGE  &
      LOOKUP ON MCCOMPTE &
          MESSAGE 1313 ; Ce compte n'existe pas.

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD UNACLE       OF GPPRA_BDG_CTB &
        REQUIRED NOCHANGE &
        ID SAME &
        LOOKUP ON MCUNITE_ADM   MESSAGE 860, &
            NOTON GPPRA_BDG_CTB &
              VIA     CIECLE, &
                      PRJCLE, &
                      PRACLE, &
                      CPTCLE, &
                      UNACLE  &
              USING   CIECLE OF GPPRO_ACTIVITE, &
                      PRJCLE OF GPPRO_ACTIVITE, &
                      PRACLE OF GPPRO_ACTIVITE, &
                      CPTCLE OF GPPRA_BDG_CTB,  &
                      UNACLE OF GPPRA_BDG_CTB   &
                      MESSAGE 887


FIELD PACMNTBDG OF GPPRA_BDG_CTB                      &
      ID SAME                                         &
      IF PRAPACDATAPP  OF GPPRO_ACTIVITE = 0          &
      VALUE 0 TO 999999999.9999

FIELD PACMNTBDGREV OF GPPRA_BDG_CTB                   &
      ID SAME                                         &
      IF PRAPACDATAPP  OF GPPRO_ACTIVITE <> 0         &
      VALUE 0 TO 999999999.9999

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les comptes.
;
;
PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTCLE)
  END
END



;----------------------------------------------------------------
;
; Popup sur les unités administratives.
;
PROCEDURE INPUT UNACLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
END

PROCEDURE EDIT UNACLE
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN

       GET MCCHARTE_UNA OPTIONAL
       ;
       ; Si la charte est incluse, le lien est obligatoire.
       ;
       IF NOT ACCESSOK
       THEN BEGIN
            IF HLDCHTCPTRLT OF MCHOLDING = "I"
            THEN ERROR 1314 ; La combinaison compte/unité administrative
                            ; n'existe pas.
       END
  END
END

PROCEDURE INPUT PACMNTBDG
BEGIN
   IF 0=SIZE(FIELDTEXT)
   THEN BEGIN
        LET FIELDTEXT = "1"
        LET T_OLDVALUE= "O"
   END
   ELSE LET T_OLDVALUE = "N"
END

PROCEDURE EDIT PACMNTBDG
BEGIN
   IF T_OLDVALUE = "O"
   THEN LET FIELDVALUE = OLDVALUE(PACMNTBDG OF GPPRA_BDG_CTB)

   IF PRAPACDATAPP  OF GPPRO_ACTIVITE <> 0  AND &
      T_OLDVALUE = "N"
   THEN BEGIN
        LET FIELDVALUE = OLDVALUE(PACMNTBDG OF GPPRA_BDG_CTB)
        WARNING 871 ; Cet élément ne peut être modifié dû au statut du budget
   END
END



PROCEDURE INPUT PACMNTBDGREV
BEGIN
   IF 0=SIZE(FIELDTEXT)
   THEN BEGIN
        LET FIELDTEXT = "1"
        LET T_OLDVALUE= "O"
   END
   ELSE LET T_OLDVALUE = "N"
END

PROCEDURE EDIT PACMNTBDGREV
BEGIN
   IF T_OLDVALUE = "O"
   THEN LET FIELDVALUE = OLDVALUE(PACMNTBDGREV OF GPPRA_BDG_CTB)

   IF PRAPACDATAPP  OF GPPRO_ACTIVITE = 0 AND &
      T_OLDVALUE = "N"
   THEN BEGIN
        LET FIELDVALUE = OLDVALUE(PACMNTBDGREV OF GPPRA_BDG_CTB)
        WARNING 871 ; Cet élément ne peut être modifié dû au statut du budget
   END
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GPPRA_BDG_CTB
  BEGIN
     ;
     ; Validation de la destruction
     ;
     IF     DELETEDRECORD OF GPPRA_BDG_CTB       &
        AND TZ_SECDEL = "0"
     THEN ERROR 1
     ;
     ; Validation de la modification.
     ;
     IF     ALTEREDRECORD     OF GPPRA_BDG_CTB       &
        AND NOT NEWRECORD     OF GPPRA_BDG_CTB       &
        AND NOT DELETEDRECORD OF GPPRA_BDG_CTB       &
        AND TZ_SECMOD = "0"
     THEN ERROR 2


     ;
     ; Destruction du budget comptable
     ;
     IF DELETEDRECORD OF GPPRA_BDG_CTB
     THEN BEGIN
       ;
       ; On initialise le flag de retour pour savoir si le sous-écran a
       ; bien fonctionné.
       ;
       LET T_FLGDEL = ""

       RUN SCREEN gp002dx MODE SAME &
                         PASSING GPPRO_ACTIVITE, &
                                 GPPRA_BDG_CTB,  &
                                 T_FLGDEL
       ;
       ; Si la valeur de retour égale 1, cela indique que le sous-écran
       ; n'a trouvé aucune référence pour l'identifiant demandé.
       ;
       IF T_FLGDEL = ""
       THEN ERROR " "
     END

     GET  GPPRO_BDG_CTB &
          VIA     CIECLE, &
                  PRJCLE, &
                  CPTCLE, &
                  UNACLE  &
          USING   CIECLE OF GPPRA_BDG_CTB,  &
                  PRJCLE OF GPPRA_BDG_CTB,  &
                  CPTCLE OF GPPRA_BDG_CTB,  &
                  UNACLE OF GPPRA_BDG_CTB OPTIONAL
     IF NOT ACCESSOK
     THEN BEGIN
          LET CIECLE OF GPPRO_BDG_CTB = CIECLE OF GPPRA_BDG_CTB
          LET PRJCLE OF GPPRO_BDG_CTB = PRJCLE OF GPPRA_BDG_CTB
          LET CPTCLE OF GPPRO_BDG_CTB = CPTCLE OF GPPRA_BDG_CTB
          LET UNACLE OF GPPRO_BDG_CTB = UNACLE OF GPPRA_BDG_CTB
     END

     IF    (NEWRECORD OF GPPRA_BDG_CTB       OR &
            ALTEREDRECORD OF GPPRA_BDG_CTB) AND &
        NOT DELETEDRECORD OF GPPRA_BDG_CTB
     THEN BEGIN
          LET PBCMNTBDG    OF GPPRO_BDG_CTB = PBCMNTBDG    OF GPPRO_BDG_CTB + &
                                              PACMNTBDG    OF GPPRA_BDG_CTB - &
                                              T_OLDMNTBDG

          LET PBCMNTBDGREV OF GPPRO_BDG_CTB = PBCMNTBDGREV OF GPPRO_BDG_CTB + &
                                              PACMNTBDGREV OF GPPRA_BDG_CTB - &
                                              T_OLDMNTBDGREV
     END

     IF DELETEDRECORD OF GPPRA_BDG_CTB
     THEN BEGIN
          LET PBCMNTBDG    OF GPPRO_BDG_CTB = PBCMNTBDG    OF GPPRO_BDG_CTB - &
                                              T_OLDMNTBDG

          LET PBCMNTBDGREV OF GPPRO_BDG_CTB = PBCMNTBDGREV OF GPPRO_BDG_CTB - &
                                              T_OLDMNTBDGREV
     END

     LET PBCUSRBDG OF GPPRO_BDG_CTB = LOGONID
     LET PBCDATBDG OF GPPRO_BDG_CTB = SYSDATE
     PUT GPPRO_BDG_CTB
  END
END

PROCEDURE POSTUPDATE
BEGIN
  FOR GPPRA_BDG_CTB
  BEGIN
     LET T_OLDMNTBDG    = PACMNTBDG    OF GPPRA_BDG_CTB
     LET T_OLDMNTBDGREV = PACMNTBDGREV OF GPPRA_BDG_CTB
  END
END

PROCEDURE POSTFIND
BEGIN
  FOR GPPRA_BDG_CTB
  BEGIN
     LET T_OLDMNTBDG    = PACMNTBDG    OF GPPRA_BDG_CTB
     LET T_OLDMNTBDGREV = PACMNTBDGREV OF GPPRA_BDG_CTB
  END
END

BUILD
@IF FORMAT
******************************* Budget comptable ******************************x
* Date approb...: xxxxxxxx xxxxxxxxxxxx                                        *
*                                                                              *
* Compte Description               Unité      Budget initial   Buget courant   *
* xxxxxx xxxxxxxxxxxxxxxxxxxx      xxxxxxxx   xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx *
* xxxxxx xxxxxxxxxxxxxxxxxxxx      xxxxxxxx   xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx *
* xxxxxx xxxxxxxxxxxxxxxxxxxx      xxxxxxxx   xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx *
* xxxxxx xxxxxxxxxxxxxxxxxxxx      xxxxxxxx   xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx *
* xxxxxx xxxxxxxxxxxxxxxxxxxx      xxxxxxxx   xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF

