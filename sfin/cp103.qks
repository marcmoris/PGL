;/T/ Consultation des lots             TYPECR, CIECLE, PECCLE, LCPCLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 15 avril 1992
;
;    Description..: Permet de visionner et de choisir un lot.
;                   Les lots sont sélectionnés par usager(LOGONID) et il
;                   doivent être journalisé.
;
;
;    Paramètre....: TYPECR ==> si <> blanc, seul les lots du même type seront
;                              sélectionnés, si = blanc tous les lots seront
;                              sélectionnés
;
;                   CIECLE ==> Compagnie, segment de la clé de recherche.
;
;                   PECCLE ==> segment de la clé de recherche.
;
;                   LCPCLE ==> segment de la clé de recherche, peut contenir
;                              le caractére '@'.
;
;

;/V/ Écran vérifier pour le passage à l'an 2000

;-------------------------------------------------------------------------------
SCREEN cp103 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_TYPECR, &
                        T_CIECLE, &
                        T_PECCLE, &
                        T_LCPCLE

USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP103"

USE cp103.hlp NOLIST

TEMPORARY T_TYPECR   CHARACTER SIZE 2
TEMPORARY T_CIECLE   CHARACTER SIZE 4
TEMPORARY T_PECCLE   CHARACTER SIZE 4
TEMPORARY T_LCPCLE   CHARACTER SIZE 4

;-------------------------------------------------------------------------------
;
FILE CPLOT            PRIMARY &
                      OCCURS 10

 ACCESS VIA     CIECLE   , &
                PECCLE   , &
                LCPCLE   , &
                LCPUSR   , &
                LCPDATJOU  &
        USING   T_CIECLE, &
                T_PECCLE, &
                T_LCPCLE, &
                LOGONID,  &
                0         &
        ORDERBY CIECLE, &
                PECCLE, &
                LCPCLE

 SELECT IF LCPTYPECR OF CPLOT = T_TYPECR OR T_TYPECR = ""

;-------------------------------------------------------------------------------
;
; Pour afficher le type d'écriture(bilingue).
;
FILE VCBI_DSC         DESIGNER &
                      OPEN READ


;-------------------------------------------------------------------------------
;
DEFINE D_LCPTYPECR CHAR SIZE 16 = "LCPTYPECR"

USE ushilite NOLIST
USE usdrwfnd NOLIST

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

@IF FRANCAIS
    TITLE "Lot Type  Validé   T-Réel T-Prévue" AT 2,7
@ELSE
   TITLE "Batch Type  Date val.   Co. Centre" AT 2,5
@ENDIF

;-------------------------------------------------------------------------------

ALIGN(3,3,6) (,,12) (,,16) (,,27) (,,34)

CLUSTER OCCURS WITH CPLOT

FIELD LCPCLE       OF CPLOT HIDDEN LABEL ">>"
FIELD LCPTYPECR    OF CPLOT
FIELD LCPDATVAL    OF CPLOT FORMAT YYYYMMDD PATTERN "####/##/##"
FIELD LCPNBRTRS    OF CPLOT &
      PICTURE "^,^^^ " TRAIL "-"
FIELD LCPNBRTRSVER OF CPLOT &
      PICTURE "^,^^^ " TRAIL "-"

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_LCPCLE, "@")
  THEN LET T_LCPCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_LCPCLE = LCPCLE OF CPLOT
  RETURN
END

BUILD
;
;*****************************************
;*     Lot Type  Validé  T-Réel T-Prévue *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;* >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
;*****************************************
