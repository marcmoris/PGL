;/T/ Consultation de l'entrepot
;
;/P/ Programmeur..: Francois Richard
;    Date Creation: 28 septembre 1999
;
;    Description..: Permet de visionner et de choisir l'entrepot
;
;    Parametres...: T_CIECLE        = Compagnie
;                   T_ETRCLE        = Entrepôt
;
;-------------------------------------------------------------------------------

SCREEN if105 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE   , &
                        T_ETRCLE


USE ustrasse NOLIST

USE ussectmp NOLIST
    "IF105"

;USE if105.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE    CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_ETRCLE    CHARACTER SIZE 4 RESET AT STARTUP

;-------------------------------------------------------------------------------

FILE IFENTREPOT  PRIMARY OCCURS 10

  ACCESS  VIA     CIECLE,     &
                  ETRCLE      &
          USING   T_CIECLE,   &
                  T_ETRCLE    &
          ORDERBY ETRCLE

; Selectionne les entrepot ouvert seulement
SELECT IF ETRSTU OF IFENTREPOT = "O"

;-------------------------------------------------------------------------------

USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP 1

TITLE "Code" AT ,5
TITLE "Description" AT ,11

SKIP

ALIGN (2,2,5) (,,11)

CLUSTER OCCURS WITH IFENTREPOT

FIELD ETRCLE OF IFENTREPOT       &
      DISPLAY                    &
      HIDDEN                     &
      LABEL ">>"

FIELD ETRDSCFRA001 OF IFENTREPOT &
      SIZE 28                    &
      DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_ETRCLE, "@")
  THEN LET T_ETRCLE = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_ETRCLE = ETRCLE OF IFENTREPOT
  RETURN
END

BUILD
