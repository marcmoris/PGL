;/T/ Vérification des factures / Notes de crédit / Ajustement
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 14-juin-1993
;
;    Description...: Pour faire la vérification des lots de factures, Note de crédit,
;                    et ajustement non journalisées au grand-livre. Remarqué que ce
;                    rapport peut-être exécuté même si le lot à déjà été journalisé.
;
;    Paramètres....: Compagnie
;                    Periode CTB
;                    No Lot (Un, Plusieurs ou tous)
;
;    Particularités: Met a jour la date de vérification du lot. (LCPDATVAL)
;
; /M/ Adaptation : Marc Morissette
;                  22 Décembre 1993
;
;-------------------------------------------------------------------------------

 USE ussetqts NOLIST   ; permet de faire les SET REPORT NOLIMIT....

RUN cp302t

REQUEST VERIFICATION_FACTURE_NC_AJUST

ACCESS CPLOT &
  LINK CIECLE OF CPLOT            , &
       PECCLE    OF CPLOT         , &
       LCPCLE    OF CPLOT           &
    TO CAPCIECLE                  , &
       PECCLE                     , &
       LCPCLE                     OF CPCPT_A_PAYER &
  LINK FOUCIECLE OF CPCPT_A_PAYER , &
       FOUCLE    OF CPCPT_A_PAYER , &
       CAPCLE    OF CPCPT_A_PAYER   &
    TO FOUCIECLE                  , &
       FOUCLE                     , &
       CAPCLE                     OF CPCAP_IMPUTATION OPTIONAL &
  LINK FOUCIECLE OF CPCPT_A_PAYER , &
       FOUCLE    OF CPCPT_A_PAYER , &
       CAPCLE    OF CPCPT_A_PAYER , &
       CAPTYPECR OF CPCPT_A_PAYER , &
       CAPTYPECR OF CPCPT_A_PAYER   &
    TO CPHCLE1                    , &
       CPHCLE2                    , &
       CPHCLE3                    , &
       CPHTYPECR                  , &
       CPHTYPTRA                  OF CPCAP_HISTO OPTIONAL


CHOOSE CIECLE    PARM, &                ; No Compagnie
       PECCLE    PARM, &                ; Période
       LCPCLE    PARM, &                ; No lot
       LCPTYPECR "FA"                   ; Type de lot facture

TEMPORARY T_CAPCLEREF CHARACTER SIZE 15 ; No facture en référence pour Note de
                                        ; crédit ou ajustement.

TEMPORARY T_CPCCLELIG INTEGER SIZE 2   ; No cédule en référence pour note de
                                        ; crédit ou ajustement.


SORT   ON LCPCLE    OF CPLOT            ; No lot

                                        ; No facture référer si note crédit ou ajustement
ITEM T_CAPCLEREF = CAPCLE OF CPCAP_HISTO  IF CAPTYPECR OF CPCPT_A_PAYER = "AJ" OR &
                                             CAPTYPECR OF CPCPT_A_PAYER = "NC" ELSE " "

                                        ; No cédule référer si note crédit ou ajustement
ITEM T_CPCCLELIG = CPCCLELIG OF CPCAP_HISTO  IF CAPTYPECR OF CPCPT_A_PAYER = "AJ" OR &
                                                CAPTYPECR OF CPCPT_A_PAYER = "NC" ELSE 0

SUBFILE WCP302 KEEP INCLUDE &
 CIECLE       OF CPLOT, &
 PECCLE       OF CPLOT, &
 LCPCLE       OF CPLOT, &
 LCPDSC       OF CPLOT, &
 LCPDATJOU    OF CPLOT, &
 LCPNBRTRS    OF CPLOT, &
 LCPNBRTRSVER OF CPLOT, &
 LCPCUMMNT    OF CPLOT, &
 LCPCUMMNTVER OF CPLOT, &
 LCPNBRTRSERR OF CPLOT, &
 CAPCLE       OF CPCPT_A_PAYER, &
 CAPMNT       OF CPCPT_A_PAYER, &
 CAPCUMMNT    OF CPCPT_A_PAYER, &
 REFCIECLE    OF CPCPT_A_PAYER, &
 REFCLENUM    OF CPCPT_A_PAYER, &
 REFCLETYP    OF CPCPT_A_PAYER, &
 RADCLE       OF CPCPT_A_PAYER, &
 CAPTYPECR    OF CPCPT_A_PAYER, &
 CAPDAT       OF CPCPT_A_PAYER, &
 CAPRTU       OF CPCPT_A_PAYER, &
 CAPTERNET    OF CPCPT_A_PAYER, &
 CAPDATNET    OF CPCPT_A_PAYER, &
 CAPTERESC1   OF CPCPT_A_PAYER, &
 CAPDATESC1   OF CPCPT_A_PAYER, &
 CAPMNTTPS    OF CPCPT_A_PAYER, &
 CAPMNTTVQ    OF CPCPT_A_PAYER, &
 CAPMNTRTI    OF CPCPT_A_PAYER, &
 CAPMNTCTI    OF CPCPT_A_PAYER, &
 CAPMNTNET    OF CPCPT_A_PAYER, &
 CAPDSC1      OF CPCPT_A_PAYER, &
 CAPDSC2      OF CPCPT_A_PAYER, &
 PRJCLE       OF CPCAP_IMPUTATION, &
 PRACLE       OF CPCAP_IMPUTATION, &
 IMMCLE       OF CPCAP_IMPUTATION, &
 CPTCLE       OF CPCAP_IMPUTATION, &
 UNACLE       OF CPCAP_IMPUTATION, &
 CCIMNT       OF CPCAP_IMPUTATION, &
 CCITAXCODTPS OF CPCAP_IMPUTATION, &
 CCITAXCODTVQ OF CPCAP_IMPUTATION, &
 CCIMNTTPS    OF CPCAP_IMPUTATION, &
 CCIMNTTVQ    OF CPCAP_IMPUTATION, &
 CLICIECLE    OF CPCAP_IMPUTATION, &
 CLICLE       OF CPCAP_IMPUTATION, &
 CCIFLGFAC    OF CPCAP_IMPUTATION, &
 T_CAPCLEREF                      , &
 T_CPCCLELIG

;---------------------------------------------------------------------------
; Mise à jour de la date de vérification du lot
; des factures.

OUTPUT CPLOT AT LCPCLE UPDATE &
  IF LCPDATJOU OF CPLOT = 0

 ITEM LCPDATVAL = SYSDATE

BUILD cp302t
