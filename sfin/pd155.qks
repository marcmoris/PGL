;/ / Unité de mesure (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-07-08
;
;    Description..: Ce panorama permet de saisir les unités de mesure.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd155 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD155"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd155.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Unité de mesure
;

FILE PDUNITE_MESURE PRIMARY NOITEM

  ACCESS VIA     CIECLE,    &
                 UNMCODUNM  &
         USING   T_CIECLEMNU,                &
                 UNMCODUNM OF PDUNITE_MESURE &
         REQUEST UNMCODUNM OF PDUNITE_MESURE &
         ORDERBY CIECLE,    &
                 UNMCODUNM

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 UNMCODUNM

         ITEM CIECLE OF PDUNITE_MESURE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Conversion unité de mesure
;

FILE PDCONVERT_UN_M DETAIL OCCURS 8


  ACCESS VIA     CIECLE,       &
                 CUMCODUNMINT  &
         USING   T_CIECLEMNU,                    &
                 UNMCODUNM     OF PDUNITE_MESURE &
         ORDERBY CIECLE,       &
                 CUMCODUNMINT, &
                 CUMCODUNMEXT


  SELECT IF CUMCODUNMINT OF PDCONVERT_UN_M <> CUMCODUNMEXT OF PDCONVERT_UN_M

  ITEM CIECLE       OF PDCONVERT_UN_M INITIAL T_CIECLEMNU
  ITEM CUMCODUNMINT OF PDCONVERT_UN_M INITIAL UNMCODUNM OF PDUNITE_MESURE


;-------------------------------------------------------------------------------
;
; Conversion unité de mesure pour lui-même
;

FILE PDCONVERT_UN_M DESIGNER ALIAS A_CUM_DEF

  ACCESS VIA     CIECLE,       &
                 CUMCODUNMINT, &
                 CUMCODUNMEXT  &
         USING   T_CIECLEMNU,                     &
                 UNMCODUNM     OF PDUNITE_MESURE, &
                 UNMCODUNM     OF PDUNITE_MESURE


;-------------------------------------------------------------------------------
;
; Unité de mesure (Validation)
;

FILE PDUNITE_MESURE REFERENCE ALIAS A_UNM_REF

  ACCESS VIA     CIECLE,                  &
                 UNMCODUNM                &
         USING   T_CIECLEMNU,             &
                 CUMCODUNMEXT OF PDCONVERT_UN_M


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour l'appel de dépisteur (pop-up)
;

TEMPORARY T_UNMCODUNM CHAR SIZE 04


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Unité de mesure " &
@ELSE
      " %%%Unité de mesure " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19)


FIELD UNMCODUNM OF PDUNITE_MESURE           &
        HIDDEN ID 05                        &
        REQUIRED NOCHANGE                   &
        LOOKUP NOTON PDUNITE_MESURE         &
          VIA   CIECLE,                     &
                UNMCODUNM                   &
          USING T_CIECLEMNU,                &
                UNMCODUNM OF PDUNITE_MESURE &
          MESSAGE 2906 ; Cette unité de mesure existe déjà.


SKIP 1


FIELD UNMDSCFRA    OF PDUNITE_MESURE        &
        HIDDEN ID 10                        &
        REQUIRED


SKIP 1


FIELD UNMDSCANG    OF PDUNITE_MESURE        &
        HIDDEN ID 15                        &
        REQUIRED                            &
        DEFAULT UNMDSCFRA OF PDUNITE_MESURE


DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Facteur conversion " &
@ELSE
      " %%%Facteur conversion " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
      "         U/M       Facteur " &
@ELSE
      "%%%         U/M       Facteur " &
@ENDIF
      AT ,2


SKIP TO LINE 14


ALIGN (10,10,11) (20,,21)


CLUSTER OCCURS WITH PDCONVERT_UN_M ID BASE 20


FIELD CUMCODUNMEXT     OF PDCONVERT_UN_M   &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED                           &
        LOOKUP ON A_UNM_REF                &
          VIA     CIECLE,                  &
                  UNMCODUNM                &
          USING   T_CIECLEMNU,             &
                  CUMCODUNMEXT OF PDCONVERT_UN_M &
          MESSAGE 3028 , & ; Cette unité de mesure n'existe pas
               NOTON PDCONVERT_UN_M        &
          VIA     CIECLE,                  &
                  CUMCODUNMINT,            &
                  CUMCODUNMEXT             &
          USING   T_CIECLEMNU,             &
                  UNMCODUNM    OF PDUNITE_MESURE, &
                  CUMCODUNMEXT OF PDCONVERT_UN_M &
          MESSAGE 3099 ; Ce facteur existe déjà pour cette unité de mesure


FIELD CUMFACCON        OF PDCONVERT_UN_M   &
        ID SAME


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs unité de mesure
;

PROCEDURE INPUT UNMCODUNM
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs unité de mesure
;

PROCEDURE INPUT CUMCODUNMEXT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDUNITE_MESURE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDUNITE_MESURE &
     AND NOT NEWRECORD     OF PDUNITE_MESURE &
     AND NOT DELETEDRECORD OF PDUNITE_MESURE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Créé l'occurence par défaut pour l'unité de mesure
  ;
  IF NEWRECORD     OF PDUNITE_MESURE
  THEN BEGIN
    GET A_CUM_DEF OPTIONAL &
      VIA CIECLE,       &
          CUMCODUNMINT, &
          CUMCODUNMEXT  &
      USING T_CIECLEMNU, &
            UNMCODUNM OF PDUNITE_MESURE, &
            UNMCODUNM OF PDUNITE_MESURE
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE       OF A_CUM_DEF = T_CIECLEMNU
      LET CUMCODUNMINT OF A_CUM_DEF = UNMCODUNM OF PDUNITE_MESURE
      LET CUMCODUNMEXT OF A_CUM_DEF = UNMCODUNM OF PDUNITE_MESURE
      LET CUMFACCON    OF A_CUM_DEF = 1.00
      PUT A_CUM_DEF
    END
  END
  ;
  ; Destruction l'occurence par défaut pour l'unité de mesure
  ;
  IF DELETEDRECORD OF PDUNITE_MESURE
  THEN BEGIN
    GET A_CUM_DEF &
      VIA CIECLE,       &
          CUMCODUNMINT, &
          CUMCODUNMEXT  &
      USING T_CIECLEMNU, &
            UNMCODUNM OF PDUNITE_MESURE, &
            UNMCODUNM OF PDUNITE_MESURE
    DELETE A_CUM_DEF
  END

END

BUILD
@IF FORMAT

2xxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
3****************************** Unité de mesure ********************************
4                                                                              *
5 Code unité....: xx                                                           *
6                                                                              *
7 Descr français: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
8                                                                              *
9 Descr anglais.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
0                                                                              *
1*******************************************************************************
2                                                                              *
3         U/M       Facteur                                                    *
4         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
5         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
6         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
7         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
8         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
9         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
0         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
1         xx        xxxxxxxxxxxxxxxxxxxxxx                                     *
2                                                                              *
3*******************************************************************************@ENDIF
@ENDIF
