;/T/ Création budget à zéro.
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 23 avril 1993
;
;
;    Description..: Création des budgets à zéro dans la relation MCSOLDE_ANNUEL
;                   à partir de la relation MCHARTE_UNA.
;                   On crée un record seulement s'il n'est pas présent.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gl003a NOMODE NOACTION FIELDMARK &
              FROM 6,15 TO 10,64 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              AUTOUPDATE &
              ACTIVITIES ENTRY &
              RECEIVING T_CIECLEMNU


USE ussectmp NOLIST     ; Variable pour la securite
    "GL003A"

USE gl003a.hlp NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie du menu.

;-------------------------------------------------------------------------------
;
;
; Lecture de la charte d'unité adm.
;
FILE MCCHARTE_UNA     DESIGNER &
                      OPEN READ

;
; Création des budgets dans MCSOLDE_ANNUEL.
;
FILE MCSOLDE_ANNUEL   DESIGNER

;
; Validation de l'année saisie.
;
FILE MCPERIODE_CTB    REFERENCE


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_SANANN     CHARACTER SIZE 2 ; Année des budgets à créer.

;-------------------------------------------------------------------------------
;
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 5,50

TITLE &
@IF FRANCAIS
      " Initialisation des budgets " &
@ELSE
      " Budget initialization " &
@ENDIF
      CENTERED AT 1,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(2,3,19)
FIELD T_SANANN HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Année.........:" &
@ELSE
        LABEL "Year..........:" &
@ENDIF
        REQUIRED &
        LOOKUP ON MCPERIODE_CTB &
          VIA   PECCLE &
          USING T_SANANN + "@" &
          MESSAGE 222 ; Cette période n'existe pas


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Si l'usager n'a pas saisie l'année, on retourne à l'écran
  ; précédent sans faire de traitement.
  ;
  IF T_SANANN = ""
  THEN RETURN

  ;
  ; Lecture de la charte d'origine.
  ;
  WHILE RETRIEVING MCCHARTE_UNA VIA CIECLE &
                                USING T_CIECLEMNU
  BEGIN
    ;
    ; On ajoute un record seulement si elle n'est pas présent.
    ;
    GET MCSOLDE_ANNUEL VIA SANANN, &
                           CIECLE, &
                           UNACLE, &
                           CPTCLE  &
                       USING T_SANANN, &
                             CIECLE OF MCCHARTE_UNA, &
                             UNACLE OF MCCHARTE_UNA, &
                             CPTCLE OF MCCHARTE_UNA  &
                       OPTIONAL
    IF NOT ACCESSOK
    THEN BEGIN
      LET SANANN     OF MCSOLDE_ANNUEL = T_SANANN
      LET CIECLE     OF MCSOLDE_ANNUEL = CIECLE OF MCCHARTE_UNA
      LET UNACLE     OF MCSOLDE_ANNUEL = UNACLE OF MCCHARTE_UNA
      LET CPTCLE     OF MCSOLDE_ANNUEL = CPTCLE OF MCCHARTE_UNA
      PUT MCSOLDE_ANNUEL
    END
  END
  RETURN
END


BUILD ;
;
;              *********** Initialisation des budgets ***********
;              *                                                *
;              * Année.........: xx                             *
;              *                                                *
;              **************************************************
;
;
;
;
;
;
;
;
;
;
;
;
;
;
;
;
;
;
