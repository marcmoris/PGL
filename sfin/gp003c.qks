;
;/M/ François Déry, 7 mai 1999
;       Modifier le run command pour le exécutable en UNIX.
;
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN gp003c      NOMODE ACTION LABEL " " &
                   FROM 2,1 TO 13,80 &
                   RECEIVING GPFRM_AFFICHE

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPFRM_AFFICHE          MASTER

FILE GPFRD_DOMAINE          DESIGNER

FILE GPPRO_BDG_CTB          DESIGNER

FILE GPPRA_BDG_CTB          DESIGNER

FILE GPFRM_DETAILS          DESIGNER

FILE GPPROJETS              DESIGNER

FILE GPPRO_ACTIVITE         DESIGNER
;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

PROCEDURE INTERNAL LIRE_BUDGET
BEGIN
   IF 0 = SIZE(TRUNCATE(PRACLE OF GPFRM_AFFICHE))
   THEN BEGIN
        WHILE RETRIEVING GPPRO_BDG_CTB &
              VIA   CIECLE,            &
                    PRJCLE             &
              USING CIECLE OF GPFRM_AFFICHE, &
                    PRJCLE OF GPFRM_AFFICHE
        BEGIN
            IF CPTCLE OF GPPRO_BDG_CTB >= FDOCPTDEB OF GPFRD_DOMAINE AND &
               CPTCLE OF GPPRO_BDG_CTB <= FDOCPTFIN OF GPFRD_DOMAINE
            THEN BEGIN
                 GET GPPROJETS &
                     VIA   CIECLE, &
                           PRJCLE  &
                     USING CIECLE OF GPPRO_BDG_CTB, &
                           PRJCLE OF GPPRO_BDG_CTB

                 LET CIECLE    OF GPFRM_DETAILS = CIECLE OF GPPRO_BDG_CTB
                 LET PRJCLE    OF GPFRM_DETAILS = PRJCLE OF GPPRO_BDG_CTB
                 LET PRACLE    OF GPFRM_DETAILS = " "
                 LET IMMCLE    OF GPFRM_DETAILS = " "
                 LET CPTCLE    OF GPFRM_DETAILS = CPTCLE OF GPPRO_BDG_CTB
                 LET UNACLE    OF GPFRM_DETAILS = UNACLE OF GPPRO_BDG_CTB

                 IF FDOOPE OF GPFRD_DOMAINE = "+"
                 THEN BEGIN
                      LET FDEMNTENG OF GPFRM_DETAILS = &
                          PBCMNTENG OF GPPRO_BDG_CTB

                      LET FDEMNTREE OF GPFRM_DETAILS = &
                          PBCMNTREE OF GPPRO_BDG_CTB

                      IF PRJPBCDATAPP OF GPPROJETS = 0
                      THEN LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PBCMNTBDG    OF GPPRO_BDG_CTB

                      ELSE LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PBCMNTBDGREV OF GPPRO_BDG_CTB
                 END
                 ELSE BEGIN
                      LET FDEMNTENG OF GPFRM_DETAILS = &
                          PBCMNTENG OF GPPRO_BDG_CTB * -1

                      LET FDEMNTREE OF GPFRM_DETAILS = &
                          PBCMNTREE OF GPPRO_BDG_CTB * -1

                      IF PRJPBCDATAPP OF GPPROJETS = 0
                      THEN LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PBCMNTBDG    OF GPPRO_BDG_CTB * -1

                      ELSE LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PBCMNTBDGREV OF GPPRO_BDG_CTB * -1
                 END
                 PUT GPFRM_DETAILS RESET
            END
        END
   END
   ELSE BEGIN
        WHILE RETRIEVING GPPRA_BDG_CTB &
              VIA   CIECLE,            &
                    PRJCLE,            &
                    PRACLE             &
              USING CIECLE OF GPFRM_AFFICHE, &
                    PRJCLE OF GPFRM_AFFICHE, &
                    PRACLE OF GPFRM_AFFICHE
        BEGIN
            IF CPTCLE OF GPPRA_BDG_CTB >= FDOCPTDEB OF GPFRD_DOMAINE AND &
               CPTCLE OF GPPRA_BDG_CTB <= FDOCPTFIN OF GPFRD_DOMAINE
            THEN BEGIN
                 GET GPPRO_ACTIVITE &
                     VIA   CIECLE, &
                           PRJCLE, &
                           PRACLE  &
                     USING CIECLE OF GPPRA_BDG_CTB, &
                           PRJCLE OF GPPRA_BDG_CTB, &
                           PRACLE OF GPPRA_BDG_CTB

                 LET CIECLE    OF GPFRM_DETAILS = CIECLE OF GPPRA_BDG_CTB
                 LET PRJCLE    OF GPFRM_DETAILS = PRJCLE OF GPPRA_BDG_CTB
                 LET PRACLE    OF GPFRM_DETAILS = PRACLE OF GPPRA_BDG_CTB
                 LET IMMCLE    OF GPFRM_DETAILS = " "
                 LET CPTCLE    OF GPFRM_DETAILS = CPTCLE OF GPPRA_BDG_CTB
                 LET UNACLE    OF GPFRM_DETAILS = UNACLE OF GPPRA_BDG_CTB

                 IF FDOOPE OF GPFRD_DOMAINE = "+"
                 THEN BEGIN
                      LET FDEMNTENG OF GPFRM_DETAILS = &
                          PACMNTENG OF GPPRA_BDG_CTB

                      LET FDEMNTREE OF GPFRM_DETAILS = &
                          PACMNTREE OF GPPRA_BDG_CTB

                      IF PRAPACDATAPP OF GPPRO_ACTIVITE = 0
                      THEN LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PACMNTBDG    OF GPPRA_BDG_CTB

                      ELSE LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PACMNTBDGREV OF GPPRA_BDG_CTB
                 END
                 ELSE BEGIN
                      LET FDEMNTENG OF GPFRM_DETAILS = &
                          PACMNTENG OF GPPRA_BDG_CTB * -1

                      LET FDEMNTREE OF GPFRM_DETAILS = &
                          PACMNTREE OF GPPRA_BDG_CTB * -1

                      IF PRAPACDATAPP OF GPPRO_ACTIVITE = 0
                      THEN LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PACMNTBDG    OF GPPRA_BDG_CTB * -1

                      ELSE LET FDEMNTBDG    OF GPFRM_DETAILS = &
                               PACMNTBDGREV OF GPPRA_BDG_CTB * -1
                 END
                 PUT GPFRM_DETAILS RESET
            END
        END
   END
END


PROCEDURE INITIALIZE
BEGIN
   CLOSE GPFRM_DETAILS

   RUN COMMAND "echo 'create file gpfrm_details' | qutil"

   WHILE RETRIEVING GPFRD_DOMAINE &
         VIA   FRMCLE,            &
               FRDCLE             &
         USING FRMCLE OF GPFRM_AFFICHE, &
               FRDCLE OF GPFRM_AFFICHE

   BEGIN
      DO LIRE_BUDGET
   END

   RETURN
END

BUILD
