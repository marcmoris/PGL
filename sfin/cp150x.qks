;/T/ Termes - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 8 septembre 1993
;
;    Description..: Cet écran vérifie si le terme de paiement reçu en
;                   paramètre est référé dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;/M/ Programmeur.......: Patrick Langlois
;    Date modification.: 09 Juin 1999
;    Description.......: Migration Achat/Réception/Inventaire de l'environnement (BOC)
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp150x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING CPTERME , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Termes de paiement.
;
FILE CPTERME          MASTER

;
; Fournisseurs - compagnie
;
FILE CPFOU_CIE        DESIGNER &
                      OPEN READ

;
; Comptes à payer(facture...)
;
FILE CPCPT_A_PAYER    DESIGNER &
                      OPEN READ

;
; Réquisition (Achat / Réception)
;
FILE ARREC_DETAIL     DESIGNER &
                      OPEN READ

;
; Détail commande (Achat / Réception)
;
FILE ARCMD_DETAIL     DESIGNER &
                      OPEN READ


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  IF NOT MATCHPATTERN(TRUNC(TERCAT OF CPTERME), "ES(A|M)")
  THEN BEGIN
    ;
    ; Vérification dans le fournisseur par compagnie.
    ;
    GET CPFOU_CIE VIA   FOCTERNET         &
                  USING TERCLE OF CPTERME &
                  OPTIONAL
    IF ACCESSOK
    THEN ERROR 468 ; Destruction annulée, cette information est référée par un
                   ; fournisseur-compagnie.
    ;
    GET CPFOU_CIE VIA   FOCTERESC1        &
                  USING TERCLE OF CPTERME &
                  OPTIONAL
    IF ACCESSOK
    THEN ERROR 468 ; Destruction annulée, cette information est référée par un
                   ; fournisseur-compagnie.
    ;
    GET CPFOU_CIE VIA   FOCTERESC2        &
                  USING TERCLE OF CPTERME &
                  OPTIONAL
    IF ACCESSOK
    THEN ERROR 468 ; Destruction annulée, cette information est référée par un
                   ; fournisseur-compagnie.
    ;
    ; Vérification dans les comptes à payer.
    ;
    GET CPCPT_A_PAYER VIA   CAPTERNET         &
                      USING TERCLE OF CPTERME &
                      OPTIONAL
    IF ACCESSOK
    THEN ERROR 477 ; Destruction annulée, cette information est référée par
                   ; un compte à payer
    ;
    GET CPCPT_A_PAYER VIA   CAPTERESC1        &
                      USING TERCLE OF CPTERME &
                      OPTIONAL
    IF ACCESSOK
    THEN ERROR 477 ; Destruction annulée, cette information est référée par
                   ; un compte à payer
    ;
    GET CPCPT_A_PAYER VIA   CAPTERESC2        &
                      USING TERCLE OF CPTERME &
                      OPTIONAL
    IF ACCESSOK
    THEN ERROR 477 ; Destruction annulée, cette information est référée par
                   ; un compte à payer
  END
  ELSE BEGIN
    ;
    ; Vérification dans les réquisition
    ;
    GET ARREC_DETAIL VIA   REDTERESCACH      &
                     USING TERCLE OF CPTERME &
                     OPTIONAL
    IF ACCESSOK
    THEN ERROR "Destruction annulée, cette information est référée par une réquisition"
    ;
    ; Vérification dans les commandes d'achat
    ;
    GET ARCMD_DETAIL VIA   CDETERESCACH      &
                     USING TERCLE OF CPTERME &
                     OPTIONAL
    IF ACCESSOK
    THEN ERROR "Destruction annulée, cette information est référée par une commande"

  END
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
