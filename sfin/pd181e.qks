;/T/ Gerrer les tranches (Commande interne)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-26
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur la commande interne auquel la tranche
;                   est affectée.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd181e ACTIONBAR                     &
             MODE LABEL "" AT 2,80          &
             NOACTION                       &
             ACTIVITIES FIND                &
             FIELDMARK RETAIN STARTUP       &
             HELP POPUP FROM 4,9 TO 20,72   &
             RECEIVING  T_CIECLEMNU ,       &
                        T_CIENOM ,          &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD181E"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd181e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Commande - assignation
;

FILE PDCOMMAN_ASSIGN PRIMARY &
                     NOITEM &
                     NODELETE &
                     NOAPPEND

  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE

         ITEM CIECLE OF PDCOMMAN_ASSIGN INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE REFERENCE

  ACCESS VIA     CIECLE,      &
                 COMNUMCOM    &
         USING   T_CIECLEMNU, &
                 COMNUMCOM    OF PDCOMMAN_ASSIGN


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Projet " &
@ELSE
      " %%%Projet " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO LINE 5

ALIGN (2,3,19)

FIELD PJTNUMPRO        OF PDCOMMAN_ASSIGN  &
        HIDDEN ID 05                       &
        DISPLAY


ALIGN (2,3,19)

FIELD COMNOM           OF PDCOMMAN_INTERNE &
        HIDDEN ID 20                       &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Projet ************************************
*                                                                              *
* Projet........: xx-xx-xxx                                                    *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
