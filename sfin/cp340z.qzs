;/T/ Liste des fournisseurs
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 93-06-11
;
;    Description...: Liste des fournisseurs
;
;    Paramètres....: fournisseur (1, plusieurs, tous)
;                    Ordre Alpha-Num
;                    Statut (1, plusieurs, tous)
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....

ACCESS VFOC_FOU


CHOOSE FOUCIECLE PARM, &              ; Compagnie fournisseur
       FOCCIECLE PARM, &              ; Compagnie menu
       FOUCLE    PARM, &              ; Fournisseur
       FOUSTU    PARM, &              ; Statut
       FOUCAT    PARM                 ; Catégorie


DEFINE D_TYP_TRI CHAR SIZE 1  = PARM  ; Ordre du tri : 1- Nom fournisseur 2-Numéro fournisseur

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = & ; titre1  du rapport
@IF FRANCAIS
                    "Liste des fournisseurs"
@ELSE
                    "%%%Liste des fournisseurs"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = & ; titre2 du rapport selon ordre demandé
@IF FRANCAIS
                    "Par ordre de nom" IF D_TYP_TRI="1" &
                    ELSE "Par ordre de numéro" IF D_TYP_TRI="2"
@ELSE
                    "In Name Order"    IF D_TYP_TRI="1" &
                    ELSE "In Number Order"     IF D_TYP_TRI="2"
@ENDIF
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP340"        ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM           ; Nom de la compagnie

USE usflgdet NOLIST                    ;Flag Bilingue (O/N)
DEFINE D_FOURTU CHARACTER SIZE 3  = DZ_FLGAFFO IF FOURTU OF VFOC_FOU ="1" &
                               ELSE DZ_FLGAFFN IF FOURTU OF VFOC_FOU = "0"

                                      ;Assigne le numéro ou nom du fournisseur dans variable du tri
DEFINE D_TRI   CHAR SIZE 40 = FOUNOM OF VFOC_FOU IF D_TYP_TRI ="1" ELSE &
                              FOUCLE OF VFOC_FOU IF D_TYP_TRI ="2"

SORT ON D_TRI                            ; Ordre du tri (Nom fournisseur ou Numéro fournisseur)


USE ustitstd NOLIST  ; Titre standard pour l'application
@IF FRANCAIS
  TAB 001 "    No                                                                    " &
  TAB 075 "------Terme-------                    Compte  Compte "&
 SKIP &
  TAB 001 "fournisseur Catégorie             Nom fournisseur              Type  Dev. " &
  TAB 075 "net  esc(1) esc(2) Stat. Retenu Lang.  CAP   Encaisse" &
@ELSE
  TAB 001 "%%% No                                                                    " &
  TAB 075 "%%%---Terme-------                    Compte  Compte "&
 SKIP &
  TAB 001 "%%%rnisseur Catégorie             Nom fournisseur              Type  Dev. " &
  TAB 075 "%%%  esc(1) esc(2) Stat. Retenu Lang.  CAP   Encaisse" &
@ENDIF
 SKIP &
  TAB 001 "----------- --------- ---------------------------------------- ----  ---- " &
  TAB 075 "---- ------ ------ ----- ------ ----- ------ --------"

REPORT &
  TAB 003 FOUCLE OF VFOC_FOU &
  TAB 016 FOUCAT OF VFOC_FOU &
  TAB 023 FOUNOM OF VFOC_FOU &
  TAB 066 FOUTYP OF VFOC_FOU &
  TAB 070 DEVCLE OF VFOC_FOU &
  TAB 075 FOCTERNET OF VFOC_FOU &
  TAB 080 FOCTERESC1 OF VFOC_FOU &
  TAB 087 FOCTERESC2 OF VFOC_FOU &
  TAB 096 FOUSTU OF VFOC_FOU &
  TAB 102 D_FOURTU &
  TAB 109 FOULNG OF VFOC_FOU &
  TAB 113 FOCCPTCAP OF VFOC_FOU &
  TAB 121 LBQCPTENC OF VFOC_FOU

BUILD cp340z

;                                                                                                   1         1         1         1
;         1         2         3         4         5         6         7         8         9         0         1         2         3
;123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
;CP340                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
;99/99/99 HH:MM xxxxxxxxxxxx                           Liste des fournisseurs
;                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
;
;    No                                                                    ------Terme-------                    Compte  Compte
;fournisseur Catégorie             Nom fournisseur              Type  Dev. net  esc(1) esc(2) Stat. Retenu Lang.  CAP   Encaisse
;----------- --------- ---------------------------------------- ----  ---- ---- ------ ------ ----- ------ ----- ------ --------
;  xxxxxx       xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx   xxxx     x     x      x   xxxxxx  xxxxxx
;  xxxxxx       xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx   xxxx     x     x      x   xxxxxx  xxxxxx
;  xxxxxx       xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx   xxxx     x     x      x   xxxxxx  xxxxxx
;  xxxxxx       xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx   xxxx     x     x      x   xxxxxx  xxxxxx
;  xxxxxx       xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   x   xxx  xxxx xxxx   xxxx     x     x      x   xxxxxx  xxxxxx
; (1) par ordre alphabétique ou par ordre numérique
