;/T/ Liste des produits / Détaillée
;
;/P/ Réalisé par.........: Marc Poulin
;    Date modification...: 15 mai 2000
;    Description.........: Impression détaillée des produits
;
;    Paramètres..........: Nom de compagnie (fourni par le système);
;                          Tri              (1=Numérique, 2=Alphabétique).
;
;    Référence...........: Demande de changement 000055.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS *WAR342A LINK CIECLE OF WAR342A,  &
                     PRDCLE OF WAR342A   &
                  TO CIECLE            , &
                     PRDCLE OF *WAR342B  & ; Descriptions du produits

                 AND CIECLE OF WAR342A , &
                     PRDCLE OF WAR342A   &
                  TO CIECLE            , &
                     PRDCLE OF *WAR342C    ; Quantités utilisées au cours des 12 derniers mois

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1  CHARACTER SIZE 60 = "Liste des produits / Détaillée"
DEFINE DZ_NOMRAP2  CHARACTER SIZE 60 = " "
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP   CHARACTER SIZE 07 = "AR342Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM   CHARACTER SIZE 40 = PARM

; --------------------------------------------------------------------------

DEFINE D_TRI_PARM     CHARACTER SIZE 01 = PARM

DEFINE D_PRDCLE_DESC1 CHARACTER SIZE 40 = PRDCLE  OF WAR342A  &
                                       IF D_TRI_PARM = "1"    &
                                     ELSE PRDDSC1 OF WAR342A

; --------------------------------------------------------------------------

DEFINE D_EFF_LIGNE    VARCHAR  SIZE 74 = " "[1:72] IF NOT RECORD WAR342C EXIST ELSE ""

; --------------------------------------------------------------------------
; Statuts de produit demandés
;
DEFINE D_CBIDSC1   CHARACTER SIZE 15 = "Actif "                                   &
                                    IF 0 <> INDEX (D_PRDSTU_PARM,"A") OR D_PRDSTU_PARM = "" 

DEFINE D_CBIDSC2   CHARACTER SIZE 15 = "Inactif "                                 &
                                    IF 0 <> INDEX (D_PRDSTU_PARM,"I") OR D_PRDSTU_PARM = "" 

DEFINE D_CBIDSCDEM CHARACTER SIZE 70 = PACK(TRUNCATE(D_CBIDSC1) + " " +           &
                                            TRUNCATE(D_CBIDSC2))

; --------------------------------------------------------------------------

SORT ON CIECLE OF WAR342A & ; Code de compagnie
     ON D_PRDCLE_DESC1    & ; Code de produit ou Description 1 du produit
     ON PRDCLE OF WAR342A   ; Code de produit

; --------------------------------------------------------------------------

USE ustit180 NOLIST
 SKIP 2                               &
  TAB 001 "Statuts demandés: "        &
  TAB 019 D_CBIDSCDEM                 &
 SKIP 2                               &
  TAB 001 "                                                                                                  Quantité      Point de      Quantité    Quantité utilisée    Trois derniers   " &
 SKIP                                 &
  TAB 001 "Code produit              Description                 Statut     Type    Caté U/M  Entr Rangée    en main       commande      maximum     12 derniers mois      fournisseurs    " &
 SKIP                                 &
  TAB 001 "------------ ---------------------------------------- ------- ---------- ---- ---- ---- ------ -------------- ------------ -------------- ----------------- --------------------" &
 SKIP 1

REPORT                                   &
  TAB 001 PRDCLE          OF WAR342B     &
          PRINT AT PRDCLE                &
  TAB 014 PRDDSC          OF WAR342B     &
  TAB 055 T_CBIDSC_PRDSTU OF WAR342A     &
          PRINT AT PRDCLE                &
  TAB 063 T_CBIDSC_PRDTYP OF WAR342A     &
          PRINT AT PRDCLE                &
  TAB 074 PRDCAT          OF WAR342A     &
          PRINT AT PRDCLE                &
  TAB 079 PRDUNI          OF WAR342A     &
          PRINT AT PRDCLE                &
  TAB 084 ENTCLE          OF WAR342C     &
  TAB 089 ENLCLE          OF WAR342C     &
  TAB 096 PREQTEPHY       OF WAR342C     &
          PICTURE "^,^^^,^^^.^^^ "       &
          TRAILING SIGN "-"              &
          SIGNIFICANCE 6                 &
  TAB 111 PREPOICOM       OF WAR342C     &
          PICTURE "^^^,^^^.^^^ "         &
          TRAILING SIGN "-"              &
          SIGNIFICANCE 6                 &
          NOBWZ                          &
  TAB 124 PREQTEMAX       OF WAR342C     &
          PICTURE "^,^^^,^^^.^^^ "       &
          TRAILING SIGN "-"              &
          SIGNIFICANCE 6                 &
  TAB 140 T_PRHQTETRS     OF WAR342C     &
          PICTURE "^^^,^^^,^^^.^^^ "     &
          TRAILING SIGN "-"              &
          SIGNIFICANCE 6                 &
  TAB 157 T_DERNIER_FOUCLE_01 OF WAR342A &
          PRINT AT PRDCLE                &
  TAB 164 T_DERNIER_FOUCLE_02 OF WAR342A &
          PRINT AT PRDCLE                &
  TAB 171 T_DERNIER_FOUCLE_03 OF WAR342A &
          PRINT AT PRDCLE                &
  TAB 084 D_EFF_LIGNE


BUILD ar342z

@IF FORMAT
                                                                                                   1         1         1         1         1         1         1         1         1
         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
AR342                                                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                       Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                                                   Liste des produits / Détaillée
                                                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Statuts demandés: xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx 
                                                                                                  Quantité      Point de      Quantité    Quantité utilisée    Trois derniers 
Code produit              Description                 Statut     Type    Caté U/M  Entr Rangée    en main       commande      maximum     12 derniers mois      fournisseurs
------------ ---------------------------------------- ------- ---------- ---- ---- ---- ------ -------------- ------------ -------------- ----------------- --------------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxx xxxx xxxxx  9,999,999.999- 999,999.999- 9,999,999.999-  999,999,999.999- xxxxxx xxxxxx xxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxx xxxx xxxxx  9,999,999.999- 999,999.999- 9,999,999.999-  999,999,999.999- xxxxxx xxxxxx xxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxx xxxx xxxxx  9,999,999.999- 999,999.999- 9,999,999.999-  999,999,999.999- xxxxxx xxxxxx xxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxx xxxx xxxxx  9,999,999.999- 999,999.999- 9,999,999.999-  999,999,999.999- xxxxxx xxxxxx xxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxx xxxx xxxxx  9,999,999.999- 999,999.999- 9,999,999.999-  999,999,999.999- xxxxxx xxxxxx xxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxx xxxxxxxxxx xxxx xxxx xxxx xxxxx  9,999,999.999- 999,999.999- 9,999,999.999-  999,999,999.999- xxxxxx xxxxxx xxxxxx
@ENDIF

