;/T/ Gestion des équipement
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 7 Octobre
;
;    Description..: Gestion des équipement...
;                   Nouvelle norme
;


;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr002 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "PR002"             ; Nom de l'écran

USE pr002.hlp NOLIST    ; Use servant à la documentation de l'écran
                        ; Ne pas oublier de créer un fichier vide dans [exefra], [exeang]


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Info complementaires       " ACTION DESIGNER D001
  MENUITEM LABEL "Informations libre format  " ACTION DESIGNER D002


FILE GPUNITE PRIMARY
  ACCESS VIA CIECLE, UNICAT, UNICLE &
       USING T_CIECLEMNU, UNICAT, UNICLE &
       REQUEST UNICAT, UNICLE &
   ORDERBY CIECLE, UNICAT, UNICLE

  ACCESS VIA CIECLE, UNICAT &
       USING T_CIECLEMNU, UNICAT &
       REQUEST UNICAT &
   ORDERBY CIECLE, UNICAT, UNICLE

  ACCESS VIA CIECLE &
       USING T_CIECLEMNU &
   ORDERBY CIECLE, UNICAT, UNICLE


ITEM CIECLE OF GPUNITE INITIAL T_CIECLEMNU

;
; Validation du type d'équipement et lecture de sa description
;
DEFINE    D_UNITYP CHARACTER SIZE 16 = "UNITYP"
TEMPORARY T_UNITYP CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_UNITYP

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_UNITYP, &
                UNITYP OF GPUNITE
;
; Validation du statut d'équipement et lecture de sa description
;
DEFINE    D_UNISTU CHARACTER SIZE 16 = "UNISTU"
TEMPORARY T_UNISTU CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_UNISTU

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_UNISTU, &
                UNISTU OF GPUNITE

;
;
DEFINE    D_UNIPRO CHARACTER SIZE 16 = "UNIPRO"
TEMPORARY T_UNIPRO CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_UNIPRO

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_UNIPRO, &
                UNIPRO OF GPUNITE

;
;
DEFINE    D_UNICAT CHARACTER SIZE 16 = "UNICAT"
TEMPORARY T_UNICAT CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_UNICAT

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_UNICAT, &
                UNICAT OF GPUNITE


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_GPMCLE1   CHARACTER SIZE 8  ; Mémo
TEMPORARY T_GPMCLE2   CHARACTER SIZE 3  ; Mémo
TEMPORARY T_GPMTYP    CHARACTER SIZE 3  ; Mémo

USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des équipements " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


ALIGN(2,2,14)(,,16)(,28,41)(,53,67)

FIELD UNITYP OF GPUNITE  LABEL "Type......:"  NOCHANGE REQUIRED HIDDEN &
      LOOKUP ON VCBI_DSC_UNITYP &
      MESSAGE 2508  ;  "*EE* Ce type de d'équipement n'existe pas"

FIELD CBIDSC OF VCBI_DSC_UNITYP  DISPLAY SIZE 9

FIELD UNICAT OF GPUNITE LABEL   "Catégorie..:" NUM SIGNIF 2 REQUIRED NOCHANGE &
      LOOKUP ON VCBI_DSC_UNICAT &
      MESSAGE 2502  ;  "*EE* Cette catégorie n'existe pas"

FIELD UNICLE OF GPUNITE LABEL   "Equipement :" REQUIRED NOCHANGE &
  LOOKUP NOTON GPUNITE VIA CIECLE, UNICAT, UNICLE &
                     USING T_CIECLEMNU, &
                           UNICAT OF GPUNITE, &
                           UNICLE OF GPUNITE &
  MESSAGE 2509 ;"*EE* Ce numéro d'équipement existe déjà"

ALIGN(2,2,14)(,28,41)(,,43)
FIELD UNIANN   OF GPUNITE LABEL "Année.....:" REQUIRED NUM SIGNIF 4 HIDDEN
FIELD UNISTU   OF GPUNITE LABEL "Statut.....:" DEFAULT "A" &
      LOOKUP ON VCBI_DSC_UNISTU &
      MESSAGE 804  ;  Ce statut n'existe pas

FIELD CBIDSC OF VCBI_DSC_UNISTU  DISPLAY SIZE 7

SKIP 1

ALIGN (2,2,16)
FIELD UNIDSC1 OF GPUNITE LABEL "Description :" FOR 1,60 POPUP ON REQUEST HIDDEN
FIELD UNIDSC2 OF GPUNITE FOR 1,60 POPUP ON REQUEST ID SAME NOLABEL
SKIP 1
ALIGN (2,2,21) (,,23)
FIELD UNIPRO OF GPUNITE LABEL "Propriété........:" DEFAULT "A" HIDDEN &
      LOOKUP ON VCBI_DSC_UNIPRO &
      MESSAGE 804  ;  Ce statut n'existe pas

FIELD CBIDSC OF VCBI_DSC_UNIPRO  DISPLAY SIZE 10

ALIGN (2,2,21)
FIELD UNIUNABIL OF GPUNITE LABEL "Unité de bilan...:" HIDDEN
FIELD UNIUNADEP OF GPUNITE LABEL "Unité de dépense.:" HIDDEN
FIELD UNIUNAREV OF GPUNITE LABEL "Unité de revenu..:" HIDDEN
FIELD UNIUNAPAI OF GPUNITE LABEL "Unité de paie....:" HIDDEN
SKIP 1
FIELD UNIBDGREV OF GPUNITE LABEL "Budget Revenus...:" HIDDEN
FIELD UNIBDGDEP OF GPUNITE LABEL "Budget Dépenses..:" HIDDEN
SKIP 1
ALIGN(2,2,27)(,,35)(,,45)(,55,76)
FIELD UNIFRM1 LABEL "Format d'impression :" HIDDEN
FIELD UNIFRM2 NOLABEL ID SAME
FIELD UNIFRM3 NOLABEL ID SAME
;FIELD UNINUIT  LABEL "Traitement de nuit :" ID SAME

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNITYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNITYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNITYP, T_UNITYP
    LET FIELDTEXT = TRUNCATE(T_UNITYP)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNICAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNICAT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNICAT, T_UNICAT
    LET FIELDTEXT = TRUNCATE(T_UNICAT)
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNISTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNISTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNISTU, T_UNISTU
    LET FIELDTEXT = TRUNCATE(T_UNISTU)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT UNIPRO
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNIPRO = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_UNIPRO, T_UNIPRO
    LET FIELDTEXT = TRUNCATE(T_UNIPRO)
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN pr002a MODE SAME &
   PASSING T_CIECLEMNU, T_CIENOM, GPUNITE
END

;
; Memo
;
PROCEDURE DESIGNER D002
BEGIN
  LET T_GPMCLE1 = UNICLE OF GPUNITE
  LET T_GPMCLE2 = UNICAT OF GPUNITE
  LET T_GPMTYP  = "UNI"
  RUN SCREEN pr050 MODE SAME &
   PASSING T_CIECLEMNU, T_CIENOM, T_GPMCLE1, T_GPMCLE2, T_GPMTYP
END



;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPUNITE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPUNITE &
     AND NOT NEWRECORD     OF GPUNITE &
     AND NOT DELETEDRECORD OF GPUNITE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END



PROCEDURE DELETE
DISABLE

BUILD
;
