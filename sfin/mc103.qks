;/T/ Consultation Compte comptable     CPTCLE, CPTTYPPAT, CPTCATPAT,PECCLE
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 02-Mar-1992
;
;    Description..: Permet de visionner et de choisir un compte comptable
;
;    Paramètres...: CPTCLE    ==> - numéro de compte
;                                 - segment de la clé de recherche
;                                 - conient la valeur de retour
;
;                   CPTTYPPAT ==> - type de compte
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;                   CPTCATPAT ==> - catégorie de compte.
;                                 - critère de sélection sous la forme
;                                   de pattern. Ex: "AV|EN"
;
;                   PECCLE    ==> - période du document.
;                                 - critère de sélection.
;                                 - si ce champ est à blanc, alors
;                                   on ne tient pas compte de la période.
;
;/M/ Stephane Jackson, 98-08-20
;
;    Ajustement pour le changement de siècle
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN mc103 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_CPTCLE   , &
                       T_CPTTYPPAT, &
                       T_CPTCATPAT, &
                       T_PECCLE

TEMPORARY T_CPTCLE    CHARACTER SIZE 6
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8
TEMPORARY T_PECCLE    CHARACTER SIZE 4

USE mc103.hlp NOLIST

;-------------------------------------------------------------------------------

FILE VCPT_DSC         PRIMARY &
                      OCCURS 11
  ACCESS VIA CPTCLE &
         USING T_CPTCLE &
         ORDERED

;  SELECT IF &
;    MATCHPATTERN(CPTTYP OF VCPT_DSC,TRUNC(T_CPTTYPPAT)) &
;    AND &
;    MATCHPATTERN(CPTCAT OF VCPT_DSC,TRUNC(T_CPTCATPAT)) &
;    AND &
;    ( &
;      ( T_PECCLE >= CPTPECDEBA OF VCPT_DSC &
;        AND &
;        ( T_PECCLE < CPTPECDEBI OF VCPT_DSC OR  &
;          0 = SIZE(TRUNC(CPTPECDEBI OF VCPT_DSC)) &
;        ) &
;      ) &
;      OR &
;      0 = SIZE(TRUNC(T_PECCLE)) &
;    )

; Ajustement pour le changement de siècle
SELECT IF   &
( &
   ( &
     CPTPECDEBA OF VCPT_DSC[1:1] <> "0" &
     OR &
     CPTPECDEBI OF VCPT_DSC[1:1] <> "9" &
   ) &
    AND &
    ( &
      T_PECCLE[1:1] <> "9" &
      OR &
      CPTPECDEBA OF VCPT_DSC[1:1] <> "0" &
      OR &
      CPTPECDEBI OF VCPT_DSC[1:1] <> " " &
    ) &
   AND &
   ( &
      ( &
        ( &
          ( T_PECCLE >= CPTPECDEBA OF VCPT_DSC &
            AND &
            ( T_PECCLE < CPTPECDEBI OF VCPT_DSC OR  &
              0 = SIZE(TRUNC(CPTPECDEBI OF VCPT_DSC)) &
            ) &
          ) &
        )  &
      ) &
      OR &
      ( &
        T_PECCLE >= CPTPECDEBA OF VCPT_DSC &
        AND &
        T_PECCLE[1:1] = "9" &
        AND &
        CPTPECDEBI OF VCPT_DSC[1:1] = "0" &
        AND &
        CPTPECDEBA OF VCPT_DSC[1:1] = "9" &
      ) &
      OR &
      ( &
        T_PECCLE < CPTPECDEBI OF VCPT_DSC &
        AND &
        T_PECCLE[1:1] = "0" &
        AND &
        CPTPECDEBA OF VCPT_DSC[1:1] = "9" &
        AND &
        CPTPECDEBI OF VCPT_DSC[1:1] = "0" &
      ) &
      OR &
      ( &
        T_PECCLE[1:1] = "0" &
        AND &
        CPTPECDEBA OF VCPT_DSC[1:1] = "9" &
        AND &
        0 = SIZE(TRUNC(CPTPECDEBI OF VCPT_DSC)) &
      ) &
   ) &
  OR &
  0 = SIZE(TRUNC(T_PECCLE)) &
) &
AND &
MATCHPATTERN(CPTTYP OF VCPT_DSC,TRUNC(T_CPTTYPPAT)) &
AND &
MATCHPATTERN(CPTCAT OF VCPT_DSC,TRUNC(T_CPTCATPAT))


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH VCPT_DSC

FIELD CPTCLE    OF VCPT_DSC HIDDEN LABEL ">>"
FIELD CPTDSCABR OF VCPT_DSC DISPLAY

CLUSTER

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CPTCLE, "@")
  THEN LET T_CPTCLE = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_CPTCLE = CPTCLE OF VCPT_DSC
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       *****************************************
