;/T/ Consultation des produits d'un entrepôt  CIECLE, ENTCLE, PRDCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 11 mars 1994
;
;    Description..: Permet de visionner et de choisir un produit.
;
;    Paramètre....: CIECLE ==> - compagnie du produit
;                              - segment de la clé de recherche.
;                   
;                   ENTCLE ==> - entrepôt du produit
;                              - segment de la clé de recherche.
;  
;                   PRDCLE ==> - segment de la clé de recherche.
;                              - peut contenir le caractère '@'.
;                              - contient la valeur de retour.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar103 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_ENTCLE, &
                        T_PRDCLE
                     

USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR103"

USE ar103.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Compagnie de l'entrepôt
TEMPORARY T_ENTCLE CHARACTER SIZE 4  ; Code de l'entrepôt
TEMPORARY T_PRDCLE CHARACTER SIZE 12 ; Code de produit

;-------------------------------------------------------------------------------
;
FILE VPRE_PRD        PRIMARY &
                    OPEN READ &
                    OCCURS 11

 ACCESS   VIA   CIECLE, &
                ENTCLE, &
                PRDCLE  & 
        USING   T_CIECLE, &
                T_ENTCLE, &
                T_PRDCLE  &
        ORDERBY CIECLE , &
                ENTCLE , &
                PRDCLE   
      


;-------------------------------------------------------------------------------
;

USE ushilite NOLIST     
USE usdrwfnd NOLIST     

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(3,3,6)(,,19)

CLUSTER OCCURS WITH VPRE_PRD

FIELD PRDCLE    OF VPRE_PRD HIDDEN LABEL ">>"
FIELD PRDDSC1   OF VPRE_PRD &
        SIZE 20

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enlève le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_PRDCLE, "@")
  THEN LET T_PRDCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_PRDCLE = PRDCLE OF VPRE_PRD
  RETURN
END

BUILD
@IF FORMAT
                                       *****************************************
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  *
                                       *****************************************
@ENDIF
