;/T/ Gestion des mémos.
;
;/P/ Programmeur..: Alain Côté / adapté pour les P.R. par Marc Morissette
;    Date Création: 7 Juin 1993
;
;    Description..: Saisie des mémos. Un mémo peut être spécifique à un
;                   projet, activité, unité
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr050 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_GPMCLE1, &
                       T_GPMCLE2, &
                       T_GPMTYP


USE ustrasse

USE ussectmp NOLIST
    "PR050"

USE pr050.hlp

USE usactecr NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-Ecrans"
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "Verification                    " ACTION DESIGNER D001
@ENDIF


;-------------------------------------------------------------------------------

TEMPORARY T_GPMCLE1   CHARACTER SIZE 8  ; Projet ou équipement
TEMPORARY T_GPMCLE2   CHARACTER SIZE 3  ; Catégorie ou Activité
TEMPORARY T_GPMTYP    CHARACTER SIZE 3  ; Type de mémo
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------
;
; RELATION MÉMO.
;
;   *** ATTENTION : LES PROCÉDURES PATH & FIND ONT ÉTÉ RÉÉCRITES ! ***
;
FILE GPMEMO PRIMARY

  ITEM CIECLE    OF GPMEMO INITIAL T_CIECLEMNU
  ITEM GPMTIMSTP OF GPMEMO INITIAL SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  ITEM GPMCLE1   OF GPMEMO INITIAL T_GPMCLE1
  ITEM GPMCLE2   OF GPMEMO INITIAL T_GPMCLE2
  ITEM GPMTYP    OF GPMEMO INITIAL T_GPMTYP
  ITEM GPMDATCRE OF GPMEMO INITIAL SYSDATE
  ITEM GPMHRECRE OF GPMEMO INITIAL SYSTIME / 10000
  ITEM GPMUSRCRE OF GPMEMO INITIAL LOGONID
  ;
  ; On enregistre la date, et heure de modification, ainsi que l'usager
  ; qui modifie le mémo
  ;
  ITEM GPMDATMOD OF GPMEMO FINAL   SYSDATE        &
                                        IF NOT NEWRECORD OF GPMEMO AND &
                                           ALTEREDRECORD OF GPMEMO
  ITEM GPMHREMOD OF GPMEMO FINAL   SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF GPMEMO AND &
                                           ALTEREDRECORD OF GPMEMO
  ITEM GPMUSRMOD OF GPMEMO FINAL   LOGONID         &
                                        IF NOT NEWRECORD OF GPMEMO AND &
                                           ALTEREDRECORD OF GPMEMO

;-------------------------------------------------------------------------------
;
; Détail du mémo.
;
;   *** ATTENTION : LES PROCÉDURES PATH & FIND ONT ÉTÉ RÉÉCRITES ! ***
;
FILE GPGPM_LIGNE      DETAIL &
                      OCCURS 15 &
                      NOITEM

  ITEM CIECLE    OF GPGPM_LIGNE INITIAL CIECLE    OF GPMEMO
  ITEM GPMTIMSTP OF GPGPM_LIGNE INITIAL GPMTIMSTP OF GPMEMO

;
; On détruit toutes les lignes si le maitre est détruit.
;
FILE GPGPM_LIGNE      DELETE &
                      NOITEM &
                      ALIAS A_GPL_DELETE

  ACCESS VIA    CIECLE, &
                GPMTIMSTP &
         USING  CIECLE    OF GPMEMO, &
                GPMTIMSTP OF GPMEMO

TEMPORARY T_NUMLIG INTEGER UNSIGNED SIZE 2 & ; Numérotation auto. des lignes.
                   OCCURS WITH GPMEMO

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

DEFINE D_LABEL   CHARACTER SIZE 13 = "Projet......:" IF T_GPMTYP <> "UNI" &
                               ELSE  "Équipement..:"

DEFINE D_LABEL2  CHARACTER SIZE 13 = "Sous-Projet.:" IF T_GPMTYP = "ACT" &
                               ELSE  "Catégorie...:" IF T_GPMTYP = "UNI" &
                               ELSE  ""
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des mémos " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN (,,3) (19,,19) (,58,67)
FIELD D_LABEL DISPLAY NOLABEL NOID
FIELD GPMCLE1 OF GPMEMO HIDDEN ID 05 &
        NOCHANGE &
        IF T_GPMCLE1 = ""

FIELD GPMDAT OF GPMEMO  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN ID 10 &
        LABEL "Date..:" &
        REQUIRED


FIELD D_LABEL2 DISPLAY NOLABEL NOID
FIELD GPMCLE2 OF GPMEMO HIDDEN ID SAME &
        NOCHANGE &
        IF T_GPMCLE1 = ""

SKIP
DRAW ,1 TO ,80
SKIP 1

TITLE &
@IF FRANCAIS
      "Texte                                                         Ligne" &
@ELSE
      "Text                                                          Line " &
@ENDIF
      AT ,3

SKIP

ALIGN (3,2,3) (,,65)

CLUSTER OCCURS WITH GPGPM_LIGNE ID BASE 15

FIELD GPLTXT    OF GPGPM_LIGNE HIDDEN LABEL " "

FIELD GPLCLELIG OF GPGPM_LIGNE

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Assigne un numéro de ligne automatiquement.
;
;
PROCEDURE INPUT GPLCLELIG
BEGIN
  IF     0 = SIZE( FIELDTEXT ) &
     AND 0 = GPLCLELIG OF GPGPM_LIGNE &
     AND NOT FINDMODE
  THEN LET FIELDTEXT = ASCII( ( T_NUMLIG + 100 ) / 100 )
END


;-------------------------------------------------------------------------------
;
;
; Incrémente le numéro de ligne.
;
;
PROCEDURE PROCESS GPLCLELIG
BEGIN
  IF T_NUMLIG < GPLCLELIG OF GPGPM_LIGNE
  THEN LET T_NUMLIG = GPLCLELIG OF GPGPM_LIGNE + 100
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Sous-écran   Vérification.
;
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN pr050a PASSING GPMEMO
END

;-------------------------------------------------------------------------------
;
;
;
;
PROCEDURE PATH
BEGIN
  IF T_GPMCLE1 <> ""
  THEN LET PATH = 99
  ELSE BEGIN
    REQUEST GPMCLE1 OF GPMEMO
    IF PROMPTOK
    THEN BEGIN
      REQUEST GPMDAT OF GPMEMO
      IF PROMPTOK
      THEN LET PATH = 1
      ELSE LET PATH = 2
    END
    IF PATH = 0
    THEN BEGIN
      REQUEST GPMDAT OF GPMEMO
      IF PROMPTOK
      THEN LET PATH = 3
    END
  END
END


PROCEDURE FIND
BEGIN
  ;
  ; Recherche sur le fournisseur reçu en paramètre.
  ;
  IF PATH = 99
  THEN GET GPMEMO VIA   CIECLE   , &
                        GPMCLE1, &
                        GPMCLE2     &
                  USING T_CIECLEMNU, &
                        T_GPMCLE1, &
                        T_GPMCLE2
  ;
  ; Recherche par fournisseur et date
  ;
  IF PATH = 1
  THEN GET GPMEMO VIA   CIECLE   , &
                        GPMCLE1  , &
                        GPMCLE2  , &
                        GPMDAT     &
                  USING T_CIECLEMNU, &
                        T_GPMCLE1, &
                        GPMCLE2 OF GPMEMO, &
                        GPMDAT OF GPMEMO

  ;
  ; Recherche par fournisseur.
  ;
  IF PATH = 2
  THEN GET GPMEMO VIA     CIECLE   , &
                          GPMCLE1  , &
                          GPMCLE2    &
                  USING   T_CIECLEMNU, &
                          T_GPMCLE1  , &
                          GPMCLE2 OF GPMEMO &
                  ORDERBY CIECLE, &
                          GPMDAT
  ;
  ; Recherche par date.
  ;
  IF PATH = 3
  THEN GET GPMEMO VIA     CIECLE   , &
                          GPMDAT     &
                  USING   T_CIECLEMNU, &
                          GPMDAT OF GPMEMO &
                  ORDERBY GPMCLE1, &
                          GPMCLE2, &
                          CIECLE, &
                          GPMDAT

  ;
  ; Recherche par compagnie du menu.
  ;
  IF PATH = 0
  THEN GET GPMEMO VIA     CIECLE &
                  USING   T_CIECLEMNU &
                  ORDERBY GPMCLE1, &
                          GPMCLE2, &
                          CIECLE, &
                          GPMDAT
END


PROCEDURE DETAIL FIND
BEGIN
  FOR GPGPM_LIGNE
  BEGIN
    GET GPGPM_LIGNE VIA     CIECLE   , &
                            GPMTIMSTP  &
                    USING   CIECLE    OF GPMEMO, &
                            GPMTIMSTP OF GPMEMO  &
                    ORDERBY CIECLE, &
                            GPMTIMSTP, &
                            GPLCLELIG
  END
END


;-------------------------------------------------------------------------------
;
; VALIDATION DE LA MISE À JOUR
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPMEMO &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPMEMO &
     AND NOT NEWRECORD     OF GPMEMO &
     AND NOT DELETEDRECORD OF GPMEMO &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Le timbre de modification du mémo est mis à jour si l'usager modifie
  ; le détail.
  ;
  IF NOT NEWRECORD OF GPMEMO
  THEN BEGIN
    FOR GPGPM_LIGNE
    BEGIN
      IF   NEWRECORD     OF GPGPM_LIGNE &
        OR ALTEREDRECORD OF GPGPM_LIGNE &
        OR DELETEDRECORD OF GPGPM_LIGNE
      THEN BEGIN
        LET GPMDATMOD OF GPMEMO = SYSDATE
        LET GPMHREMOD OF GPMEMO = SYSTIME / 10000
        LET GPMUSRMOD OF GPMEMO = LOGONID
      END
    END
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Gestion des mémos *******************************
* Fournisseur...: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
* Date..........: xxxxxxxx                                                     *
********************************************************************************
* Texte                                                         Ligne          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxx          *
********************************************************************************
@ENDIF
