;/T/ Consultation des factures.
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 7 Septembre 1994
;
;    Description..: Permet de visionner et de choisir une réception
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar118 NOMODE NOACTION FIELDMARK &
             FROM 11,14 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_FOUCLE, &
                        T_FACCLE 
 
                    

USE ustrasse NOLIST

USE ar118.hlp NOLIST

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Compagnie de la réception
TEMPORARY T_FACCLE    CHARACTER SIZE 12; Code de réception
TEMPORARY T_FOUCLE    CHARACTER SIZE 6 ; Numéro de fournisseur

;-------------------------------------------------------------------------------
;
FILE ARFACTURE_REC       PRIMARY &
                        OCCURS 10

 ACCESS   VIA     FACCIECLE, &
                  FACCLE     &
          USING   T_CIECLE,  &
                  T_FACCLE   &
          ORDERBY FACCIECLE, &
                  FACCLE   
  
;-------------------------------------------------------------------------------
;
;
;
FILE MCREF_ADRESSE     REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
         USING  FOUCIECLE OF ARFACTURE_REC, &
                "F"                       , &
                FOUCLE    OF ARFACTURE_REC, &
                RADCLE    OF ARFACTURE_REC

;-------------------------------------------------------------------------------
;
; Permet d'avoir l'information du fournisseur pour la cie de transaction.
;
FILE VFOC_FOU            REFERENCE

  ACCESS VIA   FOUCIECLE, &
               FOUCLE   , &
               FOCCIECLE  &
         USING FOUCIECLE OF ARFACTURE_REC, &
               FOUCLE    OF ARFACTURE_REC, &
               T_CIECLE


;-------------------------------------------------------------------------------
;
; Pour la présentation de la devise ou non.
;
FILE MCHOLDING           REFERENCE &
                        NOITEM
  ACCESS VIA   CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;

DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING &
          ELSE "/" + DEVCLE OF VFOC_FOU


USE ushilite NOLIST     
DRAW 01,01 TO 13,67

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE "    Facture             Fournisseur             Montant tot  " AT ,02

SKIP

ALIGN(3,3,6)(,,19)(,,26)(,,47)(,,62)

CLUSTER OCCURS WITH ARFACTURE_REC

FIELD FACCLE    OF ARFACTURE_REC HIDDEN LABEL ">>"
FIELD FOUCLE    OF ARFACTURE_REC
FIELD RADNOM    OF MCREF_ADRESSE SIZE 20
FIELD FACMNTTOT OF ARFACTURE_REC
FIELD D_DEVCLE

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_FACCLE, "@")
  THEN LET T_FACCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_FOUCLE = FOUCLE OF ARFACTURE_REC
  LET T_FACCLE = FACCLE OF ARFACTURE_REC
  RETURN
END

BUILD
@IF FORMAT
             *******************************************************************
             *    Facture             Fournisseur             Montant tot      *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             * >> xxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
             *******************************************************************
@ENDIF
