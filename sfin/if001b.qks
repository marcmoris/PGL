;/T/ Historique des mouvements de blocs.
;
;/P/ Programmeur....: Marc Poulin
;    Date Création..: 11 octobre 1999
;
;    Description....: Cet écran permet la consultation et la mise à jour de 
;                     l'historique des mouvements de blocs.
;
;-------------------------------------------------------------------------------
;
;    Questions......: 1- Les frais de transports devraient-ils venir de l'écran
;                        de facturation des Comptes à payer ?
;                     2- À quel moment les informations devraient-elles être non
;                        modifiables ? Lorsque le bloc a été commandé ou expédié ?
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN if001b ACTIONBAR                         &
              MODE LABEL "" AT 2,132            &
              FROM 1,1 TO 23,132                &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72      &
              ACTIVITIES FIND,                  &
                         CHANGE                 &
              RECEIVING  T_CIECLEMNU,           &
                         T_CIENOM   ,           &
                         IFBLOC     

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "IF001B"

;-------------------------------------------------------------------------------
;
; Déclaration de la transaction QUERY pour les sous-écrans
;
USE ustrasse  NOLIST

;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE if001b.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU  CHARACTER SIZE 04  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM     CHARACTER SIZE 40  RESET AT STARTUP ; Nom de compagnie


;-------------------------------------------------------------------------------
;
; Blocs - Table reçue en paramêtre
;

FILE IFBLOC MASTER


;-------------------------------------------------------------------------------
;
; Historique des mouvements de blocs
;

FILE IFHISTO_MOUBLOC PRIMARY OCCURS 15 NOITEMS
  ACCESS VIA CIECLE             , &
             TGRCODGRA          , &
             IFCCLE             , &
             BLCCLE             , &
             BLCRED               &
       USING T_CIECLEMNU        , &
             TGRCODGRA OF IFBLOC, &
             IFCCLE    OF IFBLOC, &
             BLCCLE    OF IFBLOC, &
             BLCRED    OF IFBLOC  &
     ORDERBY HMBDATCRE DESC     , &
             HMBHRECRE DESC     

;-------------------------------------------------------------------------------
;
; Code et non de l'usager
;
FILE GSUSAGER REFERENCE OCCURS WITH IFHISTO_MOUBLOC
  ACCESS VIA USRCLE  &
       USING HMBUSRCRE OF IFHISTO_MOUBLOC

;-------------------------------------------------------------------------------
;
; Fichier permettant de valider que le fournisseur existe.
;
FILE VFOC_FOU REFERENCE OCCURS WITH IFHISTO_MOUBLOC

;-------------------------------------------------------------------------------
;
; Entrepôts
;
FILE IFENTREPOT REFERENCE OCCURS WITH IFHISTO_MOUBLOC
  ACCESS VIA CIECLE     , &
             ETRCLE       &
       USING T_CIECLEMNU, &
             ETRCLE OF IFHISTO_MOUBLOC

;-------------------------------------------------------------------------------
;
; Pour voir s'il y a une charte de fournisseurs
;
FILE MCHOLDING DESIGNER OPEN READ

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 04 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)

;-------------------------------------------------------------------------------
;
; Description du type de mouvement
;
DEFINE D_TYPMOU_DSC  CHARACTER SIZE 09 = "Entrée"                           &
                                      IF HMBTYPMOU OF IFHISTO_MOUBLOC = "E" &
                                    ELSE "Transfert"                        

;-------------------------------------------------------------------------------
;
; Temporaire associée au frais de transport
;
TEMPORARY T_OLD_HMBFRSTRP  INTEGER SIZE 8  OCCURS WITH IFHISTO_MOUBLOC

;-------------------------------------------------------------------------------
;
; Temporaires associées au fournisseur (transporteur)
;
TEMPORARY T_FOUCLE     CHARACTER SIZE 06  RESET AT STARTUP ; Code de fournisseur.
TEMPORARY T_FOUCIECLE  CHARACTER SIZE 04  RESET AT STARTUP ; Compagnie du fournisseur.
TEMPORARY T_FOCCIECLE  CHARACTER SIZE 04  RESET AT STARTUP ; Compagnie disponible au fournisseur.
TEMPORARY T_USINE_SEC  CHARACTER SIZE 01  RESET AT STARTUP ; Code de sécurité

;-------------------------------------------------------------------------------
;
;
USE usdrw132    NOLIST ; Draw pour les écrans
USE ustitstd132 NOLIST ; En-tête pour les écrans pleine page
USE ushilite    NOLIST ; Hilite pour tout les écrans

SKIP

TITLE " Historique des mouvements de blocs " CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF

SKIP TO 4
ALIGN (,3,19)(,,22)(,24,25)(,31,32)(,36,42)

FIELD TGRCODGRA OF IFBLOC               &
      LABEL "No bloc.......:"           &
      PREDISPLAY                        &
      FIXED                             &
      DISPLAY                           
                                        
FIELD IFCCLE OF IFBLOC                  &
      PREDISPLAY                        &
      FIXED                             &
      DISPLAY                           
                                        
FIELD BLCCLE OF IFBLOC                  &
      PREDISPLAY                        &
      FIXED                             &
      DISPLAY                           
                                        
FIELD BLCRED OF IFBLOC                  &
      LABEL "-"                         &
      PREDISPLAY                        &
      FIXED                             &
      DISPLAY

FIELD BLCDAT OF IFBLOC                  &
      LABEL "Date:"                     &
      FORMAT YYYYMMDD                   &
      PREDISPLAY                        &
      FIXED                             &
      DISPLAY

DRAW 5,1 TO 5,132

TITLE "   Date       Heure        Usager            Type      Transporteur   No référence   Frais transport         Entrepôt       " AT 6,6

SKIP TO 7

ALIGN (3,,6)(,,18)(,,28)(,,48)(,,50)(,,64)(,,77)(,,91)(,,108)(,,113)
CLUSTER OCCURS WITH IFHISTO_MOUBLOC

FIELD HMBDATCRE OF IFHISTO_MOUBLOC      &
      ID 01                             &
      FORMAT YYYYMMDD                   &
      DISPLAY                           

FIELD HMBHRECRE OF IFHISTO_MOUBLOC      &
      PIC "^^:^^:^^"                    &
      OUTPUT SCALE -2                   &
      SIGNIFICANCE  8                   &
      DISPLAY                           
                                        
FIELD USREMPNOM OF GSUSAGER             &
      SIZE 18                           &
      DISPLAY                           
                                        
FIELD HMBTYPMOU OF IFHISTO_MOUBLOC      &
      DISPLAY                           
                                        
FIELD D_TYPMOU_DSC                      &
      DISPLAY

FIELD FOUCLE    OF IFHISTO_MOUBLOC      &
      LOOKUP ON VFOC_FOU                &
         VIA FOUCIECLE                , &
             FOUCLE                   , &
             FOCCIECLE                  &
       USING T_FOUCIECLE              , &
             FOUCLE OF IFHISTO_MOUBLOC, &
             CIECLE OF IFHISTO_MOUBLOC  &
     MESSAGE 1355 ; Ce fournisseur n'existe pas.

FIELD HMBNUMREF OF IFHISTO_MOUBLOC         &
      LOOKUP NOTON IFHISTO_MOUBLOC         &
         VIA CIECLE                      , &
             HMBNUMREF                     &
       USING CIECLE    OF IFHISTO_MOUBLOC, &
             HMBNUMREF OF IFHISTO_MOUBLOC  &
     MESSAGE 3292 ; PD392 Ce numéro de référence a déjà été utilisé

FIELD HMBFRSTRP OF IFHISTO_MOUBLOC      &
      PIC "^^^,^^^,^^^.^^ "             &
      SIGNIFICANCE 5                    &
      TRAILING SIGN "-"

FIELD ETRCLE    OF IFHISTO_MOUBLOC      &
      DISPLAY

FIELD ETRDSCFRA001 OF IFENTREPOT        &
      SIZE 17                           &
      DISPLAY

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST

  ;
  ; On accède au Holding pour voir s'il y a une charte de fournisseurs
  ; par compagnie et/ou une charte d'employés par compagnie. Si non, on
  ; par les caractères de remplacement contenus dans le Holding. (Voir
  ; remplace les clés compagnie du fournisseur et/ou compagnie de l'employé
  ; la procédure PROCESS du champs T_CIECLEMNU)
  ;
  GET MCHOLDING VIA CIENMC USING "1"

  IF HLDCHTFOU OF MCHOLDING = "1"
  THEN LET T_FOUCIECLE = T_CIECLEMNU
  ELSE LET T_FOUCIECLE = HLDCHRSUB OF MCHOLDING
END

;-------------------------------------------------------------------------------
;
; Ecran de consultation (dépisteur) des fournisseurs
;
PROCEDURE INPUT FOUCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd117 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_FOUCLE   , &
                             T_USINE_SEC  &
                     WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_FOUCLE )
  END
END

;-------------------------------------------------------------------------------
;
; Validations unitaires sur le code de fournisseur.
;
PROCEDURE EDIT FOUCLE
BEGIN
  IF FOUSTU OF VFOC_FOU = "I"
  THEN ERROR 1391 ; Le fournisseur est inactif.

  IF FOURTU OF VFOC_FOU = "1"
  THEN WARNING 1392 ; Le fournisseur est présentement retenu.
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF IFHISTO_MOUBLOC &
     AND NOT NEWRECORD     OF IFHISTO_MOUBLOC &
     AND NOT DELETEDRECORD OF IFHISTO_MOUBLOC &
     AND TZ_SECMOD = "0"
  THEN ERROR 2


  ; Ajustement du coût du bloc

  FOR IFHISTO_MOUBLOC
  BEGIN
    LET BLCMNTM3 OF IFBLOC = BLCMNTM3 OF IFBLOC &
                           - T_OLD_HMBFRSTRP    &
                           + HMBFRSTRP OF IFHISTO_MOUBLOC
  END
END

;-------------------------------------------------------------------------------
;
; Assignations des frais de transports originaux
;
PROCEDURE POSTFIND
BEGIN
  FOR IFHISTO_MOUBLOC
  BEGIN
    LET T_OLD_HMBFRSTRP = HMBFRSTRP OF IFHISTO_MOUBLOC
  END
END


;-------------------------------------------------------------------------------
;
; Assignations des nouveaux frais de transports
;
PROCEDURE POSTUPDATE
BEGIN
  FOR IFHISTO_MOUBLOC
  BEGIN
    LET T_OLD_HMBFRSTRP = HMBFRSTRP OF IFHISTO_MOUBLOC
  END
END




BUILD LIST DETAIL

@IF FORMAT
@ENDIF

