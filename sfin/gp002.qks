;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Fiche de sous-projet
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp002 ACTIONBAR &
             MODE LABEL "" AT 2,132 &
             NOACTION &
             from 1,1 to 23,132 &
             FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_FIELDVALUE_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie - client
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_PRACLE    CHARACTER SIZE 3  RESET AT STARTUP ; Numero de sous-projet
TEMPORARY T_CLICLE2   CHARACTER SIZE 6  RESET AT STARTUP ; Numéro de client
TEMPORARY T_RADCLE2   INTEGER UNSIGNED SIZE 2 RESET AT STARTUP

USE ussectmp NOLIST     ; Variable pour la securite
    "GP002"             ; Nom de l'écran

USE gp002.hlp NOLIST    ; Use servant à la documentation de l'écran
                        ; Ne pas oublier de créer un fichier vide dans [exefra], [exeang]


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Informations complémentaires" ACTION DESIGNER D001
  MENUITEM LABEL "Client supplémentaire       " ACTION DESIGNER D002
  MENUITEM LABEL "Budget d'opération          " ACTION DESIGNER D003
  MENUITEM LABEL "Budget comptable            " ACTION DESIGNER D004
  MENUITEM LABEL "Information opération       " ACTION DESIGNER D005
  MENUITEM LABEL "Information comptable       " ACTION DESIGNER D006
  MENUITEM LABEL "Piste de vérification       " ACTION DESIGNER D007
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "Informations complémentaires" ACTION DESIGNER D001
  MENUITEM LABEL "Client supplémentaire       " ACTION DESIGNER D002
  MENUITEM LABEL "Budget d'opération          " ACTION DESIGNER D003
  MENUITEM LABEL "Budget comptable            " ACTION DESIGNER D004
  MENUITEM LABEL "Information opération       " ACTION DESIGNER D005
  MENUITEM LABEL "Information comptable       " ACTION DESIGNER D006
  MENUITEM LABEL "Piste de vérification       " ACTION DESIGNER D007
@ENDIF

;------------------------------------
; Fichier de sous-projets
;------------------------------------
FILE GPPRO_ACTIVITE         PRIMARY &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   T_CIECLEMNU, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE  &
          REQUEST PRJCLE, &
                  PRACLE  &
          ORDERBY PRJCLE, &
                  PRACLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE  &
          USING   T_CIECLEMNU, &
                  PRJCLE OF GPPRO_ACTIVITE &
          REQUEST PRJCLE  &
          ORDERBY PRJCLE, &
                  PRACLE

  ACCESS  VIA     CIECLE &
          USING   T_CIECLEMNU &
          ORDERBY PRJCLE, &
                  PRACLE

  ITEM CIECLE       OF GPPRO_ACTIVITE  INITIAL T_CIECLEMNU
  ITEM CLICIECLE    OF GPPRO_ACTIVITE  INITIAL T_CLICIECLE
  ITEM PRAUSRCRE    OF GPPRO_ACTIVITE  INITIAL LOGONID
  ITEM PRADATCRE    OF GPPRO_ACTIVITE  INITIAL SYSDATE
  ITEM PRAHRECRE    OF GPPRO_ACTIVITE  INITIAL SYSTIME
  ITEM PRADATMOD    OF GPPRO_ACTIVITE  FINAL   SYSDATE        &
                                       IF NOT NEWRECORD OF GPPRO_ACTIVITE  AND &
                                          ALTEREDRECORD OF GPPRO_ACTIVITE
  ITEM PRAHREMOD    OF GPPRO_ACTIVITE  FINAL   SYSTIME &
                                       IF NOT NEWRECORD OF GPPRO_ACTIVITE  AND &
                                          ALTEREDRECORD OF GPPRO_ACTIVITE
  ITEM PRAUSRMOD    OF GPPRO_ACTIVITE  FINAL   LOGONID         &
                                       IF NOT NEWRECORD OF GPPRO_ACTIVITE  AND &
                                          ALTEREDRECORD OF GPPRO_ACTIVITE


;---------------------------------
; Fichier des clients
;---------------------------------
FILE GPPRA_CLIENT           SECONDARY &
                            NOITEMS


;--------------------
; Fichier des projets
;--------------------
TEMPORARY T_PRJCLE    CHARACTER SIZE 8  RESET AT STARTUP ; Numero de projet
TEMPORARY T_PRJSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.

FILE GPPROJETS              REFERENCE

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRJCLE    OF GPPRO_ACTIVITE


;---------------------------------------------
; Fichier des unités administratives (dépense)
;---------------------------------------------
TEMPORARY T_PRAUNADEP CHARACTER SIZE 8

FILE MCUNITE_ADM            REFERENCE &
                            ALIAS MCUNITE_ADM_DEP

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAUNADEP OF GPPRO_ACTIVITE


;------------------------------------------
; Fichier des unités administratives (paie)
;------------------------------------------
TEMPORARY T_PRAUNAPAI CHARACTER SIZE 8

FILE MCUNITE_ADM            REFERENCE &
                            ALIAS MCUNITE_ADM_PAY

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAUNAPAI OF GPPRO_ACTIVITE


;--------------------------------------------
; Fichier des unités administratives (revenu)
;--------------------------------------------
TEMPORARY T_PRAUNAREV CHARACTER SIZE 8

FILE MCUNITE_ADM            REFERENCE &
                            ALIAS MCUNITE_ADM_REV

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAUNAREV OF GPPRO_ACTIVITE


;----------------------------
; Fichier des immobilisations
;----------------------------
TEMPORARY T_IMMCLE    CHARACTER SIZE 12

FILE IMIMMOBILISATION       REFERENCE

  ACCESS  VIA   CIECLE, &
                IMMCLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAIMMCLE OF GPPRO_ACTIVITE


;---------------------------------
; Fichier des clients / compagnies
;---------------------------------
FILE VCLC_CLI               REFERENCE

  ACCESS  VIA   CLICIECLE, &
                CLICLE,    &
                CIECLE     &
          USING CLICIECLE OF GPPRO_ACTIVITE, &
                CLICLE    OF GPPRO_ACTIVITE, &
                CIECLE    OF GPPRO_ACTIVITE


;---------------------------------
; Fichier des références d'adresse
;---------------------------------
TEMPORARY T_CLICLE CHARACTER SIZE 6

FILE MCREF_ADRESSE          REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
          USING CLICIECLE OF GPPRO_ACTIVITE, &
                "C"                        , &
                CLICLE    OF GPPRO_ACTIVITE, &
                1


;-----------------------------------------------------------------------
; Validation du type de cargaison du projet et lecture de sa description
;-----------------------------------------------------------------------
DEFINE    D_PRACRG CHARACTER SIZE 16 = "CRGCLE"
TEMPORARY T_PRACRG CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRACRG

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRACRG, &
                PRACRG OF GPPRO_ACTIVITE


;-------------------------------------------------------------------
; Validation du responsable du projet et lecture de sa description
;-------------------------------------------------------------------
DEFINE    D_PRARES CHARACTER SIZE 16 = "RESCLE"
TEMPORARY T_PRARES CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRARES

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRARES, &
                PRARES OF GPPRO_ACTIVITE


;----------------------------------------------------------
; Validation du statut du projet et lecture de sa description
;----------------------------------------------------------
DEFINE    D_PRASTU CHARACTER SIZE 16 = "PRASTU"
TEMPORARY T_PRASTU CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_PRASTU

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_PRASTU, &
                PRASTU OF GPPRO_ACTIVITE



;-------------------------------------------------------
; Fichier de destruction des descriptions / sous-projets
;-------------------------------------------------------
FILE GPPRA_DESC             DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE, &
                PRACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRJCLE    OF GPPRO_ACTIVITE, &
                PRACLE    OF GPPRO_ACTIVITE


;--------------------------------------------------
; Fichier de destruction des clients / sous-projets
;--------------------------------------------------
FILE GPPRA_CLIENT           DELETE &
                            ALIAS GPPRA_CLIENT_2 &
                            OPEN 1 &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE, &
                PRACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRJCLE    OF GPPRO_ACTIVITE, &
                PRACLE    OF GPPRO_ACTIVITE


;---------------------------------------------------------
; Fichier de destruction du budget comptable / sous-projet
;---------------------------------------------------------
FILE GPPRA_BDG_CTB          DELETE  &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE, &
                PRACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRJCLE    OF GPPRO_ACTIVITE, &
                PRACLE    OF GPPRO_ACTIVITE


;-----------------------------------------------------------
; Fichier de destruction du budget d'opération / sous-projet
;-----------------------------------------------------------
FILE GPPRA_BDG_UNT          DELETE &
                            NOITEMS

  ACCESS  VIA   CIECLE, &
                PRJCLE, &
                PRACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRJCLE    OF GPPRO_ACTIVITE, &
                PRACLE    OF GPPRO_ACTIVITE





;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr132 NOLIST  ; Draw pour les écrans pleine page
USE ustitstd132 NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Fiche du sous-projet " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN(3,3,19)(,,28)

FIELD PRJCLE OF GPPRO_ACTIVITE                        &
label "Projet........:" &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LOOKUP ON GPPROJETS                             &
        MESSAGE 857; Ce projet est inexistant.

FIELD PRJDSC1 OF GPPROJETS                            &
      ID SAME                                         &
      DISPLAY

ALIGN(3,3,19)(,45,61)(,,63)

FIELD PRACLE OF GPPRO_ACTIVITE                        &
label "Sous-projet...:" &
      HIDDEN                                          &
      REQUIRED NOCHANGE                               &
      LOOKUP NOTON GPPRO_ACTIVITE                     &
        VIA   CIECLE,                                 &
              PRJCLE,                                 &
              PRACLE                                  &
        USING T_CIECLEMNU,                            &
              PRJCLE OF GPPRO_ACTIVITE,               &
              PRACLE OF GPPRO_ACTIVITE                &
        MESSAGE 858; Ce sous-projet existe déjà.

FIELD PRASTU OF GPPRO_ACTIVITE                        &
label "Statut........:" &
      ID SAME                                         &
      DEFAULT "A"                                     &
      LOOKUP ON MCCODE_BUIL_PRASTU                    &
        MESSAGE 859; Ce statut de sous-projet est invalide

FIELD CBIDSC    OF MCCODE_BUIL_PRASTU                 &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 15

ALIGN (3,3,19)
FIELD PRADSC1 OF GPPRO_ACTIVITE                       &
label "Description...:" &
      HIDDEN

FIELD PRADSC2 OF GPPRO_ACTIVITE                       &
      LABEL " "                                       &
      HIDDEN

SKIP 1

ALIGN (3,3,19) (,32,48) (,,53)
FIELD PRAANN OF GPPRO_ACTIVITE                        &
label "Année sous-pro:" &
      HIDDEN

FIELD PRACRG OF GPPRO_ACTIVITE                        &
label "Type cargaison:" &
      ID SAME                                         &
      REQUIRED                                        &
      LOOKUP ON MCCODE_BUIL_PRACRG                    &
        MESSAGE 865; Ce type de cargaison est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_PRACRG                 &
      ID SAME                                         &
      DISPLAY

ALIGN (3,3,19) (,,28) (,,69)
FIELD CLICLE OF GPPRO_ACTIVITE                        &
label "Client........:" &
      HIDDEN                                          &
      LOOKUP ON VCLC_CLI                              &
        MESSAGE 836,                                  &
             ON MCREF_ADRESSE                         &
        MESSAGE 836; Ce client est inexistant

FIELD CLINOM OF VCLC_CLI                              &
      ID SAME                                         &
      DISPLAY

FIELD DEVCLE OF VCLC_CLI                              &
      ID SAME                                         &
      DISPLAY                                         &
      PICTURE "/^^^"

ALIGN (3,3,19) (,,28)
FIELD PRARES OF GPPRO_ACTIVITE                        &
label "Responsable...:" &
      HIDDEN                                          &
      REQUIRED                                        &
      LOOKUP ON MCCODE_BUIL_PRARES                    &
        MESSAGE 837; Ce responsable est inexistant

FIELD CBIDSC    OF MCCODE_BUIL_PRARES                 &
      ID SAME                                         &
      DISPLAY

SKIP 1

ALIGN (3,3,49) (,,60) (,,72) (,,82)

FIELD PRADATPOSEST OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      HIDDEN                                          &
      LABEL "Date / heure positionnement estimée, réelle.:"

FIELD PRAHREPOSEST OF GPPRO_ACTIVITE                  &
      ID SAME

FIELD PRADATPOSREE OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      ID SAME                                         &
      NOENTRY

FIELD PRAHREPOSREE OF GPPRO_ACTIVITE                  &
      ID SAME                                         &
      NOENTRY

FIELD PRADATDEPEST OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      HIDDEN                                          &
      LABEL "Date / heure départ estimée, réelle.........:"

FIELD PRAHREDEPEST OF GPPRO_ACTIVITE                  &
      ID SAME

FIELD PRADATDEPREE OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      ID SAME                                         &
      NOENTRY

FIELD PRAHREDEPREE OF GPPRO_ACTIVITE                  &
      ID SAME                                         &
      NOENTRY

FIELD PRADATFINEST OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      HIDDEN                                          &
      LABEL "Date / heure fin estimée, réelle............:"

FIELD PRAHREFINEST OF GPPRO_ACTIVITE                  &
      ID SAME

FIELD PRADATFINREE OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      ID SAME                                         &
      NOENTRY

FIELD PRAHREFINREE OF GPPRO_ACTIVITE                  &
      ID SAME                                         &
      NOENTRY

ALIGN (,3,49) (,,60)
FIELD PRAPACDATAPP OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      LABEL "Date d'approbation du budget comptable......:" &
      DISPLAY

FIELD PRAPACUSRAPP OF GPPRO_ACTIVITE                  &
      DISPLAY

FIELD PRAPAUDATAPP OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##" &
      LABEL "Date d'approbation du budget d'opération....:" &
      DISPLAY

FIELD PRAPAUUSRAPP OF GPPRO_ACTIVITE                  &
      DISPLAY

SKIP 1

ALIGN (3,3,19) (,,32)
FIELD PRAUNADEP OF GPPRO_ACTIVITE                     &
label "Unité dépense.:" &
      HIDDEN                                          &
      REQUIRED

FIELD UNANOM OF MCUNITE_ADM_DEP                       &
      ID SAME                                         &
      DISPLAY

FIELD PRAUNAREV OF GPPRO_ACTIVITE                     &
label "Unité revenu..:" &
      HIDDEN                                          &
      REQUIRED

FIELD UNANOM OF MCUNITE_ADM_REV                       &
      ID SAME                                         &
      DISPLAY


FIELD PRAUNAPAI OF GPPRO_ACTIVITE                     &
label "Unité dépense.:" &
      HIDDEN                                          &
      REQUIRED

FIELD UNANOM OF MCUNITE_ADM_PAY                       &
      ID SAME                                         &
      DISPLAY

FIELD PRAIMMCLE OF GPPRO_ACTIVITE                     &
label "Immobilisation:" &
      HIDDEN                                          &
      REQUIRED                                        &
      LOOKUP ON IMIMMOBILISATION                      &
        MESSAGE 861 ; Immobilisation inexistante.

FIELD IMMDSC OF IMIMMOBILISATION                      &
      ID SAME                                         &
      DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRJCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PRJSTUPAT = "A"
    RUN SCREEN gp104 MODE F PASSING T_CIECLEMNU, &
                                    T_PRJCLE   , &
                                    T_PRJSTUPAT
    LET FIELDTEXT = TRUNCATE(T_PRJCLE)
  END
END

PROCEDURE EDIT PRJCLE
BEGIN
   IF PRJSTU OF GPPROJETS <> "A"
   THEN ERROR 862; Le statut du projet doit être actif.
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRASTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRASTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRASTU, T_PRASTU
    LET FIELDTEXT = TRUNCATE(T_PRASTU)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRACRG
BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
       IF " " = OLDVALUE(PRACRG OF GPPRO_ACTIVITE)
       THEN LET FIELDTEXT = PRJCRG OF GPPROJETS
       ELSE LET FIELDTEXT = OLDVALUE(PRACRG OF GPPRO_ACTIVITE)
  END
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRACRG = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRACRG, T_PRACRG
    LET FIELDTEXT = TRUNCATE(T_PRACRG)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT PRARES
BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
       IF " " = OLDVALUE(PRARES OF GPPRO_ACTIVITE)
       THEN LET FIELDTEXT = PRJRES OF GPPROJETS
       ELSE LET FIELDTEXT = OLDVALUE(PRARES OF GPPRO_ACTIVITE)
  END

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRARES = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_PRARES, T_PRARES
    LET FIELDTEXT = TRUNCATE(T_PRARES)
  END
END




;-------------------------------------------------------------------------------
;
; Appel du popup pour les clients.
;
;
PROCEDURE INPUT CLICLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLICLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp103 PASSING T_CLICLE,     &
                             T_CLICIECLE,  &
                             T_CIECLEMNU MODE F
    LET FIELDTEXT = TRUNC(T_CLICLE)
  END
END

PROCEDURE PROC CLICLE
BEGIN
   IF CLICLE OF GPPRO_ACTIVITE <> " "
   THEN LET RADCLE OF GPPRO_ACTIVITE = RADCLE OF MCREF_ADRESSE
END


;--------------------------------------------
; Date de départ estimée
;
PROCEDURE EDIT PRADATDEPEST
BEGIN
   IF FIELDVALUE < PRADATPOSEST OF GPPRO_ACTIVITE AND &
               0<> PRADATPOSEST OF GPPRO_ACTIVITE
   THEN ERROR 838; La date de départ estimée doit être supérieure ou égale
                 ; à la date de pos. estimée.
END

;--------------------------------------------
; Date de départ réelle
;
PROCEDURE EDIT PRADATDEPREE
BEGIN
   IF FIELDVALUE < PRADATPOSREE OF GPPRO_ACTIVITE AND &
               0<> PRADATPOSREE OF GPPRO_ACTIVITE
   THEN ERROR 839; La date de départ réelle doit être supérieure ou égale
                 ; à la date de pos. réelle.
END

;--------------------------------------------
; Date de fin estimée
;
PROCEDURE EDIT PRADATFINEST
BEGIN
   IF FIELDVALUE < PRADATDEPEST OF GPPRO_ACTIVITE AND &
               0<> PRADATDEPEST OF GPPRO_ACTIVITE
   THEN ERROR 840; La date de fin estimée doit être supérieure ou égale
                 ; à la date de départ estimée.
END

;--------------------------------------------
; Date de fin réelle
;
PROCEDURE EDIT PRADATFINREE
BEGIN
   IF FIELDVALUE < PRADATDEPREE OF GPPRO_ACTIVITE AND &
               0<> PRADATDEPREE OF GPPRO_ACTIVITE
   THEN ERROR 841; La date de fin réelle doit être supérieure ou égale
                 ; à la date de départ réelle.
END


;--------------------------------------------
; Année du projet
;
PROCEDURE INPUT PRAANN
BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
       IF 0 = OLDVALUE(PRAANN OF GPPRO_ACTIVITE)
       THEN LET FIELDTEXT = ASC(PRJANN OF GPPROJETS)
       ELSE LET FIELDTEXT = ASC(OLDVALUE(PRAANN OF GPPRO_ACTIVITE))
  END
END

PROCEDURE EDIT PRAANN
BEGIN

   IF FIELDVALUE < NCON(ASC(SYSDATE)[1:4]) OR &
      FIELDVALUE > 2100
   THEN ERROR 849; Année invalide.
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les unité administrative (dépense)
;
;
PROCEDURE INPUT PRAUNADEP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRAUNADEP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp105 MODE F PASSING T_CIECLEMNU, T_PRAUNADEP
    LET FIELDTEXT = TRUNCATE(T_PRAUNADEP)
  END
END

PROCEDURE EDIT PRAUNADEP
BEGIN
  IF 0 <> SIZE( TRUNCATE ( FIELDTEXT ) )
  THEN BEGIN
    GET MCUNITE_ADM_DEP &
          VIA   CIECLE, &
                UNACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAUNADEP OF GPPRO_ACTIVITE  OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 860 ; Unité administrative inexistante.
  END
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour les unité administrative (dépense)
;
;
PROCEDURE INPUT PRAUNAPAI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRAUNAPAI = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp105 MODE F PASSING T_CIECLEMNU, T_PRAUNAPAI
    LET FIELDTEXT = TRUNCATE(T_PRAUNAPAI)
  END
END


PROCEDURE EDIT PRAUNAPAI
BEGIN
  IF 0 <> SIZE( TRUNCATE ( FIELDTEXT ) )
  THEN BEGIN
    GET MCUNITE_ADM_PAY &
          VIA   CIECLE, &
                UNACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAUNAPAI OF GPPRO_ACTIVITE  OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 860 ; Unité administrative inexistante.
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les unité administrative (revenu)
;
;
PROCEDURE INPUT PRAUNAREV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRAUNAREV = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp105 MODE F PASSING T_CIECLEMNU, T_PRAUNAREV
    LET FIELDTEXT = TRUNCATE(T_PRAUNAREV)
  END
END


PROCEDURE EDIT PRAUNAREV
BEGIN
  IF 0 <> SIZE( TRUNCATE ( FIELDTEXT ) )
  THEN BEGIN
    GET MCUNITE_ADM_REV &
          VIA   CIECLE, &
                UNACLE  &
          USING CIECLE    OF GPPRO_ACTIVITE, &
                PRAUNAREV OF GPPRO_ACTIVITE  OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 860 ; Unité administrative inexistante.
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les immobilisations
;
;
PROCEDURE INPUT PRAIMMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IMMCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp106 MODE F PASSING T_CIECLEMNU, T_IMMCLE
    LET FIELDTEXT = TRUNCATE(T_IMMCLE)
  END
END



;-------------------------------------------------------------------------------
;
; Appel du sous-écran des informations complémentaires.
;
PROCEDURE DESIGNER D001
BEGIN
  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  RUN SCREEN gp002a PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de client supplémentaire.
;
PROCEDURE DESIGNER D002
BEGIN
  IF CLICLE OF GPPRO_ACTIVITE = " "
  THEN ERROR 843; Création du client supplémentaire impossible; Il y a aucun
                ; client principal

  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  LET T_PRJCLE = PRJCLE OF GPPRO_ACTIVITE
  LET T_PRACLE = PRACLE OF GPPRO_ACTIVITE
  LET T_CLICLE2= CLICLE OF GPPRO_ACTIVITE
  LET T_RADCLE2= RADCLE OF GPPRO_ACTIVITE

  RUN SCREEN gp002b PASSING &
                        T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PRJCLE   , &
                        T_PRACLE   , &
                        T_CLICLE2  , &
                        T_RADCLE2    &
                MODE F

  LET CLICLE OF GPPRO_ACTIVITE = T_CLICLE2
  LET RADCLE OF GPPRO_ACTIVITE = T_RADCLE2

  IF CLICLE OF GPPRO_ACTIVITE = " "
  THEN DELETE GPPRA_CLIENT_2

  DISPLAY CLICLE
  DISPLAY CLINOM
  DISPLAY DEVCLE
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran du budget d'opération
;
PROCEDURE DESIGNER D003
BEGIN
  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  RUN SCREEN gp002c PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran du budget comptable.
;
PROCEDURE DESIGNER D004
BEGIN
  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  RUN SCREEN gp002d PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE &
                MODE F
END



;-------------------------------------------------------------------------------
;
; Appel du sous-écran d'information opération.
;
PROCEDURE DESIGNER D005
BEGIN
  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  RUN SCREEN gp002e PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran d'information comptable.
;
PROCEDURE DESIGNER D006
BEGIN
  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  RUN SCREEN gp002g PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de piste de vérification.
;
PROCEDURE DESIGNER D007
BEGIN
  IF NEWRECORD     OF GPPRO_ACTIVITE OR &
     ALTEREDRECORD OF GPPRO_ACTIVITE OR &
     DELETEDRECORD OF GPPRO_ACTIVITE
  THEN ERROR 2507

  RUN SCREEN gp002j PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE &
                MODE F
END




;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPPRO_ACTIVITE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPPRO_ACTIVITE &
     AND NOT NEWRECORD     OF GPPRO_ACTIVITE &
     AND NOT DELETEDRECORD OF GPPRO_ACTIVITE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Destruction du projet
  ;
  IF DELETEDRECORD OF GPPRO_ACTIVITE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le projet est
    ; référée dans la définition MCHIS_PROJET, MCHIS_UNITE,
    ; CPCAP_IMPUTATION, CRCAR_IMPUTATION, CPPAM_IMPUTATION et
    ; CRDPO_IMPUTATION.
    ;
    RUN SCREEN gp002x MODE SAME &
                      PASSING GPPRO_ACTIVITE, &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END

   IF PRADATDEPEST OF GPPRO_ACTIVITE < PRADATPOSEST OF GPPRO_ACTIVITE AND &
      0 <> PRADATPOSEST OF GPPRO_ACTIVITE
   THEN ERROR 838; La date de départ estimée doit être supérieure ou égale
                 ; à la date de pos. estimée.

   IF PRADATDEPREE OF GPPRO_ACTIVITE < PRADATPOSREE OF GPPRO_ACTIVITE AND &
      0 <> PRADATPOSREE OF GPPRO_ACTIVITE
   THEN ERROR 839; La date de départ réelle doit être supérieure ou égale
                 ; à la date de pos. réelle.

   IF PRADATFINEST OF GPPRO_ACTIVITE < PRADATDEPEST OF GPPRO_ACTIVITE AND &
      0 <> PRADATDEPEST OF GPPRO_ACTIVITE
   THEN ERROR 840; La date de fin estimée doit être supérieure ou égale
                 ; à la date de départ estimée.

   IF PRADATFINREE OF GPPRO_ACTIVITE < PRADATDEPREE OF GPPRO_ACTIVITE AND &
               0<> PRADATDEPREE OF GPPRO_ACTIVITE
   THEN ERROR 841; La date de fin réelle doit être supérieure ou égale
                 ; à la date de départ réelle.

  IF CLICLE OF GPPRO_ACTIVITE <> " " AND &
     NOT DELETEDRECORD OF GPPRO_ACTIVITE
  THEN BEGIN
       GET GPPRA_CLIENT VIA   CIECLE, &
                              PRJCLE, &
                              PRACLE, &
                              PCLCLE   &
                        USING CIECLE OF GPPRO_ACTIVITE, &
                              PRJCLE OF GPPRO_ACTIVITE, &
                              PRACLE OF GPPRO_ACTIVITE, &
                              1 OPT
       IF NOT ACCESSOK
       THEN BEGIN
            LET CIECLE OF GPPRA_CLIENT = CIECLE OF GPPRO_ACTIVITE
            LET PRJCLE OF GPPRA_CLIENT = PRJCLE OF GPPRO_ACTIVITE
            LET PRACLE OF GPPRA_CLIENT = PRACLE OF GPPRO_ACTIVITE
            LET PCLCLE OF GPPRA_CLIENT = 1
       END

       LET CLICIECLE OF GPPRA_CLIENT = CLICIECLE OF GPPRO_ACTIVITE
       LET CLICLE    OF GPPRA_CLIENT = CLICLE    OF GPPRO_ACTIVITE
       LET RADCLE    OF GPPRA_CLIENT = RADCLE    OF GPPRO_ACTIVITE
  END

  LET PRAPAUDATAPP OF GPPRO_ACTIVITE = PRJPBUDATAPP OF GPPROJETS
  LET PRAPACDATAPP OF GPPRO_ACTIVITE = PRJPBCDATAPP OF GPPROJETS
  LET PRAPAUUSRAPP OF GPPRO_ACTIVITE = PRJPBUUSRAPP OF GPPROJETS
  LET PRAPACUSRAPP OF GPPRO_ACTIVITE = PRJPBCUSRAPP OF GPPROJETS
END

PROCEDURE DELETE
BEGIN
  IF PRACLE OF GPPRO_ACTIVITE = "000"
  THEN ERROR 856; Impposible de supprimer le sous-projet "000".

  DELETE GPPRO_ACTIVITE
  DELETE GPPRA_CLIENT
  DELETE GPPRA_DESC
  DELETE GPPRA_CLIENT_2
  DELETE GPPRA_BDG_CTB
  DELETE GPPRA_BDG_UNT
END

BUILD LIST DETAIL
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Fiche du sous-projet *****************************
* Projet........: xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
* Sous-projet...: xxx                       Statut........: x xxxxxxxxxxxxxxx  *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
*                                                                              *
* Année sous-pro: xxxx         Type cargaison: xxxx xxxxxxxxxxxxxxxxxxxxxxxxx  *
* Client........: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx       *
* Responsable...: xxxx     xxxxxxxxxxxxxxxxxxxxxxxxx                           *
*                                                                              *
* Date / heure positionnement estimée, réelle.: xxxxxxxx xxxxx xxxxxxxx xxxxx  *
* Date / heure départ estimée, réelle.........: xxxxxxxx xxxxx xxxxxxxx xxxxx  *
* Date / heure fin estimée, réelle............: xxxxxxxx xxxxx xxxxxxxx xxxxx  *
* Date d'approbation du budget comptable......: xxxxxxxx xxxxxxxxxxxx          *
* Date d'approbation du budget d'opération....: xxxxxxxx xxxxxxxxxxxx          *
*                                                                              *
* Unité dépense.: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Unité revenu..: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Unité dépense.: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Immobilisation: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
@ENDIF

