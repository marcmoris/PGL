;/T/ Consultation du numéro de commande
;
;/P/ Programmeur..: Francois Richard
;    Date Creation: 11 octobre 1999
;
;    Description..: Permet de visionner et de choisir la derniere partie
;                   du numero de commande
;
;    Parametres...: T_CIECLE        = Compagnie
;                   T_PJTABR        = Abréviation projet
;                   T_PJTANN        = Année du projet
;                   T_COMNUMCOMINT  = Numéro de commande
;
;-------------------------------------------------------------------------------

SCREEN if110 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_PJTABR, &
                        T_PJTANN, &
                        T_COMNUMCOMINT

USE ustrasse NOLIST

USE ussectmp NOLIST
    "IF110"

;USE if110.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE       CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_PJTABR       CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_PJTANN       CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 3 RESET AT STARTUP
TEMPORARY T_COMSTU       CHARACTER SIZE 1 RESET AT STARTUP

;-------------------------------------------------------------------------------

FILE PDCOMMAN_INTERNE PRIMARY OCCURS 10

  ACCESS  VIA     CIECLE,        &
                  PJTABR,        &
                  PJTANN,        &
                  COMNUMCOMINT   &
          USING   T_CIECLE,      &
                  T_PJTABR,      &
                  T_PJTANN,      &
                  T_COMNUMCOMINT &
          ORDERBY PJTABR,        &
                  PJTANN,        &
                  COMNUMCOMINT

;-------------------------------------------------------------------------------

USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP 1

SKIP

TITLE "No comm" AT ,5
TITLE "Description" AT ,16

ALIGN (2,3,5)(,7,8)(,10,11)(,,16)

CLUSTER OCCURS WITH PDCOMMAN_INTERNE

FIELD PJTABR OF PDCOMMAN_INTERNE   &
      DISPLAY                      &
      HIDDEN                       &
      LABEL ">>"

FIELD PJTANN       OF PDCOMMAN_INTERNE  &
      LABEL "-"                         &
      DISPLAY

FIELD COMNUMCOMINT OF PDCOMMAN_INTERNE  &
      LABEL "-"                         &
      DISPLAY

FIELD COMNOM OF PDCOMMAN_INTERNE &
      DISPLAY                    &
      SIZE 22
 
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_COMNUMCOMINT, "@")
  THEN LET T_COMNUMCOMINT = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_COMNUMCOMINT = COMNUMCOMINT OF PDCOMMAN_INTERNE
  RETURN
END

BUILD

