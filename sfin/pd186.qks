;/T/ 2.4.4. Enregistrer le débitage
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-11-30
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le débitage des pièces de granit.
;
;    Note.........: La procédure APPEND est identique à la procédure DESIGNER 35
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;/M/ Programmeur..: Sébastien Chabot
;    Date Modif...: 18 Septembre 1996
;    Description..: Modification selon la demande du client (gra01-9311-305)
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd186  ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_DEBITAGE IN DICT


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD186"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd186.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Opérateurs                      " ACTION DESIGNER D001
  MENUITEM LABEL "Arrêt débitage                  " ACTION DESIGNER D002
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Opérateurs                   " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Arret débitage               " ACTION DESIGNER D002
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_PASSE    CHAR SIZE 1 RESET AT STARTUP

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04
DEFINE    D_DEBSTU    CHARACTER SIZE 16 = "DEBSTU"
TEMPORARY T_DEBSTU    CHARACTER SIZE 04

TEMPORARY T_DIAEPA       INTEGER   SIZE 4
TEMPORARY T_DIAEPAPLU    INTEGER   SIZE 4
TEMPORARY T_DIAEPAMOI    INTEGER   SIZE 4


;-------------------------------------------------------------------------------
;
; Variable pour l'appel des dépisteur (pop-up)
;

TEMPORARY T_MDECODMAC    CHARACTER        SIZE 04
TEMPORARY T_TRANUMTRA002 CHARACTER        SIZE 07
TEMPORARY T_DIANUM002    CHARACTER        SIZE 07
TEMPORARY T_COD          INTEGER UNSIGNED SIZE 02


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_SILENT CHARACTER SIZE 01
TEMPORARY T_NOLIG  INT       SIZE 04 RESET AT STARTUP ; Pour no ligne automatique.


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum des pièces de granit
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calculd'un intervalle de temps
;

USE usdiftim NOLIST


;-------------------------------------------------------------------------------
;
; Débitage
;

FILE PDDEBITAGE       PRIMARY &
                      NOITEM


  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP        &
         USING   T_CIECLEMNU,                         &
                 DEBNUMRAP        OF PDDEBITAGE       &
         REQUEST DEBNUMRAP        OF PDDEBITAGE       &
         ORDERBY CIECLE,          &
                 DEBNUMRAP


  ACCESS VIA     CIECLE,          &
                 DEBDATPRD,       &
                 MDECODMAC,       &
                 QRTCODQRT        &
         USING   T_CIECLEMNU,                         &
                 DEBDATPRD        OF PDDEBITAGE,      &
                 MDECODMAC        OF PDDEBITAGE,      &
                 QRTCODQRT        OF PDDEBITAGE       &
         REQUEST DEBDATPRD        OF PDDEBITAGE,      &
                 MDECODMAC        OF PDDEBITAGE,      &
                 QRTCODQRT        OF PDDEBITAGE       &
         ORDERBY CIECLE,          &
                 DEBNUMRAP



  ACCESS VIA     CIECLE,          &
                 DEBDATPRD,       &
                 MDECODMAC        &
         USING   T_CIECLEMNU,                         &
                 DEBDATPRD        OF PDDEBITAGE,      &
                 MDECODMAC        OF PDDEBITAGE       &
         REQUEST DEBDATPRD        OF PDDEBITAGE,      &
                 MDECODMAC        OF PDDEBITAGE       &
         ORDERBY CIECLE,          &
                 DEBNUMRAP


  ACCESS VIA     CIECLE,          &
                 DEBDATPRD        &
         USING   T_CIECLEMNU,                         &
                 DEBDATPRD        OF PDDEBITAGE       &
         REQUEST DEBDATPRD        OF PDDEBITAGE       &
         ORDERBY CIECLE,          &
                 DEBNUMRAP


  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 DEBNUMRAP

  ITEM CIECLE OF PDDEBITAGE INITIAL T_CIECLEMNU
  ITEM DEBSTU OF PDDEBITAGE INITIAL "E" ; En cours


;-------------------------------------------------------------------------------
;
; Détail débitage
;

FILE PDDET_DEBITAGE DETAIL OCCURS 9 TIMES &
                    NOITEM

  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP        &
         USING   T_CIECLEMNU,                         &
                 DEBNUMRAP        OF PDDEBITAGE       &
         ORDERBY CIECLE,          &
                 DEBNUMRAP,       &
                 DDENUMLIG

  SELECT IF DDESTU OF PDDET_DEBITAGE <> "M" &
            AND &
            DDESTU OF PDDET_DEBITAGE <> "D"

  ITEM CIECLE    OF PDDET_DEBITAGE INITIAL T_CIECLEMNU
  ITEM DEBNUMRAP OF PDDET_DEBITAGE INITIAL DEBNUMRAP OF PDDEBITAGE FIXED
  ITEM DDESTU    OF PDDET_DEBITAGE INITIAL "E"

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour le débitage
;

FILE PDSEQ_DEBITAGE  DESIGNER &
                     TRANSACTION NO_SEQ

;-------------------------------------------------------------------------------
;

FILE PDCOMMAN_INTERNE DESIGNER

;-------------------------------------------------------------------------------

;
; Détail débitage (ancien pour valider)
;

FILE PDDET_DEBITAGE DESIGNER ALIAS DDE_OLD


;-------------------------------------------------------------------------------
;
; Détail débitage (Nouveau)
;

FILE PDDET_DEBITAGE DESIGNER ALIAS DDE_MAJ

;-------------------------------------------------------------------------------

FILE PDDEBITAGE DESIGNER ALIAS PDDEBITAGE_VALIDE

  ACCESS VIA     CIECLE,          &
                 DEBDATPRD,       &
                 MDECODMAC,       &
                 QRTCODQRT        &
         USING   T_CIECLEMNU,                         &
                 DEBDATPRD        OF PDDEBITAGE,      &
                 MDECODMAC        OF PDDEBITAGE,      &
                 QRTCODQRT        OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Débitage validation des plages horraire pour le rapport
;

FILE PDDEBITAGE DESIGNER ALIAS DEB_DEBITAGE

  ACCESS VIA     CIECLE,          &
                 DEBDATPRD,       &
                 MDECODMAC        &
         USING   T_CIECLEMNU,                         &
                 DEBDATPRD        OF PDDEBITAGE,      &
                 MDECODMAC        OF PDDEBITAGE

  ;
  ; Sélectionne si une des deux bornes du nouvel enregistrement se retrouve
  ; parmis l'intervale déjà enregistré.
  ;

  SELECT IF ( (DEBDEP OF PDDEBITAGE >= DEBDEP OF DEB_DEBITAGE    &
               AND                                               &
               DEBDEP OF PDDEBITAGE <= DEBARR OF DEB_DEBITAGE )  &
             OR                                                  &
              (DEBARR OF PDDEBITAGE >= DEBDEP OF DEB_DEBITAGE    &
               AND                                               &
               DEBARR OF PDDEBITAGE <= DEBARR OF DEB_DEBITAGE )) &
            OR                                                   &
              (DEBDEP OF PDDEBITAGE <= DEBDEP OF DEB_DEBITAGE    &
               AND                                               &
               DEBARR OF PDDEBITAGE >= DEBARR OF DEB_DEBITAGE )


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE REFERENCE OCCURS WITH PDDET_DEBITAGE

  ACCESS VIA   CIECLE,           &
               TRANUMTRA002      &
         USING T_CIECLEMNU,      &
               TRANUMTRA002      OF PDDET_DEBITAGE


  ITEM TGRCODGRA OF PDDET_DEBITAGE INITIAL TGRCODGRA OF PDTRANCHE
  ITEM TYFCODFIN OF PDDET_DEBITAGE INITIAL TYFCODFIN OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Tranche (Validation)
;

FILE PDTRANCHE DESIGNER ALIAS TRA_VALID


;-------------------------------------------------------------------------------
;
; Machine opération de surface
;

FILE PDMACH_DEBITAGE REFERENCE

  ACCESS VIA     CIECLE,          &
                 MDECODMAC        &
         USING   T_CIECLEMNU,     &
                 MDECODMAC        OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Machine opération de surface (Validation)
;

FILE PDMACH_DEBITAGE DESIGNER ALIAS MDE_VALID


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE OCCURS WITH PDDET_DEBITAGE

  ACCESS VIA     CIECLE,   &
                 TGRCODGRA &
         USING   T_CIECLEMNU, &
                 TGRCODGRA OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE OCCURS WITH PDDET_DEBITAGE

  ACCESS VIA     CIECLE,       &
                 TYFCODFIN     &
         USING   T_CIECLEMNU,  &
                 TYFCODFIN OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT DESIGNER ALIAS TGR_FIC


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI DESIGNER ALIAS TYF_FIC


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME DESIGNER


;-------------------------------------------------------------------------------
;
; Produit dimension
;

FILE PDPROD_DIMENSION DESIGNER


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_DEBSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_DEBSTU,                             &
               DEBSTU           OF PDDEBITAGE


FILE PDOPER_DEBITAGE DESIGNER ALIAS OPD_VALIDE

  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP,       &
                 DEBDATPRD        &
         USING   T_CIECLEMNU,                    &
                 DEBNUMRAP        OF PDDEBITAGE, &
                 DEBDATPRD        OF PDDEBITAGE

;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_DECOUP CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Débitage " &
@ELSE
      " %%%Débitage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19)


FIELD DEBNUMRAP        OF PDDEBITAGE       &
label "Numéro rapport:"&
        HIDDEN ID 05                       &
        DISPLAY


FIELD DEBDATPRD        OF PDDEBITAGE        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        HIDDEN ID 10                       &
        DEFAULT SYSDATE


ALIGN (2,3,19) (,,24)


FIELD MDECODMAC        OF PDDEBITAGE       &
label "Code machine..:"&
        HIDDEN ID 15                       &
        REQUIRED                           &
        LOOKUP ON PDMACH_DEBITAGE          &
          MESSAGE 2915 ; Ce code de machine n'existe pas


FIELD MDEDSC001        OF PDMACH_DEBITAGE  &
        DISPLAY


SKIP TO LINE 5


ALIGN (41,42,58)


FIELD DEBDEP           OF PDDEBITAGE       &
label "Hre début.....:"&
        HIDDEN ID 20                       &
        REQUIRED


FIELD DEBARR           OF PDDEBITAGE       &
label "Hre fin.......:"&
        HIDDEN ID 25                       &
        REQUIRED


SKIP TO LINE 8


ALIGN (2,3,19) (,,24)


FIELD QRTCODQRT        OF PDDEBITAGE       &
label "Quart travail.:"&
        HIDDEN ID 30                       &
        REQUIRED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY


ALIGN (,3,19) (,,24)


FIELD DEBSTU           OF PDDEBITAGE       &
label "Statut........:"&
        DISPLAY


FIELD CBIDSC           OF A_CBI_DEBSTU     &
        DISPLAY                            &
        SIZE 18






DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Détail débitage " &
@ELSE
      " %%%Détail débitage " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
    " S Lig Tranche Diagr.  Pr Gra Fi Long Haut Épai   Qte Ok Commentaire " & ; <-- SEBCHA960918
@ELSE
    "%%%Lig Tranche Diagr.  Pr Gra Fi Long Haut Épai   Qte Ok Commentaire " & ; <-- SEBCHA960918
@ENDIF
      AT ,2

SKIP TO LINE 14


ALIGN (02,02,03) (,,5) (,,9) (,,17) (,,25) (,,28) (,,32) (,,35) (,,40) (,,45) (,,50) (,,56) (,,59) ; <-- SEBCHA960918


CLUSTER OCCURS WITH PDDET_DEBITAGE ID BASE 35


FIELD DDESTU           OF PDDET_DEBITAGE   &
        HIDDEN                             &
        LABEL " "                          &
        DISPLAY


FIELD DDENUMLIG        OF PDDET_DEBITAGE   &
        NOCHANGE              size 3             &
        LOOKUP NOTON PDDET_DEBITAGE        &
          VIA    CIECLE,                              &
                 DEBNUMRAP,                           &
                 DDENUMLIG                            &
          USING  T_CIECLEMNU,                         &
                 DEBNUMRAP        OF PDDEBITAGE,      &
                 DDENUMLIG        OF PDDET_DEBITAGE   &
          MESSAGE 3055 ; Ce numéro de ligne existe déjà


FIELD TRANUMTRA002     OF PDDET_DEBITAGE   &
        HIDDEN                             &
        NUMERIC      BWZ


FIELD DIANUM002        OF PDDET_DEBITAGE  &
        HIDDEN


FIELD DDECODPRI        OF PDDET_DEBITAGE   &               ; <-- SEBCHA960918
        DEFAULT "01" &
        HIDDEN                                             ; <-- SEBCHA960918


FIELD TGRCODGRA        OF PDDET_DEBITAGE   &
        HIDDEN                             &
        NUMERIC                            &
        BWZ                                &
        IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) AND &
           0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))


FIELD TYFCODFIN        OF PDDET_DEBITAGE   &
        HIDDEN                             &
        NUMERIC                            &
        BWZ                                &
        IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) AND &
           0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))


FIELD DDELON           OF PDDET_DEBITAGE   &
        HIDDEN           size 4                  &
        IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) AND &
           0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))


FIELD DDEHAU           OF PDDET_DEBITAGE   &
        HIDDEN             size 4                &
        IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) AND &
           0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))


FIELD DDEEPA           OF PDDET_DEBITAGE   &
        HIDDEN          size 4                   &
        IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) AND &
           0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))


FIELD DDEQTEFAB        OF PDDET_DEBITAGE   &
        HIDDEN                             size 5


FIELD DDEACCERR        OF PDDET_DEBITAGE   &
        HIDDEN                             size 1


FIELD DDECOM           OF PDDET_DEBITAGE   &
        HIDDEN                             &
        FOR ,20


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
PROCEDURE INPUT DDECODPRI
BEGIN
IF 1=SIZE(TRUNC(FIELDTEXT))
 THEN LET FIELDTEXT = "0" + FIELDTEXT
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT MDECODMAC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MDECODMAC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd109 MODE F &
                     PASSING T_MDECODMAC, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_MDECODMAC )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le code de machine existe
;

PROCEDURE EDIT MDECODMAC
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    GET MDE_VALID OPTIONAL     &
      VIA     CIECLE,          &
              MDECODMAC        &
      USING   T_CIECLEMNU,     &
              MDECODMAC        OF PDDEBITAGE
    IF NOT ACCESSOK
    THEN BEGIN
      WARNING 2915 ; Ce code de machine n'existe pas
    END
  END
  ELSE BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs (codes bilingues)
;

PROCEDURE INPUT QRTCODQRT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    ;
    ; Appel du dépisteur pour le champs (codes bilingues)
    ;
    LET T_QRTCODQRT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd119 MODE F &
                     PASSING D_QRTCODQRT , &
                             T_QRTCODQRT, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_QRTCODQRT )
  END
  ELSE BEGIN
    ;
    ; Assigne une valeur par défaut pour le quart de travail
    ;
    IF 0 = SIZE(FIELDTEXT)
    THEN BEGIN
      LET FIELDTEXT = "J"
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'heure de début est plus petite que l'heure de fin
;

PROCEDURE EDIT DEBARR
BEGIN
  IF FIELDVALUE < DEBDEP OF PDDEBITAGE
  THEN BEGIN
    ERROR 3007 ; L'heure de début doit être plus petite que l'heure de fin
  END
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT DDENUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT) &
     AND &
     DDENUMLIG OF PDDET_DEBITAGE = 0
  THEN BEGIN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE PROCESS DDENUMLIG
BEGIN
  LET T_NOLIG = DDENUMLIG OF PDDET_DEBITAGE
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;

PROCEDURE INPUT TRANUMTRA002
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_COD          = 0 ; Toutes les tranches
    LET T_TRANUMTRA002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd122 MODE F &
                     PASSING T_TRANUMTRA002, &
                             T_COD,&
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TRANUMTRA002 )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que la tranche existe
;

PROCEDURE EDIT TRANUMTRA002
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    GET TRA_VALID OPTIONAL     &
      VIA     CIECLE,          &
              TRANUMTRA002     &
      USING   T_CIECLEMNU,     &
              TRANUMTRA002     OF PDDET_DEBITAGE
    IF NOT ACCESSOK
    THEN BEGIN
      ERROR = "Ce numéro de tranche n'existe pas  "
    END
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;

PROCEDURE INPUT DIANUM002
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DIANUM002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd126 MODE F &
                     PASSING T_DIANUM002, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DIANUM002 )
  END
END


;-------------------------------------------------------------------------------
;
; Validation du numéro de diagramme
;

PROCEDURE EDIT DIANUM002
BEGIN
  ;
  ; Validation du numéro de diagramme
  ;
  GET PDDIAGRAMME OPTIONAL &
    VIA   CIECLE,          &
          DIANUM002        &
    USING T_CIECLEMNU,     &
          DIANUM002        OF PDDET_DEBITAGE
  IF NOT ACCESSOK
  THEN BEGIN
    WARNING 2998 ; Ce numéro de diagramme n'existe pas
  END
  ELSE BEGIN
    ; Erreur dans le cas ou l'épaisseur de la tranche est différent de celle
    ; du diagramme ...
    IF 0 <> SIZE(TRUNC(TRANUMTRA002 OF PDDET_DEBITAGE))
    THEN BEGIN
  ;    IF DIAEPA OF PDDIAGRAMME <> TRAEPA OF PDTRANCHE
;----
        LET T_DIAEPA = DIAEPA OF PDDIAGRAMME
        IF T_DIAEPA <> TRAEPA OF PDTRANCHE
        THEN BEGIN

          GET PDCOMMAN_INTERNE &
              VIA     CIECLE,          &
                      PJTABR,&
                      PJTANN,&
                      COMNUMCOMINT &
              USING   T_CIECLEMNU,     &
                      PJTABR OF PDDIAGRAMME,&
                      PJTANN OF PDDIAGRAMME,&
                      COMNUMCOMINT  OF PDDIAGRAMME
           LET T_DIAEPAPLU = T_DIAEPA + COMEPAPLU OF PDCOMMAN_INTERNE
           LET T_DIAEPAMOI = T_DIAEPA - COMEPAMOI OF PDCOMMAN_INTERNE

          IF TRAEPA OF PDTRANCHE > T_DIAEPAPLU OR TRAEPA OF PDTRANCHE < T_DIAEPAMOI

            THEN WARNING "L'épaisseur de la tranche est différente de celle du diagramme !"
       END

    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ a été saisit
;

PROCEDURE INPUT TGRCODGRA
BEGIN
  IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) &
     AND &
     0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))
  THEN BEGIN
    IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
       AND &
       0 = SIZE(TRUNCATE(TGRCODGRA OF PDDET_DEBITAGE))
    THEN BEGIN
      ERROR 3044 ; Ce champ est requis
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le code de granit existe
;

PROCEDURE EDIT TGRCODGRA
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    GET TGR_FIC OPTIONAL     &
      VIA     CIECLE,        &
              TGRCODGRA      &
      USING   T_CIECLEMNU,   &
              TGRCODGRA      OF PDDET_DEBITAGE
    IF NOT ACCESSOK
    THEN BEGIN
      WARNING 2910 ; Ce type de granit n'existe pas
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ a été saisit
;

PROCEDURE INPUT TYFCODFIN
BEGIN
  IF 0 = SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE)) &
     AND &
     0 = SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))
  THEN BEGIN
    IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
       AND &
       0 = SIZE(TRUNCATE(TYFCODFIN OF PDDET_DEBITAGE))
    THEN BEGIN
      ERROR 3044 ; Ce champ est requis
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le type de fini existe
;

PROCEDURE EDIT TYFCODFIN
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    GET TYF_FIC OPTIONAL     &
      VIA     CIECLE,        &
              TYFCODFIN      &
      USING   T_CIECLEMNU,   &
              TYFCODFIN      OF PDDET_DEBITAGE
    IF NOT ACCESSOK
    THEN BEGIN
      WARNING 2911 ; Ce type de fini n'existe pas
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champs à bien été saisie pour un produit de dimension
;

PROCEDURE INPUT DDELON
BEGIN
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD" &
     AND &
     0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     ENTRYMODE
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions
;

PROCEDURE EDIT DDELON
BEGIN
  ;
  ; Valide que le champ a été saisi lors de correction
  ;
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD" &
     AND &
     0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide les dimensions de produit de dimension
  ;
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD"
  THEN BEGIN
    IF DDELON       OF PDDET_DEBITAGE > T_DIM_MAX_LAR_PD &
       OR &
       DDELON       OF PDDET_DEBITAGE < T_DIM_MIN_LAR_PD
    THEN BEGIN
      ERROR 2962 ; Cette dimension est hors des limites
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champs à bien été saisie pour un produit de dimension
;

PROCEDURE INPUT DDEHAU
BEGIN
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD" &
     AND &
     0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     ENTRYMODE
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions
;

PROCEDURE EDIT DDEHAU
BEGIN
  ;
  ; Valide que le champ a été saisi lors de correction
  ;
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD" &
     AND &
     0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide les dimensions de produit de dimension
  ;
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD"
  THEN BEGIN
    IF DDEHAU       OF PDDET_DEBITAGE > T_DIM_MAX_HAU_PD &
       OR &
       DDEHAU       OF PDDET_DEBITAGE < T_DIM_MIN_HAU_PD
    THEN BEGIN
      ERROR 2962 ; Cette dimension est hors des limites
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champs à bien été saisie pour un produit de dimension
;

PROCEDURE INPUT DDEEPA
BEGIN
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD" &
     AND &
     0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     ENTRYMODE
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions
;

PROCEDURE EDIT DDEEPA
BEGIN
  ;
  ; Valide que le champ a été saisi lors de correction
  ;
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD" &
     AND &
     0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide les dimensions de produit de dimension
  ;
  IF TPDTYPPRD OF PDDET_DEBITAGE = "PD"
  THEN BEGIN
    IF DDEEPA       OF PDDET_DEBITAGE > T_DIM_MAX_EPA_PD &
       OR &
       DDEEPA       OF PDDET_DEBITAGE < T_DIM_MIN_EPA_PD
    THEN BEGIN
      ERROR 2962 ; Cette dimension est hors des limites
    END
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;

PROCEDURE INPUT DDEACCERR
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT DDEACCERR
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Saisie d'une ligne de détail
;

PROCEDURE INTERNAL SAISIE_LIGNE_DETAIL
BEGIN
  IF DDESTU OF PDDET_DEBITAGE = "M"
  THEN BEGIN
    NULL
  END
  ELSE BEGIN
    ACCEPT DDENUMLIG        OF PDDET_DEBITAGE
  END
  ACCEPT TRANUMTRA002     OF PDDET_DEBITAGE
  ACCEPT DIANUM002        OF PDDET_DEBITAGE
  IF 0 <> SIZE(TRUNCATE(DIANUM002 OF PDDET_DEBITAGE))
  THEN BEGIN
    ACCEPT DDECODPRI      OF PDDET_DEBITAGE                ; <-- SEBCHA960918
    LET TPDTYPPRD    OF PDDET_DEBITAGE = "DI"
    GET PDDIAGRAMME OPTIONAL &
      VIA   CIECLE,          &
            DIANUM002     &
      USING T_CIECLEMNU,  &
            DIANUM002     OF PDDET_DEBITAGE
    IF ACCESSOK
    THEN BEGIN
      LET     DDELON       OF PDDET_DEBITAGE = DIALON       OF PDDIAGRAMME
      LET     DDEHAU       OF PDDET_DEBITAGE = DIAHAU       OF PDDIAGRAMME
      LET     DDEEPA       OF PDDET_DEBITAGE = DIAEPA       OF PDDIAGRAMME
      LET     TGRCODGRA    OF PDDET_DEBITAGE = TGRCODGRA    OF PDDIAGRAMME
      LET     TYFCODFIN    OF PDDET_DEBITAGE = TYFCODFIN    OF PDDIAGRAMME
      DISPLAY DDELON       OF PDDET_DEBITAGE
      DISPLAY DDEHAU       OF PDDET_DEBITAGE
      DISPLAY DDEEPA       OF PDDET_DEBITAGE
      DISPLAY TGRCODGRA    OF PDDET_DEBITAGE
      DISPLAY TYFCODFIN    OF PDDET_DEBITAGE
      LET     PJTANN       OF PDDET_DEBITAGE = PJTANN       OF PDDIAGRAMME
      LET     PJTNUMSEQ    OF PDDET_DEBITAGE = PJTNUMSEQ    OF PDDIAGRAMME
      LET     PJTNUMPRO    OF PDDET_DEBITAGE = PJTNUMPRO    OF PDDIAGRAMME
      LET     COMNUMCOMINT OF PDDET_DEBITAGE = COMNUMCOMINT OF PDDIAGRAMME
      LET     COMNUMCOM    OF PDDET_DEBITAGE = COMNUMCOM    OF PDDIAGRAMME
      IF 0 <> SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE))
      THEN BEGIN
        IF TGRCODGRA    OF PDDIAGRAMME <> TGRCODGRA    OF PDTRANCHE
        THEN BEGIN
          WARNING 3121 ; Le type de granit ne correspond pas à celui commandé
        END
        IF TYFCODFIN    OF PDDIAGRAMME <> TYFCODFIN    OF PDTRANCHE
        THEN BEGIN
          WARNING 3122 ; Le type de fini ne correspond pas à celui commandé
        END
      END
    END
    ELSE BEGIN
      WARNING 2998 ; Ce numéro de diagramme n'existe pas
    END
    LET PJTABR       OF PDDET_DEBITAGE = DIANUM002 OF PDDET_DEBITAGE[1:2]
    LET DIANUMDIA002 OF PDDET_DEBITAGE = DIANUM002 OF PDDET_DEBITAGE[3:5]
  END
  ELSE BEGIN
    LET TPDTYPPRD            OF PDDET_DEBITAGE = "PD"
    ;
    ; Initialise les champs que l'on peut déduire selon la tranche
    ;
    IF 0 <> SIZE(TRUNCATE(TRANUMTRA002 OF PDDET_DEBITAGE))
    THEN BEGIN
      LET     TGRCODGRA        OF PDDET_DEBITAGE = TGRCODGRA    OF PDTRANCHE
      LET     TYFCODFIN        OF PDDET_DEBITAGE = TYFCODFIN    OF PDTRANCHE
      DISPLAY TGRCODGRA        OF PDDET_DEBITAGE
      DISPLAY TYFCODFIN        OF PDDET_DEBITAGE
    END
    ELSE BEGIN
      ACCEPT  TGRCODGRA        OF PDDET_DEBITAGE
      ACCEPT  TYFCODFIN        OF PDDET_DEBITAGE
    END
    ACCEPT  DDELON           OF PDDET_DEBITAGE
    ACCEPT  DDEHAU           OF PDDET_DEBITAGE
    ACCEPT  DDEEPA           OF PDDET_DEBITAGE
  END
  ACCEPT  DDEQTEFAB        OF PDDET_DEBITAGE
  ACCEPT  DDEACCERR        OF PDDET_DEBITAGE
  ACCEPT  DDECOM           OF PDDET_DEBITAGE
END


;-------------------------------------------------------------------------------
;
; Saisie du cluster identique à la procedure DESIGNER 35
;

PROCEDURE APPEND
BEGIN
  DO INTERNAL SAISIE_LIGNE_DETAIL
END


;-------------------------------------------------------------------------------
;
; Recherche le dernier numéro de ligne utilisé
;

PROCEDURE POSTFIND
BEGIN
  GET DDE_OLD   OPTIONAL     &
    VIA     CIECLE,          &
            DEBNUMRAP        &
    USING   T_CIECLEMNU,                   &
            DEBNUMRAP        OF PDDEBITAGE &
    ORDERBY CIECLE,          &
            DEBNUMRAP,       &
            DDENUMLIG        DESCENDING
  IF ACCESSOK
  THEN BEGIN
    LET T_NOLIG = DDENUMLIG OF DDE_OLD
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
  LET T_NOLIG = 0
END


;-------------------------------------------------------------------------------
;
; Procédure ENTRY
;

PROCEDURE ENTRY
BEGIN

  DISPLAY DEBNUMRAP        OF PDDEBITAGE
  ACCEPT  DEBDATPRD        OF PDDEBITAGE
  ACCEPT  MDECODMAC        OF PDDEBITAGE
  DISPLAY MDEDSC001        OF PDMACH_DEBITAGE
  ACCEPT  DEBDEP           OF PDDEBITAGE
  ACCEPT  DEBARR           OF PDDEBITAGE
  ACCEPT  QRTCODQRT        OF PDDEBITAGE
  DISPLAY CBIDSC           OF A_CBI_QRTCODQRT
  DISPLAY DEBSTU           OF PDDEBITAGE
  DISPLAY CBIDSC           OF A_CBI_DEBSTU
  FOR PDDET_DEBITAGE
  BEGIN
    PERFORM APPEND
  END
END


;-------------------------------------------------------------------------------
;
; Saisie du cluster identique à la procedure APPEND
;

PROCEDURE DESIGNER 35
BEGIN
  DO INTERNAL SAISIE_LIGNE_DETAIL
END


;-------------------------------------------------------------------------------
;
; Opérateurs
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd186a &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDDEBITAGE          &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Arret opération surface
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd186b &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDDEBITAGE          &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Détail débitage diagramme
;

PROCEDURE DESIGNER D003
BEGIN
  RUN SCREEN  pd186c &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDDEBITAGE,         &
                      PDDET_DEBITAGE      &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Détail débitage prod dimension
;

PROCEDURE DESIGNER D004
BEGIN
  RUN SCREEN  pd186d &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDDEBITAGE,         &
                      PDDET_DEBITAGE      &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Initialise le numéro de ligne au dernier numéro de ligne trouvé
;

PROCEDURE DETAIL POSTFIND
BEGIN
  LET T_NOLIG = DDENUMLIG OF PDDET_DEBITAGE
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE INTERNAL MAJ_LIGNE_TRANS
BEGIN
  LET DDESTU OF PDDET_DEBITAGE = "E"
  GET DDE_OLD   OPTIONAL     &
    VIA     CIECLE,          &
            DEBNUMRAP,       &
            DDENUMLIG,       &
            DDESTU           &
    USING   T_CIECLEMNU,                          &
            DEBNUMRAP        OF PDDEBITAGE,       &
            DDENUMLIG        OF PDDET_DEBITAGE,   &
            "T"
  IF ACCESSOK
  THEN BEGIN
    ;
    ; Lecture pour vérifier qu'il n'existe pas d'enregistrement de
    ; modification avant de créé un nouvel enregistrement.
    ;
    GET DDE_MAJ OPTIONAL       &
      VIA     CIECLE,          &
              DEBNUMRAP,       &
              DDENUMLIG,       &
              DDESTU           &
      USING   T_CIECLEMNU,                          &
              DEBNUMRAP        OF PDDEBITAGE,       &
              DDENUMLIG        OF PDDET_DEBITAGE,   &
              "M"
    IF NOT ACCESSOK
    THEN BEGIN
        LET CIECLE           OF DDE_MAJ = T_CIECLEMNU
        LET DEBNUMRAP        OF DDE_MAJ = DEBNUMRAP        OF DDE_OLD
        LET DDENUMLIG        OF DDE_MAJ = DDENUMLIG        OF DDE_OLD
        LET DDESTU           OF DDE_MAJ = "M"
        LET TRANUMTRA002     OF DDE_MAJ = TRANUMTRA002     OF DDE_OLD
        LET TPDTYPPRD        OF DDE_MAJ = TPDTYPPRD        OF DDE_OLD
        LET PJTABR           OF DDE_MAJ = PJTABR           OF DDE_OLD
        LET DIANUMDIA002     OF DDE_MAJ = DIANUMDIA002     OF DDE_OLD
        LET DDELON           OF DDE_MAJ = DDELON           OF DDE_OLD
        LET DDEHAU           OF DDE_MAJ = DDEHAU           OF DDE_OLD
        LET DDEEPA           OF DDE_MAJ = DDEEPA           OF DDE_OLD
        LET TGRCODGRA        OF DDE_MAJ = TGRCODGRA        OF DDE_OLD
        LET TYFCODFIN        OF DDE_MAJ = TYFCODFIN        OF DDE_OLD
        LET DIANUM002        OF DDE_MAJ = DIANUM002        OF DDE_OLD
        LET PJTANN           OF DDE_MAJ = PJTANN           OF DDE_OLD
        LET PJTNUMSEQ        OF DDE_MAJ = PJTNUMSEQ        OF DDE_OLD
        LET PJTNUMPRO        OF DDE_MAJ = PJTNUMPRO        OF DDE_OLD
        LET COMNUMCOMINT     OF DDE_MAJ = COMNUMCOMINT     OF DDE_OLD
        LET COMNUMCOM        OF DDE_MAJ = COMNUMCOM        OF DDE_OLD
        LET DDEQTEFAB        OF DDE_MAJ = DDEQTEFAB        OF DDE_OLD
        LET DDEEPARES        OF DDE_MAJ = DDEEPARES        OF DDE_OLD
        LET DDEACCERR        OF DDE_MAJ = DDEACCERR        OF DDE_OLD
        LET DDECOM           OF DDE_MAJ = DDECOM           OF DDE_OLD
        PUT DDE_MAJ
    END
    ELSE BEGIN

    ERROR "PROBLÈME TECHNIQUE DDE_MAJ"
    END
  END
  ELSE BEGIN

    ERROR "PROBLÈME TECHNIQUE DDE_OLD"
  END
END


PROCEDURE INTERNAL VALIDE
BEGIN
  WHILE RETRIEVING OPD_VALIDE
  BEGIN
    IF (DEBDEP OF PDDEBITAGE <> OPDHREDEB OF OPD_VALIDE   &
        AND                                                       &
        DEBARR OF PDDEBITAGE <> OPDHREFIN OF OPD_VALIDE )
    THEN BEGIN
      IF ( (DEBDEP OF PDDEBITAGE >= OPDHREDEB OF OPD_VALIDE     &
            AND                                                         &
            DEBDEP OF PDDEBITAGE <= OPDHREFIN OF OPD_VALIDE )   &
          OR                                                            &
           (DEBARR OF PDDEBITAGE >= OPDHREDEB OF OPD_VALIDE     &
            AND                                                         &
            DEBARR OF PDDEBITAGE <= OPDHREFIN OF OPD_VALIDE ))  &
         OR                                                             &
           (DEBDEP OF PDDEBITAGE <= OPDHREDEB OF OPD_VALIDE     &
            AND                                                         &
            DEBARR OF PDDEBITAGE >= OPDHREFIN OF OPD_VALIDE )
      THEN BEGIN
        ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
       END
    END
  END
END

PROCEDURE INTERNAL BOUCLE
BEGIN
  WHILE RETRIEVING DDE_OLD VIA CIECLE, DEBNUMRAP &
               USING T_CIECLEMNU, &
                     DEBNUMRAP OF PDDET_DEBITAGE
  BEGIN
    IF TRANUMTRA002 OF DDE_OLD = TRANUMTRA002 OF TRA_VALID &
       AND DDENUMLIG OF DDE_OLD <> DDENUMLIG OF PDDET_DEBITAGE
    THEN LET T_PASSE = "N"
  END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDEBITAGE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDEBITAGE &
     AND NOT NEWRECORD     OF PDDEBITAGE &
     AND NOT DELETEDRECORD OF PDDEBITAGE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Calcul de la durée du traitement pour l'opération de surface
  ;
  LET TZ_DATDEB   = DEBDATPRD        OF PDDEBITAGE
  LET TZ_TMPDEB   = ASCII(DEBDEP     OF PDDEBITAGE,4) + "00"
  LET TZ_DATFIN   = DEBDATPRD        OF PDDEBITAGE
  LET TZ_TMPFIN   = ASCII(DEBARR     OF PDDEBITAGE,4) + "00"

  LET DEBDURSEC OF PDDEBITAGE = DZ_DIFSECTOT

;  DO INTERNAL VALIDE

  ;
  ; Valide les lignes de détail du rapport
  ;
  FOR PDDET_DEBITAGE
  BEGIN
    ;
    ; Si la ligne à déjà été valider il faut valider les modification avant
    ; d'effectuer la mise à jour de la pièce
    ;
    IF DDESTU OF PDDET_DEBITAGE = "V"
    THEN BEGIN
      LET DDESTU OF PDDET_DEBITAGE = "E"
      IF DEBSTU OF PDDEBITAGE = "V"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "E"
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END
    ;
    ; Si la ligne à déjà été transferrer il faut replacer la pièce dans
    ; l'état initial avant de faire la nouvelle mise à jour
    ;
    IF DDESTU OF PDDET_DEBITAGE = "T" &
       AND &
       (ALTEREDRECORD OF PDDET_DEBITAGE OR DELETEDRECORD OF PDDET_DEBITAGE)
    THEN BEGIN
      DO INTERNAL MAJ_LIGNE_TRANS
      GET TRA_VALID OPTIONAL     &
        VIA     CIECLE,          &
                TRANUMTRA002     &
        USING   T_CIECLEMNU,     &
                TRANUMTRA002     OF PDDET_DEBITAGE
      IF ACCESSOK
      THEN BEGIN
          LET T_PASSE = "O"
          DO INTERNAL BOUCLE
        IF T_PASSE = "O"
        THEN BEGIN
        LET TRASTU OF TRA_VALID = "I"
        PUT TRA_VALID RESET
       END
      END
    END
    ;
    ;
    ;
    IF ALTEREDRECORD OF PDDET_DEBITAGE
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF DEBSTU OF PDDEBITAGE = "V"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "E" ; En cours , il faut reprendre la validation
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF DEBSTU OF PDDEBITAGE = "T"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "C"  ; Correction après transfert
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END


    IF NEWRECORD OF PDDET_DEBITAGE
    THEN BEGIN
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF DEBSTU OF PDDEBITAGE = "T"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "C"  ; Correction après transfert
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END
  END

  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = DEBNUMRAP OF PDDEBITAGE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_DEBITAGE OPTIONAL         &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_DEBITAGE = T_CIECLEMNU
    END
    LET SEQNUMSEQ OF PDSEQ_DEBITAGE = SEQNUMSEQ OF PDSEQ_DEBITAGE + 1
    LET DEBNUMRAP OF PDDEBITAGE     = SEQNUMSEQ OF PDSEQ_DEBITAGE
    PUT PDSEQ_DEBITAGE
    DISPLAY DEBNUMRAP OF PDDEBITAGE
  END
END


;-------------------------------------------------------------------------------
PROCEDURE EDIT QRTCODQRT
BEGIN
GET PDDEBITAGE_VALIDE OPT
 IF ACCESSOK
 THEN WARNING ="ATTENTION:Machine,Quart,Date existant"
END
;-------------------------------------------------------------------------------
;
; Permet de faire afficher le numéro de séquence
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY DEBNUMRAP OF PDDEBITAGE
    WARNING = " "
  END
;  RUN SCREEN  pd186a &
;              PASSING T_CIECLEMNU,        &
;                      T_CIENOM,           &
;                      PDDEBITAGE          &
;              MODE SAME
END


;------------------------------------
PROCEDURE DETAIL DELETE
BEGIN
   get pdtranche opt via    ciecle,tranumtra002 &
                     using  t_cieclemnu,tranumtra002 of pddet_debitage
   if accessok
   then let trastu of pdtranche = "I"
    DELETE PDDET_DEBITAGE
END



BUILD detail list

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Débitage ***********************************
*                                                                              *
* Numéro rapport: xxxxxxxxxxx            Hre début.....: xxxxx                 *
* Date product..: xxxxxxxx               Hre fin.......: xxxxx                 *
* Code machine..: xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Quart travail.: x    xxxxxxxxxxxxxxxxxx                                      *
* Statut........: x    xxxxxxxxxxxxxxxxxx                                      *
*                                                                              *
******************************* Détail débitage ********************************
*                                                                              *
* S Lig Tranche Diagr.  Pr Gra Fi Long Haut Épai   Qte Ok Commentaire          *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
* x xxx xxxxxxx xxxxxxx xx xxx xx xxxx xxxx xxxx xxxxx x  xxxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF
