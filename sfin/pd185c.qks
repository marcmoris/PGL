;/T/ 2.4.3 Enregistrer les opérations de surface (détail opération de surface tranche)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-28
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les opérations de surface pour les tranches.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd185c ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD185C"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd185c.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04
DEFINE    D_OSUSTU    CHARACTER SIZE 16 = "OSUSTU"
TEMPORARY T_OSUSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Clés de recherche pour les popup de consultations et les sous écrans
;

TEMPORARY T_TRANUMTRA002     CHARACTER SIZE 7
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 02


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_SILENT           CHARACTER SIZE 1


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum des pièces de granit
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Opération de surface
;

FILE PDOPERATION_SURF  MASTER


;-------------------------------------------------------------------------------
;
; Détail opération surface
;

FILE PDDET_OPER_SURF PRIMARY &
                     NOITEM  &
                     OCCURS 7

  ACCESS VIA     CIECLE,          &
                 OSUNUMRAP,       &
                 TPDTYPPRD        &
         USING   T_CIECLEMNU,                          &
                 OSUNUMRAP        OF PDOPERATION_SURF, &
                 "TR"                                  &
         ORDERBY CIECLE,          &
                 OSUNUMRAP,       &
                 TRANUMTRA002



  ITEM CIECLE           OF PDDET_OPER_SURF INITIAL T_CIECLEMNU
  ITEM OSUNUMRAP        OF PDDET_OPER_SURF INITIAL OSUNUMRAP        OF PDOPERATION_SURF  FIXED
  ITEM TPDTYPPRD        OF PDDET_OPER_SURF INITIAL "TR"
  ITEM DOSSTU           OF PDDET_OPER_SURF INITIAL "E" ; En cours


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE REFERENCE &
               ALIAS  TRA_AVANT &
               OCCURS WITH FILE PDDET_OPER_SURF

  ACCESS VIA   CIECLE,          &
               TRANUMTRA002     &
         USING T_CIECLEMNU,     &
               TRANUMTRA002     OF PDDET_OPER_SURF


  ITEM TRANUMTRA002     OF PDDET_OPER_SURF INITIAL TRANUMTRA002     OF TRA_AVANT
  ITEM DOSLON           OF PDDET_OPER_SURF INITIAL TRALON           OF TRA_AVANT
  ITEM DOSHAU           OF PDDET_OPER_SURF INITIAL TRAHAU           OF TRA_AVANT
  ITEM DOSEPA           OF PDDET_OPER_SURF INITIAL TRAEPA           OF TRA_AVANT
  ITEM TGRCODGRA        OF PDDET_OPER_SURF INITIAL TGRCODGRA        OF TRA_AVANT
  ITEM DOSCODFIN001     OF PDDET_OPER_SURF INITIAL TYFCODFIN        OF TRA_AVANT


;-------------------------------------------------------------------------------
;
; Tranche (avec le nouveau fini)
;

;;;;FILE PDTRANCHE DESIGNER  ALIAS  TRA_APRES
;;;;
;;;;  ACCESS VIA   CIECLE,          &
;;;;               TRANUMTRA002     &
;;;;         USING T_CIECLEMNU,     &
;;;;               TRANUMTRA002     OF PDDET_OPER_SURF


;-------------------------------------------------------------------------------
;
;
;

FILE PDDET_OPER_SURF DESIGNER ALIAS DOS_OLD


;-------------------------------------------------------------------------------
;
;
;

FILE PDDET_OPER_SURF DESIGNER ALIAS DOS_MAJ


;-------------------------------------------------------------------------------
;
; Machine opération de surface
;

FILE PDMACH_OP_SURF  REFERENCE

  ACCESS VIA     CIECLE,          &
                 MOPCODMAC        &
         USING   T_CIECLEMNU,     &
                 MOPCODMAC        OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_OSUSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_OSUSTU,                             &
               OSUSTU           OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT        OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Type de fini
;

;;;FILE PDTYPE_FINI   REFERENCE ALIAS TYF_FINI_APRES
;;;
;;;  ACCESS VIA     CIECLE,          &
;;;                 TYFCODFIN        &
;;;         USING   T_CIECLEMNU,     &
;;;                 TYFCODFIN        OF TRA_APRES


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Opération de surface " &
@ELSE
      " %%%Opération de surface " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19) (,42,58)


FIELD OSUNUMRAP        OF PDOPERATION_SURF &
LABEL "Numéro rapport:"&
        FIXED


FIELD OSUDEP           OF PDOPERATION_SURF &
LABEL "Heure départ..:"&
        FIXED


FIELD OSUDATPRD        OF PDOPERATION_SURF  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
LABEL "Date oper surf:"&
        FIXED


FIELD OSUARR           OF PDOPERATION_SURF &
LABEL "Heure arrêt...:"&
        FIXED


ALIGN (,3,19) (,,24)


FIELD MOPCODMAC        OF PDOPERATION_SURF &
LABEL "Code machine..:"&
        FIXED


FIELD MOPDSC001        OF PDMACH_OP_SURF   &
        FIXED


FIELD QRTCODQRT        OF PDOPERATION_SURF &
LABEL "Quart travail.:"&
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY                            &
        FIXED

FIELD OSUSTU           OF PDOPERATION_SURF &
LABEL "Statut.......:"&
        FIXED


FIELD CBIDSC           OF A_CBI_OSUSTU     &
        DISPLAY                            &
        FIXED


DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Tranches " &
@ELSE
      " %%%Tranches " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13

TITLE &
@IF FRANCAIS
    "                                           Epai.  Type     Type fini" &
@ELSE
    "%%%                                        Epai.  Type     Type fini" &
@ENDIF
     AT ,2

skip

TITLE &
@IF FRANCAIS
    "   No tranche   Long.    Haut.    Epai.    Res.   granit  avant après Err" &
@ELSE
    "%%%No tranche   Long.    Haut.    Epai.    Res.   granit  avant après Err" &
@ENDIF
     AT ,2

SKIP TO LINE 15


ALIGN (05,05,06) (,,18) (,,27) (,,36) (,,45) (,,54) (,,61) (,,67) (,,73)


CLUSTER OCCURS WITH PDDET_OPER_SURF  ID BASE 40


FIELD TRANUMTRA002     OF PDDET_OPER_SURF  &
        HIDDEN                             &
        LABEL " "                          &
        NUMERIC                            &
        BWZ                                &
        REQUIRED NOCHANGE                  &
        LOOKUP ON TRA_AVANT                &
          MESSAGE 2940 , & ; Ce numéro de tranche n'existe pas
               NOTON PDDET_OPER_SURF       &
          VIA   CIECLE,                    &
                OSUNUMRAP,                 &
                TRANUMTRA002               &
          USING T_CIECLEMNU,                      &
                OSUNUMRAP    OF PDOPERATION_SURF, &
                TRANUMTRA002 OF PDDET_OPER_SURF   &
          MESSAGE 3119 ; Ce numéro de tranche existe déjà pour ce rapport


FIELD DOSLON           OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY SIZE 5


FIELD DOSHAU           OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY SIZE 5


FIELD DOSEPA           OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY SIZE 5


FIELD DOSEPARES        OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                          SIZE 5


FIELD TGRCODGRA        OF TRA_AVANT        &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY


FIELD TYFCODFIN        OF TRA_AVANT        &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY


FIELD DOSCODFIN002     OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE


FIELD T_SILENT                             &
        SILENT


FIELD DOSACCERR        OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;

PROCEDURE INPUT TRANUMTRA002
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_COD          = 0 ; Toutes les tranches
    LET T_TRANUMTRA002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd122 MODE F &
                     PASSING T_TRANUMTRA002, &
                             T_COD,          &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TRANUMTRA002 )
  END
END


;-------------------------------------------------------------------------------
;
; Initialise les dimensions pour affichage
;

PROCEDURE PROCESS TRANUMTRA002
BEGIN
  LET DOSLON           OF PDDET_OPER_SURF  = TRALON OF TRA_AVANT
  LET DOSHAU           OF PDDET_OPER_SURF  = TRAHAU OF TRA_AVANT
  LET DOSEPA           OF PDDET_OPER_SURF  = TRAEPA OF TRA_AVANT
END


;-------------------------------------------------------------------------------
;
; Initialise la valeur du champ epaisseur résultante
;

PROCEDURE INPUT DOSEPARES
BEGIN
  IF 0 = SIZE(FIELDTEXT) AND ENTRYMODE AND NOT CHANGEMODE
  THEN BEGIN
    LET FIELDTEXT = ASCII(TRAEPA OF TRA_AVANT)
  END
END


;-------------------------------------------------------------------------------
;
; Initialise la valeur du champ epaisseur résultante
;

PROCEDURE EDIT DOSEPARES
BEGIN
  IF FIELDVALUE > TRAEPA OF TRA_AVANT
  THEN BEGIN
    ERROR 3030 ; L'épaisseur résultante doit être plus petite ou égale a l'épaisseur de départ
  END
END


;-------------------------------------------------------------------------------
;
; Assigne une valeur par défauts pour le champ
;

PROCEDURE INPUT DOSCODFIN002

BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
    LET DOSCODFIN002     OF PDDET_OPER_SURF = TYFCODFIN        OF TRA_AVANT
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le fini de surface ou l'epaisseur resultante ont été modifié
;

PROCEDURE EDIT T_SILENT
BEGIN
  IF DOSEPARES        OF PDDET_OPER_SURF = TRAEPA           OF TRA_AVANT &
     AND &
     DOSCODFIN002     OF PDDET_OPER_SURF = TYFCODFIN        OF TRA_AVANT
  THEN BEGIN
    ERROR 3027 ; Ni l'épaisseur, ni le fini de surface n'ont été modifié
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  FOR PDDET_OPER_SURF
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDDET_OPER_SURF &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDDET_OPER_SURF &
       AND NOT NEWRECORD     OF PDDET_OPER_SURF &
       AND NOT DELETEDRECORD OF PDDET_OPER_SURF &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Valide les dimension minimum et maximum pour un produit dimension
    ;
    IF DOSEPARES        OF PDDET_OPER_SURF  > T_DIM_MAX_EPA_TR &
       OR &
       DOSEPARES        OF PDDET_OPER_SURF  < T_DIM_MIN_EPA_TR
    THEN BEGIN
      ERROR 2962 ; Cette dimension est hors des limites
    END
  END
  ;
  ; Valide les lignes de détail du rapport
  ;
  FOR PDDET_OPER_SURF
  BEGIN
    ;
    ; Si la ligne à déjà été valider il faut valider les modification avant
    ; d'effectuer la mise à jour de la pièce
    ;
    IF DOSSTU OF PDDET_OPER_SURF = "V"
    THEN BEGIN
      LET DOSSTU OF PDDET_OPER_SURF = "E"
      IF OSUSTU OF PDOPERATION_SURF = "V"
      THEN BEGIN
        LET OSUSTU OF PDOPERATION_SURF = "E"
      END
    END
    ;
    ; Si la ligne à déjà été transferrer il faut replacer la pièce dans
    ; l'état initial avant de faire la nouvelle mise à jour
    ;
    IF DOSSTU OF PDDET_OPER_SURF = "T"
    THEN BEGIN
      LET DOSSTU OF PDDET_OPER_SURF = "E"
      GET DOS_OLD   OPTIONAL     &
        VIA     CIECLE,          &
                OSUNUMRAP,       &
                TRANUMTRA002     &
        USING   T_CIECLEMNU,                          &
                OSUNUMRAP        OF PDOPERATION_SURF, &
                TRANUMTRA002     OF PDDET_OPER_SURF
      IF ACCESSOK
      THEN BEGIN
        GET DOS_MAJ OPTIONAL       &
          VIA     CIECLE,          &
                  OSUNUMRAP,       &
                  TRANUMTRA002,    &
                  DOSSTU           &
          USING   T_CIECLEMNU,                          &
                  OSUNUMRAP        OF PDOPERATION_SURF, &
                  TRANUMTRA002     OF PDDET_OPER_SURF,  &
                  "M"
        IF NOT ACCESSOK
        THEN BEGIN
          LET CIECLE       OF DOS_MAJ = T_CIECLEMNU
          LET OSUNUMRAP    OF DOS_MAJ = OSUNUMRAP    OF DOS_OLD
          LET DOSSTU       OF DOS_MAJ = "M"
          LET TPDTYPPRD    OF DOS_MAJ = TPDTYPPRD    OF DOS_OLD
          LET TRANUMTRA002 OF DOS_MAJ = TRANUMTRA002 OF DOS_OLD
          LET DOSLON       OF DOS_MAJ = DOSLON       OF DOS_OLD
          LET DOSHAU       OF DOS_MAJ = DOSHAU       OF DOS_OLD
          LET DOSEPA       OF DOS_MAJ = DOSEPA       OF DOS_OLD
          LET TGRCODGRA    OF DOS_MAJ = TGRCODGRA    OF DOS_OLD
          LET DOSCODFIN001 OF DOS_MAJ = DOSCODFIN001 OF DOS_OLD
          LET DOSCODFIN002 OF DOS_MAJ = DOSCODFIN002 OF DOS_OLD
          LET DOSEPARES    OF DOS_MAJ = DOSEPARES    OF DOS_OLD
          LET DOSPRD       OF DOS_MAJ = DOSPRD       OF DOS_OLD
          LET DOSHREDIS    OF DOS_MAJ = DOSHREDIS    OF DOS_OLD
          LET DOSACCERR    OF DOS_MAJ = DOSACCERR    OF DOS_OLD
          PUT DOS_MAJ
        END
      END
      ELSE BEGIN
        ERROR "PROBLÈME TECHNIQUE"
      END

      IF OSUSTU OF PDOPERATION_SURF = "V" OR &
         OSUSTU OF PDOPERATION_SURF = "T"
      THEN BEGIN
        LET OSUSTU OF PDOPERATION_SURF = "E"
      END
    END

  END




;;;  ;
;;;  ; Calcul du volume de la tranche si l'épaisseur a été modifié et met a jour
;;;  ; le fini de surface.
;;;  ;
;;;  IF DOSEPARES OF PDDET_OPER_SURF <> DOSEPA OF PDDET_OPER_SURF
;;;  THEN BEGIN
;;;    LET TZ_LARGEUR              = DOSLON    OF PDDET_OPER_SURF
;;;    LET TZ_HAUTEUR              = DOSHAU    OF PDDET_OPER_SURF
;;;    LET TZ_EPAISSEUR            = DOSEPARES OF PDDET_OPER_SURF
;;;    DO INTERNAL CALVOL
;;;    GET TRA_APRES
;;;    IF ACCESSOK
;;;    THEN BEGIN
;;;      LET TRASURMC2        OF TRA_APRES = TZ_MC2
;;;      LET TRAVOLMC3        OF TRA_APRES = TZ_VOLUME
;;;      LET TRAEPA           OF TRA_APRES = DOSEPARES OF PDDET_OPER_SURF
;;;      LET TYFCODFIN        OF TRA_APRES = DOSCODFIN002     OF PDDET_OPER_SURF
;;;      PUT TRA_APRES
;;;    END
;;;  END
;;;  ;
;;;  ; Met a jour le fini de surface de la tranche
;;;  ;
;;;  ELSE BEGIN
;;;    GET TRA_APRES
;;;    IF ACCESSOK
;;;    THEN BEGIN
;;;      LET TYFCODFIN        OF TRA_APRES = DOSCODFIN002     OF PDDET_OPER_SURF
;;;      PUT TRA_APRES
;;;    END
;;;  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Opération de surface *****************************
*                                                                              *
* Numéro rapport: xxxxxxxxxxx            Heure départ..: xxxxx                 *
* Date oper surf: xxxxxxxx               Heure arrêt...: xxxxx                 *
* Code machine..: xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Quart travail.: x    xxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                              *
********************************************************************************
*                                                                              *
*                                           Epai.  Type     Type fini          *
*   No tranche   Larg.    Haut.    Epai.    Res.   granit  avant après Err     *
*                                                                              *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*    xxxxxxxxx   xxxxx    xxxxx    xxxxx    xxxxx    xxx    xx    xx    x      *
*                                                                              *
********************************************************************************
@ENDIF
