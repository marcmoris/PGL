;/T/ Gestion des Lots
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 7 mars 1994
;
;    Description..: Cet écran permet de définir les lots des transactions
;                   pour les achats et réceptions. Les autorisations de 
;                   journalisation sont également gérées dans cet écran.
;
;                   Cet écran est utilisé par les lots de réception et de 
;                   retour. Ce qui est différent c'est le type de lot
;                   (RC,RT)
;
;/M/ Pascal Tremblay, 98-01-20
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar003 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_PECCLEMNU, &
                       T_PECCLECOU, &
                       T_PECCLEFND, &
                       T_LARCLEFND

USE ussectmp  NOLIST     
    "AR003"                 

USE ar003.hlp NOLIST

USE ustraecr NOLIST

USE usactecr  NOLIST     

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Vérification" ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreens"
  MENUITEM LABEL "Verification" ACTION DESIGNER D001
@ENDIF

;
; Il faut ouvrir une nouvelle transaction, car cet écran est appelé comme
; sous-écran dans les transactions (réception,retour).
;
USE ustrasse NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Nom de la compagnie du menu.

TEMPORARY T_PECCLEMNU CHARACTER SIZE 4 ; Période du menu
TEMPORARY T_PECCLECOU CHARACTER SIZE 4 ; Période courante du module A/R.
TEMPORARY T_PECCLEFND CHARACTER SIZE 4 ; Période de recherche.
TEMPORARY T_LARCLEFND CHARACTER SIZE 4 ; Numéro de lot de recherche.


;-------------------------------------------------------------------------------
;
; Lot
;
FILE ARLOT             PRIMARY NOITEM

;-------------------------------------------------------------------------------
;
; Pour valider et afficher l'usager.
;
FILE GSUSAGER          DESIGNER &
                      OPEN READ

;-------------------------------------------------------------------------------
;
; Validation du type d'écriture.
;
DEFINE    D_LARTYPECR CHARACTER SIZE 16 = "LARTYPECR"
TEMPORARY T_LARTYPECR CHARACTER SIZE 4  

FILE VCBI_DSC          REFERENCE

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_LARTYPECR, &
                LARTYPECR OF ARLOT


;-------------------------------------------------------------------------------

TEMPORARY T_USRCLE    CHARACTER SIZE 12 ; Popup sur les usagers 
TEMPORARY T_PECCLE    CHARACTER SIZE  4 ; Popup sur les périodes 
TEMPORARY T_MODCLE    CHARACTER SIZE  2 ; Popup sur les périodes

;
; Pour afficher le nom des usagers.
;
TEMPORARY T_LARUSR_NOM    CHARACTER SIZE 40
TEMPORARY T_LARUSRVAL_NOM CHARACTER SIZE 40
TEMPORARY T_LARUSRJOU_NOM CHARACTER SIZE 40
TEMPORARY T_LARUSRATR_NOM CHARACTER SIZE 40

;
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)

;
; Calcul de l'écart entre le montant vérifié et le montant cumulé dans le
; lot.
;
DEFINE D_DIFMNTTRS FLOAT   SIZE 8 = LARCUMMNTVER OF ARLOT - &
                                    LARCUMMNT    OF ARLOT
;
; Calcul de l'écart entre le nombre vérifié et le nombre cumulé dans le
; lot.
;
DEFINE D_DIFNBRTRS INTEGER SIZE 2 = LARNBRTRSVER OF ARLOT - &
                                    LARNBRTRS    OF ARLOT
;
; Hors balance.
;
DEFINE D_HRSBLC CHARACTER SIZE 20 = &
@IF FRANCAIS
                                    "*** Hors Balance ***" &
@ELSE
                                    "** Out of balance **" &
@ENDIF
                                    IF  0 <> LARNBRTRSERR OF ARLOT


;-------------------------------------------------------------------------------

USE usdrwecr NOLIST     
USE ustitstd NOLIST
USE ushilite NOLIST     

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des lots " &
@ELSE
      " Batch Management " &
@ENDIF
      CENTERED AT ,1

SKIP

USE ustitoff NOLIST           

ALIGN (19,3,19) (,40,56)

FIELD PECCLE OF ARLOT &
        HIDDEN ID 05 

FIELD LARCLE     OF ARLOT 

SKIP 1

ALIGN (19,3,19)

FIELD LARDSC     OF ARLOT &
        HIDDEN ID 10

SKIP 1

ALIGN (19,3,19) (,,22)

FIELD LARTYPECR  OF ARLOT &
        HIDDEN ID 15 

FIELD CBIDSC OF VCBI_DSC 

ALIGN (19,3,19) (,,32)

FIELD LARUSR     OF ARLOT &
        HIDDEN ID 20 

FIELD T_LARUSR_NOM &
        DISPLAY

SKIP 1

FIELD LARDATVAL  OF ARLOT  FORMAT YYYYMMDD &
        HIDDEN ID 25 
SKIP

FIELD LARUSRVAL  OF ARLOT &
        HIDDEN ID 30 

FIELD T_LARUSRVAL_NOM &
        DISPLAY

SKIP 1

ALIGN (19,3,19) (,,24)

FIELD LARATRJOU  OF ARLOT &
        HIDDEN ID 35 &
        SIZE 3

HILITE DISPLAY OFF

FIELD D_HRSBLC &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF D_HRSBLC <> ""
          
USE ushilite NOLIST

ALIGN (19,3,19) (,,32)

FIELD LARUSRATR OF ARLOT HIDDEN ID 40 &
        DISPLAY

FIELD T_LARUSRATR_NOM &
        DISPLAY

SKIP 1

ALIGN(19,3,19) (,,32)

FIELD LARDATJOU  OF ARLOT  FORMAT YYYYMMDD HIDDEN ID 45 &
        DISPLAY 

FIELD LARHREJOU  OF ARLOT &
        DISPLAY

ALIGN (19,3,19) (,,32)

FIELD LARUSRJOU OF ARLOT HIDDEN ID 50 &
        DISPLAY

FIELD T_LARUSRJOU_NOM &
        DISPLAY

SKIP

DRAW ,1 TO ,80

SKIP 1

TITLE &
@IF FRANCAIS
      "    Vérifié            Cumulé             Ecart" &
@ELSE
      "   Verified          Totalled           %%%cart" &
@ENDIF
      AT ,21

SKIP

ALIGN(19,3,19) (,,37) (,,55)

FIELD LARCUMMNTVER OF ARLOT HIDDEN ID 55 &
        REQUIRED  &
@IF FRANCAIS
        LABEL "Montant total.:"
@ELSE
        LABEL "Total amount..:"
@ENDIF 

FIELD LARCUMMNT    OF ARLOT &
        DISPLAY 

FIELD D_DIFMNTTRS &
        DISPLAY &
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

ALIGN(19,3,24) (,,42) (,,60)
FIELD LARNBRTRSVER OF ARLOT HIDDEN ID 60 &
        REQUIRED   & 
@IF FRANCAIS 
        LABEL "Nombre total..:"
@ELSE
        LABEL "Total number..:"
@ENDIF 

FIELD LARNBRTRS    OF ARLOT &
        DISPLAY

FIELD D_DIFNBRTRS &
        DISPLAY &
        PICTURE "^,^^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 2

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT PECCLE
BEGIN
  ;
  ; Popup sur les périodes.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_MODCLE = "AR"
    RUN SCREEN mc109 PASSING T_CIECLEMNU, T_PECCLE, T_MODCLE MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les types de lot(codes bilingues).
;
;
PROCEDURE INPUT LARTYPECR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LARTYPECR = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_LARTYPECR, T_LARTYPECR
    LET FIELDTEXT = TRUNC(T_LARTYPECR)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les usagers.
;
;
PROCEDURE INPUT LARUSR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_USRCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gs104 PASSING T_USRCLE MODE F
    LET FIELDTEXT = TRUNC(T_USRCLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager du lot(Utilisateur).
;
;
PROCEDURE OUTPUT T_LARUSR_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LARUSR OF ARLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager de validation.
;
;
PROCEDURE OUTPUT T_LARUSRVAL_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LARUSRVAL OF ARLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; On transpose les valeurs O et N en valeurs 1 et 0.
;
;
PROCEDURE INPUT LARATRJOU
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
;
; On affiche les valeurs 1 et 0 sous la forme Oui et Non.
;
;
PROCEDURE OUTPUT LARATRJOU
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager qui a autorisé le lot.
;
;
PROCEDURE OUTPUT T_LARUSRATR_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LARUSRATR OF ARLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END


;-------------------------------------------------------------------------------
;
;
; Pour afficher le nom de l'usager qui a journalisé le lot.
;
;
PROCEDURE OUTPUT T_LARUSRJOU_NOM
BEGIN
  GET GSUSAGER VIA USRCLE &
               USING LARUSRJOU OF ARLOT &
               OPTIONAL
  LET FIELDTEXT = USREMPNOM OF GSUSAGER
END

;-------------------------------------------------------------------------------
;
;
;
; Sous-écran   Vérification
;
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN ar003a PASSING ARLOT
END


;-------------------------------------------------------------------------------
;
;
; Le path suivant tient compte de la période et du lot reçus en paramètre.
;
;
PROCEDURE PATH
BEGIN
  IF T_PECCLEFND = ""
  THEN BEGIN
    REQUEST PECCLE OF ARLOT
    IF PROMPTOK
    THEN REQUEST LARCLE OF ARLOT
    IF PROMPTOK
    THEN LET PATH = 1
    IF PATH = 0
    THEN BEGIN
      REQUEST PECCLE OF ARLOT
      IF PROMPTOK
      THEN LET PATH = 2
    END
    IF PATH = 0
    THEN LET PATH = 3
  END
  ELSE LET PATH = 99
END


;-------------------------------------------------------------------------------
;
;
; Find spécial du au path 99.
;
;
PROCEDURE FIND
BEGIN
  IF PATH = 1
  THEN GET ARLOT VIA CIECLE, &
                     PECCLE, &
                     LARCLE  &
                 USING T_CIECLEMNU, &
                       PECCLE OF ARLOT, &
                       LARCLE OF ARLOT
  IF PATH = 2
  THEN GET ARLOT VIA CIECLE, &
                     PECCLE  &
                 USING T_CIECLEMNU, &
                       PECCLE OF ARLOT &
                 ORDERBY CIECLE, &
                         PECCLE, &
                         LARCLE
  IF PATH = 3
  THEN GET ARLOT VIA CIECLE     &
                 USING T_CIECLEMNU &
                 ORDERBY CIECLE, &
                         PECCLE, &
                         LARCLE
  IF PATH = 99
  THEN GET ARLOT VIA CIECLE, &
                     PECCLE, &
                     LARCLE  &
                 USING T_CIECLEMNU, &
                       T_PECCLEFND, &
                       T_LARCLEFND  
END

BUILD  
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Gestion des lots *******************************
* Période ......: xxxxx                Lot...........: xxxx                    *
*                                                                              *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                              *
* Type de lot...: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* Usager........: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
* Vérifié le....: xxxxxxxx                                                     *
* Vérifié par...: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
* Autorisé......: xxx  xxxxxxxxxxxxxxxxxxxx                                    *
* Autorisé par..: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
* Journalisé le.: xxxxxxxx xxxxx                                               *
* Journalisé par: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
*                       Vérifié            Cumulé             Ecart            *
* Montant total.: xxxxxxxxxxxxxxx   xxxxxxxxxxxxxxx   xxxxxxxxxxxxxx           *
* Nombre total..:      xxxxx             xxxxx             xxxxxx              *
********************************************************************************
@ENDIF 
