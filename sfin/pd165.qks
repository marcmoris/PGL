;/T/ Machine de débitage (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-14
;
;    Description..: Cette table contient les informations de base des
;                   machine servant à éffectuer le débitage.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd165 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD165"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd165.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;
TEMPORARY T_MDECODMAC CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Machine de débitage
;

FILE PDMACH_DEBITAGE PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 MDECODMAC &
         USING   T_CIECLEMNU,                 &
                 MDECODMAC OF PDMACH_DEBITAGE &
         REQUEST MDECODMAC OF PDMACH_DEBITAGE &
         ORDERBY CIECLE,   &
                 MDECODMAC

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 MDECODMAC

         ITEM CIECLE OF PDMACH_DEBITAGE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Machine de débitage " &
@ELSE
      " %%%Machine de débitage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD MDECODMAC OF PDMACH_DEBITAGE              &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE                       &
        LOOKUP NOTON PDMACH_DEBITAGE            &
          VIA   CIECLE,                         &
                MDECODMAC                       &
          USING T_CIECLEMNU,                    &
                MDECODMAC OF PDMACH_DEBITAGE    &
          MESSAGE 2914 ; Ce code de machine existe déjà.


SKIP 1


FIELD MDEDSC001 OF PDMACH_DEBITAGE         &
        HIDDEN ID 10                       &
        REQUIRED


FIELD MDEDSC002 OF PDMACH_DEBITAGE         &
        ID SAME                            &
        NOLABEL


FIELD MDEDSC003 OF PDMACH_DEBITAGE         &
        ID SAME                            &
        NOLABEL


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT MDECODMAC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MDECODMAC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd109 MODE F &
                     PASSING T_MDECODMAC, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_MDECODMAC )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDMACH_DEBITAGE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDMACH_DEBITAGE &
     AND NOT NEWRECORD     OF PDMACH_DEBITAGE &
     AND NOT DELETEDRECORD OF PDMACH_DEBITAGE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Machine de débitage ******************************
*                                                                              *
* Code machine..: xxx                                                          *
*                                                                              *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
