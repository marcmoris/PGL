;/T/ Grand livre sommaire  - Par compagnie / Consolidé
;
;/P/ Programmeur..: Chantal Martineau
;    Date Création: 5 mai 1993
;
;    Description..: Le but du grand livre sommaire est de nous données une image
;                   rapide du solde des comptes selon les périodes sélectionnées.
;
;                   Le rapport imprime trois solde: le solde de début, la variation
;                   et le solde de fin.
;                     - Le solde de début est le solde d'ourverture de la première
;                       période de la sélection. (N'inclus pas le solde cumulatif reel
;                       de la première période sélectionné.)
;                     - La variation représente la sommation des transactions qui ont
;                       affectées le compte pour les périodes sélectionnées. C-a-d.
;                       la sommation du solde courant de chaque période.
;                     - Le solde de fin est le solde de début plus la variation.
;
;                   Ce rapport peut être demandé par compagnie.
;
;    Paramètres...: Compagnie
;                   Compte comptable
;                   Année financière
;                   Période de début
;                   Période de fin
;
;---------------------------------------------------------------------------

RUN gl332t

USE ussetqts NOLIST

;==============================================================================
;
REQUEST PREPARE_SOLDE

ACCESS MCSOLDE_ANNUEL


CHOOSE CIECLE PARM      , &   ; Un, plusieur ou tous
       CPTCLE PARM RANGE, &   ; Compte comptable
       UNACLE PARM            ; Unité


DEFINE D_ANNFIN CHARACTER SIZE 2 = PARM ; Année financière
DEFINE D_PECDEB CHARACTER SIZE 2 = PARM ; Période de début
DEFINE D_PECFIN CHARACTER SIZE 2 = PARM ; Période de fin




SELECT MCSOLDE_ANNUEL IF SANANN OF MCSOLDE_ANNUEL = D_ANNFIN



TEMPORARY T_SLDDEB FLOAT SIZE 8    ; Solde de début.
TEMPORARY T_SLDVAR FLOAT SIZE 8    ; Variation
TEMPORARY T_TOTSLDDEB FLOAT SIZE 8 ; Total du solde de début
TEMPORARY T_TOTSLDVAR FLOAT SIZE 8 ; Total de la variation

SORT ON CIECLE OF MCSOLDE_ANNUEL & ; Compagnie
     ON CPTCLE OF MCSOLDE_ANNUEL & ; Compte
     ON UNACLE OF MCSOLDE_ANNUEL   ; Unité
;
; Calcul du Solde de début. (Solde ouverture + cummulatif réel
; jusqu'à la période de début sélectionné)
;
ITEM T_SLDDEB = SANSLDOUV OF MCSOLDE_ANNUEL

ITEM T_SLDDEB = T_SLDDEB + SANCUMREE1  OF MCSOLDE_ANNUEL IF D_PECDEB >= "02"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE2  OF MCSOLDE_ANNUEL IF D_PECDEB >= "03"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE3  OF MCSOLDE_ANNUEL IF D_PECDEB >= "04"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE4  OF MCSOLDE_ANNUEL IF D_PECDEB >= "05"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE5  OF MCSOLDE_ANNUEL IF D_PECDEB >= "06"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE6  OF MCSOLDE_ANNUEL IF D_PECDEB >= "07"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE7  OF MCSOLDE_ANNUEL IF D_PECDEB >= "08"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE8  OF MCSOLDE_ANNUEL IF D_PECDEB >= "09"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE9  OF MCSOLDE_ANNUEL IF D_PECDEB >= "10"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE10 OF MCSOLDE_ANNUEL IF D_PECDEB >= "11"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE11 OF MCSOLDE_ANNUEL IF D_PECDEB >= "12"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE12 OF MCSOLDE_ANNUEL IF D_PECDEB >= "13"
ITEM T_SLDDEB = T_SLDDEB + SANCUMREE13 OF MCSOLDE_ANNUEL IF D_PECDEB >= "14"

;
; Variation, addition des soldes courant selon les périodes sélectionnées.
;
ITEM T_SLDVAR = 0

ITEM T_SLDVAR = T_SLDVAR + SANCUMREE1  OF MCSOLDE_ANNUEL IF "01" >= D_PECDEB AND "01" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE2  OF MCSOLDE_ANNUEL IF "02" >= D_PECDEB AND "02" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE3  OF MCSOLDE_ANNUEL IF "03" >= D_PECDEB AND "03" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE4  OF MCSOLDE_ANNUEL IF "04" >= D_PECDEB AND "04" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE5  OF MCSOLDE_ANNUEL IF "05" >= D_PECDEB AND "05" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE6  OF MCSOLDE_ANNUEL IF "06" >= D_PECDEB AND "06" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE7  OF MCSOLDE_ANNUEL IF "07" >= D_PECDEB AND "07" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE8  OF MCSOLDE_ANNUEL IF "08" >= D_PECDEB AND "08" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE9  OF MCSOLDE_ANNUEL IF "09" >= D_PECDEB AND "09" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE10 OF MCSOLDE_ANNUEL IF "10" >= D_PECDEB AND "10" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE11 OF MCSOLDE_ANNUEL IF "11" >= D_PECDEB AND "11" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE12 OF MCSOLDE_ANNUEL IF "12" >= D_PECDEB AND "12" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE13 OF MCSOLDE_ANNUEL IF "13" >= D_PECDEB AND "13" <= D_PECFIN
ITEM T_SLDVAR = T_SLDVAR + SANCUMREE14 OF MCSOLDE_ANNUEL IF "14" >= D_PECDEB AND "14" <= D_PECFIN

;
; Sommation des soldes par compte. (Solde de début et solde de variation)
;
ITEM T_TOTSLDDEB SUBTOTAL T_SLDDEB RESET AT UNACLE
ITEM T_TOTSLDVAR SUBTOTAL T_SLDVAR RESET AT UNACLE

;
; Sous-fichier pour l'impression du rapport.
;
SUBFILE WGL332A AT UNACLE &
  KEEP &
  INCLUDE CIECLE OF MCSOLDE_ANNUEL, &
          D_ANNFIN, &
          D_PECDEB, &
          D_PECFIN, &
          CPTCLE OF MCSOLDE_ANNUEL, &
          UNACLE OF MCSOLDE_ANNUEL, &
          T_TOTSLDDEB, &
          T_TOTSLDVAR

BUILD
