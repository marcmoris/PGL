;/T/ Gestion des responsables de projets - Validation de la destruction
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 14 décembre 1994
;
;    Description..: Cet écran vérifie si le responsable de projets reçue en
;                   paramètre est référée dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp152x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCCODE_BILINGUE , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Responsables de projets
;
FILE MCCODE_BILINGUE  MASTER

;
; Définitions de projets
;
FILE GPPROJETS        REFERENCE

;
; Définitions des activités
;
FILE GPPRO_ACTIVITE   REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN

  GET GPPROJETS VIA PRJRES &
                    USING CBICLE OF MCCODE_BILINGUE &
                    OPTIONAL
  IF ACCESSOK
  THEN ERROR 813  ; Destruction annulée, le responsable de projets est reféré
                  ; dans les projets.

  GET GPPRO_ACTIVITE VIA PRARES &
                     USING CBICLE OF MCCODE_BILINGUE &
                     OPTIONAL
  IF ACCESSOK
  THEN ERROR 814  ; Destruction annulée, le responsable de projets est reféré
                  ; dans les sous-projets.
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
