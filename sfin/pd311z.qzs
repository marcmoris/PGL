;/T/ 2.2.4 Liste des sorties/réceptions de produit
;
;/P/ Programmeur...: Rémy Demers
;    Date Création.: 21 octobre 1997
;
;    Description...: Liste des sorties et des réceptions des produits
;
;    Paramètres....: aucun
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS *WPD311SR &

        LINK    G_CIECLE    OF WPD311SR,   &
                G_TGRCODGRA OF WPD311SR    &
        TO      CIECLE,                    &
                TGRCODGRA OF PDTYPE_GRANIT OPT &

        LINK    G_CIECLE    OF WPD311SR,   &
                G_RSOCODSOR OF WPD311SR    &
        TO      CIECLE,                    &
                RSOCODSOR OF PDRAISON_SORTIE OPT &

        LINK    G_FOUCIECLE OF WPD311SR , &
                G_FOUCLE OF WPD311SR &
        TO      FOUCIECLE, &
                FOUCLE OF VFOC_FOU OPT &

        LINK    ("F",&
                G_SFOUCLE OF WPD311SR) &
        TO      REFCLETYP ,&
                REFCLENUM OF MCREF_ADRESSE OPT &

        LINK G_CIECLE OF WPD311SR &
        TO   CIECLE OF MCCOMPAGNIE OPT

        DEFINE D_TRI CHAR SIZE 17 = G_CAINUMCAI OF WPD311SR + G_CODE_PROD OF WPD311SR


SORT    ON G_DAT_MOU OF WPD311SR &
        ON G_NUM_MOU OF WPD311SR &
        ON D_TRI                 &
        ON G_CODE_PROD OF WPD311SR


SELECT  VFOC_FOU &
        IF FOCCIECLE OF VFOC_FOU = G_CIECLE OF WPD311SR

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 &
        = "Liste des transferts (réception/sortie) de produit"

;        = "Pour la période du " &
;        + ASCII(G_DATE_DEB,8)[1:4] &
;        + "/" &
;        + ASCII(G_DATE_DEB,8)[5:2] &
;        + "/" &
;        + ASCII(G_DATE_DEB,8)[7:2] &
;        + " au " &
;        + ASCII(G_DATE_FIN,8)[1:4] &
;        + "/" &
;        + ASCII(G_DATE_FIN,8)[5:2] &
;        + "/" &
;        + ASCII(G_DATE_FIN,8)[7:2]

;-------------------------------------------------------------------------------
;
; Numéro du rapport.
;

DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD311"


;-------------------------------------------------------------------------------
;
; Nom de la compagnie.
;

DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


;-------------------------------------------------------------------------------
;
; Définition des éléments variables
;

DEFINE D_TYPE_MOU CHAR SIZE 9 &
        = CASE OF G_TYP_MOU OF WPD311SR &
        WHEN "S"        THEN "Sortie" &
        WHEN "R"        THEN "Réception" &
        DEFAULT " "

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = D_TYPE_MOU

DEFINE D_FOUCOMP CHAR SIZE 40 = G_RADNOM OF WPD311SR IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE CIENOM OF MCCOMPAGNIE
DEFINE D_FOU1 CHAR SIZE 40 = G_RADADR1 OF WPD311SR IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE CIEADR1 OF MCCOMPAGNIE
DEFINE D_FOU2 CHAR SIZE 40 = G_RADADR2 OF WPD311SR IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE CIEADR2 OF MCCOMPAGNIE
DEFINE D_FOU3 CHAR SIZE 40 = G_RADADR3 OF WPD311SR IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE CIEADR3 OF MCCOMPAGNIE
DEFINE D_FOU4 CHAR SIZE 40 = G_RADADR4 OF WPD311SR IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE CIEADR4 OF MCCOMPAGNIE
DEFINE D_FOU5 CHAR SIZE 40 = G_RADCP OF WPD311SR IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE CIECP OF MCCOMPAGNIE

DEFINE D_COMPDES CHAR SIZE 40 = CIENOM OF MCCOMPAGNIE IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE RADNOM OF MCREF_ADRESSE
DEFINE D_COM1 CHAR SIZE 40 = CIEADR1 OF MCCOMPAGNIE IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE RADADR1 OF MCREF_ADRESSE
DEFINE D_COM2 CHAR SIZE 40 = CIEADR2 OF MCCOMPAGNIE IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE RADADR2 OF MCREF_ADRESSE
DEFINE D_COM3 CHAR SIZE 40 = CIEADR3 OF MCCOMPAGNIE IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE RADADR3 OF MCREF_ADRESSE
DEFINE D_COM4 CHAR SIZE 40 = CIECP OF MCCOMPAGNIE IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE RADADR4 OF MCREF_ADRESSE
DEFINE D_COM5 CHAR SIZE 40 = " "IF G_TYP_MOU OF WPD311SR ="R" &
                             ELSE RADCP OF MCREF_ADRESSE


DEFINE D_TGRDSCFRA001 CHAR SIZE 15 = TGRDSCFRA001 OF PDTYPE_GRANIT




DEFINE D_CONFIRME CHAR SIZE 3 &
        = CASE OF G_SPRCON OF WPD311SR &
        WHEN "1"        THEN "Oui" &
        WHEN "0"        THEN "Non" &
        DEFAULT " "

DEFINE D_TYPE_PROD CHAR SIZE 5 &
        = CASE OF G_TYPE_PROD OF WPD311SR &
        WHEN "B"        THEN "Bloc" &
        WHEN "T"        THEN "Tran." &
        WHEN "D"        THEN "Diag." &
        WHEN "P"        THEN "Prod." &
        DEFAULT " "

DEFINE D_NUMERO CHAR SIZE 12 &
        =       G_CODE_PROD OF WPD311SR[1:4] &
                + "-" &
                + G_CODE_PROD OF WPD311SR[5:5] &
                + "-" &
                + G_CODE_PROD OF WPD311SR[10:1] &
                IF G_TYPE_PROD = "B" &

        ELSE    G_CODE_PROD OF WPD311SR[1:2] &
                + "-" &
                + G_CODE_PROD OF WPD311SR[3:5] &
                IF G_TYPE_PROD OF WPD311SR = "D" &

        ELSE    G_CODE_PROD OF WPD311SR[1:7] &
                IF G_TYPE_PROD OF WPD311SR = "T" &

        ELSE    " "

;USE ustitstd52 NOLIST
USE ustitgor132_45 NOLIST
       SKIP 3 &

                TAB 017 "Num. Transfert.:" &
                TAB 034 G_NUM_MOU OF WPD311SR &
               ; TAB 100 "Type mouvement:" &
               ; TAB 116 D_TYPE_MOU &
        SKIP 1 &
                TAB 017 "Date Transfert:" &
                TAB 034 G_DAT_MOU OF WPD311SR PIC "^^^^/^^/^^" &
        SKIP 1 &
                TAB 017 "Raison........:" &
                TAB 034 G_RSOCODSOR OF WPD311SR &
                TAB 041 RSODSC001 OF PDRAISON_SORTIE &
       ; SKIP 1 &
       ;         TAB 017 "Fournisseur...:" &
       ;         TAB 034 G_FOUCLE OF WPD311SR &
       ;         TAB 041 FOUNOM OF VFOC_FOU &
        SKIP 1 &
                TAB 017 "No de P/O.....:" &
                TAB 034 G_BCMNUMBCM OF WPD311SR BWZ &
       ; SKIP 1 &
       ;         TAB 017 "Demandeur.....:" &
       ;         TAB 034 G_DMR_CNS OF WPD311SR &
       ; SKIP 1 &
       ;         TAB 017 "Confirmé (O/N):" &
       ;         TAB 034 D_CONFIRME &

       SKIP 3 &
                TAB 017 "Lieu d'origine" &
                TAB 084 "Lieu d'arrive"  &
       SKIP 1 &
                TAB 017 D_FOUCOMP &
                TAB 084 D_COMPDES &
       SKIP 1 &
                TAB 017 D_FOU1  &
                TAB 084 D_COM1  &
       SKIP 1 &
                TAB 017 D_FOU2  &
                TAB 084 D_COM2  &
       SKIP 1 &
                TAB 017 D_FOU3  &
                TAB 084 D_COM3  &
       SKIP 1 &
                TAB 017 D_FOU4  &
                TAB 084 D_COM4  &
       SKIP 1 &
                TAB 017 D_FOU5  &
                TAB 084 D_COM5  &
         SKIP 2 &
                TAB 092 "--Bloc--" &
                TAB 101 "-Tranche-" &
                TAB 113 "-Dia/prod-" &
                TAB 127 "-Diag-" &
        SKIP 1 &
                TAB 001 "Caisson" &
                TAB 011 "Numéro" &
                TAB 024 "Genre" &
                TAB 034 "Type de granit" &
                TAB 066 "Longueur" &
                TAB 075 "Hauteur" &
                TAB 083 "Epaisseur" &
                TAB 092 "  M3 net" &
                TAB 101 "    M2" &
                TAB 113 "Fini  Qte" &
                TAB 127 "Prio." &
        SKIP 2

REPORT &;FOOTING AT D_TRI &
                TAB 001 G_CAINUMCAI OF WPD311SR &
                TAB 011 D_NUMERO &
                TAB 024 D_TYPE_PROD &
                TAB 029 G_TGRCODGRA OF WPD311SR &
                TAB 034 D_TGRDSCFRA001 &
                TAB 066 G_LONGUEUR OF WPD311SR PICTURE "^^^^" &
                TAB 075 G_LARGEUR OF WPD311SR PICTURE "^^^^" &
                TAB 083 G_EPAISSEUR OF WPD311SR PICTURE "^^^^" &
                TAB 092 G_VOLMC3 OF WPD311SR PICTURE "^^^^.^^^^" BWZ &
                        OUTPUT SCALE 4 &
                TAB 101 G_SURMC2 OF WPD311SR PICTURE "^^^^.^^^^" BWZ &
                        OUTPUT SCALE 4 &
                TAB 114 G_CDDFIN OF WPD311SR &
                TAB 118 G_QTE OF WPD311SR PICTURE "^^^^" BWZ &
                TAB 128 G_CODPRI OF WPD311SR &
        SKIP

PAGE FOOTING &
               TAB 014 "Signature..: [______________________]" PRINT AT G_NUM_MOU OF WPD311SR &
               TAB 060 "Poids.(Kg)..:" PRINT AT G_NUM_MOU OF WPD311SR &
               TAB 075 G_POIDS OF WPD311SR SUBTOTAL PICTURE "^^^,^^^.^^^^" PRINT AT G_NUM_MOU OF WPD311SR &
               TAB 100 "No..:" PRINT AT G_NUM_MOU OF WPD311SR &
               TAB 107 G_NUM_MOU OF WPD311SR PRINT AT G_NUM_MOU OF WPD311SR


BUILD pd311z
@IF FORMAT
                                                                                                   1
         1         2         3         4         5         6         7         8         9         0         1         2         3
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
             |xx999                           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 PAGE:9999
             |xxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    AA/MM/JJ 99:99
             |                  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
             |
             |
             |
             |
             |
-------------+

             -   Num. mouvement.: 9999999                                                        -    Type mouvement: (entrée/sortie)
                Date mouvement.: AA/MM/JJ
                Raison.........: XXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
             -   Destination....: 123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                No de P/O......: 123456
             -   Demandeur......: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
             -   Confirmé(O/N)..: X


                FOURNISSEUR+ADDRESSE SI RECEPTION                                           COMPAGNIE+ADDRESSE SI RECEPTION
                  OU                                                                                OU
                COMPAGNIE+ADDRESSE                                                          DESCRIPTION DU CODE DE SORTIE



                                                                                           --bloc-- -tranche-   -Dia/Prod-   -Diag-
 Numéro      GENRE  Type de granit                               Longueur Largeur Épaisseur  M3 net      M2     Fini  Qte     Prio.
9999-99999-A bloc   999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999   999.9999
9999-99999-A bloc   999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999   999.9999
9999-99999-A bloc   999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999   999.9999

XX-XXXXX     diag.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                                      12
XX-XXXXX     diag.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                                      12
XX-XXXXX     diag.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                                      12

             prod.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                        12  1234
             prod.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                        12  1234
             prod.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                        12  1234

9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
@ENDIF
