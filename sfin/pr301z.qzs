;/T/ LISTE SOMMAIRE DES PROJETS / SOUS-PROJETS

USE ussetqzs NOLIST
SET FORMFEED NOBLANKS

ACCESS GPPROJET  &
  LINK CIECLE, PROCLE TO &
       CIECLE, PROCLE OF  GPACTIVITE  OPT  &
  LINK ("PROCAT", PROCAT OF GPPROJET) TO &
         XELNOM, CBICLE OF VCBI_DSC OPT

CHOOSE CIECLE PARM, PROCLE PARM, PROSTU PARM, PROANN PARM

SELECT GPACTIVITE IF ACTSTU OF GPACTIVITE = "A"

SORT ON PROCAT  OF GPPROJET &
     ON PROCLE  OF GPPROJET &
     ON ACTCLE  OF GPACTIVITE


DEFINE CATEGORIE CHAR*45 = TRUNC("Catégorie " + PROCAT OF GPPROJET + " - " + &
                                  CBIDSC OF VCBI_DSC )
DEFINE TITRE_CAT CHAR*132 = CENTER( CATEGORIE[1:132] )
DEFINE SOUL_CAT  CHAR*45 = "---------------------------------------------"

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
  "Liste sommaire des projets / sous-projets"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "PR301"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie


USE ustitstd NOLIST
  SKIP 2

HEADING AT PROCAT OF GPPROJET  &
        TAB 001 TITRE_CAT &
   SKIP 1 &
        TAB 044 SOUL_CAT &
   SKIP 2


HEADING AT PROCLE OF GPPROJET &
   SKIP 3 &
        TAB 002 "Projet:" &
        TAB 010 PROCLE    OF GPPROJET &
        TAB 020 PRODSC    OF GPPROJET HEADING "" &
        TAB 095 "Statut:" &
        TAB 103 PROSTU &
        TAB 106 "Début est." &
        TAB 117 PRODATDEBEST OF GPPROJET &
        TAB 126 PROHREDEBEST OF GPPROJET SIGNIF 4 &
   SKIP 1 &
        TAB 020 PRODSC2 OF GPPROJET HEADING "" &
   SKIP 1 &
        TAB 020 PRODSC3 OF GPPROJET HEADING "" &
   SKIP 2


REPORT &
        TAB 005 "Sous-projet:" PRINT AT PROCLE  &
        TAB 019  ACTCLE    OF GPACTIVITE &
        TAB 024  ACTDSC1   OF GPACTIVITE &
        TAB 095 "Statut:" &
        TAB 103 ACTSTU &
        TAB 106 "Début est." &
        TAB 117 ACTDATDEBEST OF GPACTIVITE &
        TAB 126 ACTHREDEBEST OF GPACTIVITE SIGNIF 4 &
   SKIP 1 &
        TAB 024  ACTDSC2 OF GPACTIVITE   &
   SKIP 1 &
        TAB 024  ACTDSC3 OF GPACTIVITE &
   SKIP 2


BUILD pr301z
