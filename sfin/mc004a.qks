;/T/ Taxes, test de calcul
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 6 avril 1993
;
;
;    Description..: Ce sous-écran sert à effectuer des tests des calculs
;                   de taxe fédérale et provinciale.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN mc004a NOMODE NOACTION FIELDMARK &
              FROM 4,11 TO 16,68 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY

USE mc004a.hlp

;-------------------------------------------------------------------------------
;
TEMPORARY T_TAXCLECOD_TPS CHARACTER SIZE 2
TEMPORARY T_TAXCLECOD_TVP CHARACTER SIZE 2
TEMPORARY T_MNTTST        FLOAT     SIZE 8

;
; Variable pour le calcul des taxes.
;
USE ustaxtmp NOLIST

;-------------------------------------------------------------------------------
;
; Code de taxe fédérale.
;
FILE VTAX_DSC         REFERENCE &
                      ALIAS A_TAX_TPS
  ACCESS VIA TAXCLETYP, &
             TAXCLECOD  &
         USING "F", &
               T_TAXCLECOD_TPS

;
; Code de taxe provinciale.
;
FILE VTAX_DSC         REFERENCE &
                      ALIAS A_TAX_TVP
  ACCESS VIA TAXCLETYP, &
             TAXCLECOD  &
         USING "P", &
               T_TAXCLECOD_TVP

;-------------------------------------------------------------------------------
;
;
TEMPORARY T_TAXCLETYP CHARACTER SIZE 1 ; Popup sur les taxes
TEMPORARY T_TAXCLECOD CHARACTER SIZE 2 ; Popup sur les taxes
TEMPORARY T_TAXMODPAT CHARACTER SIZE 5 ; Popup sur les taxes

;
; Calcul du remboursement de taxe.
;
DEFINE D_MNTRBS_TPS FLOAT SIZE 8 = ROUND( TZ_MNTTPS * TAXPCTCTI OF A_TAX_TPS )
DEFINE D_MNTRBS_TVP FLOAT SIZE 8 = ROUND( TZ_MNTTVP * TAXPCTCTI OF A_TAX_TVP )


;-------------------------------------------------------------------------------
;

USE ushilite NOLIST

DRAW 1,1 TO 13,58

TITLE &
@IF FRANCAIS
      " Test - Calcul de taxe " &
@ELSE
      " %%%TEST " &
@ENDIF
      AT ,1 CENTERED
SKIP 1

USE ustitoff NOLIST


ALIGN (3,3,19) (,,22)

FIELD T_TAXCLECOD_TPS HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Fédéral.......:" &
@ELSE
        LABEL "Federal.......:" &
@ENDIF
        UPSHIFT  &
        REQUIRED &
        LOOKUP ON A_TAX_TPS &
          MESSAGE 150 ; Ce code de taxe n'existe pas

FIELD TAXDSC OF A_TAX_TPS &
        DISPLAY
SKIP 1

FIELD T_TAXCLECOD_TVP HIDDEN ID 10 &
@IF FRANCAIS
        LABEL "Provincial....:" &
@ELSE
        LABEL "Provincial....:" &
@ENDIF
        UPSHIFT  &
        REQUIRED &
        LOOKUP ON A_TAX_TVP &
          MESSAGE 150 ; Ce code de taxe n'existe pas

FIELD TAXDSC OF A_TAX_TVP &
        DISPLAY

SKIP 1

ALIGN (3,3,19)

FIELD T_MNTTST HIDDEN ID 15 &
        REQUIRED &
@IF FRANCAIS
        LABEL "Montant.......:" &
@ELSE
        LABEL "Amount........:" &
@ENDIF
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5 &
        INPUT SCALE 2

SKIP 1

ALIGN (3,3,19) (,35,43)

FIELD TZ_MNTTPS HIDDEN ID 20 &
        DISPLAY &
@IF FRANCAIS
        LABEL "T.P.S.........:" &
@ELSE
        LABEL "G.S.T.........:" &
@ENDIF
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

FIELD D_MNTRBS_TPS &
@IF FRANCAIS
        LABEL "C.T.I.:" &
@ELSE
        LABEL "C.T.I.:" &
@ENDIF
        DISPLAY &
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

SKIP 1

FIELD TZ_MNTTVP HIDDEN ID 25 &
        DISPLAY &
@IF FRANCAIS
        LABEL "T.V.P.........:" &
@ELSE
        LABEL "P.S.T.........:" &
@ENDIF
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

FIELD D_MNTRBS_TVP &
@IF FRANCAIS
        LABEL "C.T.I.:" &
@ELSE
        LABEL "C.T.I.:" &
@ENDIF
        DISPLAY &
        PICTURE "^^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5


;-------------------------------------------------------------------------------
;
;
; Popup sur les codes de taxes fédérales.
;
;
PROCEDURE INPUT T_TAXCLECOD_TPS
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TAXCLETYP = "F"
    LET T_TAXCLECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TAXMODPAT = "@"
    RUN SCREEN mc111 MODE F PASSING T_TAXCLETYP, &
                                    T_TAXCLECOD, &
                                    T_TAXMODPAT
    LET FIELDTEXT = TRUNC(T_TAXCLECOD)
  END
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les codes de taxes provinciales.
;
;
PROCEDURE INPUT T_TAXCLECOD_TVP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TAXCLETYP = "P"
    LET T_TAXCLECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TAXMODPAT = "@"
    RUN SCREEN mc111 MODE F PASSING T_TAXCLETYP, &
                                    T_TAXCLECOD, &
                                    T_TAXMODPAT
    LET FIELDTEXT = TRUNC(T_TAXCLECOD)
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide le nombre de décimal.
;
;
PROCEDURE EDIT T_MNTTST
BEGIN
  USE usvaldec NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE PROCESS T_MNTTST
BEGIN
  LET TZ_MNTTXB = T_MNTTST
  USE ustaxcal NOLIST
  DISPLAY TZ_MNTTPS
  DISPLAY D_MNTRBS_TPS
  DISPLAY TZ_MNTTVP
  DISPLAY D_MNTRBS_TVP
END

BUILD ;
;
;          ***************** Test - Calcul de taxe ******************
;          *                                                        *
;          * Fédéral.......: xx xxxxxxxxxxxxxxxxxxxx                *
;          *                                                        *
;          * Provincial....: xx xxxxxxxxxxxxxxxxxxxx                *
;          *                                                        *
;          * Montant.......: xxxxxxxxxxxxxx                         *
;          *                                                        *
;          * T.P.S.........: xxxxxxxxxxxxxx  C.T.I.: xxxxxxxxxxxxxx *
;          *                                                        *
;          * T.V.P.........: xxxxxxxxxxxxxx  C.T.I.: xxxxxxxxxxxxxx *
;          *                                                        *
;          **********************************************************
