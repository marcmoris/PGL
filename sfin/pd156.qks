;/T/ Unité de travail (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-12
;
;    Description..: Cette table contient les informations de base des
;                   unités de travail.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd156 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD156"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd156.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour l'appel de dépisteur (pop-up)
;

TEMPORARY T_UTRNUMUNITRV CHARACTER SIZE 04
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Unité de travail
;

FILE PDUNITE_TRAVAIL PRIMARY NOITEM

  ACCESS VIA     CIECLE,      &
                 UTRNUMUNITRV &
         USING   T_CIECLEMNU, &
                 UTRNUMUNITRV OF PDUNITE_TRAVAIL &
         REQUEST UTRNUMUNITRV OF PDUNITE_TRAVAIL &
         ORDERBY CIECLE,      &
                 UTRNUMUNITRV

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 UTRNUMUNITRV

          ITEM CIECLE OF PDUNITE_TRAVAIL INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Détail unité de travail
;

FILE PDDET_UNIT_TRAV DETAIL OCCURS 9 &
                     NOITEM

  ACCESS VIA     CIECLE,      &
                 UTRNUMUNITRV &
         USING   T_CIECLEMNU, &
                 UTRNUMUNITRV OF PDUNITE_TRAVAIL &
         ORDERBY CIECLE,       &
                 UTRNUMUNITRV, &
                 DUTCODUNMVEN

  ITEM UTRNUMUNITRV OF PDDET_UNIT_TRAV INITIAL UTRNUMUNITRV OF PDUNITE_TRAVAIL
  ITEM CIECLE OF PDDET_UNIT_TRAV INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Unité de mesure (Vente)
;

FILE PDUNITE_MESURE  REFERENCE ALIAS A_UNM_VEN

  ACCESS VIA   CIECLE,     &
               UNMCODUNM   &
         USING T_CIECLEMNU,&
               DUTCODUNMVEN     OF PDDET_UNIT_TRAV


;-------------------------------------------------------------------------------
;
; Unité de mesure (Production)
;

FILE PDUNITE_MESURE  REFERENCE ALIAS A_UNM_PRD

  ACCESS VIA   CIECLE,     &
               UNMCODUNM   &
         USING T_CIECLEMNU,&
               UTRCODUNMPRD     OF PDUNITE_TRAVAIL


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Unité de travail " &
@ELSE
      " %%%Unité de travail " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD UTRNUMUNITRV OF PDUNITE_TRAVAIL           &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE                       &
        LOOKUP NOTON PDUNITE_TRAVAIL            &
          VIA   CIECLE,                         &
                UTRNUMUNITRV                    &
          USING T_CIECLEMNU,                    &
                UTRNUMUNITRV OF PDUNITE_TRAVAIL &
          MESSAGE 2907 ; Cette unité existe déjà


FIELD UTRDSC001 OF PDUNITE_TRAVAIL         &
        HIDDEN ID 10                       &
        REQUIRED


FIELD UTRDSC002 OF PDUNITE_TRAVAIL         &
        HIDDEN ID 12                       &
        REQUIRED


FIELD UTRCODUNMPRD OF PDUNITE_TRAVAIL      &
        HIDDEN ID 14                       &
        REQUIRED                           &
        LOOKUP ON A_UNM_PRD                &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas


FIELD UTRTMPEST OF PDUNITE_TRAVAIL         &
        HIDDEN ID 16


DRAW 10,2 TO 10,79

SKIP TO LINE 12

TITLE &
@IF FRANCAIS
  "U/M vente   Coûts unitaire  " &
@ELSE
  "%%%U/M vente   Coûts unitaire " &
@ENDIF
      AT ,14


SKIP TO LINE 13


ALIGN (15,16,17) (,,26)


CLUSTER OCCURS WITH PDDET_UNIT_TRAV


FIELD DUTCODUNMVEN     OF PDDET_UNIT_TRAV  &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED NOCHANGE                  &
        LOOKUP NOTON PDDET_UNIT_TRAV       &
          VIA     CIECLE,                  &
                  UTRNUMUNITRV,            &
                  DUTCODUNMVEN             &
          USING   T_CIECLEMNU,             &
                  UTRNUMUNITRV     OF PDUNITE_TRAVAIL, &
                  DUTCODUNMVEN     OF PDDET_UNIT_TRAV  &
          MESSAGE 3030 , & ; Cette unité de mesure existe déjà pour cette unité de travail
               ON A_UNM_VEN                &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas


FIELD DUTCOUUNI        OF PDDET_UNIT_TRAV  &
        REQUIRED


CLUSTER



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (popup) pour le champs
;

PROCEDURE INPUT UTRNUMUNITRV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UTRNUMUNITRV = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd102 MODE F &
                     PASSING T_UTRNUMUNITRV, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UTRNUMUNITRV )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (popup) pour le champs
;

PROCEDURE INPUT DUTCODUNMVEN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (popup) pour le champs
;

PROCEDURE INPUT UTRCODUNMPRD
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Valide le nombre de décimal
;

PROCEDURE EDIT DUTCOUUNI
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec2 NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDUNITE_TRAVAIL &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDUNITE_TRAVAIL &
     AND NOT NEWRECORD     OF PDUNITE_TRAVAIL &
     AND NOT DELETEDRECORD OF PDUNITE_TRAVAIL &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

2xxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
3****************************** Unité de travail *******************************
4                                                                              *
5 Unité travail.: xxx                                                          *
6 Description..F: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
7 Description A.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
8 Utrcodunmprd    xx                                                           *
9*****************xxxxxxx                                                      *
0                                                                              *
*            U/M vente   Coûts unitaire       Hre/std   U/M prod.              *
*                                                                              *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*               xx       xxxxxxxxxxxxxxx                                       *
*                                                                              *
********************************************************************************
@ENDIF
