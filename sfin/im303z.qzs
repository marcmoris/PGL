;/T/ Liste des amortissements effectués
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Alain Côté
;    Date création: 1 mars 1994
;
;    Description..: Ce rapport imprime les amortissements effectués entre
;                   les deux périodes demandées.
;
;    Tris.........: Par ordre de catégorie, par numéro d'immobilisation et
;                   par période.
;
;
;/V3.02.00/ Alain Côté, 1 mars 1994, Immobilisation
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS IMIMMOBILISATION &

  LINK CIECLE OF IMIMMOBILISATION , &
       IMCCLE OF IMIMMOBILISATION   &
    TO CIECLE , &
       IMCCLE OF IMCATEGORIE  &

  LINK CIECLE OF IMIMMOBILISATION , &
       IMMCLE OF IMIMMOBILISATION   &
    TO CIECLE , &
       IMMCLE OF IMIMM_SUIVI

CHOOSE CIECLE PARM , &
       IMCCLE PARM , &
       IMMCLE PARM

DEFINE D_PER_DEB CHARACTER SIZE 4 = PARM
DEFINE D_PER_FIN CHARACTER SIZE 4 = PARM

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF IMIMM_SUIVI &
         IF PECCLE OF IMIMM_SUIVI[1:1] < "8" &
         ELSE "0" + PECCLE OF IMIMM_SUIVI

; Ajustement pour le changement de siècle
DEFINE D_PER_DEB_SIE CHARACTER SIZE 5 = "1" + D_PER_DEB &
         IF D_PER_DEB[1:1] < "8" &
         ELSE "0" + D_PER_DEB

; Ajustement pour le changement de siècle
DEFINE D_PER_FIN_SIE CHARACTER SIZE 5 = "1" + D_PER_FIN &
         IF D_PER_FIN[1:1] < "8" &
         ELSE "0" + D_PER_FIN

SELECT IF     IMITYP OF IMIMM_SUIVI = "AM" &
          AND D_PECCLE_SIE >= D_PER_DEB_SIE &
          AND D_PECCLE_SIE <= D_PER_FIN_SIE


;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des amortissements effectués"
@ELSE
       "%%%Listes des amortissements effectués"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "De la période " + &
       D_PER_DEB[1:2] + "-" + D_PER_DEB[3:2] + &
       " à la période " + &
       D_PER_FIN[1:2] + "-" + D_PER_FIN[3:2]
@ELSE
       "%%%De la période " + &
       D_PER_DEB[1:2] + "-" + D_PER_DEB[3:2] + &
       " à la période " + &
       D_PER_FIN[1:2] + "-" + D_PER_FIN[3:2]
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM303"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE2 CHARACTER SIZE 5 = "1" + PECCLE OF IMIMM_SUIVI &
         IF PECCLE OF IMIMM_SUIVI[1:1] < "8" &
         ELSE "0" + PECCLE OF IMIMM_SUIVI

SORT ON CIECLE OF IMIMMOBILISATION &
     ON IMCCLE OF IMIMMOBILISATION &
     ON IMMCLE OF IMIMMOBILISATION &
     ON D_PECCLE_SIE2


USE ustitstd NOLIST
SKIP 2  &
@IF FRANCAIS
  TAB 001 "Catégorie:" &
  TAB 012 IMCCLE    OF IMIMMOBILISATION &
  TAB 017 IMCDSCFRA OF IMCATEGORIE      &
@ELSE
  TAB 001 "%%%égorie:" &
  TAB 012 IMCCLE    OF IMIMMOBILISATION &
  TAB 017 IMCDSCANG OF IMCATEGORIE      &
@ENDIF
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
"    Numéro                                                  Valeur      Type                 Valeur" &
@ELSE
"%%% Numéro                                                  Valeur      Type                 Valeur" &
@ENDIF
SKIP &
  TAB 001 &
@IF FRANCAIS
"Immobilisation Description                                Acquisition   Amort.   Période   Non amortie    Amortissement" &
@ELSE
"%%%obilisation Description                                Acquisition   Amort.   Période   Non amortie    Amortissement" &
@ENDIF
SKIP &
  TAB 001 &
"-------------- ----------------------------------------  -------------  ------   -------  -------------   -------------"


HEADING AT IMMCLE &
 SKIP 2


REPORT &
  TAB 001 IMMCLE    OF IMIMMOBILISATION &
            PRINT AT IMMCLE &
  TAB 016 IMMDSC    OF IMIMMOBILISATION &
            PRINT AT IMMCLE &
  TAB 058 IMMMNTINI OF IMIMMOBILISATION &
            PRINT AT IMMCLE &
  TAB 075 IMMTYPAMO OF IMIMMOBILISATION &
            PRINT AT IMMCLE &
  TAB 083 PECCLE       OF IMIMM_SUIVI &
  TAB 091 IMIMNTNONAMT OF IMIMM_SUIVI &
  TAB 107 IMIMNTAMT    OF IMIMM_SUIVI &
  RESERV 3 LINES

;
; Total par immobilisation.
;
FOOTING AT IMMCLE &
  TAB 106 "--------------" &
SKIP &
  TAB 106 IMIMNTAMT OF IMIMM_SUIVI &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4


;
; Total par catégorie.
;
FOOTING AT IMCCLE &
  TAB 106 "--------------" &
SKIP &
  TAB 106 IMIMNTAMT OF IMIMM_SUIVI &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
SKIP &
  TAB 106 "==============" &
SKIP PAGE

BUILD im303z
@IF FORMAT
IM303                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                      Liste des amortissements effectués
                                               De la période xx-xx à la période xx-xx

Catégorie: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

    Numéro                                                  Valeur      Type                 Valeur
Immobilisation Description                                Acquisition   Amort.   Période   Non amortie    Amortissement
-------------- ----------------------------------------  -------------  ------   -------  -------------   -------------
xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999,999.99    x       xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                                         --------------
                                                                                                         999,999,999.99

xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999,999.99    x       xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                                         --------------
                                                                                                         999,999,999.99

xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999,999.99    x       xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                  xx-xx   99,999,999.99   99,999,999.99
                                                                                                         --------------
                                                                                                         999,999,999.99
                                                                                                         --------------
                                                                                                         999,999,999.99
                                                                                                         ==============
@ENDIF
