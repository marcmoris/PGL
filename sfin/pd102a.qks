;/T/ Dépisteur Unité de travail avec unité de mesure
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-13
;
;    Description..: Permet de visionner et de choisir une unité de travail.
;
;    Paramètres...: T_UTRNUMUNITRV ==> - code
;                                      - segment de la clé de recherche
;                                      - peut contenir la valeur @
;                                      - contient la valeur de retour.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd102a NOMODE NOACTION FIELDMARK &
              FROM 10,33 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND &
              RECEIVING T_UTRNUMUNITRV, &
                        T_UNMCODUNM,    &
                        T_CIECLEMNU


TEMPORARY T_UTRNUMUNITRV CHARACTER SIZE 04 ; Unité de travail
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04 ; Unité de mesure pour l'unité de travailk
TEMPORARY T_CIECLEMNU    CHARACTER SIZE 04

USE pd102a.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Unité de travail
;

FILE PDDET_UNIT_TRAV PRIMARY &
                     OCCURS 11

  ACCESS VIA     CIECLE,      &
                 UTRNUMUNITRV &
         USING   T_CIECLEMNU, &
                 T_UTRNUMUNITRV &
         ORDERBY CIECLE,        &
                 UTRNUMUNITRV,  &
                 DUTCODUNMVEN


;-------------------------------------------------------------------------------
;
; Unité de travail
;

FILE PDUNITE_TRAVAIL REFERENCE OCCURS WITH PDDET_UNIT_TRAV

  ACCESS VIA     CIECLE,      &
                 UTRNUMUNITRV &
         USING   T_CIECLEMNU, &
                 UTRNUMUNITRV OF PDDET_UNIT_TRAV


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,47

USE ustitoff NOLIST     ; Pour placer le TITLE OFF

SKIP TO 2

ALIGN (3,3,6) (,,10) (,,41) (,,44)

CLUSTER OCCURS WITH PDDET_UNIT_TRAV

FIELD UTRNUMUNITRV OF PDDET_UNIT_TRAV HIDDEN LABEL ">>" DISPLAY
FIELD UTRDSC001    OF PDUNITE_TRAVAIL DISPLAY SIZE 30
FIELD UTRCODUNMPRD OF PDUNITE_TRAVAIL DISPLAY
FIELD DUTCODUNMVEN OF PDDET_UNIT_TRAV DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_UTRNUMUNITRV , "@")
  THEN BEGIN
    LET T_UTRNUMUNITRV = ""
    LET T_UNMCODUNM    = ""
  END
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_UTRNUMUNITRV = UTRNUMUNITRV OF PDDET_UNIT_TRAV
  LET T_UNMCODUNM    = DUTCODUNMVEN OF PDDET_UNIT_TRAV
  RETURN
END

BUILD


@IF FORMAT

                                ***********************************************
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx xx *
                                ***********************************************
@ENDIF
