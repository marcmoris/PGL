;/T/ Réception de commande d'achat
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 28 mars 1994
;
;    Description..: Cet écran permet de saisir les réceptions d'une commande
;                   d'achat.  Chaque réception ne concerne qu'un et un seul
;                   entrepôt.  Cependant, plusieurs réceptions peuvent être
;                   saisies pour une même commande.
;
;/M/ Pascal Tremblay, 98-01-20
;
;    Ajustement pour le changement de siècle
;
;
;/M/ Programmeur.......: Patrick Langlois
;    Date modification.: 09 Juin 1999
;    Description.......: Migration Achat/Réception/Inventaire de l'environnement (BOC)
;
;/M/ Programmeur.......: Francois Richard
;    Date modification.: 24 Aout 1999
;    Description.......: Ajout du numéro d'emballage
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/V/ Écran vérifier pour le passage à l'an 2000
;
;/M/ Programmeur.......: Patrick Langlois
;    Date modification.: 09 Juin 1999
;    Description.......: Imprimer automatiquement (Liste des réceptions détaillés)
;                        lorsque le réception est approuvé.
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin
;    Date..............: 25 février 2000.
;    Description.......: Modifier le libellé "Journalsé le" par "Transférée le".
;
;    Référence.........: Stabilisation du module Achat/Réception Inventaire (point 2).
;
;    Signet............: MP-00-02-23_S02.
;
;    -------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 08 mars 2000.
;    Description.......: Création d'une gestion des acheteurs afin de cibler plus rapidement ceux qui initient les commandes et qui les réceptionnent.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 14.
;    
;    Signet............: MP-00-03-08_S14.
;-----------------------------------------------------------------------------------------------------------------------------------

SCREEN ar009 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             AUTOUPDATE &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM,    &
                       T_PECCLEMNU, &
                       T_FOUCIECLE, &
                       T_RECCLEFND


USE ussectmp NOLIST     ; Variable pour la sécurité
    "AR009"             ; Nom de l'écran

USE ar009.hlp NOLIST ; Use servant à la documentation de l'écran

USE ustraecr NOLIST

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-écrans
@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Détail de réception            " ACTION DESIGNER D001
  MENUITEM LABEL "Désengagements                 " ACTION DESIGNER D002
  MENUITEM LABEL "Imputation                     " ACTION DESIGNER D003
  MENUITEM LABEL "Ecriture comptable             " ACTION DESIGNER D004
  MENUITEM LABEL "Facturation                    " ACTION DESIGNER D005
  MENUITEM LABEL "Vérification                   " ACTION DESIGNER D006
  MENUITEM LABEL "Mémo - achat                   " ACTION DESIGNER D007
  MENUITEM LABEL "Impression détaillée           " ACTION DESIGNER D008
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Détail de réception" ACTION DESIGNER D001
  MENUITEM LABEL "%%%Désengagements     " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Imputation         " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Facturation        " ACTION DESIGNER D004
  MENUITEM LABEL "%%%Ecriture comptable " ACTION DESIGNER D005
  MENUITEM LABEL "%%%Vérification       " ACTION DESIGNER D006
  MENUITEM LABEL "%%%o - achat          " ACTION DESIGNER D007
@ENDIF

;-------------------------------------------------------------------------------
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4  ; Période du menu
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4  ; Compagnie fournisseur
TEMPORARY T_RECCLEFND CHARACTER SIZE 12 ; Numéro réception à chercher.
;-------------------------------------------------------------------------------

FILE ARRECEPTION      PRIMARY NOITEM
  ACCESS VIA   CIECLE,               &
               RECCLE                &
         USING T_CIECLEMNU,          &
               RECCLE OF ARRECEPTION  &
         REQUEST RECCLE    &
         ORDERBY CIECLE,   &
                 RECCLE

  ACCESS VIA   CIECLE,               &
               ENTCLE                &
         USING T_CIECLEMNU,          &
               ENTCLE OF ARRECEPTION  &
         REQUEST ENTCLE  &
         ORDERBY CIECLE, &
                 ENTCLE, &
                 PECCLE, &
                 RECCLE

  ACCESS VIA   CIECLE,               &
               PECCLE,               &
               LARCLE                &
         USING T_CIECLEMNU,          &
               PECCLE OF ARRECEPTION,&
               LARCLE OF ARRECEPTION &
         REQUEST PECCLE, &
                 LARCLE  &
         ORDERBY CIECLE, &
                 ENTCLE, &
                 PECCLE, &
                 LARCLE, &
                 RECCLE

  ACCESS VIA   CIECLE,               &
               PECCLE                &
         USING T_CIECLEMNU,          &
               PECCLE OF ARRECEPTION  &
         REQUEST PECCLE, &
                 LARCLE  &
         ORDERBY CIECLE, &
                 ENTCLE, &
                 PECCLE, &
                 RECCLE


  ACCESS VIA   CIECLE               &
         USING T_CIECLEMNU          &
         ORDERBY CIECLE,   &
                 RECCLE

  ITEM CIECLE    OF ARRECEPTION INITIAL T_CIECLEMNU
  ITEM FOUCIECLE OF ARRECEPTION INITIAL T_FOUCIECLE
  ITEM REFCLETYP OF ARRECEPTION INITIAL "F"
  ITEM RECTYPECR OF ARRECEPTION INITIAL "RC"
  ITEM RECSTU    OF ARRECEPTION INITIAL "P"     ; Initialisation à préliminaire
  ITEM RECDATCRE OF ARRECEPTION INITIAL SYSDATE
  ITEM RECHRECRE OF ARRECEPTION INITIAL SYSTIME / 10000
  ITEM RECUSRCRE OF ARRECEPTION INITIAL TZ_LOGONID
  ITEM RECDATMOD OF ARRECEPTION FINAL   SYSDATE        &
                                        IF NOT NEWRECORD OF ARRECEPTION AND &
                                           ALTEREDRECORD OF ARRECEPTION
  ITEM RECHREMOD OF ARRECEPTION FINAL   SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF ARRECEPTION AND &
                                           ALTEREDRECORD OF ARRECEPTION
  ITEM RECUSRMOD OF ARRECEPTION FINAL   TZ_LOGONID         &
                                        IF NOT NEWRECORD OF ARRECEPTION AND &
                                           ALTEREDRECORD OF ARRECEPTION

;
; Fichier pour mettre le statut de la commande à sélectionnée ou complète
;
FILE ARCOMMANDE  REFERENCE

  ACCESS VIA   CIECLE, &
               CMDCLE, &
               CMDAJT  &
         USING T_CIECLEMNU, &
               CMDCLE OF ARRECEPTION, &
               "000"

;
; Fichier pour déterminer le numéro de séquence de la réception.
;
FILE ARRECEPTION  DESIGNER ALIAS A_REC_NUMSEQ
  ACCESS VIA   CIECLE, &
               CMDCLE  &
         USING CIECLE OF ARRECEPTION, &
               CMDCLE OF ARRECEPTION &
         ORDERBY CIECLE, &
                 RECCLE  DESCENDING



;
; Pour valider que l'entrepôt existe
;
FILE ARENTREPOT  REFERENCE
  ACCESS VIA   CIECLE, &
               ENTCLE  &
         USING CIECLE OF ARRECEPTION, &
               ENTCLE OF ARRECEPTION

;
; Fichier permettant de valider que le lot existe
;
FILE ARLOT  REFERENCE
  ACCESS  VIA   CIECLE, &
                PECCLE, &
                LARCLE &
          USING CIECLE OF ARRECEPTION, &
                PECCLE OF ARRECEPTION, &
                LARCLE OF ARRECEPTION


;
; Permet de valider que les lignes sont complètes ou annulées lorsque la réception est
; complète.
;
FILE ARREC_DETAIL  DESIGNER &
                  OPEN READ SHARE
  ACCESS VIA   CIECLE, &
               RECCLE  &
         USING CIECLE OF ARRECEPTION,  &
               RECCLE OF ARRECEPTION


;
; Fichier d'historique des transactions
;
FILE ARPRD_HISTO  DESIGNER



; Fichier permettant de valider que le fournisseur existe.
;
FILE VFOC_FOU  REFERENCE
  ACCESS VIA   FOUCIECLE, &
               FOUCLE,    &
               FOCCIECLE  &
         USING T_FOUCIECLE,          &
               FOUCLE OF ARRECEPTION, &
               CIECLE OF ARRECEPTION


FILE VPRE_PRD  REFERENCE

  ACCESS VIA   CIECLE, &
               PRDCLE  &
         USING CIECLE OF ARREC_DETAIL, &
               PRDCLE OF ARREC_DETAIL


; Codes bilingues du statut de la réception
;
DEFINE    D_RECSTU CHARACTER SIZE 16 = "RECSTU"
TEMPORARY T_RECSTU CHARACTER SIZE 4
TEMPORARY T_CNSFLD CHARACTER SIZE 1 ; Conserver fieldtext du RECSTU

FILE VCBI_DSC  REFERENCE ALIAS A_CBI_RECSTU
  ACCESS  VIA XELNOM, &
              CBICLE  &
          USING D_RECSTU, &
                RECSTU OF ARRECEPTION

;
; Paramètres pour la compagnie consolidée(holding)
;
FILE MCHOLDING  REFERENCE
  ACCESS VIA CIENMC USING "1"

;
; Fichier pour valider la période
;
FILE MCPERIODE_CIE  DESIGNER &
                   OPEN READ SHARE
  ACCESS VIA   CIECLE, &
               PECCLE &
         USING T_CIECLEMNU, &
               PECCLE OF ARRECEPTION


;
; Fichier pour afficher le nom du fournisseur
;
FILE MCREF_ADRESSE  REFERENCE
  ACCESS VIA   REFCIECLE, &
               REFCLETYP, &
               REFCLENUM, &
               RADCLE     &
         USING FOUCIECLE OF ARCOMMANDE, &
               REFCLETYP OF ARCOMMANDE, &
               FOUCLE    OF ARCOMMANDE, &
               RADCLE    OF ARCOMMANDE


FILE ARPRODUIT           DESIGNER

  SELECT IF ("I" = PRDTYP OF ARPRODUIT OR &
             "N" = PRDTYP OF ARPRODUIT)

FILE ARPRD_ENT           DESIGNER

FILE ARRECEPTION         DESIGNER ALIAS A_ARRECEPTION
  ACCESS VIA   CIECLE &
         USING CIECLE OF ARRECEPTION

;
; Valide l'immobilisation
;
FILE IMIMMOBILISATION REFERENCE
  ACCESS  VIA   CIECLE, &
                IMMCLE &
          USING CIECLE OF ARREC_DETAIL , &
                IMMCLE OF ARREC_DETAIL

;-------------------------------------------------------------------------------
; Table des usagers
;
FILE GSUSAGER DESIGNER &
                   OPEN READ SHARE

;-------------------------------------------------------------------------------
; Table des acheteurs.
;
FILE ARACHETEUR DESIGNER &           ; MP-00-03-08_S14
                   OPEN READ SHARE

;-------------------------------------------------------------------------------

FILE ARCMD_DETAIL  DESIGNER OPEN 2
  ACCESS VIA   CIECLE                , &
               CMDCLE                , &
               CMDAJT                , &
               CDECLE                  &
         USING CIECLE OF ARRECEPTION , &
               CMDCLE OF ARRECEPTION , &
               CMDAJT OF ARREC_DETAIL, &
               REDCLE OF ARREC_DETAIL

;-------------------------------------------------------------------------------
;
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les périodes.
TEMPORARY T_MOD       CHARACTER SIZE 2 ; Code de module.
TEMPORARY T_ENTCLE    CHARACTER SIZE 4 ; Popup sur entrepôts
TEMPORARY T_CMDCLE    CHARACTER SIZE 9 ; Popup sur commandes.
TEMPORARY T_LARCLE    CHARACTER SIZE  4 ;Popup sur les lots
TEMPORARY T_TYPECR    CHARACTER SIZE  2 ; Popup sur les lots
TEMPORARY T_RECSTUOLD CHARACTER SIZE 1  INITIAL "P" ; Ancienne valeur du statut de la réception
TEMPORARY T_NBRREC    CHARACTER SIZE 3  ; Nombre de réceptions

;
; Gestion des mémos.
;
TEMPORARY T_ARMTYPFND   CHARACTER SIZE 1  INITIAL "3"
TEMPORARY T_NUMREFFND   CHARACTER SIZE 12
TEMPORARY T_FOUCLE      CHARACTER SIZE 6
TEMPORARY T_REDQTEREC   FLOAT     SIZE 8
TEMPORARY T_FLAG        CHARACTER SIZE 1
TEMPORARY T_MODIFIER    CHARACTER SIZE 1
TEMPORARY T_OLDRECSTU   CHARACTER SIZE 1

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM)

DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING OR &
             0 = SIZE(TRUNCATE(FOUCLE OF ARRECEPTION)) &
          ELSE "/" + DEVCLE OF VFOC_FOU


;
; Si le lot est autorisé, sert au sous-écran AR009C
;
DEFINE D_LARATRJOU    CHARACTER SIZE 1 = LARATRJOU OF ARLOT


;-------------------------------------------------------------------------------
;
; Clef d'accès pour la consultation des écritures.
;
DEFINE D_HISCLE    INTEGER UNSIGNED SIZE 4 = HISCLE OF ARRECEPTION

;-------------------------------------------------------------------------------
; MP-00-03-08_S14.
; Temporaire associée à la recherche du code d'acheteur.

TEMPORARY T_ACHCLE              CHARACTER SIZE 12   RESET AT STARTUP


;-------------------------------------------------------------------------------
;
; Liste des paramètres pour l'impression
;
DEFINE D_IMPREC CHARACTER SIZE 7   = "AR324A" ; Impression détail réception

DEFINE D_LISPAR CHARACTER SIZE 204 = "F_CIECLEMNU     |F_CIENOM        |" + &
                                     "F_RECCLE        |F_PECCLE        |"
DEFINE D_INDLON CHARACTER SIZE 112 = "001004|005040|045012|057004"
DEFINE D_LISVAL CHARACTER SIZE 300 = DZ_CIECLE &
                                   + DZ_CIENOM &
                                   + RECCLE OF ARRECEPTION &
                                   + PECCLE OF ARRECEPTION

USE usvarprd NOLIST

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tous les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Réception " &
@ELSE
      " %%%Réception " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

;-------------------------------------------------------------------------------

SKIP
ALIGN (3,3,19) (,,24)

FIELD ENTCLE OF ARRECEPTION &
        HIDDEN ID 05            &
        REQUIRED NOCHANGE       &
        LOOKUP ON ARENTREPOT &
          MESSAGE 1337        ; Ce code d'entrepôt n'existe pas.


FIELD ENTNOM OF ARENTREPOT DISPLAY

ALIGN (3,3,19) (,43,59)

FIELD PECCLE OF ARRECEPTION &
        HIDDEN ID 10    &
        NOCHANGE        &
        DEFAULT T_PECCLEMNU &
        PICTURE "^^-^^" &
        LOOKUP ON MCPERIODE_CIE &
          MESSAGE 1307 ; Cette période n'existe pas

FIELD ARRFLGIMP OF ARRECEPTION &
 DISPLAY SIZE 3

ALIGN (3,3,19) (,43,59) (,,61)

FIELD LARCLE OF ARRECEPTION &
        HIDDEN ID 15 &
        NUMERIC &
        SIGNIFICANCE 4 &
        DISPLAY


FIELD RECSTU OF ARRECEPTION &
        NOENTRY          &
        LOOKUP ON A_CBI_RECSTU &
          MESSAGE 1397 ; Ce statut de réception est invalide.


FIELD CBIDSC OF A_CBI_RECSTU DISPLAY  &
        SIZE 15

ALIGN (,43,59)

FIELD RECDATAPP OF ARRECEPTION  FORMAT YYYYMMDD &
        DISPLAY

ALIGN (3,3,19) (,43,59)

FIELD CMDCLE OF ARRECEPTION &
        HIDDEN ID 20 &
        REQUIRED NOCHANGE &
        LOOKUP ON ARCOMMANDE &
          MESSAGE 1399 ; Ce numéro de commande n'existe pas. &


FIELD CMDDAT OF ARCOMMANDE  FORMAT YYYYMMDD &
        DISPLAY &
@IF FRANCAIS
      LABEL "Date commande.:"
@ELSE
      LABEL "%%%Date commande.:"
@ENDIF

ALIGN (3,3,19) (,43,59)

FIELD RECCLE OF ARRECEPTION &
        HIDDEN ID 35 &
        DISPLAY &
        NUMERIC &
        VALUE "1" TO "999999999999" &
        SIGNIFICANCE 12

FIELD RECDAT OF ARRECEPTION  FORMAT YYYYMMDD &
        DEFAULT SYSDATE

ALIGN (,43,59) (,,71)

FIELD RECDATJOU OF ARRECEPTION  &
      FORMAT YYYYMMDD           &
      LABEL "Transférée le.:"   & ; MP-00-02-23_S02
      DISPLAY

FIELD RECHREJOU OF ARRECEPTION &
        DISPLAY

SKIP 1
ALIGN (3,3,19) (,,26)

FIELD FOUCLE  OF ARCOMMANDE &
        HIDDEN ID 40 &
        DISPLAY


FIELD RADNOM OF MCREF_ADRESSE &
        FOR 2,30 &
        DISPLAY

ALIGN (3,3,19)

FIELD RECPAR OF ARRECEPTION &
      HIDDEN ID 45          &
      DOWNSHIFT             ; MP-00-03-08_S14.


ALIGN (3,3,19)(,,31)(,,43)(,,55)

FIELD RECNUMLISEMB OF ARRECEPTION &
      HIDDEN ID 47 &
      LABEL "No emballages.:"

FIELD RECNUMLISEMB2 OF ARRECEPTION 

FIELD RECNUMLISEMB3 OF ARRECEPTION 

FIELD RECNUMLISEMB4 OF ARRECEPTION 


FIELD RECTRP OF ARRECEPTION &
        HIDDEN ID 50

FIELD RECNUMCON OF ARRECEPTION &
        HIDDEN ID 55

FIELD RECCOM OF ARRECEPTION  &
        HIDDEN ID 60


SKIP

ALIGN (3,3,19) (,,34) (,43,59)

FIELD RECMNTTOT OF ARRECEPTION &
         HIDDEN ID 65 &
         DISPLAY PREDISPLAY &
         REFRESH


FIELD D_DEVCLE   DISPLAY PREDISPLAY

FIELD RECMNTNET OF ARRECEPTION &
        DISPLAY PREDISPLAY &
        REFRESH

ALIGN (3,3,19) (,43,59)

FIELD RECMNTTPS OF ARRECEPTION &
        HIDDEN ID 70 &
        DISPLAY PREDISPLAY &
        REFRESH

FIELD RECMNTCTI OF ARRECEPTION &
        DISPLAY PREDISPLAY &
        REFRESH

FIELD RECMNTTVQ OF ARRECEPTION &
        HIDDEN ID 75  &
        DISPLAY PREDISPLAY &
        REFRESH

FIELD RECMNTRTI OF ARRECEPTION &
        DISPLAY PREDISPLAY &
        REFRESH

FIELD RECMNTESC OF ARRECEPTION &
        HIDDEN ID 80 &
        DISPLAY PREDISPLAY &
        REFRESH

FIELD RECNBRITE OF ARRECEPTION &
        DISPLAY PREDISPLAY &
        REFRESH

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
  ;
  ; Si l'écran n'est pas appelé par le menu, alors on interdit la saisie,
  ; la destruction et la modification des données.
  ;
  IF T_RECCLEFND <> ""
  THEN BEGIN
    LET TZ_SECENT = "0"
    LET TZ_SECMOD = "0"
    LET TZ_SECDEL = "0"
  END

END

;-------------------------------------------------------------------------------
; Cette procédure permet de générer le numéro de la réception séquentiellement.
;
PROCEDURE INTERNAL GENERER_NUMERO_RECEPTION
BEGIN

  LET T_NBRREC = "000"

  GET A_REC_NUMSEQ OPTIONAL
  IF ACCESSOK
  THEN LET T_NBRREC = RECCLE OF A_REC_NUMSEQ[10:12]

  LET RECCLE OF ARRECEPTION = CMDCLE OF ARRECEPTION + &
                              ASCII((NCONVERT(T_NBRREC) + 1),3)

END

;-------------------------------------------------------------------------------
; Cette procédure permet de faire un écran dépisteur pour l'entrepôt
;
PROCEDURE INPUT ENTCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ENTCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN ar100 MODE F PASSING T_CIECLEMNU, T_ENTCLE
    LET FIELDTEXT = TRUNCATE(T_ENTCLE)
  END
END


;-------------------------------------------------------------------------------
; Cette procédure permet de faire l'affichage des champs initialisés
; au départ.
;
PROCEDURE PROCESS ENTCLE
BEGIN
  DISPLAY RECSTU    OF ARRECEPTION
  DISPLAY CBIDSC    OF A_CBI_RECSTU
END


;-------------------------------------------------------------------------------
; Ecran dépisteur pour la période comptable.
;
PROCEDURE INPUT PECCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MOD = "AR"
    LET T_PECCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN  mc109 PASSING T_CIECLEMNU, T_PECCLE, T_MOD  MODE F
    LET FIELDTEXT = TRUNCATE(T_PECCLE)
  END
  ;
  ; On force la validation de la période.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(PECCLE OF ARRECEPTION))
  THEN LET FIELDTEXT = PECCLE OF ARRECEPTION
END

;-------------------------------------------------------------------------------
;
;
; Validation du statut de la période.
;
;
PROCEDURE EDIT PECCLE
BEGIN
  ;
  IF PCCSTUAR OF MCPERIODE_CIE = "F"
  THEN ERROR 1309 ; Cette période est fermée.
  ;
  IF PCCSTUAR OF MCPERIODE_CIE <> "C"
  THEN WARNING 1310 ; La période saisie n'est pas la courante.
END



;-------------------------------------------------------------------------------
;
; Consultation & validation du lot
;
;
PROCEDURE INPUT LARCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = PECCLE OF ARRECEPTION
    LET T_TYPECR = "RC"
    LET T_LARCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN ar105 MODE F PASSING T_TYPECR, T_CIECLEMNU, T_PECCLE, T_LARCLE
    LET FIELDTEXT = TRUNCATE(T_LARCLE)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue statut de la réception.
;
;
PROCEDURE INPUT RECSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RECSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_RECSTU, T_RECSTU
    LET FIELDTEXT = TRUNCATE(T_RECSTU)
  END
END

;-------------------------------------------------------------------------------
; Cette procédure permet de valider que le seul statut modifiable est "P" et
; que la seul valeur permise en saisie est "A"
;
PROCEDURE EDIT RECSTU
BEGIN
  IF (CHANGEMODE AND OLDVALUE(RECSTU OF ARRECEPTION) <> "A") OR &
     (CORRECTMODE AND OLDVALUE(RECSTU OF ARRECEPTION) <> "A" AND &
      FIELDTEXT = "A")
  THEN BEGIN
    LET T_MODIFIER = "O"
    LET T_OLDRECSTU = OLDVALUE(RECSTU OF ARRECEPTION)
  END

  IF OLDVALUE (RECSTU OF ARRECEPTION) <> "P"
  THEN ERROR 1398 ; On ne peut pas changer le statut de la réception.

  IF FIELDTEXT <> "A" AND FIELDTEXT <> "P"
  THEN ERROR 1370 ; Cette valeur n'est pas permise.
END


;------------------------------------------------------------------------------
; Cette procédure permet de mettre à jour la date d'approbation de la réception.
;
PROCEDURE PROCESS RECSTU
BEGIN
 IF RECSTU OF ARRECEPTION = "A"
 THEN BEGIN
   LET RECDATAPP OF ARRECEPTION = SYSDATE
   DISPLAY RECDATAPP OF ARRECEPTION
 END
 ;-------------------------------
 ;
 ; Validation de l'IMMOBILISATION
 ;
 IF IMMCLE OF ARREC_DETAIL <> " "
 then BEGIN
   IF IMMTYP OF IMIMMOBILISATION <> "E"
   THEN ERROR = "L'immobilisation " + IMMCLE OF ARREC_DETAIL + "doit etre un équipement."
   IF IMMSTU OF IMIMMOBILISATION <> "A"
   THEN ERROR = "L'immobilisation " + IMMCLE OF ARREC_DETAIL + "doit etre active."
  END
END


;-------------------------------------------------------------------------------
; Cette procédure permet de faire un écran dépisteur pour le numéro de commande.
;
PROCEDURE INPUT CMDCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE = ""
    LET T_ENTCLE = ENTCLE OF ARRECEPTION
    LET T_CMDCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN ar155 MODE F PASSING T_CIECLEMNU, T_ENTCLE, T_CMDCLE, T_FOUCLE
    LET FIELDTEXT = TRUNCATE(ASC(NCO(T_CMDCLE),9))
  END
  ELSE BEGIN
    USE usinpcmd NOLIST
  END
END

;-------------------------------------------------------------------------------
; Cette procédure permet de valider que la commande sélectionnée est approuvée
; ou sélectionnée.
;
PROCEDURE EDIT CMDCLE
BEGIN
  IF CMDSTU OF ARCOMMANDE = "P" 
  THEN ERROR 1400 ; La commande que vous sélectionnée n'a pas le bon statut.

  IF ENTCLE OF ARRECEPTION <> ENTCLE OF ARCOMMANDE
  THEN ERROR 1403 ; L'entrepôt de la commande doit être le même que celui de la
                  ; réception.

END


;-------------------------------------------------------------------------------
; Cette procédure permet de générer le numéro de la réception.
;
PROCEDURE PROCESS CMDCLE
BEGIN
  LET FOUCLE OF ARRECEPTION = FOUCLE OF ARCOMMANDE
  LET RADCLE OF ARRECEPTION = RADCLE OF ARCOMMANDE
END

;-------------------------------------------------------------------------------
;
PROCEDURE INPUT RECPAR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ACHCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN ar159 MODE F PASSING T_ACHCLE
    LET FIELDTEXT = T_ACHCLE
  END

  IF 0<>SIZE(TRUNC(FIELDTEXT)) OR (0=SIZE(TRUNC(FIELDTEXT)) AND RECPAR = ' ')
  THEN BEGIN
    GET ARACHETEUR VIA ACHCLE USING FIELDTEXT OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      LET ACHCLE OF ARRECEPTION = FIELDTEXT

      GET GSUSAGER VIA USRCLE USING FIELDTEXT OPTIONAL
      LET FIELDTEXT = TRUNCATE(USREMPNOM OF GSUSAGER)
    END
    ELSE ERROR 18 ; GS018 *E* Ce code n'existe pas.
  END
  ELSE LET FIELDTEXT = ''
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
; Cette procédure est codée pour permettre l'appel de l'écran de génération
; du détail de la réception (GHOST SCREEN).
;
PROCEDURE ENTRY
BEGIN
  ACCEPT  ENTCLE OF ARRECEPTION
  DISPLAY ENTNOM OF ARENTREPOT

  ACCEPT PECCLE OF ARRECEPTION

  DISPLAY CBIDSC OF A_CBI_RECSTU

  DISPLAY RECDATAPP OF ARRECEPTION
  ACCEPT  CMDCLE    OF ARRECEPTION
  DISPLAY RECCLE    OF ARRECEPTION
  DISPLAY CMDDAT    OF ARCOMMANDE

  ACCEPT  RECDAT    OF ARRECEPTION
  DISPLAY RECDATJOU OF ARRECEPTION
  DISPLAY RECHREJOU OF ARRECEPTION

  DISPLAY FOUCLE    OF ARCOMMANDE
  DISPLAY RADNOM    OF MCREF_ADRESSE
  ACCEPT  RECPAR    OF ARRECEPTION
  ACCEPT RECNUMLISEMB OF ARRECEPTION
  ACCEPT RECNUMLISEMB2 OF ARRECEPTION
  ACCEPT RECNUMLISEMB3 OF ARRECEPTION
  ACCEPT RECNUMLISEMB4 OF ARRECEPTION

  ACCEPT  RECTRP    OF ARRECEPTION
  ACCEPT  RECNUMCON OF ARRECEPTION
  ACCEPT  RECCOM    OF ARRECEPTION

  DISPLAY RECMNTTOT OF ARRECEPTION
  DISPLAY D_DEVCLE
  DISPLAY RECMNTNET OF ARRECEPTION
  DISPLAY RECMNTTPS OF ARRECEPTION
  DISPLAY RECMNTCTI OF ARRECEPTION
  DISPLAY RECMNTTVQ OF ARRECEPTION
  DISPLAY RECMNTRTI OF ARRECEPTION
  DISPLAY RECMNTESC OF ARRECEPTION
  DISPLAY RECNBRITE OF ARRECEPTION

  DO INTERNAL GENERER_NUMERO_RECEPTION

  ; Cet écran permet de générer le détail de la réception automatiquement à
  ; partir de la commande. (GHOST SCREEN)
  ;
  RUN SCREEN ar009e &
        PASSING ARRECEPTION &
        MODE E

  commit


  ; Appel de l'écran de consultation du détail de la réception en mode recherche.
  ;
  RUN SCREEN ar009a &
        PASSING T_CIECLEMNU, &
                T_CIENOM,    &
                T_RECSTUOLD, &
                ARRECEPTION &
        MODE F
END


;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran du détail de la réception.
;
PROCEDURE DESIGNER D001
BEGIN
  IF    RECCLE OF ARRECEPTION = "" &
     OR (    ALTEREDRECORD OF ARRECEPTION &
         AND RECSTU OF ARRECEPTION <> "P")
  THEN ERROR 1390 ; Vous devez faire une mise à jour avant d'accéder le sous-écran.
  ELSE RUN SCREEN ar009a &
               PASSING T_CIECLEMNU, &
                       T_CIENOM,    &
                       T_RECSTUOLD, &
                       ARRECEPTION  &
               MODE SAME
END



;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran de la consultation des désengagements.
;
PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN ar009b &
             PASSING ARRECEPTION  &
             MODE F
END



;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran de l'imputation.
;
PROCEDURE DESIGNER D003
BEGIN
  IF RECSTU OF ARRECEPTION <> "P"
  THEN RUN SCREEN ar009c &
          PASSING T_CIECLEMNU, &
                  T_CIENOM,    &
                  T_RECSTUOLD, &
                  D_LARATRJOU, &
                  ARRECEPTION  &
          MODE F
END

;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran de l'écriture comptable.
;
PROCEDURE DESIGNER D004
BEGIN
  IF RECDATJOU OF ARRECEPTION = 0
  THEN ERROR 1402 ; L'écriture doit être journalisée pour accèder le sous-écran.

  RUN SCREEN mc090 &
             PASSING D_HISCLE  &
             MODE F
END


;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran de facturation
;
PROCEDURE DESIGNER D005
BEGIN
  IF RECSTU OF ARRECEPTION <> "P"
  THEN RUN SCREEN ar009g &
          PASSING T_CIECLEMNU, &
                  T_CIENOM   , &
                  T_FOUCIECLE, &
                  T_PECCLEMNU, &
                  ARRECEPTION  &
          MODE F
END

;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran de la piste de vérification.
;
PROCEDURE DESIGNER D006
BEGIN
  RUN SCREEN ar009d &
             PASSING ARRECEPTION  &
             MODE F
END
;-------------------------------------------------------------------------------
;
; Procédure d'appel du sous-écran des mémos.
;
PROCEDURE DESIGNER D007
BEGIN
  LET T_NUMREFFND = RECCLE OF ARRECEPTION
  LET T_FOUCLE    = FOUCLE OF ARRECEPTION
  RUN SCREEN ar012 MODE F &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_ARMTYPFND, &
                        T_NUMREFFND, &
                        T_FOUCIECLE, &
                        T_FOUCLE
END

;-------------------------------------------------------------------------------
;
; Procédure d'appel de l'impression de la réception
;
PROCEDURE DESIGNER D008
BEGIN
  RUN SCREEN gs092  &
             MODE E &
             PASSING D_IMPREC, &
                     D_LISPAR, &
                     D_INDLON, &
                     D_LISVAL
END

;-------------------------------------------------------------------------------
; Cette procédure permet de mettre à jour l'historique lorsque la réception
; est approuvée.  A noter, qu'un trigger met à jour l'information des quantités
; dans le catalogue fournisseur ainsi que dans le produit entrepôt.
;
PROCEDURE INTERNAL MAJ_HISTORIQUE
BEGIN
  LET CIECLE    OF ARPRD_HISTO = CIECLE OF ARREC_DETAIL
  LET PRDCLE    OF ARPRD_HISTO = PRDCLE OF ARREC_DETAIL
  LET PRDTYP    OF ARPRD_HISTO = PRDTYP OF VPRE_PRD
  LET PRHTIMSTP OF ARPRD_HISTO = SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  LET ENTCLE    OF ARPRD_HISTO = ENTCLE OF ARRECEPTION
  LET PECCLE    OF ARPRD_HISTO = PECCLE OF ARRECEPTION
  LET PRHNUMREF OF ARPRD_HISTO = RECCLE OF ARREC_DETAIL
  LET PRHDATREF OF ARPRD_HISTO = RECDAT OF ARRECEPTION
  LET PRHTYP    OF ARPRD_HISTO = "02"   ; Réception.

  LET PRHQTETRS OF ARPRD_HISTO = REDQTEREC OF ARREC_DETAIL
  LET PRHUSRCRE OF ARPRD_HISTO = TZ_LOGONID
  LET PRHDATCRE OF ARPRD_HISTO = SYSDATE
  LET PRHHRECRE OF ARPRD_HISTO = SYSTIME / 10000

  LET PRHITM     OF ARPRD_HISTO = REDCLE    OF ARREC_DETAIL
  LET PRHFLGCPL  OF ARPRD_HISTO = REDFLGCOM OF ARREC_DETAIL
  LET PRHNUMREF2 OF ARPRD_HISTO = CMDCLE    OF ARRECEPTION + &
                                  CMDAJT    OF ARREC_DETAIL

  IF DEVCLE OF VFOC_FOU <> DEVCLE OF MCHOLDING
  THEN LET PRHDERCOU OF ARPRD_HISTO = ROUND(REDPRIUNI OF ARREC_DETAIL &
                                          * DEVTAU    OF ARCMD_DETAIL)
  ELSE LET PRHDERCOU OF ARPRD_HISTO = REDPRIUNI OF ARREC_DETAIL

  LET PRHNBRJRS  OF ARPRD_HISTO = ( DAYS (RECDAT OF ARRECEPTION) - &
                                    DAYS (CMDDAT OF ARCOMMANDE )) + 1
  LET FOUCIECLE  OF ARPRD_HISTO = FOUCIECLE OF ARRECEPTION
  LET FOUCLE     OF ARPRD_HISTO = FOUCLE    OF ARRECEPTION

  IF DEVCLE OF VFOC_FOU <> DEVCLE OF MCHOLDING
  THEN LET PRHVAL OF ARPRD_HISTO = ROUND((REDMNTNET OF ARREC_DETAIL + &
                                          REDMNTTPS OF ARREC_DETAIL + &
                                          REDMNTTVQ OF ARREC_DETAIL - &
                                          REDMNTRTI OF ARREC_DETAIL - &
                                          REDMNTCTI OF ARREC_DETAIL)  &
                                        * DEVTAU    OF ARCMD_DETAIL)
  ELSE LET PRHVAL OF ARPRD_HISTO =        REDMNTNET OF ARREC_DETAIL + &
                                          REDMNTTPS OF ARREC_DETAIL + &
                                          REDMNTTVQ OF ARREC_DETAIL - &
                                          REDMNTRTI OF ARREC_DETAIL - &
                                          REDMNTCTI OF ARREC_DETAIL

  LET PRHDSC OF ARPRD_HISTO = RADNOM OF MCREF_ADRESSE
  PUT ARPRD_HISTO RESET

END

;-------------------------------------------------------------------------------
; Cette procédure permet de mettre à jour les informations du produit,
; de l'historique etc... suite à l'approbation de la réception.
;
PROCEDURE INTERNAL MAJ_INFORMATIONS_RECEPTION
BEGIN

  WHILE RETRIEVING ARREC_DETAIL
    BEGIN
        ; On n'écrit pas l'historique pour un produit divers ou un produit de
        ; frais.
        ;

      IF    PRDTYP OF VPRE_PRD <> "D" &
        AND PRDTYP OF VPRE_PRD <> "F"
      THEN BEGIN
        GET ARCMD_DETAIL OPTIONAL
        DO INTERNAL MAJ_HISTORIQUE
      END

    END

  ; Cet écran permet de générer l'imputation de la réception automatiquement à
  ; partir de la réception. (GHOST SCREEN)
  ;
  RUN SCREEN ar009f &
        PASSING T_CIECLEMNU, &
                T_CIENOM   , &
                ARRECEPTION &
        MODE E

END

;-------------------------------------------------------------------------------
; La procédure PATH est codée afin de permettre l'appel de cet écran à partir
; de l'historique des produits.
;
PROCEDURE PATH
BEGIN
  IF T_RECCLEFND <> ""
  THEN BEGIN
    IF 12 = SIZE(TRUNCATE(T_RECCLEFND))
    THEN LET PATH = 99 ; L'appel provient de l'historique des produits
    ELSE LET PATH = 98 ; L'appel provient du détail de commande
  END
  ELSE BEGIN

    REQUEST RECCLE OF ARRECEPTION
    IF PROMPTOK
    THEN LET PATH = 1

    IF PATH = 0
    THEN BEGIN
      REQUEST ENTCLE OF ARRECEPTION
      IF PROMPTOK
      THEN LET PATH = 2
    END

    IF PATH = 0
    THEN BEGIN
      REQUEST PECCLE OF ARRECEPTION
      IF PROMPTOK
      THEN BEGIN
        LET PATH = 3
        REQUEST LARCLE OF ARRECEPTION
        IF PROMPTOK
        THEN LET PATH = 4
      END
    END

    IF PATH = 0
    THEN BEGIN
      LET PATH = 5
    END
  END

END

;-------------------------------------------------------------------------------
; La procédure FIND est codée pour les mêmes raisons que la procédure PATH
;
PROCEDURE FIND
BEGIN
  IF PATH = 1
  THEN GET ARRECEPTION &
          VIA   CIECLE , &
                RECCLE   &
          USING T_CIECLEMNU , &
                RECCLE OF ARRECEPTION &
          ORDERBY CIECLE , &
                  RECCLE

  IF PATH = 2
  THEN GET ARRECEPTION &
          VIA   CIECLE , &
                ENTCLE   &
          USING T_CIECLEMNU , &
                ENTCLE OF ARRECEPTION &
          ORDERBY CIECLE , &
                  ENTCLE , &
                  PECCLE , &
                  RECCLE

  IF PATH = 3
  THEN GET ARRECEPTION &
          VIA   CIECLE , &
                PECCLE   &
          USING T_CIECLEMNU , &
                PECCLE OF ARRECEPTION &
          ORDERBY CIECLE , &
                  ENTCLE , &
                  PECCLE , &
                  RECCLE

  IF PATH = 4
  THEN GET ARRECEPTION &
          VIA   CIECLE , &
                PECCLE , &
                LARCLE   &
          USING T_CIECLEMNU , &
                PECCLE OF ARRECEPTION,&
                LARCLE OF ARRECEPTION &
          ORDERBY CIECLE , &
                  ENTCLE , &
                  PECCLE , &
                  LARCLE , &
                  RECCLE

  IF PATH = 5
  THEN GET ARRECEPTION &
          VIA   CIECLE   &
          USING T_CIECLEMNU &
          ORDERBY CIECLE , &
                  RECCLE


  ; L'appel provient du détail de commande
  ;
  IF PATH = 98
  THEN BEGIN
    GET ARRECEPTION &
       VIA   CIECLE, &
             CMDCLE  &
       USING T_CIECLEMNU, &
             T_RECCLEFND
    LET T_RECCLEFND = ""
  END

  ; L'appel provient de l'historique des produits.
  ;
  IF PATH = 99
  THEN BEGIN
    GET ARRECEPTION &
       VIA   CIECLE, &
             RECCLE  &
       USING T_CIECLEMNU, &
             T_RECCLEFND
    LET T_RECCLEFND = ""
  END
END



;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF ARRECEPTION &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF ARRECEPTION &
     AND NOT NEWRECORD     OF ARRECEPTION &
     AND NOT DELETEDRECORD OF ARRECEPTION &
     AND TZ_SECMOD = "0"
  THEN ERROR 2


  ;
  ; L'imputation est non modifiable Si le lot est autorisé.
  ;
  IF     D_LARATRJOU = "1" &
     AND RECDATJOU OF ARRECEPTION = 0
  THEN ERROR 1424 ;Mise à jour non autorisée. Ce lot est journalisé.


  ; Enlever le droit de modification si la réception n'est plus préliminaire.
  ; Cependant, on permet la mise à jour si le statut vient juste d'être changer
  ; pour "A".
  ;
  IF ((    ALTEREDRECORD OF ARRECEPTION   &
       AND NOT NEWRECORD OF ARRECEPTION)  &
       OR DELETEDRECORD OF ARRECEPTION)   &
    AND T_RECSTUOLD  <> "P"
  THEN ERROR 1401 ; Seule la modification d'une réception préliminaire est
                  ; permise.


  IF     NOT NEWRECORD     OF ARRECEPTION &
     AND DELETEDRECORD     OF ARRECEPTION
  THEN BEGIN
    GET ARREC_DETAIL OPTIONAL
    IF ACCESSOK
    THEN ERROR 1468 ; Vous devez d'abord détruire les lignes de détail.
  END
  ; Appel de la procédure de mise à jour des produits entrepôt, du catalogue
  ; du fournisseur et de l'historique des transactions lors de
  ; l'approbation de la réception.
  ;
  IF     ALTEREDRECORD OF ARRECEPTION &
     AND RECSTU OF ARRECEPTION = "A"
  THEN DO INTERNAL MAJ_INFORMATIONS_RECEPTION
END

;-------------------------------------------------------------------------------
; Cette procédure permet de conserver l'ancien statut de la réception pour
; vérifier lors du PREUPDATE si la modification est permise.
;
PROCEDURE POSTFIND
BEGIN
  LET T_RECSTUOLD = RECSTU OF ARRECEPTION
END

PROCEDURE POSTUPDATE
BEGIN
  ;
  ; Impression automatique du détail de la réception lorsque le statut passe à
  ; l'état approuvé
  ;
  IF T_RECSTUOLD = "P" AND RECSTU OF ARRECEPTION = "A"
  THEN BEGIN
    RUN SCREEN gs092  &
               MODE E &
               PASSING D_IMPREC, &
                       D_LISPAR, &
                       D_INDLON, &
                       D_LISVAL
  END
  LET T_RECSTUOLD = RECSTU OF ARRECEPTION
  IF    CORRECTMODE
  THEN BEGIN
    DISPLAY RECCLE OF ARRECEPTION
    WARNING = " "
  END
END

;--------------------------------------------------------------
PROCEDURE OUTPUT ARRFLGIMP
BEGIN
  USE usflgout3 NOLIST ; Pour afficher la valeur Oui/Non avec 3 caract\350res.
END


BUILD
@IF FORMAT
********************************** Réception ***********************************
* Entrepôt......: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Période ......: xxxxx                                                        *
* Lot...........: xxxx                    Statut........: x xxxxxxxxxxxxxxx    *
*                                         Date approb...: xxxxxxxxxx           *
* Commande......: xxxxxxxxx               Date commande.: xxxxxxxxxx           *
* Réception.....: xxxxxxxxxxxx            Date réception: xxxxxxxxxx           *
*                                         Transférée le.: xxxxxxxxxx  xxxxx    *
*                                                                              *
* Fournisseur...: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                        *
*                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                        *
* Reçu par......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* No emballage..: xxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxx               *
* Transporteur..: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Connaissement.: xxxxxxxxxxxxxxxxxxxx                                         *
* Commentaire...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Montant brut..: xxxxxxxxxxxxxxxxxxx     Montant net...: xxxxxxxxxxxxxxx      *
* Montant TPS...: xxxxxxxxxxxxxxx         Montant CTI...: xxxxxxxxxxxxxxx      *
* Montant TVQ...: xxxxxxxxxxxxxxx         Montant RTI...: xxxxxxxxxxxxxxx      *
* Montant esc...: xxxxxxxxxxxxxxx         Nombre d'item.: xxx                  *
********************************************************************************
@ENDIF
