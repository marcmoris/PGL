;/T/ 1.4.5.1 P/O transfert, sous-contractant, achats
;
;/P/ Programmeur...: René Bonneau
;    Date Création.: 1994-10-19
;
;    Description...: Liste de base des bons de commande par commande interne
;
;    Paramètres....: Numéro du P/O (Un, plusieurs ou tous)
;
;    Particularités: AUCUNE
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; Set process nolimit


RUN pd301t


REQUEST BON_COMMANDE
;-------------------
;
; L'access a la table bon de commande se fait selement une fois pour les
; 3 listes de la demande (1.4.5.1 P/O transfert, sous-contractant, achats)
;

ACCESS PDBON_COMMANDE                      &
  LINK CIECLE          OF PDBON_COMMANDE,  &
       PJTABR          OF PDBON_COMMANDE,  &
       PJTNUMSEQ       OF PDBON_COMMANDE  &
    TO CIECLE,                             &
       PJTABR,                             &
       PJTNUMSEQ                           OF  PDPROJET &

  LINK CIECLE          OF PDBON_COMMANDE,  &
       PJTABR          OF PDBON_COMMANDE,  &
       PJTANN          OF PDBON_COMMANDE,  &
       COMNUMCOMINT    OF PDBON_COMMANDE   &
    TO CIECLE,                             &
       PJTABR,                             &
       PJTANN ,                            &
       COMNUMCOMINT OF PDCOMMAN_INTERNE OPTIONAL &

  LINK FOUCLE          OF PDBON_COMMANDE,  &
       FOCCIECLE       OF PDBON_COMMANDE   &
    TO FOUCLE,                             &
       FOCCIECLE                           OF VFOC_FOU         OPTIONAL  &

  LINK FOUCIECLE       OF VFOC_FOU,        &
       FOUREFTYP       OF VFOC_FOU,        &
       FOUCLE          OF VFOC_FOU,        &
       FOCADRACH       OF VFOC_FOU         &
    TO REFCIECLE,                          &
       REFCLETYP,                          &
       REFCLENUM,                          &
       RADCLE                              OF MCREF_ADRESSE    OPTIONAL



CHOOSE CIECLE PARM , BCMNUMBCM PARM


SORT ON BCMNUMBCM        OF PDBON_COMMANDE


SUBFILE WPD301A    KEEP INCLUDE &
  CIECLE           OF PDBON_COMMANDE,   &
  BCMNUMBCM        OF PDBON_COMMANDE,   &
  PJTABR           OF PDBON_COMMANDE,   &
  PJTANN           OF PDBON_COMMANDE,   &
  PJTNUMSEQ        OF PDBON_COMMANDE,   &
  PJTNOM           OF PDPROJET      ,   &
  COMNUMCOMINT     OF PDBON_COMMANDE,   &
  COMNOM           OF PDCOMMAN_INTERNE, &
  BCMCODTYPBCM     OF PDBON_COMMANDE,   &
  BCMTYPBCM        OF PDBON_COMMANDE,   &
  FOUCIECLE        OF PDBON_COMMANDE,   &
  FOUCLE           OF PDBON_COMMANDE,   &
  FOCCIECLE        OF PDBON_COMMANDE,   &
  FOUNOM           OF VFOC_FOU,         &
  RADADR1          OF MCREF_ADRESSE,    &
  RADADR2          OF MCREF_ADRESSE,    &
  RADADR3          OF MCREF_ADRESSE,    &
  RADCP            OF MCREF_ADRESSE,    &
  RADTEL           OF MCREF_ADRESSE,    &
  RADFAX           OF MCREF_ADRESSE,    &
  BCMDSC001        OF PDBON_COMMANDE,   &
  BCMDSC002        OF PDBON_COMMANDE,   &
  BCMPROPOT        OF PDBON_COMMANDE,   &
  BCMFOR           OF PDBON_COMMANDE


;
; Pour obtenir la meme date et heure lors de la première impression
; l'initialisation d'une seule date et heure puis redistribue dans la
; date et heure de première impression et de la date et heure de dernière
; impression.
;

TEMPORARY T_DATE_IMP  DATE
TEMPORARY T_HEURE_IMP INTEGER SIZE 4

  ITEM T_DATE_IMP  = SYSDATE
  ITEM T_HEURE_IMP = SYSTIME / 10000


TEMPORARY T_DATE_IMP_1  DATE
TEMPORARY T_HEURE_IMP_1 INTEGER SIZE 4
TEMPORARY T_DATE_IMP_2  DATE
TEMPORARY T_HEURE_IMP_2 INTEGER SIZE 4

  ITEM T_DATE_IMP_1  = T_DATE_IMP  IF BCMDATIMP = 0 ELSE BCMDATIMP
  ITEM T_HEURE_IMP_1 = T_HEURE_IMP IF BCMDATIMP = 0 ELSE BCMHREIMP
  ITEM T_DATE_IMP_2  = T_DATE_IMP
  ITEM T_HEURE_IMP_2 = T_HEURE_IMP


OUTPUT PDBON_COMMANDE UPDATE
  ITEM BCMDATIMP       = T_DATE_IMP_1
  ITEM BCMHREIMP       = T_HEURE_IMP_1
  ITEM BCMDATDERIMP    = T_DATE_IMP_2
  ITEM BCMHREDERIMP    = T_HEURE_IMP_2



BUILD pd301t
