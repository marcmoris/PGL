;/T/ Périodes comptables - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 11 août 1993
;
;    Description..: Cet écran vérifie si la période reçu en paramètre est
;                   référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc012x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCPERIODE_CTB , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Période comptable.
;
FILE MCPERIODE_CTB    MASTER

;
; Lot des comptes à payer.
;
FILE CPLOT            REFERENCE

;
; Lot des comptes à recevoir.
;
FILE CRLOT            REFERENCE

;
; Lot du Grand Livre.
;
FILE GLLOT            REFERENCE

;
; Ecriture de journal, période de destination.
;
FILE GLECRITURE_JG    REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans le lots.
  ;
  GET CPLOT VIA PECCLE &
                USING PECCLE OF MCPERIODE_CTB &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 169 ; Destruction annulée, cette information est référée par un
                 ; lot.
  ;
  GET CRLOT VIA PECCLE &
                USING PECCLE OF MCPERIODE_CTB &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 169 ; Destruction annulée, cette information est référée par un
                 ; lot.
  ;
  GET GLLOT VIA PECCLE &
                USING PECCLE OF MCPERIODE_CTB &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 169 ; Destruction annulée, cette information est référée par un
                 ; lot.

  ;
  ; On vérifie dans les écritures de journal par la période de destination.
  ;
  GET GLECRITURE_JG VIA ECJPECDES &
                    USING PECCLE OF MCPERIODE_CTB &
                    OPTIONAL
  IF ACCESSOK
  THEN ERROR 170 ; Destruction annulée, cette information est référée par une
                 ; écriture de journal.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
