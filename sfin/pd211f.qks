;/T/ 2.3.2 Enregistrer les diagrammes. (Rejet)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-08-07
;
;    Description..: Le but de cette unité de traitement est de permettre de
;                   consulter les information sur le débitage des diagrammes.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd211f ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              ACTIVITIES FIND               &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD211F"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd211f.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_CAISTU    CHARACTER SIZE 16 = "CAISTU"
TEMPORARY T_CAISTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME MASTER


;-------------------------------------------------------------------------------
;
; Détail rejet
;

FILE PDDET_REJET  PRIMARY &
                  NOITEM &
                  NOAPPEND &
                  NODELETE &
                  OCCURS 11

  ACCESS VIA     CIECLE,          &
                 DIANUM002        &
         USING   T_CIECLEMNU,                         &
                 DIANUM002        OF PDDIAGRAMME      &
         ORDERBY DIANUM002,       &
                 REJNUMREJ

  ITEM CIECLE       OF PDDET_REJET INITIAL T_CIECLEMNU
  ITEM PJTABR       OF PDDET_REJET INITIAL PJTABR OF PDDIAGRAMME
  ITEM DIANUMDIA002 OF PDDET_REJET INITIAL DIANUMDIA002 OF PDDIAGRAMME

;-------------------------------------------------------------------------------
;
; Rejet
;

FILE PDREJET REFERENCE OCCURS WITH PDDET_REJET

  ACCESS VIA    CIECLE,          &
                REJNUMREJ        &
         USING  T_CIECLEMNU,     &
                REJNUMREJ        OF PDREJET


;-------------------------------------------------------------------------------
;
; Code billingue (statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_CAISTU

  ACCESS VIA   XELNOM &
;               CBICLE &
         USING D_CAISTU
;               CAISTU OF PDCAISSON

;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Diagramme " &
@ELSE
      " %%%Diagramme " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD DIANUM002        OF PDDIAGRAMME      &
label "Num. diagramme:"&
        DISPLAY                            &
        FIXED


ALIGN (2,3,19) (27,28,44) (52,53,69)


FIELD DIALON           OF PDDIAGRAMME      &
label "Longueur......:"&
size 4 &
        DISPLAY                            &
        FIXED


FIELD DIAHAU           OF PDDIAGRAMME      &
label "Hauteur.......:"&
size 4 &
        DISPLAY                            &
        FIXED


FIELD DIAEPA           OF PDDIAGRAMME      &
label "Épaisseur.....:"&
size 4 &
        DISPLAY                            &
        FIXED


DRAW 8,2 TO 8,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Rejet " &
@ELSE
      " %%%Rejet " &
@ENDIF
      CENTERED AT ,1



SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      "                       Num. rapp.   Date          Qte " &
@ELSE
      "%%%                    Num. rapp.   Date          Qte " &
@ENDIF
      AT ,2


SKIP TO LINE 11


ALIGN (24,24,25) (,,38) (,,50)


CLUSTER OCCURS WITH PDDET_REJET ID BASE 30


FIELD REJNUMREJ        OF PDDET_REJET      &
        HIDDEN                             &
        LABEL " "                          &
        DISPLAY


FIELD REJDATREJ        OF PDREJET           FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        DISPLAY


FIELD DREQTE           OF PDDET_REJET      &
        DISPLAY


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

;PROCEDURE INITIALIZE
;BEGIN
;  USE ussececr NOLIST
;END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************** Diagramme ***********************************
*                                                                              *
* Num. diagramme: xxxxxxx                                                      *
* Longueur......: xxxx     Hauteur.......: xxxx     Épaisseur.....: xxxx       *
*                                                                              *
******************************** Rejet *****************************************
*                                                                              *
*                       Num. rapp.   Date          Qte                         *
*                       999999999    AA/MM/JJ    99999
*                       999999999    AA/MM/JJ    99999    *
*                       999999999    AA/MM/JJ    99999    *
*                       999999999    AA/MM/JJ    99999    *
*                       999999999    AA/MM/JJ    99999    *
*                       999999999    AA/MM/JJ    99999    *
*                       999999999    AA/MM/JJ    99999    *
*                       999999999    AA/MM/JJ    99999    *
*                                                                              *
********************************************************************************
@ENDIF
