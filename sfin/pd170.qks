;/T/ 5.3 Défaut de tranche (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-18
;
;    Description..: Cette table contient les informations de base des
;                   défaut de tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd170 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD170"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd170.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable servant à l'appel des pop-ups
;

TEMPORARY T_DTRCODDEF CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Défaut de tranche
;

FILE PDDEF_DE_TRANCHE PRIMARY NOITEM

  ACCESS VIA     CIECLE,    &
                 DTRCODDEF  &
         USING   T_CIECLEMNU,                  &
                 DTRCODDEF OF PDDEF_DE_TRANCHE &
         REQUEST DTRCODDEF OF PDDEF_DE_TRANCHE &
         ORDERBY CIECLE,    &
                 DTRCODDEF

  ACCESS VIA     CIECLE        &
         USING   T_CIECLEMNU   &
         ORDERBY CIECLE,       &
                 DTRCODDEF

         ITEM CIECLE OF PDDEF_DE_TRANCHE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Défaut de tranche " &
@ELSE
      " %%%Défaut de tranche " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD DTRCODDEF        OF PDDEF_DE_TRANCHE &
        HIDDEN ID 05                       &
        REQUIRED NOCHANGE                  &
        LOOKUP NOTON PDDEF_DE_TRANCHE      &
          VIA   CIECLE,                    &
                DTRCODDEF                  &
          USING T_CIECLEMNU,               &
                DTRCODDEF OF PDDEF_DE_TRANCHE &
          MESSAGE 2916 ; Ce défaut existe déjà.


FIELD DTRDSCFRA        OF PDDEF_DE_TRANCHE &
        HIDDEN ID 10                       &
        REQUIRED


FIELD DTRDSCANG        OF PDDEF_DE_TRANCHE &
        HIDDEN ID 15                       &
        REQUIRED


FIELD DTREFFNIV        OF PDDEF_DE_TRANCHE &
        HIDDEN ID 20                       &
        REQUIRED                           &
        SIZE 3


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs
;

PROCEDURE INPUT DTRCODDEF
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DTRCODDEF = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd116 MODE F &
                     PASSING T_DTRCODDEF, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DTRCODDEF )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;

PROCEDURE INPUT DTREFFNIV
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT DTREFFNIV
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDEF_DE_TRANCHE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDEF_DE_TRANCHE &
     AND NOT NEWRECORD     OF PDDEF_DE_TRANCHE &
     AND NOT DELETEDRECORD OF PDDEF_DE_TRANCHE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Défaut de tranche *******************************
*                                                                              *
* Code défaut...: xx                                                           *
* Desc. français: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Desc anglais..: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Efface niv....: xxx                                                          *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
