;/P/ Liste des blocs non expédiés par carrière
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Modifié par André Bilodeau 25/04/94
;  - Ajouter date de transport
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme et pour que le rapport soit par type de
;                     granit au lieu de par carriere.
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 04 juillet 2000.
;    Description......: Ajout des paramètres associés à l'entrepôt, à la catégorie, à la couleur et à la qualité de granit.
;
;    Référence........: Demande de changement 000071.
;
;    Signet...........: MP-00-07-04_D71.
;
;    ----------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 12 avril 2001.
;    Description......: Ajustement du rapport afin d'y inclure le code d'entrepôt et sa description. Reformattage du rapport.
;
;    Référence.........: Demande de changement 000134.
;
;    Signet............: MP-01-04-17_D134.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC IN DICT &
 LINK CIECLE OF IFBLOC, &
      ETRCLE OF IFBLOC & 
   TO CIECLE, &
      ETRCLE                  OF IFENTREPOT &

 LINK CIECLE OF IFBLOC, &
      EXNCLE OF IFBLOC & 
   TO CIECLE, &
      EXNCLE                  OF IFEXPEDITION OPTIONAL

CHOOSE CIECLE    PARM, &
       ETRCLE    PARM, & ; MP-00-07-04_D71.
       TGRCODGRA PARM, &
       CATCLE    PARM, & ; MP-00-07-04_D71.
       CLRCLE    PARM, & ; MP-00-07-04_D71.
       QLTCLE    PARM    ; MP-00-07-04_D71.

DEFINE TMP-DAT-FIN DATE = PARM ;---------PROMPT             &
;------------               "ENTRER LA DATE DE FIN   (AAMMJJ): "

; LE SELECT PREND LES ENREGISTREMENTS REPONDANT AUX CRITERES SUIVANT :

; - LE NUMERO D'EXPEDITION DU BLOC=0 OU LA DATE D'EXPEDITION EST
;   PLUS GRANDE QUE LA DATE DE FIN ENTREE
; - LA DATE DU BLOC EST COMPRISE DANS LES LIMITES ENTREES SI IL Y EN A UNE

SELECT IF     (     EXNCLE OF IFBLOC = 0 &
                OR  EXNDAT OF IFEXPEDITION > TMP-DAT-FIN &
              ) &
          AND (    BLCDAT <= TMP-DAT-FIN &
                OR TMP-DAT-FIN = 0 &
              )
;

DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""

DEFINE D_ETRCLE CHARACTER SIZE 4 = RJ(ETRCLE OF IFBLOC)

SORT ON D_ETRCLE  &
                                        ON TGRCODGRA &
     ON D_NUMBLC 

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" IF TMP-DAT-FIN = 0 &
        ELSE "En date du : " + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                               ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                               ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF305"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

USE ustitstd45 NOLIST
  SKIP 001                                                                                                                                      & ; MP-01-04-17_D134.
   TAB 001 "Entrepôt: "                                                                                                                         & ; MP-01-04-17_D134.
   TAB 011 ETRCLE OF IFBLOC                                                                                                                     & ; MP-01-04-17_D134.
   TAB 016 ETRDSCFRA001 OF IFENTREPOT                                                                                                           & ; MP-01-04-17_D134.
  SKIP 002 &
   TAB 001 "TYPE       NO         DATE      DIMENSIONS      TOTAL      T/M      NO      NO     CAT COUL QUAL   DATE    NO    DATE    DATE DE  " & ; MP-01-04-17_D134.
  SKIP 001 &
   TAB 001 "GRAN      BLOC        PROD   LONG  HAUT  LARG    M3                FACT     COM.       GRAN GRAN   DEBUT  EXPED  EXPED   TRANSPORT" & ; MP-01-04-17_D134.
  SKIP 001 &
   TAB 001 "---- -------------- -------- ---------------- --------- --------- ------ --------- --- ---- ---- -------- ----- -------- ---------" & ; MP-01-04-17_D134.
  SKIP 2

HEADING AT D_ETRCLE SKIP PAGE                                      ; MP-01-04-17_D134.

REPORT                                                           & ; MP-01-04-17_D134.
  TAB 001 TGRCODGRA    OF IFBLOC PRINT AT TGRCODGRA              & ; MP-01-04-17_D134.
  TAB 006 D_NUMBLC                                               & ; MP-01-04-17_D134.
  TAB 021 BLCDAT       OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 030 BLCVENDULONG OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 036 BLCVENDUHAU  OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 042 BLCVENDULAR  OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 047 BLCM3TOT     OF IFBLOC PICTURE "^^^^^^.^^"             & ; MP-01-04-17_D134.
  TAB 057 BLCTFINAL    OF IFBLOC PICTURE "^^^^^^.^^"             & ; MP-01-04-17_D134.
  TAB 067 FTRNUMFAC    OF IFBLOC PICTURE "^^^^^^"                & ; MP-01-04-17_D134.
  TAB 074 D_NUMCOM                                               & ; MP-01-04-17_D134.
  TAB 084 CATCLE       OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 088 CLRCLE       OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 093 QLTCLE       OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 098 COEDATDEB    OF IFBLOC                                 & ; MP-01-04-17_D134.
  TAB 107 EXNCLE       OF IFBLOC PICTURE "^^^^^"                 & ; MP-01-04-17_D134.
  TAB 113 EXNDAT       OF IFEXPEDITION                           & ; MP-01-04-17_D134.
  TAB 122 BLCDATREC    OF IFBLOC                                 & ; MP-01-04-17_D134.
 RESERVE 3 LINES                                                   ; MP-01-04-17_D134.

FOOTING AT TGRCODGRA OF IFBLOC                                   & ; MP-01-04-17_D134.
  TAB 005 "______________"                                       & ; MP-01-04-17_D134.
  TAB 047 "_________"                                            & ; MP-01-04-17_D134.
  TAB 057 "_________"                                            & ; MP-01-04-17_D134.
 SKIP 001                                                        & ; MP-01-04-17_D134.
  TAB 006 COUNT "Blocs"                                          & ; MP-01-04-17_D134.
  TAB 035 "Sous-total:"                                          & ; MP-01-04-17_D134.
  TAB 047 BLCM3TOT  SUBTOTAL PICTURE "^^^^^^.^^"                 & ; MP-01-04-17_D134.
  TAB 057 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^"                 & ; MP-01-04-17_D134.
 SKIP 002                                                        

FINAL FOOTING                                                    & ; MP-01-04-17_D134.
 SKIP 001                                                        & ; MP-01-04-17_D134.
  TAB 005 "______________"                                       & ; MP-01-04-17_D134.
  TAB 047 "_________"                                            & ; MP-01-04-17_D134.
  TAB 057 "_________"                                            & ; MP-01-04-17_D134.
 SKIP 001                                                        & ; MP-01-04-17_D134.
  TAB 006 COUNT "Blocs"                                          & ; MP-01-04-17_D134.
  TAB 035 "Total:"                                               & ; MP-01-04-17_D134.
  TAB 047 BLCM3TOT  SUBTOTAL PICTURE "^^^^^^.^^"                 & ; MP-01-04-17_D134.
  TAB 057 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^"                   ; MP-01-04-17_D134.

BUILD  if305z
