;/T/ Gestion des entrepôts
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 25 février 1994
;
;    Description..: Cet écran permet de faire la gestion des entrepôts. Les 
;                   postes comptables définis dans ce panorama serviront de
;                   valeurs par défaut dans les différentes transactions du
;                   module.
;
;-------------------------------------------------------------------------------
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar001 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM 

USE ussectmp NOLIST     ; Variable pour la sécurité
    "AR001"             ; Nom de l'écran

USE ar001.hlp NOLIST  ; Use servant à la documentation de l'écran
                      
USE ustraecr NOLIST

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-écrans

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Localisation                    " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Localisation                 " ACTION DESIGNER D001
@ENDIF

;-------------------------------------------------------------------------------
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;
; Entrepot
;
FILE ARENTREPOT        PRIMARY NOITEM
  ACCESS VIA   CIECLE,               &
               ENTCLE                &   
         USING T_CIECLEMNU,          &
               ENTCLE OF ARENTREPOT  &
         REQUEST ENTCLE    &  
         ORDERBY CIECLE,   &
                 ENTCLE     

  ACCESS VIA   CIECLE               &
         USING T_CIECLEMNU          &
         ORDERBY CIECLE,   &
                 ENTCLE     

  ITEM CIECLE OF ARENTREPOT INITIAL T_CIECLEMNU

;
; Pour détruire les localisations lorsque l'entrepôt est détruite
;
FILE ARENT_LOCAL   DELETE &
                      NOITEM
  ACCESS VIA   CIECLE, &
               ENTCLE  &
         USING CIECLE OF ARENTREPOT, &
               ENTCLE OF ARENTREPOT


;
; Pour valider l'unité d'inventaire
;
FILE MCUNITE_ADM       REFERENCE &
                      ALIAS A_UNA_INV

  ACCESS VIA   CIECLE,  &
               UNACLE   &
         USING CIECLE    OF ARENTREPOT, &
               ENTUNAINV OF ARENTREPOT

;
; Pour valider l'unité couru
;
FILE MCUNITE_ADM       REFERENCE &
                      ALIAS A_UNA_COU

  ACCESS VIA   CIECLE,  &
               UNACLE   &
         USING CIECLE    OF ARENTREPOT, &
               ENTUNACOU OF ARENTREPOT 

;
; Pour valider l'unité d'ajustement
;
FILE MCUNITE_ADM       REFERENCE &
                      ALIAS A_UNA_AJT

  ACCESS VIA   CIECLE,   &
               UNACLE    &
         USING T_CIECLEMNU            , &
               ENTUNAAJT OF ARENTREPOT 

;
; Pour valider l'unité d'entré
;
FILE MCUNITE_ADM       REFERENCE &
                      ALIAS A_UNA_ENT

  ACCESS VIA   CIECLE,   &
               UNACLE    &
         USING T_CIECLEMNU            , &
               ENTUNAENT OF ARENTREPOT 

;
; Pour valider l'unité de sortie
;
FILE MCUNITE_ADM       REFERENCE &
                      ALIAS A_UNA_SOR

  ACCESS VIA   CIECLE,   &
               UNACLE    &
         USING T_CIECLEMNU            , &
               ENTUNASOR OF ARENTREPOT 


;
; Pour valider l'unité de sortie
;
FILE MCUNITE_ADM       REFERENCE &
                      ALIAS A_UNA_IMP

  ACCESS VIA   CIECLE,   &
               UNACLE    &
         USING T_CIECLEMNU            , &
               ENTUNAIMP OF ARENTREPOT 

;
; Pour valider le lien compte / unité
;
FILE MCCHARTE_UNA      DESIGNER 

;
; Pour le dépisteur du compte d'inventaire
;
FILE VCPT_DSC          REFERENCE  &
                      ALIAS A_CPT_INV
  ACCESS VIA   CPTCLE    &
         USING ENTCPTINV OF ARENTREPOT


;
; Pour le dépisteur du compte couru
;
FILE VCPT_DSC          REFERENCE  &
                      ALIAS A_CPT_COU
  ACCESS VIA   CPTCLE    &
         USING ENTCPTCOU OF ARENTREPOT


;
; Pour le dépisteur du compte d'ajustement
;
FILE VCPT_DSC          REFERENCE  &
                      ALIAS A_CPT_AJT
  ACCESS VIA   CPTCLE    &
         USING ENTCPTAJT OF ARENTREPOT

;
; Pour le dépisteur du compte d'entré
;
FILE VCPT_DSC          REFERENCE  &
                      ALIAS A_CPT_ENT
  ACCESS VIA   CPTCLE    &
         USING ENTCPTENT OF ARENTREPOT
;
; Pour le dépisteur du compte de sortie
;
FILE VCPT_DSC          REFERENCE  &
                      ALIAS A_CPT_SOR
  ACCESS VIA   CPTCLE    &
         USING ENTCPTSOR OF ARENTREPOT

;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING         REFERENCE
  ACCESS VIA CIENMC USING "1"

;-------------------------------------------------------------------------------
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes.
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes.
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes.
TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur unité administrative.
TEMPORARY T_FLGDEL    CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tous les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Entrepôts " &
@ELSE
      " %%%Entrepôts" &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

;-------------------------------------------------------------------------------

SKIP
ALIGN (3,3,19)

FIELD ENTCLE OF ARENTREPOT      &
        HIDDEN ID 05            &
        REQUIRED NOCHANGE       &
        LOOKUP NOTON ARENTREPOT &
          VIA   CIECLE, &
                ENTCLE  &
          USING CIECLE OF ARENTREPOT , &
                ENTCLE OF ARENTREPOT   &
          MESSAGE 1311       & ; Ce code d'entrepôt existe déjà
@IF FRANCAIS
        LABEL "Code..........:"
@ELSE
        LABEL "%%%e..........:"
@ENDIF


SKIP
ALIGN (3,3,19)

FIELD ENTNOM OF ARENTREPOT   &
        HIDDEN ID 10

SKIP
ALIGN (3,3,19)

FIELD ENTNOMABR OF ARENTREPOT   &
        HIDDEN ID 15 &
        DEFAULT ENTNOM OF ARENTREPOT

SKIP 1
ALIGN (3,3,19)

FIELD ENTADR1 OF ARENTREPOT   &
        HIDDEN ID 20

SKIP
ALIGN (,,19)

FIELD ENTADR2 OF ARENTREPOT   
FIELD ENTADR3 OF ARENTREPOT   
FIELD ENTADR4 OF ARENTREPOT   

ALIGN (3,3,19)
FIELD ENTCP OF ARENTREPOT     &
        HIDDEN ID 25        

ALIGN (3,3,19) (,40,56)

FIELD ENTTEL OF ARENTREPOT    &
        HIDDEN ID 30

FIELD ENTFAX OF ARENTREPOT    

ALIGN (3,3,19)

FIELD ENTRPS OF ARENTREPOT    &
        HIDDEN ID 40
   
SKIP 1
ALIGN (3,3,19) (,,28) (,53,69)

FIELD ENTCPTINV OF ARENTREPOT       &
        HIDDEN ID 45                &
        REQUIRED                    &
        LOOKUP ON A_CPT_INV    &
          MESSAGE 1313  ; Ce compte n'existe pas. 

FIELD CPTDSCABR OF A_CPT_INV  &
        DISPLAY 

FIELD ENTUNAINV OF ARENTREPOT  &
        REQUIRED               &  
        LOOKUP ON A_UNA_INV &
          MESSAGE 1317   ; Cette unité administrative n'existe pas.

ALIGN (3,3,19) (,,28) (,53,69)
 
FIELD ENTCPTCOU OF ARENTREPOT       &
        HIDDEN ID 55                &
        REQUIRED                    &
        LOOKUP ON A_CPT_COU    &
          MESSAGE 1313  ; Ce compte n'existe pas. 


FIELD CPTDSCABR OF A_CPT_COU  &
        DISPLAY 

FIELD ENTUNACOU OF ARENTREPOT  &
        REQUIRED               &  
        LOOKUP ON A_UNA_COU &
          MESSAGE 1317   ; Cette unité administrative n'existe pas.


SKIP
ALIGN (3,3,19) (,,28) (,53,69)
 
FIELD ENTCPTAJT OF ARENTREPOT       &
        HIDDEN ID 65                &
        REQUIRED                    &
        LOOKUP ON A_CPT_AJT    &
          MESSAGE 1313  ; Ce compte n'existe pas. 


FIELD CPTDSCABR OF A_CPT_AJT  &
        DISPLAY 

FIELD ENTUNAAJT OF ARENTREPOT  &
        REQUIRED               &  
        LOOKUP ON A_UNA_AJT &
          MESSAGE 1317   ; Cette unité administrative n'existe pas.

SKIP
ALIGN (3,3,19) (,,28) (,53,69)
 
FIELD ENTCPTENT OF ARENTREPOT       &
        HIDDEN ID 70                &
        REQUIRED                    &
        LOOKUP ON A_CPT_ENT    &
          MESSAGE 1313  ; Ce compte n'existe pas. 


FIELD CPTDSCABR OF A_CPT_ENT  &
        DISPLAY 

FIELD ENTUNAENT OF ARENTREPOT  &
        REQUIRED               &  
        LOOKUP ON A_UNA_ENT &
          MESSAGE 1317   ; Cette unité administrative n'existe pas.

SKIP
ALIGN (3,3,19) (,,28) (,53,69)
 
FIELD ENTCPTSOR OF ARENTREPOT       &
        HIDDEN ID 75                &
        REQUIRED                    &
        LOOKUP ON A_CPT_SOR    &
          MESSAGE 1313  ; Ce compte n'existe pas. 


FIELD CPTDSCABR OF A_CPT_SOR  &
        DISPLAY 

FIELD ENTUNASOR OF ARENTREPOT  &
        REQUIRED               &  
        LOOKUP ON A_UNA_SOR &
          MESSAGE 1317   ; Cette unité administrative n'existe pas.

SKIP 1
ALIGN (3,3,19) (,,28) 
 
FIELD ENTUNAIMP OF ARENTREPOT  &
        HIDDEN ID 80           &
        REQUIRED               &
        LOOKUP ON A_UNA_IMP &
          MESSAGE 1317   ; Cette unité administrative n'existe pas.

FIELD UNANOMABR OF A_UNA_IMP &
        DISPLAY 


SUBSCREEN ar001a &
            NOMARK   &
            NOID     &
            NOLABEL  &
            AUTO     &
            PASSING ARENTREPOT &
            MODE SAME

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de code postal
;
PROCEDURE INPUT ENTCP
BEGIN
  USE uscpfmt NOLIST  ;Formate le numéro de code postal 
END



;-------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de téléphone
;
PROCEDURE INPUT ENTTEL
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain   
END

;-------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de FAX
;
PROCEDURE INPUT ENTFAX
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone fax nord américain   
END



;-------------------------------------------------------------------------------
; Procédure pour le dépisteur du numéro de compte inventaire
;
PROCEDURE INPUT ENTCPTINV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;  Cette procédure permet de valider que la nature comptable du compte 
;  inventaire est de type actif.
;
PROCEDURE EDIT ENTCPTINV 
BEGIN
  IF    NACCLE OF A_CPT_INV <> 1 &
    AND NACCLE OF A_CPT_INV <> 5
  THEN ERROR 1312  ; La nature comptable du compte inventaire doit être de 
                     ; type actif ou dépense.
END



;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administratives pour le compte inventaire
;
;
PROCEDURE INPUT ENTUNAINV
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = ENTCPTINV OF ARENTREPOT
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
      ELSE RUN SCREEN mc116 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
  ;
  ; Force le lookup de l'unité pour la charte.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(ENTUNAINV OF ARENTREPOT))
  THEN LET FIELDTEXT = ENTUNAINV OF ARENTREPOT

END

;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT ENTUNAINV
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA   CPTCLE , &
                           CIECLE , &
                           UNACLE   &
                     USING ENTCPTINV OF ARENTREPOT , &
                           CIECLE    OF ARENTREPOT , &
                           ENTUNAINV OF ARENTREPOT   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1314 ; La combinaison compte/unité administrative n'existe pas. 
    END
  END
END




;-------------------------------------------------------------------------------
; Procédure pour le dépisteur du numéro de compte couru
;
PROCEDURE INPUT ENTCPTCOU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;  Cette procédure permet de valider que la nature comptable du compte 
;  couru est de type passif.
;
PROCEDURE EDIT ENTCPTCOU 
BEGIN
  IF NACCLE OF A_CPT_COU <> 2
  THEN ERROR 1315    ; La nature comptable du compte couru doit être de 
                     ; type passif
END



;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administratives pour le compte couru
;
;
PROCEDURE INPUT ENTUNACOU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = ENTCPTCOU OF ARENTREPOT
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
      ELSE RUN SCREEN mc116 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
  ;
  ; Force le lookup de l'unité pour la charte.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(ENTUNACOU OF ARENTREPOT))
  THEN LET FIELDTEXT = ENTUNACOU OF ARENTREPOT
END


;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT ENTUNACOU
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA   CPTCLE , &
                           CIECLE , &
                           UNACLE   &
                     USING ENTCPTCOU OF ARENTREPOT , &
                           CIECLE    OF ARENTREPOT , &
                           ENTUNACOU OF ARENTREPOT   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1314 ; La combinaison compte/unité administrative n'existe pas. 
    END
  END
END


;-------------------------------------------------------------------------------
; Procédure pour le dépisteur du numéro de compte ajustement
;
PROCEDURE INPUT ENTCPTAJT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;  Cette procédure permet de valider que la nature comptable du compte 
;  ajustement est de type actif.
;
PROCEDURE EDIT ENTCPTAJT
BEGIN
  IF    NACCLE OF A_CPT_AJT <> 1 &
    AND NACCLE OF A_CPT_AJT <> 5
  THEN ERROR 1316  ; La nature comptable du compte ajustement doit être de 
                     ; type actif ou dépense.
END



;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administratives pour le compte ajustement
;
;
PROCEDURE INPUT ENTUNAAJT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = ENTCPTAJT OF ARENTREPOT
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
      ELSE RUN SCREEN mc116 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
  ;
  ; Force le lookup de l'unité pour la charte.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(ENTUNAAJT OF ARENTREPOT))
  THEN LET FIELDTEXT = ENTUNAAJT OF ARENTREPOT

END

;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT ENTUNAAJT
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA   CPTCLE , &
                           CIECLE , &
                           UNACLE   &
                     USING ENTCPTAJT OF ARENTREPOT , &
                           CIECLE    OF ARENTREPOT , &
                           ENTUNAAJT OF ARENTREPOT   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1314 ; La combinaison compte/unité administrative n'existe pas. 
    END
  END
END


;-------------------------------------------------------------------------------
; Procédure pour le dépisteur du numéro de compte d'entré
;
PROCEDURE INPUT ENTCPTENT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administratives pour le compte d'entré
;
;
PROCEDURE INPUT ENTUNAENT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = ENTCPTENT OF ARENTREPOT
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
      ELSE RUN SCREEN mc116 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
  ;
  ; Force le lookup de l'unité pour la charte.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(ENTUNAENT OF ARENTREPOT))
  THEN LET FIELDTEXT = ENTUNAENT OF ARENTREPOT

END

;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT ENTUNAENT
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA   CPTCLE , &
                           CIECLE , &
                           UNACLE   &
                     USING ENTCPTENT OF ARENTREPOT , &
                           CIECLE    OF ARENTREPOT , &
                           ENTUNAENT OF ARENTREPOT   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1314 ; La combinaison compte/unité administrative n'existe pas. 
    END
  END
END


;-------------------------------------------------------------------------------
; Procédure pour le dépisteur du numéro de compte de sortie
;
PROCEDURE INPUT ENTCPTSOR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
; Popup sur les unités administratives pour le compte de sorite
;
PROCEDURE INPUT ENTUNASOR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = ENTCPTSOR OF ARENTREPOT
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
      ELSE RUN SCREEN mc116 MODE F PASSING T_CIECLEMNU, T_CPTCLE, T_UNACLE, &
                                           T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
  ;
  ; Force le lookup de l'unité pour la charte.
  ;
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(ENTUNASOR OF ARENTREPOT))
  THEN LET FIELDTEXT = ENTUNASOR OF ARENTREPOT

END

;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT ENTUNASOR
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA   CPTCLE , &
                           CIECLE , &
                           UNACLE   &
                     USING ENTCPTSOR OF ARENTREPOT , &
                           CIECLE    OF ARENTREPOT , &
                           ENTUNASOR OF ARENTREPOT   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1314 ; La combinaison compte/unité administrative n'existe pas. 
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administratives pour l'imputation
;
;
PROCEDURE INPUT ENTUNAIMP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
END

;-------------------------------------------------------------------------------
; Procédure d'appel du sous-écran des localisations de l'entrepôt
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN ar001a &
             PASSING ARENTREPOT  &
             MODE SAME
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF ARENTREPOT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF ARENTREPOT &
     AND NOT NEWRECORD     OF ARENTREPOT &
     AND NOT DELETEDRECORD OF ARENTREPOT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Destruction d'un entrepôt
  ;
  IF DELETEDRECORD OF ARENTREPOT
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si l'entrepôt est utilisé
    ;
    RUN SCREEN ar001x MODE SAME &
                      PASSING ARENTREPOT, &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END 
END

BUILD list detail


@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Entrepôts***********************************
* Code..........: xxxx                                                         *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Code postal...: xxxxxxx                                                      *
* Téléphone.....: xxxxxxxxxxxxx        Fax...........: xxxxxxxxxxxxx           *
* Responsable...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Cpt inventaire: xxxxxx   xxxxxxxxxxxxxxxxxxxx     Unité.........: xxxxxxxx   *
* Cpt couru.....: xxxxxx   xxxxxxxxxxxxxxxxxxxx     Unité.........: xxxxxxxx   *
* Cpt ajustement: xxxxxx   xxxxxxxxxxxxxxxxxxxx     Unité.........: xxxxxxxx   *
* Cpt. entré....: xxxxxx   xxxxxxxxxxxxxxxxxxxx     Unité.........: xxxxxxxx   *
* Cpt. sortie...: xxxxxx   xxxxxxxxxxxxxxxxxxxx     Unité.........: xxxxxxxx   *
*                                                                              *
* Unité imputat.: xxxxxxxx xxxxxxxxxxxxxxxxxxxx                                *
********************************************************************************
@ENDIF
