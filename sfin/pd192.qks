;/T/ Gérer les sorties de produits.
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1994-10-20
;
;    Description..: Cette écran permet de faire la gestion des produits qui
;                   sortent de l'usine sans utiliser le processus normal.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd192 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ_SORTIE READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_SORTIE IN DICT


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

; Variables temporaire pour la sécurité
;
; Description: Déclaration des variables temporaire pour la gestion
;              de la sécurité-prosig.
;
; Utilisation: Doit être utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la définition du premier fichier.
;
;------------------------------------------------------------------------------
;
; Accès au programme.
;
TEMPORARY TZ_SECACC CHARACTER SIZE 1 RESET AT STARTUP
;
; Saisie de données(Entry).
;
TEMPORARY TZ_SECENT CHARACTER SIZE 1 RESET AT STARTUP
;
; Destruction.(Delete)
;
TEMPORARY TZ_SECDEL CHARACTER SIZE 1 RESET AT STARTUP
;
; Modification.(Change mode)
;
TEMPORARY TZ_SECMOD CHARACTER SIZE 1 RESET AT STARTUP
;
; Variable pour l'élément de la sécurité.
;
TEMPORARY TZ_XELNOM CHARACTER SIZE 16 RESET AT STARTUP
TEMPORARY TZ_FLGAUT CHARACTER SIZE  1 RESET AT STARTUP
;
; Nom de l'écran pour retrouver la sécurité.
;
TEMPORARY TZ_NOMECR CHARACTER SIZE 7 RESET AT STARTUP INIT &
;
;
;**FIN USSECTMP
    "PD192"

TEMPORARY T_SOUS      CHAR SIZE 25
TEMPORARY T_USINE_SEC CHAR SIZE 01
TEMPORARY T_FOUCLE    CHAR SIZE 06
;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

Description of screen &
@IF FRANCAIS
"                                                            " &
"     Cet écran permet gérer les produits qui sortent de     " &
"  l'usine sans utiliser le processus normal de facturation. " &
"                                                            "
@ELSE
"                                                            " &
"     This screen permits ....                               " &
@ENDIF


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

; ACTIONBAR
;
; Description: Définition du menu déroulant standard situé dans le haut de
;              l'écran.
;
; Utilisation: Il faut l'utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la déclaration des fichiers.
;
;
;
@IF FRANCAIS
ACTIONMENU LABEL "Rech"   ACTION FIND
ACTIONMENU LABEL "MAJ"    ACTION UPDATE
ACTIONMENU LABEL "Entrée" ACTION ENTRY
ACTIONMENU LABEL "Ajout"  ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Supp"   ACTION DELETE
@ELSE
ACTIONMENU LABEL "Find"   ACTION FIND
ACTIONMENU LABEL "Update" ACTION UPDATE
ACTIONMENU LABEL "Entry"  ACTION ENTRY
ACTIONMENU LABEL "Append" ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Del"    ACTION DELETE
@ENDIF

@IF VAXVMS AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Ecran      <PF1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Champ          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Champ dét. <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Recherche     <rech>" ACTION FIND
  MENUITEM LABEL "MAJ            <PF3>" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour <PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde      <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entrée         <ins>" ACTION ENTRY
  MENUITEM LABEL "Ajout      <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Sélection   <sélect>" ACTION SELECT
  MENUITEM LABEL "Supp.    <eff.texte>" ACTION DELETE
  MENUITEM LABEL "Supp.Lig  <PF1 eff.>" ACTION DELETE MARK
  MENUITEM LABEL "Edition         <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant  <page suiv>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.    <PF4>" ACTION RETURN
  MENUITEM LABEL "Sortie         <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "Impression     <F20>" ACTION LIST
  MENUITEM LABEL "Système       <exec>" ACTION SYSTEM
@ENDIF

@IF VAXVMS AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Screen     <pf1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Field          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Ext. Field <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Find Mode     <find>" ACTION FIND
  MENUITEM LABEL "Update         <PF3>" ACTION UPDATE
  MENUITEM LABEL "Update Ret.<PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay    <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode     <ins>" ACTION ENTRY
  MENUITEM LABEL "Append     <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Selection   <select>" ACTION SELECT
  MENUITEM LABEL "Delete      <remove>" ACTION DELETE
  MENUITEM LABEL "Del. Line <PF1 rem.>" ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Next     <next page>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen   <pf4>" ACTION RETURN
  MENUITEM LABEL "Quick Exit     <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "List           <F20>" ACTION LIST
  MENUITEM LABEL "System          <Do>" ACTION SYSTEM
@ENDIF

@IF UNIX AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Aide            F1_H" ACTION HELP
  MENUITEM LABEL "Aide dét.       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Recherche      F1_F7" ACTION FIND
  MENUITEM LABEL "MAJ               F5" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour     F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde           " ACTION UPDATE STAY
  MENUITEM LABEL "Entrée            F7" ACTION ENTRY
  MENUITEM LABEL "Ajout               " ACTION APPEND
  MENUITEM LABEL "Sélection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Supp.             F2" ACTION DELETE
  MENUITEM LABEL "Supp.Lig            " ACTION DELETE MARK
  MENUITEM LABEL "Edition         F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant       <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.       F8" ACTION RETURN
  MENUITEM LABEL "Sortie         F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "Impression écran  F3" ACTION LIST
@ENDIF

@IF UNIX AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Help            F1_H" ACTION HELP
  MENUITEM LABEL "Ext. Help       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Find mode      F1_F7" ACTION FIND
  MENUITEM LABEL "Update            F5" ACTION UPDATE
  MENUITEM LABEL "Update Return  F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay         " ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode        F7" ACTION ENTRY
  MENUITEM LABEL "Append              " ACTION APPEND
  MENUITEM LABEL "Selection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Delete            F2" ACTION DELETE
  MENUITEM LABEL "Del line            " ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Next          <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen      f8" ACTION RETURN
  MENUITEM LABEL "Quick Exit     F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "List              F3" ACTION LIST
@ENDIF
;
;
;**FIN USACTECR


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Blocs                    " ACTION DESIGNER D001
  MENUITEM LABEL "Tranches                 " ACTION DESIGNER D002
  MENUITEM LABEL "Diagrammes               " ACTION DESIGNER D003
  MENUITEM LABEL "Produits de dimension    " ACTION DESIGNER D004
  MENUITEM LABEL "Vérification             " ACTION DESIGNER D005
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Blocs                 " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Tranches              " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Diagrammes            " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Produits de dimension " ACTION DESIGNER D004
  MENUITEM LABEL "%%%Vérification          " ACTION DESIGNER D005
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Clés de recherche pour les popup de consultations et les sous-écrans
;

TEMPORARY T_RSOCODSOR CHARACTER SIZE 5
TEMPORARY T_DETAIL CHARACTER SIZE 1

;-------------------------------------------------------------------------------
;
; Sortie de produit
;

FILE PDSORTIE_PRODUIT PRIMARY                          &
                      NOITEM

  ACCESS VIA     CIECLE,                               &
                 SPRNUMSOR                             &
         USING   T_CIECLEMNU,                          &
                 SPRNUMSOR        OF PDSORTIE_PRODUIT  &
         REQUEST SPRNUMSOR        OF PDSORTIE_PRODUIT  &
         ORDERBY CIECLE,                               &
                 SPRNUMSOR

  ACCESS VIA     CIECLE                                &
         USING   T_CIECLEMNU                           &
         ORDERBY CIECLE,                               &
                 SPRNUMSOR

  ITEM CIECLE       OF PDSORTIE_PRODUIT INITIAL T_CIECLEMNU
  ITEM SPRDATCRE    OF PDSORTIE_PRODUIT INITIAL SYSDATE
  ITEM SPRHRECRE    OF PDSORTIE_PRODUIT INITIAL SYSTIME / 10000
  ITEM SPRUSRCRE    OF PDSORTIE_PRODUIT INITIAL LOGONID
  ITEM SPRDATMAJ    OF PDSORTIE_PRODUIT FINAL   SYSDATE &
                       IF NOT NEWRECORD OF PDSORTIE_PRODUIT AND &
                            ALTEREDRECORD     OF PDSORTIE_PRODUIT
  ITEM SPRHREMAJ    OF PDSORTIE_PRODUIT FINAL   SYSTIME / 10000 &
                       IF NOT NEWRECORD OF PDSORTIE_PRODUIT AND &
                            ALTEREDRECORD     OF PDSORTIE_PRODUIT
  ITEM SPRUSRMAJ    OF PDSORTIE_PRODUIT FINAL   LOGONID &
                       IF NOT NEWRECORD OF PDSORTIE_PRODUIT AND &
                            ALTEREDRECORD     OF PDSORTIE_PRODUIT


;-------------------------------------------------------------------------------
;
; Raison de sortie
;

FILE PDRAISON_SORTIE  REFERENCE
  ACCESS VIA    CIECLE,                            &
                RSOCODSOR                          &
         USING  T_CIECLEMNU,                       &
                RSOCODSOR      OF PDSORTIE_PRODUIT


;-------------------------------------------------------------------------------
FILE VFOC_FOU REFERENCE

  ACCESS VIA     FOCCIECLE,                   &
                 FOUCLE                       &
         USING   T_CIECLEMNU,                 &
                 FOUCLE OF PDSORTIE_PRODUIT
;-------------------------------------------------------------------------------

FILE PDSORTIE_BLOCS DESIGNER
  ACCESS VIA   CIECLE,      &
               SPRNUMSOR    &
         USING T_CIECLEMNU, &
               SPRNUMSOR OF PDSORTIE_PRODUIT

;-------------------------------------------------------------------------------
FILE PDSORTIE_TRANCHE DESIGNER
  ACCESS VIA   CIECLE,      &
               SPRNUMSOR    &
         USING T_CIECLEMNU, &
               SPRNUMSOR OF PDSORTIE_PRODUIT

;-------------------------------------------------------------------------------
FILE PDSORTIE_DIAGRAM DESIGNER
  ACCESS VIA   CIECLE,      &
               SPRNUMSOR    &
         USING T_CIECLEMNU, &
               SPRNUMSOR OF PDSORTIE_PRODUIT

;-------------------------------------------------------------------------------
FILE PDSOR_PROD_DIM DESIGNER
  ACCESS VIA   CIECLE,      &
               SPRNUMSOR    &
         USING T_CIECLEMNU, &
               SPRNUMSOR OF PDSORTIE_PRODUIT

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les bons de commande
;

FILE PDSEQ_SORTIE     DESIGNER TRANSACTION NO_SEQ_SORTIE

;-------------------------------------------------------------------------------
;
;
; DRAW des écrans pleine page.
;
; Description: Ce USE sert à dessiner le cadre des écrans pleine page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD>
;
;
DRAW 3,01 TO 23,80
;
;
;**FIN USDRWECR
; En-tête standard des écrans pleine page.
;
; Description: Ce USE sert à afficher l'en-tête standard des écrans pleine
;              page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD.
;
;
SKIP TO LINE 2

@IF VAXVMS
HILITE DISPLAY HALFTONE
@ENDIF

ALIGN(,,1) (,,21)

FIELD DZ_CIECLE FIXED
FIELD DZ_CIENOM FIXED

;
;
;**FIN USTITSTD
; HILITE standard de tous les écrans
;
; Description: Définition du HILITE standard de tous les écrans, menu
;              écrans pleine page, popup...
;
; Utilisation: Doit être utilisé avant la première énoncé FIELD.
;
;
@IF VAXVMS
HILITE ACTIONBAR     INVERSE                     , &
       ACTIONBARMARK HALFTONE INVERSE            , &
       FIELDMARK     INVERSE                     , &
       MENUMARK      HALFTONE INVERSE            , &
       MODE          HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       HALFTONE UNDERLINE          , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE HALFTONE AUDIBLE    , &
       ERROR         INVERSE HALFTONE AUDIBLE    , &
       SEVERE        INVERSE HALFTONE AUDIBLE BLINKING
@ELSE
HILITE ACTIONBAR     HALFTONE INVERSE            , &
       ACTIONBARMARK INVERSE                     , &
       FIELDMARK     INVERSE HALFTONE            , &
       MENUMARK      INVERSE HALFTONE            , &
       MODE          HALFTONE                    , &
       LABEL         HALFTONE                    , &
       LINEDRAWING   HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       UNDERLINE                   , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE AUDIBLE             , &
       ERROR         INVERSE AUDIBLE             , &
       SEVERE        INVERSE AUDIBLE BLINKING
@ENDIF
;
;**FIN USHILITE

SKIP

TITLE &
@IF FRANCAIS
      " Sortie de produits " &
@ELSE
      " %%%Sortie de produits " &
@ENDIF
      CENTERED AT ,1

; HILITE TITLE OFF
;
; Description: Sert à désactiver l'option HILITE pour toutes les
;              énoncés TITLE qui suit ce USE. Seul le titre principal
;              a un HILITE particulier.
;
; Utilisation: Doit être utilisé après le titre principal de l'écran.
;
;
HILITE TITLE OFF
;
;
;**FIN USHILITE


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD SPRNUMSOR        OF PDSORTIE_PRODUIT        &
label "No sortie.....:"&
        HIDDEN ID 05                              &
        DISPLAY                                   &
        REFRESH


SKIP 1


ALIGN (2,3,19) (24,25,25)


FIELD RSOCODSOR        OF PDSORTIE_PRODUIT        &
label "Code sortie...:"&
        HIDDEN ID 15                              &
        REQUIRED                                  &
        LOOKUP ON PDRAISON_SORTIE                 &
           MESSAGE 2999 ; Cette raison de sortie n'existe pas


FIELD RSODSC001        OF PDRAISON_SORTIE          &
        HIDDEN ID 20                               &
        DISPLAY                                    &
        NOLABEL


ALIGN (2,3,19)


FIELD SPRDMR           OF PDSORTIE_PRODUIT         &
label "Demandeur.....:"&
        HIDDEN ID 25                               &
        REQUIRED


FIELD SPRDSC001        OF PDSORTIE_PRODUIT         &
label "Description...:"&
        HIDDEN ID 30                               &
        REQUIRED


FIELD SPRCON           OF PDSORTIE_PRODUIT         &
label "Confirmé......:"&
        HIDDEN ID 35                               &
        NOENTRY

FIELD T_SOUS  &
      HIDDEN ID 37 &
      LABEL "Type de produit" &
      DISPLAY

ALIGN (2,3,19) (27,,28)

FIELD FOUCLE  OF PDSORTIE_PRODUIT   &
label "Fournisseur..:"&
      HIDDEN  ID 39                 &
      REQUIRED                      &
        LOOKUP ON VFOC_FOU &
          MESSAGE 2917 ; Ce fournisseur n'existe pas



FIELD FOUNOM OF VFOC_FOU &
      HIDDEN ID 40 &
      DISPLAY

SKIP TO LINE 5


ALIGN (,40,58)


FIELD SPRDATSOR        OF PDSORTIE_PRODUIT         FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date sortie...:"&
        HIDDEN ID 45


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

; Appel de l'écran de sécurité.
;
; Description: Appel de l'écran de sécurité du module Gestion de système.
;              Les variables temporaires utilisées proviennent du USEFILE
;              USSECTMP.
;
; Utilisation: Doit être inséré dans la procédure INITIALIZE de l'écran.
;
;
RUN SCREEN gs000 PASSING TZ_NOMECR, & ; Nom de l'écran
                         TZ_SECACC, & ; Accès
                         TZ_SECENT, & ; Entrée
                         TZ_SECMOD, & ; Modification
                         TZ_SECDEL    ; Destruction

IF "3" = TZ_SECACC
THEN ERROR 6 ; L'usager n'est pas défini au système.

IF "2" = TZ_SECACC
THEN ERROR 7 ; Votre accès au système est expiré.

IF "0" = TZ_SECACC
THEN ERROR 8 ; Vous n'avez pas accès à cet option.
;
;
;**FIN USSECECR

END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

; Sécurité pour le mode ENTRY.
;
; Description: Valide si l'usager peut faire de la saisie dans cette
;              écran.
;
; Utilisation: Peut être utiliser dans la procédure INPUT du premier
;              champ saisie à l'écran, ou dans la procédure ENTRY
;              si celle-ci a été altérée.
;
;
IF     ENTRYMODE       &
   AND TZ_SECENT = "0"
THEN SEVERE 16 ; Vous ne pouvez pas saisir de données.
;
;
;**FIN USSECENT

  LET     SPRDATSOR    OF PDSORTIE_PRODUIT = SYSDATE
  DISPLAY SPRDATSOR    OF PDSORTIE_PRODUIT

END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour les codes de raisons.
;

PROCEDURE INPUT RSOCODSOR
BEGIN

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RSOCODSOR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd105 MODE F &
                     PASSING T_RSOCODSOR,&
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RSOCODSOR )
  END

END


;-------------------------------------------------------------------------------
;
; Affectation de l'indicateur confirmé ou non
;

PROCEDURE PROCESS SPRDSC001
BEGIN
  IF ENTRYMODE
  THEN BEGIN
    LET     SPRCON         OF PDSORTIE_PRODUIT = "N"
    DISPLAY SPRCON         OF PDSORTIE_PRODUIT
  END

END


;-------------------------------------------------------------------------------
;
; Saisie des blocs a sortir
;

PROCEDURE DESIGNER D001
BEGIN
  IF 0 = SPRNUMSOR        OF PDSORTIE_PRODUIT
  THEN ERROR "Vous devez faire M.A.J. avant d'aller au sous-écran"
  DISPLAY SPRNUMSOR
  RUN SCREEN  pd192a &
              PASSING PDSORTIE_PRODUIT, &
                      T_CIECLEMNU       &
              MODE SAME

END


;-------------------------------------------------------------------------------
;
; Saisie des tranches a sortir
;

PROCEDURE DESIGNER D002
BEGIN
  IF 0 = SPRNUMSOR        OF PDSORTIE_PRODUIT
  THEN ERROR "Vous devez faire M.A.J. avant d'aller au sous-écran"
  DISPLAY SPRNUMSOR
  RUN SCREEN  pd192b &
              PASSING PDSORTIE_PRODUIT, &
                      T_CIECLEMNU       &
              MODE SAME

END


;-------------------------------------------------------------------------------
;
; Saisie des diagrammes a sortir
;

PROCEDURE DESIGNER D003
BEGIN
  IF 0 = SPRNUMSOR        OF PDSORTIE_PRODUIT
  THEN ERROR "Vous devez faire M.A.J. avant d'aller au sous-écran"
  DISPLAY SPRNUMSOR
  RUN SCREEN  pd192c &
              PASSING PDSORTIE_PRODUIT, &
                      T_CIECLEMNU       &
              MODE SAME

END


;-------------------------------------------------------------------------------
;
; Saisie des produits de dimension a sortir
;

PROCEDURE DESIGNER D004
BEGIN
  IF 0 = SPRNUMSOR        OF PDSORTIE_PRODUIT
  THEN ERROR "Vous devez faire M.A.J. avant d'aller au sous-écran"
  DISPLAY SPRNUMSOR
  RUN SCREEN  pd192d &
              PASSING PDSORTIE_PRODUIT, &
                      T_CIECLEMNU       &
              MODE SAME

END


;-------------------------------------------------------------------------------
;
; Vérification
;

PROCEDURE DESIGNER D005
BEGIN

  RUN SCREEN  pd192e &
              PASSING PDSORTIE_PRODUIT, &
                      T_CIECLEMNU,      &
                      T_CIENOM          &
              MODE SAME

END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDSORTIE_PRODUIT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDSORTIE_PRODUIT &
     AND NOT NEWRECORD     OF PDSORTIE_PRODUIT &
     AND NOT DELETEDRECORD OF PDSORTIE_PRODUIT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;-----------------------------------------------------------------------------
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = SPRNUMSOR        OF PDSORTIE_PRODUIT
  THEN BEGIN
    START TRANSACTION NO_SEQ_SORTIE
    GET PDSEQ_SORTIE OPTIONAL SEQUENTIAL
    LET CIECLE    OF PDSEQ_SORTIE       = T_CIECLEMNU
    LET SPRNUMSOR OF PDSEQ_SORTIE       = SPRNUMSOR OF PDSEQ_SORTIE + 1
    LET SPRNUMSOR OF PDSORTIE_PRODUIT   = SPRNUMSOR OF PDSEQ_SORTIE
    DISPLAY SPRNUMSOR OF PDSORTIE_PRODUIT
    PUT PDSEQ_SORTIE
  END
END


;-------------------------------------------------------------------------------
;
; Cette procedure permet de ne pas effacer l'ecran après l'UPDATE
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY SPRNUMSOR OF PDSORTIE_PRODUIT
    WARNING = " "
  END
END

PROCEDURE ENTRY
  BEGIN
    DISPLAY SPRNUMSOR OF PDSORTIE_PRODUIT
    ACCEPT SPRDATSOR OF PDSORTIE_PRODUIT
    ACCEPT RSOCODSOR OF PDSORTIE_PRODUIT
    DISPLAY RSODSC001 OF PDRAISON_SORTIE
    ACCEPT SPRDMR OF PDSORTIE_PRODUIT
    ACCEPT SPRDSC001 OF PDSORTIE_PRODUIT
    ACCEPT SPRCON OF PDSORTIE_PRODUIT
    ACCEPT T_SOUS
    ACCEPT FOUCLE OF PDSORTIE_PRODUIT
    DISPLAY FOUNOM OF VFOC_FOU
 ;   PUSH UPDATE
 ;   RUN SCREEN  pd192a &
 ;             PASSING PDSORTIE_PRODUIT, &
 ;                     T_CIECLEMNU       &
 ;             MODE SAME
    END
PROCEDURE PATH
  BEGIN
    REQUEST SPRNUMSOR OF PDSORTIE_PRODUIT
    IF PROMPTOK
      THEN LET PATH = 1
    IF PATH = 0
      THEN BEGIN
        LET PATH = 2
        END
    END
;--------------------------------------------------------------
PROCEDURE FIND
  BEGIN
    IF PATH = 1
      THEN BEGIN
          GET PDSORTIE_PRODUIT VIA CIECLE , SPRNUMSOR ORDERBY CIECLE &
          , SPRNUMSOR USING T_CIECLEMNU , SPRNUMSOR OF &
          PDSORTIE_PRODUIT
        ;IF ACCESSOK
        ;THEN BEGIN
        ;  GET VFOC_FOU OPT VIA FOCCIECLE,FOUCLE USING &
        ;                 T_CIECLEMNU,FOUCLE OF PDSORTIE_PRODUIT
        ;   IF ACCESSOK
        ;    THEN DISPLAY FOUNOM OF VFOC_FOU
        ;END
      END
    IF PATH = 2
      THEN BEGIN
          GET PDSORTIE_PRODUIT VIA CIECLE ORDERBY CIECLE , SPRNUMSOR &
          USING T_CIECLEMNU
       ; IF ACCESSOK
       ; THEN BEGIN
       ;   GET VFOC_FOU OPT VIA FOCCIECLE,FOUCLE USING &
       ;                  T_CIECLEMNU,FOUCLE OF PDSORTIE_PRODUIT
       ;    IF ACCESSOK
       ;     THEN DISPLAY FOUNOM OF VFOC_FOU
       ; END
      END

    GET PDSORTIE_BLOCS OPT
    IF ACCESSOK
      THEN LET T_SOUS = "BLOCS"
    GET PDSORTIE_TRANCHE OPT
    IF ACCESSOK
      THEN LET T_SOUS = "TRANCHE"
    GET PDSORTIE_DIAGRAM OPT
    IF ACCESSOK
      THEN LET T_SOUS = "DIAGRAMME"
    GET PDSOR_PROD_DIM OPT
    IF ACCESSOK
      THEN LET T_SOUS = "PRODUIT DIMENSSION"

END


;------------------------------------------------------------

PROCEDURE UPDATE
  BEGIN
    PUT PDSORTIE_PRODUIT
    END
;------------------------------------------------------
PROCEDURE DELETE
  BEGIN
  LET T_DETAIL = "0"

   GET PDSORTIE_BLOCS OPTIONAL &
         VIA     CIECLE,                               &
                 SPRNUMSOR                             &
         USING   T_CIECLEMNU,                          &
                 SPRNUMSOR        OF PDSORTIE_PRODUIT
   IF ACCESSOK
   THEN LET T_DETAIL = "1"

   GET PDSORTIE_TRANCHE OPTIONAL &
         VIA     CIECLE,                               &
                 SPRNUMSOR                             &
         USING   T_CIECLEMNU,                          &
                 SPRNUMSOR        OF PDSORTIE_PRODUIT
   IF ACCESSOK
   THEN LET T_DETAIL = "1"


  GET PDSORTIE_DIAGRAM  OPTIONAL &
         VIA     CIECLE,&
                 SPRNUMSOR                          &
         USING   T_CIECLEMNU,&
                 SPRNUMSOR      OF PDSORTIE_PRODUIT
    IF ACCESSOK
   THEN LET T_DETAIL = "1"


  GET PDSOR_PROD_DIM  OPTIONAL &
         VIA     CIECLE,                               &
                 SPRNUMSOR                             &
         USING   T_CIECLEMNU,                          &
                 SPRNUMSOR        OF PDSORTIE_PRODUIT
   IF ACCESSOK
   THEN LET T_DETAIL = "1"

    IF T_DETAIL <> "1"
     THEN DELETE PDSORTIE_PRODUIT
    ELSE ERROR = "Destruction impossible, voir les sous-ecrans"
END
;------------------------------------------------
PROCEDURE INPUT FOUCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd117 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_FOUCLE, &
                             T_USINE_SEC
    LET FIELDTEXT = TRUNCATE( T_FOUCLE )
  END
LET FOUCLE OF PDSORTIE_PRODUIT = T_FOUCLE
END
;-----------------------------------------------------
;PROCEDURE EDIT FOUCLE
;BEGIN
;   GET VFOC_FOU OPT VIA FOCCIECLE,FOUCLE USING &
;                         T_CIECLEMNU,FOUCLE OF PDSORTIE_PRODUIT
;    IF ACCESSOK
;      THEN DISPLAY FOUNOM OF VFOC_FOU
;END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Sortie de produits ******************************
*                                                                              *
* No sortie.....: xxxxxxxxx            Date sortie...:   xxxxxxxx              *
*                                                                              *
* Code sortie...: xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Demandeur.....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Confirmé......: x                                                            *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
