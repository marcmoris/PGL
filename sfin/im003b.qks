;/T/ Immobilisation
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Immobilisation  - Information comptable sommaire (FORMAT).
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im003b ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPFRM_AFFICHE, &
                        IMIMMOBILISATION, &
                        T_FRDDSC, &
                        T_MNTENG, &
                        T_MNTREE, &
                        T_MNTTOT, &
                        T_MNTBDG, &
                        T_MNTDIS


TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_FRDDSC    CHARACTER SIZE 45
TEMPORARY T_MNTENG    CHARACTER SIZE 15
TEMPORARY T_MNTREE    CHARACTER SIZE 15
TEMPORARY T_MNTTOT    CHARACTER SIZE 15
TEMPORARY T_MNTBDG    CHARACTER SIZE 15
TEMPORARY T_MNTDIS    CHARACTER SIZE 15

USE ussectmp NOLIST     ; Variable pour la securite
    "IM003B"            ; Nom de l'écran

USE im003b.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran



FILE GPFRM_AFFICHE          MASTER



;------------------------------------
; Fichier fiche de projet
;------------------------------------
FILE IMIMMOBILISATION       MASTER



FILE GPFRM_DETAILS          PRIMARY   &
                            OCCURS 13 &
                            NOITEMS

   ACCESS SEQUENTIAL
   SELECT IF IMMCLE OF GPFRM_DETAILS <> " "


;-------------------------------
; Fichier unité administrative
;-------------------------------
FILE MCUNITE_ADM            REFERENCE

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE OF GPFRM_DETAILS, &
                UNACLE OF GPFRM_DETAILS


;-----------------------------------------
; Fichier charte de l'unite administrative
;-----------------------------------------
FILE MCCHARTE_UNA           REFERENCE

  ACCESS  VIA   CPTCLE, &
                CIECLE, &
                UNACLE  &
          USING CPTCLE OF GPFRM_DETAILS, &
                CIECLE OF GPFRM_DETAILS, &
                UNACLE OF GPFRM_DETAILS


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPFRM_DETAILS


FILE IMIMM_BDG_CTB          DESIGNER
;-------------------------------------------------------------------------------
;
DEFINE D_MNTTOT      FLOAT SIZE 8 = FDEMNTENG OF GPFRM_DETAILS + &
                                    FDEMNTREE OF GPFRM_DETAILS

DEFINE D_MNTDIS     FLOAT SIZE 08 = FRAMNTBDG OF GPFRM_AFFICHE - &
                                    D_MNTTOT

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_CIECLE     CHAR*4
TEMPORARY T_IMMCLE     CHAR*12
TEMPORARY T_CPTCLE     CHAR*6
TEMPORARY T_UNACLE     CHAR*8

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Détail de la ligne du format de présentation " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,35)
FIELD IMMCLE    OF IMIMMOBILISATION                   &
      DISPLAY

FIELD IMMDSC    OF IMIMMOBILISATION                   &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Titre"  &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Engagé" &
@ELSE
      " %%% " &
@ENDIF
      AT ,58

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,77

TITLE &
@IF FRANCAIS
      "Total" &
@ELSE
      " %%% " &
@ENDIF
      AT ,93

TITLE &
@IF FRANCAIS
      "Budget" &
@ELSE
      " %%% " &
@ENDIF
      AT ,109

TITLE &
@IF FRANCAIS
      "Disponibilité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,118


SKIP

ALIGN (,,3) (,,50) (,,67) (,,84) (,,101) (,,117)

FIELD T_FRDDSC     DISPLAY
FIELD T_MNTENG     DISPLAY
FIELD T_MNTREE     DISPLAY
FIELD T_MNTTOT     DISPLAY
FIELD T_MNTBDG     DISPLAY
FIELD T_MNTDIS     DISPLAY

DRAW 8,1 TO 8,132

SKIP TO 9

TITLE &
@IF FRANCAIS
      "Compte Descrption            Unité     Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Engagé" &
@ELSE
      " %%% " &
@ENDIF
      AT ,75

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,98

TITLE &
@IF FRANCAIS
      "Total" &
@ELSE
      " %%% " &
@ENDIF
      AT ,119

SKIP

ALIGN (3,2,3) (,,10) (,,32) (,,42) (,,67) (,,88) (,,110)

CLUSTER OCCURS WITH GPFRM_DETAILS

FIELD CPTCLE OF GPFRM_DETAILS                         &
      LABEL " "                                       &
      HIDDEN                                          &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD UNACLE OF GPFRM_DETAILS                         &
      DISPLAY

FIELD UNANOMABR OF MCUNITE_ADM &
      DISPLAY

FIELD FDEMNTENG OF GPFRM_DETAILS

FIELD FDEMNTREE OF GPFRM_DETAILS

FIELD D_MNTTOT                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

CLUSTER


;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (ENGAGE)
;
;
PROCEDURE INPUT FDEMNTENG
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    IF 0 <> INDEX(FIELDTEXT,"*")
    THEN BEGIN
      LET T_CIECLE = CIECLE OF GPFRM_DETAILS
      LET T_IMMCLE = IMMCLE OF GPFRM_DETAILS
      LET T_CPTCLE = CPTCLE OF GPFRM_DETAILS
      LET T_UNACLE = UNACLE OF GPFRM_DETAILS

           GET IMIMM_BDG_CTB &
               VIA   CIECLE, &
                     IMMCLE, &
                     CPTCLE, &
                     UNACLE  &
               USING T_CIECLE, &
                     T_IMMCLE, &
                     T_CPTCLE, &
                     T_UNACLE

           RUN SCREEN im001l MODE F PASSING T_CIECLEMNU, T_CIENOM, &
                                            IMIMM_BDG_CTB,         &
                                            IMIMMOBILISATION
      LET FIELDTEXT = "0"
    END
    ELSE BEGIN
      LET FIELDTEXT = "0"
      INFO MESS 842; La modification de cet élément n'est pas permise.
    END
  END
END

PROCEDURE EDIT FDEMNTENG
BEGIN
   LET FIELDVALUE = OLDVALUE(FDEMNTENG OF GPFRM_DETAILS)
END

;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (REEL)
;
;
PROCEDURE INPUT FDEMNTREE
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    IF 0 <> INDEX(FIELDTEXT,"*")
    THEN BEGIN
      LET T_CIECLE = CIECLE OF GPFRM_DETAILS
      LET T_IMMCLE = IMMCLE OF GPFRM_DETAILS
      LET T_CPTCLE = CPTCLE OF GPFRM_DETAILS
      LET T_UNACLE = UNACLE OF GPFRM_DETAILS


           GET IMIMM_BDG_CTB &
               VIA   CIECLE, &
                     IMMCLE, &
                     CPTCLE, &
                     UNACLE  &
               USING T_CIECLE, &
                     T_IMMCLE, &
                     T_CPTCLE, &
                     T_UNACLE

           RUN SCREEN im001k MODE F PASSING T_CIECLEMNU, T_CIENOM, &
                                            IMIMM_BDG_CTB,         &
                                            IMIMMOBILISATION
      LET FIELDTEXT = "0"
    END
    ELSE BEGIN
      LET FIELDTEXT = "0"
      INFO MESS 842; La modification de cet élément n'est pas permise.
    END
  END
END

PROCEDURE EDIT FDEMNTREE
BEGIN
   LET FIELDVALUE = OLDVALUE(FDEMNTREE OF GPFRM_DETAILS)
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST

  CLOSE GPFRM_DETAILS
  RUN SCREEN im003c MODE F PASSING GPFRM_AFFICHE
  REFRESH ALL
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD LIST DETAIL
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

