;/T/ Consultation des projets          CIECLE, PRJCLE, PRJSTUPAT
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 8 mars 1995
;
;    Paramètres...: CIECLE    ==> - numéro de compagnie
;                                 - segment de la clé de recherche
;
;                   PRJCLE    ==> - numéro de projet
;                                 - segment de la clé de recherche
;                                 - peut contenir la valeur @
;                                 - contient la valeur de retour
;
;                   PRJSTUPAT ==> - Statut de projet
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp104 NOMODE NOACTION FIELDMARK &
             FROM 11,05 TO 23,75 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU, &
                       T_PRJCLE   , &
                       T_PRJSTUPAT

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_PRJCLE    CHARACTER SIZE 8
TEMPORARY T_PRJSTUPAT CHARACTER SIZE 8

USE gp104.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE GPPROJETS        PRIMARY &
                      OCCURS 11
  ACCESS VIA CIECLE, &
             PRJCLE  &
         USING T_CIECLEMNU, &
               T_PRJCLE &
         ORDERBY PRJCLE

  SELECT IF &
    MATCHPATTERN(PRJSTU OF GPPROJETS, TRUNCATE(T_PRJSTUPAT))

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 1,1 TO 13,71

USE ustitoff NOLIST

SKIP TO 2

ALIGN (3,3,6)(,,15)
CLUSTER OCCURS WITH GPPROJETS

FIELD PRJCLE    OF GPPROJETS HIDDEN LABEL ">>"
FIELD PRJDSC1   OF GPPROJETS DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_PRJCLE = PRJCLE OF GPPROJETS
  RETURN
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_PRJCLE, "@")
  THEN LET T_PRJCLE = ""
END

BUILD ;
;
;                                       *****************************************
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       *****************************************
;
;
;
;
;
;
;
;
;
;
