;/T/ Liste des produits
;
;/P/ Programmeur...: Marie-Josée Hamel
;    Date Création.: 96.02.13
;
;    Description...: Liste des produits
;
;-----------------------------------------------------------------------------
USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
SET NOBLANKS          ; Empêche l'impression de ligne blanche


ACCESS FATRAJET_PRODUIT &
  ;------------------------------------
  ; Pour trouver ses segments s'il en a
  LINK CIECLE OF FATRAJET_PRODUIT , &
       TRPCLE OF FATRAJET_PRODUIT   &
    TO CIECLE , &
       TRPCLE                     OF FASEGMENT OPTIONAL &
  ;------------------------------------
  ; description du segment
  LINK CIECLE OF FASEGMENT , &
       SEGCLE OF FASEGMENT   &
    TO CIECLE , &
       TRPCLE                     OF FATRAJET_PRODUIT   &
                                     ALIAS A_TRP_SEGMENT OPTIONAL &
  ;------------------------------------
  ; description du port d'origine
  LINK TRPPORORI OF FATRAJET_PRODUIT   &
    TO FPOCLE                     OF FAPORT &
                                     ALIAS A_FPO_ORIGINE OPTIONAL &
  ;------------------------------------
  ; description du port de destination
  LINK TRPPORDES OF FATRAJET_PRODUIT   &
    TO FPOCLE                     OF FAPORT &
                                     ALIAS A_FPO_DEST    OPTIONAL &
  ;------------------------------------
  ; Description de la catégorie
  LINK ("CATPRD"                    , &
         TRPCAT OF FATRAJET_PRODUIT ) &
    TO XELNOM , &
       CBICLE                     OF VCBI_DSC OPTIONAL

CHOOSE CIECLE PARM

;-----------------------------------------------------------------------------


DEFINE D_TRI CHARACTER SIZE 1 = PARM ; Tri par produit ou par categorie

DEFINE D_CLETRI CHARACTER SIZE 4 = TRPCAT OF FATRAJET_PRODUIT &
                              IF D_TRI = "1" &
                              ELSE TRPCLE OF FATRAJET_PRODUIT

SORT ON CIECLE OF FATRAJET_PRODUIT &
     ON D_CLETRI                   &
     ON TRPCLE OF FATRAJET_PRODUIT &
     ON SEGSEQ OF FASEGMENT


;-----------------------------------------------------------------------------

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 & ; Premier titre du rapport
@IF FRANCAIS
        = "Liste des produits / trajets"
@ELSE
        = "%%%Liste des produits / trajets"
@ENDIF
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "     ; Deuxieme titre du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "FA309" ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM    ; Nom de la compagnie


DEFINE D_ENT1     CHARACTER SIZE 30 = "         Segment"              &
                                  IF RECORD FASEGMENT EXISTS ELSE " "
DEFINE D_ENT2     CHARACTER SIZE 30 = "-----------------------------" &
                                  IF RECORD FASEGMENT EXISTS ELSE " "


USE usflgdet NOLIST ;Flag Bilingue (O/N)

;
;Indique si on imprime le produit
;
DEFINE D_IMP   CHARACTER SIZE 3 &
        = DZ_FLGAFFO IF TRPIMP OF FATRAJET_PRODUIT = "1" ELSE &
          DZ_FLGAFFN


;-----------------------------------------------------------------------------------------------------------------------------------

USE ustit180 NOLIST  ; Titre standard pour l'application a 180
SKIP 2 &
  TAB 001 "                 Produit                                Catégorie           Stu Imp           Origine                     Destination" &
SKIP 2 &
  TAB 001 "-------------------------------------------- ------------------------------ --- --- ------------------------------ ------------------------------" &
SKIP

HEADING AT TRPCLE &
  TAB 001 TRPCLE    OF FATRAJET_PRODUIT &
  TAB 005 TRPDSCFRA OF FATRAJET_PRODUIT &
  TAB 046 TRPCAT    OF FATRAJET_PRODUIT &
  TAB 051 CBIDSC    OF VCBI_DSC         &
  TAB 078 TRPSTU    OF FATRAJET_PRODUIT &
  TAB 081 D_IMP                         &
  TAB 085 TRPPORORI OF FATRAJET_PRODUIT &
  TAB 090 FPODSCFRA OF A_FPO_ORIGINE    &
  TAB 116 TRPPORDES OF FATRAJET_PRODUIT &
  TAB 120 FPODSCFRA OF A_FPO_DEST       &
SKIP &
  TAB 005 TRPDSCANG OF FATRAJET_PRODUIT &
  TAB 135 D_ENT1 &
SKIP &
  TAB 135 D_ENT2

REPORT &
  TAB 135 SEGCLE    OF FASEGMENT &
  TAB 139 TRPDSCFRA OF A_TRP_SEGMENT

BUILD fa309z
@IF FORMAT
                 Produit                                Catégorie           Stu Imp           Origine                     Destination
-------------------------------------------- ------------------------------ --- --- ------------------------------ ------------------------------
xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxx  x  xxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxx
    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                                     Segment
                                                                                                                                      -----------------------------
                                                                                                                                      xxx xxxxxxxxxxxxxxxxxxxxxxxxx
                                                                                                                                      xxx xxxxxxxxxxxxxxxxxxxxxxxxx
                                                                                                                                      xxx xxxxxxxxxxxxxxxxxxxxxxxxx
                                                                                                                                      xxx xxxxxxxxxxxxxxxxxxxxxxxxx

@ENDIF

