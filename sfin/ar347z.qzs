;/T/ Catalogue des produits divers
;
;/P/ Programmeur...: Patrick Langlois
;    Date Création.: 5 Aôut 1999
;
;    Description...: Impression du catalogue des produits divers 
;
;    Paramètres....: Compagnie
;
;***********************************************************************************************************************************
;
;/M/ Modifié par.......: Marc Poulin
;    Date..............: 24 janvier 2001
;    Description.......: Modifier le traitement en éliminant du rapport la colonne associée au numéro de pièce fournisseur pour 
;                        ainsi permettre d'augmenter l'espace d'impression consacré à l'information liée à la description du produit.
;
;    Référence.........: Demande de changement 000118.
;
;    Signet............: MP-01-01-24_D118.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS VREFFOU    &
   LINK (FOUCLE , &
        "----")   &
     TO FOUCLE  , &
        FOUCIECLE             OF CPFOURNISSEUR OPT &
   LINK CIECLE OF VREFFOU, &
        CMDCLE OF VREFFOU, &
        CMDAJT OF VREFFOU  &
     TO CIECLE           , &
        CMDCLE           , &
        CMDAJT                OF ARCOMMANDE OPT



CHOOSE CIECLE    PARM, &
       PRDCLE    PARM, &
       CDEREFFOU PARM, &
       CDEDSCPRD PARM, &
       FOUCLE    PARM 


DEFINE D_TRI1 CHAR SIZE 1 = PARM

DEFINE D_TRI2 CHAR SIZE 40 =   CDEREFFOU OF VREFFOU IF D_TRI1 = "1" &
                          ELSE CDEDSCPRD OF VREFFOU   

SORT ON D_TRI2, &
        CMDDAT OF ARCOMMANDE D 

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Catalogue des produits divers"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Par ordre numérique" IF D_TRI1 = "1" ELSE &
                                      "Par ordre alphabétique"

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR347Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


DEFINE D_PRDCLE CHAR SIZE 12 = PRDCLE OF VREFFOU IF PRDCLE <> "000000000000" ELSE " "

USE ustitstd NOLIST
  SKIP 2 &
TAB 001 "No produit   Description                                   Fourn. Nom                  #Commande   Date       Quantité  Prix unit. " & ; MP-01-01-24_D118.
  SKIP 1 &
TAB 001 "------------ --------------------------------------------- ------ -------------------- --------- ---------- ---------- ------------" & ; MP-01-01-24_D118.
SKIP


REPORT &
  TAB 001 D_PRDCLE                                                  &
  tab 014 cdedscprd of vreffou                                      & ; MP-01-01-24_D118.
  TAB 060 FOUCLE    OF VREFFOU                                      &
  TAB 067 FOUNOM    OF CPFOURNISSEUR PICTURE "^^^^^^^^^^^^^^^^^^^^" &
  TAB 088 CMDCLE    OF VREFFOU                                      &
  TAB 098 CMDDAT    OF ARCOMMANDE    FORMAT YYYYMMDD                &
  TAB 109 CDEQTECOM OF VREFFOU       PICTURE "^^,^^^.^^^"           &
  TAB 120 CDEPRIUNI OF VREFFOU       PICTURE "^^^,^^^.^^^^"  SIGNIF 6


BUILD ar347z


@IF FORMAT   

No produit   Description                                   Fourn. Nom                  #Commande   Date       Quantité  Prix unit. 
------------ --------------------------------------------- ------ -------------------- --------- ---------- ---------- ------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxx
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxx  

@ENDIF
