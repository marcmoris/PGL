;/P/ Detail de la facturation par commande
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 22-11-1999
;    Description....: Details des blocs factures par commande.
;
;-------------------------------------------------------------------------------


USE ussetqzs NOLIST
SET NOBLANKS

ACCESS PDDETAIL_C_I &
 LINK CIECLE OF PDDETAIL_C_I, &
      PJTABR OF PDDETAIL_C_I, &
      PJTANN OF PDDETAIL_C_I, &
      COMNUMCOMINT                    OF PDDETAIL_C_I &
   TO CIECLE, &
      PJTABR, &
      PJTANN, &
      COMNUMCOMINT                    OF  IFBLOC &
 LINK CIECLE OF IFBLOC, &
      FTRNUMFAC OF IFBLOC &
   TO CIECLE, &
      FTRNUMFAC                      OF PDFACTURE



DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""



CHOOSE CIECLE    PARM


SORT ON D_NUMCOM &
     ON FTRNUMFAC &
     ON D_NUMBLC

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF340"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 1 &
             TAB 004 "NO"               &
             TAB 019 "NO"               &
             TAB 026 "DATE "            &
             TAB 038 "NO"               &
             TAB 049 "CAT"              &
             TAB 057 "DIMENSIONS"       &
             TAB 076 "TOTAL"            &
             TAB 090 "NO"               &
SKIP 1                                  &
             TAB 001 "COMMANDE"         &
             TAB 018 "FACT"          &
             TAB 025 "FACTURE"          &
             TAB 037 "BLOC"             &
             TAB 048 "GRANIT"           &
             TAB 055 "LONG  HAUT  LARG"   &
             TAB 078 "M3"               &
             TAB 090 "EXP."             &
SKIP 1                                  &
             TAB 001 "--------------"   &
             TAB 017 "------"   &
             TAB 024 "--------"         &
             TAB 033 "--------------"   &
             TAB 049 "---"           &
             TAB 055 "----  ----  ----" &
             TAB 076 "-----"            &
             TAB 089 "-----"            &
SKIP 2

REPORT                                  &
  TAB 001 D_NUMCOM PRINT AT D_NUMCOM    &
  TAB 016 FTRNUMFAC OF IFBLOC           &
  TAB 024 FTRDATFAC OF PDFACTURE        &
  TAB 033 D_NUMBLC                      &
  TAB 049 TGRCODGRA OF IFBLOC           &
  TAB 055 BLCVENDULONG OF IFBLOC        &
  TAB 061 BLCVENDUHAU OF IFBLOC         &
  TAB 067 BLCVENDULAR OF IFBLOC         &
  TAB 075 BLCM3TOT OF IFBLOC            &
  TAB 088 EXNCLE OF IFBLOC

FOOTING AT FTRNUMFAC OF IFBLOC                      &
        TAB 1 "--------------"                            &
        TAB 73 "--------"                         &
SKIP 1                                              &
        TAB 1 COUNT PICTURE "^^^^^" "Blocs"         &
        TAB 60 "Sous-total:"                        &
        TAB 73 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
SKIP 2
FINAL FOOTING                                       &
        TAB 1 "--------------"                            &
        TAB 73 "--------"                         &
SKIP 1                                              &
        TAB 1 COUNT PICTURE "^^^^^" "Blocs"         &
        TAB 60 "Total:"                             &
        TAB 73 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
SKIP 2
BUILD  if330z
