;/T/ 2.5.2: ENREGISTRER LES EXPEDITIONS
;
;/P/ Programmeur..: Alain Simard, PROSIG informatique inc.
;    Date Création: 11 mai 1995
;
;    Description..: Cet écran a pour but de gérer les expeditions
;
;/M/ Programmeur..: Claudie Doyon
;    Date.........: 27 Octobre 1999
;    Description..: Ajout du file pdbon_livre référence.
;                   Modification de la procédure delete pour empècher de 
;                   détruire l'expédition si il y a des bons de livraison.
;
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd217 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION &
             FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU , &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_EXPED IN DICT

;
; Variables pour la sécurité.
;
; Variables temporaire pour la sécurité
;
; Description: Déclaration des variables temporaire pour la gestion
;              de la sécurité-prosig.
;
; Utilisation: Doit être utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la définition du premier fichier.
;
;------------------------------------------------------------------------------
;
; Accès au programme.
;
TEMPORARY TZ_SECACC CHARACTER SIZE 1 RESET AT STARTUP
;
; Saisie de données(Entry).
;
TEMPORARY TZ_SECENT CHARACTER SIZE 1 RESET AT STARTUP
;
; Destruction.(Delete)
;
TEMPORARY TZ_SECDEL CHARACTER SIZE 1 RESET AT STARTUP
;
; Modification.(Change mode)
;
TEMPORARY TZ_SECMOD CHARACTER SIZE 1 RESET AT STARTUP
;
; Variable pour l'élément de la sécurité.
;
TEMPORARY TZ_XELNOM CHARACTER SIZE 16 RESET AT STARTUP
TEMPORARY TZ_FLGAUT CHARACTER SIZE  1 RESET AT STARTUP
;
; Nom de l'écran pour retrouver la sécurité.
;
TEMPORARY TZ_NOMECR CHARACTER SIZE 7 RESET AT STARTUP INIT &
;
;
;**FIN USSECTMP
    "PD217"

;
; Documentation de l'écran.
;
Description of screen &
@IF FRANCAIS
"                                                            " &
"     Cet écran permet de gérer les expéditions.             " &
"                                                            " &
" A l'aide de sous-écran, il sera possible de spécifier le   " &
" contenu d'une expédition, soit les caissons composants     " &
" chacun des bons de livraison d'une expédition.             " &
"                                                            "
@ELSE
"                                                            " &
"     This screen permits ....                               " &
@ENDIF


; Actionbar standard dans les écrans pleine page et sous-ecran
; ACTIONBAR
;
; Description: Définition du menu déroulant standard situé dans le haut de
;              l'écran.
;
; Utilisation: Il faut l'utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la déclaration des fichiers.
;
;
;
@IF FRANCAIS
ACTIONMENU LABEL "Rech"   ACTION FIND
ACTIONMENU LABEL "MAJ"    ACTION UPDATE
ACTIONMENU LABEL "Entrée" ACTION ENTRY
ACTIONMENU LABEL "Ajout"  ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Supp"   ACTION DELETE
@ELSE
ACTIONMENU LABEL "Find"   ACTION FIND
ACTIONMENU LABEL "Update" ACTION UPDATE
ACTIONMENU LABEL "Entry"  ACTION ENTRY
ACTIONMENU LABEL "Append" ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Del"    ACTION DELETE
@ENDIF

@IF VAXVMS AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Ecran      <PF1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Champ          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Champ dét. <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Recherche     <rech>" ACTION FIND
  MENUITEM LABEL "MAJ            <PF3>" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour <PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde      <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entrée         <ins>" ACTION ENTRY
  MENUITEM LABEL "Ajout      <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Sélection   <sélect>" ACTION SELECT
  MENUITEM LABEL "Supp.    <eff.texte>" ACTION DELETE
  MENUITEM LABEL "Supp.Lig  <PF1 eff.>" ACTION DELETE MARK
  MENUITEM LABEL "Edition         <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant  <page suiv>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.    <PF4>" ACTION RETURN
  MENUITEM LABEL "Sortie         <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "Impression     <F20>" ACTION LIST
  MENUITEM LABEL "Système       <exec>" ACTION SYSTEM
@ENDIF

@IF VAXVMS AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Screen     <pf1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Field          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Ext. Field <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Find Mode     <find>" ACTION FIND
  MENUITEM LABEL "Update         <PF3>" ACTION UPDATE
  MENUITEM LABEL "Update Ret.<PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay    <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode     <ins>" ACTION ENTRY
  MENUITEM LABEL "Append     <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Selection   <select>" ACTION SELECT
  MENUITEM LABEL "Delete      <remove>" ACTION DELETE
  MENUITEM LABEL "Del. Line <PF1 rem.>" ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Next     <next page>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen   <pf4>" ACTION RETURN
  MENUITEM LABEL "Quick Exit     <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "List           <F20>" ACTION LIST
  MENUITEM LABEL "System          <Do>" ACTION SYSTEM
@ENDIF

@IF UNIX AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Aide            F1_H" ACTION HELP
  MENUITEM LABEL "Aide dét.       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Recherche      F1_F7" ACTION FIND
  MENUITEM LABEL "MAJ               F5" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour     F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde           " ACTION UPDATE STAY
  MENUITEM LABEL "Entrée            F7" ACTION ENTRY
  MENUITEM LABEL "Ajout               " ACTION APPEND
  MENUITEM LABEL "Sélection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Supp.             F2" ACTION DELETE
  MENUITEM LABEL "Supp.Lig            " ACTION DELETE MARK
  MENUITEM LABEL "Edition         F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant       <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.       F8" ACTION RETURN
  MENUITEM LABEL "Sortie         F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "Impression écran  F3" ACTION LIST
@ENDIF

@IF UNIX AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Help            F1_H" ACTION HELP
  MENUITEM LABEL "Ext. Help       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Find mode      F1_F7" ACTION FIND
  MENUITEM LABEL "Update            F5" ACTION UPDATE
  MENUITEM LABEL "Update Return  F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay         " ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode        F7" ACTION ENTRY
  MENUITEM LABEL "Append              " ACTION APPEND
  MENUITEM LABEL "Selection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Delete            F2" ACTION DELETE
  MENUITEM LABEL "Del line            " ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Next          <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen      f8" ACTION RETURN
  MENUITEM LABEL "Quick Exit     F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "List              F3" ACTION LIST
@ENDIF
;
;
;**FIN USACTECR

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Gestion des bons de livraison   " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%gestion des bons de livraison" ACTION DESIGNER D001
@ENDIF

;-------------------------------------------------------------------------------
;
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Expédition
;

FILE PDEXPEDITION PRIMARY &
                  NOITEM

  ACCESS  VIA     CIECLE,     &
                  EXPNUMEXP   &
          USING   T_CIECLEMNU, &
                  EXPNUMEXP    &
          REQUEST EXPNUMEXP    &
          ORDERBY CIECLE,      &
                  EXPNUMEXP

  ACCESS  VIA     CIECLE       &
          USING   T_CIECLEMNU  &
          ORDERBY CIECLE,      &
                  EXPNUMEXP

  ITEM CIECLE OF PDEXPEDITION INITIAL T_CIECLEMNU


 FILE PDBON_LIVRAISON REFERENCE 

;-------------------------------------------------------------------------------
;
; Fournisseurs cammionage
;

TEMPORARY T_FOUCLE    CHARACTER SIZE 06 ; POP-UP Fournisseur
TEMPORARY T_FOUNOM    CHARACTER SIZE 40 ; NOM DU FOURNISSEUR
                                        ; POUR LES SOUS-ÉCRANS
TEMPORARY T_USINE_SEC CHARACTER SIZE 1

FILE VFOC_FOU REFERENCE

;  ACCESS VIA     FOUCLE,                      &
;                 FOCCIECLE                    &
;         USING   FOUCLE    OF PDEXPEDITION, &
;                 T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Validation des codes saisie
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

; type d'expedition
DEFINE    D_EXPTYPEXP CHARACTER SIZE 16 = "EXPTYPEXP"
TEMPORARY T_EXPTYPEXP CHARACTER SIZE 4

FILE VCBI_DSC   REFERENCE &
        ALIAS   A_CBI_EXPTYPEXP
        ACCESS  VIA     XELNOM, &
                        CBICLE &
                USING   D_EXPTYPEXP, &
                        EXPTYPEXP OF PDEXPEDITION OPT

;-------------------------------------------------------------------------------
;
; type de camion
;

DEFINE    D_EXPTYPCAM CHARACTER SIZE 16 = "EXPTYPCAM"
TEMPORARY T_EXPTYPCAM CHARACTER SIZE 4

FILE VCBI_DSC   REFERENCE &
        ALIAS   A_CBI_EXPTYPCAM
        ACCESS  VIA     XELNOM, &
                        CBICLE &
                USING   D_EXPTYPCAM, &
                        EXPTYPCAM OF PDEXPEDITION OPT

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les opération de surface
;

FILE PDSEQ_EXPED DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; DRAW des écrans pleine page.
;
; Description: Ce USE sert à dessiner le cadre des écrans pleine page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD>
;
;
DRAW 3,01 TO 23,80
;
;
;**FIN USDRWECR
; En-tête standard des écrans pleine page.
;
; Description: Ce USE sert à afficher l'en-tête standard des écrans pleine
;              page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD.
;
;
SKIP TO LINE 2

@IF VAXVMS
HILITE DISPLAY HALFTONE
@ENDIF

ALIGN(,,1) (,,21)

FIELD DZ_CIECLE FIXED
FIELD DZ_CIENOM FIXED

;
;
;**FIN USTITSTD
; HILITE standard de tous les écrans
;
; Description: Définition du HILITE standard de tous les écrans, menu
;              écrans pleine page, popup...
;
; Utilisation: Doit être utilisé avant la première énoncé FIELD.
;
;
@IF VAXVMS
HILITE ACTIONBAR     INVERSE                     , &
       ACTIONBARMARK HALFTONE INVERSE            , &
       FIELDMARK     INVERSE                     , &
       MENUMARK      HALFTONE INVERSE            , &
       MODE          HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       HALFTONE UNDERLINE          , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE HALFTONE AUDIBLE    , &
       ERROR         INVERSE HALFTONE AUDIBLE    , &
       SEVERE        INVERSE HALFTONE AUDIBLE BLINKING
@ELSE
HILITE ACTIONBAR     HALFTONE INVERSE            , &
       ACTIONBARMARK INVERSE                     , &
       FIELDMARK     INVERSE HALFTONE            , &
       MENUMARK      INVERSE HALFTONE            , &
       MODE          HALFTONE                    , &
       LABEL         HALFTONE                    , &
       LINEDRAWING   HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       UNDERLINE                   , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE AUDIBLE             , &
       ERROR         INVERSE AUDIBLE             , &
       SEVERE        INVERSE AUDIBLE BLINKING
@ENDIF
;
;**FIN USHILITE


SKIP


TITLE &
@IF FRANCAIS
      " EXPEDITION " &
@ELSE
      " %%%EXPEDITION " &
@ENDIF
      CENTERED AT ,1


; HILITE TITLE OFF
;
; Description: Sert à désactiver l'option HILITE pour toutes les
;              énoncés TITLE qui suit ce USE. Seul le titre principal
;              a un HILITE particulier.
;
; Utilisation: Doit être utilisé après le titre principal de l'écran.
;
;
HILITE TITLE OFF
;
;
;**FIN USHILITE


SKIP 2


ALIGN (3,4,20) (33,34,50) (,,54)


FIELD EXPNUMEXP OF PDEXPEDITION &
label "Num. expédi...:"&
        HIDDEN &
        OMIT ON ENTRY &
        NOCHANGE


FIELD EXPTYPEXP OF PDEXPEDITION &
label "Type expéd....:"&
        HIDDEN &
        REQUIRED &
        LOOKUP  ON      A_CBI_EXPTYPEXP &
                VIA     XELNOM, &
                        CBICLE &
                USING   D_EXPTYPEXP, &
                        FIELDTEXT &
                MESSAGE 2904 ; Ce code n'existe pas


FIELD CBIDSC OF A_CBI_EXPTYPEXP &
        DISPLAY


SKIP


FIELD EXPDATEXP OF PDEXPEDITION  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date expéd....:"&
        HIDDEN                  &
        DEFAULT SYSDATE


SKIP


FIELD EXPDATDEP OF PDEXPEDITION  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date départ...:"&
        HIDDEN                  &
        DEFAULT SYSDATE


SKIP 1


FIELD EXPDSC001 OF PDEXPEDITION &
label "Description...:"&
        HIDDEN


SKIP


FIELD EXPDSC002 OF PDEXPEDITION &
        ID SAME HIDDEN &
        NOLABEL &
        IF EXPDSC001 OF PDEXPEDITION <> " "


SKIP


FIELD EXPDSC003 OF PDEXPEDITION &
        ID SAME HIDDEN &
        NOLABEL &
        IF EXPDSC002 OF PDEXPEDITION <> " "


SKIP 1


TITLE "-----------------------TRANSPORTEUR---------------------" AT , 4


SKIP
ALIGN (3,4,29)
FIELD EXPPREPAI OF PDEXPEDITION &
      UPSHIFT &
      VALUE "O","N","o","n"  &
      HIDDEN  &
      LABEL "Port payé/Prepaid O/N..:" &
      DEFAULT "O"

ALIGN (3,4,20) (,,28)


FIELD FOUCLE OF PDEXPEDITION &
label "Fournisseur..:"&
        HIDDEN &
        REQUIRED &
        IF EXPTYPEXP OF PDEXPEDITION <> "X" &
        LOOKUP ON VFOC_FOU &
         VIA     FOUCLE,                      &
                 FOCCIECLE                    &
         USING   FOUCLE    OF PDEXPEDITION, &
                 T_CIECLEMNU &
          MESSAGE 2917 ; Ce fournisseur n'existe pas


FIELD FOUNOM OF VFOC_FOU &
        DISPLAY


SKIP


FIELD EXPNUMCON OF PDEXPEDITION &
label "Numéro contrat:"&
        HIDDEN &
        REQUIRED &
        IF EXPTYPEXP OF PDEXPEDITION <> "X"


SKIP


FIELD EXPTYPCAM OF PDEXPEDITION &
label "Type camion...:"&
        HIDDEN &
        IF EXPTYPEXP OF PDEXPEDITION <> "X" &
        LOOKUP  ON      A_CBI_EXPTYPCAM &
                VIA     XELNOM, &
                        CBICLE &
                USING   D_EXPTYPCAM, &
                        FIELDTEXT &
                MESSAGE 2904 ; Ce code n'existe pas


FIELD CBIDSC OF A_CBI_EXPTYPCAM &
        DISPLAY


SKIP


ALIGN (3,4,20)


FIELD EXPDSCCAM OF PDEXPEDITION &
label "Descr. camion.:"&
        HIDDEN &
        IF EXPTYPEXP OF PDEXPEDITION <> "X"


SKIP


FIELD EXPCUTTRPCAM OF PDEXPEDITION &
label "Coûts transp..:"&
        HIDDEN &
        IF EXPTYPEXP OF PDEXPEDITION <> "X"


SKIP


FIELD EXPPRITRPCAM OF PDEXPEDITION &
label "Prix transp...:"&
        HIDDEN &
        IF EXPTYPEXP OF PDEXPEDITION <> "X"


SKIP


FIELD EXPNUMFACTRP OF PDEXPEDITION &
label "Numéro facture:"&
        HIDDEN &
        IF EXPTYPEXP OF PDEXPEDITION <> "X"


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
; Appel de l'écran de sécurité.
;
; Description: Appel de l'écran de sécurité du module Gestion de système.
;              Les variables temporaires utilisées proviennent du USEFILE
;              USSECTMP.
;
; Utilisation: Doit être inséré dans la procédure INITIALIZE de l'écran.
;
;
RUN SCREEN gs000 PASSING TZ_NOMECR, & ; Nom de l'écran
                         TZ_SECACC, & ; Accès
                         TZ_SECENT, & ; Entrée
                         TZ_SECMOD, & ; Modification
                         TZ_SECDEL    ; Destruction

IF "3" = TZ_SECACC
THEN ERROR 6 ; L'usager n'est pas défini au système.

IF "2" = TZ_SECACC
THEN ERROR 7 ; Votre accès au système est expiré.

IF "0" = TZ_SECACC
THEN ERROR 8 ; Vous n'avez pas accès à cet option.
;
;
;**FIN USSECECR
END


;-------------------------------------------------------------------
;
; Appel du dépisteur pour le champ.
;

PROCEDURE INPUT EXPTYPEXP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EXPTYPEXP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_EXPTYPEXP , &
                             T_EXPTYPEXP
    LET FIELDTEXT = TRUNCATE( T_EXPTYPEXP )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ a été saisi
;

PROCEDURE EDIT EXPDATEXP
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------
;
; Appel de l'ecran de consultation des fournisseurs
;

PROCEDURE INPUT FOUCLE OF PDEXPEDITION
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd117 MODE F &
                     PASSING T_CIECLEMNU, &
                             T_FOUCLE, &
                             T_USINE_SEC
    LET FIELDTEXT = TRUNCATE( T_FOUCLE )
  END
END


;-------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;

PROCEDURE INPUT EXPTYPCAM
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EXPTYPCAM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_EXPTYPCAM , &
                             T_EXPTYPCAM
    LET FIELDTEXT = TRUNCATE( T_EXPTYPCAM )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
; Sécurité pour le mode ENTRY.
;
; Description: Valide si l'usager peut faire de la saisie dans cette
;              écran.
;
; Utilisation: Peut être utiliser dans la procédure INPUT du premier
;              champ saisie à l'écran, ou dans la procédure ENTRY
;              si celle-ci a été altérée.
;
;
IF     ENTRYMODE       &
   AND TZ_SECENT = "0"
THEN SEVERE 16 ; Vous ne pouvez pas saisir de données.
;
;
;**FIN USSECENT
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran de gestion des bons de livraison
;

PROCEDURE DESIGNER D001  &
  HELP "Appel du sous-écran de gestion des bons de livraison"
BEGIN
  IF ALTEREDRECORD OF PDEXPEDITION
  THEN BEGIN
    ERROR 2991 ; Faire une mise à jour avant d'accèder au sous-écran
  END

  RUN SCREEN pd217a &
        MODE SAME &
        PASSING T_CIECLEMNU,      &
                T_CIENOM,         &
                PDEXPEDITION
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  FOR PDEXPEDITION
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDEXPEDITION &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDEXPEDITION &
       AND NOT NEWRECORD     OF PDEXPEDITION &
       AND NOT DELETEDRECORD OF PDEXPEDITION &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END

  ;
  ; Incrémentation du numéro de séquence de l'expedition
  ;
  IF 0 = EXPNUMEXP OF PDEXPEDITION
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_EXPED OPTIONAL &
      VIA     CIECLE     &
      USING   T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE    OF PDSEQ_EXPED = T_CIECLEMNU
    END
    LET SEPNUMEXP OF PDSEQ_EXPED = SEPNUMEXP OF PDSEQ_EXPED + 1
    LET EXPNUMEXP OF PDEXPEDITION = SEPNUMEXP OF PDSEQ_EXPED
    PUT PDSEQ_EXPED
    DISPLAY EXPNUMEXP OF PDEXPEDITION
  END
END


PROCEDURE ENTRY
  BEGIN
    ACCEPT EXPTYPEXP OF PDEXPEDITION
    DISPLAY CBIDSC OF A_CBI_EXPTYPEXP
    ACCEPT EXPDATEXP OF PDEXPEDITION
    ACCEPT EXPDATDEP OF PDEXPEDITION
    ACCEPT EXPDSC001 OF PDEXPEDITION
    IF EXPDSC001 OF PDEXPEDITION < > " "
      THEN ACCEPT EXPDSC002 OF PDEXPEDITION
      ELSE DISPLAY EXPDSC002 OF PDEXPEDITION
    IF EXPDSC002 OF PDEXPEDITION < > " "
      THEN ACCEPT EXPDSC003 OF PDEXPEDITION
      ELSE DISPLAY EXPDSC003 OF PDEXPEDITION
    ACCEPT EXPPREPAI OF PDEXPEDITION
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT FOUCLE OF PDEXPEDITION
      ELSE DISPLAY FOUCLE OF PDEXPEDITION
    DISPLAY FOUNOM OF VFOC_FOU
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT EXPNUMCON OF PDEXPEDITION
      ELSE DISPLAY EXPNUMCON OF PDEXPEDITION
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT EXPTYPCAM OF PDEXPEDITION
      ELSE DISPLAY EXPTYPCAM OF PDEXPEDITION
    DISPLAY CBIDSC OF A_CBI_EXPTYPCAM
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT EXPDSCCAM OF PDEXPEDITION
      ELSE DISPLAY EXPDSCCAM OF PDEXPEDITION
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT EXPCUTTRPCAM OF PDEXPEDITION
      ELSE DISPLAY EXPCUTTRPCAM OF PDEXPEDITION
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT EXPPRITRPCAM OF PDEXPEDITION
      ELSE DISPLAY EXPPRITRPCAM OF PDEXPEDITION
    IF EXPTYPEXP OF PDEXPEDITION < > "X"
      THEN ACCEPT EXPNUMFACTRP OF PDEXPEDITION
      ELSE DISPLAY EXPNUMFACTRP OF PDEXPEDITION
  PUSH UPDATE STAY
  RUN SCREEN pd217a &
        MODE SAME &
        PASSING T_CIECLEMNU,      &
                T_CIENOM,         &
                PDEXPEDITION
    END
PROCEDURE PATH
  BEGIN
    REQUEST EXPNUMEXP OF PDEXPEDITION
    IF PROMPTOK
      THEN LET PATH = 1
    IF PATH = 0
      THEN BEGIN
        LET PATH = 2
        END
    END
PROCEDURE FIND
  BEGIN
    IF PATH = 1
      THEN GET PDEXPEDITION VIA CIECLE , EXPNUMEXP ORDERBY CIECLE , &
          EXPNUMEXP USING T_CIECLEMNU , EXPNUMEXP OF PDEXPEDITION
    IF PATH = 2
      THEN GET PDEXPEDITION VIA CIECLE ORDERBY CIECLE , EXPNUMEXP &
          USING T_CIECLEMNU
    END
PROCEDURE UPDATE
  BEGIN
    PUT PDEXPEDITION
    END
PROCEDURE DELETE 
  BEGIN

    GET PDBON_LIVRAISON      &
    VIA     CIECLE,          &
            EXPNUMEXP        &
    USING   T_CIECLEMNU,               &
            EXPNUMEXP OF PDEXPEDITION  OPTIONAL
    IF ACCESSOK
      THEN WARNING "PD6754A Vous devez supprimer le détail de l'expédition."
      ELSE DELETE PDEXPEDITION

    END
BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************************************************************
*                                                                              *
*  Num. expédi...: xxxxxxxxxxx   Type expéd....: x   xxxxxxxxxxxxxxxxxxxxxxxxx *
*  Date expéd....: xxxxxxxx                                                    *
*  Date départ...: xxxxxxxx                                                    *
*                                                                              *
*  Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*                  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*                  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*                                                                              *
*  No. facture...: xxxxxxxxx                                                   *
*                                                                              *
*  -----------------------TRANSPORTEUR---------------------                    *
*  Numéro contrat: xxxxxxxxxxxxxxx                                             *
*  Type camion...: xx  xxxxxxxxxxxxxxxxxxxxxxxxx                               *
*  Descr. camion.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
*  Coûts transp..: xxxxxxxxxxxxxxx                                             *
*  Prix transp...: xxxxxxxxxxxxxxx                                             *
*  Numéro facture: xxxxxxxxxxxxxxx                                             *
*                                                                              *
********************************************************************************
@ENDIF
