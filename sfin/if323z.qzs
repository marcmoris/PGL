;/P/ Duplicata de facture
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
USE ussetqzs NOLIST

ACCESS IFFACTURE &
  LINK CIECLE OF IFFACTURE, &
       CLTCLE OF IFFACTURE  &
    TO CIECLE, &
       CLTCLE                           OF IFCLIENT &
  LINK CLICIECLE OF IFCLIENT, &
       CLICLE OF IFCLIENT &
    TO CLICIECLE, &
       CLICLE                           OF CRCLIENT &
   LINK ( CLICIECLE OF IFCLIENT, &
          CLICLE OF IFCLIENT, &
          CLIREFTYP OF CRCLIENT, &
          1 ) &
     TO REFCIECLE, &
        REFCLENUM, &
        REFCLETYP, &
        RADCLE                          OF MCREF_ADRESSE &
   LINK CLICIECLE OF IFCLIENT, &
        CLICLE OF IFCLIENT, &
        CIECLE OF IFCLIENT &
     TO CLICIECLE, &
        CLICLE, &
        CIECLE                          OF CRCLI_CIE OPTIONAL &
   LINK CLCTAXTYPTPS OF CRCLI_CIE, &
        CLCTAXCODTPS OF CRCLI_CIE  &
     TO TAXCLETYP, &
        TAXCLECOD                       OF VTAX_DSC ALIAS A_TAXFED OPTIONAL &
   LINK CLCTAXTYPTVP OF CRCLI_CIE, &
        CLCTAXCODTVP OF CRCLI_CIE  &
     TO TAXCLETYP, &
        TAXCLECOD                       OF VTAX_DSC ALIAS A_TAXPRO OPTIONAL &
  LINK CIECLE OF IFFACTURE, &
       FCECLE OF IFFACTURE  &
    TO CIECLE, &
       FCECLE                           OF IFFACTURE_DETAIL &
  LINK CIECLE OF IFFACTURE, &
       CRRCLENUM OF IFFACTURE_DETAIL, &
       CRRCLEANN OF IFFACTURE_DETAIL, &
       BLCCLE OF IFFACTURE_DETAIL &
    TO CIECLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       BLCCLE                           OF IFBLOC

CHOOSE CIECLE PARM, FCECLE PARM

SELECT IF (FCEFIN  = "O" AND FCEIMP  = "O")  OR &
           FCEANNUL = "O"

SORT ON FCECLE

DEFINE TMP-M3-PROD INTEGER SIZE 4 = FDEM3PROD
DEFINE TMP-TOT-FOB INTEGER SIZE 4 = FLOOR(FDEM3PROD * FDEPRIXFOB / 100 + .5 )
DEFINE TMP-TOT-FAS INTEGER SIZE 4 = FLOOR(FDEM3PROD * FDEPRIXFAS / 100 + .5 )
DEFINE TMP-TOTAL INTEGER SIZE 4 = FDETOTAL
DEFINE TMP-COMPT INTEGER SIZE 4 = 1
DEFINE TMP-T-M INTEGER SIZE 4 = BLCTFINAL OF IFBLOC
DEFINE D_PCTFED FLOAT SIZE 8 = TAXPCT OF A_TAXFED
DEFINE D_PCTPRO FLOAT SIZE 8 = TAXPCT OF A_TAXPRO

SORT ON FCECLE
SET SUBFILE NAME WIF323 AT FCECLE
REPORT SUMMARY &
        CIECLE &
        FCECLE          &
        FCEADR1 &
        FCEADR2 &
        FCEADR3 &
        FCECP &
        FCEDAT &
        FCETOTAL &
        FCECOM1 &
        FCECOM2 &
        CLTCLE OF IFFACTURE &
        CRRCLENUM OF IFFACTURE &
        CRRCLEANN OF IFFACTURE &
        FCECAT OF IFFACTURE  &
        COEDATDEB OF IFFACTURE &
        TMP-M3-PROD SUB RESET AT FCECLE &
        TMP-TOT-FOB SUB RESET AT FCECLE &
        TMP-TOT-FAS SUB RESET AT FCECLE &
        TMP-TOTAL   SUB RESET AT FCECLE &
        FCEMNTTPSFED OF IFFACTURE &
        FCEMNTTVQPRO OF IFFACTURE &

        TMP-T-M     SUB RESET AT FCECLE &
        FDEPRIXFOB    &
        FDEPRIXFAS    &
        TMP-COMPT   SUB RESET AT FCECLE &
        CLILNG OF CRCLIENT &
        CLINOM OF CRCLIENT &
        RADADR1 OF MCREF_ADRESSE &
        RADADR2 OF MCREF_ADRESSE &
        RADADR3 OF MCREF_ADRESSE &
        RADCP OF MCREF_ADRESSE &
        CLCEXPTVP OF CRCLI_CIE &
        D_PCTFED &
        D_PCTPRO &
        DEVCLE OF CRCLIENT

BUILD if323za
;
SET PAGE LENGTH 60 WIDTH 132
SET FORMFEED

ACCESS *WIF323 &
  LINK CIECLE OF WIF323, &
       FCECLE OF WIF323  &
    TO CIECLE, &
       FCECLE                           OF IFFACTURE_AJUST OPTIONAL &
  LINK CIECLE OF WIF323, &
       CLTCLE OF WIF323, &
       CRRCLENUM OF WIF323, &
       CRRCLEANN OF WIF323  &
    TO CIECLE, &
       CLTCLE, &
       CRRCLENUM, &
       CRRCLEANN                        OF IFCONTRAT &
  LINK CIECLE OF WIF323, &
       CLTCLE OF WIF323, &
       CRRCLENUM OF WIF323, &
       CRRCLEANN OF WIF323, &
       FCECAT OF WIF323, &
       COEDATDEB OF WIF323 &
    TO CIECLE, &
       CLTCLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       CATCLE, &
       COEDATDEB                        OF IFCONTRAT_LIGNE &
  LINK CIECLE OF WIF323, &
       CRRCLENUM OF WIF323, &
       CRRCLEANN OF WIF323  &
    TO CIECLE, &
       CRRCLENUM, &
       CRRCLEANN                        OF IFCARRIERE &
  LINK CIECLE OF WIF323 &
    TO CIECLE                           OF MCCOMPAGNIE


DEF TMP-GR CHARACTER SIZE 58 = "BLOCS BRUTS DE GRANITE: " + CRRDSC &
                                IF CLILNG = "F" ELSE TRUNC(CRRDSC) + &
                                " ROUGH GRANIT BLOCKS"
DEF TMP-LIST CHARACTER SIZE 58 = "(VOIR LISTE CI-JOINTE)" IF CLILNG = "F" &
                                ELSE "(SEE LIST ATTACHED)"
DEF TMP-M3-LG CHARACTER SIZE 10 = RJ(ASCII(TMP-M3-PROD)[1:10])
DEF TMP-M3-LG2 CHARACTER SIZE 10 = LJ(TMP-M3-LG[1:8] + "." + TMP-M3-LG[9:2])
DEF TMP-MT-LG CHARACTER SIZE 10 = RJ(ASCII(TMP-T-M)[1:10])
DEF TMP-MT-LG2 CHARACTER SIZE 10 = LJ(TMP-MT-LG[1:8] + "." + TMP-MT-LG[9:2])
DEF TMP-LG3 CHARACTER SIZE 58 = "POUR UN TOTAL DE " + ASCII(TMP-COMPT) + " BLOCS ET " &
                + TRUNC(TMP-M3-LG2) + " M3 (" + TRUNC(TMP-MT-LG2) + " T.M)" &
                IF CLILNG = "F" ELSE &
                "FOR A TOTAL OF " + ASCII(TMP-COMPT) + " BLOCKS AND " &
                + TRUNC(TMP-M3-LG2) + " CUBMT (" + TRUNC(TMP-MT-LG2) + " M.T)"
DEF TMP-FOB-LG CHARACTER SIZE 10 = RJ(ASCII(FDEPRIXFOB)[1:10])
DEF TMP-FOB-LG2 CHARACTER SIZE 10 = LJ(TMP-FOB-LG[1:8] + "." + TMP-FOB-LG[9:2])
DEF TMP-LG4 CHARACTER SIZE 58 = TRUNC(TMP-M3-LG2) + " M3 DE GRANITE " + &
                                TRUNC(CRRDSC) + " A $" + TRUNC(TMP-FOB-LG2) &
                                + " / M3" IF CLILNG = "F" ELSE &
                                TRUNC(TMP-M3-LG2) + " CUBMT OF GRANIT    " + &
                                TRUNC(CRRDSC) + " $" + TRUNC(TMP-FOB-LG2) &
                                + " / CUBMT"
DEF TMP-LG5  CHARACTER SIZE 58 = "FOB CARRIERE" IF CLILNG = "F" &
                                ELSE "FOB QUARRY"
DEF TMP-FAS CHARACTER SIZE 4 = "FAS:" IF FDEPRIXFAS <> 0
DEF TMP-LG6  CHARACTER SIZE 58 = "AJUSTEMENTS:" IF CLILNG = "F" &
                                ELSE "ADJUSTMENTS:"
DEF TMP-LG7  CHARACTER SIZE 58 = "SOUS TOTAL"  IF CLILNG = "F" &
                                ELSE "SUBTOTAL"
DEF TMP-LG71 CHARACTER SIZE 5 = "TPS "
DEF TMP-LG72 CHARACTER SIZE 5 = "TVQ "

DEF TMP-LG73 CHARACTER SIZE 58 = "TOTAL DE CETTE FACTURE"  IF CLILNG = "F" &
                                ELSE "TOTAL OF THIS INVOICE"
DEF TMP-LG8  CHARACTER SIZE 58 = "COMMENTAIRES:" IF CLILNG = "F" &
                                ELSE "COMMENTS:"

DEF TMP-SOUS-TOTAL = FCETOTAL + FCEMNTTPSFED
DEF TMP-GRAND-TOTAL = TMP-SOUS-TOTAL + FCEMNTTVQPRO

PAGE HEADING TAB 3 "." & ;----------------- IMP -- FONT-LQ &
SKIP 9  TAB 72 FCECLE      &
SKIP 1 &
        TAB 10 CLINOM     &
SKIP 1 &
        TAB 10 RADADR1 &
        TAB 51 FCEADR1    &
SKIP 1 &
        TAB 10 RADADR2 &
        TAB 51 FCEADR2    &
SKIP 1 &
        TAB 10 RADADR3 &
        TAB 51 FCEADR3    &
SKIP 1 &
        TAB 10 RADCP &
        TAB 51 FCECP  &
SKIP 3 &
        TAB 11 CONCONDITION1 &
SKIP 1 &
        TAB 11 CONCONDITION2 &
SKIP 2 &
        TAB 61 FCEDAT &
SKIP 1 &
        TAB 10 COEREFCLI &
        TAB 26 "PROV:" CLCEXPTVP &
SKIP 2 &
        TAB 10 "NUMERO INSCRIPTION TPS :" CIEENRTPS &
SKIP 4 &
        TAB 12 TMP-GR &
SKIP 2 &
        TAB 12 TMP-LIST &
SKIP 3 &
        TAB 12 TMP-LG3 &
SKIP 3 &
        TAB 12 TMP-LG4 &
        TAB 69 TMP-TOT-FOB FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 3 &
        TAB 12 TMP-LG5 &
SKIP 2 &
        TAB 12 TMP-FAS &
        TAB 18 FDEPRIXFAS BWZ  &
        TAB 69 TMP-TOT-FAS FLOAT "$" PIC "^^^^,^^^.^^" BWZ &
SKIP 2 &
        TAB 12 TMP-LG6 &
SKIP 1
REPORT &
        TAB 26 FAJDESCAJUST &
        TAB 69 FAJAJUST FLOAT "$" PIC "^^^^,^^^.^^"
FINAL FOOTING &
SKIP 2 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 40 TMP-LG7  &
        TAB 69 FCETOTAL FLOAT "$" PIC "^^^^,^^^.^^" &
SKIP 1 &
        TAB 40 TMP-LG71 &
        TAB 45 D_PCTFED PIC "^^^.^^%" OUTPUT SCALE 4 &
        TAB 69 FCEMNTTPSFED FLOAT "$" PIC "^^^^,^^^.^^"&
SKIP 1 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 69 TMP-SOUS-TOTAL FLOAT "$" PIC "^^^^,^^^.^^"&
SKIP 1 &
        TAB 40 TMP-LG72 &
        TAB 45 D_PCTPRO PIC "^^^.^^%" OUTPUT SCALE 4 &
        TAB 69 FCEMNTTVQPRO FLOAT "$" PIC "^^^^,^^^.^^"&
SKIP 1 &
        TAB 69 "___________" &
SKIP 1 &
        TAB 40 TMP-LG73 &
        TAB 69 TMP-GRAND-TOTAL FLOAT "$" PIC "^^^^,^^^.^^"&
SKIP 2 &
        TAB 12 TMP-LG8 &
SKIP 1 &
        TAB 12 FCECOM1 &
SKIP 1 &
        TAB 12 FCECOM2 &
SKIP 2 &
        TAB 12 "**** CECI EST UN DUPLICATA DE LA FACTURE ****" ;--IMP--NOFONT-LQ

BUILD if323zb
;
SET PAGE NUMBER 2

ACCESS *WIF323 &
  LINK CIECLE OF WIF323, &
       FCECLE OF WIF323  &
    TO CIECLE, &
       FCECLE                           OF IFFACTURE_DETAIL &
  LINK CIECLE OF WIF323, &
       CRRCLENUM OF IFFACTURE_DETAIL, &
       CRRCLEANN OF IFFACTURE_DETAIL, &
       BLCCLE OF IFFACTURE_DETAIL &
    TO CIECLE, &
       CRRCLENUM, &
       CRRCLEANN, &
       BLCCLE                           OF IFBLOC &
  LINK CIECLE OF WIF323, &
       EXNCLE OF IFBLOC  &
    TO CIECLE, &
       EXNCLE                           OF IFEXPEDITION OPTIONAL &
  LINK CIECLE OF WIF323, &
       TSPCLE OF IFEXPEDITION &
    TO CIECLE, &
       TSPCLE                           OF IFTRANSPORTEUR OPTIONAL

DEFINE TMP-FOB-LIG INTEGER SIZE 4 = FLOOR(FDEPRIXFOB * FDEM3PROD / 100 + .5 )
DEFINE TMP-FAS-LIG INTEGER SIZE 4 = FLOOR(FDEPRIXFAS * FDEM3PROD / 100 + .5 )

USE uspitch NOLIST

SORT ON FCECLE &
     ON CRRCLENUM OF IFFACTURE_DETAIL &
     ON CRRCLEANN OF IFFACTURE_DETAIL &
     ON BLCCLE OF IFFACTURE_DETAIL

PAGE HEADING TAB 3 "." & ;-- IMP -- FONT-LQ &
        TAB 69 "PAGE:" SYSPAGE &
SKIP 11 TAB 72 FCECLE      &
SKIP 1 &
        TAB 10 CLINOM     &
SKIP 2 &
        TAB 12 "NO CONTRAT:" &
        TAB 25 CLTCLE &
        TAB 27 CRRCLENUM &
        TAB 29 CRRCLEANN &
SKIP 1 &
        TAB 12 "CATEGORIE :" &
        TAB 25 FCECAT &
SKIP 1 &
        TAB 12 "DATE DEBUT:" &
        TAB 25 COEDATDEB &
SKIP 4 &
        TAB 61 FCEDAT PITCH-12 &  ; -------- IMP -- PITCH-12 &
SKIP 3 &
             TAB 6 "NO"                   &
             TAB 15 "DIMENSIONS "          &
             TAB 30 "TOTAL"                &
             TAB 39 "PRIX"                 &
             TAB 48 "TOTAL"                &
             TAB 59 "PRIX"                 &
             TAB 68 "TOTAL"                &
             TAB 78 "NO"                   &
             TAB 84 "NO"                   &
             TAB 91 "TOTAL"                &
SKIP 1 &
             TAB 5 "BLOC"                 &
             TAB 13 "LONG HAUT LARG"       &
             TAB 31 "M3"                   &
             TAB 40 "FOB"                  &
             TAB 49 "FOB"                  &
             TAB 60 "FAS"                  &
             TAB 69 "FAS"                  &
             TAB 78 "EXP"                  &
             TAB 83 "TRANS"                  &
             TAB 91 "LIGNE"                &
SKIP 1 &
             TAB 3 "_________"            &
             TAB 14 "_____________"        &
             TAB 29 "______"               &
             TAB 37 "_______"              &
             TAB 48 "_______"              &
             TAB 59 "_______"              &
             TAB 68 "_______"              &
             TAB 77 "______"               &
             TAB 83 "______"               &
             TAB 90 "________"             &
SKIP 2
REPORT &
        TAB 4 CRRCLENUM &
        TAB 6 CRRCLEANN &
        TAB 7 "-" &
        TAB 8 BLCCLE &
        FDEDIMLON &
        FDEDIMHAU     &
        FDEDIMLAR     &
        FDEM3PROD     &
        FDEPRIXFOB    PIC " ^^^^.^^" &
        TMP-FOB-LIG    PIC "^^^^^^.^^" &
        FDEPRIXFAS    PIC " ^^^^.^^"&
        TMP-FAS-LIG   PIC "^^^^^^.^^"  &
        EXNCLE &
        TSPCLE &
        FDETOTAL    PIC "^^^^^^.^^"
FOOTING AT FCECLE &
        TAB 3 "_________"          &
        TAB 28 "_______"            &
        TAB 48 "_______"            &
        TAB 68 "_______"            &
        TAB 90 "________"            &
SKIP 1  &
        TAB 1 COUNT PIC "^^^^" "Blocs"       &
        TAB 20 "Total:"        &
        TAB 29 BLCM3TOT SUBTOTAL  &
        TAB 47 TMP-FOB-LIG SUBTOTAL PIC "^^^^^^.^^"  &
        TAB 68 TMP-FAS-LIG SUBTOTAL PIC "^^^^^^.^^" &
        TAB 91 FDETOTAL SUBTOTAL PIC "^^^^^^.^^"  &
SKIP 3

BUILD if323zc
