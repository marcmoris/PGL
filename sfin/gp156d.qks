;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Détail du format de présentation d'opération - Domaine
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp156d ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPFORMATS  , &
                        GPFOR_DETAILS

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "GP156D"            ; Nom de l'écran

USE gp156d.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


FILE GPFORMATS              MASTER

FILE GPFOR_DETAILS          MASTER


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_DOMAINE_OP       PRIMARY &
                            NOITEMS &
                            OCCURS 22

  ACCESS  VIA     FRMCLE, &
                  FRDCLE, &
                  FDUCLE, &
                  FDUSEQ  &
          USING   FRMCLE OF GPFOR_DETAILS, &
                  FRDCLE OF GPFOR_DETAILS, &
                  FRDCLE OF GPFOR_DETAILS, &
                  FDUSEQ OF GPFRD_DOMAINE_OP  &
          REQUEST FDUSEQ  &
          ORDERBY FRDCLE, &
                  FDUCLE, &
                  FDUSEQ


  ACCESS  VIA     FRMCLE, &
                  FRDCLE  &
          USING   FRMCLE OF GPFOR_DETAILS, &
                  FRDCLE OF GPFOR_DETAILS  &
          ORDERBY FRDCLE, &
                  FDUCLE, &
                  FDUSEQ

  ITEM FRMCLE OF GPFRD_DOMAINE_OP  INITIAL FRMCLE OF GPFOR_DETAILS
  ITEM FRDCLE OF GPFRD_DOMAINE_OP  INITIAL FRDCLE OF GPFOR_DETAILS
  ITEM FDUREF OF GPFRD_DOMAINE_OP  INITIAL 0
  ITEM FDUCLE OF GPFRD_DOMAINE_OP  INITIAL FRDCLE OF GPFOR_DETAILS
  ITEM FRDTYP OF GPFRD_DOMAINE_OP  INITIAL FRDTYP OF GPFOR_DETAILS
  ITEM FDUTYP OF GPFRD_DOMAINE_OP  INITIAL "C"

;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_DOMAINE_OP       DESIGNER &
                            ALIAS GPFRD_DOMAINE_OP_3

FILE GPFRD_DOMAINE_OP       DESIGNER &
                            ALIAS GPFRD_DOMAINE_OP_4

FILE GPFOR_DETAILS          DESIGNER &
                            ALIAS GPFOR_DETAILS_2

FILE GPFRD_SOMME            DESIGNER


;--------------------------------------------------------------------------
; Validation du type de format de présentation et lecture de sa description
;--------------------------------------------------------------------------
DEFINE    D_FRDTYP CHARACTER SIZE 16 = "FRDTYP"
TEMPORARY T_FRDTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_FRDTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_FRDTYP, &
                FRDTYP OF GPFOR_DETAILS


;--------------------------
; Fichier unité de travail
;--------------------------
TEMPORARY T_UNTCLE    CHARACTER SIZE 4 ; Popup sur les comptes.

FILE GPUNITE_TRAVAIL        REFERENCE

  ACCESS  VIA   UNTCLE  &
          USING UNTCLE OF GPFRD_DOMAINE_OP


;-------------------------------------------------------------------------------
;
TEMPORARY T_FRMCLE   CHARACTER SIZE 4
TEMPORARY T_FRDCLE     INTEGER UNSIGNED SIZE 4
TEMPORARY T_FDUREF     INTEGER UNSIGNED SIZE 2
TEMPORARY T_FDUCLE     INTEGER UNSIGNED SIZE 2
TEMPORARY T_FDUSEQ     INTEGER UNSIGNED SIZE 2
TEMPORARY T_UNTREF     INTEGER SIZE 4
TEMPORARY T_UNTAJO     INTEGER UNSIGNED SIZE 2

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Détail du format de présentation - Ligne calculée " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19)(,,25)

FIELD FRMCLE OF GPFORMATS                             &
      HIDDEN                                          &
      DISPLAY

FIELD FRMDSC OF GPFORMATS                             &
      DISPLAY

SKIP

TITLE &
@IF FRANCAIS
"Unité de" &
@ELSE
      " %%% " &
@ENDIF
      AT ,18

SKIP

TITLE &
@IF FRANCAIS
"Ligne   Type    mesure     Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

SKIP

ALIGN (,,3) (,,12) (,,20) (,,30)

FIELD FRDCLE OF GPFOR_DETAILS                         &
      DISPLAY

FIELD FRDTYP OF GPFOR_DETAILS                         &
      DISPLAY

FIELD UNMCLE OF GPFOR_DETAILS                         &
      DISPLAY

FIELD FRDDSC OF GPFOR_DETAILS                         &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
"      Unité"     &
@ELSE
      " %%% " &
@ENDIF
      AT ,13

TITLE &
@IF FRANCAIS
"      Unité"     &
@ELSE
      " %%% " &
@ENDIF
      AT ,53

SKIP

TITLE &
@IF FRANCAIS
"Séquence       Travail     Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
"Séquence       Travail     Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,43

SKIP

ALIGN (4,3,4) (,,20) (,,34)

CLUSTER OCCUR WITH GPFRD_DOMAINE_OP  VERTICAL AT 12,1 FOR 1,40

FIELD FDUSEQ OF GPFRD_DOMAINE_OP                      &
      HIDDEN LABEL " "                                &
      REQUIRED NOCHANGE                               &
      LOOKUP NOTON GPFRD_DOMAINE_OP                   &
             VIA   FRMCLE,                            &
                   FRDCLE,                            &
                   FDUCLE,                            &
                   FDUSEQ                             &
             USING FRMCLE OF GPFOR_DETAILS,           &
                   FRDCLE OF GPFOR_DETAILS,           &
                   FRDCLE OF GPFOR_DETAILS,           &
                   FDUSEQ OF GPFRD_DOMAINE_OP         &
                   MESSAGE 875

FIELD UNTCLE    OF GPFRD_DOMAINE_OP                   &
      REQUIRED                                        &
      LOOKUP ON GPUNITE_TRAVAIL MESSAGE 866

FIELD FDUOPE    OF GPFRD_DOMAINE_OP                  &
      REQUIRED NOCHANGE

CLUSTER

DRAW 09,01 TO 09,80
DRAW 09,40 TO 23,40
;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les comptes.
;
;
PROCEDURE INPUT UNTCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNTCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp107 MODE F PASSING T_UNTCLE
    LET FIELDTEXT = TRUNC(T_UNTCLE)
  END

END


PROCEDURE EDIT UNTCLE
BEGIN
  IF UNTSTU OF GPUNITE_TRAVAIL <> "A"
  THEN ERROR 868 ;Le statut de l'unité de travail doit être actif.

  IF UNMCLE OF GPUNITE_TRAVAIL <> UNMCLE OF GPFOR_DETAILS
  THEN ERROR 883 ;L'unité de mesure de l'unité de travail diffère à celui
                 ;de la ligne de format

END



;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


PROCEDURE INTERNAL MAJ_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_OP_3 &
         VIA    FRMCLE,           &
                FDUCLE,           &
                FDUSEQ,           &
                FRDTYP            &
         USING  FRMCLE OF GPFRD_DOMAINE_OP, &
                FRDCLE OF GPFRD_DOMAINE_OP, &
                FDUSEQ OF GPFRD_DOMAINE_OP, &
                "F"
   BEGIN
      LET UNTCLE OF GPFRD_DOMAINE_OP_3 = UNTCLE OF GPFRD_DOMAINE_OP
      PUT GPFRD_DOMAINE_OP_3
   END
END


PROCEDURE INTERNAL AJOUT_FORMULE2
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_OP_4 &
         VIA    FRMCLE,                &
                FRDCLE                 &
         USING  FRMCLE    OF GPFRD_SOMME, &
                FRSREFLIG OF GPFRD_SOMME
   BEGIN
       LET FRMCLE    OF GPFRD_DOMAINE_OP_3 = FRMCLE      OF GPFRD_DOMAINE_OP_4
       LET FRDCLE    OF GPFRD_DOMAINE_OP_3 = FRDCLE      OF GPFRD_SOMME
       LET FDUREF    OF GPFRD_DOMAINE_OP_3 = FRSREFLIG   OF GPFRD_SOMME
       LET FDUCLE    OF GPFRD_DOMAINE_OP_3 = FDUCLE      OF GPFRD_DOMAINE_OP_4
       LET FDUSEQ    OF GPFRD_DOMAINE_OP_3 = FDUSEQ      OF GPFRD_DOMAINE_OP_4
       LET FRDTYP    OF GPFRD_DOMAINE_OP_3 = "F"
       LET FDUTYP    OF GPFRD_DOMAINE_OP_3 = FDUTYP      OF GPFRD_DOMAINE_OP_4
       LET UNTCLE    OF GPFRD_DOMAINE_OP_3 = UNTCLE      OF GPFRD_DOMAINE_OP_4

       IF  FRSOPE OF GPFRD_SOMME     = "+"
       THEN LET FDUOPE    OF GPFRD_DOMAINE_OP_3 = FDUOPE OF GPFRD_DOMAINE_OP_4
       ELSE BEGIN
            IF FDUOPE OF GPFRD_DOMAINE_OP_4 = "+"
            THEN LET FDUOPE OF GPFRD_DOMAINE_OP_3 = "-"
            ELSE LET FDUOPE OF GPFRD_DOMAINE_OP_3 = "+"
       END
       PUT GPFRD_DOMAINE_OP_3 RESET
   END
END


PROCEDURE INTERNAL RECREER_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_OP_4 &
         VIA    FRMCLE,                &
                FRDTYP                 &
         USING  FRMCLE OF GPFRD_DOMAINE_OP, &
                "F"
   BEGIN
      DELETE GPFRD_DOMAINE_OP_4
      PUT    GPFRD_DOMAINE_OP_4
   END

   WHILE RETRIEVING GPFRD_SOMME              &
         VIA     FRMCLE                      &
         USING   FRMCLE OF GPFRD_DOMAINE_OP  &
         ORDERBY FRMCLE,                     &
                 FRDCLE,                     &
                 FRSREFLIG
   BEGIN
      DO AJOUT_FORMULE2
   END
END

PROCEDURE INTERNAL DETRUIT_FORMULE_2
BEGIN
   LET T_UNTREF = 0
   WHILE RETRIEVING GPFRD_DOMAINE_OP_4       &
         VIA   FRMCLE,                        &
               FRDCLE,                        &
               FDUREF                         &
         USING FRMCLE OF GPFRD_DOMAINE_OP_3, &
               FRDCLE OF GPFRD_DOMAINE_OP_3, &
               FDUREF OF GPFRD_DOMAINE_OP_3
   BEGIN
      LET T_UNTREF = T_UNTREF + 1
   END

   IF T_UNTREF = 1
   THEN BEGIN
        GET GPFRD_SOMME         &
            VIA    FRMCLE,      &
                   FRDCLE,      &
                   FRSREFLIG    &
            USING  FRMCLE OF GPFRD_DOMAINE_OP_3, &
                   FRDCLE OF GPFRD_DOMAINE_OP_3, &
                   FDUREF OF GPFRD_DOMAINE_OP_3 OPTIONAL
        IF ACCESSOK
        THEN BEGIN
             DELETE GPFRD_SOMME
             PUT GPFRD_SOMME
        END
   END

   LET T_UNTREF = 0
   WHILE RETRIEVING GPFRD_DOMAINE_OP_4       &
         VIA   FRMCLE,                        &
               FRDCLE                         &
         USING FRMCLE OF GPFRD_DOMAINE_OP_3, &
               FRDCLE OF GPFRD_DOMAINE_OP_3
   BEGIN
      LET T_UNTREF = T_UNTREF + 1
   END

   IF T_UNTREF = 1
   THEN BEGIN
        GET GPFOR_DETAILS_2     &
            VIA    FRMCLE,      &
                   FRDCLE       &
            USING  FRMCLE OF GPFRD_DOMAINE_OP_3, &
                   FRDCLE OF GPFRD_DOMAINE_OP_3 OPTIONAL
        IF ACCESSOK
        THEN BEGIN
             DELETE GPFOR_DETAILS_2
             PUT GPFOR_DETAILS_2
        END
   END
END


PROCEDURE INTERNAL DETRUIT_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_OP_3     &
         VIA   FRMCLE,                      &
               FDUCLE,                      &
               FDUSEQ,                      &
               FRDTYP                       &
         USING FRMCLE OF GPFRD_DOMAINE_OP, &
               FDUCLE OF GPFRD_DOMAINE_OP, &
               FDUSEQ OF GPFRD_DOMAINE_OP, &
               "F"
   BEGIN
      DO DETRUIT_FORMULE_2
      DELETE GPFRD_DOMAINE_OP_3
      PUT GPFRD_DOMAINE_OP_3
   END
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GPFRD_DOMAINE_OP
  BEGIN
     ;
     ; Validation de la destruction
     ;
     IF     DELETEDRECORD OF GPFRD_DOMAINE_OP       &
        AND TZ_SECDEL = "0"
     THEN ERROR 1
     ;
     ; Validation de la modification.
     ;
     IF     ALTEREDRECORD     OF GPFRD_DOMAINE_OP   &
        AND NOT NEWRECORD     OF GPFRD_DOMAINE_OP   &
        AND NOT DELETEDRECORD OF GPFRD_DOMAINE_OP   &
        AND TZ_SECMOD = "0"
     THEN ERROR 2

     IF     ALTEREDRECORD OF GPFRD_DOMAINE_OP AND &
        NOT NEWRECORD     OF GPFRD_DOMAINE_OP AND &
        NOT DELETEDRECORD OF GPFRD_DOMAINE_OP
     THEN DO MAJ_FORMULE

     IF DELETEDRECORD     OF GPFRD_DOMAINE_OP
     THEN DO DETRUIT_FORMULE
  END
END

PROCEDURE POSTUPDATE
BEGIN
   DO RECREER_FORMULE
END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************** Détail du format de présentation - Ligne calculée ***************
*                                                                              *
* Format........: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                         *
*                                                                   Unité de   *
* Ligne   Type  Description                                          mesure    *
* xxxxx    x    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxx     *
********************************************************************************
*                 Unité                *                  Unité                *
* Séquence       Travail     Opérateur *  Séquence       Travail     Opérateur *
*                                      *                                       *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
*  xxxxx          xxxx           x     *   xxxxx          xxxx           x     *
********************************************************************************
@ENDIF

