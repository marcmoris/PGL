;/T/ Disponibilités des produits par entrepôt
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 4 mai 1994
;
;    Description...: Ce rapport présente les informations des produits 
;                    par entrepôt.
;
;    Paramètres....: Compagnie 
;                    Code de produit (Borne de début)
;                    Code de produit (Borne de fin)
;                    Catégorie       (Une, plusieurs ou tous)
;                    Type            (D=divers, F=frais,I=inventorié, 
;                                     N=non inventorié,<retour>=tous)
;                    Statut          (A=actif,I=inactif,<retour>=tous)
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS ARPRODUIT   &
   ;
   ; Enrepôts du produit
   ;
  LINK CIECLE OF ARPRODUIT, &
       PRDCLE OF ARPRODUIT  &
    TO CIECLE, &
       PRDCLE OF ARPRD_ENT   &

   ;
   ; Nom de l'entrepôt
   ;
  LINK CIECLE OF ARPRD_ENT, &
       ENTCLE OF ARPRD_ENT  &
    TO CIECLE, &
       ENTCLE OF ARENTREPOT    &

   ;
   ; Nom de la localisation de l'entrepôt si elle existe.
   ;
  LINK CIECLE OF ARPRD_ENT, &
       ENTCLE OF ARPRD_ENT, &
       ENLCLE OF ARPRD_ENT  &
    TO CIECLE, &
       ENTCLE, &
       ENLCLE OF ARENT_LOCAL   OPTIONAL &

   ;
   ; Statut du produit
   ;
  LINK ("PRDSTU",                 &
         PRDSTU OF ARPRODUIT)     &
     TO XELNOM,                   &
        CBICLE OF VCBI_DSC   ALIAS A_CBI_PRDSTU OPTIONAL & 

   ;
   ; Type de produit
   ;
  LINK ("PRDTYP",                 &
         PRDTYP OF ARPRODUIT)     &
     TO XELNOM,                   &
        CBICLE OF VCBI_DSC   ALIAS A_CBI_PRDTYP OPTIONAL 



CHOOSE CIECLE PARM,       & ; Compagnie du menu
       PRDCLE PARM RANGE, & ; Code de produit
       PRDCAT PARM          ; Catégorie de produit


DEFINE D_PRDTYP CHARACTER SIZE 6 = PARM UPSHIFT ; Type de produit

DEFINE D_PRDSTU CHARACTER SIZE 2 = PARM UPSHIFT ; Statut du produit


SELECT ARPRODUIT IF (    ( 0 <> INDEX (D_PRDTYP,PRDTYP) OR D_PRDTYP = "") &
                     AND ( 0 <> INDEX (D_PRDSTU,PRDSTU) OR D_PRDSTU = "") &
                    )

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Disponibilités des produits par entrepôt"
@ELSE
       "%%%Disponibilités des produits par entrepôt"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       ""
@ELSE
       ""
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR343Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


 ;
 ; Statuts de produit demandés
 ;
DEFINE D_CBIDSC1 CHARACTER SIZE 15 = "Actif " &
  IF 0 <> INDEX (D_PRDSTU,"A") OR  D_PRDSTU = "" &
    ELSE ""

DEFINE D_CBIDSC2 CHARACTER SIZE 15 = "Inactif "&
  IF 0 <> INDEX (D_PRDSTU,"I") OR  D_PRDSTU = "" &
    ELSE ""

DEFINE D_CBIDSCDEM CHARACTER SIZE 70 = &
  PACK (TRUNCATE(D_CBIDSC1) + " " + &
        TRUNCATE(D_CBIDSC2))  


DEFINE D_CBIDSC_PRDSTU CHARACTER SIZE 10 = CBIDSC OF A_CBI_PRDSTU


USE usflgdet NOLIST ; Flag Oui/Non

;
;Indique s'il y a une validation du maximum
;
DEFINE D_PREFLGGESMAX CHARACTER SIZE 3 &
        = DZ_FLGAFFO IF PREFLGGESMAX OF ARPRD_ENT <> "0" ELSE &
          DZ_FLGAFFN 

;
;Indique s'il y a gestion des quantités négatives
;
DEFINE D_PREFLGAUTNEG CHARACTER SIZE 3 &
        = DZ_FLGAFFO IF PREFLGAUTNEG OF ARPRD_ENT <> "0" ELSE &
          DZ_FLGAFFN 



SORT ON CIECLE OF ARPRODUIT    & ; Compagnie
     ON PRDTYP OF ARPRODUIT    & ; Type de produit
     ON PRDCLE OF ARPRODUIT    & ; Code de produit
     ON ENTCLE OF ARPRD_ENT ; Entrepôt
     


USE ustitstd NOLIST 
  SKIP 2 &
@IF FRANCAIS
TAB 001 "Type..............:" &
@ELSE
TAB 001 "%%%Type..............:" &
@ENDIF

TAB 021 CBIDSC OF A_CBI_PRDTYP &
  SKIP &
@IF FRANCAIS
         TAB 001 "Statuts demandés..:" &
@ELSE
         TAB 001 "%%%tuts demandés..:" &
@ENDIF

TAB 021 D_CBIDSCDEM &
  SKIP 2 &
@IF FRANCAIS
         TAB 001 "Code de                                                                    Unité  " &
@ELSE
         TAB 001 "%%%" &
@ENDIF
  SKIP &
@IF FRANCAIS
         TAB 001 "produit                   Description                  Catégorie   Statut  mesure                   Entrepot" &
@ELSE
         TAB 001 "%%%" &
@ENDIF
  SKIP   TAB 001 "------------ ----------------------------------------  --------- --------- ------ --------------------------------------------" &
  SKIP 2 

HEADING AT PRDCLE &
         TAB 001 PRDCLE  OF ARPRODUIT &  
         TAB 014 PRDDSC1 OF ARPRODUIT &
         TAB 058 PRDCAT  OF ARPRODUIT &
         TAB 066 D_CBIDSC_PRDSTU      &
         TAB 077 PRDUNI OF ARPRODUIT  &
         TAB 083 ENTCLE OF ARPRD_ENT  &
         TAB 088 ENTNOM OF ARENTREPOT &   
@IF FRANCAIS
  SKIP 2 TAB 001 "----------- Quantité --------------  Point de  Validation    Quantité  Quantité       Coût    Dernier                           " &
  SKIP 1 TAB 001 "   demande   commande   disponible   commande  du maximum     maximum  négative      moyen    coûtant  Localisation             " &
@ELSE
  SKIP 2 TAB 001 "%%%%------- Quantité --------------  Point de  Validation    Quantité  Quantité       Coût    Dernier                           " &
  SKIP 1 TAB 001 "%%%%emande   commande   disponible   commande  du maximum     maximum  négative      moyen    coûtant  Localisation             " &
@ENDIF
  SKIP 1 TAB 001 "----------- ----------- ----------- ---------- ---------- -----------  --------  ---------- ---------- -------------------------" 
                                                                                                                                
REPORT &
TAB 002 PREQTEDEM OF ARPRD_ENT &
  PICTURE "^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 014 PREQTECOM OF ARPRD_ENT &
  PICTURE "^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 026 PREQTEPHY OF ARPRD_ENT &
  PICTURE "^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 037 PREPOICOM OF ARPRD_ENT &
  PICTURE "^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 051 D_PREFLGGESMAX &
TAB 059 PREQTEMAX OF ARPRD_ENT &
  PICTURE "^^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 074 D_PREFLGAUTNEG &
TAB 082 PRECOUMYN OF ARPRD_ENT &
  PICTURE "^^^^.^^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
TAB 093 PREDERCOU OF ARPRD_ENT &
  PICTURE "^^^^.^^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
TAB 104 ENLCLE OF ARPRD_ENT &
TAB 110 ENLDSC OF ARENT_LOCAL
 

FOOTING AT PRDCLE &
  SKIP 1 &
TAB 001 "----------- ----------- -----------" &
  SKIP &
TAB 001 PREQTEDEM OF ARPRD_ENT SUBTOTAL &
  PICTURE "^^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 013 PREQTECOM OF ARPRD_ENT SUBTOTAL &
  PICTURE "^^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
TAB 025 PREQTEPHY OF ARPRD_ENT SUBTOTAL &
  PICTURE "^^,^^^.^^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 6       &
  BWZ                  &
  SKIP 2

FOOTING AT PRDTYP &
  SKIP PAGE

BUILD ar343z


@IF FORMAT 
         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20
12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
AR343                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                       Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                  Disponibilités des produits par entrepôt
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 

Type..............: xxxxxxxxxxxxxxx
Statuts demandés..: xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx 

Code de                                                                    Unité  
produit                   Description                  Catégorie   Statut  mesure                   Entrepot
------------ ----------------------------------------  --------- --------- ------ --------------------------------------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxx    xxxxxxxxx  xxxx  xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   
                                                                                  
----------- Quantité --------------  Point de  Validation    Quantité  Quantité       Coût    Dernier                           
   demande   commande   disponible   commande  du maximum     maximum  négative      moyen    coûtant  Localisation             
----------- ----------- ----------- ---------- ---------- -----------  --------  ---------- ---------- -------------------------
                                                                                                                                
 9,999.999-  9,999.999-  9,999.999- 9,999.999-    xxx     99,999.999-    xxx     99,999.99- 99,999.99- xxxx xxxxxxxxxxxxxxxxxxxx
 9,999.999-  9,999.999-  9,999.999- 9,999.999-    xxx     99,999.999-    xxx     99,999.99- 99,999.99- xxxx xxxxxxxxxxxxxxxxxxxx
----------- ----------- -----------                                                                                             
99,999.999- 99,999.999- 99,999.999-                                                                                             


xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxx    xxxxxxxxx  xxxx  xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     
                                                                                  
----------- Quantité --------------  Point de  Validation    Quantité  Quantité       Coût    Dernier                           
   demande   commande   disponible   commande  du maximum     maximum  négative      moyen    coûtant  Localisation             
----------- ----------- ----------- ---------- ---------- -----------  --------  ---------- ---------- -------------------------
                                                                                                                                
 9,999.999-  9,999.999-  9,999.999- 9,999.999-    xxx     99,999.999-    xxx     99,999.99- 99,999.99- xxxx xxxxxxxxxxxxxxxxxxxx
 9,999.999-  9,999.999-  9,999.999- 9,999.999-    xxx     99,999.999-    xxx     99,999.99- 99,999.99- xxxx xxxxxxxxxxxxxxxxxxxx
----------- ----------- -----------                                                                                             
99,999.999- 99,999.999- 99,999.999-                                                                                             

@ENDIF
