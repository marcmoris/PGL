;/T/ Type de fini (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1994-07-04
;
;    Description..: Ce panorama permet de saisir les types de fini de granit
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd153 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD153"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd153.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
; Type de fini
;

FILE PDTYPE_FINI PRIMARY NOITEM
  ACCESS VIA     CIECLE,   &
                 TYFCODFIN &
         USING   T_CIECLEMNU,             &
                 TYFCODFIN OF PDTYPE_FINI &
         REQUEST TYFCODFIN OF PDTYPE_FINI &
         ORDERBY TYFCODFIN

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 TYFCODFIN

         ITEM CIECLE OF PDTYPE_FINI INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_TYFCODFIN CHARACTER SIZE 16 = "TYFCODFIN"
TEMPORARY T_TYFCODFIN CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Type de fini " &
@ELSE
      " %%%Type de fini " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF

SKIP TO LINE 5

ALIGN (2,3,19)

FIELD TYFCODFIN    OF PDTYPE_FINI        &
        HIDDEN ID 05                     &
        REQUIRED NOCHANGE                &
        NUMERIC                          &
        LOOKUP NOTON PDTYPE_FINI         &
          VIA   CIECLE,                  &
                TYFCODFIN                &
          USING T_CIECLEMNU,             &
                TYFCODFIN OF PDTYPE_FINI &
          MESSAGE 2903 ; Ce type de fini existe déjà


SKIP 1


FIELD TYFDSCFRA001 OF PDTYPE_FINI        &
        HIDDEN ID 10

FIELD TYFDSCFRA002 OF PDTYPE_FINI        &
        ID SAME                          &
        NOLABEL


SKIP 1


FIELD TYFDSCANG001 OF PDTYPE_FINI        &
        HIDDEN ID 20

FIELD TYFDSCANG002 OF PDTYPE_FINI        &
        ID SAME                          &
        NOLABEL

SKIP 1

FIELD TYFFACBRU    OF PDTYPE_FINI        &
        HIDDEN ID 25

FIELD TYFFACPOL    OF PDTYPE_FINI        &
        HIDDEN ID 30


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT TYFCODFIN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd101 MODE F &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDTYPE_FINI &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDTYPE_FINI &
     AND NOT NEWRECORD     OF PDTYPE_FINI &
     AND NOT DELETEDRECORD OF PDTYPE_FINI &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************* Type de fini *********************************
*                                                                              *
* Code fini.....: xx                                                           *
*                                                                              *
* Desc française: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Desc anglaise : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
