;/T/ Ligne de cédule de déboursé     FOUCIECLE, FOUCLE, CAPCLE, CPCCLELIG
;
;/P/ Programmeur..: Chantal Martineau / Adapté pour PGL par Thomas BRENNEUR
;    Date Création: 2 Juillet 1993
;
;    Description..: Consultation des lignes de cédules de paiement d'un
;                   compte à payer.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;-------------------------------------------------------------------------------
SCREEN cp107 NOMODE NOACTION FIELDMARK &
             FROM 11,32 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_FOUCIECLE, &
                        T_FOUCLE   , &
                        T_CAPCLE   , &
                        T_CPCCLELIG

USE ustrasse NOLIST

USE ussectmp  NOLIST
"CP107"

USE cp107.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_FOUCIECLE   CHARACTER SIZE 4
TEMPORARY T_FOUCLE      CHARACTER SIZE 6
TEMPORARY T_CAPCLE      CHARACTER SIZE 15
TEMPORARY T_CPCCLELIG   INTEGER   SIZE 2

;-------------------------------------------------------------------------------

FILE CPCAP_CEDULE     PRIMARY &
                      OCCURS 10

 ACCESS   VIA     FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE     &
          USING   T_FOUCIECLE, &
                  T_FOUCLE   , &
                  T_CAPCLE     &
          ORDERBY FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE   , &
                  CPCCLELIG

;-------------------------------------------------------------------------------
;
;
; CONSULTATION DU SOLDE DE LA CÉDULE
;
;
FILE VCPC_SLD REFERENCE &
              NOITEM

 ACCESS   VIA     FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE   , &
                  CPCCLELIG  &
          USING   FOUCIECLE OF CPCAP_CEDULE, &
                  FOUCLE    OF CPCAP_CEDULE, &
                  CAPCLE    OF CPCAP_CEDULE, &
                  CPCCLELIG OF CPCAP_CEDULE

DEFINE  D_CPCMNTPYE FLOAT SIZE 8 &
        = V_CPCMNTESC OF VCPC_SLD &
        + V_CPCMNTPYE OF VCPC_SLD

;-------------------------------------------------------------------------------

USE ushilite NOLIST

DRAW 1,1 TO 13,49


SKIP 1

@IF FRANCAIS
 TITLE "Lig." AT ,3
 TITLE "Due" AT ,9
 TITLE "Mnt prévu" AT ,18
 TITLE "Mnt encaissé" AT ,34
@ELSE
 TITLE "Line" AT ,3
 TITLE "Due" AT ,9
 TITLE "Amt. Projected" AT ,16
 TITLE "Amt. Received" AT ,32
@ENDIF

SKIP

ALIGN(2,2,3) (,,6) (,,18) (,,34)

CLUSTER OCCURS WITH CPCAP_CEDULE

FIELD CPCCLELIG OF CPCAP_CEDULE &
      HIDDEN size 2 &
      LABEL " "

FIELD CPCDATDUE OF CPCAP_CEDULE FORMAT YYYYMMDD PATTERN "####/##/##"

FIELD CPCMNT OF CPCAP_CEDULE

FIELD D_CPCMNTPYE &
      PICTURE "^,^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END
;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CPCCLELIG = CPCCLELIG OF CPCAP_CEDULE
  RETURN
END

BUILD
@IF FORMAT

                                   *********************************************
                                   * Lig.  Due      Mnt prévu     Mnt encaissé *
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *  xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
                                   *********************************************
@ENDIF
