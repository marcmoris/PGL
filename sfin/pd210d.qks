;/T/ 2.3.1 Enregistrer la commande interne. - Coûts globaux
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-30
;
;    Description..: Sous-écran pour saisir les coûts globaux
;
;/M/ François Déry, 8 jhuin 1999
;       Suite à la conversion en ISQL, modifier la vue pour enlever le calcul
;       au cube pour le mettre dans l'écran.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210d ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD210D"


;-------------------------------------------------------------------------------
;
; Panorama d'aide
;

USE pd210d.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;

USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_COMTYPCMD       CHARACTER SIZE 16 = "COMTYPCMD"
TEMPORARY T_COMTYPCMD       CHARACTER SIZE 04
DEFINE    D_COMFRSCOUINC    CHARACTER SIZE 16 = "COMFRSCOUINC"
TEMPORARY T_COMFRSCOUINC    CHARACTER SIZE 04
DEFINE    D_COMFRSTRPTERINC CHARACTER SIZE 16 = "COMFRSTRPTERINC"
TEMPORARY T_COMFRSTRPTERINC CHARACTER SIZE 04
DEFINE    D_COMFRSTRPMARINC CHARACTER SIZE 16 = "COMFRSTRPMARINC"
TEMPORARY T_COMFRSTRPMARINC CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE MASTER


;-------------------------------------------------------------------------------
;
; Vue sur les ventilations
;

FILE vven_grp PRIMARY  &
              NOITEM   &
              NOAPPEND &
              NODELETE &
              OCCURS 8 TIMES

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   CIECLE           OF PDCOMMAN_INTERNE, &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  

;-------------------------------------------------------------------------------

temporary t_total_ven    float size 08 occurs with vven_grp
temporary t_total_unmprd float size 08 occurs with vven_grp
temporary t_total_fac2   float size 08 occurs with vven_grp

;-------------------------------------------------------------------------------
;
; Compte
;

FILE MCCOMPTE REFERENCE OCCURS WITH vven_grp

  ACCESS VIA     CPTCLE           &
         USING   CPTCLE           OF vven_grp



;-------------------------------------------------------------------------------
;
; Code bilingue (Type de commande)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_COMTYPCMD

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_COMTYPCMD,                          &
               COMTYPCMD        OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Code bilingue (Frais de courtage)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_COMFRSCOUINC

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_COMFRSCOUINC,                       &
               COMFRSCOUINC     OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Code bilingue (Frais de transport terrestre)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_COMFRSTRPTERINC

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_COMFRSTRPTERINC,                    &
               COMFRSTRPTERINC  OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Code bilingue (Frais de transport maritime)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_COMFRSTRPMARINC

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_COMFRSTRPMARINC,                    &
               COMFRSTRPMARINC  OF PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------

file pdventilation    designer
file pddetail_c_i     designer
file pdfac_imputation designer

;-------------------------------------------------------------------------------
DEFINE D_TOT_MC2 FLOAT SIZE 8 = ROUND( T_TOTAL_VEN / T_TOTAL_UNMPRD)
;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Prix globaux " &
@ELSE
      " %%%Prix globaux " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        FIXED


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        FIXED


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        FIXED


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        FIXED


DRAW 5,2 TO 5,79


SKIP TO LINE 7


ALIGN (02,03,19) (,,21) (47,48,64)


FIELD COMTYPCMD        OF PDCOMMAN_INTERNE &
        HIDDEN ID 15                       &
        REQUIRED


FIELD CBIDSC           OF A_CBI_COMTYPCMD  &
        DISPLAY                            &
        SIZE 19


FIELD COMMNTFOR        OF PDCOMMAN_INTERNE &
        HIDDEN ID SAME


ALIGN (02,03,19) (,,22)


FIELD COMFRSCOUINC     OF PDCOMMAN_INTERNE &
        HIDDEN ID 16                       &
        LOOKUP ON A_CBI_COMFRSCOUINC       &
          MESSAGE 3192 ; Code inexistant


FIELD CBIDSC           OF A_CBI_COMFRSCOUINC &
        DISPLAY


FIELD COMFRSTRPTERINC  OF PDCOMMAN_INTERNE &
        HIDDEN ID 17                       &
        LOOKUP ON A_CBI_COMFRSTRPTERINC    &
          MESSAGE 3192 ; Code inexistant


FIELD CBIDSC           OF A_CBI_COMFRSTRPTERINC &
        DISPLAY


FIELD COMFRSTRPMARINC  OF PDCOMMAN_INTERNE &
        HIDDEN ID 19                       &
        LOOKUP ON A_CBI_COMFRSTRPMARINC    &
          MESSAGE 3192 ; Code inexistant


FIELD CBIDSC           OF A_CBI_COMFRSTRPMARINC &
        DISPLAY


ALIGN (02,03,19)


FIELD COMPRIDET        OF PDCOMMAN_INTERNE &
        HIDDEN ID 21                       &
        SIZE 3 &
        DEFAULT "NON"

FIELD COMIMPMET    OF PDCOMMAN_INTERNE &
      HIDDEN         &
      LABEL "IMP/MET...:" &
      DEFAULT "IMP"


SKIP TO LINE 13

TITLE &
@IF FRANCAIS
      "Description          Compte     Prix total      Prix au M2  Montant facturé" &
@ELSE
      "%%%cription          Compte     Prix total      Prix au M2  Montant facturé" &
@ENDIF
      AT ,3


SKIP TO LINE 14


ALIGN (02,02,03) (,,24) (,,31) (,,47) (,,63)


CLUSTER OCCURS WITH vven_grp ID BASE 50


FIELD CPTDSCABRFRA     OF MCCOMPTE         &
        HIDDEN                             &
        LABEL " "                          &
        DISPLAY


FIELD CPTCLE           OF vven_grp         &
        NOID                               &
        DISPLAY



FIELD T_TOTAL_VEN                          &
        NOID                               &
        PICTURE "^^^,^^^,^^^.^^ "          &
        LEADING SIGN "-"                   &
        SIGNIFICANCE 5                     &
        DISPLAY


FIELD D_TOT_MC2                            &
        NOID                               &
        PICTURE "^^^,^^^,^^^.^^ "          &
        LEADING SIGN "-"                   &
        SIGNIFICANCE 5                     &
        DISPLAY


FIELD   T_TOTAL_FAC2                       &
        NOID                               &
        PICTURE "^^^,^^^,^^^.^^ "          &
        LEADING SIGN "-"                   &
        SIGNIFICANCE 5                     &
        DISPLAY


CLUSTER


;-------------------------------------------------------------------------------
;
; Simule le défault pour le champ
;

PROCEDURE INPUT COMTYPCMD OF PDCOMMAN_INTERNE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = SIZE(TRUNCATE(COMTYPCMD OF PDCOMMAN_INTERNE))
  THEN BEGIN
    LET FIELDTEXT = "E"
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ à été saisit
;

PROCEDURE EDIT COMTYPCMD OF PDCOMMAN_INTERNE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ (codes bilingues)
;

PROCEDURE INPUT COMFRSCOUINC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_COMFRSCOUINC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_COMFRSCOUINC , &
                             T_COMFRSCOUINC
    LET FIELDTEXT = TRUNCATE( T_COMFRSCOUINC )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ à été saisit
;

PROCEDURE EDIT COMFRSCOUINC OF PDCOMMAN_INTERNE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END



;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ (codes bilingues)
;

PROCEDURE INPUT COMFRSTRPTERINC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_COMFRSTRPTERINC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_COMFRSTRPTERINC , &
                             T_COMFRSTRPTERINC
    LET FIELDTEXT = TRUNCATE( T_COMFRSTRPTERINC )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ à été saisit
;

PROCEDURE EDIT COMFRSTRPTERINC OF PDCOMMAN_INTERNE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ (codes bilingues)
;

PROCEDURE INPUT COMFRSTRPMARINC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_COMFRSTRPMARINC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_COMFRSTRPMARINC , &
                             T_COMFRSTRPMARINC
    LET FIELDTEXT = TRUNCATE( T_COMFRSTRPMARINC )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ à été saisit
;

PROCEDURE EDIT COMFRSTRPMARINC OF PDCOMMAN_INTERNE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;

PROCEDURE INPUT COMPRIDET
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide que le champ à été saisit
;

PROCEDURE EDIT COMPRIDET OF PDCOMMAN_INTERNE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;
PROCEDURE OUTPUT COMPRIDET
BEGIN
  USE usflgout3 NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide que le champ à été saisit
;

PROCEDURE EDIT COMIMPMET OF PDCOMMAN_INTERNE
BEGIN
  IF COMIMPMET OF PDCOMMAN_INTERNE[1:1] = "M"
  THEN BEGIN
    LET COMIMPMET OF PDCOMMAN_INTERNE = "MET"
  END
  ELSE LET COMIMPMET OF PDCOMMAN_INTERNE = "IMP"
DISPLAY COMIMPMET OF PDCOMMAN_INTERNE
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDCOMMAN_INTERNE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDCOMMAN_INTERNE &
     AND NOT NEWRECORD     OF PDCOMMAN_INTERNE &
     AND NOT DELETEDRECORD OF PDCOMMAN_INTERNE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

;-------------------------------------------------------------------------------

procedure internal i_cumul
begin
  while retrieving pddetail_c_i via ciecle                  , &
                                    pjtabr                  , &
                                    pjtann                  , &
                                    comnumcomint              &
                              using ciecle       of vven_grp, &
                                    pjtabr       of vven_grp, &
                                    pjtann       of vven_grp, &
                                    comnumcomint of vven_grp
  begin
    let t_total_unmprd = t_total_unmprd + dciqteunmprd of pddetail_c_i
  end

  while retrieving pdventilation via ciecle                  , &
                                     pjtabr                  , &
                                     pjtann                  , &
                                     comnumcomint            , &
                                     cptcle                    &
                               using ciecle       of vven_grp, &
                                     pjtabr       of vven_grp, &
                                     pjtann       of vven_grp, &
                                     comnumcomint of vven_grp, &
                                     cptcle       of vven_grp
  begin
    get pddetail_c_i via ciecle                       , &
                         pjtabr                       , &
                         pjtann                       , & 
                         comnumcomint                 , &
                         dcinumlig                      &
                   using ciecle       of pdventilation, &
                         pjtabr       of pdventilation, &
                         pjtann       of pdventilation, &
                         comnumcomint of pdventilation, &
                         dcinumlig    of pdventilation  opt
    let t_total_ven = t_total_ven + (venpriuni of pdventilation * dciqteunmprd of pddetail_c_i)
  end

  while retrieving pdfac_imputation via ciecle               , &
                                     pjtabr                  , &
                                     pjtann                  , &
                                     comnumcomint            , &
                                     cptcle                    &
                               using ciecle       of vven_grp, &
                                     pjtabr       of vven_grp, &
                                     pjtann       of vven_grp, &
                                     comnumcomint of vven_grp, &
                                     cptcle       of vven_grp
  begin
    let t_total_fac2 = t_total_fac2 + crimnt of pdfac_imputation
  end
end

;--------------------------------------------------------------

procedure path
begin
  let path = 1
end

;--------------------------------------------------------------

procedure find
begin
  for missing vven_grp
  begin
    get vven_grp via ciecle                          , &
                     pjtabr                          , &
                     pjtann                          , &
                     comnumcomint                      &
               using ciecle       of pdcomman_interne, &
                     pjtabr       of pdcomman_interne, &
                     pjtann       of pdcomman_interne, &
                     comnumcomint of pdcomman_interne
    do internal i_cumul
  end
end

;-------------------------------------------------------------------------------


BUILD


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************* Prix globaux *********************************
* Num. commande.: xx-xx-xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
*                                                                              *
* Type commande.: x xxxxxxxxxxxxxxxxxxx        Mnt forfait...: xxxxxxxxxxxxxxx *
* Frs. courtage.: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* Frs. trp. ter.: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* Frs. trp. mar.: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* Prix/détail...: xxx                                                          *
*                                                                              *
* Description          Compte     Prix total      Prix au M2  Montant facturé  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
* xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx  *
*                                                                              *
********************************************************************************
@ENDIF
