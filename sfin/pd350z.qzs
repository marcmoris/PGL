;/T/ ; 2.3.7 Liste de débitage
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 25 juin 1995
;
;    Description...: Liste de débitage
;
;    Paramètres....: Num. commande interne (un, plusieur, tous )
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------


CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS PDDIAGRAMME                           &
  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE &

  LINK CIECLE           OF PDDIAGRAMME,      &
       PJTABR           OF PDDIAGRAMME,      &
       PJTANN           OF PDDIAGRAMME,      &
       COMNUMCOMINT     OF PDDIAGRAMME       &
  TO   CIECLE,                               &
       PJTABR,                               &
       PJTANN,                               &
       COMNUMCOMINT     OF PDCOMMAN_INTERNE  &

  LINK CIECLE           OF PDDIAGRAMME,      &
       TGRCODGRA        OF PDDIAGRAMME       &
  TO   CIECLE,                               &
       TGRCODGRA        OF PDTYPE_GRANIT     &

  LINK CIECLE           OF PDDIAGRAMME,      &
       TYFCODFIN        OF PDDIAGRAMME       &
  TO   CIECLE,                               &
       TYFCODFIN        OF PDTYPE_FINI       &

  LINK CIECLE           OF PDDIAGRAMME,      &
       DIANUMDIA002     OF PDDIAGRAMME,      &
       PJTABR           OF PDDIAGRAMME       &
    TO CIECLE,                               &
       DIANUMDIA002,                         &
       PJTABR           OF PDPRIORITE_DIAG   &

  LINK CIECLE           OF PDDIAGRAMME,      &
       PRICODPRI        OF PDPRIORITE_DIAG,  &
       PJTABR           OF PDPRIORITE_DIAG,  &
       PJTANN           OF PDPRIORITE_DIAG,  &
       COMNUMCOMINT     OF PDPRIORITE_DIAG   &
    TO CIECLE,                               &
       PRICODPRI,                            &
       PJTABR,                               &
       PJTANN,                               &
       COMNUMCOMINT     OF PDPRIORITE        &

  LINK CIECLE           OF PDDIAGRAMME,      &
       PJTABR           OF PDDIAGRAMME       &
    TO CIECLE,                               &
        PJTABR           OF PDPROJET



CHOOSE CIECLE       PARM, &
       COMNUMCOM    PARM, &
       DIANUMDIA002 PARM


;-------------------------------------------------------------------------------
;
; Variable de tri
;

DEFINE D_TRI CHARACTER SIZE 12 = TGRCODGRA OF PDDIAGRAMME + &
                                 TYFCODFIN OF PDDIAGRAMME + &
                                 ASCII(DIAEPA OF PDDIAGRAMME,4) + &
                                 DCINUMLIG OF PDDIAGRAMME


SORT ON COMNUMCOM OF PDDIAGRAMME &
     ON DIANUM002 OF PDDIAGRAMME &
     ON PRICODPRI OF PDPRIORITE_DIAG &
     ON D_TRI &
     ON TGRCODGRA OF PDDIAGRAMME &
     ON TYFCODFIN OF PDDIAGRAMME &
     ON DIAEPA    OF PDDIAGRAMME





;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""


;-------------------------------------------------------------------------------
;
; Numéro du rapport.
;

DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD350Z"


;-------------------------------------------------------------------------------
;
; Nom de la compagnie.
;

DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


;-------------------------------------------------------------------------------
;
; Définition des éléments variables
;

DEFINE D_DCINUMLIG ZONED UNSIGNED SIZE 02 = NCONVERT(DCINUMLIG OF PDDIAGRAMME)


USE ustitstd NOLIST
   SKIP 2

HEADING AT COMNUMCOM &
   TAB 001 "Liste de débitage pour : " &
   TAB 026 PJTNOM OF PDPROJET &
   TAB 067 PJTABR OF PDPROJET &
   TAB 069 "-" &
   TAB 070 PJTANN OF PDPROJET &
   TAB 077 "Épaisseur en +:" &
   TAB 093 COMEPAPLU OF PDCOMMAN_INTERNE &
   TAB 099 "Épaisseur en -:" &
   TAB 115 COMEPAMOI OF PDCOMMAN_INTERNE &
   SKIP &
   TAB 077 "Dimension en +:" &
   TAB 093 COMDIMPLU OF PDCOMMAN_INTERNE &
   TAB 099 "Dimension en -:" &
   TAB 115 COMDIMMOI OF PDCOMMAN_INTERNE &
   SKIP &
   TAB 077 "Critères esthé:" &
   TAB 093 COMCRIEST001 OF PDCOMMAN_INTERNE &
   SKIP


HEADING AT PRICODPRI &
   TAB 001 "Num. commande.:" &
   TAB 017 COMNUMCOM OF PDDIAGRAMME &
   TAB 030 "Priorité......:" &
   TAB 046 PRICODPRI OF PDPRIORITE_DIAG &
   TAB 049 "(" &
   TAB 050 PRIDATFIN OF PDPRIORITE &
   TAB 057 ")" &
   SKIP



HEADING AT D_TRI                               &
  SKIP 3                                       &
  TAB 001 "Num. ligne....:"                    &
  TAB 017 D_DCINUMLIG PICTURE "^^^"            &
  TAB 021 "Épaisseur.....:"                    &
  TAB 037 DIAEPA           OF PDDIAGRAMME      &
  SKIP                                         &
  TAB 001 "Type granit...:"                    &
  TAB 017 TGRCODGRA        OF PDDIAGRAMME      &
  TAB 021 TGRDSCFRA001     OF PDTYPE_GRANIT    &
  TAB 062 "Type fini.....:"                    &
  TAB 078 TYFCODFIN        OF PDDIAGRAMME      &
  TAB 081 TYFDSCFRA001     OF PDTYPE_FINI      &
  SKIP 2                                       &
  TAB 001 "Num diagramme  Long   Haut   Épai  Qte cmd P C    D C    P US   D US   P SC   D SC   P C    F C    P US   F US   P SC   F SC"


HEADING AT DIANUM002 &
  TAB 003 DIANUM002        OF PDDIAGRAMME      &
  TAB 016 DIALON           OF PDDIAGRAMME      &
  TAB 023 DIAHAU           OF PDDIAGRAMME      &
  TAB 030 DIAEPA           OF PDDIAGRAMME      &
  TAB 037 DIAQTECMD        OF PDDIAGRAMME      &
  TAB 044 DIAQTEPLADBTGRA  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 051 DIAQTEDBTGRA     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 058 DIAQTEPLADBTUSC  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 065 DIAQTEDBTUSC     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 072 DIAQTEPLADBTSCT  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 079 DIAQTEDBTSCT     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 086 DIAQTEPLAFINGRA  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 093 DIAQTEFINGRA     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 100 DIAQTEPLAFINUSC  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 107 DIAQTEFINUSC     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 114 DIAQTEPLAFINSCT  OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-" &
  TAB 121 DIAQTEFINSCT     OF VDIAQTE          &
                PICTURE "^^^^^ " TRAILING SIGN "-"


FOOTING AT COMNUMCOM SKIP PAGE


BUILD pd350z


@IF FORMAT

PD350Z                             XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                      PAGE: 9999
AA/MM/JJ HH:MM XXXXXXXXXXXX       XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Liste de débitage pour : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xx-99     Épaisseur en +: 9999  Épaisseur en -: 9999
                                                                            Dimension en +: 9999  Dimension en -: 9999
                                                                            Critères esthé: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
Num. commande.: XX-XX-XXX    Priorité......: 99 (AA/MM/JJ)
Type granit...: 999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Type fini.....: 99  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Épaisseur.....: 9999

* Num. diagramme     Longueur        Hauteur      Quantité  Reste à faire

*        xxxxxxx     9999 (9999)     9999 (9999)     99999  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
*        xxxxxxx     9999 (9999)     9999 (9999)     99999  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
*        xxxxxxx     9999 (9999)     9999 (9999)     99999  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
*        xxxxxxx     9999 (9999)     9999 (9999)     99999  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
*        xxxxxxx     9999 (9999)     9999 (9999)     99999  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
*        xxxxxxx     9999 (9999)     9999 (9999)     99999  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx


                      M2 total......: 999999.9999    99999
@ENDIF


