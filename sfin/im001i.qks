;/T/ Gestion des immobilisation
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche d'immobilisation - Information opération sommaire.
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001i ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        IMIMMOBILISATION

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "IM001I"            ; Nom de l'écran

USE im001i.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier fiche de l'immobilisation
;------------------------------------
FILE IMIMMOBILISATION       MASTER



;----------------------------
; Fichier budget d'opération
;----------------------------
FILE IMIMM_BDG_UNT          PRIMARY   &
                            OCCURS 15 &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  IMMCLE, &
                  UNTCLE  &
          USING   CIECLE OF IMIMMOBILISATION, &
                  IMMCLE OF IMIMMOBILISATION, &
                  UNTCLE OF IMIMM_BDG_UNT   &
          REQUEST UNTCLE &
          ORDERBY UNTCLE

  ACCESS  VIA     CIECLE, &
                  IMMCLE  &
          USING   CIECLE OF IMIMMOBILISATION, &
                  IMMCLE OF IMIMMOBILISATION  &
          ORDERBY UNTCLE



;-------------------------------
; Fichier unité de travail
;-------------------------------
FILE GPUNITE_TRAVAIL        REFERENCE

  ACCESS  VIA   UNTCLE  &
          USING UNTCLE OF IMIMM_BDG_UNT



;-------------------------------
; Fichier unité de mesure
;-------------------------------
FILE GPUNITE_MESURE         REFERENCE

  ACCESS  VIA   UNMCLE  &
          USING UNMCLE OF GPUNITE_TRAVAIL


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPUNITE_TRAVAIL


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Information opération sommaire " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN (,3,19) (,,35)
FIELD IMMCLE    OF IMIMMOBILISATION                   &
      DISPLAY

FIELD IMMDSC    OF IMIMMOBILISATION                   &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Unité de travail" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Compte Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,31


TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,69

SKIP

ALIGN (,,3) (,,8) (,,31) (,,38) (,,59) (,,76)

CLUSTER OCCURS WITH IMIMM_BDG_UNT

FIELD UNTCLE OF IMIMM_BDG_UNT                         &
      DISPLAY

FIELD UNTDSC OF GPUNITE_TRAVAIL                       &
      DISPLAY                                         &
      SIZE 22

FIELD CPTCLE OF GPUNITE_TRAVAIL                       &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD IBUMNTREE OF IMIMM_BDG_UNT                      &
      DISPLAY

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

