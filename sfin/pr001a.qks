;/T/ Ecran des activités
;
;/P/ Programmeur..: Alain Côté
;    Date création: 13 avril 1993
;
;    Description..: Ecran permettant de définir et de modifer des activités
;                   associées à un projet spécifique.
;                   Impossible de détruire une activité on doit changer le
;                   statut.
;                   Le budget peut être modifié si l'activité est active.
;
;
;-------------------------------------------------------------------------------
;**M
;**M 93-04-13 ALAIN COTE
;**M
;**M RESTE LE DELETE
;**M
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr001a ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY, FIND, CHANGE &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM ,&
                        GPPROJET

USE ussectmp NOLIST
    "PR001A"

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE pr001a.hlp NOLIST

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; No  Compagnie de transaction
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom Compagnie de transaction

;
; Action bar pour ecran pleine page
;
USE usactecr NOLIST
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Informations libre format  " ACTION DESIGNER D001

;-------------------------------------------------------------------------------
;
FILE GPPROJET         MASTER

FILE GPACTIVITE       PRIMARY &
                      NOITEM  &
                      OPEN EACH LEVEL
  ACCESS REQUEST ACTCLE &
         VIA     CIECLE, &
                 PROCLE, &
                 ACTCLE &
         USING CIECLE OF GPPROJET, &
               PROCLE OF GPPROJET, &
               ACTCLE OF GPACTIVITE &
         ORDERBY CIECLE, PROCLE, ACTCLE

  ACCESS VIA     CIECLE, &
                 PROCLE  &
         USING CIECLE OF GPPROJET, &
               PROCLE OF GPPROJET &
         ORDERBY CIECLE, PROClE, ACTCLE

  ITEM CIECLE OF GPACTIVITE INITIAL CIECLE OF GPPROJET
  ITEM PROCLE OF GPACTIVITE INITIAL PROCLE OF GPPROJET

;
; Validation du statut(code bilingue).
;
DEFINE    D_ACTSTU CHARACTER SIZE 16 = "ACTSTU"
TEMPORARY T_ACTSTU CHARACTER SIZE  4
;
FILE VCBI_DSC         REFERENCE
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_ACTSTU, &
               ACTSTU OF GPACTIVITE


;
; Validation du responsable de projet et lecture de sa description
;
DEFINE    D_ACTRPS CHARACTER SIZE 16 = "PRORPS"
TEMPORARY T_ACTRPS CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_ACTRPS

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_ACTRPS, &
                ACTRPS OF GPACTIVITE



;
; Validation du catégorie du projet et lecture de sa description
;
DEFINE    D_ACTCAR CHARACTER SIZE 16 = "PROCAR"
TEMPORARY T_ACTCAR CHARACTER SIZE 4

FILE VCBI_DSC         REFERENCE &
                      ALIAS VCBI_DSC_ACTCAR

  ACCESS  VIA   XELNOM, &
                CBICLE &
          USING D_ACTCAR, &
                ACTCAR OF GPACTIVITE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )
TEMPORARY T_GPMCLE1   CHARACTER SIZE 8  ; Mémo
TEMPORARY T_GPMCLE2   CHARACTER SIZE 3  ; Mémo
TEMPORARY T_GPMTYP    CHARACTER SIZE 3  ; Mémo
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

TITLE &
@IF FRANCAIS
      " Sous Projet " &
@ELSE
      " %%% ACTIVITES " &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1
ALIGN(,2,16)(,25,41)
FIELD PROCAT OF GPPROJET LABEL "Catégorie...:"   DISPLAY
FIELD PROCLE OF GPPROJET LABEL "Numéro projet.:" DISPLAY

SKIP
ALIGN(2,2,16)(,25,41)(,,43)
FIELD ACTCLE  OF GPACTIVITE LABEL "Sous-projet.:" &
  REQUIRED NOCHANGE SIZE 3 NUM SIGNIF 3 HIDDEN &
  LOOKUP NOTON GPACTIVITE VIA CIECLE, PROCLE, ACTCLE &
         USING T_CIECLEMNU, &
               PROCLE OF GPPROJET, &
               ACTCLE OF GPACTIVITE &
  MESSAGE 806 ; Cette activité existe déjà

FIELD ACTSTU OF GPACTIVITE &
        LOOKUP ON VCBI_DSC &
          MESSAGE 804 ; Code invalide.

FIELD CBIDSC OF VCBI_DSC  DISPLAY SIZE 10

ALIGN(2,2,16)(,25,41)(,55,64)
FIELD ACTANN OF GPACTIVITE LABEL "Année.......:" REQUIRED NUM SIGNIF 4 HIDDEN

FIELD ACTCAR OF GPACTIVITE REQUIRED LABEL "Type cargo....:" &
      LOOKUP ON VCBI_DSC_ACTCAR &
      MESSAGE 2503 ; = "*EE* Ce cargo n'existe pas"

FIELD CLICLE OF GPACTIVITE LABEL "Client.:"
SKIP
ALIGN(2,2,16)(,,25)
FIELD ACTRPS OF GPACTIVITE LABEL "Responsable.:" HIDDEN &
      LOOKUP ON VCBI_DSC_ACTRPS &
      MESSAGE 2505 ;= "*EE* Ce code de responsable n'existe pas"

FIELD CBIDSC OF VCBI_DSC_ACTRPS  DISPLAY
SKIP
ALIGN(2,2,16)
FIELD ACTDSC1 OF GPACTIVITE LABEL "Description :" FOR 1,60  POPUP ON REQUEST HIDDEN
ALIGN(,2,16)
FIELD ACTDSC2 OF GPACTIVITE NOLABEL FOR 1,60 POPUP ON REQUEST
FIELD ACTDSC3 OF GPACTIVITE NOLABEL FOR 1,60 POPUP ON REQUEST
SKIP
ALIGN (2,2,24) (,34,44) (,55,62)
FIELD ACTUNADEP OF GPACTIVITE LABEL "Unité de dépense :" HIDDEN
FIELD ACTUNAREV OF GPACTIVITE LABEL "Revenus :"
FIELD ACTUNAPAI OF GPACTIVITE LABEL "Paie :"
SKIP
ALIGN (2,2,24)
FIELD ACTREVEST    OF GPACTIVITE LABEL "Revenus estimés.....:" HIDDEN
FIELD ACTDEPEST    OF GPACTIVITE LABEL "Dépenses estimées...:" HIDDEN
FIELD ACTJOUEST    OF GPACTIVITE LABEL "Jours estimés.......:" HIDDEN
FIELD ACTHREEST    OF GPACTIVITE LABEL "Heures estimées.....:" HIDDEN
FIELD ACTTONEST    OF GPACTIVITE LABEL "Tonnage estimé......:" HIDDEN
FIELD ACTDATDEBEST OF GPACTIVITE  FORMAT YYYYMMDD PATTERN "####/##/##" LABEL "Date début estimée..:" HIDDEN
FIELD ACTDATFINEST OF GPACTIVITE  FORMAT YYYYMMDD PATTERN "####/##/##"  LABEL "Date fin estimée....:" HIDDEN
FIELD ACTHREDEBEST OF GPACTIVITE LABEL "Heure début estimée.:" HIDDEN
FIELD ACTHREFINEST OF GPACTIVITE LABEL "Heure fin estimée...:" HIDDEN
SKIP TO 12
ALIGN (48,48,62)
FIELD ACTREV    OF GPACTIVITE LABEL "Revenus.....:" HIDDEN
FIELD ACTDEP    OF GPACTIVITE LABEL "Dépenses....:" HIDDEN
FIELD ACTJOU    OF GPACTIVITE LABEL "Jours.......:" HIDDEN
FIELD ACTHRE    OF GPACTIVITE LABEL "Heures......:" HIDDEN
FIELD ACTTON    OF GPACTIVITE LABEL "Tonnage.....:" HIDDEN
FIELD ACTDATDEB OF GPACTIVITE  FORMAT YYYYMMDD PATTERN "####/##/##"  LABEL "Date début..:" HIDDEN
FIELD ACTDATFIN OF GPACTIVITE  FORMAT YYYYMMDD PATTERN "####/##/##"  LABEL "Date fin....:" HIDDEN
FIELD ACTHREDEB OF GPACTIVITE LABEL "Heure début.:" HIDDEN
FIELD ACTHREFIN OF GPACTIVITE LABEL "Heure fin...:" HIDDEN
SKIP 1
ALIGN(2,2,24)(,,35)(,,45)
FIELD ACTFRM1 LABEL "Format d'impression :" DEFAULT "RESUL"
FIELD ACTFRM2 NOLABEL ID SAME
FIELD ACTFRM3 NOLABEL ID SAME


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

; Memo
;
PROCEDURE DESIGNER D001
BEGIN
  LET T_GPMCLE1 = PROCLE OF GPPROJET
  LET T_GPMCLE2 = ACTCLE OF GPACTIVITE
  LET T_GPMTYP  = "ACT"
  RUN SCREEN pr050 MODE SAME &
   PASSING T_CIECLEMNU, T_CIENOM, T_GPMCLE1, T_GPMCLE2, T_GPMTYP
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les statuts(code bilingue).
;
;
PROCEDURE INPUT ACTSTU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ACTSTU = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_ACTSTU , T_ACTSTU
    LET FIELDTEXT = TRUNC(T_ACTSTU)
  END
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les (code bilingue).
;
;
PROCEDURE INPUT ACTCAR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ACTCAR = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_ACTCAR , T_ACTCAR
    LET FIELDTEXT = TRUNC(T_ACTCAR)
  END
END
;-------------------------------------------------------------------------------
;
;
; Popup sur les (code bilingue).
;
;
PROCEDURE INPUT ACTRPS
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ACTRPS = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_ACTRPS , T_ACTRPS
    LET FIELDTEXT = TRUNC(T_ACTRPS)
  END
END



;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPACTIVITE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPACTIVITE &
     AND NOT NEWRECORD     OF GPACTIVITE &
     AND NOT DELETEDRECORD OF GPACTIVITE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD ;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
;********************************************************************************
;*Catégorie...: xx       Numéro projet.: xxxxxxxx                               *
;*Sous-projet.: xxxxxx   Statut........: x xxxxxxxxxx                           *
;*Année.......: xxxx     Type cargo....: xxxx          Client.: xxxxxx          *
;*Responsable.: xxxx     xxxxxxxxxxxxxxxxxxxxxxxxx                              *
;*Description : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
;*              xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
;*              xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
;*Unite de dépense :    xxxxxxxx  Revenus : xxxxxxxx   Paie : xxxxxxxx          *
;*Revenus estimes.....: xxxxxxxxxxxxxx          Revenus.....: xxxxxxxxxxxxxx    *
;*Depenses estimees...: xxxxxxxxxxxxxx          Dépenses....: xxxxxxxxxxxxxx    *
;*Jours estimes.......: xxxxxx                  Jours.......: xxxxxxxx          *
;*Heures estimees.....: xxxxx                   Heures......: xxxxx             *
;*Tonnage estime......: xxxxxxxxxxxxxx          Tonnage.....: xxxxxxxxxxxxxx    *
;*Date debut estimee..: xxxxxxxx                Date début..: xxxxxxxx          *
;*Date fin estimee....: xxxxxxxx                Date fin....: xxxxxxxx          *
;*Heure debut estimee.: xxxxx                   Heure début.: xxxxx             *
;*Heure fin estimee...: xxxxx                   Heure fin...: xxxxx             *
;*                                                                              *
;*Format d'impression : xxxxx      xxxxx     xxxxx                              *
;********************************************************************************
