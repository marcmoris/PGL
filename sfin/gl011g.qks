;/T/ Initialisation d'une colonne
;
;/P/ Programmeur..: Alain Côté
;    Date création: 6 avril 1994
;
;/V3.02.00/ Alain Côté, 6 avril 1994, 199
;
;    Description..: Le but est de mettre une valeur automatiquement pour toutes
;                   les lignes d'un état financier.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gl011g NOMODE NOACTION FIELDMARK &
              FROM 4,15 TO 10,66 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY &
              AUTOUPDATE       &
              RECEIVING GLETAT_FINANCIER , &
                        T_COLVAL


USE ussectmp   NOLIST
    "GL011G"

USE gl011g.hlp NOLIST


USE ustrasse NOLIST

;-------------------------------------------------------------------------------
;
;
TEMPORARY T_COLVAL CHARACTER        SIZE 9 ; Liste des colonnes valident.
TEMPORARY T_COLINI CHARACTER        SIZE 1 ; Colonne à initialiser.
TEMPORARY T_VALINI INTEGER UNSIGNED SIZE 4 ; Année de destination.
;
; On attribut une valeur numérique au code de colonne pour accéder la
; table des colonnes de l'état financier.
;
DEFINE D_EFCNUMCOL INTEGER SIZE 2 = INDEX( "ABCDEFGHIJKLMNOP", T_COLINI )

;-------------------------------------------------------------------------------
;
; État financier.
;
FILE GLETAT_FINANCIER MASTER

;
; Détail de l'état financier.
;
FILE GLEFI_DETAIL     DESIGNER
  ACCESS VIA EFICIECLE , &
             EFICLE      &
         USING EFICIECLE OF GLETAT_FINANCIER , &
               EFICLE    OF GLETAT_FINANCIER
  ;
  ; Les ratios sont valident que pour certain type de ligne, le sélect suivant
  ; nous assure que les lignes affichées à l'écran font partie des types de
  ; ligne valide pour les ratios.
  ;
  SELECT IF    "CPT"  = EFDCOD OF GLEFI_DETAIL &
            OR "SDEB" = EFDCOD OF GLEFI_DETAIL &
            OR "SFIN" = EFDCOD OF GLEFI_DETAIL &
            OR "TOT"  = EFDCOD OF GLEFI_DETAIL &
            OR "NOTE" = EFDCOD OF GLEFI_DETAIL &
            OR "JUM"  = EFDCOD OF GLEFI_DETAIL &
            OR "BAL"  = EFDCOD OF GLEFI_DETAIL


;
; Pour afficher le type et l'en-tête de la colonne.
;
FILE GLEFI_COLONNE    REFERENCE
  ACCESS VIA EFICIECLE , &
             EFICLE    , &
             EFCNUMCOL   &
         USING EFICIECLE OF GLETAT_FINANCIER , &
               EFICLE    OF GLETAT_FINANCIER , &
               D_EFCNUMCOL


;-------------------------------------------------------------------------------
;

USE ushilite NOLIST

DRAW 1,1 TO 7,52


TITLE &
@IF FRANCAIS
      " Initialisation d'une colonne " &
@ELSE
      " %%%Initialisation d'une colonne " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST

SKIP 1

ALIGN (19,3,19) (,,21) (,,26)

FIELD T_COLINI ID 05 HIDDEN &
@IF FRANCAIS
        LABEL "Colonne.......:" &
@ELSE
        LABEL "%%%colonne....:" &
@ENDIF
        REQUIRED &
        UPSHIFT  &
        PATTERN = T_COLVAL

FIELD EFCTYPCOL OF GLEFI_COLONNE &
        DISPLAY

FIELD EFCENTCOL OF GLEFI_COLONNE &
        DISPLAY &
        SIZE 25

SKIP 1

FIELD T_VALINI ID 10 HIDDEN &
@IF FRANCAIS
        LABEL "Valeur........:" &
@ELSE
        LABEL "%%%Valeur.....:" &
@ENDIF
        REQUIRED &
        PICTURE "^^^.^^" &
        INPUT SCALE 2    &
        SIGNIFICANCE 4   &
        BWZ


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Initialisation de la colonne choisi par l'usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Lecture du détail de l'état financier.
  ;
  WHILE RETRIEVING GLEFI_DETAIL
  BEGIN
    IF T_COLINI = "B"
    THEN LET EFDRATB OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "C"
    THEN LET EFDRATC OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "D"
    THEN LET EFDRATD OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "E"
    THEN LET EFDRATE OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "F"
    THEN LET EFDRATF OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "G"
    THEN LET EFDRATG OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "H"
    THEN LET EFDRATH OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "I"
    THEN LET EFDRATI OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "J"
    THEN LET EFDRATJ OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "K"
    THEN LET EFDRATK OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "L"
    THEN LET EFDRATL OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "M"
    THEN LET EFDRATM OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "N"
    THEN LET EFDRATN OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "O"
    THEN LET EFDRATO OF GLEFI_DETAIL = T_VALINI

    IF T_COLINI = "P"
    THEN LET EFDRATP OF GLEFI_DETAIL = T_VALINI

    PUT GLEFI_DETAIL
  END
  RETURN
END

BUILD
@IF FORMAT

              *********** Initialisation d'une colonne ***********
              *                                                  *
              * Colonne.......: x xxxx xxxxxxxxxxxxxxxxxxxxxxxxx *
              *                                                  *
              * Valeur........: xxxxxx                           *
              *                                                  *
              ****************************************************
@ENDIF
