;/T/ 2.4.2 Enregistrer les tranches.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-19
;
;    Description..: Cette unité de traitement permet d'enregistrer les
;                   tranche produite par Granicor.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd184a ACTIONBAR                           &
             MODE LABEL "" AT 2,80                &
             NOACTION                             &
             ACTIVITIES FIND,ENTRY,CHANGE,DELETE  &
             FIELDMARK RETAIN STARTUP             &
             HELP POPUP FROM 4,9 TO 20,72         &
             RECEIVING T_CIECLEMNU,               &
                       T_CIENOM,                  &
                       PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD184A"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd184a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

TEMPORARY T-T CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_DDTRCODDEF CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_DDDTRCODDEF CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_DTRTRANUMTRA002_1     CHARACTER SIZE 07
TEMPORARY T_DTRCODDEF_OLD_1       CHAR SIZE 2
TEMPORARY T_DTRTRANUMTRA002_2     CHARACTER SIZE 07
TEMPORARY T_DTRCODDEF_OLD_2       CHAR SIZE 2

TEMPORARY T_COMPTEUR  INT SIZE 2
;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Variable pour l'appel des pop-up
;
TEMPORARY T_DTRCODDEF CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Dimension maximun et minimum
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC    MASTER

;-------------------------------------------------------------------------------
;
; Tranche
;
FILE PDTRANCHE       PRIMARY &
                     NOITEM &
                     OCCURS 13 TIMES

  ACCESS VIA     CIECLE,            &
                 BLONUMGRA001APR,   &
                 BLONUMGRA002APR,   &
                 BLORECAPR          &
         USING   T_CIECLEMNU,                             &
                 BLONUMGRA001APR    OF PDMONTAGE_BLOC,    &
                 BLONUMGRA002APR    OF PDMONTAGE_BLOC,    &
                 BLORECAPR          OF PDMONTAGE_BLOC     &
         ORDERBY CIECLE,                                  &
                 BLONUMGRA001APR,  &
                 BLONUMGRA002APR,  &
                 BLORECAPR,        &
                 TRANUMTRA002

  ITEM CIECLE           OF PDTRANCHE INITIAL T_CIECLEMNU
  ITEM TRANUMTRA001     OF PDTRANCHE INITIAL "       "
  ITEM TRACODPRD        OF PDTRANCHE INITIAL "P"
  ITEM TRASTU           OF PDTRANCHE INITIAL "I"
  ITEM TRADATCRE        OF PDTRANCHE INITIAL SYSDATE
  ITEM TRAHRECRE        OF PDTRANCHE INITIAL SYSTIME/10000
  ITEM TRAUSRCRE        OF PDTRANCHE INITIAL LOGONID
  ITEM TYFCODFIN        OF PDTRANCHE INITIAL "00"




FILE PDTRANCHE_DEF SECONDARY NOITEM &
                    OCCURS WITH PDTRANCHE &
                    ALIAS DEFAUT_1
  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002,                &
                 RMSNUMSEQ,                   &
                 DTRPOS                       &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE,   &
                 RMSNUMSEQ    OF PDMONTAGE_BLOC,&
                 "1"

  ITEM RMSNUMSEQ    OF DEFAUT_1 INITIAL RMSNUMSEQ OF PDMONTAGE_BLOC
  ITEM DTRPOS       OF DEFAUT_1 INITIAL "1"
  ITEM TRANUMTRA002 OF DEFAUT_1 INITIAL TRANUMTRA002 OF PDTRANCHE
  ITEM CIECLE       OF DEFAUT_1 INITIAL T_CIECLEMNU

FILE PDTRANCHE_DEF SECONDARY NOITEM &
                    OCCURS WITH PDTRANCHE &
                    ALIAS DEFAUT_2
  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002,                &
                 RMSNUMSEQ,                   &
                 DTRPOS                       &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE,   &
                 RMSNUMSEQ    OF PDMONTAGE_BLOC,&
                 "2"
  ITEM RMSNUMSEQ    OF DEFAUT_2 INITIAL RMSNUMSEQ OF PDMONTAGE_BLOC
  ITEM DTRPOS       OF DEFAUT_2 INITIAL "2"
  ITEM TRANUMTRA002 OF DEFAUT_2 INITIAL TRANUMTRA002 OF PDTRANCHE
  ITEM CIECLE       OF DEFAUT_2 INITIAL T_CIECLEMNU

FILE PDTRANCHE_DEF DESIGNER &
                   ALIAS DEFAUT_3

FILE PDDEF_DE_TRANCHE REFERENCE ALIAS DEF_1

  ACCESS VIA     CIECLE,                      &
                 DTRCODDEF                    &
         USING   T_CIECLEMNU,                 &
                 DTRCODDEF OF DEFAUT_1

FILE PDDEF_DE_TRANCHE REFERENCE ALIAS DEF_2

  ACCESS VIA     CIECLE,                      &
                 DTRCODDEF                    &
         USING   T_CIECLEMNU,                 &
                 DTRCODDEF OF DEFAUT_2

;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC REFERENCE

  ACCESS VIA     CIECLE,          &
                 BLONUMGRA001APR, &
                 BLONUMGRA002APR, &
                 BLORECAPR        &
         USING   T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDMONTAGE_BLOC, &
                 BLONUMGRA002APR OF PDMONTAGE_BLOC, &
                 BLORECAPR       OF PDMONTAGE_BLOC


  ITEM BLONUMGRA001APR  OF PDTRANCHE INITIAL BLONUMGRA001APR  OF PDBLOC
  ITEM BLONUMGRA002APR  OF PDTRANCHE INITIAL BLONUMGRA002APR  OF PDBLOC
  ITEM BLORECAPR        OF PDTRANCHE INITIAL BLORECAPR        OF PDBLOC
  ITEM BLONUMBLOAPR     OF PDTRANCHE INITIAL BLONUMBLOAPR     OF PDBLOC
  ITEM TGRCODGRA        OF PDTRANCHE INITIAL TGRCODGRA        OF PDBLOC


;-------------------------------------------------------------------------------
;
; Sciage blocs
;

FILE PDSCIAGE_BLOCS REFERENCE

  ACCESS VIA     CIECLE,                              &
                 RMSNUMSEQ                            &
         USING   T_CIECLEMNU,                         &
                 RMSNUMSEQ        OF PDMONTAGE_BLOC   &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ


;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE REFERENCE

  ACCESS VIA     CIECLE,                              &
                 RMSNUMSEQ                            &
         USING   T_CIECLEMNU,                         &
                 RMSNUMSEQ        OF PDMONTAGE_BLOC   &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                              &
                 SCINUMSCI                            &
         USING   T_CIECLEMNU,                         &
                 SCINUMSCI        OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE

  ACCESS VIA     CIECLE,          &
                 TYFCODFIN        &
         USING   T_CIECLEMNU,     &
                 TYFCODFIN       OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_TYFCODFIN CHARACTER SIZE 04
TEMPORARY T_SURFACE   FLOAT     SIZE 08 OCCURS WITH PDTRANCHE


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Tranches par bloc " &
@ELSE
      " %%%Tranches par bloc " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19) (49,50,66)


FIELD RMSNUMSEQ        OF PDMONTAGE_BLOC   &
label "Num. montage..:"&
        HIDDEN ID 05                       &
        FIXED


FIELD SCBDATSCI        OF PDSCIAGE_BLOCS    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date sciage...:"&
        HIDDEN ID 10                       &
        REFRESH FIXED


ALIGN (2,3,19)


FIELD BLONUMBLOAPR     OF PDMONTAGE_BLOC    &
label "Num. bloc.....:"&
        HIDDEN ID 20                        &
        FIXED


DRAW 7,2 TO 7,79


SKIP TO LINE 7


TITLE &
@IF FRANCAIS
      " Tranches " &
@ELSE
      " %%%tranches " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 9


TITLE &
@IF FRANCAIS
"     No tranche   Long    Haut    Epais   Surface       Fini     Défauts" &
@ELSE
"%%%  No tranche   Long    Haut    Epais   Surface       Fini     Défauts " &
@ENDIF
     AT ,2

SKIP


ALIGN (05,06,07) (,,20) (,,28) (,,36) (,,44) (,,58) (,,67) (,,72)


CLUSTER OCCURS WITH PDTRANCHE ID BASE 25


FIELD TRANUMTRA002     OF PDTRANCHE        &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED NOCHANGE                  &
        NUMERIC                            &
        BWZ                                &
        LOOKUP NOTON PDTRANCHE             &
          VIA   CIECLE,                    &
                TRANUMTRA001,              &
                TRANUMTRA002               &
          USING T_CIECLEMNU,               &
                TRANUMTRA001      OF PDTRANCHE,      &
                TRANUMTRA002      OF PDTRANCHE       &
          MESSAGE 2939 ; Ce numéro de tranche existe déjà


FIELD TRALON           OF PDTRANCHE        &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                          &
        REQUIRED                           size 4


FIELD TRAHAU           OF PDTRANCHE        &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                          &
        REQUIRED                           size 4


FIELD TRAEPA           OF PDTRANCHE        &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                          &
        REQUIRED                           size 4


FIELD TRASURMC2        OF PDTRANCHE        &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY


FIELD TYFCODFIN        OF PDTRANCHE        &
        HIDDEN                             &
        NOLABEL                            &
        DISPLAY

FIELD DTRCODDEF OF DEFAUT_1 &
        HIDDEN                                &
;        LOOKUP ON FILE DEF_1                  &
;          VIA     CIECLE,                     &
;                  DTRCODDEF                   &
;          USING   T_CIECLEMNU,                &
;                  DTRCODDEF OF DEFAUT_1       &
;          MESSAGE 2954 ,&; ,                      &  Ce code de défaut n'existe pas
         LOOKUP  NOTON FILE DEFAUT_1 &
          VIA     CIECLE,                               &
                  TRANUMTRA002,                         &
                  DTRCODDEF                             &
          USING   T_CIECLEMNU,                          &
                  TRANUMTRA002    OF PDTRANCHE,&;DEFAUT_1, &
                  DTRCODDEF       OF DEFAUT_1 &
          MESSAGE 2956 ; Ce code de défaut existe déjà pour cette tranche

FIELD DTRCODDEF OF DEFAUT_2 &
        HIDDEN                                &
;        LOOKUP ON FILE DEF_2                  &
;          VIA     CIECLE,                     &
;                  DTRCODDEF                   &
;          USING   T_CIECLEMNU,                &
;                  DTRCODDEF OF DEFAUT_2 &
;          MESSAGE 2954 ,&;,                      &  Ce code de défaut n'existe pas
         LOOKUP  NOTON FILE DEFAUT_2 &
          VIA     CIECLE,                               &
                  TRANUMTRA002,                         &
                  DTRCODDEF                             &
          USING   T_CIECLEMNU,                          &
                  TRANUMTRA002    OF PDTRANCHE,&;DEFAUT_2, &
                  DTRCODDEF       OF DEFAUT_2 &
          MESSAGE 2956 ; Ce code de défaut existe déjà pour cette tranche

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

;PROCEDURE INITIALIZE
;BEGIN
;  USE ussececr NOLIST
;END


;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions minimum et maximum
;

PROCEDURE EDIT TRALON
BEGIN
  IF FIELDVALUE < T_DIM_MIN_LON_TR OR &
     FIELDVALUE > T_DIM_MAX_LON_TR
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
  IF FIELDVALUE > BLOLONNET        OF PDBLOC
  THEN BEGIN
    WARNING 2934 ; Cette dimension est plus grande que la dimension nette
  END
  IF FIELDVALUE > BLOLONBRU        OF PDBLOC
  THEN BEGIN
    ERROR 2933 ; Cette dimension est plus grande que la dimension brute
  END
END

;-------------------------------------------------------------------------------
;
; Appel du pop-up pour le choix des défauts.
;

PROCEDURE INPUT DTRCODDEF OF DEFAUT_1
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DTRCODDEF = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd116 MODE F &
                     PASSING T_DTRCODDEF, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DTRCODDEF )
  END
END

PROCEDURE EDIT DTRCODDEF OF DEFAUT_1
BEGIN
  LET T_DTRCODDEF_OLD_1   = OLDVALUE(DTRCODDEF OF DEFAUT_1)
  LET T_DTRTRANUMTRA002_1 = TRANUMTRA002 OF PDTRANCHE
    LET T_DDTRCODDEF = FIELDTEXT
 ; IF 0 <> SIZE(TRUNC(T_DDTRCODDEF)) AND 0 = SIZE(TRUNC(FIELDTEXT))
 ; THEN LET FIELDTEXT = T_DDTRCODDEF
 END
;--------------------------------------------------------------
;PROCEDURE OUTPUT DTRCODDEF OF DEFAUT_1
;BEGIN
 ; IF NOT ENTRYMODE AND NOT CHANGEMODE AND NOT CORRECTMODE
 ; THEN BEGIN
 ; LET T_COMPTEUR = 0
 ; LET FIELDTEXT = " "
 ; WHILE RETRIEVING DEFAUT_3 &
 ;        VIA     CIECLE,                      &
 ;                TRANUMTRA002                 &
 ;        USING   T_CIECLEMNU,                 &
 ;                TRANUMTRA002 OF PDTRANCHE
 ; BEGIN
 ;   LET T_COMPTEUR = T_COMPTEUR + 1
 ;   IF T_COMPTEUR = 1
 ;   THEN BEGIN
 ;          LET FIELDTEXT = DTRCODDEF OF DEFAUT_3
 ;          BREAK
 ;        END
 ; END
 ; END
;END

PROCEDURE INPUT DTRCODDEF OF DEFAUT_2
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DTRCODDEF = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd116 MODE F &
                     PASSING T_DTRCODDEF, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DTRCODDEF )
  END
END

PROCEDURE EDIT DTRCODDEF OF DEFAUT_2
BEGIN
  LET T_DTRCODDEF_OLD_2   = OLDVALUE(DTRCODDEF OF DEFAUT_2)
  LET T_DTRTRANUMTRA002_2 = TRANUMTRA002 OF PDTRANCHE
  LET T_DDDTRCODDEF = FIELDTEXT
 ; IF 0 <> SIZE(TRUNC(T_DDDTRCODDEF)) AND 0 = SIZE(TRUNC(FIELDTEXT))
 ; THEN LET FIELDTEXT = T_DDDTRCODDEF
END
;PROCEDURE OUTPUT DTRCODDEF OF DEFAUT_2
;BEGIN
;  IF NOT ENTRYMODE AND NOT CHANGEMODE AND NOT CORRECTMODE
;  THEN BEGIN
;  LET T_COMPTEUR = 0
;  LET FIELDTEXT = " "
;  WHILE RETRIEVING DEFAUT_3 &
;         VIA     CIECLE,                      &
;                 TRANUMTRA002                 &
;         USING   T_CIECLEMNU,                 &
;                 TRANUMTRA002 OF PDTRANCHE
;  BEGIN
;    LET T_COMPTEUR = T_COMPTEUR + 1
;    IF T_COMPTEUR = 2
;    THEN BEGIN
;           LET FIELDTEXT = DTRCODDEF OF DEFAUT_3
;           BREAK
;         END
;  END
;  END
;END

;-------------------------------------------------------------------------------
;
; Calcul la surface de la tranche
;

PROCEDURE PROCESS TRALON
BEGIN
  LET TZ_LARGEUR   = TRALON    OF PDTRANCHE
  LET TZ_HAUTEUR   = TRAHAU    OF PDTRANCHE
  LET TZ_EPAISSEUR = TRAEPA    OF PDTRANCHE
  DO INTERNAL CALVOL
  LET TRASURMC2        OF PDTRANCHE = TZ_MC2
  LET TRAVOLMC3        OF PDTRANCHE = TZ_VOLUME
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions minimum et maximum
;

PROCEDURE EDIT TRAHAU
BEGIN
  IF FIELDVALUE < T_DIM_MIN_HAU_TR OR &
     FIELDVALUE > T_DIM_MAX_HAU_TR
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
  IF FIELDVALUE > BLOHAUNET        OF PDBLOC
  THEN BEGIN
    WARNING 2934 ; Cette dimension est plus grande que la dimension nette
  END
  IF FIELDVALUE > BLOHAUBRU        OF PDBLOC
  THEN BEGIN
    ERROR 2933 ; Cette dimension est plus grande que la dimension brute
  END
END


;-------------------------------------------------------------------------------
;
; Calcul la surface de la tranche
;

PROCEDURE PROCESS TRAHAU
BEGIN
  LET TZ_LARGEUR   = TRALON    OF PDTRANCHE
  LET TZ_HAUTEUR   = TRAHAU    OF PDTRANCHE
  LET TZ_EPAISSEUR = TRAEPA    OF PDTRANCHE
  DO INTERNAL CALVOL
  LET TRASURMC2        OF PDTRANCHE = TZ_MC2
  LET TRAVOLMC3        OF PDTRANCHE = TZ_VOLUME
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions minimum et maximum
;

PROCEDURE EDIT TRAEPA
BEGIN
  IF FIELDVALUE < T_DIM_MIN_EPA_TR OR &
     FIELDVALUE > T_DIM_MAX_EPA_TR
  THEN BEGIN
    ERROR 2951 ; Cette dimension hors des dimensions limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul la surface de la tranche
;

PROCEDURE PROCESS TRAEPA
BEGIN
  LET TZ_LARGEUR   = TRALON    OF PDTRANCHE
  LET TZ_HAUTEUR   = TRAHAU    OF PDTRANCHE
  LET TZ_EPAISSEUR = TRAEPA    OF PDTRANCHE
  DO INTERNAL CALVOL
  LET TRASURMC2        OF PDTRANCHE = TZ_MC2
  LET TRAVOLMC3        OF PDTRANCHE = TZ_VOLUME
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs
;

PROCEDURE INPUT TYFCODFIN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd101 MODE F &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
END


;-------------------------------------------------------------------------------
;
; Valide le statut du montage avant de permettre la saisit des tranches
;

PROCEDURE PREENTRY
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE <> "T"
  THEN BEGIN
    ERROR 3068 ; Le statut ne permet pas cette opération
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDTRANCHE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDTRANCHE &
     AND NOT NEWRECORD     OF PDTRANCHE &
     AND NOT DELETEDRECORD OF PDTRANCHE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ;
      IF     ALTEREDRECORD     OF DEFAUT_1 &
       AND NOT NEWRECORD       OF DEFAUT_1 &
       AND NOT DELETEDRECORD   OF DEFAUT_1
    THEN BEGIN
     GET DEFAUT_1 OPT    &
         VIA   CIECLE,TRANUMTRA002,RMSNUMSEQ,DTRCODDEF,DTRPOS &
         USING T_CIECLEMNU,T_DTRTRANUMTRA002_1,RMSNUMSEQ OF PDMONTAGE_BLOC,T_DTRCODDEF_OLD_1,"1"
     IF ACCESSOK
      THEN LET DTRCODDEF OF DEFAUT_1 = T_DTRCODDEF
     ELSE BEGIN
      LET CIECLE       OF DEFAUT_1 = T_CIECLEMNU
      LET TRANUMTRA002 OF DEFAUT_1 = T_DTRTRANUMTRA002_1
      LET DTRCODDEF    OF DEFAUT_1 = T_DTRCODDEF
      LET RMSNUMSEQ    OF DEFAUT_1 = RMSNUMSEQ OF PDMONTAGE_BLOC
      LET DTRPOS       OF DEFAUT_1 = "1"
     END
     PUT DEFAUT_1
    END

      IF     ALTEREDRECORD     OF DEFAUT_2 &
       AND NOT NEWRECORD       OF DEFAUT_2 &
       AND NOT DELETEDRECORD   OF DEFAUT_2
    THEN BEGIN
     GET DEFAUT_2 OPT    &
         VIA   CIECLE,TRANUMTRA002,RMSNUMSEQ,DTRCODDEF,DTRPOS &
         USING T_CIECLEMNU,T_DTRTRANUMTRA002_2,RMSNUMSEQ OF PDMONTAGE_BLOC,T_DTRCODDEF_OLD_2,"2"
     IF ACCESSOK
      THEN LET DTRCODDEF OF DEFAUT_2 = T_DTRCODDEF
     ELSE BEGIN
      LET CIECLE       OF DEFAUT_2 = T_CIECLEMNU
      LET TRANUMTRA002 OF DEFAUT_2 = T_DTRTRANUMTRA002_2
      LET DTRCODDEF    OF DEFAUT_2 = T_DTRCODDEF
      LET RMSNUMSEQ    OF DEFAUT_2 = RMSNUMSEQ OF PDMONTAGE_BLOC
      LET DTRPOS       OF DEFAUT_2 = "2"
     END
     PUT DEFAUT_2
    END


  FOR PDTRANCHE
  BEGIN
    IF ALTEREDRECORD OF PDTRANCHE &
       AND &
       NOT NEWRECORD OF PDTRANCHE
    THEN BEGIN
      LET TRADATMAJ        OF PDTRANCHE = SYSDATE
      LET TRAHREMAJ        OF PDTRANCHE = SYSTIME/10000
      LET TRAUSRMAJ        OF PDTRANCHE = LOGONID
    END
  END
END
;-----------------------------------------------------------
PROCEDURE UPDATE
BEGIN
 PUT PDMONTAGE_BLOC
 FOR PDTRANCHE
   BEGIN
       PUT PDTRANCHE
       PUT DEFAUT_1
       PUT DEFAUT_2
    END
END


BUILD DETAIL LIST

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Sciage ************************************
*                                                                              *
* Num. montage..: xxxxxxxxxxx                    Date sciage...: xxxxxxxx      *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Num. bloc.....: xxxxxxxxxxxx                                                 *
*                                                                              *
*********************************** Tranches ***********************************
*                                                                              *
*     No tranche   Long    Haut    Epais   Surface       Fini                  *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*     xxxxxxx      xxxx    xxxx    xxxx    xxxxxxxxxx    xx                    *
*                                                                              *
********************************************************************************
@ENDIF
