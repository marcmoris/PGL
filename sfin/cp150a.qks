;/T/ Test - Calcul de date
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 17 mai 1993
;
;    Description..: Ce sous-écran sert à effectuer des tests pour le calcul
;                   des date due pour les termes de paiements.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN cp150a NOMODE NOACTION FIELDMARK &
              FROM 5,8 TO 13,72 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY

USE cp150a.hlp

;-------------------------------------------------------------------------------
;
; Variables saisie ou affiché à l'écran.
;
TEMPORARY T_TERCLE_TMP    CHARACTER SIZE 4
TEMPORARY T_DAT_TMP       DATE

;-------------------------------------------------------------------------------
;
; Validation du code de terme.
;
FILE VTER_DSC         REFERENCE
  ACCESS VIA TERCLE &
         USING T_TERCLE_TMP
;
; Pour le calcul de la date.
;
FILE CPTERME          REFERENCE
  ACCESS VIA TERCLE &
         USING T_TERCLE_TMP


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_TERCLE    CHARACTER SIZE  4 ; Popup sur terme de paiement.
TEMPORARY T_TERCAT    CHARACTER SIZE  4 ; Popup sur terme de paiement.
;
; Variable temporaire pour le calcul des dates.
;
USE ustertmp NOLIST

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST

DRAW 1,1 TO 9,65

TITLE &
@IF FRANCAIS
      " Test - Calcul de date " &
@ELSE
      " %%%TEST " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

USE ustitoff NOLIST

ALIGN (3,3,19) (,,24)

FIELD T_TERCLE_TMP HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Code de terme.:" &
@ELSE
        LABEL "Term code.....:" &
@ENDIF
        UPSHIFT  &
        REQUIRED &
        LOOKUP ON VTER_DSC &
          MESSAGE 419 ; Ce terme est inexistant.

FIELD TERDSC OF VTER_DSC &
        DISPLAY

SKIP 1

ALIGN (3,3,19)

FIELD T_DAT_TMP HIDDEN ID   10 FORMAT YYYYMMDD PATTERN "####/##/##"&
        REQUIRED &
@IF FRANCAIS
        LABEL "Date cible....:"
@ELSE
        LABEL "Target date...:"
@ENDIF

SKIP 1

FIELD TZ_DATRES HIDDEN ID 15 format yyyymmdd PATTERN "####/##/##"&
        DISPLAY &
@IF FRANCAIS
        LABEL "Date due......:"
@ELSE
        LABEL "Due date......:"
@ENDIF


;-------------------------------------------------------------------------------
; Popup sur les termes de paiements.
;
PROCEDURE INPUT T_TERCLE_TMP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TERCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_TERCAT = ""
    RUN SCREEN cp102 MODE F PASSING T_TERCLE, T_TERCAT
    LET FIELDTEXT = TRUNCATE( T_TERCLE )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul de la date due.
;
USE ustercal NOLIST


;-------------------------------------------------------------------------------
;
;
PROCEDURE PROCESS T_DAT_TMP
BEGIN
  LET TZ_DATCAL = T_DAT_TMP
  DO INTERNAL TER_DATE_DUE
  DISPLAY TZ_DATRES
END

BUILD
;
;       ********************* Test - Calcul de date *********************
;       *                                                               *
;       * Code de terme.: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;       *                                                               *
;       * Date cible....: xxxxxxxx                                      *
;       *                                                               *
;       * Date due......: xxxxxxxx                                      *
;       *                                                               *
;       *****************************************************************
