;/T/ Historique des comptes à recevoir
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Alain Côté
;    Date Création.: 27 juillet 1993
;
;    Description...: Impression de l'historique des transactions d'un client
;                    pour un interval de période. Cet historique imprime toutes
;                    les factures(FA,CR,NA) avec les transactions associées à
;                    celles-ci.
;
;    Note..........: Ce QTP est nécéssaire à cause du compteur T_CNTCAR. Qui va
;                    permettre de calculer le solde du client pour la période.
;
;    Paramètres....: Cie du menu
;                    Numéro du client
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST

RUN cr304t


GLOBAL TEMPORARY G_PECDEB CHARACTER SIZE  4 PARM ; Période de début
GLOBAL TEMPORARY G_PECFIN CHARACTER SIZE  4 PARM ; Période de fin de la recherche


;-------------------------------------------------------------------------------

REQUEST RECHERCHE_TRANSACTIONS

ACCESS CRCPT_A_REC &
  ;
  ; Information sur le client.
  ;
  LINK REFCIECLE OF CRCPT_A_REC, &
       REFCLENUM OF CRCPT_A_REC  &
    TO CLICIECLE , &
       CLICLE OF                 CRCLIENT &
  ;
  ; Historique de la facture.
  ;
  LINK CIECLE OF CRCPT_A_REC, &
       CARCLE OF CRCPT_A_REC  &
    TO CIECLE, &
       CARCLE OF              CRCAR_HISTO OPTIONAL


CHOOSE CIECLE    PARM, & ; Compagnie du menu
       REFCLENUM PARM    ; Numéro de client


;
; On ne sélectionne que les factures (FA) et paiements non appliqués (NA)
; les notes de crédit, ajustements, dépots sont retrouvés grace à l'historique.
; Les transactions de l'historiques ne sont sélectionnées que si elles font
; parties de l'interval de période demandée. Les pièces (FA,NA) ne sont
; sélectionnées que si elles font parties de l'interval de période demandé.
; Ou bien, si elles possèdent des transactions dans cet interval.
;

;
; On sélectionne les factures, les crédits et les paiements
; non appliqués comme étant une transaction maitre.
;
SELECT CRCPT_A_REC IF    CARTYPECR OF CRCPT_A_REC = "FA" &
                      OR CARTYPECR OF CRCPT_A_REC = "CR" &
                      OR CARTYPECR OF CRCPT_A_REC = "NA"

;
; Les transactions de l'historiques sont sélectionnées que si elles font
; parties de l'interval de période demandée. L'historique contient les
; ajustements, les notes de crédit, les mauvaises créance et les encaissements.
;

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF CRCAR_HISTO &
         IF PECCLE OF CRCAR_HISTO[1:1] < "8" &
         ELSE "0" + PECCLE OF CRCAR_HISTO
; Ajustement pour le changement de siècle
DEFINE G_PECDEB_SIE CHARACTER SIZE 5 = "1" + G_PECDEB &
         IF G_PECDEB[1:1] < "8" &
         ELSE "0" + G_PECDEB
; Ajustement pour le changement de siècle
DEFINE G_PECFIN_SIE CHARACTER SIZE 5 = "1" + G_PECFIN &
         IF G_PECFIN[1:1] < "8" &
         ELSE "0" + G_PECFIN

SELECT CRCAR_HISTO &
          IF     (D_PECCLE_SIE >= G_PECDEB_SIE OR G_PECDEB = "" ) &
             AND (D_PECCLE_SIE <= G_PECFIN_SIE OR G_PECFIN = "" )

;
; Les transactions maitre(facture) sont sélectionnées que si elles font
; parties de l'interval de période demandé, ou bien, si elles possèdent des
; transactions(historique) dans cet interval.
;

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE2 CHARACTER SIZE 5 = "1" + PECCLE OF CRCPT_A_REC &
         IF PECCLE OF CRCPT_A_REC[1:1] < "8" &
         ELSE "0" + PECCLE OF CRCPT_A_REC
; Ajustement pour le changement de siècle
DEFINE G_PECDEB_SIE2 CHARACTER SIZE 5 = "1" + G_PECDEB &
         IF G_PECDEB[1:1] < "8" &
         ELSE "0" + G_PECDEB
; Ajustement pour le changement de siècle
DEFINE G_PECFIN_SIE2 CHARACTER SIZE 5 = "1" + G_PECFIN &
         IF G_PECFIN[1:1] < "8" &
         ELSE "0" + G_PECFIN

SELECT IF   (     (D_PECCLE_SIE2 >= G_PECDEB_SIE2 OR G_PECDEB = "") &
              AND (D_PECCLE_SIE2 <= G_PECFIN_SIE2 OR G_PECFIN = "") ) &
         OR RECORD CRCAR_HISTO EXISTS


;
; Compteur permettant d'identifier la première ligne du CAR pour calculer le solde (voir le quiz)
;
TEMPORARY T_CNTCAR INTEGER SIZE 4



SORT ON REFCLENUM OF CRCPT_A_REC, & ; Numéro de client
        CARDAT    OF CRCPT_A_REC, & ; Date de la facture
        CARCLE    OF CRCPT_A_REC, & ; Numéro de facture
        CRHDAT    OF CRCAR_HISTO    ; Date de l'historique



ITEM T_CNTCAR COUNT RESET AT CARCLE

;
; Transfert des données vers le quiz
;
SUBFILE WCR304A KEEP &
          INCLUDE   CLICLE    OF CRCLIENT                     , &
                    CLINOM    OF CRCLIENT                     , &
                    DEVCLE    OF CRCLIENT                     , &
                    G_PECDEB                                  , &
                    G_PECFIN                                  , &
                    CARCLE    OF CRCPT_A_REC                  , &
                    PECCLE    OF CRCPT_A_REC                  , &
                    LCRCLE    OF CRCPT_A_REC                  , &
                    CARDAT    OF CRCPT_A_REC                  , &
                    CARDATJOU OF CRCPT_A_REC                  , &
                    CARMNT    OF CRCPT_A_REC                  , &
                    T_CNTCAR                                  , &
                    CRHCLE2   OF CRCAR_HISTO                  , &
                    CRHDAT    OF CRCAR_HISTO                  , &
                    CRHDATJOU OF CRCAR_HISTO                  , &
                    CRHTYPTRA OF CRCAR_HISTO                  , &
                    PECCLE    OF CRCAR_HISTO ALIAS CRH_PECCLE , &
                    LCRCLE    OF CRCAR_HISTO ALIAS CRH_LCRCLE , &
                    CRHMNTCAR OF CRCAR_HISTO                  , &
                    CRHMNTREC OF CRCAR_HISTO                  , &
                    CRHMNTESC OF CRCAR_HISTO
BUILD cr304t
