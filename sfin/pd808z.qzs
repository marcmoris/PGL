;/T/ Liste des Ventes directes non transférées aux Comptes à recevoir.
;
;/P/ Réalisé par.......: Marc Poulin
;    Date..............: 24 janvier 2001
;
;    Description.......: Ce rapport vise à identifier quelles factures (liées exclisivement aux ventes directe) n'ont pas encore 
;                        fait l'objet d'un transfert aux Comptes à recevoir. Cette liste est destinée à améliorer les contrôles 
;                        exécutés en fin de mois.
;
;    Référence.........: Demande de réalisation 000122.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST ; Définitions de paramètres d'impression


; Accès aux tables de la production

access pdfacture link  ciecle    of pdfacture               , &  ; Table des factures.
                       ftrnumfac of pdfacture                 &
                   to  ciecle                               , &
                       ftrnumfac of pdlot_facture   optional  &  ; Table des lots de factures associés aux transferts.


                 link  ciecle    of pdfacture               , & 
                       ftrnumcar of pdfacture                 &
                   to  ciecle                               , &                     
                       carcle    of crcar_histo     optional  &  ; Table des encaissements (Comptes à recevoir)

                 link ("----"                               , &
                       clicle    of pdfacture               , &
                       ciecle    of pdfacture)                &
                   to  cliciecle                            , &
                       clicle                               , &
                       ciecle    of vclc_cli        optional     ; Table des clients.


; Accès via index selon critères de sélection.

choose ciecle    parm, &  ; Code de compagnie passé en paramètre
       ftrtypfac "D"      ; Type de facture - Vente directe.


; Accès séquentiel selon critères de sélection.

select pdfacture if ftrflgblc of pdfacture <> "BL" ; La facture ne doit pas être liée au module de Gestion de blocs.

select if not record pdlot_facture exists and not record crcar_histo exists


; Tri des données.

sort on ciecle of pdfacture   , &  ; Code de compagnie.
        ftrnumfac of pdfacture     ; Numéro de facture.


; Variables obligatoires pour les entêtes standards.

define dz_nomrap1 character size 60 = "Factures (Ventes directes) non transférées aux C.A.R."
define dz_nomrap2 character size 60 = ""


; Numéro du rapport.

define dz_numrap  character size  7 = "pd808z"


; Nom de la compagnie.

define dz_cienom  character size 40 = parm

use ustitstd nolist ; Entête standardisée
   skip 002 &
    tab 001 " Numéro   Date         Date        Commande                                                                   " &
   skip 001 &
    tab 001 " facture  facturation  impression  interne    Client                                           Montant facture" &
   skip 001 &
    tab 001 " -------  -----------  ----------  ---------  -----------------------------------------------  ---------------" &
   skip


; Impression du détail

report                             &
    tab 002 ftrnumfac of pdfacture &
    tab 011 ftrdatfac of pdfacture &
            format yyyymmdd        &
    tab 024 ftrdatprt of pdfacture &
            format yyyymmdd        &
    tab 036 comnumcom of pdfacture &
    tab 047 clicle    of pdfacture &
    tab 054 clinom    of vclc_cli  &
    tab 096 ftrmnt    of pdfacture


; Total fin de rapport

final footing                      &
    skip 003                       &
     tab 002 "Nombre de factures:" &
              count                &
              picture "^^^,^^^"   
    
    
build pd808z


@if format   

 Numéro   Date         Date        Commande                                                                   
 facture  facturation  impression  interne    Client                                           Montant facture
 -------  -----------  ----------  ---------  -----------------------------------------------  ---------------
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-
 xxxxxxx  xxxx/xx/xx   xxxx/xx/xx  xxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999,999,999.99-


 Nombre de factures: 999,999

@endif
