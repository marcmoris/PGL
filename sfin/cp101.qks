;/T/ Consultation paiement   T_CIECLE, T_LBQCPTENC, T_PAMCLE, T_PAMNUMPIM,...
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 5 Juillet 1993
;
;    Description..: Permet de visionner et de choisir les paiement
;
;    Parametres...: T_CIECLE        = Compagnie
;                   T_LBQCPTENC     = Compte d'encaisse
;                   T_PAMCLE        = Numéro informatique (accepte les "@")
;                   T_PAMNUMPIM     = Numéro pré-imprimé  (accepte les "@")
;                   T_PAMFLGJOU     = "1" = paiements journalisés
;                                     "0" = paiements non journalisés
;                                     " " = on ne vérifie pas la journalisation
;                   T_PAMFLGIMP     = "1" = paiements imprimés
;                                     "0" = paiements non imprimés
;                                     "@" = on ne vérifie pas l'impression
;                   T_PAMCODANN     = "1" = paiements annulés
;                                     "0" = paiements non annulés
;                                     "@" = on ne vérifie pas l'annulation
;                   T_PAMCLEDEB     = Borne de début de l'intervalle à
;                                     consulter
;                   T_PAMCLEFIN     = Borne de fin de l'intervalle à
;                                     consulter
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp101 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE   , &
                        T_LBQCPTENC, &
                        T_PAMCLE   , &
                        T_PAMNUMPIM, &
                        T_PAMFLGJOU, &
                        T_PAMFLGIMP, &
                        T_PAMCODANN, &
                        T_PAMCLEDEB, &
                        T_PAMCLEFIN


USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP101"

USE cp101.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE    CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_LBQCPTENC CHARACTER SIZE 6 RESET AT STARTUP
TEMPORARY T_PAMCLE    CHARACTER SIZE 7 RESET AT STARTUP
TEMPORARY T_PAMNUMPIM CHARACTER SIZE 8 RESET AT STARTUP
TEMPORARY T_PAMFLGJOU CHARACTER SIZE 1 RESET AT STARTUP
TEMPORARY T_PAMFLGIMP CHARACTER SIZE 1 RESET AT STARTUP
TEMPORARY T_PAMCODANN CHARACTER SIZE 1 RESET AT STARTUP
TEMPORARY T_PAMCLEDEB CHARACTER SIZE 7 RESET AT STARTUP
TEMPORARY T_PAMCLEFIN CHARACTER SIZE 7 RESET AT STARTUP

;-------------------------------------------------------------------------------

FILE CPPAIEMENT   PRIMARY OCCURS 10

  ACCESS  VIA   CIECLE   , &
                LBQCPTENC, &
                PAMCLE   , &
                PAMNUMPIM, &
                PAMFLGIMP, &
                PAMCODANN, &
                PAMTYPECR, &
                PAMCCLDAT  &
          USING T_CIECLE   , &
                T_LBQCPTENC, &
                T_PAMCLE   , &
                T_PAMNUMPIM, &
                T_PAMFLGIMP, &
                T_PAMCODANN, &
                "CH"       , &
                0            &
          ORDERBY   CIECLE   , &
                    LBQCPTENC, &
                    PAMCLE

SELECT  IF    (     ( T_PAMFLGJOU = "1" AND PAMDATJOU OF CPPAIEMENT <> 0) &
                OR  ( T_PAMFLGJOU = "0" AND PAMDATJOU OF CPPAIEMENT =  0) &
                OR    T_PAMFLGJOU = " "                                   &
              )                                                           &
          AND (     (     PAMCLE OF CPPAIEMENT >= T_PAMCLEDEB             &
                      OR  T_PAMCLEDEB = "" )                              &
                AND (     PAMCLE OF CPPAIEMENT <= T_PAMCLEFIN             &
                      OR  T_PAMCLEFIN = "" )                              &
              )

;-------------------------------------------------------------------------------

USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP 1

TITLE &
@IF FRANCAIS
      "   Paiem.  Chèque   daté du  Rèf.     " &
@ELSE
      "   %%%%%%  %%%%%%   %%%%%%%  %%%%     " &
@ENDIF
      AT ,2

SKIP

ALIGN (2,2,5) (,,13) (,,22) (,,33)

CLUSTER OCCURS WITH CPPAIEMENT

FIELD PAMCLE    OF CPPAIEMENT DISPLAY HIDDEN LABEL ">>"
FIELD PAMNUMPIM OF CPPAIEMENT DISPLAY
FIELD PAMDAT    OF CPPAIEMENT  FORMAT YYYYMMDD PATTERN "####/##/##" DISPLAY
FIELD REFCLENUM OF CPPAIEMENT DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_PAMCLE, "@")
  THEN LET T_PAMCLE = ""
  IF 0 <> IND(T_PAMNUMPIM, "@")
  THEN LET T_PAMNUMPIM = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_PAMCLE    = PAMCLE OF CPPAIEMENT
  LET T_PAMNUMPIM = PAMNUMPIM OF CPPAIEMENT
  RETURN
END

BUILD
;
;*****************************************
;*   Paiem.  Chèque   daté du  Rèf.      *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*>> xxxxxxx xxxxxxxx xxxxxxxx xxxxxx    *
;*****************************************
