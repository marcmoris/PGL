;/T/ Liste des produits par localisation
;
;/P/ Programmeur..........: Martin Perron
;
;    Date de Création..: 14 décembre 1999
;
;    Description..........: Liste des produits par localisation.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin
;    Date..............: 13 mars 2000.
;    Description.......: Permettre la saisie du caractère de recherche générique (@) lors du lancement du traitement
;                        et corriger l'identifiant du programme produisant cette liste.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 19.
;    
;    Signet............: MP-00-03-10_S19.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS ARPRD_ENT &
  LINK CIECLE OF ARPRD_ENT , &
       PRDCLE OF ARPRD_ENT   &
  TO   CIECLE              , &
       PRDCLE OF ARPRODUIT   &

  LINK CIECLE OF ARPRD_ENT , &
       ENTCLE OF ARPRD_ENT , &
       ENLCLE OF ARPRD_ENT   &
  TO   CIECLE              , &
       ENTCLE              , &
       ENLCLE OF ARENT_LOCAL 

CHOOSE CIECLE              PARM,   & ;Compagnie du menu
       ENTCLE              PARM,   & ;Entrepot
       ENLCLE              PARM      ;Localisation

DEFINE D_PRDSTU CHARACTER SIZE 1 = PARM

SELECT IF (PRDSTU OF ARPRODUIT = D_PRDSTU OR &
           D_PRDSTU = "")

SORT ON ENTCLE , &
        ENLCLE , &
        PRDCLE

;--------------------------------------------------------------------
;
; Variables obligatoires pour les en-tetes standards
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
       "Liste des produits par localisation"

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
       ""

;
; Numero du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR355Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;DEFINE D_CBIDSC CHARACTER SIZE 15 = CBIDSC OF VCBI_DSC


USE ustit132 NOLIST

SKIP 2 &
  TAB 005 " Numéro               Numéro       Description                                      Numéro             Quantité " &
SKIP &

  TAB 005 "Entrepôt              Produit      Produit                                       Localisation           En main " &
SKIP &
  TAB 005 "--------          -------------    ----------------------------------------      ------------        ----------  " &
SKIP 2

REPORT &
  TAB 008 ENTCLE    OF ARPRD_ENT &
  TAB 024 PRDCLE    OF ARPRD_ENT &
  TAB 040 PRDDSC1   OF ARPRODUIT &
  TAB 090 ENLCLE    OF ARPRD_ENT &
  TAB 105 PREQTEPHY OF ARPRD_ENT &
  TAB 120 "____________"     &
SKIP 3


BUILD ar355z
