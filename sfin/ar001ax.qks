;/T/ Localisation des entrepôts - Validation de la destruction
;
;/P/ Programmeur..: Nancy Marceau
;    Date création: 28 février 1994
;
;    Description..: Cet écran vérifie si la localisation d'entrepôt reçue en 
;                   paramètre est référée dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar001ax NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING ARENT_LOCAL, &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Localisation d'entrepôts
;
FILE ARENT_LOCAL   MASTER

;
; Produits des entrepôts
;
FILE ARPRD_ENT   DESIGNER

;
; Décomptes physiques
;
FILE ARDECOMPTE    DESIGNER
  ACCESS VIA   CIECLE, &
               ENTCLE  &
         USING CIECLE OF ARENT_LOCAL, &
               ENTCLE OF ARENT_LOCAL 


FILE ARDEC_DETAIL  DESIGNER


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
; 
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  
  GET ARPRD_ENT  &
    VIA   CIECLE, &
          ENTCLE, &
          ENLCLE  & 
    USING CIECLE OF ARENT_LOCAL, &
          ENTCLE OF ARENT_LOCAL, &
          ENLCLE OF ARENT_LOCAL OPTIONAL

  IF ACCESSOK
  THEN ERROR 1320 ; Destruction annulée, la localisation est référée 
                  ; par les produits.

  WHILE RETRIEVING ARDECOMPTE 
  BEGIN
    GET ARDEC_DETAIL &
      VIA   CIECLE, &
            ENLCLE  &  
      USING CIECLE OF ARDECOMPTE, &
            ENLCLE OF ARENT_LOCAL OPTIONAL

      IF ACCESSOK
      THEN ERROR 1336 ; Destruction annulée, la localisation est référée
                      ; par les décomptes physiques.

  END
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
