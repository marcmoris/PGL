;/T/ Impression de la liste d'erreur de l'inventaire physique des caissons.
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-05-07
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste d'erreur des caissons de l'inventaire physique
;                   de caisson de Granicor.
;
;    Paramètres....: Compagnie
;
;===============================================================================
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INCAISSON                                        &
 LINK   CAINUMCAI       OF INCAISSON,                   &
        CIECLE          OF INCAISSON                    &
   TO   CAINUMCAI,                                      &
        CIECLE          OF PDCAISSON                    &
 LINK   PJTABR          OF PDCAISSON,                   &
        PJTANN          OF PDCAISSON,                   &
        PJTNUMSEQ       OF PDCAISSON,                   &
        CIECLE                                          &
   TO   PJTABR,                                         &
        PJTANN,                                         &
        PJTNUMSEQ,                                      &
        CIECLE          OF PDPROJET OPTIONAL

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG308Z"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-ERREUR CHARACTER SIZE 40 =                             &
        "NUMERO DE CAISSON INEXISTANT"                          &
        IF NOT RECORD PDCAISSON EXIST                           &
        ELSE                                                    &
;        "CAISSON DE TUILE"                                     &
;        IF TPDTYPPRD OF PDCAISSON = "T"                        &
;        ELSE                                                   &

;       Dans le nouveau système des produits, on ne parle plus d'une tuile
;       mais d'un produit de dimension

        "CAISSON DE PRODUIT DE DIMENSION"                       &
        IF TPDTYPPRD OF PDCAISSON = "PD"                        &
        ELSE                                                    &
        "STATUT DU CAISSON INVALIDE  "                          &
        IF CAISTU OF PDCAISSON <> "I"                           &
        ELSE                                                    &
        "NUMERO DE PROJET INVALIDE   "                          &
        IF PJTNUMPRO OF INCAISSON <> PJTNUMPRO OF PDPROJET      &
        ELSE ""

SELECT IF D-ERREUR <> ""

SORT ON ICANUMPGE OF INCAISSON  &
     ON ICADATINV OF INCAISSON  &
     ON ICAHREINV OF INCAISSON  &
     ON CAINUMCAI OF INCAISSON

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES ERREURS DE L'INVENTAIRE PHYSIQUE DE CAISSONS                          ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4 &
  TAB 005 "                         PROJET     NUMÉRO      PROJET              NUMÉRO     TYPE" &
SKIP 1 &
  TAB 005 "PAGE  NO. CAISSON  LOCA  ABRÉGÉ     PROJET     PHYSIQUE   STATUT  EXPÉDITION  PRODUIT  ERREUR (Description)" &
SKIP 1 &
  TAB 005 "COMMENTAIRE" &
SKIP 1 &
  TAB 005 "-----------------  ----  ------     ------     --------   ------  ----------  -------  --------------------" &
SKIP

REPORT &
  TAB 003 ICANUMPGE     OF INCAISSON                            &
  TAB 011 CAINUMCAI     OF INCAISSON                            &
  TAB 024 ICANUMLOC     OF INCAISSON                            &
  TAB 032 PJTABR        OF PDCAISSON                            &
  TAB 041 PJTNUMPRO     OF PDPROJET                             &
  TAB 052 PJTNUMPRO     OF INCAISSON                            &
  TAB 065 CAISTU        OF PDCAISSON                            &
  TAB 073 SPRNUMSOR     OF PDCAISSON PICTURE "^^^^^^"           &
  TAB 085 TPDTYPPRD     OF PDCAISSON                            &
  TAB 092 D-ERREUR                                              &
SKIP                                                            &
  TAB 005 ICADESC       OF INCAISSON                            &
SKIP 2

FINAL FOOTING SKIP 2 &
       TAB 20 "NOMBRE TOTAL DE CAISSONS EN ERREUR: " COUNT

BUILD ig308z_1

;===============================================================================

ACCESS PDCAISSON                                        &
  LINK  CAINUMCAI       OF PDCAISSON,                   &
        CIECLE          OF PDCAISSON                    &
    TO  CAINUMCAI,                                      &
        CIECLE          OF INCAISSON OPTIONAL           &
  LINK  PJTABR          OF PDCAISSON,                   &
        PJTANN          OF PDCAISSON,                   &
        PJTNUMSEQ       OF PDCAISSON,                   &
        CIECLE          OF PDCAISSON                    &
    TO  PJTABR,                                         &
        PJTANN,                                         &
        PJTNUMSEQ,                                      &
        CIECLE          OF PDPROJET

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG308Z"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

SELECT IF CAISTU OF PDCAISSON <> "E" &
;       AND TPDTYPPRD OF PDCAISSON <> "T" AND &

;      (T = Tuile) remplacé par (PD = Produit de dimension)

       AND TPDTYPPRD OF PDCAISSON <> "PD" &
       AND NOT RECORD INCAISSON EXIST

DEFINE D-ERREUR CHARACTER SIZE 40 = &
        "NUMERO DE CAISSON EXISTE AVANT NON APRES"

SORT ON ICANUMPGE OF INCAISSON &
     ON ICADATINV OF INCAISSON &
     ON ICAHREINV OF INCAISSON &
     ON CAINUMCAI OF INCAISSON

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES CAISSONS EXISTANTS AVANT ET NON APRES                                 ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4 &
  TAB 003 "NUMÉRO     PROJET  NUMÉRO     CODE      NUMÉRO     TYPE    ERREUR" &
SKIP 1 &
  TAB 003 "CAISSON    ABRÉGÉ  PROJET     STATUT  EXPÉDITION  PRODUIT  DESCRIPTION" &
SKIP 1 &
  TAB 003 "-------    ------  ------     ------  ----------  -------  -----------" &
SKIP 1

REPORT &
  TAB 003 CAINUMCAI     OF PDCAISSON                            &
  TAB 016 PJTABR        OF PDCAISSON                            &
  TAB 022 PJTNUMPRO     OF PDPROJET                             &
  TAB 035 CAISTU        OF PDCAISSON                            &
  TAB 043 SPRNUMSOR     OF PDCAISSON PICTURE "^^^^^^"           &
  TAB 055 TPDTYPPRD     OF PDCAISSON                            &
  TAB 062 D-ERREUR                                              &
SKIP 2

FINAL FOOTING SKIP 2 &
       TAB 20 "NOMBRE TOTAL DE CAISSONS EN ERREUR: " COUNT

BUILD ig308z_2

;===============================================================================

ACCESS INCAISSON

CHOOSE CIECLE PARM

SORT ON CAINUMCAI OF INCAISSON

DEFINE D-NBRE-CAISSON INTEGER SIZE 2 = 1

SET SUBFILE NAME W_IG308_1 AT CAINUMCAI

REP SUMMARY &
        CIECLE          OF INCAISSON        &
        CAINUMCAI       OF INCAISSON        &
        D-NBRE-CAISSON SUB

BUILD ig308z_3

;===============================================================================

ACCESS *W_IG308_1                               &
  LINK CAINUMCAI        OF W_IG308_1,           &
       CIECLE           OF W_IG308_1            &
    TO CAINUMCAI,                               &
       CIECLE           OF INCAISSON

SELECT IF D-NBRE-CAISSON > 1

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG308Z"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-ERREUR CHARACTER SIZE 40 = &
        "NUMERO DE CAISSON EN DOUBLE"

SORT ON ICANUMPGE OF INCAISSON &
     ON ICADATINV OF INCAISSON &
     ON ICAHREINV OF INCAISSON &
     ON CAINUMCAI OF INCAISSON

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES CAISSONS EN DOUBLE                                                    ")

USE ustitgor200_45 NOLIST

SKIP 3 &
  TAB 025 D-TITRE-CENTRE &
SKIP 4 &
  TAB 005 "PAGE  NO. CAISSON  LOCA  ERREURS (Description)                     COMMENTAIRE" &
SKIP 1 &
  TAB 005 "----  -----------  ----  ---------------------                     -----------" &
SKIP 1

REPORT &
  TAB 003 ICANUMPGE     OF INCAISSON            &
  TAB 011 CAINUMCAI     OF INCAISSON            &
  TAB 024 ICANUMLOC     OF INCAISSON            &
  TAB 030 D-ERREUR                              &
  TAB 072 ICADESC       OF INCAISSON            &
SKIP 2

FINAL FOOTING SKIP 2 &
       TAB 20 "NOMBRE TOTAL DE CAISSONS EN ERREUR: " COUNT

BUILD ig308z_4
