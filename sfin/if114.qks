;/T/ Consultation d'un bloc à expédier
;
;/P/ Programmeur..: Marc Poulin
;    Date Creation: 26 octobre 1999
;
;    Description..: Permet de visionner et de choisir un bloc à expédier
;
;    *** ATTENTION - LA PROCEDURE FIND A ÉTÉ MODIFIÉE ***
;-------------------------------------------------------------------------------

SCREEN if114 NOMODE NOACTION FIELDMARK &
             FROM 12,27 TO 23,132      &
             ACTIVITIES FIND           &
             RECEIVING  T_CIECLE     , &
                        T_TGRCODGRA  , &
                        T_IFCCLE     , &
                        T_BLCCLE     , &
                        T_BLCRED     , &
                        T_CLICLE     , &
                        T_FOUCLE     , &
                        T_IFLCLE     , &
                        TS_TGRCODGRA , &
                        TS_CATCLE    , &
                        TS_CLRCLE    , &
                        TS_QLTCLE    , &
                        TS_ETRCLE    , &
                        T_NUM_BLOC_1 , &
                        T_NUM_BLOC_2 , &
                        T_NUM_BLOC_3 , &
                        T_NUM_BLOC_4 , &
                        T_NUM_BLOC_5 , &
                        T_NUM_BLOC_6 , &
                        T_NUM_BLOC_7 , &
                        T_NUM_BLOC_8

USE ustrasse NOLIST

USE ussectmp NOLIST
    "IF114"

USE if114.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE         CHARACTER SIZE 04  RESET AT STARTUP
TEMPORARY T_TGRCODGRA      CHARACTER SIZE 03  RESET AT STARTUP
TEMPORARY T_IFCCLE         CHARACTER SIZE 02  RESET AT STARTUP
TEMPORARY T_BLCCLE         CHARACTER SIZE 06  RESET AT STARTUP
TEMPORARY T_BLCRED         CHARACTER SIZE 01  RESET AT STARTUP
TEMPORARY T_CLICLE         CHARACTER SIZE 06  RESET AT STARTUP
TEMPORARY T_FOUCLE         CHARACTER SIZE 06  RESET AT STARTUP
TEMPORARY T_IFLCLE         CHARACTER SIZE 09  RESET AT STARTUP

TEMPORARY TS_TGRCODGRA     CHARACTER SIZE 03  RESET AT STARTUP
TEMPORARY TS_CATCLE        CHARACTER SIZE 03  RESET AT STARTUP
TEMPORARY TS_CLRCLE        CHARACTER SIZE 03  RESET AT STARTUP
TEMPORARY TS_QLTCLE        CHARACTER SIZE 03  RESET AT STARTUP
TEMPORARY TS_ETRCLE        CHARACTER SIZE 04  RESET AT STARTUP

TEMPORARY T_NUM_BLOC_1     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_2     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_3     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_4     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_5     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_6     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_7     CHARACTER SIZE 12  RESET AT STARTUP
TEMPORARY T_NUM_BLOC_8     CHARACTER SIZE 12  RESET AT STARTUP

;-------------------------------------------------------------------------------

FILE IFBLOC PRIMARY OCCURS 9
  ACCESS VIA CIECLE      , &
             TGRCODGRA   , &
             IFCCLE      , &
             BLCCLE      , &
             BLCRED      , &
             CATCLE      , &
             CLRCLE      , &
             QLTCLE      , &
             ETRCLE      , &
             CLICLE        &
       USING T_CIECLE    , &
             TS_TGRCODGRA, &
             T_IFCCLE    , &
             T_BLCCLE    , &
             T_BLCRED    , &
             TS_CATCLE   , &
             TS_CLRCLE   , &
             TS_QLTCLE   , &
             TS_ETRCLE   , &
             T_CLICLE      &
     ORDERBY TGRCODGRA   , &
             IFCCLE      , &
             BLCCLE      , &
             BLCRED      , &
             CATCLE      , &
             CLRCLE      , &
             QLTCLE      , &
             ETRCLE

SELECT IF EXNCLE       OF IFBLOC  = 0         AND &
         (PJTABR       OF IFBLOC <> " "       AND &
          PJTANN       OF IFBLOC <> " "       AND &
          COMNUMCOMINT OF IFBLOC <> " "       AND &
          DCINUMLIG    OF IFBLOC <> " ")      AND &
          FTRNUMFAC    OF IFBLOC  = 0         AND &

         (IFLCLE OF IFBLOC = T_IFLCLE OR IFLCLE OF IFBLOC = " ") AND &
         (FOUCLE OF IFBLOC = T_FOUCLE OR FOUCLE OF IFBLOC = " " OR T_FOUCLE = " " )

;-----------------------------------------------------------------------------
;
; Categorie de granit - Pour affichage
;
FILE IFCATEGORIE  REFERENCE  OCCURS WITH IFBLOC  OPEN 3
  ACCESS VIA CIECLE          , &
             CATCLE            &
       USING T_CIECLE        , &
             CATCLE OF IFBLOC

;-----------------------------------------------------------------------------
;
; Couleur - Pour affichage
;
FILE IFCOULEUR  REFERENCE  OCCURS WITH IFBLOC  OPEN 3
  ACCESS VIA CIECLE          , &
             CLRCLE            &
       USING T_CIECLE        , &
             CLRCLE OF IFBLOC

;-----------------------------------------------------------------------------
;
; Qualite de granit - Pour affichage
;
FILE IFQUALITE  REFERENCE  OCCURS WITH IFBLOC  OPEN 3
  ACCESS VIA CIECLE          , &
             QLTCLE            &
       USING T_CIECLE        , &
             QLTCLE OF IFBLOC

;-----------------------------------------------------------------------------
;
; Entrepôt - Pour affichage
;
FILE IFENTREPOT  REFERENCE  OCCURS WITH IFBLOC  OPEN 3
  ACCESS VIA CIECLE          , &
             ETRCLE            &
       USING T_CIECLE        , &
             ETRCLE OF IFBLOC

;-----------------------------------------------------------------------------
;
; Compagnie - Pour validation
;
FILE MCCOMPAGNIE  DESIGNER
  SELECT IF 1 = 2

;-------------------------------------------------------------------------------

TEMPORARY T_NUM_BLOC_SEL   CHARACTER SIZE 12

;-------------------------------------------------------------------------------

USE ushilite NOLIST

DRAW 1,01 TO 12,106

USE ustitoff NOLIST

SKIP 1

TITLE "   No bloc        Date    M3 total  ___Catégorie___ ____Couleur____ ____Qualité____ ____Entrepôt___"  AT ,6

ALIGN(2,3,6)(,,9)(,11,12)(,18,19)(,,21)(,,33)(,,42)(,,46)(,,58)(,,62)(,,74)(,,78)(,,90)(,,95)
CLUSTER OCCURS WITH IFBLOC
FIELD TGRCODGRA OF IFBLOC              &
      HIDDEN                           &
      LABEL ">>"                       &
      DISPLAY

FIELD IFCCLE OF IFBLOC                 &
      DISPLAY

FIELD BLCCLE OF IFBLOC                 &
      LABEL "-"                        &
      DISPLAY

FIELD BLCRED OF IFBLOC                 &
      LABEL "-"                        &
      DISPLAY

FIELD BLCDAT OF IFBLOC                 &
      FORMAT YYYYMMDD                  &
      DISPLAY

FIELD BLCM3TOT     OF IFBLOC           &
      DISPLAY

FIELD CATCLE       OF IFBLOC           &
      DISPLAY

FIELD CATDSCFRA    OF IFCATEGORIE      &
      DISPLAY                          &
      SIZE 11

FIELD CLRCLE       OF IFBLOC           &
      DISPLAY

FIELD CLRDSCFRA    OF IFCOULEUR        &
      DISPLAY                          &
      SIZE 11

FIELD QLTCLE       OF IFBLOC           &
      DISPLAY

FIELD QLTDSCFRA    OF IFQUALITE        &
      DISPLAY                          &
      SIZE 11

FIELD ETRCLE       OF IFBLOC           &
      DISPLAY

FIELD ETRDSCFRA001 OF IFENTREPOT       &
      DISPLAY                          &
      SIZE 10

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_TGRCODGRA = TGRCODGRA OF IFBLOC
  LET T_IFCCLE    = IFCCLE    OF IFBLOC
  LET T_BLCCLE    = BLCCLE    OF IFBLOC
  LET T_BLCRED    = BLCRED    OF IFBLOC

  RETURN
END

PROCEDURE FIND
BEGIN
  FOR MISSING IFBLOC
  BEGIN
    GET IFBLOC VIA CIECLE       , &
                   TGRCODGRA    , &
                   IFCCLE       , &
                   BLCCLE       , &
                   BLCRED       , &
                   CATCLE       , &
                   CLRCLE       , &
                   QLTCLE       , &
                   ETRCLE       , &
                   CLICLE         &
             USING T_CIECLE     , &
                   TS_TGRCODGRA , &
                   T_IFCCLE     , &
                   T_BLCCLE     , &
                   T_BLCRED     , &
                   TS_CATCLE    , &
                   TS_CLRCLE    , &
                   TS_QLTCLE    , &
                   TS_ETRCLE    , &
                   T_CLICLE       &
           ORDERBY TGRCODGRA    , &
                   IFCCLE       , &
                   BLCCLE       , &
                   BLCRED       , &
                   CATCLE       , &
                   CLRCLE       , &
                   QLTCLE       , &
                   ETRCLE

    LET T_NUM_BLOC_SEL = TGRCODGRA OF IFBLOC + IFCCLE OF IFBLOC &
                       + BLCCLE    OF IFBLOC + BLCRED OF IFBLOC

    IF T_NUM_BLOC_SEL = T_NUM_BLOC_1 OR T_NUM_BLOC_SEL = T_NUM_BLOC_2 OR &
       T_NUM_BLOC_SEL = T_NUM_BLOC_3 OR T_NUM_BLOC_SEL = T_NUM_BLOC_4 OR &
       T_NUM_BLOC_SEL = T_NUM_BLOC_5 OR T_NUM_BLOC_SEL = T_NUM_BLOC_6 OR &
       T_NUM_BLOC_SEL = T_NUM_BLOC_7 OR T_NUM_BLOC_SEL = T_NUM_BLOC_8
    THEN GET MCCOMPAGNIE VIA CIECLE USING " "
  END
END


BUILD LIST DETAIL

@IF FORMAT
                                                                                                   1         1         1         1
         1         2         3         4         5         6         7         8         9         0         1         2         3
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
                          ********************************************************************************************************** 01
                          *       No bloc        Date    M3 total  ___Catégorie___ ____Couleur____ ____Qualité____ ____Entrepôt___ * 02
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 03
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 04
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 05
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 06
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 07
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 08
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 19
                          * >> xxxxx-xxxxxx-x xxxxxxxxxx  xxxxxx   xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxx xxxxxxxxxxx xxxx xxxxxxxxxx * 10
                          ********************************************************************************************************** 13
@ENDIF                                                               
                                                                    