;/T/ Cons. des transactions(type)      CIECLE, FOUCLE, CAPCLE, TYPTRAPAT
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 15 avril 1992
;
;    Description..: Permet de visionner les transactions selon un ou des types
;                   d'écriture spécifique qui ont affectés une facture. Sert
;                   à expliquer le détail d'un montant, exemple le montant
;                   des ajustements dans les factures. La table utilisé
;                   contient que des transactions journsalisées.
;
;    Paramètre....: CIECLE    ==> - compagnie du compte à payer.
;                                 - segment de la clé de recherche.
;
;                   FOUCLE    ==> - numéro de fournisseur.
;                                 - segment de la clé de recherche.
;
;                   CAPCLE    ==> - numéro du compte à payer.
;                                 - segment de le clé de recherche.
;
;                   TYPTRAPAT ==> - Type de transaction ( ne pas confondre avec
;                                   le type d'écriture CPHTYPECR)
;                                 - critère de sélection sous la forme de
;                                   PATTERN, ex: "NC|AJ"
;
;     Remarque.....: Cette écran ne retourne aucune valeur.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp106 NOMODE NOACTION FIELDMARK &
             FROM 11,33 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_FOUCLE, &
                        T_CAPCLE, &
                        T_TYPTRAPAT

USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP106"

USE cp106.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE    CHARACTER SIZE 4
TEMPORARY T_FOUCLE    CHARACTER SIZE 6
TEMPORARY T_CAPCLE    CHARACTER SIZE 15
TEMPORARY T_TYPTRAPAT CHARACTER SIZE 30

;-------------------------------------------------------------------------------

FILE CPCAP_HISTO      PRIMARY &
                      OCCURS 10

  ACCESS  VIA     FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE     &
          USING   T_CIECLE , &
                  T_FOUCLE , &
                  T_CAPCLE   &
          ORDERBY FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE   , &
                  CPHTIMSTP

  SELECT IF MATCHPATTERN(CPHTYPTRA OF CPCAP_HISTO,TRUNC(T_TYPTRAPAT))

;-------------------------------------------------------------------------------

DEFINE D_MNTTOT FLOAT SIZE 8 &
        = CPHMNTCAP OF CPCAP_HISTO &
            IF    CPHTYPTRA OF CPCAP_HISTO <> "CH" &
              OR  CPHTYPTRA OF CPCAP_HISTO <> "CA" ELSE &
          CPHMNTPYE OF CPCAP_HISTO &
        + CPHMNTESC OF CPCAP_HISTO

;-------------------------------------------------------------------------------

USE ushilite NOLIST
DRAW 1,1 TO 13,48
SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE &
@IF FRANCAIS
      "Numéro          Date journ.         Brut" &
@ELSE
      "Number          Post. date          Gross" &
@ENDIF
      AT 2,6

ALIGN(3,3,6) (,,22) (,,33)

CLUSTER OCCURS WITH CPCAP_HISTO

FIELD CPHCLE3   OF CPCAP_HISTO HIDDEN LABEL ">>"
FIELD CPHDATJOU OF CPCAP_HISTO FORMAT YYYYMMDD PATTERN "####/##/##"
FIELD D_MNTTOT &
         PICTURE "^^,^^^,^^^.^^ " TRAIL SIGN "-" SIGNIF 5
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

BUILD
@IF FORMAT

                                ************************************************
                                *    Numéro          Date journ.         Brut  *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                * >> xxxxxxxxxxxxxxx  xxxxxxxx  xxxxxxxxxxxxxx *
                                ************************************************
@ENDIF
