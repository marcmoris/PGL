;/T/ 2.5.6 Facturation (Gestion de blocs) / Commentaires blocs.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-03-14
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les commentaires pour ventes directes de bloc.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin
;    Date..............: 05 avril 2000
;    Description.......: Incrémentation du numéro de facture par compagnie.
;
;    Référence.........: Stabilisation du module Gestion de blocs (point 2).
;
;    Signet............: MP-00-04-05_S02.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd225e ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDFACTURE,          &
                        PDBLOC_FACTURE


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_FACTURE IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD225E"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd225e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_SILENT           CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Facture
;

FILE PDFACTURE MASTER


;-------------------------------------------------------------------------------
;
; Bloc facture
;

FILE PDBLOC_FACTURE MASTER


;-------------------------------------------------------------------------------
;
; Commentaire facture
;

FILE PDCOMMEN_BLOC PRIMARY &
                   NOITEM &
                   OCCURS 11 TIMES

  ACCESS VIA     CIECLE,           &
                 FTRNUMFAC,        &
                 BLONUMBLOAPR      &
         USING   T_CIECLEMNU,                           &
                 FTRNUMFAC         OF PDBLOC_FACTURE,   &
                 BLONUMBLOAPR      OF PDBLOC_FACTURE    &
         ORDERBY CIECLE,           &
                 FTRNUMFAC,        &
                 BLONUMBLOAPR,     &
                 COBNUMLIG


  ITEM CIECLE           OF PDCOMMEN_BLOC INITIAL T_CIECLEMNU
  ITEM BLONUMGRA001APR  OF PDCOMMEN_BLOC INITIAL BLONUMGRA001APR  OF PDBLOC_FACTURE FIXED
  ITEM BLONUMGRA002APR  OF PDCOMMEN_BLOC INITIAL BLONUMGRA002APR  OF PDBLOC_FACTURE FIXED
  ITEM BLORECAPR        OF PDCOMMEN_BLOC INITIAL BLORECAPR        OF PDBLOC_FACTURE FIXED
  ITEM BLONUMBLOAPR     OF PDCOMMEN_BLOC INITIAL BLONUMBLOAPR     OF PDBLOC_FACTURE FIXED
  ITEM FTRNUMFAC        OF PDCOMMEN_BLOC INITIAL FTRNUMFAC        OF PDFACTURE FIXED


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les factures
;

FILE PDSEQ_FACTURE DESIGNER TRANSACTION NO_SEQ
  ACCESS VIA CIECLE USING T_CIECLEMNU ; MP-00-04-05_S02.


;-------------------------------------------------------------------------------
;
; Client
;

FILE VCLC_CLI        REFERENCE

  ACCESS VIA     CLICIECLE,        &
                 CLICLE,           &
                 CIECLE            &
         USING   "----",                                &
                 CLICLE            OF PDFACTURE,        &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
FILE PDLOT_TRANSFERT DESIGNER
;-------------------------------------------------------------------------------
FILE PDLOT_FACTURE DESIGNER
;-------------------------------------------------------------------------------
;
; Variable servant au traitement
;

TEMPORARY T_NOLIG INT SIZE 04 RESET AT STARTUP ; Pour no ligne automatique.


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Facturation bloc " &
@ELSE
      " %%%Ventes directes " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD FTRNUMFAC        OF PDFACTURE        &
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,28)


FIELD CLICLE           OF PDFACTURE        &
        DISPLAY                            &
        FIXED


FIELD CLINOM           OF VCLC_CLI         &
        DISPLAY                            &
        FIXED


DRAW 8,2 TO 8,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Commentaire " &
@ELSE
      " %%%Commentaire " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      "    Lig.   Description                                Imp. " &
@ELSE
      "%%% Lig.   Description                                Imp. " &
@ENDIF
      AT ,2


ALIGN (05,05,06) (,,13) (,,57)


CLUSTER OCCURS WITH PDCOMMEN_BLOC ID BASE 90


FIELD COBNUMLIG        OF PDCOMMEN_BLOC    &
        HIDDEN                             &
        LABEL " "


FIELD COBCOM           OF PDCOMMEN_BLOC    &
        HIDDEN


FIELD COBFLGIMP        OF PDCOMMEN_BLOC    &
        HIDDEN


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT COBNUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT ) AND &
     COBNUMLIG OF PDCOMMEN_BLOC = 0
  THEN BEGIN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE PROCESS COBNUMLIG
BEGIN
  LET T_NOLIG = COBNUMLIG OF PDCOMMEN_BLOC
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;

PROCEDURE INPUT COBFLGIMP
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;

PROCEDURE OUTPUT COBFLGIMP
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDCOMMEN_BLOC &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDCOMMEN_BLOC &
     AND NOT NEWRECORD     OF PDCOMMEN_BLOC &
     AND NOT DELETEDRECORD OF PDCOMMEN_BLOC &
     AND TZ_SECMOD = "0"
  THEN ERROR 2


  ;
 ;--validation du status du lot
 ;
  GET PDLOT_FACTURE OPTIONAL &
    VIA   CIECLE,          &
          FTRNUMFAC        &
    USING T_CIECLEMNU,     &
          FTRNUMFAC        OF PDFACTURE
  IF ACCESSOK
  THEN BEGIN
   GET PDLOT_TRANSFERT OPTIONAL &
    VIA   CIECLE,          &
          LTRNUMLOT        &
    USING CIECLE,     &
          LTRNUMLOT        OF PDLOT_FACTURE
    IF ACCESSOK
    THEN BEGIN
     IF LTRSTU OF PDLOT_TRANSFERT = "T"
      THEN ERROR 2

    END
   END

  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = FTRNUMFAC OF PDFACTURE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_FACTURE OPTIONAL                      ; MP-00-04-05_S02.
    LET CIECLE        OF PDSEQ_FACTURE = T_CIECLEMNU; MP-00-04-05_S02.
    LET SEFNUMRAP     OF PDSEQ_FACTURE = SEFNUMRAP OF PDSEQ_FACTURE + 1
    LET FTRNUMFAC     OF PDFACTURE     = SEFNUMRAP OF PDSEQ_FACTURE
    PUT PDSEQ_FACTURE
    DISPLAY FTRNUMFAC OF PDFACTURE
  END

END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Ventes directes ********************************
*                                                                              *
* No. facture...: xxxxxxxxx                                                    *
* Client........: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
********************************************************************************
*                                                                              *
*    Lig.   Description                                Imp.                    *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*                                                                              *
********************************************************************************
@ENDIF
