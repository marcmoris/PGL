;/T/ 2.1.1 Redimension de bloc.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-07
;
;    Description..: Le but de cette unité de traitement est de redimensionner
;                   un bloc.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd177e ACTIONBAR                   &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             ACTIVITIES FIND              &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM ,         &
                       T_FOUNO,           &
                       T_FOUNOM ,         &
                       T_RDBDATRED,       &
                       PDBLOC ,           &
                       PDRECEPTION ,      &
                       PDRECEPTION_BLOC

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD177E"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd177e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Variable reçu en parametre
;
TEMPORARY T_FOUNO     CHARACTER SIZE 06 RESET AT STARTUP ; No fournisseur
TEMPORARY T_FOUNOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom fournisseur
TEMPORARY T_RDBDATRED INT SIZE 4 RESET AT STARTUP

;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC MASTER


;-------------------------------------------------------------------------------
;
; Réception
;
FILE PDRECEPTION MASTER


;-------------------------------------------------------------------------------
;
; Réception de bloc
;
FILE PDRECEPTION_BLOC MASTER


;-------------------------------------------------------------------------------
;
; Bloc inscription du/des nouveaux blocs
;

FILE PDBLOC PRIMARY &
            ALIAS BLO_NEW &
            NODELETE &
            NOITEM

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR                       &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDBLOC,            &
                 BLONUMGRA002APR OF PDBLOC             &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

         ITEM CIECLE OF PDBLOC INITIAL T_CIECLEMNU

   SELECT IF T_RDBDATRED <> 0
;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TGRCODGRA                       &
         USING   T_CIECLEMNU,                    &
                 TGRCODGRA OF BLO_NEW


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
;
;
TEMPORARY T_MC3_BRU FLOAT SIZE 8 RESET AT STARTUP ; Volume brut du nouveau bloc
TEMPORARY T_MC3_NET FLOAT SIZE 8 RESET AT STARTUP ; Volume net du nouveau bloc
TEMPORARY T_MC3_PYE FLOAT SIZE 8 RESET AT STARTUP ; Volule à payer du nouveau bloc
TEMPORARY T_FLAG_BLOSTU CHARACTER SIZE 1 RESET AT STARTUP ; Flag du statut du bloc

;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Redimensionnement de bloc " &
@ELSE
      " %%%Redimensionnement de bloc " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


TITLE "-" AT 5,23

TITLE "-" AT 5,29

SKIP TO LINE 5

ALIGN (,3,19) (,23,24) (,29,30) (,53,69)


FIELD BLONUMGRA001APR OF PDBLOC               &
@IF FRANCAIS
        LABEL "Nun. bloc.....:"               &
@ELSE
        LABEL "%%%. bloc.....:"               &
@ENDIF
        DISPLAY                               &
        NUMERIC                               &
        FIXED

FIELD BLONUMGRA002APR  OF PDBLOC              &
        LABEL "-"                             &
        DISPLAY                               &
        NUMERIC                               &
        FIXED


FIELD BLORECAPR        OF PDBLOC              &
        LABEL "-"                             &
        DISPLAY                               &
        NUMERIC                               &
        FIXED


FIELD BLOSTU           OF PDBLOC              &
        DISPLAY                               &
        FIXED


SKIP TO LINE 6


ALIGN (,3,19) (,28,44) (,53,69)


FIELD BLOLONBRU OF PDBLOC                     &
        DISPLAY                               &
        FIXED

FIELD BLOHAUBRU OF PDBLOC                     &
        DISPLAY                               &
        FIXED

FIELD BLOLARBRU OF PDBLOC                     &
        DISPLAY                               &
        FIXED

ALIGN (,3,19) (,53,69)

FIELD BLOVOLMC3BRU     OF PDBLOC              &
        DISPLAY                               &
        LABEL "Volume brut...:"               &
        FIXED

FIELD RCTNUMREC OF PDRECEPTION                &
        DISPLAY                               &
        FIXED

ALIGN (,3,19) (,,28)

FIELD T_FOUNO                                 &
        DISPLAY                               &
        FIXED                                 &
        LABEL "Fournisseur...:"

FIELD T_FOUNOM                                &
        DISPLAY                               &
        FIXED

DRAW 10,1 TO 10,80

SKIP TO LINE 10

TITLE &
@IF FRANCAIS
      " Nouveau bloc " &
@ELSE
      " %%%Nouveau bloc " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 12


ALIGN (,3,19) (,23,24) (,29,30) (,53,69)


FIELD BLONUMGRA001APR OF BLO_NEW              &
@IF FRANCAIS
        LABEL "Nun. bloc.....:"               &
@ELSE
        LABEL "%%%. bloc.....:"               &
@ENDIF
        NUMERIC                               &
        DISPLAY


FIELD BLONUMGRA002APR OF BLO_NEW              &
        LABEL "-"                             &
        DISPLAY                               &
        NUMERIC


FIELD BLORECAPR       OF BLO_NEW              &
        LABEL "-"                             &
        DISPLAY


FIELD BLOSTU          OF BLO_NEW              &
        DISPLAY


SKIP TO LINE 14

ALIGN (,3,19)(,,25)

FIELD TGRCODGRA OF BLO_NEW                    &
        NUMERIC                               &
        DISPLAY                               &
        BWZ


FIELD TGRDSCFRA001 OF PDTYPE_GRANIT           &
        DISPLAY

SKIP TO LINE 16

ALIGN (,3,19)

FIELD BLOLONBRU OF BLO_NEW                    &
        HIDDEN ID 15                          &
        DISPLAY


FIELD BLOHAUBRU OF BLO_NEW                    &
        HIDDEN ID 20                          &
        DISPLAY


FIELD BLOLARBRU OF BLO_NEW                    &
        HIDDEN ID 25                          &
        DISPLAY

ALIGN (,3,19)

FIELD T_MC3_BRU                               &
        DISPLAY                               &
        LABEL "Total m3 brut.:"               &
        SIGNIFICANCE 6                        &
        OUTPUT SCALE 4                        &
        PICTURE "^^.^^^^"


SKIP TO LINE 16

ALIGN (,29,45)

FIELD BLOLONNET OF BLO_NEW                    &
        HIDDEN ID 30                          &
        DISPLAY

FIELD BLOHAUNET OF BLO_NEW                    &
        HIDDEN ID 35                          &
        DISPLAY

FIELD BLOLARNET OF BLO_NEW                    &
        HIDDEN ID 40                          &
        DISPLAY


ALIGN (,29,45)

FIELD T_MC3_NET                               &
        DISPLAY                               &
        LABEL "Total m3 nette:"               &
        SIGNIFICANCE 6                        &
        OUTPUT SCALE 4                        &
        PICTURE "^^.^^^^"

SKIP TO LINE 16

ALIGN (,55,71)

FIELD BLOLONPYE OF BLO_NEW                    &
        HIDDEN ID 45                          &
        DISPLAY

FIELD BLOHAUPYE OF BLO_NEW                    &
        HIDDEN ID 50                          &
        DISPLAY

FIELD BLOLARPYE OF BLO_NEW                    &
        HIDDEN ID 55                          &
        DISPLAY

ALIGN (,55,71)

FIELD T_MC3_PYE                               &
        DISPLAY                               &
        LABEL "Total m3 payer:"               &
        SIGNIFICANCE 6                        &
        OUTPUT SCALE 4                        &
        PICTURE "^^.^^^^"



;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;
;
USE uscalvol NOLIST

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Calcul du volume du bloc à redimesionner
;
PROCEDURE POSTFIND
BEGIN
  ;
  ; Calcul du volume brut du bloc
  ;
  LET TZ_LARGEUR   = BLOLONBRU OF BLO_NEW
  LET TZ_HAUTEUR   = BLOHAUBRU OF BLO_NEW
  LET TZ_EPAISSEUR = BLOLARBRU OF BLO_NEW
  DO INTERNAL CALVOL
  LET T_MC3_BRU    = TZ_VOLUME
  DISPLAY T_MC3_BRU
  ;
  ; Calcul du volume net du bloc
  ;
  LET TZ_LARGEUR   = BLOLONNET OF BLO_NEW
  LET TZ_HAUTEUR   = BLOHAUNET OF BLO_NEW
  LET TZ_EPAISSEUR = BLOLARNET OF BLO_NEW
  DO INTERNAL CALVOL
  LET T_MC3_NET    = TZ_VOLUME
  DISPLAY T_MC3_NET
  ;
  ; Calcul du volume à payer du bloc
  ;
  LET TZ_LARGEUR   = BLOLONPYE OF BLO_NEW
  LET TZ_HAUTEUR   = BLOHAUPYE OF BLO_NEW
  LET TZ_EPAISSEUR = BLOLARPYE OF BLO_NEW
  DO INTERNAL CALVOL
  LET T_MC3_PYE    = TZ_VOLUME
  DISPLAY T_MC3_PYE
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************** Redimensionnement de bloc ***************************
*                                                                              *
* Nun. bloc.....: xxxx-xxxxx-x                      Statut........: x          *
* Longueur brut.: xxxx     Hauteur brut..: xxxx     Largeur brut..: xxxx       *
* Volume brut...: xxxxxxxxx                         No. réception.: xxxxxxxxx  *
* Fournisseur...: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
********************************* Nouveau bloc *********************************
*                                                                              *
* Nun. bloc.....: xxxx-xxxxx-x                      Statut........: x          *
*                                                                              *
* Code granit...: xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
* Longueur brut.: xxxx      Longueur nette: xxxx      Longueur payer: xxxx     *
* Hauteur brut..: xxxx      Hauteur nette.: xxxx      Hauteur payer.: xxxx     *
* Largeur brut..: xxxx      Largeur nette.: xxxx      Largeur payer.: xxxx     *
* Total m3 brut.: xxxxxxx   Total m3 nette: xxxxxxx   Total m3 payer: xxxxxxx  *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
