;/T/ Gestion des fiches de sous-projets - Validation de la destruction
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 03 décembre 1995
;
;    Description..: Cet écran vérifie si la fiche de sous-projet reçue en
;                   paramètre est référée dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp002x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING GPPRO_ACTIVITE, &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Définitions de projets
;
FILE GPPRO_ACTIVITE   MASTER

FILE MCHIS_PROJET     REFERENCE
;FILE MCHIS_UNITE      REFERENCE
FILE CPCAP_IMPUTATION REFERENCE
FILE CRCAR_IMPUTATION REFERENCE
FILE CPPAM_IMPUTATION REFERENCE
FILE CRDPO_IMPUTATION REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN

  GET MCHIS_PROJET  VIA PRJCLE, &
                        PRACLE  &
                    USING PRJCLE OF GPPRO_ACTIVITE, &
                          PRACLE OF GPPRO_ACTIVITE  &
                    OPTIONAL
  IF ACCESSOK
  THEN ERROR 850  ; Destruction annulée, la fiche de projet est reféré
                  ; dans MCHIS_PROJET.

; GET MCHIS_UNITE   VIA PRJCLE &
;                   USING PRJCLE OF GPPRO_ACTIVITE &
;                   OPTIONAL
; IF ACCESSOK
; THEN ERROR 851  ; Destruction annulée, la fiche de projet est reféré
;                 ; dans MCHIS_UNITE.

  GET CPCAP_IMPUTATION VIA   FOUCIECLE, &
                             PRJCLE   , &
                             PRACLE     &
                       USING CIECLE OF GPPRO_ACTIVITE, &
                             PRJCLE OF GPPRO_ACTIVITE, &
                             PRACLE OF GPPRO_ACTIVITE  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 852  ; Destruction annulée, la fiche de projet est reféré
                  ; dans CPCAP_IMPUTATION.

  GET CRCAR_IMPUTATION VIA   CIECLE,    &
                             PRJCLE,    &
                             PRACLE     &
                       USING CIECLE OF GPPRO_ACTIVITE, &
                             PRJCLE OF GPPRO_ACTIVITE, &
                             PRACLE OF GPPRO_ACTIVITE  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 853  ; Destruction annulée, la fiche de projet est reféré
                  ; dans CRCAR_IMPUTATION.

  GET CPPAM_IMPUTATION VIA   CIECLE,    &
                             PRJCLE,    &
                             PRACLE     &
                       USING CIECLE OF GPPRO_ACTIVITE, &
                             PRJCLE OF GPPRO_ACTIVITE, &
                             PRACLE OF GPPRO_ACTIVITE  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 854  ; Destruction annulée, la fiche de projet est reféré
                  ; dans CPPAM_IMPUTATION.

  GET CRDPO_IMPUTATION VIA   CIECLE,    &
                             PRJCLE,    &
                             PRACLE     &
                       USING CIECLE OF GPPRO_ACTIVITE, &
                             PRJCLE OF GPPRO_ACTIVITE, &
                             PRACLE OF GPPRO_ACTIVITE  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 855  ; Destruction annulée, la fiche de projet est reféré
                  ; dans CRDPO_IMPUTATION.


  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
