;/T/ Validation sommaire des factures / Notes de crédit / Crédits / Ajustements
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 4 février 1994
;
;    Description...: Permet de faire une liste de validation des factures,
;                    des notes de crédit et des ajustements.
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;


 USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
 SET NOBLANK           ; Empêche l'impression de ligne blanche


ACCESS *WCP307 &
  LINK REFCIECLE OF WCP307, &
       REFCLETYP OF WCP307, &
       REFCLENUM OF WCP307, &
       RADCLE    OF WCP307  &
    TO REFCIECLE          , &
       REFCLETYP          , &
       REFCLENUM          , &
       RADCLE             OF MCREF_ADRESSE OPTIONAL &

  LINK REFCIECLE OF WCP307, &
       REFCLETYP OF WCP307, &
       REFCLENUM OF WCP307  &
    TO REFCIECLE          , &
       REFCLETYP          , &
       REFCLENUM          OF MCREFERENCE OPTIONAL &

  LINK CPTCLE   OF WCP307   &
    TO CPTCLE             OF VCPT_DSC    OPTIONAL &

  LINK CIECLE   OF WCP307 , &
       UNACLE   OF WCP307   &
    TO CIECLE             , &
       UNACLE             OF MCUNITE_ADM OPTIONAL &

  LINK CIECLE OF WCP307   , &
       PRJCLE OF WCP307     &
    TO CIECLE             , &
       PRJCLE             OF GPPROJETS    OPTIONAL &

  LINK CIECLE OF WCP307   , &
       PRJCLE OF WCP307   , &
       PRACLE OF WCP307     &
    TO CIECLE             , &
       PRJCLE             , &
       PRACLE             OF GPPRO_ACTIVITE  OPTIONAL &

  LINK CIECLE OF WCP307  , &
       IMMCLE OF WCP307    &
    TO CIECLE             , &
       IMMCLE             OF IMIMMOBILISATION OPTIONAL &

  LINK CLICIECLE OF WCP307  , &
       CLICLE    OF WCP307  &
    TO CLICIECLE , &
       CLICLE OF CRCLIENT OPTIONAL



 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = & ; titre1  du rapport
@IF FRANCAIS
                   "Validation des factures/Notes de crédit/Crédits/Ajustements"
@ELSE
                   "%%%Validation des factures/Notes de crédit/Crédits/Ajustements"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Sommaire" ; titre2  du rapport

 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP307Z"       ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM           ; nom de la compagnie


USE usflgdet NOLIST                    ;Flag Bilingue (O/N)

DEFINE D_HRSBLC    CHARACTER SIZE 3 =  DZ_FLGAFFO IF 0 <> LCPNBRTRSERR OF WCP307 &
                                       ELSE DZ_FLGAFFN IF LCPNBRTRSERR OF WCP307 = 0

DEFINE D_CAPRTU    CHARACTER SIZE 3 =  DZ_FLGAFFO      IF CAPRTU OF WCP307 = "1" &
                                       ELSE DZ_FLGAFFN IF CAPRTU OF WCP307 = "0"

DEFINE D_JOURNALISE CHAR SIZE 12 = &                                    ;Indique si Journalisé
@IF FRANCAIS
                            "Journalisé" &
@ELSE
                            "Posted" &
@ENDIF
                            IF LCPDATJOU OF WCP307 <> 0 ELSE " "


DEFINE D_LCPNBRTRSDIF INTEGER SIZE 4 = LCPNBRTRSVER - LCPNBRTRS          ;Différence nbre transactions réel et nbre transactions évalué
DEFINE D_LCPCUMMNTDIF FLOAT SIZE 8   = LCPCUMMNTVER - LCPCUMMNT           ;Différence Montant transaction réel et Montant transaction évalué
;
; Vérification si montant entré par l'usager balance avec le total imputation si
; ne balance imprimer un "*"
;
DEFINE D_BALANCE  CHARACTER SIZE 1 = "*" IF CAPMNT    OF WCP307 <> &
                                            CAPCUMMNT OF WCP307 ELSE " "


SORT ON LCPCLE    OF WCP307 &          ; No lot
     ON REFCLENUM OF WCP307 &          ; No Client
     ON RADCLE    OF WCP307 &          ; Numéro d'adresse.
     ON CAPDAT    OF WCP307 &          ; Date de facture
     ON CAPCLE    OF WCP307 &          ; Numéro de facture
     ON CCITIMSTP OF WCP307            ; Date et heure imputation



 USE ustit180 NOLIST  ; Titre standard pour l'application à 220
SKIP 2&
@IF FRANCAIS
 TAB 001 "Lot....: "   &
@ELSE
 TAB 001 "Batch..: "   &
@ENDIF
 TAB 011 LCPCLE OF WCP307  &
 TAB 016 LCPDSC OF WCP307  &
 TAB 061 "Hors balance = " &
 TAB 076 D_HRSBLC          &
SKIP &
@IF FRANCAIS
 TAB 001 "Période: " &
@ELSE
 TAB 001 "Period.: " &
@ENDIF
 TAB 010 PECCLE OF WCP307 &
 TAB 061 D_JOURNALISE     &
SKIP 2 &
  TAB 001 "                              Numéro          Ty      Date     Facture         Date             " &
  TAB 106 "                                                      SS-            Unité                           Co Co  Refact" &
SKIP &
  TAB 001 "* Fournisseur                 facture         fa Ret  facture  référée         due          Montant brut" &
  TAB 106 "Dev          CTI         RTI     Montant net Projet   prj Immobilis.   adm.     Compte       Montant TF TP  client" &
SKIP &
  TAB 001 "- --------------------------- --------------- -- ---  -------- --------------- --------  ---------------" &
  TAB 106 "---  ----------- ----------- --------------- -------- --- ------------ -------- ------ ------------- -- --  ------" &
SKIP 2


REPORT &
  TAB 001  D_BALANCE                  PRINT AT CAPCLE &
  TAB 003  REFCLENUM OF WCP307        PRINT AT REFCLENUM &
  TAB 010  RADNOMABR OF MCREF_ADRESSE PRINT AT RADCLE &
  TAB 051  CAPDSC1   OF WCP307        PRINT AT CAPCLE &
  TAB 092  CAPDSC2   OF WCP307        PRINT AT CAPCLE &
SKIP 1 &
  TAB 031  CAPCLE      OF WCP307          PRINT AT CAPCLE &
  TAB 047  CAPTYPECR   OF WCP307          PRINT AT CAPCLE &
  TAB 050  D_CAPRTU                       PRINT AT CAPCLE &
  TAB 055  CAPDAT      OF WCP307          PRINT AT CAPCLE &
  TAB 064  T_CAPCLEREF OF WCP307          PRINT AT CAPCLE &
  TAB 080  CAPDATNET   OF WCP307          PRINT AT CAPCLE &
  TAB 090  CAPMNT      OF WCP307  &
    PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 PRINT AT CAPCLE &
                                 TRAILING SIGN "-" &
  TAB 106  DEVCLE      OF MCREFERENCE      PRINT AT CAPCLE &
  TAB 111  CAPMNTCTI   OF WCP307           PRINT AT CAPCLE &
  TAB 123  CAPMNTRTI   OF WCP307           PRINT AT CAPCLE &
  TAB 135  CAPMNTNET   OF WCP307  &
    PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 PRINT AT CAPCLE &
                                 TRAILING SIGN "-" &

  TAB 151 PRJCLE       OF WCP307           &
  TAB 160 PRACLE       OF WCP307           &
  TAB 164 IMMCLE       OF WCP307           &
  TAB 177 UNACLE       OF WCP307           &
  TAB 185 CPTCLE       OF WCP307           &
  TAB 193 CCIMNT       OF WCP307 PICTURE "^,^^^,^^^.^^ " SIGNIFICANCE 4 &
                                 TRAILING SIGN "-" &
  TAB 207 CCITAXCODTPS OF WCP307           &
  TAB 210 CCITAXCODTVQ OF WCP307           &
  TAB 214 CLICLE       OF WCP307


FOOTING AT LCPCLE &
SKIP 2 &
  TAB 069 " Montant cumulé         Montant brut              CTI         RTI     Montant net                                          Montant imputé" &
SKIP &
  TAB 069 "---------------      ---------------      ----------- ----------- ---------------                                         ---------------" &
SKIP &
@IF FRANCAIS
  TAB 011 "Total du Lot" &
@ELSE
  TAB 011 "Batch Total" &
@ENDIF
  TAB 024 LCPCLE      OF WCP307          &
  TAB 069 LCPCUMMNT   OF WCP307 SUBTOTAL AT LCPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 090 CAPMNT      OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 111 CAPMNTCTI   OF WCP307 SUBTOTAL AT CAPCLE &
  TAB 123 CAPMNTRTI   OF WCP307 SUBTOTAL AT CAPCLE &
  TAB 135 CAPMNTNET   OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 191 CCIMNT      OF WCP307 SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
SKIP &
  TAB 069 "---------------      ---------------      ----------- ----------- ---------------                                         ---------------" &
SKIP PAGE

FINAL FOOTING &
SKIP 2 &
  TAB 069 " Montant cumulé         Montant brut              CTI         RTI     Montant net                                          Montant imputé" &
SKIP &
  TAB 069 "---------------      ---------------      ----------- ----------- ---------------                                         ---------------" &
SKIP &
@IF FRANCAIS
  TAB 011 "Total pour tous les lots" &
@ELSE
  TAB 011 "Batch Total For All" &
@ENDIF
  TAB 069 LCPCUMMNT   OF WCP307 SUBTOTAL AT LCPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 090 CAPMNT      OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 111 CAPMNTCTI   OF WCP307 SUBTOTAL AT CAPCLE &
  TAB 123 CAPMNTRTI   OF WCP307 SUBTOTAL AT CAPCLE &
  TAB 135 CAPMNTNET   OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 191 CCIMNT      OF WCP307 SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
SKIP &
TAB 069 "---------------      ---------------      ----------- ----------- ---------------                                         ---------------" &
SKIP PAGE

BUILD cp307z
@IF FORMAT

                                                                                                   1         1         1         1         1         1         1         1         1         1          2         2
         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9          0         1
1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
CP307                                                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                         Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                                   Validation des factures/Notes de crédit/Crédits/Ajustements
                                                                                       Sommaire
                                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Lot....:  xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     Hors balance = xxx  Journalisé
Période: 99-99

                              Numéro          Ty      Date     Facture         Date                                                                            SS-            Unité                           Co Co  Refact
* Fournisseur                 facture         fa Ret  facture  référée         due          Montant brut Dev          CTI         RTI     Montant net Projet   prj Équipement adm.     Compte         Montant TF TP  client
- --------------------------- --------------- -- ---  -------- --------------- --------  --------------- ---  ----------- ----------- --------------- -------- --- ---------- -------- ------ --------------- -- --  ------
x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                              xxxxxxxxxxxxxxx xx xxx  xx/xx/xx xxxxxxxxxxxxxxx xx/xx/xx  999,999,999.99- xxx  999,999.99- 999,999.99- 999,999,999.99- xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx  xxxxxx


x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                              xxxxxxxxxxxxxxx xx xxx  xx/xx/xx xxxxxxxxxxxxxxx xx/xx/xx  999,999,999.99- xxx  999,999.99- 999,999.99- 999,999,999.99- xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx  xxxxxx


                                                                     Montant cumulé         Montant brut              CTI         RTI     Montant net                                          Montant imputé
                                                                    ---------------      ---------------      ----------- ----------- ---------------                                         ---------------
          Total du lot xxxx                                         999,999,999.99-      999,999,999.99-      999,999.99- 999,999.99- 999,999,999.99-                                         999,999,999.99-
                                                                    ---------------      ---------------      ----------- ----------- ---------------                                         ---------------


                                                                     Montant cumulé         Montant brut              CTI         RTI     Montant net                                          Montant imputé
                                                                    ---------------      ---------------      ----------- ----------- ---------------                                         ---------------
          Total pour tous les lots                                  999,999,999.99-      999,999,999.99-      999,999.99- 999,999.99- 999,999,999.99-                                         999,999,999.99-
@ENDIF
;================================================================
;
;   SOMMAIRE
;
;/M/ Nancy Breton, 98/11/19, Correction pour convertir les taxes en US s'il y a
;                            lieu.
;
;==================================================================

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
SET NOBLANK           ; Empêche l'impression de ligne blanche


ACCESS *WCP307 &
  LINK REFCIECLE OF WCP307, &
       REFCLETYP OF WCP307, &
       REFCLENUM OF WCP307  &
    TO REFCIECLE          , &
       REFCLETYP          , &
       REFCLENUM          OF MCREFERENCE OPTIONAL &
  LINK DEVCLE OF MCREFERENCE &
    TO DEVCLE             OF MCDEVISE OPTIONAL


 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = & ; titre1  du rapport
@IF FRANCAIS
                   "Validation des factures/Notes de crédit/Crédits/Ajustements"
@ELSE
                   "%%%Validation des factures/Notes de crédit/Crédits/Ajustements"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Sommaire" ; titre2  du rapport

 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP307ZB"       ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM           ; nom de la compagnie



DEFINE D_MNTCAN FLOAT SIZE 8 = ROUND(CAPMNTNET OF WCP307 * DEVTAU OF MCDEVISE)
DEFINE D_MNTCTI FLOAT SIZE 8 = ROUND(CAPMNTCTI OF WCP307 * DEVTAU OF MCDEVISE)
DEFINE D_MNTRTI FLOAT SIZE 8 = ROUND(CAPMNTRTI OF WCP307 * DEVTAU OF MCDEVISE)

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_WCP307 CHARACTER SIZE 5 = "1" + PECCLE OF WCP307 &
         IF PECCLE OF WCP307[1:1] < "8" &
         ELSE "0" + PECCLE OF WCP307

SORT ON D_PECCLE_WCP307 &
     ON LCPCLE    OF WCP307 &          ; No lot
     ON DEVCLE    OF MCREFERENCE &
     ON REFCLENUM OF WCP307 &          ; No Client
     ON RADCLE    OF WCP307 &          ; Numéro d'adresse.
     ON CAPDAT    OF WCP307 &          ; Date de facture
     ON CAPCLE    OF WCP307            ; Numéro de facture


 USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP &
@IF FRANCAIS
 TAB 001 "Période: " &
@ELSE
 TAB 001 "Period.: " &
@ENDIF
 TAB 010 PECCLE OF WCP307 &
SKIP 2 &
  TAB 007 "Lot      Devise        Montant brut                CTI         RTI     Montant net Montant net can nbr" &
SKIP &
  TAB 007 "---- ---------------  ---------------      ----------- ----------- --------------- --------------- ---"  &
SKIP 2


HEADING AT D_PECCLE_WCP307 &
SKIP PAGE

FOOTING AT DEVCLE &
  TAB 007 LCPCLE OF WCP307 &
  TAB 012 DEVDSCFRA OF MCDEVISE PICTURE "^^^^^^^^^^^^^^^" &
  TAB 029 CAPMNT      OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 050 D_MNTCTI              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ "     SIGNIFICANCE 4 &
  TAB 062 D_MNTRTI              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ "     SIGNIFICANCE 4 &
  TAB 074 CAPMNTNET   OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 090 D_MNTCAN              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 106 COUNT                          AT CAPCLE PICTURE "^^^"


FOOTING AT LCPCLE &
SKIP 2 &
  TAB 029 "---------------      ----------- ----------- ---------------  --------------- ---" &
SKIP &
@IF FRANCAIS
  TAB 001 "Total Lot" LCPCLE OF WCP307 &
@ELSE
  TAB 001 "Batch total" LCPCLE OF WCP307 &
@ENDIF
  TAB 029 CAPMNT      OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 050 D_MNTCTI              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ "     SIGNIFICANCE 4 &
  TAB 062 D_MNTRTI              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ "     SIGNIFICANCE 4 &
  TAB 074 CAPMNTNET   OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 090 D_MNTCAN              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 106 COUNT                          AT CAPCLE PICTURE "^^^" &
SKIP 3

FINAL FOOTING &
SKIP 2 &
  TAB 029 "---------------      ----------- ----------- ---------------  --------------- ---" &
SKIP &
@IF FRANCAIS
  TAB 001 "Total pour tous les lots" &
@ELSE
  TAB 001 "Batch Total For All" &
@ENDIF
  TAB 029 CAPMNT      OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 050 D_MNTCTI              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ "     SIGNIFICANCE 4 &
  TAB 062 D_MNTRTI              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ "     SIGNIFICANCE 4 &
  TAB 074 CAPMNTNET   OF WCP307 SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 090 D_MNTCAN              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
  TAB 106 COUNT                          AT CAPCLE PICTURE "^^^^" &
SKIP &
TAB 029 "===============      =========== =========== ===============  =============== ==="

BUILD cp307zb
@IF FORMAT

                                                                                                   1         1         1         1         1         1         1         1         1         1          2         2
         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9          0         1
1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
CP307                                                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                         Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                                   Validation des factures/Notes de crédit/Crédits/Ajustements
                                                                                       Sommaire
                                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Lot....:  xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     Hors balance = xxx  Journalisé
Période: 99=99

                              Numéro          Ty      Date     Facture         Date                                                                            SS=            Unité                           Co Co  Refact
* Fournisseur                 facture         fa Ret  facture  référée         due          Montant brut Dev          CTI         RTI     Montant net Projet   prj Équipement adm.     Compte         Montant TF TP  client
= =========================== =============== == ===  ======== =============== ========  =============== ===  =========== =========== =============== ======== === ========== ======== ====== =============== == ==  ======
x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                              xxxxxxxxxxxxxxx xx xxx  xx/xx/xx xxxxxxxxxxxxxxx xx/xx/xx  999,999,999.99= xxx  999,999.99= 999,999.99= 999,999,999.99= xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99= xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99= xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99= xx xx  xxxxxx


x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                              xxxxxxxxxxxxxxx xx xxx  xx/xx/xx xxxxxxxxxxxxxxx xx/xx/xx  999,999,999.99= xxx  999,999.99= 999,999.99= 999,999,999.99= xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99= xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99= xx xx  xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99= xx xx  xxxxxx


                                                                     Montant cumulé         Montant brut              CTI         RTI     Montant net                                          Montant imputé
                                                                    ===============      ===============      =========== =========== ===============                                         ===============
          Total du lot xxxx                                         999,999,999.99=      999,999,999.99=      999,999.99= 999,999.99= 999,999,999.99=                                         999,999,999.99=
                                                                    ===============      ===============      =========== =========== ===============                                         ===============


                                                                     Montant cumulé         Montant brut              CTI         RTI     Montant net                                          Montant imputé
                                                                    ===============      ===============      =========== =========== ===============                                         ===============
          Total pour tous les lots                                  999,999,999.99=      999,999,999.99=      999,999.99= 999,999.99= 999,999,999.99=                                         999,999,999.99=
@ENDIF
