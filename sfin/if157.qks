;/T/ Type de lot
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 21 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base   
;                   concernant le type de lot  
;
;
SCREEN if157 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF157"

;USE if157.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFTYPE_LOT IN DICT PRIMARY NOITEM &
                      OCCURS 16 TIMES

  ACCESS VIA     CIECLE,   &
                 TYLCLE    &
         USING   T_CIECLE, &
                 TYLCLE    &  
         REQUEST TYLCLE    &
         ORDERBY TYLCLE  

  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY TYLCLE

  ITEM CIECLE OF IFTYPE_LOT INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Type de lot d'expédition " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST


TITLE "Type" AT ,7
TITLE "Desc. francaise" AT ,18
TITLE "Desc. anglaise" AT ,46
SKIP

ALIGN(5,6,7) (,,18) (,,46)
CLUSTER OCCURS WITH IFTYPE_LOT
FIELD TYLCLE OF IFTYPE_LOT &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFTYPE_LOT &
          VIA   CIECLE, &
                TYLCLE  &
          USING T_CIECLE, &
                TYLCLE OF IFTYPE_LOT &
          MESSAGE 3249 ; PD349 Ce type de lot existe déjà

FIELD TYLDSCFRA OF IFTYPE_LOT &
      REQUIRED

FIELD TYLDSCANG OF IFTYPE_LOT 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Valeur par defaut pour la description anglaise
;
PROCEDURE INPUT TYLDSCANG
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = TYLDSCFRA OF IFTYPE_LOT
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un type de lot
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN if157x MODE SAME         &
                   PASSING IFTYPE_LOT, &
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3250 ; PD350 Impossible de supprimer ce type de lot,
                     ; il est d\351j\340 utilis\351e
   ELSE DELETE IFTYPE_LOT
END
BUILD
