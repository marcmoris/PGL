;/T/ Paiements - saisie du montant de pré-paiement.
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 12 octobre 1994
;
;    Description..:
;
;/M/ François Déry, 30 avril 1999
;       Modifier l'accèss au fichier de séquence car il a
;       été converti en format interbase au lieu d'indexé.
;
;/M/ Marie-Josée Hamel, 96.03.22,copier la description du paiement dans la fact.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp008f   ACTIONBAR &
                MODE LABEL "" AT 2,80 &
                NOACTION FIELDMARK RETAIN STARTUP &
                FROM 11,1 TO 23,80 &
                MESSAGE ON 24 &
                HELP POPUP FROM 4,9 TO 20,72 &
                RECEIVING T_CIECLEMNU   , &
                          T_CIENOM      , &
                          T_FOUCIECLE   , &
                          T_EMPCIECLE   , &
                          D_LCPATRJOU   , &
                          CPPAIEMENT    , &
                          MCREF_ADRESSE , &
                          MCREFERENCE

USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP008F"

USE cp008f.hlp NOLIST

USE usactecr NOLIST
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE CPPAIE_NA_SEQ IN DICT
                                                ; Le IN DICT est pour unix
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ2 READ WRITE &
                        RESERVING FOR PROTECTED WRITE CPPAM_SEQ IN DICT
                                                ; Le IN DICT est pour unix
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_RAD_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE MCRAD_SEQ IN DICT
                                                ; Le IN DICT est pour unix

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 RESET AT STARTUP ; compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom de la Cie
TEMPORARY T_FOUCIECLE CHARACTER SIZE  4 RESET AT STARTUP ; Cie du fournisseur
TEMPORARY T_EMPCIECLE CHARACTER SIZE  4 RESET AT STARTUP ; Cie de l'employé

DEFINE    D_LCPATRJOU CHARACTER SIZE  1

;-------------------------------------------------------------------------------
;
; Relation maitres provenant de l'écran principal contenants :
;
;     Le paiement,
;     Le fournisseur,
;     L'adresse (dans le cas d'un fournisseur divers)
;     La référence (pour avoir l'employé)
;     Le lot
;
FILE CPPAIEMENT     MASTER
FILE MCREF_ADRESSE  MASTER
FILE MCREFERENCE    MASTER

;-------------------------------------------------------------------------------
;
; On détermine la compagnie à utiliser pour retrouver la référence en fonction
; de son type (Fournisseur ou Employé)
;
DEFINE  D_FOUEMPCIECLE CHARACTER SIZE 4 &
        = T_EMPCIECLE IF REFCLETYP OF CPPAIEMENT = "E" ELSE &
          T_FOUCIECLE

;
; Si on ventile des factures alors on utilise la compagnie du fournisseur
; pour retrouver les documents. Mais si on ventile des rapports de dépenses,
; alors on utilise la compagnie du menu pour retrouver les documents car les
; rapports de dépenses sont spécifiques à chaque compagnie alors que les
; facture sont par fournisseur.
;
DEFINE  D_CIECLE CHARACTER SIZE 4 &
        = T_FOUCIECLE IF REFCLETYP OF CPPAIEMENT = "F" ELSE  &
          T_CIECLEMNU
;
; Dans le cas des rapports de dépenses, la clée du document est vide
;
DEFINE  D_FOUCLE CHARACTER SIZE 6 &
        = REFCLENUM OF CPPAIEMENT IF REFCLETYP OF CPPAIEMENT = "F" ELSE &
          "      "

;-------------------------------------------------------------------------------
;
; On ventile le pré-paiement directement dans l'historique
;
; Note : Lors de ce traitement, les triggers TCPHSTO et TCPHDEL vont
;        gérer le flag de réservation des cédules de paiements
;
;
FILE CPCAP_HISTO  PRIMARY &
                  NOITEMS

  ACCESS  VIA   CPHCLE1, &
                CPHCLE2, &
                CPHCLE3  &
          USING CIECLE    OF CPPAIEMENT, &
                LBQCPTENC OF CPPAIEMENT, &
                PAMCLE    OF CPPAIEMENT

  ITEM  FOUCIECLE OF CPCAP_HISTO  INITIAL D_CIECLE
  ITEM  FOUCLE    OF CPCAP_HISTO  INITIAL D_FOUCLE
  ITEM  CPHTIMSTP OF CPCAP_HISTO  INITIAL SYSDATE * 1000000 &
                                          + ROUND(SYSTIME / 100)
  ITEM  CPHCLE1   OF CPCAP_HISTO  INITIAL CIECLE    OF CPPAIEMENT
  ITEM  CPHCLE2   OF CPCAP_HISTO  INITIAL LBQCPTENC OF CPPAIEMENT
  ;
  ; L'énoncé fixed est égal à mettre l'item initial et final en même temps.
  ;
  ; Il y a une valeur FIXED car le PAMCLE est initialiser
  ; au preupdate. ainsi le lookup noton fonctionne.
  ;
  ITEM CPHCLE3   OF CPCAP_HISTO INITIAL PAMCLE    OF CPPAIEMENT FIXED
  ITEM CPHTYPECR OF CPCAP_HISTO INITIAL PAMTYPECR OF CPPAIEMENT
  ITEM CPHTYPTRA OF CPCAP_HISTO INITIAL PAMTYPECR OF CPPAIEMENT
  ITEM CPHDAT    OF CPCAP_HISTO INITIAL PAMDAT    OF CPPAIEMENT
  ITEM PECCLE    OF CPCAP_HISTO INITIAL PECCLE    OF CPPAIEMENT
  ITEM LCPCLE    OF CPCAP_HISTO INITIAL LCPCLE    OF CPPAIEMENT
  ITEM CPCCLELIG OF CPCAP_HISTO INITIAL 1

;-------------------------------------------------------------------------------
;
; Numérotation du numéro de paiement, le numéro est attribué par compagnie
; et par compte d'encaisse.
;
FILE CPPAM_SEQ  DESIGNER &
                TRANSACTION GEN_NUM_SEQ2

  ACCESS  VIA   CIECLE   , &
                LBQCPTENC  &
          USING CIECLE    OF CPPAIEMENT, &
                LBQCPTENC OF CPPAIEMENT

  ITEM CIECLE     OF CPPAM_SEQ INITIAL CIECLE    OF CPPAIEMENT
  ITEM LBQCPTENC  OF CPPAM_SEQ INITIAL LBQCPTENC OF CPPAIEMENT

;-------------------------------------------------------------------------------
;
; Numéro de séquence des adresses pour les clients divers.
;
FILE MCRAD_SEQ        DESIGNER &
                      TRANSACTION GEN_RAD_SEQ


;------------------------------------------------------------------------------
;
; Pour le paiement non-appliqué
;
FILE CPCPT_A_PAYER    DESIGNER

;------------------------------------------------------------------------------
;
; Pour le paiement non-appliqué
;
FILE CPCAP_CEDULE     DESIGNER

;------------------------------------------------------------------------------
;
; Pour le paiement non-appliqué
;
FILE CPPAIE_NA_SEQ    DESIGNER &
                      TRANSACTION GEN_NUM_SEQ

;------------------------------------------------------------------------------
;
; Pour le paiement non-appliqué
;
FILE CPPARAMETRE      REFERENCE

  ACCESS VIA    CIENMC &
         USING  "1"

;------------------------------------------------------------------------------
;
; Pour le paiement non-appliqué
;
FILE CPCPT_A_PAYER    REFERENCE &
                      ALIAS A_CAP_REFER

  ACCESS VIA    FOUCIECLE, &
                FOUCLE   , &
                CAPCLE     &
         USING  FOUCIECLE OF CPCAP_HISTO, &
                FOUCLE    OF CPCAP_HISTO, &
                CAPCLE    OF CPCAP_HISTO


FILE CPCAP_HISTO  DESIGNER &
                  ALIAS A_CPH_EXISTS


FILE VFOC_FOU     REFERENCE

  ACCESS  VIA   FOUCIECLE, &
                FOUCLE   , &
                FOCCIECLE  &
          USING REFCIECLE OF CPPAIEMENT , &
                REFCLENUM OF CPPAIEMENT , &
                T_CIECLEMNU

;-------------------------------------------------------------------------------

TEMPORARY T_FOUCLE    CHARACTER SIZE 6 ; Pour consulter les transactions
TEMPORARY T_REFCLENUM CHARACTER SIZE 6 ; Pour consulter les références
TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Pour consulter les transactions
TEMPORARY T_CAPCIECLE CHARACTER SIZE 4 ; Pour consulter les transactions
TEMPORARY T_REFCIECLE CHARACTER SIZE 4 ; Pour consulter les références
TEMPORARY T_REFCLETYP CHARACTER SIZE 1 ; pour les types de transactions
TEMPORARY T_TYPECRPAT CHARACTER SIZE 30; pattern pour consulation des documents
TEMPORARY T_FLGSLD    CHARACTER SIZE 1 ; flag pour consultation des documents
TEMPORARY T_CAPCLE    CHARACTER SIZE 15; pour consulter les documents á payer
TEMPORARY T_CPCCLELIG INTEGER   SIZE 2
;
; Variable pour valider qu'il n'y est pas un paiement automatique et une facture.
;
TEMPORARY T_VALPA     CHARACTER SIZE 1

;
; Variables temporaires permettant de calculer l'imputation d'un paiement
; automatique. (Voir traitement dans CP702T)
;
TEMPORARY T_CPIMNT    FLOAT   SIZE 8 ; Montant imputé
TEMPORARY T_CPIMNTTPS FLOAT   SIZE 8 ; Taxe Fédérale
TEMPORARY T_CPIMNTCTI FLOAT   SIZE 8 ; Taxe Fédérale remboursable
TEMPORARY T_MNTNETTPS FLOAT   SIZE 8 ; Montant sans TPS ni TVQ ( = NET )
TEMPORARY T_CPIMNTTVQ FLOAT   SIZE 8 ; Taxe Provinciale
TEMPORARY T_CPIMNTRTI FLOAT   SIZE 8 ; Taxe Provinciale remboursable
TEMPORARY T_MNTNETTVQ FLOAT   SIZE 8 ; Montant sans TVQ (mais avec TPS)
TEMPORARY T_PCTREPTOT INTEGER SIZE 2 ; Total du pourcentage de répartition
TEMPORARY T_MNTIMPTOT FLOAT   SIZE 8 ; Montant total imputé
TEMPORARY T_DIFMNT    FLOAT   SIZE 8 ; Différence entre total et imputé
TEMPORARY T_PAMMNTOLD FLOAT   SIZE 8 ; Montant du paiement avant modification


DEFINE D_REFNOM CHAR SIZE 15 = &
@IF FRANCAIS
 "Employé.......:" &
@ELSE
 "Employe.......:" &
@ENDIF
  IF REFCLETYP OF CPPAIEMENT = "E" ELSE &
@IF FRANCAIS
 "Fournisseur...:" &
@ELSE
 "Supplier......:" &
@ENDIF
  IF REFCLETYP OF CPPAIEMENT = "F"


DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE ustitstd NOLIST
DRAW 3,01 TO 13,80
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Saisie du pré-paiement " &
@ELSE
      "%%%isie du pré-paiement " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (3,3,19) (,49,65)

FIELD PAMCLE OF CPPAIEMENT &
LABEL "No paiement...:"&
      DISPLAY &
      PREDISPLAY

FIELD PAMMNT OF CPPAIEMENT &
LABEL "Mnt paiement..:"&
      DISPLAY &
      PREDISPLAY

SKIP

ALIGN (3,3,3) (,,19) (,,26)

FIELD D_REFNOM &
      DISPLAY &
      PREDISPLAY

FIELD REFCLENUM OF CPPAIEMENT &
      DISPLAY &
      PREDISPLAY

FIELD RADNOMABR OF MCREF_ADRESSE &
      DISPLAY &
      PREDISPLAY

SKIP

DRAW FROM ,1 TO ,80

SKIP 1

TITLE &
@IF FRANCAIS
      "       Numéro de       No          Montant     Escompte         Montant      " &
@ELSE
      "       %%%%%%%%%       %%%         %%%%%%%     %%%%%%%%         %%%%%%%      " &
@ENDIF
      AT ,2

SKIP

TITLE &
@IF FRANCAIS
      "       document      Cédule        à payer         pris            payé       " &
@ELSE
      "       %%%%%%%%      %%%%%%        %%%%%%%         %%%%            %%%%       " &
@ENDIF
      AT ,2

SKIP

ALIGN (2,3,8) (,,25) (,,31) (,,47) (,,60)

FIELD CAPCLE OF CPCAP_HISTO &
        HIDDEN ID 05 &
        LABEL " " &
        DISPLAY

FIELD CPCCLELIG OF CPCAP_HISTO &
        DISPLAY

FIELD CPHMNTCAP OF CPCAP_HISTO

FIELD CPHMNTESC OF CPCAP_HISTO &
        PICTURE "^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5 &
        DISPLAY

FIELD CPHMNTPYE OF CPCAP_HISTO &
        DISPLAY

;-------------------------------------------------------------------------------
; *** Sécurité d'accès ***
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

END

;-------------------------------------------------------------------------------
;
;
PROCEDURE EDIT CPHMNTCAP
BEGIN
  ;
  ; Validation du nombre de décimales.
  ;
  USE usvaldec NOLIST

  LET PAMCUMMNT OF CPPAIEMENT   = PAMCUMMNT OF CPPAIEMENT &
                                - CPHMNTPYE OF CPCAP_HISTO
  LET PAMMNT    OF CPPAIEMENT   = PAMMNT    OF CPPAIEMENT &
                                - CPHMNTPYE OF CPCAP_HISTO

  LET CPHMNTPYE OF CPCAP_HISTO  = CPHMNTCAP OF CPCAP_HISTO &
                                - CPHMNTESC OF CPCAP_HISTO

  LET PAMCUMMNT OF CPPAIEMENT   = PAMCUMMNT OF CPPAIEMENT &
                                + CPHMNTPYE OF CPCAP_HISTO
  LET PAMMNT    OF CPPAIEMENT   = PAMMNT    OF CPPAIEMENT &
                                + CPHMNTPYE OF CPCAP_HISTO
END

PROCEDURE PROCESS CPHMNTCAP
BEGIN
  DISPLAY PAMMNT    OF CPPAIEMENT
  DISPLAY CPHMNTPYE OF CPCAP_HISTO
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPCAP_HISTO &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CPCAP_HISTO &
     AND NOT NEWRECORD     OF CPCAP_HISTO &
     AND NOT DELETEDRECORD OF CPCAP_HISTO &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Mise à jour du timbre de modification si l'usager change l'imputation
  ;
  IF    NOT NEWRECORD OF CPPAIEMENT &
    AND ALTEREDRECORD OF CPCAP_HISTO
  THEN BEGIN
    LET PAMDATMOD OF CPPAIEMENT = SYSDATE
    LET PAMHREMOD OF CPPAIEMENT = SYSTIME / 10000
    LET PAMUSRMOD OF CPPAIEMENT = LOGONID
  END

  IF     PAMFLGIMP OF CPPAIEMENT = "1"  &
     AND PAMTYPPAM OF CPPAIEMENT <> "E" &
     AND PAMTYPPAM OF CPPAIEMENT <> "2"
  THEN ERROR 490 ; Vous ne pouvez plus modifier cette information, le chèque
                 ; est imprimé.

  IF PAMDATJOU OF CPPAIEMENT <> 0
  THEN ERROR 527 ; Impossible de modifier, ce paiement est journalisé.

  IF PAMCODANN OF CPPAIEMENT = "1"
  THEN ERROR 505 ; Ce paiement est annulé, vous ne pouvez plus le modifier.

  IF D_LCPATRJOU = "1"
  THEN ERROR 544 ; Ce lot est déjà autorisé.

  IF PAMTYPECR OF CPPAIEMENT = "CA"
  THEN ERROR 999 ; Impossible de modifier, ce paiement est une annulation.

  IF NEWRECORD OF CPCAP_HISTO
  THEN BEGIN
    GET A_CPH_EXISTS VIA   CPHCLE1, &
                           CPHCLE2, &
                           CPHCLE3  &
                     USING CIECLE    OF CPPAIEMENT, &
                           LBQCPTENC OF CPPAIEMENT, &
                           PAMCLE    OF CPPAIEMENT  OPTIONAL
    IF ACCESSOK
    THEN ERROR 584 ; Faire une recherche le montant existe déjà.

  END

END
;
; Mise à jour des données
;
PROCEDURE UPDATE
BEGIN
  ;
  ; On incrémente l'adresse pour les fournisseurs divers
  ;
  IF    REFCLETYP OF CPPAIEMENT = "F" &
    AND FOUTYP    OF VFOC_FOU   = "D" &
    AND PAMDEPDIR OF CPPAIEMENT = "1"
  THEN BEGIN
    START TRANSACTION GEN_RAD_SEQ
    GET MCRAD_SEQ VIA REFCIECLE, &
                      REFCLETYP, &
                      REFCLENUM  &
                  USING REFCIECLE OF CPPAIEMENT, &
                        REFCLETYP OF CPPAIEMENT, &
                        REFCLENUM OF CPPAIEMENT  &
                  OPTIONAL
    LET REFCIECLE OF MCRAD_SEQ = REFCIECLE OF CPPAIEMENT
    LET REFCLETYP OF MCRAD_SEQ = REFCLETYP OF CPPAIEMENT
    LET REFCLENUM OF MCRAD_SEQ = REFCLENUM OF CPPAIEMENT
    LET RASNUMSEQ OF MCRAD_SEQ = RASNUMSEQ OF MCRAD_SEQ + 1
    LET RADCLE    OF CPPAIEMENT = RASNUMSEQ OF MCRAD_SEQ
    PUT MCRAD_SEQ
    COMMIT GEN_RAD_SEQ
  END

  ;
  ; On obtient un numéro de paiement pour le nouveau paiement
  ;
  IF NEWRECORD OF CPPAIEMENT
  THEN BEGIN
    START TRANSACTION GEN_NUM_SEQ2
    GET CPPAM_SEQ OPTIONAL
    LET PSENUMSEQ OF CPPAM_SEQ = PSENUMSEQ OF CPPAM_SEQ + 1
    LET PAMCLE OF CPPAIEMENT &
                = ASCII(PSENUMSEQ OF CPPAM_SEQ,PSELONNUM OF CPPAM_SEQ)
    PUT CPPAM_SEQ
    COMMIT TRANSACTION GEN_NUM_SEQ2
  END
  ;
  ; Génération du NA.
  ;
  IF             NEWRECORD OF CPCAP_HISTO &
     AND NOT DELETEDRECORD OF CPCAP_HISTO
  THEN BEGIN
    ;
    ; Attribution du numéro du P.N.A.
    ;
    START TRANSACTION GEN_NUM_SEQ
    GET CPPAIE_NA_SEQ &
                VIA     CIECLE, &
                        PNSANNCLE &
                USING   CIECLE OF CPPAIEMENT, &
                        PECCLE OF CPPAIEMENT[1:2] OPTIONAL
    LET CIECLE    OF CPPAIE_NA_SEQ = CIECLE    OF CPPAIEMENT
    LET PNSANNCLE OF CPPAIE_NA_SEQ = PECCLE    OF CPPAIEMENT[1:2]
    ;LET PNSNUMSEQ OF CPPAIE_NA_SEQ = PNSNUMSEQ OF CPPAIE_NA_SEQ + 1
    ;an 2000
    if newrecord of cppaie_na_seq
    then LET PNSNUMSEQ OF CPPAIE_NA_SEQ = 0
    LET PNSNUMSEQ OF CPPAIE_NA_SEQ = PNSNUMSEQ OF CPPAIE_NA_SEQ + 1
    LET CAPCLE    OF CPCAP_HISTO   = TRUNCATE(CPPCODPNA OF CPPARAMETRE) + &
                                     PNSANNCLE OF CPPAIE_NA_SEQ         + &
                                     ASCII(PNSNUMSEQ OF CPPAIE_NA_SEQ, &
                                           PNSLONNUM OF CPPAIE_NA_SEQ - 2)
    PUT CPPAIE_NA_SEQ
    COMMIT TRANSACTION GEN_NUM_SEQ
    ;
    ; Création du paiement non-appliqué.
    ;
    LET FOUCIECLE OF CPCPT_A_PAYER = D_CIECLE
    LET FOUCLE    OF CPCPT_A_PAYER = D_FOUCLE
    LET CAPCLE    OF CPCPT_A_PAYER = CAPCLE OF CPCAP_HISTO
    LET CAPCIECLE OF CPCPT_A_PAYER = CIECLE OF CPPAIEMENT
    LET REFCIECLE OF CPCPT_A_PAYER = D_FOUEMPCIECLE
    LET REFCLETYP OF CPCPT_A_PAYER = REFCLETYP OF CPPAIEMENT
    LET REFCLENUM OF CPCPT_A_PAYER = REFCLENUM OF CPPAIEMENT
    LET RADCLE    OF CPCPT_A_PAYER = FOCADRPAM OF VFOC_FOU
    LET PECCLE    OF CPCPT_A_PAYER = PECCLE    OF CPPAIEMENT
    LET CAPTYPECR OF CPCPT_A_PAYER = "NA"
    LET CAPDAT    OF CPCPT_A_PAYER = PAMDAT OF CPPAIEMENT
    LET CAPDATNET OF CPCPT_A_PAYER = PAMDAT OF CPPAIEMENT
    LET CAPCPTCAP OF CPCPT_A_PAYER = FOCCPTCAP OF VFOC_FOU
    LET CAPDATCRE OF CPCPT_A_PAYER = SYSDATE
    LET CAPUSRCRE OF CPCPT_A_PAYER = LOGONID
    LET CAPHRECRE OF CPCPT_A_PAYER = SYSTIME / 10000
    LET CAPDSC1   OF CPCPT_A_PAYER = PAMDSC1 OF CPPAIEMENT
    LET CAPDSC2   OF CPCPT_A_PAYER = PAMDSC2 OF CPPAIEMENT
    ;
    PUT CPCPT_A_PAYER RESET
    ;
    ; Création de la cédule
    ;
    LET FOUCIECLE OF CPCAP_CEDULE = D_CIECLE
    LET FOUCLE    OF CPCAP_CEDULE = D_FOUCLE
    LET CAPCLE    OF CPCAP_CEDULE = CAPCLE OF CPCAP_HISTO
    LET CPCCLELIG OF CPCAP_CEDULE = 1
    LET CPCDATDUE OF CPCAP_CEDULE = PAMDAT OF CPPAIEMENT
    LET CPCFLGSEL OF CPCAP_CEDULE = "0"
    ;
    PUT CPCAP_CEDULE RESET
  END
  ;
  ; Si on détruit la ligne de ventilation qui réfère un paiement
  ; non-appliqué, et que c'est cette même ligne qui a créé le paiement
  ; non-appliqué, il faut détruire le paiement non-appliqué. Si la date
  ; de journalisation du paiement non-appliqué est à zéro, cela indique
  ; que c'est un nouveau paiement non-appliqué.
  ;
  IF     DELETEDRECORD OF CPCAP_HISTO &
     AND ALTEREDRECORD OF CPCAP_HISTO &
     AND NOT NEWRECORD OF CPCAP_HISTO &
     AND CAPTYPECR     OF A_CAP_REFER = "NA" &
     AND CAPDATJOU     OF A_CAP_REFER = 0
  THEN BEGIN
    GET CPCPT_A_PAYER VIA     FOUCIECLE, &
                              FOUCLE   , &
                              CAPCLE     &
                      USING   D_CIECLE, &
                              D_FOUCLE, &
                              CAPCLE OF CPCAP_HISTO
    DELETE CPCPT_A_PAYER
    PUT CPCPT_A_PAYER

    GET CPCAP_CEDULE  VIA     FOUCIECLE, &
                              FOUCLE   , &
                              CAPCLE   , &
                              CPCCLELIG  &
                      USING   D_CIECLE, &
                              D_FOUCLE, &
                              CAPCLE OF CPCAP_HISTO, &
                              1
    DELETE CPCAP_CEDULE
    PUT CPCAP_CEDULE

  END

  PUT CPPAIEMENT
  PUT MCREF_ADRESSE
  PUT MCREFERENCE
  PUT CPCAP_HISTO
END

;-------------------------------------------------------------------------------
;
; Suppression d'une ligne de ventilation du paiement
;
PROCEDURE DELETE
BEGIN
  IF NOT DELETEDRECORD OF CPCAP_HISTO
  THEN BEGIN
    LET PAMCUMMNT OF CPPAIEMENT = PAMCUMMNT OF CPPAIEMENT &
                                - CPHMNTPYE OF CPCAP_HISTO
    LET PAMMNT    OF CPPAIEMENT = PAMMNT    OF CPPAIEMENT &
                                - CPHMNTPYE OF CPCAP_HISTO
    DELETE CPCAP_HISTO
    DISPLAY PAMMNT OF CPPAIEMENT
  END
END


PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY CAPCLE OF CPCAP_HISTO
    WARNING = " "
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************** Ventilation des documents ***************************
* No paiement...: xxxxxxx                       Mnt paiement..: xxxxxxxxxxxxxx *
* xxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx                                  *
********************************************************************************
*       Numéro de       No          Montant     Escompte         Montant       *
*       document      Cédule        à payer         pris            payé       *
*      xxxxxxxxxxxxxxx  xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
********************************************************************************
@ENDIF
