;/T/ Gestion des listes de prix particulière.
;
;/P/ Programmeur..: René Bonneau
;    Date création: 1994-11-08
;
;    Description..: Ce panorama permet de gérer les listes de prix particulière
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd206 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_LISTE_PRIX IN DICT


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD206"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd206.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Vérification                 " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Vérification              " ACTION DESIGNER D001
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Classe de granit
;

FILE PDLISTE_PRIX     PRIMARY NOITEM

  ACCESS VIA     CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE,                               &
                 CLGCODCLA                             &
         USING   T_CIECLEMNU,                          &
                 LPRNUMSEQ        OF PDLISTE_PRIX,     &
                 CLICLE           OF PDLISTE_PRIX,     &
                 CLGCODCLA        OF PDLISTE_PRIX      &
         REQUEST LPRNUMSEQ        OF PDLISTE_PRIX,     &
                 CLICLE           OF PDLISTE_PRIX,     &
                 CLGCODCLA        OF PDLISTE_PRIX      &
         ORDERBY CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE,                               &
                 CLGCODCLA

  ACCESS VIA     CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE                                &
         USING   T_CIECLEMNU,                          &
                 LPRNUMSEQ        OF PDLISTE_PRIX,     &
                 CLICLE           OF PDLISTE_PRIX      &
         REQUEST LPRNUMSEQ        OF PDLISTE_PRIX,     &
                 CLICLE           OF PDLISTE_PRIX      &
         ORDERBY CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE,                               &
                 CLGCODCLA

  ACCESS VIA     CIECLE,                               &
                 LPRNUMSEQ                             &
         USING   T_CIECLEMNU,                          &
                 LPRNUMSEQ        OF PDLISTE_PRIX      &
         REQUEST LPRNUMSEQ        OF PDLISTE_PRIX      &
         ORDERBY CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE,                               &
                 CLGCODCLA

  ACCESS VIA     CIECLE                                &
         USING   T_CIECLEMNU                           &
         ORDERBY CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE,                               &
                 CLGCODCLA


  ITEM   CIECLE       OF PDLISTE_PRIX INITIAL T_CIECLEMNU
  ITEM   LPRDATCRE    OF PDLISTE_PRIX INITIAL SYSDATE
  ITEM   LPRHRECRE    OF PDLISTE_PRIX INITIAL SYSTIME / 10000
  ITEM   LPRUSRCRE    OF PDLISTE_PRIX INITIAL LOGONID

  ITEM   LPRDATMOD    OF PDLISTE_PRIX FINAL SYSDATE &
                                        IF NOT NEWRECORD OF PDLISTE_PRIX AND &
                                           ALTEREDRECORD     OF PDLISTE_PRIX
  ITEM   LPRHREMOD    OF PDLISTE_PRIX FINAL SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF PDLISTE_PRIX AND &
                                           ALTEREDRECORD     OF PDLISTE_PRIX
  ITEM   LPRUSRMOD    OF PDLISTE_PRIX FINAL LOGONID &
                                        IF NOT NEWRECORD OF PDLISTE_PRIX AND &
                                           ALTEREDRECORD     OF PDLISTE_PRIX


;-------------------------------------------------------------------------------
;
; Client
;

FILE CRCLIENT         REFERENCE

  ACCESS VIA     CLICIECLE,                            &
                 CLICLE                                &
         USING   T_CIECLEMNU,                          &
                 CLICLE           OF PDLISTE_PRIX


;-------------------------------------------------------------------------------
;
; Unuté de mesure
;

FILE PDUNITE_MESURE   REFERENCE

  ACCESS VIA     CIECLE,                               &
                 UNMCODUNM                             &
         USING   T_CIECLEMNU,                          &
                 UNMCODUNM        OF PDLISTE_PRIX


;-------------------------------------------------------------------------------
;
; Classe
;

FILE PDCLASSE         REFERENCE

  ACCESS VIA     CIECLE,                               &
                 CLGCODCLA                             &
         USING   T_CIECLEMNU,                          &
                 CLGCODCLA        OF PDLISTE_PRIX


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les listes de prix
;

FILE PDSEQ_LISTE_PRIX DESIGNER &
                      TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP

TITLE &
@IF FRANCAIS
      " Classe de granit " &
@ELSE
      " %%%Classe de granit " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19)


FIELD LPRNUMSEQ        OF PDLISTE_PRIX                 &
label "No liste prix.:"&
        HIDDEN ID 02                                   &
        DISPLAY


SKIP 1


ALIGN (2,3,19) (26,,26)


FIELD CLICLE           OF PDLISTE_PRIX                 &
label "Client........:"&
        HIDDEN ID 05                                   &
        REQUIRED                                       &
        LOOKUP ON CRCLIENT                             &
          MESSAGE 2968 & ; Ce client n'existe pas
        ,NOTON PDLISTE_PRIX                            &
         VIA     CIECLE,                               &
                 LPRNUMSEQ,                            &
                 CLICLE                                &
         USING   T_CIECLEMNU,                          &
                 LPRNUMSEQ        OF PDLISTE_PRIX,     &
                 CLICLE           OF PDLISTE_PRIX      &
          MESSAGE 3022 ; Cette classe de granit existe déjà


FIELD CLINOM           OF CRCLIENT                    &
        HIDDEN ID 10                                  &
        DISPLAY


FIELD CLGCODCLA        OF PDLISTE_PRIX                &
label "Code classe...:"&
        HIDDEN ID 15                                  &
        LOOKUP ON PDCLASSE                            &
          MESSAGE 3025 ; Cette classe n'existe pas


FIELD CLGDSCFRA        OF PDCLASSE                    &
        HIDDEN ID 20                                  &
        DISPLAY


SKIP 1


ALIGN (26,,26)


FIELD CLGDSCANG        OF PDCLASSE                    &
        HIDDEN ID 25                                  &
        DISPLAY


ALIGN (2,3,19)


FIELD UNMCODUNM        OF PDLISTE_PRIX                &
label "Code unité....:"&
        HIDDEN ID 30                                  &
        REQUIRED                                      &
        LOOKUP ON PDUNITE_MESURE                      &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas


SKIP 1


ALIGN (2,3,19) (31,,32)

FIELD LPRDATIMP        OF PDLISTE_PRIX      FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        HIDDEN ID 90                       &
        DISPLAY                            &
        LABEL "Impression 1..:"


FIELD LPRHREIMP        OF PDLISTE_PRIX     &
        ID SAME                            &
        DISPLAY                            &
        NOLABEL


FIELD LPRDATDERIMP     OF PDLISTE_PRIX      FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        HIDDEN ID 92                       &
        DISPLAY                            &
        LABEL "Dernière imp..:"


FIELD LPRHREDERIMP     OF PDLISTE_PRIX     &
        ID SAME                            &
        DISPLAY                            &
        NOLABEL


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;


PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

END


;-------------------------------------------------------------------------------
;
; Vérification liste de prix
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd206a &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        PDLISTE_PRIX    &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDLISTE_PRIX &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDLISTE_PRIX &
     AND NOT NEWRECORD     OF PDLISTE_PRIX &
     AND NOT DELETEDRECORD OF PDLISTE_PRIX &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;-----------------------------------------------------------------------------
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = LPRNUMSEQ OF PDLISTE_PRIX
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_LISTE_PRIX OPTIONAL SEQUENTIAL
    LET SEQNUMSEQ OF PDSEQ_LISTE_PRIX = SEQNUMSEQ OF PDSEQ_LISTE_PRIX + 1
    LET LPRNUMSEQ OF PDLISTE_PRIX     = SEQNUMSEQ OF PDSEQ_LISTE_PRIX
    PUT PDSEQ_LISTE_PRIX
    DISPLAY LPRNUMSEQ OF PDLISTE_PRIX
  END

END


BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Classe de granit *******************************
*                                                                              *
* No liste prix.: xxxxxxxxxxx                                                  *
*                                                                              *
* Client........: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
*                                                                              *
* Code classe...: xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
*                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
*                                                                              *
* Code unité....: xx  ( Pi2 / M2 )                                             *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
* Impression 1..: xxxxxxxx xxxxx    Dernière imp..: xxxxxxxx xxxxx             *
*                                                                              *
********************************************************************************
@ENDIF

