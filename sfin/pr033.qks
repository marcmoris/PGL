;/T/ Gestion des formats d'impression
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 15 Oct 1993
;
;    Description..: Gestion des formats d'impression
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr033 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "PR033"             ; Nom de l'écran

USE pr033.hlp Nolist ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

FILE GPFORMAT         PRIMARY
  ACCESS VIA FORCLE USING FORCLE REQUEST FORCLE &
    ORDERBY FORCLE
 ACCESS SEQUENTIAL &
    ORDERBY FORCLE

FILE GPFOR_DETAIL  DETAIL OCCURS 10 TIMES NOITEM
  ACCESS VIA FORCLE USING FORCLE OF GPFORMAT ORDERBY FORCLE, FODLIG

  ITEM FORCLE OF GPFOR_DETAIL INITIAL FORCLE OF GPFORMAT

FILE GPFOR_DETAIL  DELETE ALIAS A_GPFOR_DETAIL_DEL
  ACCESS VIA FORCLE USING FORCLE OF GPFORMAT

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des formats d'impressions " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP
ALIGN (2,3,11)(,25,39)
FIELD FORCLE OF GPFORMAT LABEL "Format :" REQUIRED NOCHANGE HIDDEN &
  LOOKUP NOTON GPFORMAT VIA FORCLE USING FORCLE OF GPFORMAT &
  MESSAGE 2514   ;"*EE* Ce format d'impression existe deja"
FIELD FORDSC OF GPFORMAT LABEL "Description :"
SKIP 1
TITLE "Titre du format" AT ,33
ALIGN (2,3,3)
FIELD FORTIT1 OF GPFORMAT REQUIRED LABEL " " HIDDEN
FIELD FORTIT2 OF GPFORMAT ID SAME  LABEL " " HIDDEN
FIELD FORTIT3 OF GPFORMAT ID SAME  LABEL " " HIDDEN
SKIP 1
TITLE "No. Type Description                                        Compte Compte D/C" AT ,3

SKIP 1
ALIGN (2,3,3)(,,8)(,,12)(,,62)(,,70)(,,78)
CLUSTER OCCURS WITH GPFOR_DETAIL

FIELD FODLIG OF GPFOR_DETAIL REQUIRED NOCHANGE NUM SIGNIF 3 LABEL " " HIDDEN &
  LOOKUP NOTON GPFOR_DETAIL VIA FORCLE, FODLIG &
    USING FORCLE OF GPFORMAT, FODLIG OF GPFOR_DETAIL OPT &
  MESSAGE 2515 ; Ce numéro de ligne existe déjà
FIELD FODTYP    OF GPFOR_DETAIL DEFAULT "C" UPSHIFT
FIELD FODDSC    OF GPFOR_DETAIL REQUIRED
FIELD FODCPTDUE OF GPFOR_DETAIL REQUIRED &
  IF FODTYP OF GPFOR_DETAIL = "C"
FIELD FODCPTAVA  OF GPFOR_DETAIL REQUIRED &
  IF FODTYP OF GPFOR_DETAIL = "C"

FIELD FODCODDC OF GPFOR_DETAIL REQUIRED VALUE "C", "D" &
  IF FODTYP OF GPFOR_DETAIL = "C"

CLUSTER


PROCEDURE INPUT FORTIT1
BEGIN
  LET FIELDTEXT = CENTER(FIELDTEXT[1:70])
END

PROCEDURE INPUT FORTIT2
BEGIN
  LET FIELDTEXT = CENTER(FIELDTEXT[1:70])
END

PROCEDURE INPUT FORTIT3
BEGIN
  LET FIELDTEXT = CENTER(FIELDTEXT[1:70])
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


PROCEDURE DESIGNER 3
BEGIN
  ACCEPT FODTYP OF GPFOR_DETAIL
  ACCEPT FODDSC
  IF FODTYP OF GPFOR_DETAIL = "C"
  THEN BEGIN
    ACCEPT FODCPTDUE OF GPFOR_DETAIL
    ACCEPT FODCPTAVA OF GPFOR_DETAIL
    ACCEPT FODCODDC  OF GPFOR_DETAIL
  END
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPFORMAT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPFORMAT &
     AND NOT NEWRECORD     OF GPFORMAT &
     AND NOT DELETEDRECORD OF GPFORMAT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END
BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************** Gestion des formats d'impressions ***********************
*   Format : xxxxx      Description : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
*                                                                              *
*                               Titre du format                                *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     *
*   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     *
*                                                                              *
*   No. Type  Description                                        Compte Compte *
*                                                                              *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*   xxx  xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
