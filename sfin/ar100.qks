;/T/ Consultation des entrepôts      CIECLE, ENTCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 3 mars 1994
;
;    Description..: Permet de visionner et de choisir un entrepôt.
;
;    Paramètre....: CIECLE ==> - compagnie de l'entrepôt
;                              - segment de la clé de recherche.
;
;                   ENTCLE ==> - segment de la clé de recherche.
;                              - peut contenir le caractère '@'.
;                              - contient la valeur de retour.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar100 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_ENTCLE 
                     

USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR100"

USE ar100.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4 ; Compagnie de l'entrepôt
TEMPORARY T_ENTCLE CHARACTER SIZE 4 ; Code de l'entrepôt


;-------------------------------------------------------------------------------
;
FILE ARENTREPOT        PRIMARY &
                      OCCURS 11

 ACCESS   VIA   CIECLE, &
                ENTCLE  &
        USING   T_CIECLE, &
                T_ENTCLE  &
        ORDERBY CIECLE , &
                ENTCLE   
           

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST     
USE usdrwfnd NOLIST     

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH ARENTREPOT

FIELD ENTCLE    OF ARENTREPOT HIDDEN LABEL ">>"
FIELD ENTNOMABR OF ARENTREPOT

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_ENTCLE, "@")
  THEN LET T_ENTCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_ENTCLE = ENTCLE OF ARENTREPOT
  RETURN
END

BUILD
@IF FORMAT
                                       *****************************************
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       * >> xxxx   xxxxxxxxxxxxxxxxxxxx        *
                                       *****************************************
@ENDIF
