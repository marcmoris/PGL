;/T/ Devises - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 10 août 1993
;
;    Description..: Cet écran vérifie si la devise reçu en paramètre est
;                   référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc003x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCDEVISE , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Devise.
;
FILE MCDEVISE         MASTER

;
; Holding.
;
FILE MCHOLDING        REFERENCE

;
; Fichier des référence.
;
FILE MCREFERENCE      REFERENCE

;
; Plan comptable.
;
FILE MCCOMPTE         REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans le holding.
  ;
  GET MCHOLDING VIA DEVCLE &
                USING DEVCLE OF MCDEVISE &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 156 ; Destruction annulée, cette information est référée par le
                 ; holding.

  ;
  ; On vérifie dans le référence.
  ;
  GET MCREFERENCE VIA DEVCLE &
                  USING DEVCLE OF MCDEVISE &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 157 ; Destruction annulée, cette information est référée par une
                 ; référence.

  ;
  ; On vérifie dans le plan comptable.
  ;
  GET MCCOMPTE VIA DEVCLE &
               USING DEVCLE OF MCDEVISE &
               OPTIONAL
  IF ACCESSOK
  THEN ERROR 158 ; Destruction annulée, cette information est référée par un
                 ; compte ctb.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
