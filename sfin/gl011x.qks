;/T/ Etat financier - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 10 août 1993
;
;    Description..: Cet écran vérifie si l'état financier reçu en
;                   paramètre est référée dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gl011x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING GLETAT_FINANCIER , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Etat financier.
;
FILE GLETAT_FINANCIER MASTER

;
; Détail des cédules d'impression.
;
FILE GLECE_DETAIL     REFERENCE

;
; Détail des états financiers.
;
FILE GLEFI_DETAIL     REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans le détail des cédules d'impression.
  ;
  GET GLECE_DETAIL VIA EFICIECLE , &
                       EFICLE      &
                   USING EFICIECLE OF GLETAT_FINANCIER , &
                         EFICLE    OF GLETAT_FINANCIER   &
                   OPTIONAL
  IF ACCESSOK
  THEN ERROR 341 ; Destruction annulée, cette information est référée par une
                 ; cédule d'impression.

  ;
  ; On vérifie dans le détail des états financiers si l'état financier
  ; à détruire est appelé comme note.
  ;
  GET GLEFI_DETAIL VIA EFICIECLE , &
                       CPTCLE    , &
                       EFDCOD      &
                   USING EFICIECLE OF GLETAT_FINANCIER , &
                         EFICLE    OF GLETAT_FINANCIER , &
                         "NOTE"                          &
                   OPTIONAL
  IF ACCESSOK
  THEN ERROR 342 ; Destruction annulée, cette information est référée par un
                 ; état financier.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
@IF FORMAT

@ENDIF
