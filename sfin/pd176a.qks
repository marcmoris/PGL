;/T/ 2.2.2 Détail réception (Bloc)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-08-22
;
;    Description..: Cette écran permet d'enregistrer la réception des bons
;                   de commandes pour les blocs.
;
;/M/ Programmeur : Francois Richard
;    Date        : 1999-05-10
;    Description : En mode entry amener automatiquement le sous-écran
;                  PD176E
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 17 avril 2000.
;    Description.......: Génération de la mise à jour automatique des prix au mètre cube au niveau
;                        du ou des blocs redimensionnés associés.
;
;    Référence.........: Demande de changement 000038.
;    
;    Signet............: MP-00-04-17_D38.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 30 mai 2000.
;    Description.......: ajout d'un nouvel élément et déplace de l'élément "Complet".
;
;    Référence.........: Demande de changement 000061.
;    
;    Signet............: MP-00-05-30_D61.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 17 janvier 2002.
;    Description.......: Ajuster la condition de génération automatique des assignation afin de tester
;                        également le code de projet (PJTABR).
;
;    Référence.........: Demande de changement 200208.
;    
;    Signet............: MP-02-01-17_D200208.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN pd176a ACTIONBAR                   &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM,          &
                       T_FOUNOM,          &
                       PDRECEPTION,       &
                       PDBON_COMMANDE

;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD176A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd176a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Défauts de bloc                 " ACTION DESIGNER D001
  MENUITEM LABEL "Vérification bloc               " ACTION DESIGNER D002
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Défauts de bloc              " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Vérification bloc            " ACTION DESIGNER D002
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom compagnie
TEMPORARY T_FOUNOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom du fournisseur
TEMPORARY T_FLG CHAR SIZE 1
TEMPORARY T_PODNUMLIG CHARACTER SIZE 3
TEMPORARY T_BCMNUMBCM INTEGER SIZE 4
TEMPORARY T_TGRCODGRA        CHAR SIZE 3
TEMPORARY T_TYFCODFIN        CHAR SIZE 2
TEMPORARY T_PODLON           INTE SIZE 4
TEMPORARY T_PODHAU           INTE SIZE 4
TEMPORARY T_PODEPA           INTE SIZE 4


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_BLOSTU          CHARACTER SIZE 16 = "BLOSTU"
TEMPORARY T_BLOSTU          CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;

USE usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimun
;
USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Réception
;

FILE PDRECEPTION    MASTER

;-------------------------------------------------------------------------------
;
; Bon de commande
;

FILE PDBON_COMMANDE MASTER


;-------------------------------------------------------------------------------
;
; Détail réception bloc
;

FILE PDRECEPTION_BLOC PRIMARY &
                      NOITEM

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION,       &
                 BLONUMGRA001APR OF PDRECEPTION_BLOC,  &
                 BLONUMGRA002APR OF PDRECEPTION_BLOC,  &
                 BLORECAPR       OF PDRECEPTION_BLOC   &
         REQUEST BLONUMGRA001APR OF PDRECEPTION_BLOC,  &
                 BLONUMGRA002APR OF PDRECEPTION_BLOC,  &
                 BLORECAPR       OF PDRECEPTION_BLOC   &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR                       &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION,       &
                 BLONUMGRA001APR OF PDRECEPTION_BLOC,  &
                 BLONUMGRA002APR OF PDRECEPTION_BLOC   &
         REQUEST BLONUMGRA001APR OF PDRECEPTION_BLOC,  &
                 BLONUMGRA002APR OF PDRECEPTION_BLOC   &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR                       &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION,       &
                 BLONUMGRA001APR OF PDRECEPTION_BLOC   &
         REQUEST BLONUMGRA001APR OF PDRECEPTION_BLOC   &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC       OF PDRECEPTION        &
         ORDERBY CIECLE,                               &
                 RCTNUMREC


  ITEM CIECLE       OF PDRECEPTION_BLOC INITIAL T_CIECLEMNU
  ITEM RCTNUMREC    OF PDRECEPTION_BLOC INITIAL RCTNUMREC OF PDRECEPTION
  ITEM REBQTE       OF PDRECEPTION_BLOC INITIAL 1
  ITEM BLONUMBLOAPR OF PDRECEPTION_BLOC FINAL BLONUMGRA001APR OF PDRECEPTION_BLOC + &
                                              BLONUMGRA002APR OF PDRECEPTION_BLOC + &
                                              BLORECAPR       OF PDRECEPTION_BLOC

;-------------------------------------------------------------------------------
;
; Inscription du bloc dans la base de donnée
;

FILE PDBLOC SECONDARY &
            NEED 1    &
            NOITEM

  ACCESS VIA     CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR                             &
         USING   T_CIECLEMNU,                          &
                 BLONUMGRA001APR OF PDRECEPTION_BLOC,  &
                 BLONUMGRA002APR OF PDRECEPTION_BLOC,  &
                 BLORECAPR       OF PDRECEPTION_BLOC

  ITEM BLONUMGRA001APR  OF PDBLOC INITIAL BLONUMGRA001APR OF PDRECEPTION_BLOC FIXED
  ITEM BLONUMGRA002APR  OF PDBLOC INITIAL BLONUMGRA002APR OF PDRECEPTION_BLOC FIXED
  ITEM BLORECAPR        OF PDBLOC INITIAL BLORECAPR       OF PDRECEPTION_BLOC FIXED
  ITEM BLOSTU           OF PDBLOC INITIAL "I"
  ITEM BLODATCRE        OF PDBLOC INITIAL SYSDATE
  ITEM BLOHRECRE        OF PDBLOC INITIAL SYSTIME / 10000
  ITEM BLOUSRCRE        OF PDBLOC INITIAL LOGONID
  ITEM CIECLE           OF PDBLOC INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Code bilingue (Statut du bloc)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_BLOSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_BLOSTU,                             &
               BLOSTU           OF PDBLOC


;-------------------------------------------------------------------------------
;
; Défauts de bloc
;

FILE PDBLOC_DEFAUTS DESIGNER


;-------------------------------------------------------------------------------
;
; Commande - assignation
;

FILE PDCOMMAN_ASSIGN DESIGNER


;-------------------------------------------------------------------------------
;
; Redimention de bloc
;

FILE PDREDIM_BLOC    DESIGNER

FILE PDREDIM_BLOC  ALIAS A_PDREDIM_BLOC  DESIGNER


;-------------------------------------------------------------------------------
;
; Bloc soumission
;

FILE PDBLOCS_SOUMI   DESIGNER


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDDET_RAP_MONT  DESIGNER


;-------------------------------------------------------------------------------
;
; Sortie de bloc
;

FILE PDSORTIE_BLOCS  DESIGNER


;-------------------------------------------------------------------------------
;
; Réservation de bloc
;

FILE PDRESER_BLOCS   DESIGNER


;-------------------------------------------------------------------------------
;
; Lien bloc - facture
;

FILE PDBLOC_FACTURE  DESIGNER


;-------------------------------------------------------------------------------
;
; Définition de variable temporaire pour le traitement
;

TEMPORARY T_TYPE_PRODUIT CHARACTER SIZE 2 INITIAL "BL" RESET AT STARTUP

TEMPORARY T_OLD_VOL      FLOAT     SIZE 8

TEMPORARY T_SILENT       CHARACTER SIZE 1

TEMPORARY T_SILENT_2     CHARACTER SIZE 1 ; appel du sous écran
                                          ; pd176e directement en saisie

;-------------------------------------------------------------------------------
;
; Détail du bon de commande
;

FILE PDDET_BON_COMMAN SECONDARY &
                      NOITEM
  ACCESS VIA     CIECLE,                         &
                 BCMNUMBCM,                      &
                 PODNUMLIG                       &
         USING   T_CIECLEMNU,                    &
                 BCMNUMBCM OF PDBON_COMMANDE,    &
                 PODNUMLIG OF PDRECEPTION_BLOC

  ITEM REBPRIBCMMC3 OF PDRECEPTION_BLOC FINAL   PODPRI       OF PDDET_BON_COMMAN
  ITEM TGRCODGRA    OF PDBLOC           INITIAL TGRCODGRA    OF PDDET_BON_COMMAN

  ITEM CIECLE       OF PDDET_BON_COMMAN INITIAL T_CIECLEMNU
  ITEM PODRCU       OF PDDET_BON_COMMAN FINAL "0" IF PODQTERCU OF PDDET_BON_COMMAN = 0 &
                                                  ELSE "1"


;-------------------------------------------------------------------------------
;
; Type de produit
;

FILE PDTYPE_PRODUIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TPDTYPPRD                       &
         USING   T_CIECLEMNU,                    &
                 TPDTYPPRD OF PDDET_BON_COMMAN


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TGRCODGRA                       &
         USING   T_CIECLEMNU,                    &
                 TGRCODGRA OF PDDET_BON_COMMAN



  ITEM BLOCODCOU       OF PDBLOC INITIAL TGRCODCOUGRA OF PDTYPE_GRANIT


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TYFCODFIN                       &
         USING   T_CIECLEMNU,                    &
                 TYFCODFIN OF PDDET_BON_COMMAN

; ----------------------------------------------------------------------------------------
;
; MP-01-10-26_DD.
;
; Lignes de détails d'une commande interne.
;
; Validation du type de granit.

FILE PDDETAIL_C_I DESIGNER


; ----------------------------------------------------------------------------------------
; MP-00-04-17_D38.
;
; Tables associées à la mise à jour des prix au mètre cube pour les blocs redimensionnés
;
FILE PDBLOC           ALIAS A1_PDBLOC          DESIGNER OPEN 2
FILE PDBLOC           ALIAS A2_PDBLOC          DESIGNER OPEN 3

; ----------------------------------------------------------------------------------------
; MP-00-04-17_D38.
;
; Variables de travail associées à la mise à jour des prix au mètre cube pour 
; les blocs redimensionnés
;
TEMPORARY T_BLONUMBLO      CHARACTER SIZE 10 RESET AT STARTUP
TEMPORARY T_BLONUMBLO_OCC  CHARACTER SIZE 10 OCCURS 100 TIMES
TEMPORARY T_OCC_NB         INTEGER   SIZE 04 RESET AT STARTUP

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Réception de bloc " &
@ELSE
      " %%%Réception de bloc " &
@ENDIF
      CENTERED AT ,1



SKIP TO LINE 4


ALIGN (,3,19) (,43,59)


FIELD RCTNUMREC OF PDRECEPTION                &
label "No. réception.:"&
        DISPLAY                               &
        PREDISPLAY REFRESH


FIELD RCTDATREC OF PDRECEPTION           FORMAT YYYYMMDD     &
PATTERN "####/##/##"&
label "Date r.ception:"&
        DISPLAY PREDISPLAY REFRESH


ALIGN (,3,19) (,,27)


FIELD FOUCLE       OF PDBON_COMMANDE          &
label "Fournisseur...:"&
        DISPLAY PREDISPLAY REFRESH


FIELD T_FOUNOM                                &
        DISPLAY PREDISPLAY REFRESH


DRAW 7,1 TO 7,80


SKIP TO LINE 7


TITLE &
@IF FRANCAIS
      " Blocs " &
@ELSE
      " %%%Blocs " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 9


ALIGN (2,3,19) (41,43,59)


FIELD PODNUMLIG    OF PDRECEPTION_BLOC            &
label "Numéro ligne..:"&
        HIDDEN ID 25 size 3                             &
        BWZ                                       &
        DUPLICATE                                 &
        LOOKUP ON FILE PDDET_BON_COMMAN           &
          VIA     CIECLE,                         &
                  BCMNUMBCM,                      &
                  PODNUMLIG,                      &
                  TPDTYPPRD                       &
          USING   T_CIECLEMNU,                    &
                  BCMNUMBCM OF PDBON_COMMANDE,    &
                  PODNUMLIG OF PDRECEPTION_BLOC,  &
                  T_TYPE_PRODUIT                  &
          MESSAGE 2931 ;


FIELD PODPRI       OF PDDET_BON_COMMAN            &
        HIDDEN ID 30                              &
        BWZ                                       &
        LABEL "Prix M3 cmd...:"                   &
        DISPLAY REFRESH


TITLE "-" AT 10,23

TITLE "-" AT 10,29


SKIP TO LINE 10


ALIGN (2,3,19) (,,24) (,,30) (41,43,59) (,,61)


FIELD BLONUMGRA001APR OF PDRECEPTION_BLOC     &
        HIDDEN ID 35                          &
        BWZ                                   &
        NUMERIC                               &
        LABEL "No bloc.......:"               &
        AUTONEXT                              &
        REQUIRED NOCHANGE


FIELD BLONUMGRA002APR OF PDRECEPTION_BLOC     &
        ID SAME                               &
        BWZ                                   &
        NUMERIC                               &
        AUTONEXT                              &
        REQUIRED NOCHANGE


FIELD BLORECAPR       OF PDRECEPTION_BLOC     &
        ID SAME


FIELD T_SILENT                                &
        SILENT                                &
        LOOKUP NOTON PDRECEPTION_BLOC         &
          VIA     CIECLE,                     &
                  BLONUMGRA001APR,            &
                  BLONUMGRA002APR,            &
                  BLORECAPR                   &
          USING   T_CIECLEMNU,                &
                  BLONUMGRA001APR OF PDRECEPTION_BLOC , &
                  BLONUMGRA002APR OF PDRECEPTION_BLOC , &
                  BLORECAPR       OF PDRECEPTION_BLOC   &
          MESSAGE 3001 ; Ce numéro de bloc existe déjà


FIELD BLOSTU          OF PDBLOC            &
label "Statut........:"&
        HIDDEN ID 45                       &
        DISPLAY


FIELD CBIDSC           OF A_CBI_BLOSTU     &
        DISPLAY                            &
        SIZE 18


ALIGN (,3,19) (,,25)


FIELD TGRCODGRA    OF PDBLOC                  &
label "Code granit...:"&
        NUMERIC                               &
        BWZ                                   &
        DISPLAY


FIELD TGRDSCFRA001 OF PDTYPE_GRANIT           &
        DISPLAY


SKIP TO LINE 12


ALIGN (2,3,19)

FIELD BLONUMLOC OF PDBLOC &
label "Localisation..:"&
     HIDDEN ID 53


FIELD BLOLONBRU OF PDBLOC                     &
label "Longueur brut.:"&
        HIDDEN ID 54    size 4                      &
        BWZ                                   &
        REQUIRED


FIELD BLOHAUBRU OF PDBLOC                     &
label "Hauteur brut..:"&
        HIDDEN ID 56      size 4                    &
        BWZ                                   &
        REQUIRED


FIELD BLOLARBRU OF PDBLOC                     &
label "Largeur brut..:"&
        HIDDEN ID 58  size 4                        &
        BWZ                                   &
        REQUIRED


ALIGN (,3,19)


FIELD BLOVOLMC3BRU     OF PDBLOC              &
label "Volume brut...:"&
        DISPLAY size 9


SKIP TO LINE 13


ALIGN (28,29,45)


FIELD BLOLONNET OF PDBLOC                     &
label "Longueur nette:"&
        HIDDEN ID 62   size 4                       &
        BWZ                                   &
        REQUIRED


FIELD BLOHAUNET OF PDBLOC                     &
label "Hauteur nette.:"&
        HIDDEN ID 64 size 4                         &
        BWZ                                   &
        REQUIRED


FIELD BLOLARNET OF PDBLOC                     &
label "Largeur nette.:"&
        HIDDEN ID 66  size 4                        &
        BWZ                                   &
        REQUIRED


ALIGN (,29,45)


FIELD BLOVOLMC3NET     OF PDBLOC              &
label "Volume net....:"&
        DISPLAY    size 9


SKIP TO LINE 13


ALIGN (54,55,71)


FIELD BLOLONPYE OF PDBLOC                     &
label "Longueur payer:"&
        HIDDEN ID 70   size 4                       &
        BWZ                                   &
        REQUIRED


FIELD BLOHAUPYE OF PDBLOC                     &
label "Hauteur payer.:"&
        HIDDEN ID 72      size 4                    &
        BWZ                                   &
        REQUIRED


FIELD BLOLARPYE OF PDBLOC                     &
label "Largeur payer.:"&
        HIDDEN ID 74  size 4                        &
        BWZ                                   &
        REQUIRED


ALIGN (,55,71)


FIELD BLOVOLMC3PYE     OF PDBLOC              &
label "Volume à payer:"&
        DISPLAY  size 9


SKIP TO LINE 17


ALIGN (02,03,19)(41,43,59) ; MP-00-05-30_D61.

FIELD BLONUMCARBLO     OF PDBLOC              &
label "Num.carte....:"&
        HIDDEN ID 76                          &
        REQUIRED                              &
        LOOKUP NOTON PDBLOC                   &
          VIA   CIECLE,                       &
                BLONUMCARBLO                  &
          USING T_CIECLEMNU,                  &
                BLONUMCARBLO     OF PDBLOC    &
          MESSAGE 3127 ; Ce numéro de carte de bloc existe déjà

FIELD PODCPL OF PDDET_BON_COMMAN              &
label "Complet.......:"&
        HIDDEN ID 78

SKIP TO LINE 18


ALIGN (,3,19) (41,43,59)


FIELD PODQTECMD OF PDDET_BON_COMMAN           &
label "Qte CMD BCM...:"&
        DISPLAY                               &
        BWZ


FIELD PODQTERCU OF PDDET_BON_COMMAN           &
label "Qte rcu à date:"&
        HIDDEN ID 80                          &
        DISPLAY                               &
        BWZ


ALIGN (,3,19) (,43,59)


FIELD PODVOLCMD OF PDDET_BON_COMMAN           &
label "Volume cmd....:"&
        BWZ                                   &
        DISPLAY


FIELD PODVOLRCU OF PDDET_BON_COMMAN           &
label "Volume rcu....:"&
        BWZ                                   &
        DISPLAY REFRESH


ALIGN (2,3,19) (41,43,59)


FIELD REBPRIMC3 OF PDRECEPTION_BLOC           &
label "Prix m3.......:"&
        HIDDEN ID 82 ;                         &
;        DEFAULT PODPRI OF PDDET_BON_COMMAN


FIELD REBPRIMC3VEN OF PDRECEPTION_BLOC        &
label "Prix m3 vent..:"&
        HIDDEN ID 84                          &
        BWZ


FIELD FOUNUMFACDOU OF PDRECEPTION_BLOC        & ; MP-00-05-30_D61.
      LABEL "Fac.douane....:"                 &
      HIDDEN ID 88


FIELD REBNUMBLOFOU OF PDRECEPTION_BLOC        &
label "No bloc fourn.:"&
        HIDDEN ID 90

FIELD FOUNUMFACBLO OF PDRECEPTION_BLOC &
      LABEL "Fac.fourni....:" &
      HIDDEN ID 92

FIELD TRANUMFACBLO OF PDRECEPTION_BLOC &
      LABEL "Fac.transport.:" &
      HIDDEN ID 94

FIELD T_SILENT_2 SILENT

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;

use uscalvol NOLIST
;--------------------------------------------------------------------------
PROCEDURE INPUT PODNUMLIG
BEGIN
 LET T_BCMNUMBCM = BCMNUMBCM OF PDBON_COMMANDE
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PODNUMLIG = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd176i MODE F &
                     PASSING T_PODNUMLIG   , &
                             T_BCMNUMBCM, &
                             T_TGRCODGRA ,&
                             T_TYFCODFIN ,&
                             T_PODLON    ,&
                             T_PODHAU    ,&
                             T_PODEPA    ,&
                             T_FLG,       &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_PODNUMLIG )
  END
END

;-------------------------------------------------------------------------------
;
; Valide le champ
;

PROCEDURE EDIT PODNUMLIG
BEGIN
  ;
  ; Valide que le champs a été saisit s'il n'y a pas eu de DUPLICATE
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Valide que la ligne n'est pas déjà complète
  ;
  IF PODCPL OF PDDET_BON_COMMAN = "1"
  THEN BEGIN
    ERROR 2932 ; Cette ligne est déjà complétée
  END
END


;-------------------------------------------------------------------------------
;
; incrémente la quantité reçu (en saisie seulement)
;

PROCEDURE PROCESS PODNUMLIG
BEGIN
  ;
  ; Incrémente la quantité reçu (En saisie seulement)
  ;
  IF ENTRYMODE
  THEN BEGIN
    LET PODQTERCU OF PDDET_BON_COMMAN = PODQTERCU OF PDDET_BON_COMMAN + 1
  END
  LET TGRCODGRA OF PDBLOC = TGRCODGRA OF PDDET_BON_COMMAN
END


;-------------------------------------------------------------------------------
;
; Assignation du numéro de bloc complèt
;

PROCEDURE EDIT T_SILENT
BEGIN
  LET BLONUMBLOAPR OF PDRECEPTION_BLOC = BLONUMGRA001APR OF PDRECEPTION_BLOC + &
                                         BLONUMGRA002APR OF PDRECEPTION_BLOC + &
                                         BLORECAPR       OF PDRECEPTION_BLOC
  LET  BLONUMBLOAPR OF PDBLOC = BLONUMBLOAPR OF PDRECEPTION_BLOC
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions brut du bloc
; N.B.: on ne peut pas avoir de bloc plus petit que 500 mm. et plus grand que
;       4500 mm.
;

PROCEDURE EDIT BLOLARBRU
BEGIN
  IF FIELDVALUE < T_DIM_MIN_LAR_BL OR &
     FIELDVALUE > T_DIM_MAX_LAR_BL
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
  IF FIELDVALUE < BLOLARNET OF PDBLOC
  THEN BEGIN
    ERROR 3124 ; Cette dimension est plus petite que la dimension net
  END
  LET TZ_LARGEUR   = FIELDVALUE ; BLOLARBRU OF PDBLOC
  LET TZ_HAUTEUR   = BLOHAUBRU OF PDBLOC
  LET TZ_EPAISSEUR = BLOLONBRU OF PDBLOC
  DO INTERNAL CALVOL
  LET BLOVOLMC3BRU OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3BRU OF PDBLOC
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions brut du bloc
; N.B.: on ne peut pas avoir de bloc plus petit que 500 mm. et plus grand que
;       4500 mm.
;

PROCEDURE EDIT BLOHAUBRU
BEGIN
  IF FIELDVALUE < T_DIM_MIN_HAU_BL OR &
     FIELDVALUE > T_DIM_MAX_HAU_BL
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
  IF FIELDVALUE < BLOHAUNET OF PDBLOC
  THEN BEGIN
    ERROR 3124 ; Cette dimension est plus petite que la dimension net
  END
  LET TZ_LARGEUR   = BLOLARBRU OF PDBLOC
  LET TZ_HAUTEUR   = FIELDVALUE ; BLOHAUBRU OF PDBLOC
  LET TZ_EPAISSEUR = BLOLONBRU OF PDBLOC
  DO INTERNAL CALVOL
  LET BLOVOLMC3BRU OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3BRU OF PDBLOC
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions brut du bloc
; N.B.: on ne peut pas avoir de bloc plus petit que 500 mm. et plus grand que
;       4500 mm.
;

PROCEDURE EDIT BLOLONBRU
BEGIN
  IF FIELDVALUE < T_DIM_MIN_EPA_BL OR &
     FIELDVALUE > T_DIM_MAX_EPA_BL
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
  IF FIELDVALUE < BLOLONNET OF PDBLOC
  THEN BEGIN
    ERROR 3124 ; Cette dimension est plus petite que la dimension net
  END
  LET TZ_LARGEUR   = BLOLARBRU OF PDBLOC
  LET TZ_HAUTEUR   = BLOHAUBRU OF PDBLOC
  LET TZ_EPAISSEUR = FIELDVALUE ; BLOLONBRU OF PDBLOC
  DO INTERNAL CALVOL
  LET BLOVOLMC3BRU OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3BRU OF PDBLOC
END



;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions nettes du bloc
; N.B.: on ne peut pas avoir de dimension nette plus grande que les dimensions
;       brutes.
;

PROCEDURE EDIT BLOLARNET
BEGIN
  IF FIELDVALUE > BLOLARBRU OF PDBLOC
  THEN BEGIN
    ERROR 2933 ; Cette dimension est plus grande que la dimension brut
  END
  IF FIELDVALUE < BLOLARPYE OF PDBLOC
  THEN BEGIN
;    ERROR 3125 ; Cette dimension est plus petite que la dimension à payer
    WARNING = "Cette dimension est plus petite que la dimension à payer"
  END
  LET TZ_LARGEUR   = FIELDVALUE ; BLOLARNET OF PDBLOC
  LET TZ_HAUTEUR   = BLOHAUNET OF PDBLOC
  LET TZ_EPAISSEUR = BLOLONNET OF PDBLOC
  DO INTERNAL CALVOL
  LET BLOVOLMC3NET OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3NET OF PDBLOC
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions nettes du bloc
; N.B.: on ne peut pas avoir de dimension nette plus grande que les dimensions
;       brutes.
;

PROCEDURE EDIT BLOHAUNET
BEGIN
  IF FIELDVALUE > BLOHAUBRU OF PDBLOC
  THEN BEGIN
    ERROR 2933 ; Cette dimension est plus grande que la dimension brut
  END
  IF FIELDVALUE < BLOHAUPYE OF PDBLOC
  THEN BEGIN
;    ERROR 3125 ; Cette dimension est plus petite que la dimension à payer
    WARNING = "Cette dimension est plus petite que la dimension à payer"
  END
  LET TZ_LARGEUR   = BLOLARNET OF PDBLOC
  LET TZ_HAUTEUR   = FIELDVALUE ; BLOHAUNET OF PDBLOC
  LET TZ_EPAISSEUR = BLOLONNET OF PDBLOC
  DO INTERNAL CALVOL
  LET BLOVOLMC3NET OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3NET OF PDBLOC
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions nettes du bloc
; N.B.: on ne peut pas avoir de dimension nette plus grande que les dimensions
;       brutes.
;

PROCEDURE EDIT BLOLONNET
BEGIN
  IF FIELDVALUE > BLOLONBRU OF PDBLOC
  THEN BEGIN
    ERROR 2933 ; Cette dimension est plus grande que la dimension brut
  END
  IF FIELDVALUE < BLOLONPYE OF PDBLOC
  THEN BEGIN
;    ERROR 3125 ; Cette dimension est plus petite que la dimension à payer
    WARNING "Cette dimension est plus petite que la dimension à payer"
  END
  LET TZ_LARGEUR   = BLOLARNET OF PDBLOC
  LET TZ_HAUTEUR   = BLOHAUNET OF PDBLOC
  LET TZ_EPAISSEUR = FIELDVALUE ; BLOLONNET OF PDBLOC
  DO INTERNAL CALVOL
  LET BLOVOLMC3NET OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3NET OF PDBLOC
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions à payer du bloc
; N.B.: on ne peut pas avoir de dimension à payer plus grande que les dimensions
;       nettes.
;

PROCEDURE EDIT BLOLARPYE
BEGIN
  IF FIELDVALUE > BLOLARNET OF PDBLOC
  THEN BEGIN
;    ERROR 2934 ; Cette dimension est plus grande que la dimension net
   WARNING = "Cette dimension est plus grande que la dimension net"
  END
  ;
  ; Calcul du volume (old)
  ;
  LET T_OLD_VOL = 0.0000
  IF CHANGEMODE
  THEN BEGIN
    LET TZ_LARGEUR   = OLDVALUE(BLOLARPYE OF PDBLOC)
    LET TZ_HAUTEUR   = BLOHAUPYE OF PDBLOC
    LET TZ_EPAISSEUR = BLOLONPYE OF PDBLOC
    DO INTERNAL CALVOL
    LET T_OLD_VOL    = TZ_VOLUME
  END
  ;
  ; Calcul de volume (new)
  ;
  LET TZ_LARGEUR   = FIELDVALUE ; BLOLARPYE OF PDBLOC
  LET TZ_HAUTEUR   = BLOHAUPYE OF PDBLOC
  LET TZ_EPAISSEUR = BLOLONPYE OF PDBLOC
  DO INTERNAL CALVOL

  LET BLOVOLMC3PYE OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3PYE OF PDBLOC

END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu
;

PROCEDURE PROCESS BLOLARPYE
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODVOLRCU OF PDDET_BON_COMMAN = PODVOLRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_VOL  +                    &
                                      TZ_VOLUME
  IF NOT ENTRYMODE
  THEN BEGIN
    DISPLAY PODVOLRCU OF PDDET_BON_COMMAN
  END
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions à payer du bloc
; N.B.: on ne peut pas avoir de dimension à payer plus grande que les dimensions
;       nettes.
;

PROCEDURE EDIT BLOHAUPYE
BEGIN
  IF FIELDVALUE > BLOHAUNET OF PDBLOC
  THEN BEGIN
;    ERROR 2934 ; Cette dimension est plus grande que la dimension net
    WARNING = "Cette dimension est plus grande que la dimension net"
  END
  ;
  ; Calcul de volume (old)
  ;
  LET T_OLD_VOL = 0.00
  IF CHANGEMODE
  THEN BEGIN
    LET TZ_LARGEUR   = BLOLARPYE OF PDBLOC
    LET TZ_HAUTEUR   = OLDVALUE(BLOHAUPYE OF PDBLOC)
    LET TZ_EPAISSEUR = BLOLONPYE OF PDBLOC
    DO INTERNAL CALVOL
    LET T_OLD_VOL    = TZ_VOLUME
  END
  ;
  ; Calcul de volume (new)
  ;
  LET TZ_LARGEUR   = BLOLARPYE OF PDBLOC
  LET TZ_HAUTEUR   = FIELDVALUE ; BLOHAUPYE OF PDBLOC
  LET TZ_EPAISSEUR = BLOLONPYE OF PDBLOC
  DO INTERNAL CALVOL

  LET BLOVOLMC3PYE OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3PYE OF PDBLOC

END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu
;

PROCEDURE PROCESS BLOHAUPYE
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODVOLRCU OF PDDET_BON_COMMAN = PODVOLRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_VOL +                     &
                                      TZ_VOLUME
  IF NOT ENTRYMODE
  THEN BEGIN
    DISPLAY PODVOLRCU OF PDDET_BON_COMMAN
  END
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions à payer du bloc
; N.B.: on ne peut pas avoir de dimension à payer plus grande que les dimensions
;       nettes.
;

PROCEDURE EDIT BLOLONPYE
BEGIN
  IF BLOLONPYE OF PDBLOC > BLOLONNET OF PDBLOC
  THEN BEGIN
;    ERROR 2934 ; Cette dimension est plus grande que la dimension net
    WARNING = "Cette dimension est plus grande que la dimension net"
  END
  ;
  ; Calcul de volume (old)
  ;
  LET T_OLD_VOL = 0.00
  IF CHANGEMODE
  THEN BEGIN
    LET TZ_LARGEUR   = BLOLARPYE OF PDBLOC
    LET TZ_HAUTEUR   = BLOHAUPYE OF PDBLOC
    LET TZ_EPAISSEUR = OLDVALUE(BLOLONPYE OF PDBLOC)
    DO INTERNAL CALVOL
    LET T_OLD_VOL    = TZ_VOLUME
  END
  ;
  ; Calcul de volume (new)
  ;
  LET TZ_LARGEUR   = BLOLARPYE OF PDBLOC
  LET TZ_HAUTEUR   = BLOHAUPYE OF PDBLOC
  LET TZ_EPAISSEUR = FIELDVALUE ; BLOLONPYE OF PDBLOC
  DO INTERNAL CALVOL

  LET BLOVOLMC3PYE OF PDBLOC = TZ_VOLUME
  DISPLAY BLOVOLMC3PYE OF PDBLOC

END


;-------------------------------------------------------------------------------
;
; Calcul du volume reçu
;

PROCEDURE PROCESS BLOLONPYE
BEGIN
  ;
  ; Différence Volume = Volume - Old + New
  ;
  LET PODVOLRCU OF PDDET_BON_COMMAN = PODVOLRCU OF PDDET_BON_COMMAN - &
                                      T_OLD_VOL +                     &
                                      TZ_VOLUME
  DISPLAY PODVOLRCU OF PDDET_BON_COMMAN
END

;-------------------------------------------------------------------------------
;
; Valide que le champs a été saisi
;

PROCEDURE EDIT REBPRIMC3
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END

END


;-------------------------------------------------------------------------------
;
; Informe l'utilisateur que la QTE RCU > QTE CMD
;
PROCEDURE OUTPUT PODVOLRCU
BEGIN
  IF NOT FINDMODE
  THEN BEGIN
  IF PODVOLRCU OF PDDET_BON_COMMAN > PODVOLCMD OF PDDET_BON_COMMAN
  THEN BEGIN
    WARNING 2936 ; la quantité reçu est plus grande que la quantité commandé
  END
  END
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;
PROCEDURE INPUT PODCPL
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT PODCPL
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel automatique de l'écran pd176e en mode entry
;
PROCEDURE PROCESS T_SILENT_2
BEGIN

  IF NEWRECORD OF PDRECEPTION_BLOC
  THEN BEGIN

    LET BLONUMGRA001APR OF PDBLOC = BLONUMGRA001APR OF PDRECEPTION_BLOC
    LET BLONUMGRA002APR OF PDBLOC = BLONUMGRA002APR OF PDRECEPTION_BLOC
    LET BLORECAPR       OF PDBLOC = BLORECAPR       OF PDRECEPTION_BLOC

    RUN SCREEN  pd176e            &
            PASSING   T_CIECLEMNU,    &
                      T_CIENOM,       &
                      PDBLOC          &
            MODE SAME
  END
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Consultation des date de création et de modification du bloc
;

PROCEDURE DESIGNER D001
BEGIN
 LET BLONUMGRA001APR OF PDBLOC = BLONUMGRA001APR OF PDRECEPTION_BLOC
 LET BLONUMGRA002APR OF PDBLOC = BLONUMGRA002APR OF PDRECEPTION_BLOC
 LET BLORECAPR       OF PDBLOC = BLORECAPR       OF PDRECEPTION_BLOC

    RUN SCREEN  pd176e &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        PDBLOC          &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Sous-écran de défauts de bloc
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd176f &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        PDBLOC          &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Retrouve le détail bon de commande pour pas avoir d'erreur si plus d'une
; destruction pour un même bon de commande.
;

PROCEDURE POSTFIND
BEGIN
  GET PDDET_BON_COMMAN                      &
    VIA     CIECLE,                         &
            BCMNUMBCM,                      &
            PODNUMLIG,                      &
            TPDTYPPRD                       &
    USING   T_CIECLEMNU,                    &
            BCMNUMBCM OF PDBON_COMMANDE,    &
            PODNUMLIG OF PDRECEPTION_BLOC,  &
            T_TYPE_PRODUIT
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDRECEPTION_BLOC &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDRECEPTION_BLOC &
       AND NOT NEWRECORD     OF PDRECEPTION_BLOC &
       AND NOT DELETEDRECORD OF PDRECEPTION_BLOC &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Affectation du volume du bloc
    ;

;;;    LET TZ_LARGEUR   = BLOLARBRU OF PDBLOC
;;;    LET TZ_HAUTEUR   = BLOHAUBRU OF PDBLOC
;;;    LET TZ_EPAISSEUR = BLOLONBRU OF PDBLOC
;;;    DO INTERNAL CALVOL
;;;    LET BLOVOLMC3BRU OF PDBLOC = TZ_VOLUME
;;;
;;;    LET TZ_LARGEUR   = BLOLARNET OF PDBLOC
;;;    LET TZ_HAUTEUR   = BLOHAUNET OF PDBLOC
;;;    LET TZ_EPAISSEUR = BLOLONNET OF PDBLOC
;;;    DO INTERNAL CALVOL
;;;    LET BLOVOLMC3NET OF PDBLOC = TZ_VOLUME
;;;
;;;    LET TZ_LARGEUR   = BLOLARPYE OF PDBLOC
;;;    LET TZ_HAUTEUR   = BLOHAUPYE OF PDBLOC
;;;    LET TZ_EPAISSEUR = BLOLONPYE OF PDBLOC
;;;    DO INTERNAL CALVOL
;;;    LET BLOVOLMC3PYE     OF PDBLOC = TZ_VOLUME

    ;
    ; Évaluation du poids estimé du bloc
    ;
    LET BLOPOIEST       OF PDBLOC = BLOVOLMC3BRU OF PDBLOC * TGRPOI OF PDTYPE_GRANIT


    ;
    ; Évaluation des champs indiquant une mise à jour du bon de commande.
    ;
    IF NOT NEWRECORD OF PDRECEPTION_BLOC AND &
       ALTEREDRECORD OF PDRECEPTION_BLOC
    THEN BEGIN
      LET RCTDATDERMAJ    OF PDRECEPTION = SYSDATE
      LET RCTHREDERMAJ    OF PDRECEPTION = SYSTIME / 10000
      LET RCTUSRDERMAJ    OF PDRECEPTION = LOGONID
    END

;   si le bloc est redim on doit mettre les prix a jour
    GET A_PDREDIM_BLOC OPTIONAL &
      VIA   CIECLE,      &
            BLONUMBLOAPR &
      USING T_CIECLEMNU, &
            BLONUMBLOAPR OF PDRECEPTION_BLOC
    IF ACCESSOK
    THEN BEGIN
      LET REBPRIMC3    OF A_PDREDIM_BLOC = REBPRIMC3    OF PDRECEPTION_BLOC
      LET REBPRIMC3VEN OF A_PDREDIM_BLOC = REBPRIMC3VEN OF PDRECEPTION_BLOC
    END

    PUT A_PDREDIM_BLOC RESET
    
    
; MP-01-10-26_DD.
;

  IF NEWRECORD OF PDRECEPTION_BLOC AND NOT DELETEDRECORD OF PDRECEPTION_BLOC
  THEN BEGIN
    IF PJTABR    OF PDBON_COMMANDE <> " " AND & ; MP-02-01-17_D200208
       COMNUMCOM OF PDBON_COMMANDE <> " "
    THEN BEGIN
   
      GET PDCOMMAN_ASSIGN VIA CIECLE,                          &
                              BLONUMBLOAPR                     &
                        USING T_CIECLEMNU,                     &
                              BLONUMBLOAPR OF PDRECEPTION_BLOC OPTIONAL
   
      IF NOT ACCESSOK
      THEN BEGIN
        LET CIECLE           OF PDCOMMAN_ASSIGN = T_CIECLEMNU
        LET TPDTYPPRD        OF PDCOMMAN_ASSIGN = "BL"
        LET PJTABR           OF PDCOMMAN_ASSIGN = PJTABR          OF PDBON_COMMANDE
        LET PJTANN           OF PDCOMMAN_ASSIGN = PJTANN          OF PDBON_COMMANDE
        LET COMNUMCOMINT     OF PDCOMMAN_ASSIGN = COMNUMCOMINT    OF PDBON_COMMANDE
        LET COMNUMCOM        OF PDCOMMAN_ASSIGN = PJTABR          OF PDBON_COMMANDE + &
                                                  PJTANN          OF PDBON_COMMANDE + &
                                                  COMNUMCOMINT    OF PDBON_COMMANDE
        LET PJTNUMPRO        OF PDCOMMAN_ASSIGN = PJTNUMPRO       OF PDBON_COMMANDE
        LET TRANUMTRA002     OF PDCOMMAN_ASSIGN = " "
        LET BLONUMGRA001APR  OF PDCOMMAN_ASSIGN = BLONUMGRA001APR OF PDRECEPTION_BLOC
        LET BLONUMGRA002APR  OF PDCOMMAN_ASSIGN = BLONUMGRA002APR OF PDRECEPTION_BLOC
        LET BLORECAPR        OF PDCOMMAN_ASSIGN = BLORECAPR       OF PDRECEPTION_BLOC
        LET BLONUMBLOAPR     OF PDCOMMAN_ASSIGN = BLONUMBLOAPR    OF PDRECEPTION_BLOC
     
     
        GET PDDETAIL_C_I VIA CIECLE,                          &
                             PJTABR,                          &
                             COMNUMCOMINT,                    &
                             TGRCODGRA                        &
                       USING T_CIECLEMNU,                     &
                             PJTABR       OF PDCOMMAN_ASSIGN, &
                             COMNUMCOMINT OF PDCOMMAN_ASSIGN, &
                             TGRCODGRA    OF PDBLOC           OPTIONAL
     
        IF NOT ACCESSOK
        THEN LET CASACCERR OF PDCOMMAN_ASSIGN = "1"
        
        PUT PDCOMMAN_ASSIGN RESET
      END
    END
    ELSE WARNING "Le bloc n'a pas été assigné à la commande. Absence du no commande sur le P/O" NOW RESP
  END

END

;-------------------------------------------------------------------------------
;
; Validation avant la destruction
;

PROCEDURE INTERNAL RECHERCHE_BLOC
BEGIN
  ;
  ; Recherche si le bloc a été assigné à une commande
  ;
  GET PDCOMMAN_ASSIGN OPTIONAL &
    VIA   CIECLE,              &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Assignation commande interne") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si le bloc a été redimensionné
  ;
  GET PDREDIM_BLOC OPTIONAL &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Redimension bloc") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si le bloc a été assigné à une soumission
  ;
  GET PDBLOCS_SOUMI OPTIONAL &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Soumission") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si le bloc a été assigné à un montage bloc
  ;
  GET PDDET_RAP_MONT OPTIONAL &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Montage bloc") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si le bloc a été assigné à une sortie de bloc
  ;
  GET PDSORTIE_BLOCS OPTIONAL &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Sortie de bloc") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si le bloc a été assigné à une réservation de bloc
  ;
  GET PDRESER_BLOCS OPTIONAL &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Réservation de bloc") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
  ;
  ; Recherche si le bloc a été facturé
  ;
  GET PDBLOC_FACTURE OPTIONAL &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  IF ACCESSOK
  THEN BEGIN
    ERROR = SUBSTITUTE(3188," Facture") ; DESTRUCTION ANNULÉ, l'élément est référé
  END
END


;-------------------------------------------------------------------------------
;
; Procedure de destruction
;

PROCEDURE DELETE
BEGIN
  IF NOT DELETEDRECORD OF PDRECEPTION_BLOC
  THEN BEGIN
    ;
    ; Valide le statut du bloc avant la destruction
    ;
    GET PDBLOC &
      VIA   CIECLE,      &
            BLONUMBLOAPR &
      USING T_CIECLEMNU, &
            BLONUMBLOAPR OF PDRECEPTION_BLOC
    IF ACCESSOK
    THEN BEGIN
      IF BLOSTU OF PDBLOC <> "I"
      THEN BEGIN
        ERROR 3068 ; Le statut ne permet pas cette opération
      END
    END
    ELSE BEGIN
      ERROR = "PROBLÈME TECHNIQUE (1) - Le bloc n'existe pas"
    END
    ;
    ; Recherche si le bloc a déjà été utilisé
    ;
    DO INTERNAL RECHERCHE_BLOC
    ;
    ; Décrémente la quantité reçu
    ;
    LET PODQTERCU OF PDDET_BON_COMMAN = PODQTERCU OF PDDET_BON_COMMAN - 1
    ;
    ; Décrémente de volume reçu
    ;
    LET TZ_LARGEUR   = BLOLARPYE OF PDBLOC
    LET TZ_HAUTEUR   = BLOHAUPYE OF PDBLOC
    LET TZ_EPAISSEUR = BLOLONPYE OF PDBLOC
    DO INTERNAL CALVOL
    LET PODVOLRCU OF PDDET_BON_COMMAN = PODVOLRCU OF PDDET_BON_COMMAN - TZ_VOLUME
    ;
    ; Initialise les indicateurs de réception
    ;
    IF PODQTERCU OF PDDET_BON_COMMAN = 0
    THEN BEGIN
      LET PODRCU OF PDDET_BON_COMMAN = "0"
    END
    ELSE BEGIN
      LET PODRCU OF PDDET_BON_COMMAN = "1"
    END
    ;
    ; Si la commade était complète alors on indique qu'elle ne le sera plus
    ;
    IF PODCPL OF PDDET_BON_COMMAN = "1"
    THEN BEGIN
      LET PODCPL OF PDDET_BON_COMMAN = "0"
    END
  END
  ;
  ; Destruction des défauts de bloc
  ;
  WHILE RETRIEVING PDBLOC_DEFAUTS &
    VIA   CIECLE,      &
          BLONUMBLOAPR &
    USING T_CIECLEMNU, &
          BLONUMBLOAPR OF PDRECEPTION_BLOC
  BEGIN
    DELETE PDBLOC_DEFAUTS
  END
  ;
  ; Destriction des enregistrements
  ;
  DELETE PDRECEPTION_BLOC
  DELETE PDBLOC
  ;
  ; Affiche les nouvelles valeurs
  ;
  DISPLAY PODQTERCU OF PDDET_BON_COMMAN
  DISPLAY PODVOLRCU OF PDDET_BON_COMMAN
END

; ----------------------------------------------------------------------------------------
; MP-00-04-17_D38.
;
; Conserver les blocs fils dans le vecteur associé
;
PROCEDURE INTERNAL I_OCC_BLO
BEGIN
  FOR EACH T_BLONUMBLO_OCC
  BEGIN
    IF T_OCC_NB = OCCURRENCE OF T_BLONUMBLO_OCC
    THEN LET T_BLONUMBLO_OCC = BLONUMBLOAPR OF A2_PDBLOC ; Conserver les blocs "fils"
  END
END

; ----------------------------------------------------------------------------------------
; MP-00-04-17_D38.
;
; Procédure de mise à jour des prix au mètre cube pour les blocs redimensionnés
;
PROCEDURE INTERNAL I_MAJ_PRX
BEGIN
  GET A1_PDBLOC VIA CIECLE                    , & ; Recherche du bloc "père" ou "fils"
                    BLONUMBLOAPR                &
              USING CIECLE OF PDRECEPTION_BLOC, &
                    T_BLONUMBLO                 OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    GET A_PDREDIM_BLOC VIA CIECLE             , & ; Recherche du bloc redimensionné associé
                           BLONUMBLOAPR         &
                     USING CIECLE OF A1_PDBLOC, &
                           T_BLONUMBLO          OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      LET REBPRIMC3    OF A_PDREDIM_BLOC = REBPRIMC3    OF PDRECEPTION_BLOC
      LET REBPRIMC3VEN OF A_PDREDIM_BLOC = REBPRIMC3VEN OF PDRECEPTION_BLOC

      PUT A_PDREDIM_BLOC RESET ; Mise à jour du bloc redimensionné associé
    END

    LET REBPRIMC3    OF A1_PDBLOC = REBPRIMC3    OF PDRECEPTION_BLOC
    LET REBPRIMC3VEN OF A1_PDBLOC = REBPRIMC3VEN OF PDRECEPTION_BLOC

    PUT A1_PDBLOC ; Mise à jour du bloc "père" ou "fils"
  END

  WHILE RETRIEVING A2_PDBLOC VIA CIECLE              , & ; Recherche des blocs "fils"
                                 BLONUMBLOAVT          &
                           USING CIECLE OF A1_PDBLOC , &
                                 T_BLONUMBLO               
  BEGIN
    LET T_OCC_NB = T_OCC_NB + 1 ; Incrémentation du compteur de blocs "fils"
    DO INTERNAL I_OCC_BLO       ; Appel de la procédure conservant les blocs fils
  END
END

; ----------------------------------------------------------------------------------------

PROCEDURE POSTUPDATE
BEGIN

; MP-00-04-17_D38.
;
; Initialisations et appel de la procédure de mise à jour des prix au mètre cube 
; pour les blocs redimensionnés
;
  LET T_OCC_NB    = 0    ; Initialisation du compteur de blocs "père" et "fils"
  LET T_BLONUMBLO = " "  ; Initialisation du bloc "fils"

  LET T_BLONUMBLO = BLONUMGRA001APR OF PDRECEPTION_BLOC + & ; Assignation du bloc "père"
                    BLONUMGRA002APR OF PDRECEPTION_BLOC + &
                    BLORECAPR       OF PDRECEPTION_BLOC

  DO INTERNAL I_MAJ_PRX ; Appel de la procédure de mise à jour des prix au mètre cube

  FOR EACH T_BLONUMBLO_OCC ; Mise à jour des prix au mètre cube pour les blocs "fils"
  BEGIN
    IF T_BLONUMBLO_OCC <> " "
    THEN BEGIN
      LET T_BLONUMBLO = T_BLONUMBLO_OCC  ; Assignation du bloc "fils"
      DO INTERNAL I_MAJ_PRX              ; Appel de la procédure de mise à jour des prix au mètre cube
      LET T_BLONUMBLO_OCC = " "          ; Réinitialisation de l'occurence du bloc "fils"
    END
  END

END


BUILD DETAIL LIST

@IF FORMAT
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
****************************** Réception de bloc *******************************
* No. réception.: xxxxxxxxx               Date réception: xxxxxxxx             *
* Fournisseur...: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
*                                                                              *
************************************ Blocs *************************************
*                                                                              *
* Numéro ligne..: xxx                     Prix M3 cmd...: xxxxxxxxxxxxxxx      *
* No bloc.......: xxxx-xxxxx-x            Statut........: x xxxxxxxxxxxxxxxxxx *
* Code granit...: xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* LOCALISATION..: XXX                                                          *
* Longueur brut.: xxxx      Longueur nette: xxxx      Longueur payer: xxxx     *
* Hauteur brut..: xxxx      Hauteur nette.: xxxx      Hauteur payer.: xxxx     *
* Largeur brut..: xxxx      Largeur nette.: xxxx      Largeur payer.: xxxx     *
* Volume brut...: xxxxxxxxx Volume net....: xxxxxxxxx Volume à payer: xxxxxxxxx*
* Num. carte....: xxxxxx                  Complet.......: x                    *
* Qte CMD BCM...: xxxxxxxxxxxx            Qte rcu à date: xxxxxxxxxxxx         *
* Volume cmd....: xxxxxxxxx               Volume rcu....: xxxxxxxxx            *
* Prix m3.......: xxxxxxxxxxxxxxx         Prix m3 vent..: xxxxxxxxxxxxxxx      *
* Complet.......: x                       No bloc fourn.: xxxxxxxxxx           *
* Fac.fourni....: XXXXXXXXXXXXXXXXXX      Fac.transport.: XXXXXXXXXXXXXXXXXXXX *
********************************************************************************
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
@ENDIF
