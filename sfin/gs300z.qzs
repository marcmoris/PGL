;/T/ Usagers par application
;
;/P/ Programmeur...: Isabelle Proulx
;    Date Création.: 10 avril 1992
;
;    Description...: Ce rapport présente les caractéristiques d'utilisation
;                    pour les applications.
;
;    Paramètres....: Nom du programme (XPRNOM)
;
;    Particularités: Aucune
;
;    Modifies par  : Steve Des Rosiers 95-01-19
;    Description   : CHanger odre de selection et item de presentation
;----------------------------------------------------------------------------


USE ussetqzs NOLIST


ACCESS GSUSAGER_PROG LINK TO GSUSAGER LINK TO GSPROGRAMME
CHOOSE USRCLE PARM, &
       xprnom parm

; DEFINE D_MODULE CHAR SIZE 2 = PARM

;SELECT IF D_MODULE = XPRNOM[1:2] OR D_MODULE = " "

;----------------------------------------------------------------------------
; Variable pour les entêtes standard
;----------------------------------------------------------------------------

@IF FRANCAIS
  DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Application par usagers"
                                                ; contient le titre du rapport
  DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "     ; contient le titre du rapport
@ELSE
  DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Users by application"
                                                ; contient le titre du rapport
  DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "     ; contient le titre du rapport
@ENDIF
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GS300Z" ; contient le numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------
; Variables pour les codes bilingues
;----------------------------------------------------------------------------
@IF FRANCAIS
DEFINE D_USPFLGENT CHARACTER SIZE 1  = "O" IF USPFLGENT="1" &
                                  ELSE "N" IF USPFLGENT="0"
DEFINE D_USPFLGACS CHARACTER SIZE 1  = "O" IF USPFLGACS="1" &
                                  ELSE "N" IF USPFLGACS="0"
DEFINE D_USPFLGMOD CHARACTER SIZE 1  = "O" IF USPFLGMOD="1" &
                                  ELSE "N" IF USPFLGMOD="0"
DEFINE D_USPFLGDEL CHARACTER SIZE 1  = "O" IF USPFLGDEL="1" &
                                  ELSE "N" IF USPFLGDEL="0"
@ELSE
DEFINE D_USPFLGENT CHARACTER SIZE 1  = "Y" IF USPFLGENT="1" &
                                  ELSE "N" IF USPFLGENT="0"
DEFINE D_USPFLGACS CHARACTER SIZE 1  = "Y" IF USPFLGACS="1" &
                                  ELSE "N" IF USPFLGACS="0"
DEFINE D_USPFLGMOD CHARACTER SIZE 1  = "Y" IF USPFLGMOD="1" &
                                  ELSE "N" IF USPFLGMOD="0"
DEFINE D_USPFLGDEL CHARACTER SIZE 1  = "Y" IF USPFLGDEL="1" &
                                  ELSE "N" IF USPFLGDEL="0"
@ENDIF
;----------------------------------------------------------------------------

@IF FRANCAIS
  DEFINE D_XPRDSCFRA CHARACTER SIZE 45= XPRDSCFRA[1:45] ; Variable pour
                                                        ; description francaise
@ELSE
  DEFINE D_XPRDSCFRA CHARACTER SIZE 45= XPRDSCANG[1:45] ; Variable pour
                                                        ; description francaise
@ENDIF
DEFINE D_USREMPNOM CHARACTER SIZE 30= USREMPNOM[1:30] ; Variable pour le nom
                                                      ; de l'usager

DEFINE D_GENRE CHAR SIZE 10 = "PROCEDURE" IF XPRGNR = "P" &
                       ELSE  "RAPPORT" IF XPRGNR = "R" &
                       ELSE  "ECRAN" IF XPRGNR = "E" &
                       ELSE  "MENU" IF XPRGNR = "M"

DEFINE D_TRI INT SIZ 2 = 1 IF XPRGNR = "M" &
                  ELSE  2 IF XPRGNR = "E" &
                  ELSE  3 IF XPRGNR = "R" &
                  ELSE  4 IF XPRGNR = "P"

DEFINE D_MODUL CHAR SIZE 20 = "GRAND LIVRE" IF XPRNOM[1:2] = "GL" &
                         else "ACHAT ET ENGAGEMENT" IF XPRNOM[1:2] = "AR" &
                         else "COMPTE A PAYER" IF XPRNOM[1:2] = "CP" &
                         else "COMPTE A RECEVOIR" IF XPRNOM[1:2] = "CR" &
                         else "GESTION DE SYSTEME" IF XPRNOM[1:2] = "GS" &
                         else "PRIX DE REVIENT" IF XPRNOM[1:2] = "PR" &
                         else "IMMOBILISATION" IF XPRNOM[1:2] = "IM" &
                         else "INVENTAIRE" IF XPRNOM[1:2] = "IN" &
                         ELSE "INFORMATION DE BASE" IF XPRNOM[1:2] = "MC" &
                         ELSE "GESTION DE PROJETS" IF XPRNOM[1:2] = "GP"

SORT on usrcle on d_modul ON D_TRI ON XPRNOM

INITIAL HEADING "" &
SKIP 3 &
TAB 58 "SECURITE DES USAGER" &
SKIP &
TAB 58 "===================" &
SKIP 5 &
TAB 50"COMPAGNIE.....:____________________" &
SKIP 2 &
TAB 50 "USAGER........:____________________" &
SKIP 2 &
TAB 50 "POSTE.........:____________________" &
SKIP 5 &
TAB 50 "AUTHORIZE PAR.:____________________" &
SKIP 2 &
TAB 50 "DATE..........:____________________" &
SKIP 5 &
TAB 50 "CREER PAR.....:____________________" &
SKIP 2 &
TAB 50 "DATE..........:____________________" &
SKIP 5 &
TAB 60 "COMMENTAIRE...:" &
SKIP PAGE



USE ustitstd NOLIST ; Titre standard
" " &
@IF FRANCAIS
   SKIP 3 "Module:" d_modul &
   skip 2 &
   "Genre de programme:" D_GENRE  &
    TAB 103 "-------- Utilisation --------" &
@ELSE
   SKIP 4 "Program Type" XPRGNR &
   TAB 103 "----------- Use -------------" &
@ENDIF
   SKIP 2 &
   KEEP COLUMN HEADINGS

HEADING AT D_TRI SKIP PAGE
HEADING AT XPRNOM SKIP 1
HEADING AT D_MODUL SKIP PAGE
heading at usrcle skip page

@IF FRANCAIS
  REPORT XPRNOM PRINT AT XPRNOM &
       TAB 11 D_XPRDSCFRA &
       HEADING "^Description Francaise^-----------------------------------" &
       PRINT AT XPRNOM &
       TAB 58 USRCLE HEADING "Code^usager^------------" &
       TAB 71 D_USREMPNOM  HEADING "^Nom usager^-----------------------------" &
       TAB 103 D_USPFLGACS HEADING "^Accès^-----" &
       TAB 110 D_USPFLGENT HEADING "^Saisie^------" &
       TAB 119 D_USPFLGDEL HEADING "^Destruc.^--------" &
       TAB 128 D_USPFLGMOD HEADING "^Modif.^------"
@ELSE
  REPORT XPRNOM PRINT AT XPRNOM &
       TAB 11 D_XPRDSCFRA &
       HEADING "^French Description^------------------------------------" &
       PRINT AT XPRNOM &
       TAB 58 USRCLE HEADING "User^Code^------------" &
       TAB 71 D_USREMPNOM  HEADING "^User Name^---------------------------" &
       TAB 103 D_USPFLGACS HEADING "^Access^------" &
       TAB 110 D_USPFLGENT HEADING "^Input^-----" &
       TAB 119 D_USPFLGDEL HEADING "^Destroy^-------" &
       TAB 128 D_USPFLGMOD HEADING "^Modif.^------"
@ENDIF
BUILD gs300z
