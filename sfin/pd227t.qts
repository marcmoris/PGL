;/T/ 2.5.5 Impression du formulaire de douane
;
;/P/ Programmeur..: Denis Pouliot
;    Date Création: 1995-06-05
;
;    Description..: Inscrire dans un sous fichier les informations
;                   nécessaire à cumuler pour l'impression du formulaire
;                   de douane PD227Z qui correspond à un bon de livraison.
;
;       *** NOTE : Un bon de livraison ne se fait que pour des diagrammes
;                  ou des tranches.
;
;/M/ François Déry, 6 mai 1999
;       Correction du nom des subfile miniscule/majuscule.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par...: Marc Poulin
;    Date..........: 30 mars 2000.
;    Description...: Corriger le traitement afin qu'il calcule le montant de la valeur de la marchandise lorsqu'il s'agit d'un 
;                    diagramme calculé à la pièce (avec indicateur de pièce égal à "1").
;
;    Référence.....: Demande de changement 000035.
;
;    Signet........: MP-00-03-30_D35.
;
;    --------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 27 avril 2000
;    Description......: Corriger le calcul du total de la formule de douane. Un problème d'arrondi dû à l'utilisation
;                       du type de donnée "FLOAT" causait un débalancement de quelques centièmes.
;
;    Référence........: Demande de changement 000044.
;
;    Signet...........: MP-00-04-27_D44.
;
;    --------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 01 mai 2000
;    Description......: Ajuster le traitement afin que la surface au pieds carré puisse s'imprimer au niveau du détail.
;
;    Référence........: Demande de changement 000046.
;
;    Signet...........: MP-00-05-01_D46.
;
;    --------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date modification.: 05 février 2001
;    Description.......: Afficher la description associée au type de granit. Tenir compte que la description 3 et 4 sont possibles.
;
;    Référence.........: Demande de changement 000125.
;
;    Signet............: MP-01-02-05_D125.
;
;    --------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 17 janvier 2002
;    Description......: Ajuster le traitement afin de modifier la classification des produits pour se conformer au 
;                       nouveau certificat d'origine des formules de douanes.
;
;    Référence........: Demande de changement 200204.
;
;    Signet...........: MP-02-01-17_2002046.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqts NOLIST
SET LOCK RECORD UPDATE

RUN pd227t

;-------------------------------------------------------------------------------
;
;
REQUEST CONTENU_CAISSON

ACCESS PDBON_LIVRAISON                    &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       EXPNUMEXP OF PDBON_LIVRAISON     , &
       BLINUMBLI OF PDBON_LIVRAISON       &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI OF PDLIVRE_CAISSON       &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       CAINUMCAI OF PDLIVRE_CAISSON       &
  TO   CIECLE                           , &
       CAINUMCAI OF PDCAISSON             &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       TPDTYPPRD OF PDCAISSON             &
  TO   CIECLE                           , &
       TPDTYPPRD OF PDTYPE_PRODUIT        &

  LINK CIECLE       OF PDBON_LIVRAISON  , &
       PJTABR       OF PDBON_LIVRAISON  , &
       COMNUMCOMINT OF PDBON_LIVRAISON    &
  TO   CIECLE                           , &
       PJTABR                           , &
       COMNUMCOMINT OF PDCOMMAN_INTERNE   &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       CAINUMCAI OF PDCAISSON             &
  TO   CIECLE                           , &
       CAINUMCAI OF PDEMBAL_DIAG          OPTIONAL &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       PJTABR    OF PDEMBAL_DIAG        , &
       DIANUM002 OF PDEMBAL_DIAG          &
  TO   CIECLE                           , &
       PJTABR   ,&
       DIANUM002 OF PDDIAGRAMME           OPTIONAL &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       TGRCODGRA OF PDDIAGRAMME           &
  TO   CIECLE                           , &
       TGRCODGRA OF PDTYPE_GRANIT         ALIAS A_TGR_DIA OPTIONAL &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       TYFCODFIN OF PDDIAGRAMME           &
  TO   CIECLE                           , &
       TYFCODFIN OF PDTYPE_FINI           ALIAS A_TYF_DIAFINI OPTIONAL &

  LINK CIECLE       OF PDBON_LIVRAISON  , &
       PJTABR       OF PDDIAGRAMME      , & ; Pour la description des produits
       PJTANN       OF PDDIAGRAMME      , &
       COMNUMCOMINT OF PDDIAGRAMME      , &
       DCINUMLIG    OF PDDIAGRAMME        &
  TO   CIECLE                           , &
       PJTABR                           , &
       PJTANN                           , &
       COMNUMCOMINT                     , &
       DCINUMLIG OF PDDETAIL_C_I          OPTIONAL &


  LINK CIECLE    OF PDBON_LIVRAISON     , &
       CAINUMCAI OF PDCAISSON             &
  TO   CIECLE                           , &
       CAINUMCAI OF PDEMBAL_TRANCHE       OPTIONAL &

  LINK CIECLE       OF PDBON_LIVRAISON  , &
       TRANUMTRA002 OF PDEMBAL_TRANCHE    &
  TO   CIECLE                           , &
       TRANUMTRA002 OF PDTRANCHE          OPTIONAL &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       TGRCODGRA OF PDTRANCHE             &
  TO   CIECLE                           , &
       TGRCODGRA OF PDTYPE_GRANIT         ALIAS A_TGR_TRA OPTIONAL &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       TYFCODFIN OF PDTRANCHE             &
  TO   CIECLE                           , &
       TYFCODFIN OF PDTYPE_FINI           ALIAS A_TYF_TRAFINI OPTIONAL &

  LINK CIECLE       OF PDBON_LIVRAISON  , &
       PJTABR       OF PDCAISSON        , & ; Pour la description des produits
       PJTANN       OF PDCAISSON        , &
       COMNUMCOMINT OF PDCAISSON        , &
       DCINUMLIG    OF PDEMBAL_TRANCHE    &
  TO   CIECLE                           , &
       PJTABR                           , &
       PJTANN                           , &
       COMNUMCOMINT                     , &
       DCINUMLIG OF PDDETAIL_C_I  ALIAS PDDETAIL_C_I_T  OPTIONAL


;
; Variable de tri
;

DEFINE D_TRI_BONDOU CHARACTER SIZE 12 =    ASCII(EXPNUMEXP OF PDBON_LIVRAISON,9) &
                                         + ASCII(NCONVERT(BLINUMBLI OF PDBON_LIVRAISON),3)
                              ; Permet d'effectuer le tri PAR bon de douane

DEFINE D_TRI_PIECE CHARACTER SIZE 7   =  DIANUM002 OF PDEMBAL_DIAG &
                                         IF TPDTYPPRD OF PDCAISSON = "DI" &
                                         ELSE TRANUMTRA002 OF PDEMBAL_TRANCHE

CHOOSE EXPNUMEXP PARM, BLINUMBLI PARM

SORT   ON D_TRI_BONDOU                       &
       ON CAINUMCAI    OF PDCAISSON          &
       ON D_TRI_PIECE                        &
       ON PJTABR       OF PDCOMMAN_INTERNE   &
       ON COMNUMCOMINT OF PDCOMMAN_INTERNE D


DEFINE D_DESCPRDCAI CHARACTER SIZE 10 = "GRANITE  "
                                       ; TRUNCATE(TPDDSC001 OF PDTYPE_PRODUIT)[1:10] &          ; Desccription du produit du caisson
                                       ; IF   COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                       ; ELSE TRUNCATE (TPDDSC002 OF PDTYPE_PRODUIT)[1:10]



; -----------------------------------------------------------------------------------
; MP-01-02-05_D125.
;
; Prendre la description du granit des diagrammes 1, 2, 3 ou 4 ET 
; anglais ou français, selon le détail de la commande interne.
;

define d_descgranit_dia character size 40 = truncate(tgrdscfra001 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i     = "1"     &
                                       else truncate(tgrdscfra002 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i     = "2"     &
                                       else truncate(tgrdscfra003 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i     = "3"     &
                                       else truncate(tgrdscfra004 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i     = "4"     &
                                       else truncate(tgrdscang001 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i     = "1"     &
                                       else truncate(tgrdscang002 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i     = "2"     &
                                       else truncate(tgrdscang003 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i     = "3"     &
                                       else truncate(tgrdscang004 of a_tgr_dia)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i     = "4" 

; -----------------------------------------------------------------------------------



DEFINE D_DESCFINI_DIA CHARACTER SIZE 40 = TRUNCATE(TYFDSCFRA001 OF A_TYF_DIAFINI)   &         ; Prendre la description du fini
                                            IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &       ; des diagrammes, 1 ou 2 ET anglais
                                              AND DCITYPFIN OF PDDETAIL_C_I = "1"     &       ; ou français, selon le détail de
                                            ELSE &                                            ; de la commande interne
                                              TRUNCATE (TYFDSCFRA002 OF A_TYF_DIAFINI)   &
                                              IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"  &
                                                AND DCITYPFIN OF PDDETAIL_C_I = "2"      &
                                              ELSE &
                                                TRUNCATE (TYFDSCANG001 OF A_TYF_DIAFINI)   &
                                                IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A"  &
                                                  AND DCITYPFIN OF PDDETAIL_C_I = "1"      &
                                                ELSE &
                                                  TRUNCATE (TYFDSCANG002 OF A_TYF_DIAFINI)   &
                                                  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A"  &
                                                    AND DCITYPFIN OF PDDETAIL_C_I = "2"



; -----------------------------------------------------------------------------------
; MP-01-02-05_D125.
;
; Prendre la description du granit des tranches 1, 2, 3 ou 4 ET 
; anglais ou français, selon le détail de la commande interne.
;

define d_descgranit_tra character size 40 = truncate(tgrdscfra001 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i_t   = "1"     &
                                       else truncate(tgrdscfra002 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i_t   = "2"     &
                                       else truncate(tgrdscfra003 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i_t   = "3"     &
                                       else truncate(tgrdscfra004 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "F" and &
                                            dcitypgra             of pddetail_c_i_t   = "4"     &
                                       else truncate(tgrdscang001 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i_t   = "1"     &
                                       else truncate(tgrdscang002 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i_t   = "2"     &
                                       else truncate(tgrdscang003 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i_t   = "3"     &
                                       else truncate(tgrdscang004 of a_tgr_tra)                 &
                                         if comcodlngcor          of pdcomman_interne = "A" and &
                                            dcitypgra             of pddetail_c_i_t   = "4" 

; -----------------------------------------------------------------------------------



DEFINE D_DESCFINI_TRA CHARACTER SIZE 40 = TRUNCATE(TYFDSCFRA001 OF A_TYF_TRAFINI)   &           ; Prendre la description du fini
                                            IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &         ; des tranches, 1 ou 2 ET anglais
                                              AND DCITYPFIN OF PDDETAIL_C_I_T = "1"     &         ; ou français, selon le détail de
                                            ELSE &                                              ; de la commande interne
                                              TRUNCATE (TYFDSCFRA002 OF A_TYF_TRAFINI)   &
                                              IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"  &
                                                AND DCITYPFIN OF PDDETAIL_C_I_T = "2"      &
                                              ELSE &
                                                TRUNCATE (TYFDSCANG001 OF A_TYF_TRAFINI)   &
                                                IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A"  &
                                                  AND DCITYPFIN OF PDDETAIL_C_I_T = "1"      &
                                                ELSE &
                                                  TRUNCATE (TYFDSCANG002 OF A_TYF_TRAFINI)   &
                                                  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "A"  &
                                                    AND DCITYPFIN OF PDDETAIL_C_I_T = "2"


; -------------------------------------------------------------------------------
; MP-02-01-17_2002046.

DEFINE D_DESC_PROD CHARACTER SIZE 32 =   D_DESCPRDCAI + ","               &         ; La decription du produit est
                                         + D_DESCGRANIT_DIA[1:10] + ","     &         ; l'ensemble du type de produit
                                         + D_DESCFINI_DIA[1:10]             &         ; + le type de granit
                                         IF TPDTYPPRD OF PDCAISSON = "DI"   &         ; + le type de fine
                                         ELSE  D_DESCPRDCAI  + ","          &
                                             + D_DESCGRANIT_TRA[1:18]


DEFINE D_DESCPRODUIT CHARACTER SIZE 41 = "6802.99 FINISH STONE PRODUCTS," + D_DESC_PROD[12:10]  &
                                      IF  TPDTYPPRD OF PDCAISSON = "DI"                         &
                                    ELSE "6802.93 GRANITE SLABS, "        + D_DESC_PROD[12:18]


DEFINE D_SURMC2 FLOAT SIZE 8 =   DIASURMC2 OF PDDIAGRAMME &
                               * EMDQTEPRD OF PDEMBAL_DIAG &
                               IF TPDTYPPRD OF PDCAISSON = "DI" &
                               ELSE   TRASURMC2 OF PDTRANCHE  &
                                    * EMTQTEPRD OF PDEMBAL_TRANCHE

; -------------------------------------------------------------------------------
; MP-00-05-01_D46.

DEFINE D_SURME2 FLOAT SIZE 8 = DIASURMC2 OF PDDIAGRAMME      &
                             * EMDQTEPRD OF PDEMBAL_DIAG     &
                            IF TPDTYPPRD OF PDCAISSON = "DI" &
                          ELSE TRASURMC2 OF PDTRANCHE        &
                             * EMTQTEPRD OF PDEMBAL_TRANCHE

; -------------------------------------------------------------------------------

DEFINE D_VOLMC3 FLOAT SIZE 8 = DIAVOLMC3 OF PDDIAGRAMME * EMDQTEPRD OF PDEMBAL_DIAG &   ; Volume en mètre cube
                               IF TPDTYPPRD OF PDCAISSON = "DI" &
                               ELSE TRAVOLMC3 OF PDTRANCHE * EMTQTEPRD OF PDEMBAL_TRANCHE


DEFINE D_POIDKGEST FLOAT SIZE 8  =   EMDQTEPRD OF PDEMBAL_DIAG  &
                                   * TGRPOI    OF A_TGR_DIA     &
                                   * DIAVOLMC3 OF PDDIAGRAMME   &
                                   IF TPDTYPPRD OF PDCAISSON = "DI" &
                                   ELSE (  EMTQTEPRD OF PDEMBAL_TRANCHE  &
                                         * TGRPOI    OF A_TGR_TRA        &
                                         * TRAVOLMC3 OF PDTRANCHE        ) / 10000


DEFINE D_VALMAR_UNDIA FLOAT SIZE 8 =    DIASURMC2 OF PDDIAGRAMME     &     ; Valeur marchande d'un diagramme
                                   * DCIPRIUNMPRD OF PDDETAIL_C_I &
                                   IF    DIATYPDIA OF PDDIAGRAMME = "R" &  ; Si prix régulier
                                     AND TPDTYPPRD OF PDCAISSON = "DI"  &
                                   ELSE  DIASURMC2 OF PDDIAGRAMME  / 2  &
                                       * DCIPRIUNMPRD OF PDDETAIL_C_I    &
                                       IF    DIATYPDIA OF PDDIAGRAMME = "T" &  ; Si prix pour un triangle
                                         AND TPDTYPPRD OF PDCAISSON = "DI"  &
                                       ELSE   DIAMNTPRXPIE OF PDDIAGRAMME   &
                                            IF DIAFLGPRXPIE OF PDDIAGRAMME = "1" &  ; Si prix pour la pièce
                                            ELSE 0

; ------------------------------------------------------------------------
; MP-00-03-30_D35.

DEFINE D_VALMAR_DIA FLOAT SIZE 8 = DIAMNTPRXPIE OF PDDIAGRAMME           &
                                 * EMDQTEPRD    OF PDEMBAL_DIAG          &
                                IF DIAFLGPRXPIE OF PDDIAGRAMME = "1"     & ; Si prix pour la pièce
                              ELSE DIASURMC2    OF PDDIAGRAMME           & ; Valeur marchande des diagrammes
                                 * DCIPRIUNMPRD OF PDDETAIL_C_I          &
                                 * EMDQTEPRD    OF PDEMBAL_DIAG          &
                                IF DIATYPDIA    OF PDDIAGRAMME = "R" AND & ; Si prix régulier
                                   TPDTYPPRD    OF PDCAISSON   = "DI"    &
                              ELSE DIASURMC2    OF PDDIAGRAMME  / 2      &
                                 * DCIPRIUNMPRD OF PDDETAIL_C_I          &
                                 * EMDQTEPRD    OF PDEMBAL_DIAG          &
                                IF DIATYPDIA    OF PDDIAGRAMME = "T" AND & ; Si prix pour des triangles
                                   TPDTYPPRD    OF PDCAISSON   = "DI"    &
                              ELSE 0

; ------------------------------------------------------------------------

DEFINE D_VALMAR_TRA FLOAT SIZE 8 =   EMTSURMC2VEN OF PDEMBAL_TRANCHE &  ;TRASURMC2 OF PDTRANCHE       &         ; Valeur marchande d'une tranche
                                   * DCIPRIUNMPRD OF PDDETAIL_C_I_T &
                                   IF TPDTYPPRD OF PDCAISSON = "TR" &
                                   ELSE 0

;
; Variable qui permet de cumuler les informations par produit
;

TEMPORARY T_COMNUMINT      CHARACTER SIZE 3
TEMPORARY T_QTEPRD         INTEGER   SIZE 4
TEMPORARY T_TOTQTEPRD      INTEGER   SIZE 4
TEMPORARY T_TGRCLADOU      CHARACTER SIZE 15
TEMPORARY T_EPAIS          INTEGER   SIZE 4
TEMPORARY T_TOTPOIDKGEST   FLOAT     SIZE 8
TEMPORARY T_POIDKGREE      FLOAT     SIZE 8
TEMPORARY T_TOTVALMAR      FLOAT     SIZE 8

  ITEM T_POIDKGREE    = 0
  ITEM T_TOTVALMAR    = 0
  ITEM T_TOTPOIDKGEST = 0

  ITEM T_COMNUMINT = COMNUMCOMINT OF PDCOMMAN_INTERNE &
                     IF T_COMNUMINT = "" &
                        OR &
                        T_COMNUMINT < COMNUMCOMINT OF PDCOMMAN_INTERNE &
                     AT PJTABR OF PDCOMMAN_INTERNE RESET AT D_TRI_BONDOU


  ITEM T_TGRCLADOU = TGRCLADOU OF A_TGR_DIA           &  ; Code de douane du produit
                     IF TPDTYPPRD OF PDCAISSON = "DI" &
                     ELSE TGRCLADOU OF A_TGR_TRA      &
                     AT D_TRI_PIECE

  ITEM T_EPAIS     = DIAEPA OF PDDIAGRAMME             &
                     IF TPDTYPPRD OF PDCAISSON = "DI"  &
                     ELSE TRAEPA OF PDTRANCHE          &
                     AT D_TRI_PIECE

  ITEM T_QTEPRD    = EMDQTEPRD OF PDEMBAL_DIAG &
                     IF TPDTYPPRD OF PDCAISSON = "DI"   &
                     ELSE EMTQTEPRD OF PDEMBAL_TRANCHE  &
                     AT D_TRI_PIECE

  SUBFILE WPD227_BONLIVRDOU KEEP AT D_TRI_PIECE &
    INCLUDE CIECLE    OF PDBON_LIVRAISON , &
            EXPNUMEXP OF PDBON_LIVRAISON , &
            BLINUMBLI OF PDBON_LIVRAISON , &
            D_TRI_BONDOU                 , &
            BLIQTECAI OF PDBON_LIVRAISON , &
            T_COMNUMINT                  , &
            CAINUMCAI OF PDCAISSON       , &
            TPDTYPPRD OF PDCAISSON       , &
            D_TRI_PIECE                  , &
            T_TGRCLADOU                  , &
            D_DESCPRODUIT                , &
            T_EPAIS                      , &
            D_POIDKGEST                  , &
            T_QTEPRD                     , &
            T_TOTQTEPRD                  , &
            D_SURMC2                     , &
            D_SURME2                     , & ; MP-00-05-01_D46.
            D_VOLMC3                     , &
            T_TOTPOIDKGEST               , &
            T_POIDKGREE                  , &
            D_VALMAR_UNDIA               , &
            D_VALMAR_DIA                 , &
            D_VALMAR_TRA                 , &
            T_TOTVALMAR

;------------------------------------------------------------------------------
;
;
REQUEST CUMUL_ITEM1

ACCESS *WPD227_BONLIVRDOU                 &

  LINK EXPNUMEXP OF WPD227_BONLIVRDOU   , &
       BLINUMBLI OF WPD227_BONLIVRDOU     &
  TO   EXPNUMEXP                        , &
       BLINUMBLI OF PDLIVRE_CAISSON       &

  LINK CAINUMCAI OF WPD227_BONLIVRDOU     &
  TO   CAINUMCAI OF PDCAISSON


SORT   ON D_TRI_BONDOU OF WPD227_BONLIVRDOU  &
       ON CAINUMCAI    OF WPD227_BONLIVRDOU  &
       ON D_TRI_PIECE  OF WPD227_BONLIVRDOU



;
; Variable qui permet de cumuler les informations sur le contenu
; des caissons par bon de douane.
;
; Contient les quantités par bon de douane
;
TEMPORARY T_TOT_QTEPRD    INTEGER SIZE 4
TEMPORARY T_TOT_SURMC2    FLOAT   SIZE 8
TEMPORARY T_TOT_VOLMC3    FLOAT   SIZE 8
TEMPORARY T_TOT_POIDKGEST FLOAT   SIZE 8
  ;
  ; Faire les sommes par bon de douane
  ;

  ITEM T_TOT_QTEPRD SUBTOTAL T_QTEPRD OF WPD227_BONLIVRDOU &
                    AT D_TRI_PIECE                         &
                    RESET AT D_TRI_BONDOU


  ITEM T_TOT_SURMC2  SUBTOTAL D_SURMC2 OF WPD227_BONLIVRDOU &
                     AT D_TRI_PIECE OF WPD227_BONLIVRDOU &
                     RESET AT D_TRI_BONDOU

  ITEM T_TOT_VOLMC3  SUBTOTAL D_VOLMC3 OF WPD227_BONLIVRDOU &
                     AT D_TRI_PIECE OF WPD227_BONLIVRDOU &
                     RESET AT D_TRI_BONDOU

  ITEM T_TOT_POIDKGEST  SUBTOTAL D_POIDKGEST &
                        AT D_TRI_PIECE OF WPD227_BONLIVRDOU &
                        RESET AT D_TRI_BONDOU


OUTPUT WPD227_BONLIVRDOU UPDATE AT D_TRI_PIECE
  ITEM  T_TOTQTEPRD      OF WPD227_BONLIVRDOU  FINAL T_TOT_QTEPRD
  ITEM  D_SURMC2         OF WPD227_BONLIVRDOU  FINAL T_TOT_SURMC2
  ITEM  D_VOLMC3         OF WPD227_BONLIVRDOU  FINAL T_TOT_VOLMC3
  ITEM  T_TOTPOIDKGEST   OF WPD227_BONLIVRDOU  FINAL T_TOT_POIDKGEST


;-------------------------------------------------------------------------------
;
;

REQUEST CUMUL_ITEM2

ACCESS *WPD227_BONLIVRDOU                 &

  LINK EXPNUMEXP OF WPD227_BONLIVRDOU   , &
       BLINUMBLI OF WPD227_BONLIVRDOU     &
  TO   EXPNUMEXP                        , &
       BLINUMBLI OF PDLIVRE_CAISSON       &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       CAINUMCAI OF WPD227_BONLIVRDOU     &
  TO   CIECLE , &
       CAINUMCAI OF PDCAISSON


SORT   ON D_TRI_BONDOU OF WPD227_BONLIVRDOU  &
       ON CAINUMCAI    OF PDCAISSON          &
       ON D_TRI_PIECE  OF WPD227_BONLIVRDOU



DEFINE D_POIDKGCAI     FLOAT   SIZE 8 = CAIPOIREE OF PDCAISSON         &
                                        IF CAIPOIREE OF PDCAISSON <> 0 &
                                        ELSE CAIPOIEST OF PDCAISSON

DEFINE D_VALMAR FLOAT SIZE 8 = D_VALMAR_DIA OF WPD227_BONLIVRDOU &
                               IF TPDTYPPRD OF PDCAISSON = "DI" &
                               ELSE D_VALMAR_TRA OF WPD227_BONLIVRDOU

TEMPORARY T_TOT_POIDKGREE FLOAT   SIZE 8
TEMPORARY T_TOT_VALMAR    FLOAT   SIZE 8


  ITEM T_TOT_POIDKGREE  SUBTOTAL D_POIDKGCAI &
                        AT CAINUMCAI OF PDCAISSON &
                        RESET AT D_TRI_BONDOU

  ITEM T_TOT_VALMAR     SUBTOTAL D_VALMAR  &
                        AT D_TRI_PIECE     &
                        RESET AT D_TRI_BONDOU

OUTPUT WPD227_BONLIVRDOU UPDATE AT D_TRI_BONDOU
  ITEM  T_POIDKGREE   OF WPD227_BONLIVRDOU  FINAL T_TOT_POIDKGREE
  ITEM  T_TOTVALMAR   OF WPD227_BONLIVRDOU  FINAL T_TOT_VALMAR


;-------------------------------------------------------------------------------
;
;
REQUEST BON_DOUANEFIN
;
;  Ce request sert pour obtenir, au premier enregistrement,
;   les informations cumulées.
;
ACCESS *WPD227_BONLIVRDOU

SORT  ON D_TRI_BONDOU OF WPD227_BONLIVRDOU &
      ON T_TOTQTEPRD  OF WPD227_BONLIVRDOU D


TEMPORARY T_QTEPRDTOT  INTEGER SIZE 4
TEMPORARY T_SURMC2     FLOAT   SIZE 8
TEMPORARY T_VOLMC3     FLOAT   SIZE 8
TEMPORARY T_POIDESTKG  FLOAT   SIZE 8
TEMPORARY T_POIDREELKG FLOAT   SIZE 8
TEMPORARY T_VALMARTOT  FLOAT   SIZE 8

  ITEM T_QTEPRDTOT  = T_TOTQTEPRD    OF WPD227_BONLIVRDOU &
                      IF T_QTEPRDTOT = 0                  &
                      RESET AT D_TRI_BONDOU

  ITEM T_SURMC2     = D_SURMC2       OF WPD227_BONLIVRDOU &
                      IF T_SURMC2 = 0                     &
                      RESET AT D_TRI_BONDOU

  ITEM T_VOLMC3     = D_VOLMC3       OF WPD227_BONLIVRDOU &
                      IF T_VOLMC3 = 0                     &
                      RESET AT D_TRI_BONDOU

  ITEM T_POIDESTKG  = T_TOTPOIDKGEST OF WPD227_BONLIVRDOU &
                      IF T_POIDESTKG = 0                  &
                      RESET AT D_TRI_BONDOU

  ITEM T_POIDREELKG = T_POIDKGREE    OF WPD227_BONLIVRDOU &
                      IF T_POIDREELKG = 0                 &
                      RESET AT D_TRI_BONDOU

  ITEM T_VALMARTOT  = T_TOTVALMAR    OF WPD227_BONLIVRDOU &
                      IF T_VALMARTOT = 0                  &
                      RESET AT D_TRI_BONDOU


OUTPUT WPD227_BONLIVRDOU UPDATE
  ITEM  T_TOTQTEPRD    OF WPD227_BONLIVRDOU  FINAL T_QTEPRDTOT
  ITEM  D_SURMC2       OF WPD227_BONLIVRDOU  FINAL T_SURMC2
  ITEM  D_VOLMC3       OF WPD227_BONLIVRDOU  FINAL T_VOLMC3
  ITEM  T_TOTPOIDKGEST OF WPD227_BONLIVRDOU  FINAL T_POIDESTKG
  ITEM  T_POIDKGREE    OF WPD227_BONLIVRDOU  FINAL T_POIDREELKG
  ITEM  T_TOTVALMAR    OF WPD227_BONLIVRDOU  FINAL T_VALMARTOT


;-------------------------------------------------------------------------------
;
;MP-00-04-27_D44.
;
REQUEST BON_CUMUL_EXACT_ARRONDI_1
;
ACCESS *WPD227_BONLIVRDOU                 &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       EXPNUMEXP OF WPD227_BONLIVRDOU     & ; Information sur l'expédition
  TO   CIECLE                           , &
       EXPNUMEXP OF PDEXPEDITION          &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       EXPNUMEXP OF WPD227_BONLIVRDOU   , &
       BLINUMBLI OF WPD227_BONLIVRDOU     &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI OF PDBON_LIVRAISON       &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       PJTABR    OF PDBON_LIVRAISON       &  ; Information sur le projet
  TO   CIECLE                           , &
       PJTABR    OF PDPROJET              &

  LINK CIECLE       OF PDBON_LIVRAISON  , &
       PJTABR       OF PDBON_LIVRAISON  , & ; Information sur la commande interne
       T_COMNUMINT  OF WPD227_BONLIVRDOU  &
  TO   CIECLE                           , &
       PJTABR                           , &
       COMNUMCOMINT OF PDCOMMAN_INTERNE   &

  LINK "----", &
       CLICLE       OF PDBON_LIVRAISON &
   TO  CLICIECLE, &
       CLICLE OF CRCLIENT &

  LINK "----",                            & 
       CLICLE       OF PDCOMMAN_INTERNE , &
       CIECLE       OF PDCOMMAN_INTERNE   &
  TO   CLICIECLE                        , &
       CLICLE                           , &
       CIECLE       OF VCLC_CLI           &

  LINK CLICLE      OF PDBON_LIVRAISON   , & ; Adresse de livraison
       "C"                              , &
       COMNUMADRLIV OF PDBON_LIVRAISON    &
  TO   REFCLENUM                        , &
       REFCLETYP                        , &
       RADCLE       OF MCREF_ADRESSE      &

  LINK CLICLE      OF PDCOMMAN_INTERNE  , & ; Adresse de facturation
       "C"                              , &
       COMNUMADRFAC OF PDCOMMAN_INTERNE   &
  TO   REFCLENUM                        , &
       REFCLETYP                        , &
       RADCLE       OF MCREF_ADRESSE      ALIAS A_FAC_ADRFAC OPTIONAL &

  LINK FOUCLE OF PDEXPEDITION             &
  TO   FOUCLE OF CPFOURNISSEUR            &

  LINK DEVCLE OF VCLC_CLI                 &  ; Information sur les devises
  TO   DEVCLE OF MCDEVISE                 &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       EXPNUMEXP OF WPD227_BONLIVRDOU   , &  ; Information sur les pièces
       BLINUMBLI OF WPD227_BONLIVRDOU   , &
       CAINUMCAI OF WPD227_BONLIVRDOU     &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI                        , &
       CAINUMCAI OF PDLIVRE_CAISSON       &

  LINK "2000"                             &
  TO   CIECLE OF MCCOMPAGNIE


SORT ON CIECLE        OF WPD227_BONLIVRDOU &
     ON D_TRI_BONDOU  OF WPD227_BONLIVRDOU &
     ON D_DESCPRODUIT OF WPD227_BONLIVRDOU &
     ON T_EPAIS       OF WPD227_BONLIVRDOU &
     ON T_TGRCLADOU   OF WPD227_BONLIVRDOU

;
; Variables de travail
;
TEMPORARY T_TRI_BONDOU   CHARACTER SIZE 12
TEMPORARY T_DESCPRODUIT  CHARACTER SIZE 32

TEMPORARY T_VALMAR_CUM   FLOAT     SIZE 08 
TEMPORARY T_VALMAR_SOM   FLOAT     SIZE 08 
TEMPORARY T_VALMAR_SUB   FLOAT     SIZE 08 

TEMPORARY T_SURP2        FLOAT     SIZE 08 ; MP-00-05-01_D46.
TEMPORARY T_SURFAC       FLOAT     SIZE 08 ; MP-00-05-01_D46.
TEMPORARY T_SURFAC_SUB   FLOAT     SIZE 08 ; MP-00-05-01_D46.

ITEM T_TRI_BONDOU  = D_TRI_BONDOU   OF WPD227_BONLIVRDOU
ITEM T_DESCPRODUIT = D_DESCPRODUIT  OF WPD227_BONLIVRDOU

ITEM T_VALMAR_CUM = D_VALMAR_DIA OF WPD227_BONLIVRDOU        &
                 IF TPDTYPPRD    OF WPD227_BONLIVRDOU = "DI" &
               ELSE D_VALMAR_TRA OF WPD227_BONLIVRDOU

ITEM T_VALMAR_SOM SUBTOTAL T_VALMAR_CUM RESET AT D_DESCPRODUIT

ITEM T_VALMAR_SUB = ROUND(T_VALMAR_SOM,0)

; ---------------------------------------------------------------------
; MP-00-05-01_D46.

ITEM T_SURP2      = D_SURME2 OF WPD227_BONLIVRDOU * 10.764262

ITEM T_SURFAC     = D_SURME2 OF WPD227_BONLIVRDOU           &
                 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"  &
               ELSE T_SURP2

ITEM T_SURFAC_SUB SUBTOTAL T_SURFAC RESET AT D_DESCPRODUIT

; ---------------------------------------------------------------------

SUBFILE WPD227_BONLIVRIDX KEEP AT D_DESCPRODUIT                       &
                          INCLUDE CIECLE        OF WPD227_BONLIVRDOU, &
                                  T_TRI_BONDOU                      , &
                                  T_DESCPRODUIT                     , &
                                  T_VALMAR_SUB                      , &
                                  T_SURFAC_SUB                        & ; MP-00-05-01_D46.
                            INDEX WPD227_IDX                          &
                          SEGMENT CIECLE                            , &
                                  T_TRI_BONDOU                      , &
                                  T_DESCPRODUIT 

;-------------------------------------------------------------------------------
;
;MP-00-04-27_D44.
;
REQUEST BON_CUMUL_EXACT_ARRONDI_2
;
ACCESS *WPD227_BONLIVRDOU                 &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       EXPNUMEXP OF WPD227_BONLIVRDOU     & ; Information sur l'expédition
  TO   CIECLE                           , &
       EXPNUMEXP OF PDEXPEDITION          &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       EXPNUMEXP OF WPD227_BONLIVRDOU   , &
       BLINUMBLI OF WPD227_BONLIVRDOU     &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI OF PDBON_LIVRAISON       &

  LINK CIECLE    OF PDBON_LIVRAISON     , &
       PJTABR    OF PDBON_LIVRAISON       &  ; Information sur le projet
  TO   CIECLE                           , &
       PJTABR    OF PDPROJET              &

  LINK CIECLE    OF PDBON_LIVRAISON     , &  ; Info commande en remplacement
       COMNUMCOM OF PDBON_LIVRAISON       &
    TO CIECLE                           , &
       COMNUMCOM OF PDCOMMAN_INTERNE      &

  LINK "----"                           , &
       CLICLE    OF PDBON_LIVRAISON       &
   TO  CLICIECLE                        , &
       CLICLE    OF CRCLIENT              &

  LINK "----"                           , & 
       CLICLE    OF PDBON_LIVRAISON     , &
       CIECLE    OF PDCOMMAN_INTERNE      &
  TO   CLICIECLE                        , &
       CLICLE                           , &
       CIECLE    OF VCLC_CLI              &

  LINK CLICLE       OF PDBON_LIVRAISON  , & ; Adresse de livraison
       "C"                              , &
       COMNUMADRLIV OF PDBON_LIVRAISON    &
  TO   REFCLENUM                        , &
       REFCLETYP                        , &
       RADCLE       OF MCREF_ADRESSE      &

  LINK CLICLE       OF PDCOMMAN_INTERNE , & ; Adresse de facturation
       "C"                              , &
       COMNUMADRFAC OF PDCOMMAN_INTERNE   &
  TO   REFCLENUM                        , &
       REFCLETYP                        , &
       RADCLE       OF MCREF_ADRESSE      ALIAS A_FAC_ADRFAC OPTIONAL &

  LINK FOUCLE OF PDEXPEDITION             &
  TO   FOUCLE OF CPFOURNISSEUR            &

  LINK DEVCLE OF VCLC_CLI                 &  ; Information sur les devises
  TO   DEVCLE OF MCDEVISE                 &

  LINK CIECLE    OF WPD227_BONLIVRDOU   , &
       EXPNUMEXP OF WPD227_BONLIVRDOU   , &  ; Information sur les pièces
       BLINUMBLI OF WPD227_BONLIVRDOU   , &
       CAINUMCAI OF WPD227_BONLIVRDOU     &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI                        , &
       CAINUMCAI OF PDLIVRE_CAISSON       &

  LINK "2000"                             &
  TO   CIECLE OF MCCOMPAGNIE

SORT ON CIECLE        OF WPD227_BONLIVRDOU &
     ON D_TRI_BONDOU  OF WPD227_BONLIVRDOU &
     ON T_TGRCLADOU   OF WPD227_BONLIVRDOU &
     ON D_DESCPRODUIT OF WPD227_BONLIVRDOU &
     ON T_EPAIS       OF WPD227_BONLIVRDOU

;
; Variables de travail
;
TEMPORARY T_TRI_BONDOU   CHARACTER SIZE 12
TEMPORARY T_TGRCLADOUX   CHARACTER SIZE 15
TEMPORARY T_DESCPRODUIT  CHARACTER SIZE 32
TEMPORARY T_EPAISSEUR    INTEGER   SIZE 04

TEMPORARY T_VALMAR_CUM   FLOAT     SIZE 08 
TEMPORARY T_VALMAR_SOM   FLOAT     SIZE 08 
TEMPORARY T_VALMAR_SUB   FLOAT     SIZE 08 

TEMPORARY T_SURP2        FLOAT     SIZE 08 ; MP-00-05-01_D46.
TEMPORARY T_SURFAC       FLOAT     SIZE 08 ; MP-00-05-01_D46.
TEMPORARY T_SURFAC_SUB   FLOAT     SIZE 08 ; MP-00-05-01_D46.

ITEM T_TRI_BONDOU  = D_TRI_BONDOU   OF WPD227_BONLIVRDOU
ITEM T_TGRCLADOUX  = T_TGRCLADOU    OF WPD227_BONLIVRDOU
ITEM T_DESCPRODUIT = D_DESCPRODUIT  OF WPD227_BONLIVRDOU
ITEM T_EPAISSEUR   = T_EPAIS        OF WPD227_BONLIVRDOU

ITEM T_VALMAR_CUM = D_VALMAR_DIA OF WPD227_BONLIVRDOU        &
                 IF TPDTYPPRD    OF WPD227_BONLIVRDOU = "DI" &
               ELSE D_VALMAR_TRA OF WPD227_BONLIVRDOU

ITEM T_VALMAR_SOM SUBTOTAL T_VALMAR_CUM RESET AT T_EPAIS

ITEM T_VALMAR_SUB = ROUND(T_VALMAR_SOM,0)

; -----------------------------------------------------------------------
; MP-00-05-01_D46.

ITEM T_SURP2      = D_SURME2 OF WPD227_BONLIVRDOU * 10.764262

ITEM T_SURFAC     = D_SURME2 OF WPD227_BONLIVRDOU           &
                 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"  &
               ELSE T_SURP2

ITEM T_SURFAC_SUB SUBTOTAL T_SURFAC RESET AT T_EPAIS

; ----------------------------------------------------------------------

SUBFILE WPD227_BONLIVRIDX2 KEEP AT T_EPAIS                             &
                           INCLUDE CIECLE        OF WPD227_BONLIVRDOU, &
                                   T_TRI_BONDOU                      , &
                                   T_TGRCLADOUX                      , &
                                   T_DESCPRODUIT                     , &
                                   T_EPAISSEUR                       , &
                                   T_VALMAR_SUB                      , &
                                   T_SURFAC_SUB                        & ; MP-00-05-01_D46.
                             INDEX WPD227_IDX2                         &
                           SEGMENT CIECLE                            , &
                                   T_TRI_BONDOU                      , &
                                   T_TGRCLADOUX                      , &
                                   T_DESCPRODUIT                     , &
                                   T_EPAISSEUR                       , &
                                   T_VALMAR_SUB                      


BUILD
