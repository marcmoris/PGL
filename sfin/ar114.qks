;/T/ Consultation des soumissions CIECLE, FOUCIECLE, FOUCLE, REQCLE
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 7 Septembre 1994
;
;    Description..: Permet de visionner et de choisir une soumission.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar114 NOMODE NOACTION FIELDMARK &
             FROM 11,7 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE   , &
                        T_FOUCLE   , &
                        T_REQCLE 

USE ustrasse NOLIST

USE ar114.hlp NOLIST

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Compagnie de la réquisition.
TEMPORARY T_FOUCLE    CHARACTER SIZE 6 ; Fournisseur de la réquisition.
TEMPORARY T_REQCLE    CHARACTER SIZE 9 ; Numéro de réquisition.

;-------------------------------------------------------------------------------
;
;
;
FILE ARREQ_FOU       PRIMARY &
                      OCCURS 10
  ACCESS  VIA   CIECLE, &
                REQCLE  &
         USING  T_CIECLE, &
                T_REQCLE  &
        ORDERBY CIECLE , &
                REQCLE , &
                FOUCLE

;-------------------------------------------------------------------------------
;
;
;
FILE MCREF_ADRESSE     REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
         USING  FOUCIECLE OF ARREQ_FOU, &
                "F"                     , &
                FOUCLE    OF ARREQ_FOU, &
                RADCLE    OF ARREQ_FOU

;-------------------------------------------------------------------------------
;
; Permet d'avoir l'information du fournisseur pour la cie de transaction.
;
FILE VFOC_FOU            REFERENCE

  ACCESS VIA   FOUCIECLE, &
               FOUCLE   , &
               FOCCIECLE  &
         USING FOUCIECLE OF ARREQ_FOU, &
               FOUCLE    OF ARREQ_FOU, &
               T_CIECLE


;-------------------------------------------------------------------------------
;
; Pour la présentation de la devise ou non.
;
FILE MCHOLDING           REFERENCE &
                        NOITEM
  ACCESS VIA   CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;

DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING &
          ELSE "/" + DEVCLE OF VFOC_FOU


USE ushilite NOLIST     
DRAW 01,01 TO 13,74

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE "    Soum.            Fournisseur                       Montant net" AT ,02

SKIP 

ALIGN(3,3,6)(,,16)(,,23)(,,54)(,,69)

CLUSTER OCCURS WITH ARREQ_FOU

FIELD REQCLE    OF ARREQ_FOU   HIDDEN LABEL ">>"
FIELD FOUCLE    OF ARREQ_FOU   
FIELD RADNOM    OF MCREF_ADRESSE SIZE 30
FIELD RQFMNTNET OF ARREQ_FOU
FIELD D_DEVCLE

CLUSTER

;-------------------------------------------------------------------------------
;
; Enlève le @ si l'usager fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_REQCLE, "@")
  THEN LET T_REQCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_FOUCLE = FOUCLE OF ARREQ_FOU
  LET T_REQCLE = REQCLE OF ARREQ_FOU
  RETURN
END

BUILD
@IF FORMAT
                ****************************************************************
                *    Soum.            Fournisseur             Montant net      *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                ****************************************************************
@ENDIF
