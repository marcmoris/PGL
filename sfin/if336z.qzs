;/P/ Coût/Profit par carrière
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;

USE ussetqzs NOLIST

ACCESS IFCARRIERE_CUMUL

CHOOSE CIECLE PARM, CRRCLENUM PARM, CRRCLEANN PARM

DEFINE TMP-DIRECT INTEGER SIZE 4 = CACDIRECT
DEFINE TMP-VENTEM3 INTEGER SIZE 4 = CACQTEFAC - CACQTERECL
DEFINE TMP-VENTEDO INTEGER SIZE 4 = CACVENTEFOBCN + CACVENTEFOBUS &
                                   - CACRECLFOBCN - CACRECLFOBUS
DEFINE TMP-TOT-VENTEDO INTEGER SIZE 4 = TMP-VENTEDO
DEFINE TMP-VENTECN INTEGER SIZE 4 = CACVENTEFOBCN - CACRECLFOBCN
DEFINE TMP-VENTEUS INTEGER SIZE 4 = CACVENTEFOBUS - CACRECLFOBUS
DEFINE TMP-COMPTEUR INTEGER SIZE 2 = 1
DEFINE TMP-ARRON-CO INTEGER SIZE 4 = ROUND(CACDIRECT / 100)
DEFINE TMP-ARRON-DO INTEGER SIZE 4 = ROUND(TMP-VENTEDO / 100)

;MARC-A
DEFINE TMP-M3-PROD-TOTAL INTEGER SIZE 4 = CACQTEPROD
DEFINE TMP-HRES-TOTAL INTEGER SIZE 4 = CACHRESMO
DEFINE TMP-M3-VENDUS-TOTAL INTEGER SIZE 4 = TMP-VENTEM3
DEFINE TMP-M3-BLOC-TOTAL INTEGER SIZE 4 = CACNBBLOCPROD
DEFINE TMP-1-ACTIF INTEGER SIZE 2 = 1 IF CACQTEPROD <> 0 ELSE 0
DEFINE TMP-2-ACTIF INTEGER SIZE 2 = 1 IF TMP-ARRON-CO <> 0 ELSE 0
DEFINE TMP-4-ACTIF INTEGER SIZE 2 = 1 IF CACHRESMO <> 0 ELSE 0
DEFINE TMP-6-ACTIF INTEGER SIZE 2 = 1 IF TMP-ARRON-DO <> 0 ELSE 0
DEFINE TMP-7-ACTIF INTEGER SIZE 2 = 1 IF TMP-VENTEM3 <> 0 ELSE 0
DEFINE TMP-10-ACTIF INTEGER SIZE 2 = 1 IF CACNBBLOCPROD <> 0 ELSE 0
;FIN MARC-A

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE &
         IF PECCLE[1:1] < "8" &
         ELSE "0" + PECCLE

SORT ON CRRCLENUM &
     ON CRRCLEANN &
     ON D_PECCLE_SIE

SET SUBFILE NAME WIF336A
REPORT SUMMARY &
        CIECLE &
        CRRCLENUM &
        CRRCLEANN &
        PECCLE     &
        CACQTEPROD &
        TMP-ARRON-CO &
        CACDIRECT &
        TMP-DIRECT SUB RESET AT CRRCLEANN &
        CACHRESMO  &
        TMP-ARRON-DO &
        TMP-VENTEDO &
        TMP-TOT-VENTEDO SUB RESET AT CRRCLEANN &
        TMP-VENTEM3 &
        CACNBBLOCPROD &
;MARC-A
        TMP-VENTECN SUB RESET AT CRRCLEANN     &
        TMP-VENTEUS SUB RESET AT CRRCLEANN      &
        TMP-M3-PROD-TOTAL SUB RESET AT CRRCLEANN     &
        TMP-HRES-TOTAL SUB RESET AT CRRCLEANN       &
        TMP-M3-VENDUS-TOTAL SUB RESET AT CRRCLEANN  &
        TMP-M3-BLOC-TOTAL SUB RESET AT CRRCLEANN    &
        TMP-1-ACTIF SUB RESET AT CRRCLEANN      &
        TMP-2-ACTIF SUB RESET AT CRRCLEANN      &
        TMP-4-ACTIF SUB RESET AT CRRCLEANN      &
        TMP-6-ACTIF SUB RESET AT CRRCLEANN      &
        TMP-7-ACTIF SUB RESET AT CRRCLEANN      &
        TMP-10-ACTIF SUB RESET AT CRRCLEANN
;FIN MARC-A

BUILD if336za
;

ACCESS *WIF336A &
 LINK CIECLE OF WIF336A, &
      CRRCLENUM OF WIF336A, &
      CRRCLEANN OF WIF336A  &
   TO CIECLE, &
      CRRCLENUM, &
      CRRCLEANN                         OF IFCARRIERE

DEFINE TMP-CO-M3 INTEGER SIZE 4 = ROUND(CACDIRECT / CACQTEPROD * 100)
DEFINE TMP-M3-HR INTEGER SIZE 4 = ROUND(CACQTEPROD / CACHRESMO * 1000)
DEFINE TMP-DO-M3 INTEGER SIZE 4 = ROUND(TMP-VENTEDO / TMP-VENTEM3 * 100)
DEFINE TMP-DO-HR INTEGER SIZE 4 = ROUND(TMP-VENTEDO / CACHRESMO * 10)
DEFINE TMP-M3-BL INTEGER SIZE 4 = ROUND(CACQTEPROD / CACNBBLOCPROD)
DEFINE TMP-PROFI INTEGER SIZE 4 = TMP-DO-M3 - TMP-CO-M3
DEFINE TMP-CO-AJ INTEGER SIZE 4 = TMP-DIRECT / 100

DEFINE TMP-VE-AJ INTEGER SIZE 4 = TMP-TOT-VENTEDO / 100

;MARC-A
DEFINE TMP-1 INTEGER SIZE 4 = ROUND(TMP-M3-PROD-TOTAL/TMP-1-ACTIF)
DEFINE TMP-2 INTEGER SIZE 4 = ROUND(TMP-CO-AJ/TMP-2-ACTIF)
DEFINE TMP-3 INTEGER SIZE 4 = ROUND((TMP-CO-AJ/TMP-M3-PROD-TOTAL)*10000)
DEFINE TMP-4 INTEGER SIZE 4 = ROUND((TMP-HRES-TOTAL * 10)/TMP-4-ACTIF)
DEFINE TMP-5 INTEGER SIZE 4 = ROUND((TMP-M3-PROD-TOTAL/TMP-HRES-TOTAL)*1000)
DEFINE TMP-6 INTEGER SIZE 4 = ROUND(TMP-VE-AJ/TMP-6-ACTIF)
DEFINE TMP-7 INTEGER SIZE 4 = ROUND(TMP-M3-VENDUS-TOTAL/TMP-7-ACTIF)
DEFINE TMP-8 INTEGER SIZE 4 = ROUND((TMP-VE-AJ/TMP-M3-VENDUS-TOTAL)*10000)
DEFINE TMP-9 INTEGER SIZE 4 = ROUND((TMP-VE-AJ/TMP-HRES-TOTAL)*1000)
DEFINE TMP-10 INTEGER SIZE 4 = ROUND(TMP-M3-BLOC-TOTAL/TMP-10-ACTIF)
DEFINE TMP-12 INTEGER SIZE 4 = ROUND(((TMP-VE-AJ - TMP-CO-AJ)/TMP-M3-PROD-TOTAL)*10000)
;FIN MARC-A

SORTED ON CRRCLENUM &
       ON CRRCLEANN

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF336"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2

HEADING AT CRRCLEANN &
        TAB 3 "CARRIERE: " &
        TAB 15 CRRCLENUM &
        TAB 17 CRRCLEANN &
        TAB 20 CRRDSC &
SKIP &
        TAB 40 "COUTS/PROFITS POUR LA PIERRE BRUTE" &

SKIP 1 &
        TAB 2 "MOIS" &
        TAB 11 "M3"   &
        TAB 19 "COUT" &
        TAB 28 "COUT" &
        TAB 37 "NB"   &
        TAB 43 "M3"   &
        TAB 53 "VENTES" &
        TAB 64 "M3"     &
        TAB 76 "$ /"      &
        TAB 82 "$ /"      &
        TAB 90 "NB"   &
        TAB 97 "M3"  &
        TAB 103 "PROFIT" &

SKIP 1 &
        TAB 10 "PROD"   &
        TAB 19 "DIR." &
        TAB 28 "M3" &
        TAB 37 "HRES"   &
        TAB 43 "HRES"   &
        TAB 62 "VENDUS"     &
        TAB 76 "M3"      &
        TAB 82 "HRES"      &
        TAB 90 "BLOC"   &
        TAB 97 "BLOC"  &
        TAB 103 "$/M3" &

SKIP 1 &
        TAB 2 "_____" &
        TAB 10 "_______"   &
        TAB 19 "_______" &
        TAB 28 "______" &
        TAB 36 "______"   &
        TAB 43 "______"   &
        TAB 53 "_______" &
        TAB 62 "________"   &
        TAB 74 "______"      &
        TAB 81 "_______"      &
        TAB 91 "___"   &
        TAB 96 "_____"  &
        TAB 103 "________"

REPORT &
        PECCLE PIC "^^^^" &
        CACQTEPROD PIC "^^^^^.^^" &
        TMP-ARRON-CO  PIC "^^^^^^"  &
        TMP-CO-M3  PIC "^^^^.^^"  &
        CACHRESMO  PIC "^^^^.^" &
        TMP-M3-HR    PIC "^^.^^" &
        TMP-ARRON-DO PIC "^^^^^^^^^" &
        TMP-VENTEM3 PIC "^^^^^.^^" &
        TMP-DO-M3    PIC "^^^^^.^^" &
        TMP-DO-HR    PIC "^^^.^^" &
        CACNBBLOCPROD PIC "^^^^" &
        TMP-M3-BL    PIC "^^.^^" &
        TMP-PROFI    PIC "^^^^^.^^"

FOOTING AT CRRCLEANN &
SKIP 1 &
        TAB 10 "_______"   &
        TAB 19 "_______" &
        TAB 38 "____"   &
        TAB 54 "______" &
        TAB 64 "______"   &
        TAB 92 "__"   &

SKIP 1 &
        TAB 7 CACQTEPROD SUB PIC "^^^^^.^^" &
        TAB 19 TMP-ARRON-CO SUB PIC "^^^^^^^"  &
        TAB 36 CACHRESMO  SUB PIC "^^^^^.^" &
        TAB 50 TMP-ARRON-DO SUB PIC "^^^^^^^^^" &
        TAB 62 TMP-VENTEM3 SUB PIC "^^^^^.^^" &
        TAB 90 CACNBBLOCPROD SUB PIC "^^^^" &

SKIP 2 &
        TAB 50 "REVENUS NETS"  &

SKIP 1 &
        TAB 2 "MOIS" &
        TAB 11 "M3"   &
        TAB 19 "COUT" &
        TAB 28 "COUT" &
        TAB 37 "NB"   &
        TAB 43 "M3"   &
        TAB 53 "VENTES" &
        TAB 64 "M3"     &
        TAB 73 "$ /"      &
        TAB 79 "$ /"      &
        TAB 86 "NB"   &
        TAB 92 "M3"  &
        TAB 98 "PROFIT" &

SKIP 1 &
        TAB 10 "PROD"   &
        TAB 19 "DIR." &
        TAB 28 "M3" &
        TAB 37 "HRES"   &
        TAB 43 "HRES"   &
        TAB 62 "VENDUS"     &
        TAB 73 "M3"      &
        TAB 79 "HRES"      &
        TAB 86 "BLOC"   &
        TAB 92 "BLOC"  &
        TAB 98 "$/M3" &

SKIP 1 &
        TAB 2 "_____" &
        TAB 10 "_____"   &
        TAB 19 "_______" &
        TAB 28 "____" &
        TAB 36 "____"   &
        TAB 42 "_____"   &
        TAB 53 "______" &
        TAB 62 "______"   &
        TAB 71 "_____"      &
        TAB 78 "_____"      &
        TAB 87 "__"   &
        TAB 91 "_____"  &
        TAB 98 "______" &

SKIP 1 &
        TAB 2 "TOT:" &
        TAB  7 CACQTEPROD SUB PIC "^^^^^.^^" &
        TAB 16 TMP-CO-AJ PIC "^^^^^^^^^^" &
        TAB 34 CACHRESMO  SUB PIC "^^^^^.^" &
        TAB 42 CACHRESMO RATIO CACQTEPROD PICTURE "^^.^^" &
        TAB 48 TMP-VE-AJ &
        TAB 60 TMP-VENTEM3 SUB PIC "^^^^^.^^" &
        TAB 85 CACNBBLOCPROD SUB PIC "^^^^"  &
SKIP 1 &

;MARC-A
        TAB 2 "MOY:" &
        TAB 6  TMP-1 PIC "^^^^^^.^^"   &
        TAB 16 TMP-2 PIC "^^^^^^^^^^" &
        TAB 26 TMP-3 PIC "^^^^.^^"    &
        TAB 34 TMP-4 PIC "^^^^.^^"    &
        TAB 41 TMP-5 PIC "^^.^^^^"    &
        TAB 53 TMP-6 PIC "^^^^^^"     &
        TAB 60 TMP-7 PIC "^^^^^.^^"   &
        TAB 70 TMP-8 PIC "^^^^.^^"    &
        TAB 77 TMP-9 PIC "^^^^.^^"      &
        TAB 85 TMP-10 PIC "^^^^"      &
;        TAB 91 CACQTEPROD RATIO CACNBBLOCPROD PIC "^^.^^" OUTPUT SCALE 0 &
        TAB 96 TMP-12 PIC "^^^^^.^^" &
;FIN MARC-A

SKIP 3 &
        "NOTE: LE TOTAL DES VENTES DE PIERRES BRUTES EN DOLLARS CAN: " &
        TMP-VENTECN PIC "^^^^^^^^^.^^" &
SKIP 1 &
        "      LE TOTAL DES VENTES DE PIERRES BRUTES EN DOLLARS US : " &
        TMP-VENTEUS PIC "^^^^^^^^^.^^" &
SKIP PAGE

BUILD if336zb
