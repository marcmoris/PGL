;/T/ Consultation des commandes d'achat 
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 7 septembre 1994
;
;    Description..: Permet de visionner et de choisir une commande d'achat.
;
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar116 NOMODE NOACTION FIELDMARK &
             FROM 11,17 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_FOUCLE, &
                        T_CMDCLE 
 
                    

USE ustrasse NOLIST

USE ar116.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4 ; Compagnie de l'entrepôt
TEMPORARY T_FOUCLE CHARACTER SIZE 6 ; Code de fournisseur
TEMPORARY T_CMDCLE CHARACTER SIZE 9 ; Code de commande

;-------------------------------------------------------------------------------
;
FILE ARCOMMANDE        PRIMARY &
                      OCCURS 10

 ACCESS   VIA     CIECLE, &
                  CMDCLE  &
          USING   T_CIECLE, &
                  T_CMDCLE  &
          ORDERBY CIECLE , &
                  CMDCLE 

;-------------------------------------------------------------------------------
;
;
;
FILE MCREF_ADRESSE     REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
         USING  FOUCIECLE OF ARCOMMANDE, &
                "F"                    , &
                FOUCLE    OF ARCOMMANDE, &
                RADCLE    OF ARCOMMANDE

;-------------------------------------------------------------------------------
;
; Permet d'avoir l'information du fournisseur pour la cie de transaction.
;
FILE VFOC_FOU            REFERENCE

  ACCESS VIA   FOUCIECLE, &
               FOUCLE   , &
               FOCCIECLE  &
         USING FOUCIECLE OF ARCOMMANDE, &
               FOUCLE    OF ARCOMMANDE, &
               T_CIECLE


;-------------------------------------------------------------------------------
;
; Pour la présentation de la devise ou non.
;
FILE MCHOLDING           REFERENCE &
                        NOITEM
  ACCESS VIA   CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;

DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING &
          ELSE "/" + DEVCLE OF VFOC_FOU


USE ushilite NOLIST     
DRAW 01,01 TO 13,64

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE "    Commande         Fournisseur             Montant net" AT ,02

SKIP

ALIGN(3,3,6)(,,16)(,,20)(,,27)(,,44)(,,59)

CLUSTER OCCURS WITH ARCOMMANDE 

FIELD CMDCLE    OF ARCOMMANDE HIDDEN LABEL ">>"
FIELD CMDAJT    OF ARCOMMANDE 
FIELD FOUCLE    OF ARCOMMANDE 
FIELD RADNOM    OF MCREF_ADRESSE SIZE 16
FIELD CMDMNTNET OF ARCOMMANDE 
FIELD D_DEVCLE

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CMDCLE, "@")
  THEN LET T_CMDCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_FOUCLE = FOUCLE OF ARCOMMANDE
  LET T_CMDCLE = CMDCLE OF ARCOMMANDE
  RETURN
END

BUILD
@IF FORMAT
                ****************************************************************
                *    Commande         Fournisseur             Montant net      *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                * >> xxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxx *
                ****************************************************************
@ENDIF
