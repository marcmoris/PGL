;/T/ Adresse d'expédition
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 10 janvier 1995
;
;    Description..: Saisie de l'adresse d'expédition de la commande lorsqu'on
;                   désire qu'elle soit différente de l'adresse de facturation.
;                  
;
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar153 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ar153.hlp NOLIST

USE ussectmp NOLIST     ; Variable pour la sécurite 
"AR153"                 ; Nom de l'écran

USE usactecr  NOLIST     

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom du holding

;-------------------------------------------------------------------------------
;
; Recherche de l'adresse d'expédition
;
FILE ARADRESSE_EXP     PRIMARY &
                      NOITEMS

  ACCESS  VIA     ADECLE &
          USING   ADECLE OF ARADRESSE_EXP &
          REQUEST ADECLE &
          ORDERBY ADECLE

  ACCESS  SEQUENTIAL &     
          ORDERBY ADECLE


;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST     
USE ustitstd NOLIST
USE ushilite NOLIST     

SKIP

TITLE &
@IF FRANCAIS
      " Adresse - Expédition " &             
@ELSE
      "%%%Adresse - Expédition " &                  
@ENDIF
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST           


ALIGN(2,3,19)

FIELD ADECLE    OF ARADRESSE_EXP &
        HIDDEN &
        UPSHIFT &
        REQUIRED NOCHANGE &
        LOOKUP NOTON ARADRESSE_EXP & 
          MESSAGE 1483; Cette séquence d'adresse d'expédition existe déjà.

SKIP 1

FIELD ADENOM    OF ARADRESSE_EXP &
        HIDDEN  ID 05

SKIP 1
         
FIELD ADENOMABR OF ARADRESSE_EXP &
        HIDDEN  ID 10 &
        DEFAULT ADENOM OF ARADRESSE_EXP

SKIP 1
 
FIELD ADEADR1   OF ARADRESSE_EXP &
        HIDDEN ID 15 

FIELD ADEADR2   OF ARADRESSE_EXP &
        HIDDEN ID SAME &
        NOLABEL

FIELD ADEADR3   OF ARADRESSE_EXP &
        HIDDEN ID SAME &
        NOLABEL

FIELD ADEADR4   OF ARADRESSE_EXP &
        HIDDEN ID SAME &
        NOLABEL

SKIP 1

FIELD ADECP     OF ARADRESSE_EXP &
        HIDDEN ID 20

SKIP 1

FIELD ADETEL    OF ARADRESSE_EXP &
        HIDDEN ID 25

SKIP 1

FIELD ADEFAX    OF ARADRESSE_EXP &
        HIDDEN ID 30


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;
; Force la saisie du champs
;
PROCEDURE INPUT ADECLE
BEGIN
  LET FIELDTEXT = TRUNCATE( FIELDTEXT )
END

;-------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de code postal
;
;
PROCEDURE INPUT ADECP
BEGIN
  USE uscpfmt NOLIST  ;Formate le numéro de code postal 
END

;------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de téléphone
;
PROCEDURE INPUT ADETEL
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain   
END

;------------------------------------------------------------------------------
;
; Procédure de formatage du numéro de fax 
;
PROCEDURE INPUT ADEFAX
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain   
END


;-------------------------------------------------------------------------------
;
; Procédure ENTRY standard plus le verbe RETURN à la fin. L'option AUTORETURN
; ne fonctionne pas avec un écran SLAVE.
;
;
PROCEDURE PREENTRY
BEGIN
  ;
  ; Valide si l'usager peut faire de la saisie.
  ;
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; La mise à jour va être faite dans l'écran de la commande.
;
;
PROCEDURE PREUPDATE
BEGIN
    ;
    ; Validation de la destruction
    ;
    IF DELETEDRECORD OF ARADRESSE_EXP &  
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification
    ;
    IF    ALTEREDRECORD     OF ARADRESSE_EXP &  
      AND NOT NEWRECORD     OF ARADRESSE_EXP &  
      AND NOT DELETEDRECORD OF ARADRESSE_EXP &  
      AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'un état de produit.
    ;
    IF DELETEDRECORD OF ARADRESSE_EXP
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si l'état de produit est référé dans 
      ; la réception de produit.
      ;
      RUN SCREEN ar153x MODE SAME &
                        PASSING ARADRESSE_EXP, &  
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
END

BUILD
@IF FORMAT
**************************** Adresse - Expédition ******************************
*                                                                              *
* Séquence......: xxxx                                                         *
*                                                                              *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Code Postal...: xxxxxxx                                                      *
*                                                                              *
* Téléphone.....: xxxxxxxxxxxxx                                                *
*                                                                              *
* Fax...........: xxxxxxxxxxxxx                                                *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF                                                                
