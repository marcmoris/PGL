;/T/ Carte de bloc - Vérification
;
;/P/ Programmeur..: Francois Richard
;    Date Création: 10 octobre 1999
;
;    Description..: Affiche la date,l'heure et l'usager de création et de
;                   modification de la carte de bloc.
;
;-------------------------------------------------------------------------------

SCREEN if001a SLAVE &
              FROM 6,1 TO 14,80 &
              MESSAGE ON 24 &
              NOMODE &
              NOACTION FIELDMARK STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND &
              RECEIVING IFBLOC

;USE if001a.hlp

;-------------------------------------------------------------------------------
;
FILE IFBLOC            MASTER

;
; Pour afficher le nom de l'usager de création
;
FILE GSUSAGER         REFERENCE &
                      ALIAS A_USR_CRE
  ACCESS VIA USRCLE &
         USING BLCUSRCRE OF IFBLOC

;
; Pour afficher le nom de l'usager de modification
;
FILE GSUSAGER         REFERENCE &
                      ALIAS A_USR_MOD
  ACCESS VIA USRCLE &
         USING BLCUSAGERMOD OF IFBLOC

;-------------------------------------------------------------------------------
;
DRAW 1,1 TO 9,80

USE ushilite NOLIST     ; Hilite pour tout les écrans

TITLE &
@IF FRANCAIS
      " Vérification " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19) (,,32)
FIELD BLCDATCRE OF IFBLOC     &
      FORMAT YYYYMMDD         &
      PATTERN "####/##/##"    &
      HIDDEN                  &
      ID 05                   &
      LABEL "Créé le.......:"

FIELD BLCHRECRE OF IFBLOC

ALIGN(3,3,19) (,,32)
FIELD BLCUSRCRE OF IFBLOC     &
      HIDDEN                  &
      ID 10                   &
      LABEL "Créé par......:"

FIELD USREMPNOM OF A_USR_CRE

SKIP 1

ALIGN(3,3,19) (,,32)
FIELD BLCDATDERNMOD OF IFBLOC &
      FORMAT YYYYMMDD         &
      PATTERN "####/##/##"    &
      HIDDEN                  &
      ID 15                   &
      LABEL "Modifié le....:"

FIELD BLCHREDERNMOD OF IFBLOC

ALIGN(3,3,19) (,,32)
FIELD BLCUSAGERMOD OF IFBLOC &
      HIDDEN                 & 
      ID 20                  &
      LABEL "Modifié par...:"

FIELD USREMPNOM OF A_USR_MOD

BUILD
;
;********************************* Vérification *********************************
;*                                                                              *
;* Créé le.......: xxxxxxxx xxxxx                                               *
;* Créé par......: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;* Modifié le....: xxxxxxxx xxxxx                                               *
;* Modifié par...: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
;*                                                                              *
;********************************************************************************
