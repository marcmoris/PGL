;/T/ 2.4.1 Enregistrer le montage.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-04
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le montage des blocs de granit.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd183  ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              AUTOUPDATE                   &
              ACTIVITIES CHANGE,FIND,ENTRY &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD183"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd183.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Blocs                           " ACTION DESIGNER D001
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D002
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Blocs                        " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D002
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

DEFINE D_KILO CHAR SIZE 2 = "Kg"

;-------------------------------------------------------------------------------
;
; Variables nécessaire pour l'apper de dépisteur (pop-up)
;
TEMPORARY T_SCINUMSCI CHARACTER SIZE 04
TEMPORARY T_TYCCODCHA CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_RMOSTU    CHARACTER SIZE 16 = "RMOSTU"
TEMPORARY T_RMOSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE    PRIMARY &
                      NOITEM

  ACCESS VIA     CIECLE,                              &
                 RMSNUMSEQ                            &
         USING   T_CIECLEMNU,                         &
                 RMSNUMSEQ        OF PDRAP_MONTAGE    &
         REQUEST RMSNUMSEQ        OF PDRAP_MONTAGE    &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ


  ACCESS VIA     CIECLE,                              &
                 RMOSTU                               &
         USING   T_CIECLEMNU,                         &
                 RMOSTU           OF PDRAP_MONTAGE    &
         REQUEST RMOSTU           OF PDRAP_MONTAGE    &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ


  ACCESS VIA     CIECLE,                              &
                 RMOPRI                               &
         USING   T_CIECLEMNU,                         &
                 RMOPRI           OF PDRAP_MONTAGE    &
         REQUEST RMOPRI           OF PDRAP_MONTAGE    &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ


  ACCESS VIA     CIECLE          &
         USING   T_CIECLEMNU     &
         ORDERBY CIECLE,         &
                 RMSNUMSEQ


         ITEM CIECLE OF PDRAP_MONTAGE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Détail planification sciage
;

FILE PDDET_RAP_MONT DESIGNER

  ACCESS VIA     CIECLE,          &
                 RMSNUMSEQ        &
         USING   T_CIECLEMNU,     &
                 RMSNUMSEQ OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Rapport de montage (Vérification)
;

FILE PDRAP_MONTAGE    DESIGNER ALIAS RMO_VERIF


;-------------------------------------------------------------------------------
;
; Scies - type de chariot
;

FILE PDSCIES_CHARIOT REFERENCE

  ACCESS VIA     CIECLE,    &
                 SCINUMSCI, &
                 TYCCODCHA  &
         USING   T_CIECLEMNU,                &
                 SCINUMSCI OF PDRAP_MONTAGE, &
                 TYCCODCHA OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                              &
                 SCINUMSCI                            &
         USING   T_CIECLEMNU,                         &
                 SCINUMSCI        OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Type de chariot
;

FILE PDTYPE_CHARIOT REFERENCE

  ACCESS VIA     CIECLE,                              &
                 TYCCODCHA                            &
         USING   T_CIECLEMNU,                         &
                 TYCCODCHA        OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC DESIGNER

  ACCESS VIA     CIECLE,                              &
                 RMSNUMSEQ                            &
         USING   T_CIECLEMNU,                         &
                 RMSNUMSEQ        OF PDRAP_MONTAGE    &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDBLOC DESIGNER

  ACCESS VIA     CIECLE,          &
                 BLONUMGRA001APR, &
                 BLONUMGRA002APR, &
                 BLORECAPR        &
         USING   T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDMONTAGE_BLOC, &
                 BLONUMGRA002APR OF PDMONTAGE_BLOC, &
                 BLORECAPR       OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_RMOSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_RMOSTU,                             &
               RMOSTU           OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Montage " &
@ELSE
      " %%%Montage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19)


FIELD RMSNUMSEQ        OF PDRAP_MONTAGE    &
label "Num. montage..:"&
        HIDDEN ID 05                       &
        REQUIRED NOCHANGE                  &
        LOOKUP ON PDRAP_MONTAGE            &
               VIA CIECLE, RMSNUMSEQ       &
               USING T_CIECLEMNU, RMSNUMSEQ OF PDRAP_MONTAGE &
          MESSAGE 3032 , & ; Ce numéro de rapport n'existe pas
               NOTON PDRAP_MONTAGE         &
               VIA CIECLE, RMSNUMSEQ       &
               USING T_CIECLEMNU, RMSNUMSEQ OF PDRAP_MONTAGE &
          MESSAGE 3031 ; Ce numéro de rapport existe déjà


ALIGN (2,3,19) (,,25)


FIELD RMOSTU           OF PDRAP_MONTAGE    &
label "Statut........:"&
        HIDDEN ID 10                       &
        NOENTRY


FIELD CBIDSC           OF A_CBI_RMOSTU     &
        DISPLAY


ALIGN (2,3,19)


FIELD RMOPRI           OF PDRAP_MONTAGE    &
label "Priorité......:"&
        HIDDEN ID 15


DRAW 9,2 TO 9,79


SKIP TO LINE 9


TITLE &
@IF FRANCAIS
      " Banquage " &
@ELSE
      " %%%Banquage " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 11


ALIGN (2,3,19) (,,25)


FIELD TYCCODCHA        OF PDRAP_MONTAGE    &
label "Type chariot..:"&
        HIDDEN ID 20                       &
        REQUIRED                           &
        NOENTRY


FIELD TYCDSCFRA       OF PDTYPE_CHARIOT    &
        DISPLAY


ALIGN (2,3,19)


FIELD RMODATPRD        OF PDRAP_MONTAGE     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date banquage.:"&
        ID SAME                            &
        DEFAULT SYSDATE                    &
        REQUIRED                           &
        NOENTRY

ALIGN (2,3,19) (34,35,51)

FIELD RMODATDEBPRE     OF PDRAP_MONTAGE     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date début P..:"&
        ID SAME                            &
;        REQUIRED                           &
        NOENTRY


FIELD RMOHREDEBPRE     OF PDRAP_MONTAGE    &
label "Hre. début P..:"&
        ID SAME                            &
;        REQUIRED                           &
        NOENTRY


FIELD RMODATFINPRE     OF PDRAP_MONTAGE     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date fin P....:"&
        HIDDEN ID 25


FIELD RMOHREFINPRE     OF PDRAP_MONTAGE    &
label "Hre. fin P....:"&
        ID SAME


DRAW 16,2 TO 16,79


SKIP TO LINE 16


TITLE &
@IF FRANCAIS
      " Sciage " &
@ELSE
      " %%%Sciage " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 18


ALIGN (2,3,19) (,,25)


FIELD SCINUMSCI       OF PDRAP_MONTAGE     &
label "Numéro scie...:"&
        HIDDEN ID 30                       &
        REQUIRED                           &
        NOENTRY



FIELD SCIDSC001        OF PDSCIES          &
        DISPLAY


ALIGN (2,3,19) (34,35,51)


FIELD RMODATDEBREE     OF PDRAP_MONTAGE     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date début R..:"&
        HIDDEN ID 40                       &
        REQUIRED                           &
        NOENTRY


FIELD RMOHREDEBREE     OF PDRAP_MONTAGE    &
label "Hre. début R..:"&
        ID SAME                            &
        REQUIRED                           &
        NOENTRY


FIELD RMODATFINREE     OF PDRAP_MONTAGE     FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date fin R....:"&
        HIDDEN ID 45     ;                  &
;        ID SAME              ;              &
;        NOID                         ;      &
 ;       DISPLAY


FIELD RMOHREFINREE     OF PDRAP_MONTAGE    &
label "Hre. fin R....:"&
        ID SAME              ;              &
;        NOID                        ;       &;
 ;       DISPLAY

FIELD RMOQTELAMNEU     OF PDRAP_MONTAGE    &
label "Qte lame neuve:"&
        HIDDEN ID 50

FIELD RMOHRETOTARR1    OF PDRAP_MONTAGE    &
label "Hre tot arrêt.:"&
        PIC "^^^^:^^" SIGNIF 4 BWZ           &
        ID SAME

ALIGN (2,3,19) (,,28)

FIELD RMOQTEABR        OF PDRAP_MONTAGE    &
label "Qte abrasif...:"&
       PIC " ^^^^.^^ " SIGNIF 5 BWZ INPUT SCALE 2 &
       ID SAME

FIELD D_KILO DISPLAY

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide le numéro de positionnement
;

PROCEDURE EDIT RMSNUMSEQ
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE <> "P"
  THEN BEGIN
    ERROR 3035 ; Le statut du positionnement ne permet pas le montage
  END
END


;-------------------------------------------------------------------------------
;
; Ne pas permettre de modifier le statut
;

PROCEDURE EDIT RMOSTU
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT))
  THEN BEGIN
    ERROR 3061 ; Le statut du montage ne permet pas cette modification
  END
END


;-------------------------------------------------------------------------------
;
; Ne pas permettre de modifier le statut
;

PROCEDURE EDIT RMOPRI
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT)) &
     AND &
     RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs TYCCODCHA
;

PROCEDURE INPUT TYCCODCHA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYCCODCHA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd120 MODE F &
                     PASSING T_TYCCODCHA,&
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYCCODCHA )
  END
  ;
  ; Simule le lookup sur PDTYPE_CHARIOT
  ;
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    GET PDTYPE_CHARIOT OPTIONAL                    &
      VIA     CIECLE,                              &
              TYCCODCHA                            &
      USING   T_CIECLEMNU,                         &
              FIELDTEXT
    IF NOT ACCESSOK
    THEN BEGIN
      ERROR 2969 ; Ce type de chariot n'existe pas
    END
  END
END


;-------------------------------------------------------------------------------
;
; Ne pas permettre de modifier si le statut du rapport de montage = "A"
;

PROCEDURE EDIT TYCCODCHA
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

PROCEDURE INPUT RMODATPRD
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Ne pas permettre de modifier si statut rapport montage = "A" (Annuler)
;

PROCEDURE EDIT RMODATPRD
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

;PROCEDURE INPUT RMODATDEBPRE
;BEGIN
;  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
;  THEN BEGIN
;    ERROR 3044 ; Ce champ est requis
;  END
;END


;-------------------------------------------------------------------------------
;
; Valide que la date de début prévu soit plus grande ou égale à la date
; de banquage.
;

PROCEDURE EDIT RMODATDEBPRE
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END

  IF RMODATPRD OF PDRAP_MONTAGE > RMODATDEBPRE     OF PDRAP_MONTAGE
  THEN BEGIN
    ERROR 3041 ; La date de début prévu doit être plus grande ou égale à la date de banquage
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

;PROCEDURE INPUT RMOHREDEBPRE
;BEGIN
;  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
;  THEN BEGIN
;    ERROR 3044 ; Ce champ est requis
;  END
;END


;-------------------------------------------------------------------------------
;
; Ne pas permettre de modifier si statut rapport montage = "A" (Annuler)
;

PROCEDURE EDIT RMOHREDEBPRE
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

;PROCEDURE INPUT RMODATFINPRE
;BEGIN

;  IF     0 = SIZE(TRUNCATE(FIELDTEXT)) &
;     AND 0 = OLDVALUE(RMODATFINPRE OF PDRAP_MONTAGE)
;  THEN BEGIN
;    ERROR 3044 ; Ce champ est requis
;  END
;END


;-------------------------------------------------------------------------------
;
; Valide que la date de début réel est plus petite que la date le fin
;

PROCEDURE EDIT RMODATFINPRE
BEGIN
  ;
  ; Ne pas permettre de modifier si statut rapport montage = "A" (Annuler)
  ;
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
  ;
  ; Vérifie que la date de fin à déjà été saisie
  ;
  IF RMODATDEBPRE OF PDRAP_MONTAGE > RMODATFINPRE OF PDRAP_MONTAGE
  THEN BEGIN
    ERROR 3036 ; La date de fin prévu doit être plus grande ou égale à la date de début prévu
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

;PROCEDURE INPUT RMOHREFINPRE
;BEGIN
;  IF     0 = SIZE(TRUNCATE(FIELDTEXT)) &
;     AND 0 = OLDVALUE(RMOHREFINPRE OF PDRAP_MONTAGE)
;  THEN BEGIN
;    ERROR 3044 ; Ce champ est requis
;  END
;END


;-------------------------------------------------------------------------------
;
; Valide heure de début prévu
;

PROCEDURE EDIT RMOHREFINPRE
BEGIN
  ;
  ; Ne pas permettre de modifier si statut rapport montage = "A" (Annuler)
  ;
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END

  IF RMODATFINPRE     OF PDRAP_MONTAGE    <> 0 &
     AND &
     RMOHREFINPRE     OF PDRAP_MONTAGE    <> 0
  THEN BEGIN
    ;
    ; Si deux jours différent il n'y a pas de test a faire pour l'heure de fin
    ;
    IF RMODATFINPRE OF PDRAP_MONTAGE = RMODATDEBPRE OF PDRAP_MONTAGE &
       AND &
       RMOHREFINPRE OF PDRAP_MONTAGE < RMOHREDEBPRE OF PDRAP_MONTAGE
    THEN BEGIN
      ERROR 3039 ; L'heure de fin prévu doit être plus grande ou égale à l'heure de début prévu
    END
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs SCINUMSCI
;

PROCEDURE INPUT SCINUMSCI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_SCINUMSCI = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd103 MODE F &
                     PASSING T_SCINUMSCI,&
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_SCINUMSCI )
  END
  ;
  ; Simule le lookup PDSCIES
  ;
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    GET PDSCIES OPTIONAL &
      VIA     CIECLE,    &
              SCINUMSCI  &
      USING   T_CIECLEMNU, &
              FIELDTEXT
    IF NOT ACCESSOK
    THEN BEGIN
      ERROR 2949 ; Ce code de scie n'existe pas
    END
    ;
    ; Simule le lookup PDSCIES_CHARIOT
    ;
    GET PDSCIES_CHARIOT OPTIONAL &
      VIA     CIECLE,    &
              SCINUMSCI, &
              TYCCODCHA  &
      USING   T_CIECLEMNU, &
              FIELDTEXT,   &
              TYCCODCHA OF PDRAP_MONTAGE
    IF NOT ACCESSOK
    THEN BEGIN
      ERROR 2971 ; Cette combinaison n'existe pas
    END
  END
END


;-------------------------------------------------------------------------------
;
; Ne pas permettre de modifier si le statut du rapport de montage = "A"
;

PROCEDURE EDIT SCINUMSCI
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

PROCEDURE INPUT RMODATDEBREE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Validation du champs
;

PROCEDURE EDIT RMODATDEBREE
BEGIN
  IF RMODATPRD OF PDRAP_MONTAGE > RMODATDEBREE OF PDRAP_MONTAGE
  THEN BEGIN
    ERROR 3042 ; La date de début réel doit être plus grande ou égale à la date de banquage
  END
END


;-------------------------------------------------------------------------------
;
; Simule l'option REQUIRED pour l'énoncé FIELD
;

PROCEDURE INPUT RMOHREDEBREE
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Permet de saisir les informations seulement si le statut du positionnement
; est "P" (Planifier) ou "B" (Banquer)
;

PROCEDURE DESIGNER 20
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
  ELSE BEGIN
;    IF RMOSTU           OF PDRAP_MONTAGE = "P" &
;       OR &
;       RMOSTU           OF PDRAP_MONTAGE = "B"
;    THEN BEGIN
      ACCEPT  TYCCODCHA       OF PDRAP_MONTAGE
      DISPLAY TYCDSCFRA       OF PDTYPE_CHARIOT
      IF 0 <> SIZE(TRUNCATE(TYCCODCHA OF PDRAP_MONTAGE))
      THEN BEGIN
        ACCEPT  RMODATPRD       OF PDRAP_MONTAGE
        ACCEPT  RMODATDEBPRE    OF PDRAP_MONTAGE
        ACCEPT  RMOHREDEBPRE    OF PDRAP_MONTAGE
        ACCEPT  RMODATFINPRE    OF PDRAP_MONTAGE
        ACCEPT  RMOHREFINPRE    OF PDRAP_MONTAGE
      END
      ELSE BEGIN
        LET     RMODATPRD       OF PDRAP_MONTAGE = 0
        LET     RMODATDEBPRE    OF PDRAP_MONTAGE = 0
        LET     RMOHREDEBPRE    OF PDRAP_MONTAGE = 0
        LET     RMODATFINPRE    OF PDRAP_MONTAGE = 0
        LET     RMOHREFINPRE    OF PDRAP_MONTAGE = 0
        DISPLAY RMODATPRD       OF PDRAP_MONTAGE
        DISPLAY RMODATDEBPRE    OF PDRAP_MONTAGE
        DISPLAY RMOHREDEBPRE    OF PDRAP_MONTAGE
        DISPLAY RMODATFINPRE    OF PDRAP_MONTAGE
        DISPLAY RMOHREFINPRE    OF PDRAP_MONTAGE
      END
;    END
;    ELSE BEGIN
;      ERROR 3061 ; Le statut du montage ne permet pas cette modification
;    END
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE DESIGNER 25
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE <> "T" AND RMODATDEBPRE OF PDRAP_MONTAGE <> 0
  THEN BEGIN
    ACCEPT RMODATFINPRE     OF PDRAP_MONTAGE
    ACCEPT RMOHREFINPRE     OF PDRAP_MONTAGE
  END
END


;-------------------------------------------------------------------------------
;
; Permet de saisir les informations seulement si le statut du positionnement
; est "B" (Banquer) ou "S" (Sciage)
;

PROCEDURE DESIGNER 30
BEGIN
  IF RMOSTU OF PDRAP_MONTAGE = "A"
  THEN BEGIN
    ERROR 2973 ; On ne peut pas modifier un montage qui à été annulé
  END
  ELSE BEGIN
    IF RMOSTU           OF PDRAP_MONTAGE = "B" &
       OR &
       RMOSTU           OF PDRAP_MONTAGE = "S"
    THEN BEGIN
      ;
      ; Valide que les niveaux haut et bas ont été enregistré pour tout
      ; les blocs du montage avant de permettre de saisir la scie.
      ;
      WHILE RETRIEVING PDMONTAGE_BLOC &
        VIA     CIECLE,          &
                RMSNUMSEQ        &
        USING   T_CIECLEMNU,     &
                RMSNUMSEQ        OF PDRAP_MONTAGE  &
        ORDERBY CIECLE,          &
                RMSNUMSEQ,       &
                BLONUMGRA001APR, &
                BLONUMGRA002APR, &
                BLORECAPR
      BEGIN
        IF MOBNIVHAU OF PDMONTAGE_BLOC = 0 &
           OR &
           MOBNIVBAS OF PDMONTAGE_BLOC = 0
        THEN BEGIN
          ERROR 3070 ; Les niveaux haut et bas doivent être saisis pour chaque bloc d'abord
        END
      END
      ;
      ; saisit des informations
      ;
      ACCEPT  SCINUMSCI       OF PDRAP_MONTAGE
      DISPLAY SCIDSC001       OF PDSCIES
      IF 0 <> SIZE(TRUNCATE(SCINUMSCI OF PDRAP_MONTAGE))
      THEN BEGIN
        WHILE RETRIEVING RMO_VERIF &
          VIA     CIECLE,                              &
                  SCINUMSCI                            &
          USING   T_CIECLEMNU,                         &
                  SCINUMSCI        OF PDRAP_MONTAGE    &
          ORDERBY CIECLE,                              &
                  RMODATDEBREE     DESCENDING
        IF RMOSTU OF RMO_VERIF <> "T" AND RMOSTU OF RMO_VERIF  <> "A"
        THEN BEGIN
          ERROR 3118 ; Il y a déjà un montage sous cette scie
        END
        ACCEPT  RMODATDEBREE    OF PDRAP_MONTAGE
        ACCEPT  RMOHREDEBREE    OF PDRAP_MONTAGE
        ACCEPT  RMODATFINREE    OF PDRAP_MONTAGE
        ACCEPT  RMOHREFINREE    OF PDRAP_MONTAGE
      END
      ELSE BEGIN
        LET     RMODATDEBREE    OF PDRAP_MONTAGE = 0
        LET     RMOHREDEBREE    OF PDRAP_MONTAGE = 0
        DISPLAY RMODATDEBREE    OF PDRAP_MONTAGE
        DISPLAY RMOHREDEBREE    OF PDRAP_MONTAGE
      END
    END
    ELSE BEGIN
      ERROR 3061 ; Le statut du montage ne permet pas cette modification
    END
  END
END


;-------------------------------------------------------------------------------
;
; Blocs
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd183a &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDRAP_MONTAGE       &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Vérification
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd183c &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDRAP_MONTAGE       &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRAP_MONTAGE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRAP_MONTAGE &
     AND NOT NEWRECORD     OF PDRAP_MONTAGE &
     AND NOT DELETEDRECORD OF PDRAP_MONTAGE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Replace le statut a "P" si on fait un retour en arrière
  ;
  IF 0 = SIZE(TRUNCATE(TYCCODCHA OF PDRAP_MONTAGE)) &
     AND &
     0 = SIZE(TRUNCATE(SCINUMSCI OF PDRAP_MONTAGE))
  THEN BEGIN
    LET RMOSTU           OF PDRAP_MONTAGE = "P"
  END
  ;
  ; Assigne le statut du positionnement lors du banquage
  ;
  IF (0 <> SIZE(TRUNCATE(TYCCODCHA OF PDRAP_MONTAGE)) &
      AND &
      RMOSTU OF PDRAP_MONTAGE = "P") &
     OR &
     (0 = SIZE(TRUNCATE(SCINUMSCI OF PDRAP_MONTAGE)) & ; Retour en arrière
      AND &                                            ; déassigne la scie
      RMOSTU OF PDRAP_MONTAGE = "S")                   ; pour le montage
  THEN BEGIN
    LET RMOSTU           OF PDRAP_MONTAGE = "B"
  END
  ;
  ; Assigne le statut du positionnement lorsque le chariot est placé sous une scie
  ;
  IF 0 <> SIZE(TRUNCATE(SCINUMSCI OF PDRAP_MONTAGE)) &
     AND &
     RMOSTU OF PDRAP_MONTAGE = "B"
  THEN BEGIN
    LET RMOSTU           OF PDRAP_MONTAGE = "S"
  END
  ;
  ; Mise à jour des informations sur la mise à jour
  ;
  IF (NOT NEWRECORD OF PDRAP_MONTAGE AND &
      ALTEREDRECORD OF PDRAP_MONTAGE)
  THEN BEGIN
    LET RMODATDERMAJ     OF PDRAP_MONTAGE = SYSDATE
    LET RMOHREDERMAJ     OF PDRAP_MONTAGE = SYSTIME / 10000
    LET RMOUSRDERMAJ     OF PDRAP_MONTAGE = LOGONID
  END
  ;
  ; Création des détails rapport de montage pour chaque bloc
  ;
  WHILE RETRIEVING PDDET_RAP_MONT   &
    VIA     CIECLE,          &
            RMSNUMSEQ        &
    USING   T_CIECLEMNU,     &
            RMSNUMSEQ OF PDRAP_MONTAGE &
    ORDERBY CIECLE,          &
            RMSNUMSEQ,       &
            BLONUMGRA001APR, &
            BLONUMGRA002APR, &
            BLORECAPR

  BEGIN
    GET PDMONTAGE_BLOC OPTIONAL &
      VIA     CIECLE,           &
              RMSNUMSEQ,        &
              BLONUMGRA001APR,  &
              BLONUMGRA002APR,  &
              BLORECAPR         &
      USING   T_CIECLEMNU,                         &
              RMSNUMSEQ         OF PDRAP_MONTAGE,  &
              BLONUMGRA001APR   OF PDDET_RAP_MONT, &
              BLONUMGRA002APR   OF PDDET_RAP_MONT, &
              BLORECAPR         OF PDDET_RAP_MONT
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE           OF PDMONTAGE_BLOC  = T_CIECLEMNU
      LET RMSNUMSEQ        OF PDMONTAGE_BLOC  = RMSNUMSEQ        OF PDRAP_MONTAGE
      LET BLONUMGRA001APR  OF PDMONTAGE_BLOC  = BLONUMGRA001APR  OF PDDET_RAP_MONT
      LET BLONUMGRA002APR  OF PDMONTAGE_BLOC  = BLONUMGRA002APR  OF PDDET_RAP_MONT
      LET BLORECAPR        OF PDMONTAGE_BLOC  = BLORECAPR        OF PDDET_RAP_MONT
      LET BLONUMBLOAPR     OF PDMONTAGE_BLOC  = BLONUMBLOAPR     OF PDDET_RAP_MONT
      PUT PDMONTAGE_BLOC
    END
  END
END


BUILD detail list

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Montage ************************************
*                                                                              *
* Num. montage..: xxxxxxxxxxx                                                  *
* Statut........: x     xxxxxxxxxxxxxxxxxxxxxxxxx                              *
* Priorité......: xx                                                           *
*                                                                              *
*********************************** Banquage ***********************************
*                                                                              *
* Type chariot..: xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Date banquage.: xxxxxxxx                                                     *
* Date début P..: xxxxxxxx        Hre. début P..: xxxxx                        *
* Date fin P....: xxxxxxxx        Hre. fin P....: xxxxx                        *
*                                                                              *
************************************ Sciage ************************************
*                                                                              *
* Numéro scie...: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Date début R..: xxxxxxxx        Hre. début R..: xxxxx                        *
* Date fin R....: xxxxxxxx        Hre. fin R....: xxxxx                        *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
