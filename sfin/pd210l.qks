;/T/ 2.3.1 Commande interne - Adresses supplémentaires
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-31
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les adresses pour les commandes internes.
;

;/V/ Écran vérifier pour le passage à l'an 2000
;
;/M/ Programmeur..: Claudie Doyon
;    Date.........: 09 décembre 1999
;    Description..: Modification du orderby


SCREEN pd210l ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 9,1 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING VCLC_CLI, &
                        T_RADCLE, &
                        T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Variable passé en paramêtre
;

TEMPORARY T_RADCLE    INTEGER UNSIGNED SIZE 02
TEMPORARY T_CIECLEMNU CHARACTER        SIZE 04

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp   NOLIST
    "PD210L"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd210l.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr   NOLIST

;-------------------------------------------------------------------------------
;
; Vue client
;

FILE VCLC_CLI MASTER


;-------------------------------------------------------------------------------
;
; Adresse
;

FILE MCREF_ADRESSE    PRIMARY &
                      NOITEM  &
                      OPEN EACH LEVEL

 ACCESS VIA REFCIECLE, &
            REFCLETYP, &
            REFCLENUM &
        USING CLICIECLE OF VCLC_CLI, &
              CLIREFTYP OF VCLC_CLI, &
              CLICLE    OF VCLC_CLI  &
     ORDERBY REFCIECLE, &
             REFCLETYP, &
             REFCLENUM ,&
             RADCLE

 ITEM REFCIECLE   OF MCREF_ADRESSE INITIAL CLICIECLE OF VCLC_CLI
 ITEM REFCLETYP   OF MCREF_ADRESSE INITIAL CLIREFTYP OF VCLC_CLI
 ITEM REFCLENUM   OF MCREF_ADRESSE INITIAL CLICLE    OF VCLC_CLI
 ITEM RADFACCR    OF MCREF_ADRESSE INITIAL "1"
 ITEM RADLIVCR    OF MCREF_ADRESSE INITIAL "1"
 ITEM RADETACPTCR OF MCREF_ADRESSE INITIAL "1"


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction


;-------------------------------------------------------------------------------
;

DRAW 02,01 TO 02,79
DRAW 02,01 TO 15,01
DRAW 15,01 TO 15,80
DRAW 03,80 TO 15,80

USE ushilite NOLIST

SKIP 1

TITLE &
@IF FRANCAIS
      " Adresses supplémentaires " &
@ELSE
      " Address " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST


SKIP 1


ALIGN(,3,19) (,27,40) (,45,56) (,61,75)


FIELD RADCLE    OF MCREF_ADRESSE HIDDEN ID 05 &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON MCREF_ADRESSE &
          VIA REFCIECLE, &
              REFCLETYP, &
              REFCLENUM, &
              RADCLE     &
          USING REFCIECLE OF MCREF_ADRESSE, &
                REFCLETYP OF MCREF_ADRESSE, &
                REFCLENUM OF MCREF_ADRESSE, &
                RADCLE    OF MCREF_ADRESSE  &
          MESSAGE 687 ; Ce numéro d'adresse existe déjà.


FIELD RADFACCR OF MCREF_ADRESSE &
        SIZE 3 &
@IF FRANCAIS
        LABEL "Facturation:"
@ELSE
        LABEL "Invoicing..:"
@ENDIF


FIELD RADLIVCR OF MCREF_ADRESSE &
        SIZE 3 &
@IF FRANCAIS
        LABEL "Livraison:"
@ELSE
        LABEL "Delivery.:"
@ENDIF


FIELD RADETACPTCR OF MCREF_ADRESSE &
        SIZE 3 &
@IF FRANCAIS
        LABEL "Etat compte.:"
@ELSE
        LABEL "Account Stat:"
@ENDIF


ALIG(3,3,19)


FIELD RADNOM    OF MCREF_ADRESSE HIDDEN ID 10 &
        REQUIRED


FIELD RADNOMABR OF MCREF_ADRESSE HIDDEN ID 15 &
        DEFAULT RADNOM OF MCREF_ADRESSE


FIELD RADADR1   OF MCREF_ADRESSE HIDDEN ID 20 &
        REQUIRED


FIELD RADADR2   OF MCREF_ADRESSE HIDDEN ID SAME NOLABEL


FIELD RADADR3   OF MCREF_ADRESSE HIDDEN ID SAME NOLABEL


FIELD RADADR4   OF MCREF_ADRESSE HIDDEN ID SAME NOLABEL


FIELD RADCP     OF MCREF_ADRESSE HIDDEN ID 25


FIELD RADTEL    OF MCREF_ADRESSE HIDDEN ID 30


FIELD RADFAX    OF MCREF_ADRESSE HIDDEN ID 35


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Traitement pour les indicateurs dont la valeur est Oui ou Non.
; Le procédure INPUT transpose les valeurs sous un code universel soit 1 et 0.
;

PROCEDURE INPUT RADFACCR
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; La procédure OUTPUT affiche les valeurs sous la forme O/N.
;

PROCEDURE OUTPUT RADFACCR
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
; Traitement pour les indicateurs dont la valeur est Oui ou Non.
; Le procédure INPUT transpose les valeurs sous un code universel soit 1 et 0.
;

PROCEDURE INPUT RADLIVCR
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; La procédure OUTPUT affiche les valeurs sous la forme O/N.
;

PROCEDURE OUTPUT RADLIVCR
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
; Traitement pour les indicateurs dont la valeur est Oui ou Non.
; Le procédure INPUT transpose les valeurs sous un code universel soit 1 et 0.
;

PROCEDURE INPUT RADETACPTCR
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; La procédure OUTPUT affiche les valeurs sous la forme O/N.
;

PROCEDURE OUTPUT RADETACPTCR
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
; Cette procedure formate le code postal.
;

PROCEDURE INPUT RADCP
BEGIN
  USE uscpfmt NOLIST
END


;-------------------------------------------------------------------------------
;
; Cette procedure formate le numero de telephone nord americain.
;

PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain
END


;-------------------------------------------------------------------------------
;
; Cette procedure formate le numero de telephone nord americain.
;

PROCEDURE INPUT RADFAX
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE PREENTRY
BEGIN
  ;
  ; Valide si l'usager peut faire de la saisie.
  ;
  USE ussecent NOLIST
  ;
  ;
  IF CLITYP OF VCLC_CLI = "D"
  THEN ERROR 684 ; On ne peut pas saisir d'adresse dans cette écran pour les
                 ; clients divers.
END


;-------------------------------------------------------------------------------
;
; L'adresse UN est non modifiable, car elle doit être égale à celle
; dans l'écran du client.
;

PROCEDURE DESIGNER 10
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN ACCEPT RADNOM OF MCREF_ADRESSE
END


PROCEDURE DESIGNER 15
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN ACCEPT RADNOMABR OF MCREF_ADRESSE
END


PROCEDURE DESIGNER 20
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN BEGIN
    ACCEPT RADADR1   OF MCREF_ADRESSE
    ACCEPT RADADR2   OF MCREF_ADRESSE
    ACCEPT RADADR3   OF MCREF_ADRESSE
    ACCEPT RADADR4   OF MCREF_ADRESSE
  END
END


PROCEDURE DESIGNER 25
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN ACCEPT RADCP     OF MCREF_ADRESSE
END


PROCEDURE DESIGNER 30
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN ACCEPT RADTEL    OF MCREF_ADRESSE
END


PROCEDURE DESIGNER 35
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN ACCEPT RADFAX    OF MCREF_ADRESSE
END


;-------------------------------------------------------------------------------
;
; Initialise la cle pour le retourné au programme appelant
;

PROCEDURE POSTFIND
BEGIN
  LET T_RADCLE = RADCLE OF MCREF_ADRESSE
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF MCREF_ADRESSE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF MCREF_ADRESSE &
     AND NOT NEWRECORD     OF MCREF_ADRESSE &
     AND NOT DELETEDRECORD OF MCREF_ADRESSE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction d'une adresse.
  ;
  IF DELETEDRECORD OF MCREF_ADRESSE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si l'adresse est référé.
    ;
    RUN SCREEN pd210lx MODE SAME &
                       PASSING MCREF_ADRESSE , &
                               T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END


;-------------------------------------------------------------------------------
;
; Initialise la cle pour le retourné au programme appelant
;

PROCEDURE POSTUPDATE
BEGIN
  LET T_RADCLE = RADCLE OF MCREF_ADRESSE
END


;---------------------------------------------------------------------------
;
; On ne peut pas détruire l'adresse 1.
;

PROCEDURE DELETE
BEGIN
  IF RADCLE OF MCREF_ADRESSE <> 1
  THEN DELETE MCREF_ADRESSE
END

@IF PAT
PROCEDURE PATH
BEGIN
  LET PATH = 1
END

PROCEDURE FIND
BEGIN
  GET MCREF_ADRESSE VIA REFCIECLE            , &
                        REFCLETYP            , &
                        REFCLENUM              &
                  USING CLICIECLE OF VCLC_CLI, &
                        CLIREFTYP OF VCLC_CLI, &
                        CLICLE    OF VCLC_CLI  &
                ORDERBY RADCLE
END
@ENDIF

BUILD list detail

@IF FORMAT

*************************** Adresses supplémentaires **************************x
*                                                                              *
* No adresse....: xxxx    Facturation: xxx  Livraison: xxx  Etat compte.: xxx  *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                                         *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Code Postal...: xxxxxxxxxx                                                   *
* Téléphone.....: xxxxxxxxxxxxx                                                *
* Fax...........: xxxxxxxxxxxxx                                                *
*                                                                              *
********************************************************************************
@ENDIF
