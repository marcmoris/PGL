;/T/ Consultation des notes générales 
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 10 avril 1995
;
;    Description..: Permet de visionner et de choisir une note générale.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar007h NOMODE NOACTION FIELDMARK &
               FROM 11,14 TO 23,80 &
               HELP POPUP FROM 4,9 TO 20,72 &
               RECEIVING  ARCDE_NOTE
 
                    

USE ar007h.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE ARCDE_NOTE          MASTER

FILE ARNOTE_GENERALE     PRIMARY &
                        OCCURS 10

  ACCESS SEQUENTIAL &
         ORDERBY NGECLE
  
FILE ARCDE_NOTE          DESIGNER &
                        ALIAS ARCDE_NOTE2


FILE ARNOTE_GEN_DET      DESIGNER


TEMPORARY T_NUMLIG    INTEGER SIGNED SIZE 2

USE ushilite NOLIST     

DRAW 1,1 TO 13,56

USE ustitoff NOLIST           

SKIP TO 2

ALIGN (3,3,6)(,,12)

CLUSTER OCCURS WITH ARNOTE_GENERALE

FIELD NGECLE    OF ARNOTE_GENERALE HIDDEN LABEL ">>"
FIELD NGETIT    OF ARNOTE_GENERALE 

CLUSTER


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_NUMLIG = 0
  WHILE RETRIEVING ARCDE_NOTE2 &
        VIA     CIECLE   , &
                CMDCLE   , &
                CMDAJT   , &
                CDECLE     &
        USING   CIECLE    OF ARCDE_NOTE, &
                CMDCLE    OF ARCDE_NOTE, &
                CMDAJT    OF ARCDE_NOTE, &
                CDECLE    OF ARCDE_NOTE  &
        ORDERBY CIECLE   , &
                CMDCLE   , &
                CMDAJT   , &
                CDECLE   , &
                CDNSEQ
  BEGIN
     IF T_NUMLIG < CDNSEQ OF ARCDE_NOTE2
     THEN LET T_NUMLIG = CDNSEQ OF ARCDE_NOTE2         
  END

  WHILE RETRIEVING ARNOTE_GEN_DET &
        VIA      NGECLE &
        USING    NGECLE OF ARNOTE_GENERALE &
        ORDERBY  NGECLE, &
                 NGDSEQ
  BEGIN
     LET T_NUMLIG = T_NUMLIG + 1
     LET CIECLE OF ARCDE_NOTE2 = CIECLE OF ARCDE_NOTE
     LET CMDCLE OF ARCDE_NOTE2 = CMDCLE OF ARCDE_NOTE
     LET CMDAJT OF ARCDE_NOTE2 = CMDAJT OF ARCDE_NOTE
     LET CDECLE OF ARCDE_NOTE2 = CDECLE OF ARCDE_NOTE
     LET CDNSEQ OF ARCDE_NOTE2 = T_NUMLIG
     LET CDNDSC OF ARCDE_NOTE2 = NGDDSC OF ARNOTE_GEN_DET
     LET CDNIMP OF ARCDE_NOTE2 = NGDIMP OF ARNOTE_GEN_DET
     PUT ARCDE_NOTE2 RESET
  END             
  RETURN
END

PROCEDURE ENTRY  
  DISABLE

PROCEDURE APPEND 
  DISABLE

PROCEDURE DELETE
  DISABLE


PROCEDURE UPDATE
  DISABLE

BUILD 
@IF FORMAT
@ENDIF
