;/T/ Employés - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 8 septembre 1993
;
;    Description..: Cet écran vérifie si l'employé reçu en paramètre est
;                   référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp155x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCREFERENCE , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Employé.
;
FILE MCREFERENCE      MASTER

;
; Comptes à payer.
;
FILE CPCPT_A_PAYER    REFERENCE

;
; Paiements.
;
FILE CPPAIEMENT       REFERENCE

;
; Encaissement.
;
FILE CRDEPOT          REFERENCE



;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie si l'employé a des rapports de dépenses.
  ;
  GET CPCPT_A_PAYER VIA REFCIECLE , &
                        REFCLETYP , &
                        REFCLENUM   &
                    USING REFCIECLE OF MCREFERENCE , &
                          REFCLETYP OF MCREFERENCE , &
                          REFCLENUM OF MCREFERENCE   &
                    OPTIONAL
  IF ACCESSOK
  THEN ERROR 477 ; Destruction annulée, cette information est référée par un
                 ; comptes à payer.
  ;
  ; On vérifie si l'employé a reçu un paiement.
  ;
  GET CPPAIEMENT VIA REFCIECLE , &
                     REFCLETYP , &
                     REFCLENUM   &
                 USING REFCIECLE OF MCREFERENCE , &
                       REFCLETYP OF MCREFERENCE , &
                       REFCLENUM OF MCREFERENCE   &
                 OPTIONAL
  IF ACCESSOK
  THEN ERROR 575 ; Destruction annulée, cette information est référée par un
                 ; paiement.

  ;
  ; On vérifie si l'employé a fait un remboursement.
  ;
  GET CRDEPOT VIA REFCIECLE , &
                  REFCLETYP , &
                  REFCLENUM   &
              USING REFCIECLE OF MCREFERENCE , &
                    REFCLETYP OF MCREFERENCE , &
                    REFCLENUM OF MCREFERENCE   &
              OPTIONAL
  IF ACCESSOK
  THEN ERROR 574 ; Destruction annulée, cette information est référée par un
                 ; encaissement.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
