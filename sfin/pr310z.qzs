;/T/ Sommaire des transactions à l'équipement

USE ussetqzs NOLIST
SET FORMFEED

ACCESS GPUNI_TRANS  &
  LINK CIECLE, UNICAT, UNICLE TO &
       CIECLE, UNICAT, UNICLE OF GPUNITE  &
  LINK CIECLE, UNACLE OF GPUNI_TRANS TO &
       CIECLE, UNACLE OF MCUNITE_ADM  &
  LINK CPTCLE OF GPUNI_TRANS TO &
       CPTCLE OF VCPT_DSC  &
  LINK NACCLE OF VCPT_DSC TO &
       NACCLE OF MCNATURE_CTB

CHOOSE CIECLE PARM, &
       UNICAT PARM, &
       UNICLE PARM

DEFINE D-MNT    INTEGER SIZE 4 = 0
DEFINE D-TRDMNT FLOAT   SIZE 8 = TRDMNT OF GPUNI_TRANS

DEFINE D-TRDMNT2 FLOAT   SIZE 8 = TRDMNT OF GPUNI_TRANS IF "C" = CPTCODDC &
                             ELSE 0 - TRDMNT OF GPUNI_TRANS

DEFINE D-TOT FLOAT SIZE 8 = D-TRDMNT

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
                       "Sommaire des transactions à l'équipement"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "PR310"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

SORT ON UNICAT &
     ON UNICLE &
     ON NACCLE &
     ON CPTCLE &
     ON UNACLE

USE ustitstd NOLIST
  KEEP

HEADING AT UNICLE &
  TAB 001 "Équipement :" &
  TAB 013 UNICAT   OF GPUNI_TRANS &
  TAB 016 UNICLE   OF GPUNI_TRANS &
  TAB 025 UNIDSC1  OF GPUNITE &
SKIP 1 &
  TAB 025 UNIDSC2  OF GPUNITE &
SKIP 3

REPORT &
  TAB 005 CPTCLE   OF GPUNI_TRANS HEAD "Compte"      &
  TAB 012 CPTDSCABR   OF VCPT_DSC    HEAD "Description" &
  TAB 033 UNACLE   OF GPUNI_TRANS HEAD "Unite^Adm."  &
  TAB 042 UNANOMABR   OF MCUNITE_ADM HEAD "Description" &
  TAB 063 D-MNT    PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" HEAD "Budget" &
  TAB 080 D-MNT    PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" HEAD "Engagement" &
  TAB 097 D-TRDMNT PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Réel" &
  TAB 114 D-TOT    PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Total"
; &
;  TAB 168 D-MNT    PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Ecart"

;   xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxx,xxx,xxx.xxx xxxx,xxx,xxx.xxx xxxx,xxx,xxx.xxx xxxx,xxx,xxx.xxx xxxx,xxx,xxx.xxx
FOOT AT NACCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
;  TAB 155 "---------------" &
SKIP &
  TAB 015 "Total" NACDSCFRA OF MCNATURE_CTB &
  TAB 063 D-MNT    SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D-MNT    SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D-TRDMNT SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 114 D-TOT    SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
;  TAB 155 D-MNT    SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP 2



FOOT AT UNICLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
  TAB 114 "---------------" &
;  TAB 155 "---------------" &
SKIP &
  TAB 015 "Grand total Équipement:" &
  TAB 063 D-MNT     SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D-MNT     SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D-TRDMNT2 SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
  TAB 114 D-TOT     SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
;  TAB 155 D-MNT    SUBTOTAL PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP PAGE

BUILD pr310z
