;/T/ Liste des pièces à commander
;
;/P/ Programmeur...: Patrick Langlois
;    Date Création.: 5 Aôut 1999
;
;    Description...: Impression des pièces à commander
;
;    Paramètres....: Compagnie
;                    Code de produit (Borne de début)
;                    Code de produit (Borne de fin)
;                    Catégorie       (Une, plusieurs ou tous)
;                    Type            (D=divers, F=frais,I=inventorié,
;                                     N=non inventorié,<retour>=tous)
;
;    ---------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 20 avril 2000.
;    Description.......: Ajout de l'information relative aux sorties de produits enregistrées au cours des 12 derniers mois.
;
;    Référence.........: Demande de changement 000042.
;
;    Signet............: MP-00-04-20_D42.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST

; ---------------------------------------------------------------------------------------
; MP-00-04-20_D42.

ACCESS *WAR346A LINK CIECLE OF WAR346A    , & ; Fichier des quantités (sorties) cumulées
                     PRDCLE OF WAR346A      &
                  TO CIECLE               , &
                     PRDCLE OF ARPRODUIT      ; Table des produits
; ---------------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Listes des pièces à commander"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR346Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM
;
; Variable de travail
;
DEFINE D_DSC_PRD CHARACTER SIZE 35 = PRDDSC1 OF ARPRODUIT


SELECT IF D_QTEACOM of WAR346A > 0

;
; Tri
;
SORT ON CIECLE OF ARPRODUIT    & ; Compagnie
     ON PRDCLE OF ARPRODUIT    & ; Code de produit
     ON ENTCLE OF WAR346A
;
; Entête standard du rapport (132 colonnes)
;
USE ustitstd NOLIST
  SKIP 2 &
 TAB 001 "                                                                Qté         Point       Sortie      Qté en     Qté en      Qté à   " &
  SKIP 1 &
 TAB 001 "No produit   Description                         Ent. Local   maximum     Commande     (12 mois)   commande  Inventaire  commander " &
  SKIP 1 &
 TAB 001 "------------ ----------------------------------- ---- ----- ------------ ---------- ------------- ---------- ----------- ----------" &
 SKIP
;
; Détail du rapport
;
REPORT                               &
 TAB 001 PRDCLE      OF ARPRODUIT    &
         PRINT AT PRDCLE             &
 TAB 014 D_DSC_PRD                   &
         PRINT AT PRDCLE             &
 TAB 050 ENTCLE      OF WAR346A      &
 TAB 055 ENLCLE      OF WAR346A      &
 TAB 061 PREQTEMAX   OF WAR346A      &
         PICTURE "^^^,^^^.^^^ "      &
         TRAILING SIGN "-"           &
         SIGNIFICANCE 6              &
         NOBWZ                       &
 TAB 074 PREPOICOM   OF WAR346A      &
         PICTURE "^^,^^^.^^^ "       &
         TRAILING SIGN "-"           &
         SIGNIFICANCE 6              &
         NOBWZ                       &
 TAB 085 T_PRHQTETRS OF WAR346A      & ; MP-00-04-20_D42.
         PICTURE "^,^^^,^^^.^^^"     &
         SIGNIFICANCE 5              &
         NOBWZ                       &
 TAB 099 PREQTECOM   OF WAR346A      &
         PICTURE "^^,^^^.^^^"        &
         SIGNIFICANCE 5              &
         NOBWZ                       &
 TAB 110 PREQTEPHY   OF WAR346A      &
         PICTURE "^^,^^^.^^^ "       &
         TRAILING SIGN "-"           &
         SIGNIFICANCE 6              &
         NOBWZ                       &
 TAB 122 D_QTEACOM                   &
         PICTURE "^^,^^^.^^^"        &
         SIGNIFICANCE 5              &
         NOBWZ


BUILD ar346z


@IF FORMAT
                                                                Qté         Point       Sortie      Qté en     Qté en      Qté à
No produit   Description                         Ent. Local   maximum     Commande     (12 mois)   commande  Inventaire  commander
------------ ----------------------------------- ---- ----- ------------ ---------- ------------- ---------- ----------- ----------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999
             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999
             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999
             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxx xxxxx 999,999.999- 9,999.999- 9,999,999.999 99,999.999 99,999.999- 99,999.999

@ENDIF
