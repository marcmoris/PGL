;/T/ Liste des produits / Sommaire
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 4 mai 1994
;
;    Description...: Ce rapport permet d'établir la liste des produits.
;
;    Paramètres....: Compagnie 
;                    Code de produit (Borne de début)
;                    Code de produit (Borne de fin)
;                    Catégorie       (Une, plusieurs ou tous)
;                    Type            (D=divers, F=frais,I=inventorié, 
;                                     N=non inventorié,<retour>=tous)
;                    Statut          (A=actif,I=inactif,<retour>=tous)
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS ARPRODUIT   &
  ;
  ; Accès aux cumuls par produit
  ;
 LINK CIECLE OF ARPRODUIT,       &
      PRDCLE OF ARPRODUIT        &
   TO CIECLE             ,       &
      PRDCLE OF VPRD_01          & 

  ;
  ; Statut du produit
  ;
 LINK ("PRDSTU",                 &
        PRDSTU OF ARPRODUIT)     &
    TO XELNOM,                   &
       CBICLE OF VCBI_DSC   ALIAS A_CBI_PRDSTU OPTIONAL & 

  ;
  ; Type de produit
  ;
 LINK ("PRDTYP",                 &
        PRDTYP OF ARPRODUIT)     &
    TO XELNOM,                   &
       CBICLE OF VCBI_DSC   ALIAS A_CBI_PRDTYP OPTIONAL & 

  ;
  ; Catégorie du produit
  ;
 LINK ("PRDCAT",                 &
        PRDCAT OF ARPRODUIT)     &
    TO XELNOM,                   &
       CBICLE OF VCBI_DSC   ALIAS A_CBI_PRDCAT OPTIONAL 
 


CHOOSE CIECLE PARM,       & ; Compagnie du menu
       PRDCLE PARM RANGE, & ; Code de produit
       PRDCAT PARM          ; Catégorie de produit


DEFINE D_PRDTYP CHARACTER SIZE 6 = PARM UPSHIFT ; Type de produit

DEFINE D_PRDSTU CHARACTER SIZE 2 = PARM UPSHIFT ; Statut du produit


SELECT ARPRODUIT IF (    ( 0 <> INDEX (D_PRDTYP,PRDTYP) OR D_PRDTYP = "") &
                     AND ( 0 <> INDEX (D_PRDSTU,PRDSTU) OR D_PRDSTU = "") &
                    )


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste des produits / Sommaire"

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR341Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


 ; Statuts de produit demandés
 ;
DEFINE D_CBIDSC1 CHARACTER SIZE 15 = "Actif " &
  IF 0 <> INDEX (D_PRDSTU,"A") OR  D_PRDSTU = "" &
    ELSE ""

DEFINE D_CBIDSC2 CHARACTER SIZE 15 = "Inactif "&
  IF 0 <> INDEX (D_PRDSTU,"I") OR  D_PRDSTU = "" &
    ELSE ""

DEFINE D_CBIDSCDEM CHARACTER SIZE 70 = &
  PACK (TRUNCATE(D_CBIDSC1) + " " + &
        TRUNCATE(D_CBIDSC2))  


DEFINE D_CBIDSC_PRDTYP CHARACTER SIZE 15 = CBIDSC OF A_CBI_PRDTYP
DEFINE D_CBIDSC_PRDSTU CHARACTER SIZE 10 = CBIDSC OF A_CBI_PRDSTU

SORT ON CIECLE OF ARPRODUIT &  ; Compagnie
     ON PRDCLE OF ARPRODUIT    ; Code de produit

USE ustit180 NOLIST
  SKIP 2 &
 TAB 001 "Statuts demandés..:" &
 TAB 021 D_CBIDSCDEM           &
  SKIP 2 &
 TAB 001 "Code de"             &                                                                                              
 TAB 102 "Unité   Quantité      Quantité      Quantité      Dernier       Prix" &
  SKIP 1 &
 TAB 001 "produit      Description                    Type              Catégorie                 Statut" &
 TAB 102 "Mesure  en demande    en commande   en main       coûtant       moyen       " &
  SKIP 1 &
 TAB 001 "------------ ------------------------------ ----------------- ------------------------- ------------" &
 TAB 102 "------  ------------  ------------  ------------  ------------  ------------" &
  SKIP 2

REPORT &
 TAB 001 PRDCLE  OF ARPRODUIT    &
 TAB 014 PRDDSC1 OF ARPRODUIT    &
         PIC "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
 TAB 045 PRDTYP  OF ARPRODUIT    &
 TAB 047 D_CBIDSC_PRDTYP         &
 TAB 063 PRDCAT  OF ARPRODUIT    &
 TAB 068 CBIDSC  OF A_CBI_PRDCAT &
 TAB 089 PRDSTU  OF ARPRODUIT    &
 TAB 091 D_CBIDSC_PRDSTU         &
 TAB 103 PRDUNI  OF ARPRODUIT    &
 TAB 110 V_PRDQTEDEM OF VPRD_01  &
         PICTURE "^^^,^^^.^^^ "  & 
         TRAILING SIGN "-"       &
         SIGNIFICANCE 6          &
 TAB 124 V_PRDQTECOM OF VPRD_01  &
         PICTURE "^^^,^^^.^^^ "  & 
         TRAILING SIGN "-"       &
         SIGNIFICANCE 6          &
 TAB 138 V_PRDQTEPHY OF VPRD_01  &
         PICTURE "^^^,^^^.^^^ "  &
         TRAILING SIGN "-"       &
         SIGNIFICANCE 6          &
 TAB 152 V_PRDDERCOU OF VPRD_01  &
         PICTURE "^^,^^^.^^^^ "  &
         TRAILING SIGN "-"       &
         SIGNIFICANCE 7          &
 TAB 166 V_PRDCOUMYN OF VPRD_01  &
         PICTURE "^^,^^^.^^^^ "  &
         TRAILING SIGN "-"       &
         SIGNIFICANCE 7          

BUILD ar341z

@IF FORMAT 
                                                                                                   1         1         1         1         1         1         1         1         1
         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

AR341                                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                 Page: 99
99/99/99 HH:MM xxxxxxxxxxxx                                        Liste des produits / Sommaire      
                                                     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 

Statuts demandés..: xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx 

Code de                                                                                              Unité   Quantité      Quantité      Quantité      Dernier       Prix
produit      Description                    Type              Catégorie                 Statut       mesure  en demande    en commande   en main       coûtant       moyen
------------ ------------------------------ ----------------- ------------------------- ------------ ------  ------------  ------------  ------------  ------------  ------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx x xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx x xxxxxxxxxx  xxxx   999,999.999-  999,999.999-  999,999.999-  99,999.9999-  99,999.9999-
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx x xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx x xxxxxxxxxx  xxxx   999,999.999-  999,999.999-  999,999.999-  99,999.9999-  99,999.9999-
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx x xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx x xxxxxxxxxx  xxxx   999,999.999-  999,999.999-  999,999.999-  99,999.9999-  99,999.9999-


@ENDIF
