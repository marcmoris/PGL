;/T/ Gestion des immobilisations
;
;/P/ Programmeur..: Steeve Duguay
;    Réalisé le...: 19 février 1995
;
;    Description..: Immobilisation - Information comptable détaillé (réel)
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001k ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        IMIMM_BDG_CTB, &
                        IMIMMOBILISATION

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CMDCLEFND CHARACTER SIZE  9 ; Appel de la commande.
TEMPORARY T_CMDAJTFND CHARACTER SIZE  3 ; Appel de la commande
TEMPORARY T_FLGMNU    CHARACTER SIZE 1  INITIAL "C"


USE ussectmp NOLIST     ; Variable pour la securite
    "IM001K"            ; Nom de l'écran

USE im001k.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;----------------------------
; Fichier budget comptable
;----------------------------
FILE IMIMM_BDG_CTB          MASTER


;------------------------------------
; Fichier fiche de sous-projet
;------------------------------------
FILE IMIMMOBILISATION       MASTER


;--------------------------------
; Fichier budget comptable réel
;--------------------------------
FILE VHIP_02                PRIMARY &
                            OCCURS 13 &
                            NOITEMS

  ACCESS  VIA     CPTCLE, &
                  CIECLE, &
                  UNACLE, &
                  IMMCLE  &
          USING   CPTCLE OF IMIMM_BDG_CTB,  &
                  CIECLE OF IMIMM_BDG_CTB,  &
                  UNACLE OF IMIMM_BDG_CTB,  &
                  IMMCLE OF IMIMM_BDG_CTB   &
          ORDERBY PECCLE


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF IMIMM_BDG_CTB


;-------------------------------
; Fichier unité administrative
;-------------------------------
FILE MCUNITE_ADM            REFERENCE

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE OF IMIMM_BDG_CTB, &
                UNACLE OF IMIMM_BDG_CTB


;-------------------------------------------------------------------------------
;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING REFERENCE
  ACCESS VIA CIENMC USING "1"


;----------------------------
; Fichier pour le fournisseur
;----------------------------
FILE VFOC_FOU REFERENCE

  ACCESS VIA   FOUCIECLE, &
               FOUCLE,    &
               FOCCIECLE  &
         USING REFCIECLE OF VHIP_02, &
               REFCLENUM OF VHIP_02, &
               HISCIECLE OF VHIP_02

;-------------------------------------------------------------------------------
;
DEFINE D_MNTTOT      FLOAT SIZE 8 = IBCMNTENG OF IMIMM_BDG_CTB + &
                                    IBCMNTREE OF IMIMM_BDG_CTB

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_CIECLE        CHARACTER SIZE 4
TEMPORARY T_IMMCLE        CHARACTER SIZE 12
TEMPORARY T_CPTCLE        CHARACTER SIZE 6
TEMPORARY T_UNACLE        CHARACTER SIZE 8
TEMPORARY T_FOUCIECLE     CHARACTER SIZE 4
TEMPORARY T_EMPCIECLE     CHARACTER SIZE 4
TEMPORARY T_CLICIECLE     CHARACTER SIZE 4
TEMPORARY T_PECCLEMNU     CHARACTER SIZE 4
TEMPORARY T_PECCLECOU     CHARACTER SIZE 4
TEMPORARY T_CIECLEFND     CHARACTER SIZE 4
TEMPORARY T_ECJCLENUMFND  CHARACTER SIZE 8
TEMPORARY T_ECJCLESEQFND  CHARACTER SIZE 2
TEMPORARY T_FOUCLEFND     CHARACTER SIZE 6
TEMPORARY T_CAPCLEFND     CHARACTER SIZE 15
TEMPORARY T_FACCLEFND     CHARACTER SIZE 12
TEMPORARY T_LBQCPTENCFND  CHARACTER SIZE 6
TEMPORARY T_PAMCLEFND     CHARACTER SIZE 7
TEMPORARY T_CARCLEFND     CHARACTER SIZE 15
TEMPORARY T_DPOCLEFND     CHARACTER SIZE 12
TEMPORARY T_RECCLEFND     CHARACTER SIZE 12

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

DRAW 8,1 TO 8,132

SKIP TO 3

TITLE &
@IF FRANCAIS
      " Information comptable détaillée " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,35)
FIELD IMMCLE    OF IMIMMOBILISATION                   &
      DISPLAY

FIELD IMMDSC    OF IMIMMOBILISATION                   &
      DISPLAY

SKIP 1



TITLE &
@IF FRANCAIS
      "Compte Descrption            Unité     Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Engagé" &
@ELSE
      " %%% " &
@ENDIF
      AT ,75

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,98

TITLE &
@IF FRANCAIS
      "Total" &
@ELSE
      " %%% " &
@ENDIF
      AT ,119

SKIP

ALIGN (3,2,3) (,,10) (,,32) (,,42) (,,67) (,,88) (,,110)

FIELD CPTCLE OF IMIMM_BDG_CTB                         &
      LABEL " "                                       &
      HIDDEN                                          &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD UNACLE OF IMIMM_BDG_CTB                         &
      DISPLAY

FIELD UNANOMABR OF MCUNITE_ADM &
      DISPLAY

FIELD IBCMNTENG OF IMIMM_BDG_CTB

FIELD IBCMNTREE OF IMIMM_BDG_CTB

FIELD D_MNTTOT                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5


SKIP TO 9

TITLE &
@IF FRANCAIS
"Période   Projet   Sous-projet    Mnt débit      Mnt crédit     Module Réf.   Nom" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Document        Type" &
@ELSE
      " %%% " &
@ENDIF
      AT ,103

SKIP
ALIGN (2,3,4)(,,13)(,,26)(,,34)(,,51)(,,69)(,,74)(,,81)(,,103)(,,120)
CLUSTER OCCURS WITH VHIP_02 ID BASE 10

FIELD PECCLE OF VHIP_02          &
      LABEL " " &
      HIDDEN    &
      DISPLAY

FIELD PRJCLE OF VHIP_02          &
      DISPLAY

FIELD PRACLE OF VHIP_02          &
      DISPLAY

FIELD HIPMNTDBT OF VHIP_02      &
      DISPLAY

FIELD HIPMNTCRT OF VHIP_02      &
      DISPLAY

FIELD HISCODMOD OF VHIP_02     &
      DISPLAY

FIELD REFCLENUM OF VHIP_02     &
      DISPLAY

FIELD FOUNOMABR OF VFOC_FOU &
      DISPLAY

FIELD HISNUMDOC OF VHIP_02

FIELD HISTYPECR OF VHIP_02     &
      DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé.
;
;
PROCEDURE INPUT HISNUMDOC
BEGIN
  IF 0 <> INDEX ( FIELDTEXT, "*" )
  THEN BEGIN
    ;
    ; Il faut déterminer la compagnie des chartes de clients, employés et de
    ; fournisseur pour que l'appel des écrans suivant fonctionne correctement.
    ;
    IF HLDCHTCLI OF MCHOLDING = "1"
    THEN LET T_CLICIECLE = HISCIECLE OF VHIP_02
    ELSE LET T_CLICIECLE = HLDCHRSUB OF MCHOLDING

    IF HLDCHTEMP OF MCHOLDING = "1"
    THEN LET T_EMPCIECLE = HISCIECLE OF VHIP_02
    ELSE LET T_EMPCIECLE = HLDCHRSUB OF MCHOLDING

    IF HLDCHTFOU OF MCHOLDING = "1"
    THEN LET T_FOUCIECLE = HISCIECLE OF VHIP_02
    ELSE LET T_FOUCIECLE = HLDCHRSUB OF MCHOLDING
    ;
    ; Réception
    ;
    IF HISCODMOD OF VHIP_02 = "AR"
    THEN BEGIN

      LET T_CIECLEFND = HISCLE1 OF VHIP_02
      LET T_RECCLEFND = HISCLE2 OF VHIP_02

      RUN SCREEN ar009 MODE F &
                       PASSING T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_FOUCIECLE, &
                               T_RECCLEFND
    END
    ;
    ; Journal général.
    ;
    IF HISCODMOD OF VHIP_02 = "GL"
    THEN BEGIN
      LET T_CIECLEFND    = HISCLE1 OF VHIP_02
      LET T_ECJCLENUMFND = HISCLE2 OF VHIP_02
      LET T_ECJCLESEQFND = HISCLE3 OF VHIP_02
      RUN SCREEN gl001 MODE F &
                       PASSING T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_FLGMNU   , &
                               T_ECJCLENUMFND, &
                               T_ECJCLESEQFND
    END
    ;
    ; Factures des comptes à payer.
    ;
    IF     HISCODMOD OF VHIP_02 = "CP" &
       AND (    HISTYPECR OF VHIP_02 = "FA" &
             OR HISTYPECR OF VHIP_02 = "CR" &
             OR HISTYPECR OF VHIP_02 = "NC" &
             OR HISTYPECR OF VHIP_02 = "AJ" )
    THEN BEGIN
      LET T_CIECLEFND = HISCIECLE OF VHIP_02
      LET T_FOUCIECLE = HISCLE1   OF VHIP_02
      LET T_FOUCLEFND = HISCLE2   OF VHIP_02
      LET T_CAPCLEFND = HISCLE3   OF VHIP_02
      RUN SCREEN cp003 MODE F &
                       PASSING T_FOUCIECLE, &
                               T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_FOUCLEFND, &
                               T_CAPCLEFND
    END
    ;
    ; Factures des comptes à payer.
    ;
    IF     HISCODMOD OF VHIP_02 = "CP" &
       AND (    HISTYPECR OF VHIP_02 = "FR" )
    THEN BEGIN
      LET T_CIECLEFND = HISCIECLE OF VHIP_02
      LET T_FOUCIECLE = HISCLE1   OF VHIP_02
      LET T_FOUCLEFND = HISCLE2   OF VHIP_02
      LET T_FACCLEFND = HISCLE3   OF VHIP_02
      RUN SCREEN ar011 MODE F &
                     PASSING T_CIECLEFND, &
                             T_CIENOM,    &
                             T_PECCLEMNU, &
                             T_FOUCIECLE, &
                             T_FOUCIECLE, &
                             T_FOUCLEFND, &
                             T_FACCLEFND
    END
    ;
    ; Rapports de dépenses des comptes à payer.
    ;
    IF     HISCODMOD OF VHIP_02 = "CP" &
       AND (    HISTYPECR OF VHIP_02 = "RD" &
             OR HISTYPECR OF VHIP_02 = "AR" )
    THEN BEGIN
      LET T_CIECLEFND = HISCLE1 OF VHIP_02
      LET T_CAPCLEFND = HISCLE3 OF VHIP_02
      RUN SCREEN cp005 MODE F &
                       PASSING T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_FOUCIECLE, &
                               T_EMPCIECLE, &
                               T_CAPCLEFND
    END
    ;
    ; Paiements des comptes à payer.
    ;
    IF     HISCODMOD OF VHIP_02 = "CP" &
       AND HISTYPECR OF VHIP_02 = "CH"
    THEN BEGIN
      LET T_CIECLEFND    = HISCLE1 OF VHIP_02
      LET T_LBQCPTENCFND = HISCLE2 OF VHIP_02
      LET T_PAMCLEFND    = HISCLE3 OF VHIP_02
      RUN SCREEN cp008 MODE F &
                       PASSING T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_FOUCIECLE, &
                               T_EMPCIECLE, &
                               T_LBQCPTENCFND, &
                               T_PAMCLEFND
    END
    ;
    ; Annulation de paiements des comptes à payer.
    ;
    IF     HISCODMOD OF VHIP_02 = "CP" &
       AND HISTYPECR OF VHIP_02 = "CA"
    THEN BEGIN
      LET T_CIECLEFND    = HISCLE1 OF VHIP_02
      LET T_LBQCPTENCFND = HISCLE2 OF VHIP_02
      LET T_PAMCLEFND    = HISCLE3 OF VHIP_02
      RUN SCREEN cp009 MODE F &
                       PASSING T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_FOUCIECLE, &
                               T_EMPCIECLE, &
                               T_LBQCPTENCFND, &
                               T_PAMCLEFND
    END
    ;
    ; Factures des comptes à recevoir.
    ;
    IF     HISCODMOD OF VHIP_02 = "CR" &
       AND (    HISTYPECR OF VHIP_02 = "FA" &
             OR HISTYPECR OF VHIP_02 = "CR" &
             OR HISTYPECR OF VHIP_02 = "NC" &
             OR HISTYPECR OF VHIP_02 = "AJ" )
    THEN BEGIN
      LET T_CIECLEFND = HISCLE1 OF VHIP_02
      LET T_CARCLEFND = HISCLE2 OF VHIP_02
      RUN SCREEN cr003 MODE F &
                       PASSING T_CLICIECLE, &
                               T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_CARCLEFND
    END
    ;
    ; Encaissements des comptes à recevoir.
    ;
    IF     HISCODMOD OF VHIP_02 = "CR" &
       AND HISTYPECR OF VHIP_02 = "EN"
    THEN BEGIN
      LET T_CIECLEFND = HISCLE1 OF VHIP_02
      LET T_DPOCLEFND = HISCLE2 OF VHIP_02
      RUN SCREEN cr007 MODE F &
                       PASSING T_CLICIECLE, &
                               T_EMPCIECLE, &
                               T_FOUCIECLE, &
                               T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_DPOCLEFND
    END
    ;
    ; Mauvaises créances des comptes à recevoir.
    ;
    IF     HISCODMOD OF VHIP_02 = "CR" &
       AND HISTYPECR OF VHIP_02 = "MC"
    THEN BEGIN
      LET T_CIECLEFND = HISCLE1 OF VHIP_02
      LET T_DPOCLEFND = HISCLE2 OF VHIP_02
      RUN SCREEN cr009 MODE F &
                       PASSING T_CLICIECLE, &
                               T_CIECLEFND, &
                               T_CIENOM   , &
                               T_PECCLEMNU, &
                               T_PECCLECOU, &
                               T_DPOCLEFND
    END
  END
  LET FIELDTEXT = ""
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

