;/P/ Liste des blocs par fournisseur par type de granit
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme et pour que le rapport sorte par type de
;		      type de granit au lieu de par carriere
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC &
 LINK CIECLE OF IFBLOC, &
      FOUCLE OF IFBLOC  &
   TO FOUCIECLE, &
      FOUCLE                    OF CPFOURNISSEUR OPTIONAL

CHOOSE CIECLE    PARM, &
       TGRCODGRA PARM

DEFINE TMP-DAT-DEB    DATE = PARM
DEFINE TMP-DAT-FIN    DATE = PARM

; LE SELECT PREND LES ENREGISTREMENTS REPONDANT AUX CRITERES SUIVANT :
; - LA DATE DU BLOC EST COMPRISE DANS LES LIMITES ENTREES SI IL Y EN A UNE

SELECT IF 0 <> SIZE(TRUNC(FOUCLE OF IFBLOC)) &
          AND (    (     BLCDAT >= TMP-DAT-DEB &
                     AND BLCDAT <= TMP-DAT-FIN &
                   ) &
                OR (     TMP-DAT-DEB = 0 &
                     AND TMP-DAT-FIN = 0 &
                   ) &
              )
;

DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""

SORT ON FOUCLE OF IFBLOC &
     ON D_NUMBLC  &
     ON D_NUMCOM  &
     ON CATCLE OF IFBLOC &
     ON COEDATDEB OF IFBLOC


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" &
                        IF     TMP-DAT-DEB = 0 &
                           AND TMP-DAT-FIN = 0 &
        ELSE "Entre le : " + ASCII( TMP-DAT-DEB , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[7:2] + &
             " et : "      + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF311"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 3 "NO"                    &
             TAB 58 "NO"                   &
             TAB 71 "DATE"                 &
             TAB 83 "DIMENSIONS "          &
             TAB 97 "TOTAL"                &
             TAB 110 "T/M"                  &
             TAB 118 "NO"                   &
             TAB 127 "CAT"                  &
             TAB 131 "COUL"                 &
             TAB 136 "QUAL"                 &
SKIP 1 &
             TAB 3   "FOURNISSEUR"                &
             TAB 57   "BLOC"                &
             TAB 71  "PROD"                &
             TAB 79  "LONG  HAUT  LARG"      &
             TAB 99  "M3"                  &
             TAB 117  "COM."             &
SKIP 1 &
             TAB 3 "_______________________________________________"      &
             TAB 53  "______________"      &
             TAB 69 "________"             &
             TAB 78 "_________________"       &
             TAB 97 "______"               &
             TAB 108 "______  _________  ___ ____ ____"  &

SKIP 2
REPORT  FOUCLE OF IFBLOC PRINT AT FOUCLE OF IFBLOC &
        FOUNOM OF CPFOURNISSEUR PRINT AT FOUCLE OF IFBLOC &
        D_NUMBLC                           &
        BLCDAT       OF IFBLOC             &
        BLCVENDULONG OF IFBLOC             &
        BLCVENDUHAU  OF IFBLOC             &
        BLCVENDULAR  OF IFBLOC             &
        BLCM3TOT     OF IFBLOC             &
        BLCTFINAL    OF IFBLOC             &
        D_NUMCOM                              &
        CATCLE         OF IFBLOC           &
        CLRCLE       OF IFBLOC             &
        QLTCLE       OF IFBLOC             

FOOTING AT FOUCLE OF IFBLOC &

        TAB 53 "______________"            &
        TAB 93 "__________"         &
        TAB 104 "__________"         &
SKIP 1  &
        TAB 53 COUNT "Blocs"         &
        TAB 80 "Sous-total:"        &
        TAB 94 BLCM3TOT  SUBTOTAL PICTURE "^^,^^^.^^" &
        TAB 105 BLCTFINAL SUBTOTAL PICTURE "^^,^^^.^^" &
SKIP 2
FINAL FOOTING  &
        TAB 53 "______________"            &
        TAB 93 "__________"         &
        TAB 104 "__________"         &
SKIP 1  &
        TAB 53 COUNT "Blocs"         &
        TAB 80 "Total:"             &
        TAB 93 BLCM3TOT  SUBTOTAL PICTURE "^^^,^^^.^^" &
        TAB 104 BLCTFINAL SUBTOTAL PICTURE "^^^,^^^.^^" &
SKIP 2

BUILD  if311z
