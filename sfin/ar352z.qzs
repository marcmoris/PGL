;/T/ Historique des transactions par produit
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 25 mai 1994
;
;    Description...: Ce rapport permet de présenter l'historique des 
;                    transactions sur les produits.
;
;    Paramètres....: Compagnie         (Compagnie du menu)
;                    Date d'historique (Borne de début)
;                    Date d'historique (Borne de fin)
;                    Type de transactions (01 = commandes d'achat, 02 = réceptions,
;                                          03 = retours,           04 = factures,
;                                          05 = entrée produits,   06 = sortie produits,
;                                          07 = ajustements,       08 = transferts,
;                                          09 = inventaire physique
;                                          <retour> = tous)
;                    Numéro produit    (Un, plusieurs ou tous)
;                    Numéro entrepôt   (Un, plusieurs ou tous) 
;
;/M/                 Caroline Kaine, 18 nov 96, Format passe de 180 a 175
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS ARPRD_HISTO   &
  LINK CIECLE OF ARPRD_HISTO, &
       PRDCLE OF ARPRD_HISTO, &
       ENTCLE OF ARPRD_HISTO  &
    TO CIECLE, &
       PRDCLE, &
       ENTCLE OF VPRE_PRD    &

  LINK CIECLE OF ARPRD_HISTO, &
       ENTCLE OF ARPRD_HISTO  &
    TO CIECLE, &
       ENTCLE OF ARENTREPOT   &

  LINK PRHUSRCRE OF ARPRD_HISTO &
    TO USRCLE OF GSUSAGER    OPTIONAL &     

  LINK ("PRHTYP",                  &
         PRHTYP OF ARPRD_HISTO)    &
    TO XELNOM,                     &
       CBICLE OF VCBI_DSC    ALIAS A_CBI_PRHTYP OPTIONAL 

    

CHOOSE CIECLE PARM, & ; Compagnie du menu
       ENTCLE PARM, & ; Entrepôt
       PRDCLE PARM, & ; Numéro de produit
       PRDTYP PARM    ; Type de produit        

DEFINE D_PRHTYP CHARACTER SIZE 20 = PARM UPSHIFT

DEFINE D_PRHDATDEB DATE = PARM  ; Date d'historique début
DEFINE D_PRHDATFIN DATE = PARM  ; Date d'historique fin


SELECT ARPRD_HISTO IF    ((0 <> INDEX(D_PRHTYP,PRHTYP)) OR (D_PRHTYP = "")) &
                     AND PRHDATCRE OF ARPRD_HISTO >= D_PRHDATDEB  &
                     AND PRHDATCRE OF ARPRD_HISTO <= D_PRHDATFIN


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Historique des transactions par produit"
@ELSE
       "%%%Historique des transactions par produit"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
        "du " + ASCII(D_PRHDATDEB)[1:4] + "/" + &  
                ASCII(D_PRHDATDEB)[5:2] + "/" + &  
                ASCII(D_PRHDATDEB)[7:2] +       &  
       " au " + ASCII(D_PRHDATFIN)[1:4] + "/" + &
                ASCII(D_PRHDATFIN)[5:2] + "/" + &
                ASCII(D_PRHDATFIN)[7:2]
@ELSE
        "%%%du " + ASCII(D_PRHDATDEB)[1:4] + "/" + &  
                ASCII(D_PRHDATDEB)[5:2] + "/" + &  
                ASCII(D_PRHDATDEB)[7:2] +       &  
       " au " + ASCII(D_PRHDATFIN)[1:4] + "/" + &
                ASCII(D_PRHDATFIN)[5:2] + "/" + &
                ASCII(D_PRHDATFIN)[7:2]
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR352Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

 ; Types de transactions demandés
 ;
DEFINE D_CBIDSC1 CHARACTER SIZE 15 = "Commande " &
  IF 0 <> INDEX (D_PRHTYP,"01") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC2 CHARACTER SIZE 15 = "Réception "&
  IF 0 <> INDEX (D_PRHTYP,"02") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC3 CHARACTER SIZE 15 = "Retour " &
  IF 0 <> INDEX (D_PRHTYP,"03") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC4 CHARACTER SIZE 15 = "Facture " &
  IF 0 <> INDEX (D_PRHTYP,"04") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC5 CHARACTER SIZE 15 = "Entrée " &
  IF 0 <> INDEX (D_PRHTYP,"05") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC6 CHARACTER SIZE 15 = "Sortie " &
  IF 0 <> INDEX (D_PRHTYP,"06") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC7 CHARACTER SIZE 15 = "Ajustement " &
  IF 0 <> INDEX (D_PRHTYP,"07") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC8 CHARACTER SIZE 15 = "Transfert " &
  IF 0 <> INDEX (D_PRHTYP,"08") OR  D_PRHTYP = "" &
    ELSE ""

DEFINE D_CBIDSC9 CHARACTER SIZE 15 = "Inventaire " &
  IF 0 <> INDEX (D_PRHTYP,"09") OR  D_PRHTYP = "" &
    ELSE ""



DEFINE D_CBIDSCDEM CHARACTER SIZE 145 = &
  PACK (TRUNCATE(D_CBIDSC1) + " " + &
        TRUNCATE(D_CBIDSC2) + " " + &
        TRUNCATE(D_CBIDSC3) + " " + &
        TRUNCATE(D_CBIDSC4) + " " + &
        TRUNCATE(D_CBIDSC5) + " " + &
        TRUNCATE(D_CBIDSC6) + " " + &
        TRUNCATE(D_CBIDSC7) + " " + &
        TRUNCATE(D_CBIDSC8) + " " + &
        TRUNCATE(D_CBIDSC9))


DEFINE D_CBIDSC_PRHTYP CHARACTER SIZE 15 = CBIDSC OF A_CBI_PRHTYP

DEFINE D_USREMPNOM CHARACTER SIZE 15 = USREMPNOM OF GSUSAGER


DEFINE D_MNTTRS FLOAT SIZE 8 = ROUND (PRHQTETRS OF ARPRD_HISTO * &
                                      PRHDERCOU OF ARPRD_HISTO / 100000) &
  IF  PRHTYP OF ARPRD_HISTO = "01" & ; Commande  
    ELSE PRHVAL OF ARPRD_HISTO

                                          

SORT ON PRDCLE               & ; Numéro de produit
     ON PRHDATCRE D          & ; Date d'historique
     ON PRHTYP               & ; Type de transaction
     ON ENTCLE               & ; Entrepôt
     ON PRHNUMREF              ; Numéro de document


USE ustit175 NOLIST
SKIP 2  &
@IF FRANCAIS
  TAB 001 "Types de transactions.:" &
@ELSE
  TAB 001 "%%%Types de transactions.:" &
@ENDIF
  TAB 025 D_CBIDSCDEM    &
SKIP 2 



HEADING AT PRDCLE &
SKIP 2 &
@IF FRANCAIS
  TAB 001 "No. produit  Description                                                                                                                      " &
@ELSE
  TAB 001 "%%%No. produit  Description                                                                                                                      " &
@ENDIF
SKIP &
  TAB 001 "------------ ------------------------------------------------------------------------------------------------------------------------" &
SKIP &
  TAB 001 PRDCLE  OF ARPRD_HISTO &
  TAB 014 PRDDSC1 OF VPRE_PRD    &
  TAB 055 PRDDSC2 OF VPRE_PRD    &
  TAB 096 PRDDSC3 OF VPRE_PRD    &
SKIP &

@IF FRANCAIS
  TAB 001 "                                                        No.                ------- TRANSACTION -----" &
@ELSE
  TAB 014 "%%%" &
@ENDIF
SKIP &

@IF FRANCAIS
  TAB 001 "  Date     Type               Entrepôt                  document     Per.  quantité        montant    Description                         Usager                    Date    " &
@ELSE
  TAB 014 "%%%" &
@ENDIF
SKIP &
  TAB 001 " aa/mm/jj                                                                                                                                                         aa/mm/jj " &
SKIP &
  TAB 001 "---------- ------------------ ------------------------- ------------ ----- ----------- -------------- ----------------------------------- ---------------------- ---------- " &
SKIP 1

REPORT &
  TAB 001 PRHDATCRE OF ARPRD_HISTO &
  TAB 012 PRHTYP    OF ARPRD_HISTO &
  TAB 015 D_CBIDSC_PRHTYP          &
  TAB 031 ENTCLE    OF ARPRD_HISTO &
  TAB 036 ENTNOM    OF ARENTREPOT  &
  TAB 057 PRHNUMREF OF ARPRD_HISTO &
  TAB 070 PECCLE    OF ARPRD_HISTO &
  TAB 076 PRHQTETRS OF ARPRD_HISTO PICTURE "^^,^^^.^^^ " &
          TRAILING SIGN "-"        &
  TAB 088 D_MNTTRS                 &
    PICTURE "^^,^^^,^^^.^^ " &
    TRAILING SIGN "-"        &
    SIGNIFICANCE 5           &
    BWZ                      &
  TAB 103 PRHDSC    OF ARPRD_HISTO PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  TAB 139 PRHUSRCRE OF ARPRD_HISTO &
  TAB 146 D_USREMPNOM              &
  TAB 162 PRHDATCRE OF ARPRD_HISTO 

BUILD ar352z

@IF FORMAT
AR352                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                                           Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                  Historique des transactions par produit        
                                                   du AAAA/MM/JJ au AAAA/MM/JJ 

Types de transactions.: xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx

No. produit  Description                                                                                                                      
------------ ------------------------------------------------------------------------------------------------------------------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
                                                        No.                ------- TRANSACTION -----
  Date     Type               Entrepôt                  document     Per.  quantité        montant  Description                         Usager                    Date    
---------- ------------------ ------------------------- ------------ ----- --------- -------------- ----------------------------------- ---------------------- ---------- 
AAAA/MM/JJ xx xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx xx-xx 9999.999- 99,999,999.99- xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx AAAA/MM/JJ 
AAAA/MM/JJ xx xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx xx-xx 9999.999- 99,999,999.99- xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx AAAA/MM/JJ 
AAAA/MM/JJ xx xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx xx-xx 9999.999- 99,999,999.99- xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx AAAA/MM/JJ 

No. produit  Description                                                                                                                      
------------ ------------------------------------------------------------------------------------------------------------------------
xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
                                                        No.                ------- INVENTAIRE ------
  Date     Type               Entrepôt                  document     Per.  quantité        montant  Description                              Usager                    Date    
---------- ------------------ ------------------------- ------------ ----- --------- -------------- ---------------------------------------- ---------------------- ---------- 
AAAA/MM/JJ xx xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx xx-xx 9999.999- 99,999,999.99- xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx AAAA/MM/JJ 
AAAA/MM/JJ xx xxxxxxxxxxxxxxx xxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx xx-xx 9999.999- 99,999,999.99- xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxx AAAA/MM/JJ 

@ENDIF

