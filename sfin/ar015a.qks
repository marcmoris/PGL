;/T/ Décompte physique - Détail
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 19 mai 1994
;
;    Description..: Cet écran permet de faire la gestion du détail des décomptes
;                   physiques. Le fichier du décompte physique est généré
;                   automatiquement par une procédure. Une prise d'inventaire
;                   appartient à un et un seul entrepôt.  Le décompte physique
;                   est transféré vers un mouvement d'inventaire par une
;                   procédure.
;
;/M/ Programmeur.......: Francois Richard
;    Date modification : 19 juillet 1999
;    Description.......: Aid\351 la saisie de prdcle
;
;/M/ Programmeur.......: Patrick Langlois
;    Date modification : 25 Août 1999
;    Description.......: Mettre l'écran à 132 colonnes
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar015a ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              FROM 1,1 TO 23,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM,    &
                        T_DECSTUOLD, &
                        ARDECOMPTE


USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la sécurité
    "AR015A"             ; Nom de l'écran

USE ar015a.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

;-------------------------------------------------------------------------------
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_DECSTUOLD CHARACTER SIZE 1  ; Ancienne valeur du statut du décompte.
;-------------------------------------------------------------------------------


FILE ARDECOMPTE  MASTER

FILE ARDEC_DETAIL  PRIMARY NOITEM OCCURS 15
  ACCESS VIA   CIECLE,               &
               ENTCLE,               &
               DECNUMINV,            &
               PRDGRPINV,            &
               PRDCLE                &
         USING T_CIECLEMNU,              &
               ENTCLE    OF ARDECOMPTE,  &
               DECNUMINV OF ARDECOMPTE,  &
               PRDGRPINV OF ARDECOMPTE,  &
               PRDCLE    OF ARDEC_DETAIL &
         REQUEST PRDCLE &
         ORDERBY PRDCLE

  ACCESS VIA   CIECLE,              &
               ENTCLE,              &
               DECNUMINV,           &
               PRDGRPINV            &
         USING T_CIECLEMNU,         &
               ENTCLE    OF ARDECOMPTE,  &
               DECNUMINV OF ARDECOMPTE,  &
               PRDGRPINV OF ARDECOMPTE   &
         ORDERBY PRDCLE

  ITEM CIECLE    OF ARDEC_DETAIL INITIAL T_CIECLEMNU
  ITEM ENTCLE    OF ARDEC_DETAIL INITIAL ENTCLE    OF ARDECOMPTE
  ITEM DECNUMINV OF ARDEC_DETAIL INITIAL DECNUMINV OF ARDECOMPTE
  ITEM PRDGRPINV OF ARDEC_DETAIL INITIAL PRDGRPINV OF ARDECOMPTE


;-------------------------------------------------------------------------------
; Pour valider que l'entrepôt existe
;
FILE ARENTREPOT  REFERENCE
  ACCESS VIA   CIECLE, &
               ENTCLE  &
         USING T_CIECLEMNU, &
               ENTCLE OF ARDECOMPTE


;-------------------------------------------------------------------------------
;
;
FILE VPRE_PRD  REFERENCE &
              OCCURS WITH ARDEC_DETAIL
  ACCESS VIA   CIECLE, &
               PRDCLE, &
               ENTCLE  &
         USING T_CIECLEMNU,            &
               PRDCLE OF ARDEC_DETAIL, &
               ENTCLE OF ARDECOMPTE


;
; Paramètres pour la compagnie consolidée(holding)
;
FILE MCHOLDING  REFERENCE
  ACCESS VIA CIENMC USING "1"

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Compagnie pour écran dépisteur.
TEMPORARY T_ENTCLE    CHARACTER SIZE 4 ; Popup sur entrepôts
TEMPORARY T_PRDCLE    CHARACTER SIZE 12 ; Produit pour écran dépisteur.

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM)

USE usvarprd NOLIST ; Use servant à la saisie de prdcle
;-------------------------------------------------------------------------------
;
USE usdrw132 NOLIST     ; Draw pour les écrans pleine page
USE ustitstd132 NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tous les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Décompte physique " &
@ELSE
      " %%%Décompte physique " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

;-------------------------------------------------------------------------------

SKIP
ALIGN (,3,19) (,,24) (,64,81)

FIELD ENTCLE OF ARDECOMPTE   &
        FIXED

FIELD ENTNOM OF ARENTREPOT &
        FIXED &
        SIZE 36

FIELD PRDGRPINV OF ARDECOMPTE &
        FIXED

SKIP
ALIGN  (,3,19) (,64,81)

FIELD DECNUMINV OF ARDECOMPTE &
        DISPLAY

FIELD DECMNT OF ARDECOMPTE &
        DISPLAY PREDISPLAY &
        REFRESH


SKIP 1
TITLE &
@IF FRANCAIS
       "Produit      Loc.  Description                                                    Coût moyen     Valeur    Qté inf.    Qté réel" &
@ELSE
       "%%%Produit      Description                                Qté inf.    Qté phy." &
@ENDIF
      AT ,3

SKIP


ALIGN (2,2,3) (,,16) (,,22) (,,63)(,,85)(,,97)(,,109)(,,121)
CLUSTER OCCURS WITH ARDEC_DETAIL ID BASE 10

FIELD PRDCLE OF ARDEC_DETAIL &
        HIDDEN &
        REQUIRED NOCHANGE    &
        LABEL " "            &
        LOOKUP ON VPRE_PRD   &
          MESSAGE 1469, &  ; Ce produit n'existe pas pour l'entrepôt du décompte.

        NOTON ARDEC_DETAIL   &
          VIA   CIECLE     , &
                DECNUMINV  , &
                PRDCLE       &
          USING T_CIECLEMNU, &
                DECNUMINV OF ARDECOMPTE, &
                PRDCLE    OF ARDEC_DETAIL &
          MESSAGE 1473 ; Ce produit existe déjà dans ce décompte.

FIELD ENLCLE OF ARDEC_DETAIL    &
        DISPLAY

FIELD PRDDSC1 OF VPRE_PRD       &
        DISPLAY

FIELD PRDDSC2 OF VPRE_PRD       &
        DISPLAY SIZE 21

FIELD PRECOUMYN OF VPRE_PRD     &
        DISPLAY                 &
        OUTPUT -2               &
        PICTURE "^^^,^^^.^^ "   &
        TRAIL "-"               &
        SIGNIF 5

FIELD DEDMNTAJT OF ARDEC_DETAIL &
        DISPLAY                 &
        PICTURE "^^^,^^^.^^ "   &
        TRAIL "-"               &
        SIGNIF 5

FIELD DEDQTEINF OF ARDEC_DETAIL &
        DISPLAY                 &
        PICTURE "^^,^^^.^^^ "   &
        TRAIL "-"               &
        SIGNIF 6                &
        BWZ

FIELD DEDQTEPHY OF ARDEC_DETAIL &
        PICTURE "^^,^^^.^^^"    &
        SIGNIF 5                &
        BWZ


CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
; Cette procédure permet de calculer le montant total de variation selon les
; quantités saisies.
;
PROCEDURE INTERNAL CALCUL_VARIATION
BEGIN
  ;
  ; Calcul de la variation. Si la quantité physique est plus petite que la
  ; quantité informatique alors la variation sera négative et vice-versa.
  ;
  LET DECMNT OF ARDECOMPTE = DECMNT OF ARDECOMPTE - &
                             DEDMNT OF ARDEC_DETAIL

  LET DEDMNT OF ARDEC_DETAIL = (PRECOUMYN OF VPRE_PRD * &
                                DEDQTEPHY OF ARDEC_DETAIL / 100000) - &
                                DEDMNTAJT  OF ARDEC_DETAIL

  LET DECMNT OF ARDECOMPTE = DECMNT OF ARDECOMPTE + &
                             DEDMNT OF ARDEC_DETAIL


  DISPLAY DECMNT    OF ARDECOMPTE
  DISPLAY DEDMNTAJT OF ARDEC_DETAIL

END




;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie et écran dépisteur du produit.
;
;
PROCEDURE INPUT PRDCLE
BEGIN
  USE ussecent NOLIST
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = CIECLE OF ARDECOMPTE
    LET T_ENTCLE = ENTCLE OF ARDECOMPTE
    LET T_PRDCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN ar103 MODE F PASSING T_CIECLE, T_ENTCLE, T_PRDCLE  &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PRDCLE)
  END
  ELSE BEGIN
    USE usinpprd NOLIST
  END
END


;-------------------------------------------------------------------------------
; Cette procédure permet de valider que le statut du produit est actif et que le
; produit est de type Inventorié.
;
PROCEDURE EDIT PRDCLE
BEGIN
  IF PRECOUMYN OF VPRE_PRD = 0
  THEN ERROR 1475 ; Ce produit n'a pas de coût moyen.

  IF PRDSTU OF VPRE_PRD = "I"
  THEN ERROR 1374 ; Ce produit est inactif.

  IF PRDTYP OF VPRE_PRD <> "I"
  THEN ERROR 1471 ; Seuls les produits inventoriés sont permis dans les décomptes.
END


;-------------------------------------------------------------------------------
; Cette procédure permet de calculer le montant de variation de la ligne de
; décompte.
;
PROCEDURE PROCESS DEDQTEPHY
BEGIN
  DO INTERNAL CALCUL_VARIATION
END


;
; Procédure servent à modifier la première occurrence d'un cluster
; avec Powerhouse Client.
;
PROCEDURE DESIGNER PH01
BEGIN
  ACCEPT DEDQTEPHY OF ARDEC_DETAIL
END



;
; Procédure servent à détruire la première occurrence d'un cluster
; avec Powerhouse Client.
;
PROCEDURE DESIGNER PH02
BEGIN
  DELETE ARDEC_DETAIL
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR ARDEC_DETAIL
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF ARDEC_DETAIL &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF ARDEC_DETAIL &
       AND NOT NEWRECORD     OF ARDEC_DETAIL &
       AND NOT DELETEDRECORD OF ARDEC_DETAIL &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

    ; Enlever le droit de modification si la décompte n'est plus préliminaire.
    ; Cependant, on permet la mise à jour si le statut vient juste d'être changer
    ; pour "A".
    ;
    IF  (     ALTEREDRECORD OF ARDEC_DETAIL  &
         OR  NEWRECORD      OF ARDEC_DETAIL  &
         OR DELETEDRECORD   OF ARDEC_DETAIL) &
      AND T_DECSTUOLD  <> "P"
    THEN ERROR 1467 ; Seule la modification d'un décompte préliminaire est
                    ; permise.

      ;
      ; Mise à jour du timbre de modification si l'usager change le décompte.
      ;
      IF    NOT NEWRECORD OF ARDECOMPTE &
        AND ALTEREDRECORD OF ARDEC_DETAIL
      THEN BEGIN
        LET DECDATMOD OF ARDECOMPTE = SYSDATE
        LET DECHREMOD OF ARDECOMPTE = SYSTIME / 10000
        LET DECUSRMOD OF ARDECOMPTE = LOGONID
      END
  END

END


BUILD

@IF FORMAT

* Produit      Loc.  Description                                                     Coût moyen     Valeur   Qté inf.   Qté réel
* xxxxxxxxxxxx xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx 999,999.99- 999,999.99- 99,999.999- 99,999.999 |


@ENDIF

