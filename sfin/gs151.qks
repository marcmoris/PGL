;/T/ Définition des imprimantes / localisations
;
;/P/ Programmeur..: Alain Côté
;    Date création: 31 août 1992
;    Description..: Définition des codes d'imprimantes et du lien avec les
;                   localisation pour définir le nom physique de l'imprimante.
;
;/M/ François Déry, 7 mai 1999
;       Ajout du groupe d'imprimante pour la gestion des formes.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gs151 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72

USE ussectmp NOLIST
"GS151"

USE gs151.hlp

USE usactecr NOLIST




FILE GSIMPRIMANTE     PRIMARY
  ACCESS VIA XIMCOD &
         REQUEST XIMCOD OF GSIMPRIMANTE &
         USING XIMCOD OF GSIMPRIMANTE
  ACCESS VIAINDEX XIM_U01 &
         VIA XIMCOD &
         USING "@"

;
; Définition des imprimantes par localisation.
;
FILE GSXIM_LOCAL      DETAIL &
                      OCCURS 11 &
                      NOITEM
  ACCESS VIAINDEX XIL_U01 &
         VIA XIMCOD &
         USING XIMCOD OF GSIMPRIMANTE
  ITEM XIMCOD OF GSXIM_LOCAL INITIAL XIMCOD OF GSIMPRIMANTE
;
; Pour conserver l'intégrité des données lors d'une destruction.
;
FILE GSXIM_LOCAL      ALIAS A_XIL_DELETE &
                      DELETE &
                      NOITEM
  ACCESS VIA XIMCOD &
         USING XIMCOD OF GSIMPRIMANTE
;
; Validation du groupe d'imprimante
;
FILE MCCODE_BILINGUE  REFERENCE
  ACCESS VIA   XELNOM, &
               CBICLE  &
         USING "XILGRPIMP", &
               XILGRPIMP OF GSXIM_LOCAL
;-------------------------------------------------------------------------------

TEMPORARY T_XLOCLE CHARACTER SIZE 4 ; Pour écran de consultation
TEMPORARY T_XELNOM CHARACTER SIZE 16; Code bilingue
TEMPORARY T_CBICLE CHARACTER SIZE 4 ; Code bilingue

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = ""
DEFINE DZ_CIENOM CHARACTER SIZE 40 = ""

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Codes d'imprimante " &
@ELSE
      " Printer Codes " &
@ENDIF
CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

ALIGN (2,3,19)
FIELD XIMCOD OF GSIMPRIMANTE ID 10 HIDDEN &
        REQUIRED NOCHANGE &
        LOOKUP NOTON GSIMPRIMANTE &
          VIA XIMCOD &
          USING XIMCOD OF GSIMPRIMANTE &
          MESSAGE 17 ; Ce code existe déjà

FIELD XIMDSCFRA OF GSIMPRIMANTE ID 15 HIDDEN &
        REQUIRED

FIELD XIMDSCANG OF GSIMPRIMANTE ID 20 HIDDEN &
        DEFAULT XIMDSCFRA OF GSIMPRIMANTE

SKIP 1
DRAW ,1 TO ,80
SKIP
TITLE &
@IF FRANCAIS
  " Localisations " &
@ELSE
  " %%% " &
@ENDIF
  centered at ,1

SKIP
TITLE "Groupe" AT ,74
SKIP
TITLE "Code Desc. Française      Desc. Anglaise    Nom de la file             imprim" &
        AT ,3
SKIP

ALIGN (3,2,3) (,,8) (,,29) (,,47) (,,75)

CLUSTER OCCURS WITH GSXIM_LOCAL ID BASE 25

FIELD XLOCLE OF GSXIM_LOCAL LABEL " " HIDDEN &
        REQUIRED NOCHANGE &
        LOOKUP NOTON GSXIM_LOCAL &
          VIA XIMCOD, XLOCLE &
          USING XIMCOD OF GSXIM_LOCAL, &
                XLOCLE OF GSXIM_LOCAL &
          MESSAGE 17 ; Ce code existe déjà

FIELD XILDSCFRA OF GSXIM_LOCAL &
        DEFAULT XIMDSCFRA OF GSIMPRIMANTE

FIELD XILDSCANG OF GSXIM_LOCAL &
        DEFAULT XIMDSCANG OF GSIMPRIMANTE &
        SIZE 17

FIELD XILQUENAM OF GSXIM_LOCAL &
        REQUIRED &
        FOR 1,26

FIELD XILGRPIMP OF GSXIM_LOCAL &
        REQUIRED &
        LOOKUP ON MCCODE_BILINGUE &
          MESSAGE 18 ; Ce code n'existe pas

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie(ENTRY).
;
;
PROCEDURE INPUT XIMCOD
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
PROCEDURE INPUT XLOCLE
BEGIN
  ;
  ; Valide si l'usager peut faire de la saisie(ENTRY).
  ;
  USE ussecent NOLIST
  ;
  ; Sous-écran de consultation des localisations.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_XLOCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gs108 MODE F PASSING T_XLOCLE
    LET FIELDTEXT = TRUNC(T_XLOCLE)
  END
END
;-------------------------------------------------------------------------------
;
PROCEDURE INPUT XILGRPIMP
BEGIN
  ;
  ; Sous-écran de consultation des localisations.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_XELNOM = "XILGRPIMP"
    LET T_CBICLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gs115 MODE F PASSING T_XELNOM, T_CBICLE
    LET FIELDTEXT = TRUNC(T_CBICLE)
  END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GSIMPRIMANTE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GSIMPRIMANTE &
     AND NOT NEWRECORD     OF GSIMPRIMANTE &
     AND NOT DELETEDRECORD OF GSIMPRIMANTE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR GSXIM_LOCAL
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF GSXIM_LOCAL &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GSXIM_LOCAL &
       AND NOT NEWRECORD     OF GSXIM_LOCAL &
       AND NOT DELETEDRECORD OF GSXIM_LOCAL &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD
