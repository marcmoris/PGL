;/T/ Raison retour (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date création: 1996-06-19
;
;    Description..: Ce panorama permet de saisir les raisons de retour
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd223 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD223"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd223.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour les dépisteurs (pop-up)
;

TEMPORARY T_RARCODRET   CHAR SIZE 03


;-------------------------------------------------------------------------------
;
; Raison retour
;

FILE PDRAISON_RETOUR PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 RARCODRET &
         USING   T_CIECLEMNU,                &
                 RARCODRET OF PDRAISON_RETOUR &
         REQUEST RARCODRET OF PDRAISON_RETOUR &
         ORDERBY CIECLE,   &
                 RARCODRET

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,   &
                 RARCODRET

  ITEM CIECLE OF PDRAISON_RETOUR INITIAL T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Raison retour " &
@ELSE
      " %%%Raison retour " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19)


FIELD RARCODRET        OF PDRAISON_RETOUR  &
        HIDDEN ID 05                       &
        REQUIRED                           &
        NOCHANGE                           &
        LOOKUP NOTON PDRAISON_RETOUR       &
          VIA   CIECLE,                    &
                RARCODRET                  &
          USING T_CIECLEMNU,               &
                RARCODRET OF PDRAISON_RETOUR &
          MESSAGE 2909 ; Cette raison existe déjà.


SKIP 1


FIELD RARDSCFRA        OF PDRAISON_RETOUR  &
        HIDDEN ID 10                       &
        REQUIRED


SKIP 1


FIELD RARDSCANG        OF PDRAISON_RETOUR  &
        HIDDEN ID 15                       &
        REQUIRED                           &
        DEFAULT RARDSCFRA OF PDRAISON_RETOUR


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

;PROCEDURE INITIALIZE
;BEGIN
;  USE ussececr NOLIST
;END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs
;

PROCEDURE INPUT RARCODRET OF PDRAISON_RETOUR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RARCODRET = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd137 MODE F &
                     PASSING T_RARCODRET, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RARCODRET )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRAISON_RETOUR &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRAISON_RETOUR &
     AND NOT NEWRECORD     OF PDRAISON_RETOUR &
     AND NOT DELETEDRECORD OF PDRAISON_RETOUR &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************** Raison d'arrêt ********************************
*                                                                              *
* Code arrêt....: xxxx                                                         *
*                                                                              *
* Descr français: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Descr anglais.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
