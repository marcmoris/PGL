;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de projet - Client supplémentaire.
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp001b ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FOR 9 LINES &
              ON LINE 15  &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PRJCLE   , &
                        T_CLICLE2  , &
                        T_RADCLE2

TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie - client
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_PRJCLE    CHARACTER SIZE 8  RESET AT STARTUP ; Numero de projet
TEMPORARY T_CLICLE2   CHARACTER SIZE 6  RESET AT STARTUP ; Numéro de client
TEMPORARY T_RADCLE2   INTEGER UNSIGNED SIZE 2 RESET AT STARTUP

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "GP001B"            ; Nom de l'écran

USE gp001b.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;----------------------------------------------
; Fichier des client supplémentaire
;----------------------------------------------

FILE GPPRO_CLIENT           PRIMARY &
                            OCCURS 5 &
                            OPEN 2

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  CLICLE  &
          USING   T_CIECLEMNU,            &
                  T_PRJCLE   ,            &
                  CLICLE OF GPPRO_CLIENT  &
          REQUEST CLICLE &
          ORDERBY PRCCLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRCCLE  &
          USING   T_CIECLEMNU,            &
                  T_PRJCLE,               &
                  PRCCLE OF GPPRO_CLIENT  &
          REQUEST PRCCLE &
          ORDERBY PRCCLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE  &
          USING   T_CIECLEMNU, &
                  T_PRJCLE     &
          ORDERBY PRCCLE


;---------------------------------
; Fichier des clients / compagnies
;---------------------------------
FILE VCLC_CLI               REFERENCE

  ACCESS  VIA   CLICIECLE, &
                CLICLE,    &
                CIECLE     &
          USING T_CLICIECLE,               &
                CLICLE    OF GPPRO_CLIENT, &
                T_CIECLEMNU


;---------------------------------
; Fichier des références d'adresse
;---------------------------------
TEMPORARY T_CLICLE CHARACTER SIZE 6

FILE MCREF_ADRESSE          REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE     &
          USING T_CLICIECLE           , &
                "C"                   , &
                CLICLE    OF GPPRO_CLIENT, &
                1


  ITEM CIECLE      OF GPPRO_CLIENT INITIAL T_CIECLEMNU
  ITEM PRJCLE      OF GPPRO_CLIENT INITIAL T_PRJCLE
  ITEM CLICIECLE   OF GPPRO_CLIENT INITIAL REFCIECLE OF MCREF_ADRESSE
  ITEM RADCLE      OF GPPRO_CLIENT INITIAL RADCLE    OF MCREF_ADRESSE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


SKIP
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO  9,1
DRAW FROM  3,80 TO  9,80
DRAW FROM  9,1  TO  9,80

SKIP 1

TITLE &
@IF FRANCAIS
      " Client supplémentaire " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP


TITLE &
@IF FRANCAIS
"Séq Client Nom                  Dev. Adr. Ligne 1" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

SKIP

ALIGN (3,2,3) (,,7) (,,14) (,,35) (,,40) (,,45)

CLUSTER OCCUR WITH GPPRO_CLIENT   ID BASE 10

FIELD PRCCLE OF GPPRO_CLIENT                          &
      LABEL " " HIDDEN                                &
      REQUIRED NOCHANGE

FIELD CLICLE OF GPPRO_CLIENT                          &
      REQUIRED NOCHANGE                               &
      LOOKUP ON VCLC_CLI                              &
        MESSAGE 836,                                  &
             ON MCREF_ADRESSE                         &
        MESSAGE 836; Ce client est inexistant

FIELD RADNOMABR OF MCREF_ADRESSE                      &
      ID SAME                                         &
      DISPLAY

FIELD DEVCLE    OF VCLC_CLI                           &
      ID SAME                                         &
      DISPLAY                                         &
      PICTURE "/^^^"

FIELD RADCLE OF MCREF_ADRESSE                         &
      ID SAME                                         &
      DISPLAY

FIELD RADADR1 OF MCREF_ADRESSE                        &
      ID SAME                                         &
      DISPLAY                                         &
      SIZE 34

CLUSTER


;-------------------------------------------------------------------------------
;
; Appel du popup pour les clients.
;

PROCEDURE INPUT CLICLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLICLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp103 PASSING T_CLICLE,     &
                             T_CLICIECLE,  &
                             T_CIECLEMNU MODE F
    LET FIELDTEXT = TRUNC(T_CLICLE)
  END
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF GPPRO_CLIENT        &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GPPRO_CLIENT        &
     AND NOT NEWRECORD     OF GPPRO_CLIENT        &
     AND NOT DELETEDRECORD OF GPPRO_CLIENT        &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR GPPRO_CLIENT
  BEGIN
     IF DELETEDRECORD OF GPPRO_CLIENT     AND &
        PRCCLE        OF GPPRO_CLIENT = 1
     THEN BEGIN
          LET T_CLICLE2 = " "
          LET T_RADCLE2 = 0
     END
  END
END

BUILD LIST DETAIL
@IF FORMAT
**************************** Client supplémentaire *****************************
*                                                                              *
*Séq Client Nom                  Dev. Adr. Ligne 1                             *
*xxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*xxx xxxxxx xxxxxxxxxxxxxxxxxxxx xxxx xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF

