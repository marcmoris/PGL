;/T/ 2.1.5 Enregistrer les rejet (Tranche).
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-08-08
;
;    Description..: Cette unité de traitement permet d'enregistrer les
;                   rejets de tranche.
;
;
;/M/ Programmeur..: Claudie Doyon
;    Date ........: 1999/10/20
;    Description..: enlever les commentaires devant
;                     IF NOT DELETEDRECORD OF PDDET_REJET 
;                     THEN BEGIN 
;                     LET TRASTU OF PDTRANCHE = "T" ; Terminer 
;                     END 
;
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;       Modifier pour FINAL l'affectation du numéro de rejet.
;
;
;/M/ Programmeur..: Claudie Doyon
;    Date ........: 1999/11/12
;    Description..: Remettre les commentaires devant
;                     IF NOT DELETEDRECORD OF PDDET_REJET
;                     THEN BEGIN
;                     LET TRASTU OF PDTRANCHE = "T" ; Terminer
;                     END
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd221a ACTIONBAR                    &
             MODE LABEL "" AT 2,80         &
             NOACTION                      &
             FIELDMARK RETAIN STARTUP      &
             HELP POPUP FROM 4,9 TO 20,72  &
             RECEIVING T_CIECLEMNU,        &
                       T_CIENOM,           &
                       PDREJET


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_REJET IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD221A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd221a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_RRECODREJ        CHARACTER SIZE 04
TEMPORARY T_TRANUMTRA002     CHARACTER SIZE 07
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 02


;-------------------------------------------------------------------------------
;
; Rejet
;

FILE PDREJET MASTER


;-------------------------------------------------------------------------------
;
; Détail rejet
;

FILE PDDET_REJET     PRIMARY &
                     NOITEM &
                     OCCURS 9 TIMES

  ACCESS VIA     CIECLE,            &
                 REJNUMREJ          &
         USING   T_CIECLEMNU,                             &
                 REJNUMREJ          OF PDREJET            &
         ORDERBY CIECLE,            &
                 REJNUMREJ,         &
                 TRANUMTRA002

  ITEM CIECLE       OF PDDET_REJET INITIAL T_CIECLEMNU
  ITEM REJNUMREJ    OF PDDET_REJET FINAL   REJNUMREJ OF PDREJET
  ITEM DREQTE       OF PDDET_REJET INITIAL 1
  ITEM DIANUM002    OF PDDET_REJET INITIAL " "
  ITEM DIANUMDIA002 OF PDDET_REJET INITIAL " "
  ITEM PJTABR       OF PDDET_REJET INITIAL " "

  SELECT IF TRANUMTRA002 OF PDDET_REJET <> " "

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les factures
;

FILE PDSEQ_REJET   DESIGNER &
                   TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE SECONDARY &
               NOITEM &
               OCCURS WITH PDDET_REJET

  ACCESS VIA   CIECLE,          &
               TRANUMTRA002     &
         USING T_CIECLEMNU,     &
               TRANUMTRA002 OF PDDET_REJET

  ITEM CIECLE OF PDTRANCHE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Raison rejet
;

FILE PDRAISON_REJET REFERENCE OCCURS WITH PDDET_REJET

  ACCESS VIA   CIECLE,          &
               RRECODREJ        &
         USING T_CIECLEMNU,     &
               RRECODREJ        OF PDDET_REJET


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Rejet " &
@ELSE
      " %%%Rejet " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19) (49,50,66)


FIELD REJNUMREJ        OF PDREJET          &
label "Num. rejet....:"&
        HIDDEN ID 05                       &
        FIXED


FIELD REJDATREJ        OF PDREJET           FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date rejet....:"&
        HIDDEN ID 10                       &
        FIXED


ALIGN (2,3,19)


FIELD REJDSC001        OF PDREJET          &
label "Description...:"&
        HIDDEN ID 15                       &
        FIXED


ALIGN (,,19)


FIELD REJDSC002        OF PDREJET          &
        ID SAME                            &
        FIXED


FIELD REJDSC003        OF PDREJET          &
        ID SAME                            &
        FIXED


DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Tranches " &
@ELSE
      " %%%tranches " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 12


TITLE &
@IF FRANCAIS
    " Tranche Long Haut Épai Gra Fi Ra Description" &
@ELSE
    "%%%anche Long Haut Épai Gra Fi Ra Description" &
@ENDIF
     AT ,2

SKIP


ALIGN (02,02,03) (,,11) (,,16) (,,21) (,,26) (,,30) (,,33) (,,36)


CLUSTER OCCURS WITH PDDET_REJET ID BASE 25


FIELD TRANUMTRA002     OF PDDET_REJET      &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED NOCHANGE                  &
        NUMERIC                            &
        BWZ                                &
        LOOKUP ON PDTRANCHE                &
          VIA   CIECLE,                    &
                TRANUMTRA002               &
          USING T_CIECLEMNU,                     &
                TRANUMTRA002      OF PDDET_REJET &
          MESSAGE 2940, & ; Ce numéro de tranche n'existe pas
       NOTON PDDET_REJET                   &
          VIA   CIECLE,                    &
                TRANUMTRA002               &
          USING T_CIECLEMNU,                     &
                TRANUMTRA002      OF PDDET_REJET &
          MESSAGE 3105 ; Cette tranche est déjà traitée


FIELD TRALON           OF PDTRANCHE        &
        HIDDEN                             &
        DISPLAY                            size 4


FIELD TRAHAU           OF PDTRANCHE        &
        HIDDEN                             &
        DISPLAY                            size 4


FIELD TRAEPA           OF PDTRANCHE        &
        HIDDEN                             &
        DISPLAY                            size 4


FIELD TGRCODGRA        OF PDTRANCHE        &
        HIDDEN                             &
        DISPLAY


FIELD TYFCODFIN        OF PDTRANCHE        &
        HIDDEN                             &
        DISPLAY


FIELD RRECODREJ        OF PDDET_REJET      &
        HIDDEN                             &
        LOOKUP ON PDRAISON_REJET           &
          MESSAGE 2976 ; Cette raison n'existe pas


FIELD DREDSCREJ        OF PDDET_REJET      &
        HIDDEN                             &
        DEFAULT RREDSC001  OF PDRAISON_REJET


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;

PROCEDURE INPUT TRANUMTRA002
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_COD          = 0 ; Toutes les tranches
    LET T_TRANUMTRA002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd122 MODE F &
                     PASSING T_TRANUMTRA002, &
                             T_COD, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TRANUMTRA002 )
  END
END


;-------------------------------------------------------------------------------
;
; Validation pour le champ
;

PROCEDURE EDIT TRANUMTRA002
BEGIN
  IF TRASTU OF PDTRANCHE = "T"
  THEN BEGIN
    ERROR 3105 ; Cette tranche est déjà traitée
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (POP-UP) pour le champ.
;

PROCEDURE INPUT RRECODREJ
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RRECODREJ = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd104 MODE F &
                     PASSING T_RRECODREJ, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RRECODREJ )
  END
END

;-------------------------------------------------------------------------------
;
; Valide le statut du montage avant de permettre la saisit des tranches
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDET_REJET &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDET_REJET &
     AND NOT NEWRECORD     OF PDDET_REJET &
     AND NOT DELETEDRECORD OF PDDET_REJET &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR PDDET_REJET
  BEGIN
   ; IF NOT DELETEDRECORD OF PDDET_REJET
   ; THEN BEGIN 
   ;   LET TRASTU OF PDTRANCHE = "T" ; Terminer 
   ; END 
    IF ALTEREDRECORD OF PDTRANCHE
    THEN BEGIN
      LET TRADATMAJ        OF PDTRANCHE = SYSDATE
      LET TRAHREMAJ        OF PDTRANCHE = SYSTIME/10000
      LET TRAUSRMAJ        OF PDTRANCHE = LOGONID
    END

;---A VOIR
    IF DELETEDRECORD OF PDDET_REJET
    THEN BEGIN
      LET TRASTU     OF PDTRANCHE = "I" ; Inventaire
     END
;---

  END

  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = REJNUMREJ OF PDREJET
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_REJET OPTIONAL            &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_REJET = T_CIECLEMNU
    END
    LET SRJNUMSEQ        OF PDSEQ_REJET      = SRJNUMSEQ        OF PDSEQ_REJET      + 1
    LET REJNUMREJ        OF PDREJET          = SRJNUMSEQ        OF PDSEQ_REJET
    PUT PDSEQ_REJET
    DISPLAY REJNUMREJ OF PDREJET
  END
END


;-------------------------------------------------------------------------------
;
; Permet de faire afficher le numéro de séquence
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY REJNUMREJ OF PDREJET
    WARNING = " "
  END
END


;-------------------------------------------------------------------------------
;
; Destruction
;


PROCEDURE DELETE
BEGIN
 DELETE PDDET_REJET
END



;PROCEDURE DELETE
;BEGIN
;  FOR PDDET_REJET
;  BEGIN
;    IF NOT DELETEDRECORD OF PDDET_REJET
;    THEN BEGIN
;      LET TRASTU     OF PDTRANCHE = "I" ; Inventaire
;      DELETE PDDET_REJET
;    END
;  END
;END


BUILD DETAIL LIST


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Rejet *************************************
*                                                                              *
* Num. rejet....: xxxxxxxxxxx                    Date rejet....: xxxxxxxx      *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*********************************** Tranches ***********************************
*                                                                              *
* Tranche Long Haut Épai Gra Fi Ra Description                                              *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
* xxxxxxx xxxx xxxx xxxx xxx xx xx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
********************************************************************************
@ENDIF
