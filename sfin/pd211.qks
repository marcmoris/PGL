;/T/ 2.3.2 Enregistrer les diagrammes.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-07
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les diagrammes.
;
;    Les procédures ENTRY, APPEND, DELETE ont été modifiées.
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;
;
;/M/ Programmeur..: Claudie Doyon
;    Date modif...: 1999-12-10
;    Description..: Modification de la procédure DESIGNER D001 
;                   pour obliger de sauvegarder avant d'aller en sous-ecran si il y a eu modif. 
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date modification: 14 juillet 2000.
;    Description......: Ajustement le traitement afin de corriger le calcul des quantités au des priorités par diagramme.
;
;    Référence........: Demande de changement 000075.
;
;    Signet...........: MP-00-07-14_D75.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd211  ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION &
              FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU , &
                        T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
; Variables temporaire pour la sécurité
;
; Description: Déclaration des variables temporaire pour la gestion
;              de la sécurité-prosig.
;
; Utilisation: Doit être utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la définition du premier fichier.
;
;------------------------------------------------------------------------------
;
; Accès au programme.
;
TEMPORARY TZ_SECACC CHARACTER SIZE 1 RESET AT STARTUP
;
; Saisie de données(Entry).
;
TEMPORARY TZ_SECENT CHARACTER SIZE 1 RESET AT STARTUP
;
; Destruction.(Delete)
;
TEMPORARY TZ_SECDEL CHARACTER SIZE 1 RESET AT STARTUP
;
; Modification.(Change mode)
;
TEMPORARY TZ_SECMOD CHARACTER SIZE 1 RESET AT STARTUP
;
; Variable pour l'élément de la sécurité.
;
TEMPORARY TZ_XELNOM CHARACTER SIZE 16 RESET AT STARTUP
TEMPORARY TZ_FLGAUT CHARACTER SIZE  1 RESET AT STARTUP
;
; Nom de l'écran pour retrouver la sécurité.
;
TEMPORARY TZ_NOMECR CHARACTER SIZE 7 RESET AT STARTUP INIT &
;
;
;**FIN USSECTMP
    "PD211"

TEMPORARY T_SOM INTEGER SIZE 4
;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
Description of screen &
@IF FRANCAIS
"                                                            " &
"     Cet écran permet de gérer les diagrammes.              " &
"                                                            "
@ELSE
"                                                            " &
"     This screen permits ....                               " &
@ENDIF


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
; ACTIONBAR
;
; Description: Définition du menu déroulant standard situé dans le haut de
;              l'écran.
;
; Utilisation: Il faut l'utiliser dans la partie déclaration de l'écran.
;              C'est à dire avant la déclaration des fichiers.
;
;
;
@IF FRANCAIS
ACTIONMENU LABEL "Rech"   ACTION FIND
ACTIONMENU LABEL "MAJ"    ACTION UPDATE
ACTIONMENU LABEL "Entrée" ACTION ENTRY
ACTIONMENU LABEL "Ajout"  ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Supp"   ACTION DELETE
@ELSE
ACTIONMENU LABEL "Find"   ACTION FIND
ACTIONMENU LABEL "Update" ACTION UPDATE
ACTIONMENU LABEL "Entry"  ACTION ENTRY
ACTIONMENU LABEL "Append" ACTION APPEND
ACTIONMENU LABEL "Select" ACTION SELECT
ACTIONMENU LABEL "Del"    ACTION DELETE
@ENDIF

@IF VAXVMS AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Ecran      <PF1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Champ          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Champ dét. <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Recherche     <rech>" ACTION FIND
  MENUITEM LABEL "MAJ            <PF3>" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour <PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde      <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entrée         <ins>" ACTION ENTRY
  MENUITEM LABEL "Ajout      <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Sélection   <sélect>" ACTION SELECT
  MENUITEM LABEL "Supp.    <eff.texte>" ACTION DELETE
  MENUITEM LABEL "Supp.Lig  <PF1 eff.>" ACTION DELETE MARK
  MENUITEM LABEL "Edition         <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant  <page suiv>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.    <PF4>" ACTION RETURN
  MENUITEM LABEL "Sortie         <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "Impression     <F20>" ACTION LIST
  MENUITEM LABEL "Système       <exec>" ACTION SYSTEM
@ENDIF

@IF VAXVMS AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Screen     <pf1 PF2>" ACTION EXTENDED HELP
  MENUITEM LABEL "Field          <PF2>" ACTION FIELD HELP MARK
  MENUITEM LABEL "Ext. Field <PF1 PF2>" ACTION EXTENDED FIELD HELP MARK
  MENUITEM LABEL "Find Mode     <find>" ACTION FIND
  MENUITEM LABEL "Update         <PF3>" ACTION UPDATE
  MENUITEM LABEL "Update Ret.<PF1 PF3>" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay    <F19>" ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode     <ins>" ACTION ENTRY
  MENUITEM LABEL "Append     <PF1 ins>" ACTION APPEND
  MENUITEM LABEL "Selection   <select>" ACTION SELECT
  MENUITEM LABEL "Delete      <remove>" ACTION DELETE
  MENUITEM LABEL "Del. Line <PF1 rem.>" ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      <F9>" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      <F7>" ACTION ACTIONBAR
  MENUITEM LABEL "Next     <next page>" ACTION NEXT DATA
  MENUITEM LABEL " //            <F18>" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen   <pf4>" ACTION RETURN
  MENUITEM LABEL "Quick Exit     <F10>" ACTION ACTIONBAR
  MENUITEM LABEL "Information    <tab>" ACTION INFORMATION
  MENUITEM LABEL "List           <F20>" ACTION LIST
  MENUITEM LABEL "System          <Do>" ACTION SYSTEM
@ENDIF

@IF UNIX AND FRANCAIS
ACTIONMENU LABEL "Aide"
  MENUITEM LABEL "Aide            F1_H" ACTION HELP
  MENUITEM LABEL "Aide dét.       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Recherche      F1_F7" ACTION FIND
  MENUITEM LABEL "MAJ               F5" ACTION UPDATE
  MENUITEM LABEL "MAJ Retour     F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "MAJ Garde           " ACTION UPDATE STAY
  MENUITEM LABEL "Entrée            F7" ACTION ENTRY
  MENUITEM LABEL "Ajout               " ACTION APPEND
  MENUITEM LABEL "Sélection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Supp.             F2" ACTION DELETE
  MENUITEM LABEL "Supp.Lig            " ACTION DELETE MARK
  MENUITEM LABEL "Edition         F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Bar d'action    F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Suivant       <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Ecran préc.       F8" ACTION RETURN
  MENUITEM LABEL "Sortie         F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "Impression écran  F3" ACTION LIST
@ENDIF

@IF UNIX AND ANGLAIS
ACTIONMENU LABEL "Help"
  MENUITEM LABEL "Help            F1_H" ACTION HELP
  MENUITEM LABEL "Ext. Help       F1_?" ACTION EXTENDED HELP
  MENUITEM LABEL "Find mode      F1_F7" ACTION FIND
  MENUITEM LABEL "Update            F5" ACTION UPDATE
  MENUITEM LABEL "Update Return  F1_F5" ACTION UPDATE RETURN
  MENUITEM LABEL "Update Stay         " ACTION UPDATE STAY
  MENUITEM LABEL "Entry Mode        F7" ACTION ENTRY
  MENUITEM LABEL "Append              " ACTION APPEND
  MENUITEM LABEL "Selection      F1_F6" ACTION SELECT
  MENUITEM LABEL "Delete            F2" ACTION DELETE
  MENUITEM LABEL "Del line            " ACTION DELETE MARK
  MENUITEM LABEL "Field Mark      F1_M" ACTION FIELDMARK
  MENUITEM LABEL "Action Bar      F1_B" ACTION ACTIONBAR
  MENUITEM LABEL "Next          <Next>" ACTION NEXT DATA
  MENUITEM LABEL " //             F1_A" ACTION ACTIONBAR
  MENUITEM LABEL "Prev. Screen      f8" ACTION RETURN
  MENUITEM LABEL "Quick Exit     F1_F8" ACTION ACTIONBAR
  MENUITEM LABEL "Information     F1_I" ACTION INFORMATION
  MENUITEM LABEL "List              F3" ACTION LIST
@ENDIF
;
;
;**FIN USACTECR


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Unité de travail                " ACTION DESIGNER D001
  MENUITEM LABEL "Priorité                        " ACTION DESIGNER D002
  MENUITEM LABEL "Emballage                       " ACTION DESIGNER D003
  MENUITEM LABEL "Débitage                        " ACTION DESIGNER D004
  MENUITEM LABEL "Rejet                           " ACTION DESIGNER D005
  MENUITEM LABEL "P/O                             " ACTION DESIGNER D006
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D007
  MENUITEM LABEL "Historique quantité commandée   " ACTION DESIGNER D008
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Unité de travail             " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Priorité                     " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Emballage                    " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Débitage                     " ACTION DESIGNER D004
  MENUITEM LABEL "%%%Rejet                        " ACTION DESIGNER D005
  MENUITEM LABEL "%%%P/O                          " ACTION DESIGNER D006
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D007
  MENUITEM LABEL "%%%Historique quantité commandée" ACTION DESIGNER D008
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;
TEMPORARY T_SEQ_FIND         CHARACTER SIZE 01
TEMPORARY T_SEQ_COMNUMCOMINT CHARACTER SIZE 07
TEMPORARY T_SEQ_DIANUM002    CHARACTER SIZE 07
TEMPORARY T_SEQ_PJTABR       CHARACTER SIZE 02

TEMPORARY T_PJTABR_PANO  CHARACTER SIZE 02 RESET AT STARTUP ; Code de projet
TEMPORARY T_PJTABR       CHARACTER SIZE 02
TEMPORARY T_PJTANN       CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 03
TEMPORARY T_COMNUMCOM    CHARACTER SIZE 07
TEMPORARY T_QTECMD       INTEGER SIGNED SIZE 08   ; MP-00-07-14_D75.
TEMPORARY T_QTEANCIENNE  INTEGER SIGNED SIZE 08   ; MP-00-07-14_D75.
TEMPORARY T_NEWREC       CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_NEWREC_DIA   CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_ALTREC_DIA   CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_DELREC_DIA   CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_OLDDIA       CHARACTER SIZE 07 RESET AT STARTUP


;-------------------------------------------------------------------------------
;
; Variable nécessaire à l'apple des dépisteurs
;

TEMPORARY T_PJTABR_POP       CHARACTER SIZE 02
TEMPORARY T_PJTANN_POP       CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT_POP CHARACTER SIZE 03
TEMPORARY T_DCINUMLIG_POP    CHARACTER SIZE 03


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_DIASTU    CHARACTER SIZE 16 = "DIASTU"
TEMPORARY T_DIASTU    CHARACTER SIZE 04
DEFINE    D_DIATYPDIA CHARACTER SIZE 16 = "DIATYPDIA"
TEMPORARY T_DIATYPDIA CHARACTER SIZE 04

;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
;/T/ Variables nécessaire pour le calcul du volume d'une pièce de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-26
;
;    Description..: module définit les variables nécessaire pour le calcul
;                   du volume d'une pièce de granit.
;
;    Note.........: Cette procédure s'utilise conjointement avec la procedure
;                   USCALVOL
;
;-------------------------------------------------------------------------------

TEMPORARY TZ_LARGEUR   FLOAT     SIZE 8
TEMPORARY TZ_HAUTEUR   FLOAT     SIZE 8
TEMPORARY TZ_EPAISSEUR FLOAT     SIZE 8

TEMPORARY TZ_VOLUME    FLOAT     SIZE 8
TEMPORARY TZ_MC2       FLOAT     SIZE 8

;-------------------------------------------------------------------------------




;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum des pièces de granit
;

;/T/ Dimension maximum et minimum des pièces de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-26
;
;    Description..: Dimension maximum et minimum des pièces de granit
;
;-------------------------------------------------------------------------------

TEMPORARY T_DIM_MAX_LAR_BL INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_LAR_BL INTEGER SIZE 4 INITIAL 0100 RESET AT STARTUP
TEMPORARY T_DIM_MAX_HAU_BL INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_HAU_BL INTEGER SIZE 4 INITIAL 0100 RESET AT STARTUP
TEMPORARY T_DIM_MAX_EPA_BL INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_EPA_BL INTEGER SIZE 4 INITIAL 0100 RESET AT STARTUP

TEMPORARY T_DIM_MAX_LON_TR INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_LON_TR INTEGER SIZE 4 INITIAL 0100 RESET AT STARTUP
TEMPORARY T_DIM_MAX_HAU_TR INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_HAU_TR INTEGER SIZE 4 INITIAL 0100 RESET AT STARTUP
TEMPORARY T_DIM_MAX_EPA_TR INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_EPA_TR INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP

TEMPORARY T_DIM_MAX_LAR_DI INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_LAR_DI INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP
TEMPORARY T_DIM_MAX_HAU_DI INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_HAU_DI INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP
TEMPORARY T_DIM_MAX_EPA_DI INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_EPA_DI INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP

TEMPORARY T_DIM_MAX_LAR_PD INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_LAR_PD INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP
TEMPORARY T_DIM_MAX_HAU_PD INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_HAU_PD INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP
TEMPORARY T_DIM_MAX_EPA_PD INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_EPA_PD INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP

;
; Dimension minimal et maximal pour les dimensions de vente
;
TEMPORARY T_DIM_MAX_VENTE  INTEGER SIZE 4 INITIAL 4500 RESET AT STARTUP
TEMPORARY T_DIM_MIN_VENTE  INTEGER SIZE 4 INITIAL 0010 RESET AT STARTUP


;-------------------------------------------------------------------------------



;-------------------------------------------------------------------------------
;
; Variable pour le calcul du prix d'une ligne de détail bon de commande
;
;/T/ Calcul du prix des pièces de granit (définition variables)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-26
;
;    Description..: Cette procédure permet de calculer le prix pour
;                   chaque type de produit de façon uniforme.
;
;    Note.........: Cette procédure s'utilise conjointement avec la procedure
;                   USCALPRIGRA.QKS (Calcul du prix)
;
;-------------------------------------------------------------------------------


TEMPORARY TZ_CPG_COD_PRD      CHARACTER SIZE 2 ; Code de produit

TEMPORARY TZ_CPG_PRI_UNI      FLOAT     SIZE 8 ; Prix/unité de mesure
TEMPORARY TZ_CPG_UNM          CHARACTER SIZE 2 ; Unité de mesure

TEMPORARY TZ_CPG_FAC_CON      FLOAT     SIZE 8 ; Facteur de conversion

TEMPORARY TZ_CPG_VOL_MC3      FLOAT     SIZE 8 ; Volume (BLOC)
TEMPORARY TZ_CPG_VOL_MC2      FLOAT     SIZE 8 ; Surface (TRANCHE)

TEMPORARY TZ_CPG_QTE          FLOAT     SIZE 8 ; Quantité

TEMPORARY TZ_CPG_PRI_TOT      FLOAT     SIZE 8 ; Prix calculé

;-------------------------------------------------------------------------------


;-------------------------------------------------------------------------------
;
; éléments temporarires qui serviront à réaffecter le contenu des variables
; correspondantes lors de la création des diagrammes
;

TEMPORARY T_OLD_PJTABR              CHARACTER SIZE 02 RESET AT STARTUP
TEMPORARY T_OLD_PJTANN              CHARACTER SIZE 02 RESET AT STARTUP
TEMPORARY T_OLD_COMNUMCOMINT        CHARACTER SIZE 03 RESET AT STARTUP
TEMPORARY T_OLD_DCINUMLIG           CHARACTER SIZE 03 RESET AT STARTUP



;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME PRIMARY &
                 NOITEM

  ACCESS VIA     CIECLE,      &
                 PJTABR,      &
                 COMNUMCOMINT &
         USING   T_CIECLEMNU,                     &
                 PJTABR           OF PDDIAGRAMME, &
                 COMNUMCOMINT     OF PDDIAGRAMME  &
         REQUEST PJTABR           OF PDDIAGRAMME, &
                 COMNUMCOMINT     OF PDDIAGRAMME ; &

  ACCESS VIA     CIECLE, &
                 DIANUM002 &
         USING   T_CIECLEMNU, &
                 DIANUM002        OF PDDIAGRAMME &
         REQUEST DIANUM002        OF PDDIAGRAMME

  ACCESS VIA     CIECLE        &
         USING   T_CIECLEMNU

  ITEM CIECLE           OF PDDIAGRAMME INITIAL T_CIECLEMNU
  ITEM DIASTU           OF PDDIAGRAMME INITIAL "A"

  ITEM DIADATCRE        OF PDDIAGRAMME INITIAL SYSDATE
  ITEM DIAHRECRE        OF PDDIAGRAMME INITIAL SYSTIME / 100;00
  ITEM DIAUSRCRE        OF PDDIAGRAMME INITIAL LOGONID

  ITEM DIADATMAJ        OF PDDIAGRAMME FINAL   SYSDATE &
                                            IF NOT NEWRECORD OF PDDIAGRAMME &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIAHREMAJ        OF PDDIAGRAMME FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF PDDIAGRAMME &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIAUSRMAJ        OF PDDIAGRAMME FINAL   LOGONID &
                                            IF NOT NEWRECORD OF PDDIAGRAMME &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME
  ITEM DIANUMDIA001     OF PDDIAGRAMME FINAL " "
  ITEM DIANUM001        OF PDDIAGRAMME FINAL PJTABR           OF PDDIAGRAMME + &
                                             DIANUMDIA001     OF PDDIAGRAMME IF 0 <> SIZE(TRUNCATE(DIANUMDIA001 OF PDDIAGRAMME)) &
                                                                             ELSE "       "
;
; Quantité calculée
;
FILE VDIAQTE          REFERENCE
  ACCESS VIA   PJTABR, &
               DIANUMDIA001, &
               DIANUMDIA002, &
               CIECLE &
         USING PJTABR OF PDDIAGRAMME, &
               DIANUMDIA001 OF PDDIAGRAMME, &
               DIANUMDIA002 OF PDDIAGRAMME, &
               CIECLE OF PDDIAGRAMME

;-----------------------------------------------------------------------------
;recherche seq

FILE PDDIAGRAMME  REFERENCE ALIAS PDDIAGRAMME_SEQ

  ACCESS VIA     CIECLE,            &
                 PJTABR,            &
                 COMNUMCOMINT,      &
                 DIANUM002          &
         USING   T_CIECLEMNU,       &
                 T_SEQ_PJTABR,      &
                 T_SEQ_COMNUMCOMINT,&
                 T_SEQ_DIANUM002    &
         ORDERBY PJTABR,            &
                 CIECLE,            &
                 PJTANN,            &
                 COMNUMCOMINT,      &
                 DIANUMDIA002

  ITEM CIECLE           OF PDDIAGRAMME INITIAL T_CIECLEMNU
  ITEM DIASTU           OF PDDIAGRAMME INITIAL "A"

  ITEM DIADATCRE        OF PDDIAGRAMME INITIAL SYSDATE
  ITEM DIAHRECRE        OF PDDIAGRAMME INITIAL SYSTIME / 100;00
  ITEM DIAUSRCRE        OF PDDIAGRAMME INITIAL LOGONID

  ITEM DIADATMAJ        OF PDDIAGRAMME FINAL   SYSDATE &
                                            IF NOT NEWRECORD OF PDDIAGRAMME &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIAHREMAJ        OF PDDIAGRAMME FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF PDDIAGRAMME &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIAUSRMAJ        OF PDDIAGRAMME FINAL   LOGONID &
                                            IF NOT NEWRECORD OF PDDIAGRAMME &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME
  ITEM DIANUMDIA001     OF PDDIAGRAMME FINAL " "
  ITEM DIANUM001        OF PDDIAGRAMME FINAL PJTABR           OF PDDIAGRAMME + &
                                             DIANUMDIA001     OF PDDIAGRAMME IF 0 <> SIZE(TRUNCATE(DIANUMDIA001 OF PDDIAGRAMME)) &
                                                                             ELSE "       "
;--------------------------------------------------
FILE PDDIAGRAMME  DESIGNER ALIAS A_PDDIAGRAMME

  ACCESS VIA     CIECLE,            &
                 PJTABR,            &
                 COMNUMCOMINT,      &
                 DIANUM002          &
         USING   T_CIECLEMNU,       &
                 PJTABR       OF PDDIAGRAMME,      &
                 COMNUMCOMINT OF PDDIAGRAMME,&
                 DIANUM002    OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE REFERENCE

  ACCESS VIA     PJTABR,      &
                 PJTANN,      &
                 COMNUMCOMINT, &
                 CIECLE        &
         USING   PJTABR           OF PDDIAGRAMME, &
                 PJTANN           OF PDDIAGRAMME, &
                 COMNUMCOMINT     OF PDDIAGRAMME, &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Détail de la commande
;

FILE PDDETAIL_C_I          REFERENCE

  ACCESS VIA     PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG,       &
                 CIECLE           &
         USING   PJTABR           OF PDDIAGRAMME, &
                 PJTANN           OF PDDIAGRAMME, &
                 COMNUMCOMINT     OF PDDIAGRAMME, &
                 DCINUMLIG        OF PDDIAGRAMME, &
                 T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Détail de la commande (MAJ)
;

FILE PDDETAIL_C_I          DESIGNER ALIAS A_DCI_MAJ


;-------------------------------------------------------------------------------
;
; Diagramme (verification)
;

FILE PDDIAGRAMME           DESIGNER ALIAS A_DIA_VER


;-------------------------------------------------------------------------------
;
; Priorité diagramme
;

FILE PDPRIORITE_DIAG DESIGNER

FILE PDUNI_TRAV_DIA DESIGNER

;-------------------------------------------------------------------------------
;
; Projet
;

FILE PDPROJET REFERENCE

  ACCESS VIA      CIECLE,     &
                  PJTABR      &
         USING    T_CIECLEMNU,&
                  PJTABR          OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
;
; QTE DEBITE
;

FILE PDDET_DEBITAGE DESIGNER


;-------------------------------------------------------------------------------
;
; Clients
;

FILE VCLC_CLI REFERENCE

  ACCESS VIA     CLICIECLE, &
                 CLICLE, &
                 CIECLE &
         USING   CLICIECLE OF PDCOMMAN_INTERNE, &
                 CLICLE    OF PDCOMMAN_INTERNE, &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_DIASTU

  ACCESS VIA   XELNOM, &
               CBICLE &
         USING D_DIASTU, &
               DIASTU           OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Code bilingue (Type de diagramme)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_DIATYPDIA

  ACCESS VIA   XELNOM, &
               CBICLE &
         USING D_DIATYPDIA, &
               DIATYPDIA OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA    CIECLE,     &
                TGRCODGRA   &
         USING  T_CIECLEMNU,&
                TGRCODGRA        OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI   REFERENCE

  ACCESS VIA    CIECLE,     &
                TYFCODFIN   &
         USING  T_CIECLEMNU,&
                TYFCODFIN        OF PDDIAGRAMME


FILE PDDIAG_TRACE DESIGNER

DEFINE D_DIAQTEDBT INT SIZE 4 = DIAQTEDBT OF VDIAQTE - &
                                DIAQTEREJ OF PDDIAGRAMME

;-------------------------------------------------------------------------------
;
;

; DRAW des écrans pleine page.
;
; Description: Ce USE sert à dessiner le cadre des écrans pleine page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD>
;
;
DRAW 3,01 TO 23,80
;
;
;**FIN USDRWECR
; En-tête standard des écrans pleine page.
;
; Description: Ce USE sert à afficher l'en-tête standard des écrans pleine
;              page.
;
; Utilisation: Doit être utlisé avant la première énoncée FIELD.
;
;
SKIP TO LINE 2

@IF VAXVMS
HILITE DISPLAY HALFTONE
@ENDIF

ALIGN(,,1) (,,21)

FIELD DZ_CIECLE FIXED
FIELD DZ_CIENOM FIXED

;
;
;**FIN USTITSTD
; HILITE standard de tous les écrans
;
; Description: Définition du HILITE standard de tous les écrans, menu
;              écrans pleine page, popup...
;
; Utilisation: Doit être utilisé avant la première énoncé FIELD.
;
;
@IF VAXVMS
HILITE ACTIONBAR     INVERSE                     , &
       ACTIONBARMARK HALFTONE INVERSE            , &
       FIELDMARK     INVERSE                     , &
       MENUMARK      HALFTONE INVERSE            , &
       MODE          HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       HALFTONE UNDERLINE          , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE HALFTONE AUDIBLE    , &
       ERROR         INVERSE HALFTONE AUDIBLE    , &
       SEVERE        INVERSE HALFTONE AUDIBLE BLINKING
@ELSE
HILITE ACTIONBAR     HALFTONE INVERSE            , &
       ACTIONBARMARK INVERSE                     , &
       FIELDMARK     INVERSE HALFTONE            , &
       MENUMARK      INVERSE HALFTONE            , &
       MODE          HALFTONE                    , &
       LABEL         HALFTONE                    , &
       LINEDRAWING   HALFTONE                    , &
       TITLE         HALFTONE                    , &
       DISPLAY       UNDERLINE                   , &
       INFORMATION   INVERSE                     , &
       WARNING       INVERSE AUDIBLE             , &
       ERROR         INVERSE AUDIBLE             , &
       SEVERE        INVERSE AUDIBLE BLINKING
@ENDIF
;
;**FIN USHILITE

SKIP

TITLE &
@IF FRANCAIS
      " Diagramme " &
@ELSE
      " %%%Diagramme " &
@ENDIF
      CENTERED AT ,1


; HILITE TITLE OFF
;
; Description: Sert à désactiver l'option HILITE pour toutes les
;              énoncés TITLE qui suit ce USE. Seul le titre principal
;              a un HILITE particulier.
;
; Utilisation: Doit être utilisé après le titre principal de l'écran.
;
;
HILITE TITLE OFF
;
;
;**FIN USHILITE


SKIP TO LINE 4


ALIGN (2,3,19) (21,21,22) (24,24,25) (,,29)


FIELD PJTABR           OF PDDIAGRAMME      &
        HIDDEN ID 05 &
        NOCHANGE &
        LABEL "Num. commande.:" &
        LOOKUP ON PDPROJET &
          VIA   CIECLE,     &
                PJTABR      &
          USING T_CIECLEMNU,          &
                PJTABR OF PDDIAGRAMME &
          MESSAGE 3074 ; Le code de projet n'existe pas


FIELD PJTANN           OF PDDIAGRAMME &
        HIDDEN ID 07 &
        OMIT ON ENTRY &
        NOCHANGE &
        LABEL "-" &
        NUMERIC


FIELD COMNUMCOMINT     OF PDDIAGRAMME &
        HIDDEN ID 09 &
        LABEL "-" &
        NOCHANGE &
        NUMERIC &
        LOOKUP ON PDCOMMAN_INTERNE &
          VIA   CIECLE,      &
                PJTABR ,     &
                PJTANN ,     &
                COMNUMCOMINT &
          USING T_CIECLEMNU,                 &
                PJTABR       OF PDDIAGRAMME, &
                PJTANN       OF PDDIAGRAMME, &
                COMNUMCOMINT OF PDDIAGRAMME  &
          MESSAGE 3081 ; Ce numéro de commande n'existe pas


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY


ALIGN (2,3,19) (25,26,42)

FIELD DCINUMLIG        OF PDDIAGRAMME      &
LABEL "Numéro ligne..:"&
        HIDDEN ID 11                       &
        NUMERIC                            &
        LOOKUP ON PDDETAIL_C_I             &
          MESSAGE 3054 ; Ce numéro de ligne n'existe pas


FIELD DCIEPA           OF PDDETAIL_C_I &
LABEL "Epaisseur.....:"&
        HIDDEN ID 13 &
        DISPLAY

ALIGN (2,3,19) (,,26)


FIELD TGRCODGRA        OF PDDIAGRAMME &
LABEL "Code granit...:"&
        HIDDEN ID 19 &
        DISPLAY


FIELD TGRDSCFRA001     OF PDTYPE_GRANIT &
        DISPLAY


FIELD TYFCODFIN        OF PDDIAGRAMME &
LABEL "Code fini.....:"&
        HIDDEN ID 21 &
        DISPLAY


FIELD TYFDSCFRA001     OF PDTYPE_FINI &
        DISPLAY


DRAW 8,2 TO 8,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Résumé de diagramme " &
@ELSE
      " %%%Résumé de diagramme " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 9


ALIGN (2,3,19)


FIELD DIANUM002             OF PDDIAGRAMME &
LABEL "No diagramme..:"&
        HIDDEN ID 23                       &
        REQUIRED                           &
        NOCHANGE &
        LOOKUP NOTON PDDIAGRAMME           &
          VIA   CIECLE,                    &
                DIANUM002                  &
          USING T_CIECLEMNU,               &
                DIANUM002 OF PDDIAGRAMME   &
          MESSAGE 2997 & ; Ce numéro de diagramme existe déjà
        PREDISPLAY


FIELD DIANUMPIECLI         OF PDDIAGRAMME &
LABEL "Réf. client...:"&
        HIDDEN ID 24 SIZE 10


FIELD DIALON           OF PDDIAGRAMME &
LABEL "Longueur......:"&
        HIDDEN ID 25                  &
        REQUIRED SIZE 4


FIELD DIAHAU           OF PDDIAGRAMME &
LABEL "Hauteur.......:"&
        HIDDEN ID 27                  &
        REQUIRED SIZE 4


FIELD DIAQTECMD        OF PDDIAGRAMME &
LABEL "Qte commandée.:"&
        HIDDEN ID 29                  &
        REQUIRED


SKIP 1


ALIGN (,3,19)


FIELD D_DIAQTEDBT LABEL "Qte débitée...: " PIC "^^^^^ " DISPLAY SIZE 6


FIELD DIAQTEEMB        OF PDDIAGRAMME &
LABEL "Qte emballée..:"&
        DISPLAY SIZE 6


FIELD DIAQTEEXPCLI     OF PDDIAGRAMME &
LABEL "Qte exp client:"&
        DISPLAY SIZE 6


SKIP 1


FIELD DIAQTEREJ        OF PDDIAGRAMME &
LABEL "Qte rejetée...:"&
        DISPLAY SIZE 6


FIELD DIAQTEREM        OF PDDIAGRAMME &
LABEL "Qte rempl.....:"&
        DISPLAY SIZE 6


SKIP 1


ALIGN (2,3,19) (,,21)


FIELD DIAFLGPRXPIE     OF PDDIAGRAMME &
LABEL "Prix pièce....:"&
        HIDDEN ID 33                  &
        DEFAULT "0"


FIELD DIAMNTPRXPIE     OF PDDIAGRAMME &
        HIDDEN ID SAME                &
        LABEL " "                     &
        PICTURE "^^^^^^.^^ "          &
        INPUT SCALE 2                 &
        DEFAULT 0.00                  &
        SIGNIFICANCE 5                &
        IF DIAFLGPRXPIE OF PDDIAGRAMME <> "0"


SKIP TO LINE 9


ALIGN (,33,49) (,,52)


FIELD DIASTU           OF PDDIAGRAMME &
LABEL "Statut........:"&
        DISPLAY


FIELD CBIDSC           OF A_CBI_DIASTU &
        DISPLAY


SKIP 1


ALIGN (,33,49)


FIELD DIAQTEPLADBTGRA  OF VDIAQTE &
        LABEL "Qte pl. dbt Co:"       &
        DISPLAY                       &
        REFRESH SIZE 6                &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEPLADBTUSC  OF VDIAQTE &
LABEL "Qte pl. dbt US:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"

FIELD DIAQTEPLADBTSCT  OF VDIAQTE &
LABEL "Qte pl. dbt SC:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"


SKIP 1


FIELD DIAQTEDBTGRA     OF VDIAQTE &
        LABEL "Qte dbt Co....:"       &
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEDBTUSC     OF VDIAQTE &
LABEL "Qté dbt US....:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEDBTSCT     OF VDIAQTE &
LABEL "Qte dbt SC....:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"


SKIP 1


FIELD DIAQTEPREUSC     OF PDDIAGRAMME &
LABEL "Qte prêt US...:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEEXPUSISCD  OF PDDIAGRAMME &
LABEL "Qte expédié US:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



SKIP 1


FIELD DIATYPDIA        OF PDDIAGRAMME &
LABEL "Type diagramme:"&
        HIDDEN ID 37                  &
        DEFAULT "R"                   &
        LOOKUP ON A_CBI_DIATYPDIA     &
          MESSAGE 3178 ; Ce type de diagramme n'existe pas


SKIP TO LINE 11


ALIGN (,57,73)


FIELD DIAQTEPLAFINGRA  OF VDIAQTE &
        LABEL "Qte pl.fini Co:"       &
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEPLAFINUSC  OF VDIAQTE &
LABEL "Qte pl.fini US:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEPLAFINSCT  OF VDIAQTE &
LABEL "Qte pl.fini SC:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



SKIP 1


FIELD DIAQTEFINGRA     OF VDIAQTE &
        LABEL "Qte fini Co...:"       &
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEFINUSC     OF VDIAQTE &
LABEL "Qte fini US...:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



FIELD DIAQTEFINSCT     OF VDIAQTE &
LABEL "Qte fini SC...:"&
        DISPLAY                       &
        REFRESH SIZE 6 &
        PICTURE "^^^^^ " TRAILING "-"



SKIP 1


FIELD DIAQTEPRESCT     OF PDDIAGRAMME &
LABEL "Qte prêt SC...:"&
        DISPLAY                       &
        REFRESH SIZE 6


FIELD DIAQTEEXPSCT     OF PDDIAGRAMME &
LABEL "Qte expédié SC:"&
        DISPLAY                       &
        REFRESH SIZE 6


SKIP 1


;ALIGN (56,,57)
ALIGN (,,57)


FIELD DIACOM001        OF PDDIAGRAMME &
        FOR ,22


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
; Appel de l'écran de sécurité.
;
; Description: Appel de l'écran de sécurité du module Gestion de système.
;              Les variables temporaires utilisées proviennent du USEFILE
;              USSECTMP.
;
; Utilisation: Doit être inséré dans la procédure INITIALIZE de l'écran.
;
;
RUN SCREEN gs000 PASSING TZ_NOMECR, & ; Nom de l'écran
                         TZ_SECACC, & ; Accès
                         TZ_SECENT, & ; Entrée
                         TZ_SECMOD, & ; Modification
                         TZ_SECDEL    ; Destruction

IF "3" = TZ_SECACC
THEN ERROR 6 ; L'usager n'est pas défini au système.

IF "2" = TZ_SECACC
THEN ERROR 7 ; Votre accès au système est expiré.

IF "0" = TZ_SECACC
THEN ERROR 8 ; Vous n'avez pas accès à cet option.
;
;
;**FIN USSECECR
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du prix d'une ligne de détail bon commande
;

;/T/ Calcul du prix des pièces de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-26
;
;    Description..: Cette procédure permet de calculer le prix pour
;                   chaque type de produit de façon uniforme.
;
;    Note.........: Cette procédure s'utilise conjointement avec la procedure
;                   USVALPRIGRA.QKS (définition des variables nécessaire pour
;                   le calcul)
;
;-------------------------------------------------------------------------------
PROCEDURE INTERNAL CALPRIGRA
BEGIN
  ;
  ; Initialisation du prix retourné pour ne pas calculer le prix "BIZZARE"
  ; si pas un produit connu
  ;
  LET TZ_CPG_PRI_TOT = 0
  ;
  ; Calcul du prix pour un bloc de granit
  ;
  IF TZ_CPG_COD_PRD = "BL"
  THEN BEGIN
    LET TZ_CPG_PRI_TOT = ROUND( (TZ_CPG_QTE * (TZ_CPG_VOL_MC3 * TZ_CPG_PRI_UNI * TZ_CPG_FAC_CON ) ) ,2 )
  END
  ELSE BEGIN
    ;
    ; Calcul du prix pour une tranche de granit
    ;
    IF TZ_CPG_COD_PRD = "TR"
    THEN BEGIN
      LET TZ_CPG_PRI_TOT = ROUND( (TZ_CPG_QTE * (TZ_CPG_VOL_MC2 * TZ_CPG_PRI_UNI * TZ_CPG_FAC_CON ) ) ,2 )
    END
    ELSE BEGIN
      ;
      ; Calcul du prix pour les produits de dimension
      ;
      IF TZ_CPG_COD_PRD = "PD"
      THEN BEGIN
        LET TZ_CPG_PRI_TOT = ROUND( (TZ_CPG_QTE * (TZ_CPG_VOL_MC2 * TZ_CPG_PRI_UNI * TZ_CPG_FAC_CON ) ) ,2 )
      END
      ELSE BEGIN
        ;
        ; Calcul du prix pour les diagrammes
        ;
        IF TZ_CPG_COD_PRD = "DI"
        THEN BEGIN
          LET TZ_CPG_PRI_TOT = ROUND( (TZ_CPG_QTE * (TZ_CPG_VOL_MC2 * TZ_CPG_PRI_UNI * TZ_CPG_FAC_CON ) ) ,2 )
        END
      END
    END
  END
END
;
; fin USCALPRIGRA
;
;-------------------------------------------------------------------------------


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du volume d'une pièce de granit
;

;/T/ Calcul du volume des pièces de granit
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-26
;
;    Description..: Cette procédure permet de calculer le volume des
;                   pièce de granit de façon uniforme.
;
;    Note.........: Cette procédure s'utilise conjointement avec la procedure
;                   USVALVOL.QKS (définition des variables nécessaire pour
;                   le calcul)
;
;-------------------------------------------------------------------------------
PROCEDURE INTERNAL CALVOL
BEGIN
  LET TZ_VOLUME = ROUND ( ((TZ_LARGEUR  /1000) *      &
                           (TZ_HAUTEUR  /1000) *      &
                           (TZ_EPAISSEUR/1000)),4)
  LET TZ_MC2    = ROUND ( ((TZ_LARGEUR  /1000) *      &
                           (TZ_HAUTEUR/1000)),4)
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champs.
;

PROCEDURE INPUT PJTABR OF PDDIAGRAMME
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN

    LET T_PJTABR       = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PJTANN       = " "
    LET T_COMNUMCOMINT = " "
    LET T_COMNUMCOM    = " "

    RUN SCREEN pd130 MODE F &
                     PASSING T_PJTABR, &
                             T_PJTANN, &
                             T_COMNUMCOMINT, &
                             T_COMNUMCOM,   &
                             T_CIECLEMNU


    LET     PJTANN       OF PDDIAGRAMME = TRUNCATE( T_PJTANN )
    LET     COMNUMCOMINT OF PDDIAGRAMME = TRUNCATE( T_COMNUMCOMINT )
    LET     COMNUMCOM    OF PDDIAGRAMME = TRUNCATE( T_COMNUMCOM )
    ;
    ; Doit être le dernier champ initialisé sinon on risque de perdre la valeur
    ;
    LET     FIELDTEXT                      = TRUNCATE( T_PJTABR )
  END
END


;-------------------------------------------------------------------------------
;
; Assigne la valeur pour affichage du champ
;

PROCEDURE PROCESS PJTABR
BEGIN
  LET     T_PJTABR_PANO               = PJTABR OF PDDIAGRAMME
END


;-------------------------------------------------------------------------------
;
; Validation à effectuer lors du changement de commande interne
;

PROCEDURE EDIT COMNUMCOMINT
BEGIN
  IF FIELDTEXT <> OLDVALUE(COMNUMCOMINT OF PDDIAGRAMME) &
     AND &
     (CORRECTMODE &
      OR &
      CHANGEMODE)
  THEN BEGIN
    GET A_DCI_MAJ OPTIONAL &
      VIA     PJTABR,          &
              PJTANN,          &
              COMNUMCOMINT,    &
              DCINUMLIG,        &
              CIECLE           &
      USING   PJTABR           OF PDDIAGRAMME, &
              PJTANN           OF PDDIAGRAMME, &
              COMNUMCOMINT     OF PDDIAGRAMME, &
              DCINUMLIG        OF PDDIAGRAMME, &
              T_CIECLEMNU
    IF ACCESSOK
    THEN BEGIN
      ;
      ; Accède la nouvelle ligne de détail
      ;
      GET PDDETAIL_C_I
      DISPLAY TGRCODGRA OF PDDIAGRAMME
      DISPLAY TYFCODFIN OF PDDIAGRAMME
      ;
      ; Valide que la ligne de detail est une ligne de détail de diagramme
      ;
      IF "DI" <> TPDTYPPRD OF PDDETAIL_C_I
      THEN BEGIN
        ERROR 2996 ; Cette ligne de détail ne correspond pas à un détail pour un diagramme
      END
      ;
      ; Valide que le type de granit de la ligne de détail est identique
      ; au type de granit du diagramme
      ;
      IF TGRCODGRA OF PDDETAIL_C_I <> TGRCODGRA OF PDDIAGRAMME
      THEN BEGIN
        WARNING 3179 ; Le type de granit de la commande ne correspond pas à celui du diagramme.
      END
      ;
      ; Valide que le type de fini de la ligne de détail est identique
      ; au type de fini du diagramme
      ;
      IF TYFCODFIN OF PDDETAIL_C_I <> TYFCODFIN OF PDDIAGRAMME
      THEN BEGIN
        WARNING 3180 ; Le fini de granit de la commande ne correspond pas à celui du diagramme.
      END
    END
    ELSE BEGIN
      ERROR 3181 ; Cette ligne de commande n'existe pas
    END

  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ
;

PROCEDURE INPUT DCINUMLIG
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DCINUMLIG_POP    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PJTABR_POP       = PJTABR OF PDCOMMAN_INTERNE
    LET T_PJTANN_POP       = PJTANN OF PDCOMMAN_INTERNE
    LET T_COMNUMCOMINT_POP = COMNUMCOMINT OF PDCOMMAN_INTERNE
    RUN SCREEN pd138 MODE F &
                     PASSING T_PJTABR_POP,       &
                             T_PJTANN_POP,       &
                             T_COMNUMCOMINT_POP, &
                             T_DCINUMLIG_POP,    &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DCINUMLIG_POP )
  END
END


;-------------------------------------------------------------------------------
;
; Valide le type de produit pour la ligne de détail
;

PROCEDURE EDIT DCINUMLIG
BEGIN
  IF "DI" <> TPDTYPPRD OF PDDETAIL_C_I
  THEN BEGIN
    ERROR 2996 ; Cette ligne de détail ne correspond pas à un détail pour un diagramme
  END
  ELSE BEGIN
    ;
    ; Valide que le type de granit et le type de fini ne peuvent changer si la
    ; productrion du diagramme à débuter
    ;
    IF TGRCODGRA OF PDDETAIL_C_I <> TGRCODGRA OF PDDIAGRAMME &
       OR &
       TYFCODFIN OF PDDETAIL_C_I <> TYFCODFIN OF PDDIAGRAMME &
       OR &
       DCIEPA OF PDDETAIL_C_I <> DIAEPA OF PDDIAGRAMME
    THEN BEGIN
      IF DIAQTEPLADBTUSC  OF VDIAQTE <> 0 & ; Planifier sur un P/O
         OR &
         DIAQTEPLADBTSCT  OF VDIAQTE <> 0 & ; Planifier sur un P/O
         OR &
         DIAQTEEMB OF PDDIAGRAMME <> 0 &
         OR &
         DIAQTEDBTGRA     OF VDIAQTE <> 0 &
         OR &
         DIAQTEDBTUSC     OF VDIAQTE <> 0 &
         OR &
         DIAQTEDBTSCT     OF VDIAQTE <> 0 &
         OR &
         DIAQTEPLAFINUSC  OF VDIAQTE <> 0 & ; Planifier sur un P/O
         OR &
         DIAQTEPLAFINSCT  OF VDIAQTE <> 0 & ; Planifier sur un P/O
         OR &
         DIAQTEFINGRA     OF VDIAQTE <> 0 &
         OR &
         DIAQTEFINUSC     OF VDIAQTE <> 0 &
         OR &
         DIAQTEFINSCT     OF VDIAQTE <> 0
      THEN BEGIN
        ERROR 3143 ; La production de ce diagramme a débuté
      END
    END

    ;
    ; Validation sur le dernier caractère d'un diagramme
    ;
    IF ("01" = TYFCODFIN OF PDDETAIL_C_I AND "P" = DIANUM002 OF PDDIAGRAMME[7:1]) &
       OR &
       ("02" = TYFCODFIN OF PDDETAIL_C_I AND "B" = DIANUM002 OF PDDIAGRAMME[7:1])
    THEN BEGIN
      ERROR 3141 ; Ce numéro de diagramme est en conflit avec le fini.
    END
  END
END


;-------------------------------------------------------------------------------
;
; Initialisation de certaine variable associées au numéro de ligne.
;

PROCEDURE PROCESS DCINUMLIG
BEGIN
  LET DIAEPA           OF PDDIAGRAMME = DCIEPA    OF PDDETAIL_C_I
  LET TGRCODGRA        OF PDDIAGRAMME = TGRCODGRA OF PDDETAIL_C_I
  LET TYFCODFIN        OF PDDIAGRAMME = TYFCODFIN OF PDDETAIL_C_I

  DISPLAY TGRCODGRA        OF PDDIAGRAMME

  GET PDTYPE_GRANIT
  DISPLAY TGRDSCFRA001     OF PDTYPE_GRANIT

  DISPLAY TYFCODFIN        OF PDDIAGRAMME

  GET PDTYPE_FINI
  DISPLAY TYFDSCFRA001     OF PDTYPE_FINI
END


;-------------------------------------------------------------------------------
;
; Permet de saisir que le numéro de diagramme et que le code de diagramme soit
; complèt.
;

PROCEDURE INPUT DIANUM002
BEGIN

  IF 5 >= SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = INDEX(FIELDTEXT,"@") &
     AND &
     NOT FINDMODE
  THEN BEGIN
    LET FIELDTEXT = PJTABR OF PDDIAGRAMME + &
                    TRUNCATE(FIELDTEXT[1:SIZE(TRUNCATE(FIELDTEXT))+1])
  END
END



;-------------------------------------------------------------------------------
;
; Valide que la dernière lettre du diagramme est différente de "P"
; si le fini est "brûlé" (TYFCODFIN="01") ET différente de "B"
; si le fini est "poli"  (TYFCODFIN="02")
;

PROCEDURE EDIT DIANUM002 OF PDDIAGRAMME
BEGIN
  ;
  ; Le champ soit être saisit entierment
  ;
  IF 7 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    ERROR 3148 ; Ce champ doit être saisit entièrement
  END
  ;
  ; Valide que les 2 premières position du diagramme correspondent
  ; au code de projet.
  ;
  IF FIELDTEXT[1:2] <> PJTABR OF PDDIAGRAMME
  THEN BEGIN
    ERROR 3149 ; Le code de projet du diagramme ne correspond pas à celui de la commande
  END
  ;
  ; Validation sur le dernier caractère d'un diagramme
  ;
  IF ("01" = TYFCODFIN OF PDDETAIL_C_I AND "P" = FIELDTEXT[7:1]) &
     OR &
     ("02" = TYFCODFIN OF PDDETAIL_C_I AND "B" = FIELDTEXT[7:1])
  THEN BEGIN
    ERROR 3141 ; Ce numéro de diagramme est en conflit avec le fini.
  END
  ;
  ; Valide que l'on peut modifier le numéro de diagramme
  ;
  IF FIELDTEXT <> OLDVALUE(DIANUM002 OF PDDIAGRAMME) &
     AND &
     DIAQTEEMB        OF PDDIAGRAMME <> 0 &
     AND &
     DIAQTEDBT        OF VDIAQTE <> 0 &
     AND &
     DIAQTEPLADBTUSC  OF VDIAQTE <> 0 &
     AND &
     DIAQTEPLADBTSCT  OF VDIAQTE <> 0 &
     AND &
     DIAQTEDBTGRA     OF VDIAQTE <> 0 &
     AND &
     DIAQTEDBTUSC     OF VDIAQTE <> 0 &
     AND &
     DIAQTEDBTSCT     OF VDIAQTE <> 0 &
     AND &
     DIAQTEPLAFINUSC  OF VDIAQTE <> 0 &
     AND &
     DIAQTEPLAFINSCT  OF VDIAQTE <> 0
  THEN BEGIN
    ERROR 3143 ; La production de ce diagramme a débuté
  END

  LET DIANUMDIA002 OF PDDIAGRAMME = TRUNCATE(FIELDTEXT)[3:5]

  LET T_OLDDIA = OLDVALUE(DIANUM002 OF PDDIAGRAMME)

END


;-------------------------------------------------------------------------------
;
; Calcul du volume commandé et du prix total de la ligne de détail
;

PROCEDURE EDIT DIALON
BEGIN
  ;
  ; Valide les dimension minimum et maximum pour un diagramme
  ;
  IF (FIELDVALUE        > T_DIM_MAX_LAR_DI OR &
      FIELDVALUE        < T_DIM_MIN_LAR_DI)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
  ;
  ; Empêcher de modifier le champ si la production a débuté pour ce diag.
  ;
  IF     DIAQTEDBT       OF VDIAQTE  <> 0 &
     AND OLDVALUE(DIALON OF PDDIAGRAMME) <> NCONVERT(FIELDTEXT)
  THEN ERROR 3182 ;La production a débuté pour ce diagramme
  ;
  ; Empêcher de modifier le champ si placer sur un P/O.
  ;
  IF    DIAQTEPLADBTUSC OF VDIAQTE <> 0 &
     OR DIAQTEPLADBTSCT OF VDIAQTE <> 0 &
     OR DIAQTEPLAFINUSC OF VDIAQTE <> 0 &
     OR DIAQTEPLAFINSCT OF VDIAQTE <> 0
  THEN BEGIN
    ERROR 3182 ;La production a débuté pour ce diagramme
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume commandé et du prix total de la ligne de détail
;

PROCEDURE EDIT DIAHAU
BEGIN
  ;
  ; Valide les dimension minimum et maximum pour un diagramme
  ;
  IF (FIELDVALUE        > T_DIM_MAX_HAU_DI OR &
      FIELDVALUE        < T_DIM_MIN_HAU_DI)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
  ;
  ; Empêcher de modifier le champ si la production a débuté pour ce diag.
  ;
  IF     DIAQTEDBT       OF VDIAQTE  <> 0 &
     AND OLDVALUE(DIAHAU OF PDDIAGRAMME) <> NCONVERT(FIELDTEXT)
  THEN BEGIN
    ERROR 3182 ;La production a débuté pour ce diagramme
  END
  ;
  ; Empêcher de modifier le champ si placer sur un P/O.
  ;
  IF    DIAQTEPLADBTUSC OF VDIAQTE <> 0 &
     OR DIAQTEPLADBTSCT OF VDIAQTE <> 0 &
     OR DIAQTEPLAFINUSC OF VDIAQTE <> 0 &
     OR DIAQTEPLAFINSCT OF VDIAQTE <> 0
  THEN BEGIN
    ERROR 3182 ;La production a débuté pour ce diagramme
  END
END


;-------------------------------------------------------------------------------
;
; Calcul la différence de quantité
;

PROCEDURE EDIT DIAQTECMD
BEGIN
  ;
  ;Empêcher que la quantite commander soit inferieur a co,us,sc
  ;
  ;
  IF FIELDVALUE < DIAQTEPLADBTGRA OF VDIAQTE OR &
     FIELDVALUE < DIAQTEPLADBTUSC OF VDIAQTE OR &
     FIELDVALUE < DIAQTEPLADBTSCT OF VDIAQTE
    THEN BEGIN
     WARNING = "La quantite selectionne est trop petite"     ;---REMY
    END
  ;
  ; Empêcher de modifier le champ si placer sur un P/O.
  ;
  IF    DIAQTEPLADBTUSC OF VDIAQTE <> 0 &
     OR DIAQTEPLADBTSCT OF VDIAQTE <> 0 &
     OR DIAQTEPLAFINUSC OF VDIAQTE <> 0 &
     OR DIAQTEPLAFINSCT OF VDIAQTE <> 0
  THEN BEGIN
    IF FIELDVALUE < DIAQTEEMB OF PDDIAGRAMME
    THEN ERROR 3182 ;La production a débuté pour ce diagramme
  END
  LET T_QTECMD = FIELDVALUE - T_QTEANCIENNE
END

;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI, NON en 1, 0
;

PROCEDURE INPUT DIAFLGPRXPIE
BEGIN
; Flag Oui/Non procédure INPUT
;
;
; Description: Traitement pour les indicateurs dont la valeur est Oui ou Non.
;              La procédure INPUT transpose les valeurs sous un code universel
;              soit 1 et 0.
;                Valeur saisie   Correspondance
;                      O               1
;                      N               0
;                      Y               1
;                      1               1
;                      0               0
;
; Utilisation: Doit être utilisé dans la procédure INPUT du champ à
;              traiter.
;
IF    FIELDTEXT = "O" &
   OR FIELDTEXT = "Y"
THEN LET FIELDTEXT = "1"

IF    FIELDTEXT = "N"
THEN LET FIELDTEXT = "0"

;**FIN USFLGINP
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1, 0 en OUI, NON
;

PROCEDURE OUTPUT DIAFLGPRXPIE
BEGIN
; Flag Oui/Non procédure OUTPUT
;
;
; Description: Traitement pour les indicateurs dont la valeur est Oui ou Non.
;              On affiche l'indicateur sous la langue de l'usager.
;
;                           Français  Anglais  Condition
;              Valeurs: 1 =   Oui       Yes      Vrai
;                       0 =   Non       No       Faux
;
;
; Utilisation: Doit être utilisé dans la procédure OUTPUT du champ à
;              traiter.
;
@IF FRANCAIS
  IF FIELDTEXT = "1"
  THEN LET FIELDTEXT = "O"
@ELSE
  IF FIELDTEXT = "1"
  THEN LET FIELDTEXT = "Y"
@ENDIF
  IF FIELDTEXT = "0"
  THEN LET FIELDTEXT = "N"

;**FIN USFLGOUT
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur sur types de diagrammes (codes bilingues)
;

PROCEDURE INPUT DIATYPDIA OF PDDIAGRAMME
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DIATYPDIA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F &
                     PASSING D_DIATYPDIA , &
                             T_DIATYPDIA
    LET FIELDTEXT = TRUNCATE( T_DIATYPDIA )
  END
END


;-------------------------------------------------------------------------------
;
; Empêcher de modifier le type si la production a débuté pour ce diag.
;

PROCEDURE EDIT DIATYPDIA OF PDDIAGRAMME
BEGIN
  IF             0 <> DIAQTEDBT          OF VDIAQTE &
     AND FIELDTEXT <> OLDVALUE(DIATYPDIA OF PDDIAGRAMME)
  THEN ERROR 3182 ;La production a débuté pour ce diagramme
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
; Sécurité pour le mode ENTRY.
;
; Description: Valide si l'usager peut faire de la saisie dans cette
;              écran.
;
; Utilisation: Peut être utiliser dans la procédure INPUT du premier
;              champ saisie à l'écran, ou dans la procédure ENTRY
;              si celle-ci a été altérée.
;
;
IF     ENTRYMODE       &
   AND TZ_SECENT = "0"
THEN SEVERE 16 ; Vous ne pouvez pas saisir de données.
;
;
;**FIN USSECENT
  LET T_OLDDIA = " "
END


;------------------------------------------------------------------------------
;
; Procédure modifiée pour permettre de bouclé dans la saisie de l'historique.
;

PROCEDURE ENTRY
BEGIN

  ACCEPT PJTABR OF PDDIAGRAMME

  IF PJTABR OF PDDIAGRAMME = " " &
     AND &
     T_OLD_PJTABR = " "
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END

  IF PJTABR OF PDDIAGRAMME <> " "
  THEN BEGIN
    LET PJTANN OF PDDIAGRAMME = PJTANN OF PDPROJET
    DISPLAY PJTANN OF PDDIAGRAMME
    ACCEPT COMNUMCOMINT OF PDDIAGRAMME
    ACCEPT DCINUMLIG OF PDDIAGRAMME
    LET T_OLD_PJTABR = PJTABR OF PDDIAGRAMME
    LET T_OLD_PJTANN = PJTANN OF PDDIAGRAMME
    LET T_OLD_COMNUMCOMINT = COMNUMCOMINT OF PDDIAGRAMME
    LET T_OLD_DCINUMLIG = DCINUMLIG OF PDDIAGRAMME
  END

  IF PJTABR OF PDDIAGRAMME = " " &
     AND &
     T_OLD_PJTABR <> " "
  THEN BEGIN
    LET PJTABR OF PDDIAGRAMME = T_OLD_PJTABR
    LET PJTANN OF PDDIAGRAMME = T_OLD_PJTANN
    LET COMNUMCOMINT OF PDDIAGRAMME = T_OLD_COMNUMCOMINT
    LET DCINUMLIG OF PDDIAGRAMME = T_OLD_DCINUMLIG
    DISPLAY PJTABR OF PDDIAGRAMME
    DISPLAY PJTANN OF PDDIAGRAMME
    DISPLAY COMNUMCOMINT OF PDDIAGRAMME
    DISPLAY DCINUMLIG OF PDDIAGRAMME
  END

  GET PDCOMMAN_INTERNE

  IF ACCESSOK
  THEN BEGIN

    DISPLAY COMNOM OF PDCOMMAN_INTERNE

    LET COMNUMCOM        OF PDDIAGRAMME = PJTABR       OF PDDIAGRAMME + &
                                          PJTANN       OF PDDIAGRAMME + &
                                          COMNUMCOMINT OF PDDIAGRAMME

    LET PJTNUMSEQ        OF PDDIAGRAMME = PJTNUMSEQ    OF PDCOMMAN_INTERNE

    LET PJTNUMPRO        OF PDDIAGRAMME = PJTABR       OF PDDIAGRAMME + &
                                          PJTANN       OF PDDIAGRAMME + &
                                          PJTNUMSEQ    OF PDDIAGRAMME

    GET PDDETAIL_C_I

    LET TGRCODGRA OF PDDIAGRAMME = TGRCODGRA OF PDDETAIL_C_I

    LET TYFCODFIN OF PDDIAGRAMME = TYFCODFIN OF PDDETAIL_C_I
  END
  ELSE BEGIN
    ERROR "DRELA"
  END

  DISPLAY DCIEPA OF PDDETAIL_C_I

  DISPLAY TGRCODGRA OF PDDIAGRAMME
  DISPLAY TGRDSCFRA001 OF PDTYPE_GRANIT
  DISPLAY TYFCODFIN OF PDDIAGRAMME
  DISPLAY TYFDSCFRA001 OF PDTYPE_FINI

  ACCEPT DIANUM002 OF PDDIAGRAMME
  ACCEPT DIANUMPIECLI OF PDDIAGRAMME
  ACCEPT DIALON OF PDDIAGRAMME
  ACCEPT DIAHAU OF PDDIAGRAMME
  LET    DIAEPA OF PDDIAGRAMME = DCIEPA OF PDDETAIL_C_I
  ACCEPT DIAQTECMD OF PDDIAGRAMME

  DISPLAY D_DIAQTEDBT
  DISPLAY DIAQTEEMB        OF PDDIAGRAMME
  DISPLAY DIAQTEEXPCLI     OF PDDIAGRAMME
  DISPLAY DIAQTEREJ        OF PDDIAGRAMME
  DISPLAY DIAQTEREM        OF PDDIAGRAMME
  ACCEPT  DIAFLGPRXPIE     OF PDDIAGRAMME
  ACCEPT  DIAMNTPRXPIE     OF PDDIAGRAMME

  DISPLAY DIASTU OF PDDIAGRAMME
  DISPLAY CBIDSC OF A_CBI_DIASTU

  DISPLAY DIAQTEPLADBTGRA OF VDIAQTE
  DISPLAY DIAQTEPLADBTUSC OF VDIAQTE
  DISPLAY DIAQTEPLADBTSCT OF VDIAQTE

  DISPLAY DIAQTEDBTGRA OF VDIAQTE
  DISPLAY DIAQTEDBTUSC OF VDIAQTE
  DISPLAY DIAQTEDBTSCT OF VDIAQTE

  DISPLAY DIAQTEPREUSC OF PDDIAGRAMME
  DISPLAY DIAQTEEXPUSISCD OF PDDIAGRAMME

  ACCEPT  DIATYPDIA OF PDDIAGRAMME

  DISPLAY DIAQTEPLAFINGRA OF VDIAQTE
  DISPLAY DIAQTEPLAFINUSC OF VDIAQTE
  DISPLAY DIAQTEPLAFINSCT OF VDIAQTE

  DISPLAY DIAQTEFINGRA OF VDIAQTE
  DISPLAY DIAQTEFINUSC OF VDIAQTE
  DISPLAY DIAQTEFINSCT OF VDIAQTE

  DISPLAY DIAQTEPRESCT OF PDDIAGRAMME
  DISPLAY DIAQTEEXPSCT OF PDDIAGRAMME

  ACCEPT DIACOM001 OF PDDIAGRAMME

  IF NEWRECORD OF PDDIAGRAMME &
     AND &
     NOT DELETEDRECORD OF PDDIAGRAMME
  THEN BEGIN
    LET T_NEWREC = "1"
  END
  RUN SCREEN pd211a &
       PASSING     T_CIECLEMNU, &
                   T_CIENOM, &
                   PDDIAGRAMME, &
                   T_NEWREC, &
                   T_QTECMD, &
                   T_QTEANCIENNE &

       MODE SAME

  let t_qtecmd = 0 ; MP-00-07-14_D75.
END


;-------------------------------------------------------------------------------
;
; Assigne la valeur pour affichage du champ
;

PROCEDURE POSTFIND
BEGIN
  LET T_QTEANCIENNE = DIAQTECMD OF PDDIAGRAMME
END


;-------------------------------------------------------------------------------
;
; Unité de travail
;

PROCEDURE DESIGNER D001
BEGIN
  IF NEWRECORD OF PDDIAGRAMME &
     AND &
     NOT DELETEDRECORD OF PDDIAGRAMME
  THEN BEGIN
    LET T_NEWREC = "1"
  END

  IF ALTEREDRECORD OF PDDIAGRAMME
  THEN ERROR "Vous devez faire une mise à jour."
  ELSE
  RUN SCREEN  pd211a &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME, &
                      T_NEWREC, &
                      T_QTECMD, &
                      T_QTEANCIENNE &
              MODE SAME
  
END


;-------------------------------------------------------------------------------
;
; Priorité
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd211b &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Emballage
;

PROCEDURE DESIGNER D003
BEGIN
  RUN SCREEN  pd211d &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Débitage
;

PROCEDURE DESIGNER D004
BEGIN
  RUN SCREEN  pd211e &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Rejet
;

PROCEDURE DESIGNER D005
BEGIN
  RUN SCREEN  pd211f &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
            MODE F
END


;-------------------------------------------------------------------------------
;
; P/O
;

PROCEDURE DESIGNER D006
BEGIN
  RUN SCREEN  pd211g &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Vérification
;

PROCEDURE DESIGNER D007
BEGIN
  RUN SCREEN  pd211c &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE F
END

PROCEDURE DESIGNER D008
BEGIN
  RUN SCREEN  pd211h &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE F
END


PROCEDURE INTERNAL MAINTIEN_QTE_COMMANDEE
BEGIN
  IF NEWRECORD
  THEN BEGIN
         LET CIECLE       OF PDDIAG_TRACE = CIECLE       OF PDDIAGRAMME
         LET DIANUMDIA002 OF PDDIAG_TRACE = DIANUMDIA002 OF PDDIAGRAMME
         LET DIANUMDIA001 OF PDDIAG_TRACE = DIANUMDIA001 OF PDDIAGRAMME
         LET PJTABR       OF PDDIAG_TRACE = PJTABR       OF PDDIAGRAMME
         LET DTRUSR       OF PDDIAG_TRACE = LOGONID
         LET DTRDAT       OF PDDIAG_TRACE = SYSDATE
         LET DTRHRE       OF PDDIAG_TRACE = SYSTIME / 10000
         LET DTRQTEANC    OF PDDIAG_TRACE = 0
         LET DTRQTENOU    OF PDDIAG_TRACE = DIAQTECMD    OF PDDIAGRAMME
         PUT PDDIAG_TRACE RESET
  END
  IF ALTEREDRECORD AND T_QTEANCIENNE <> DIAQTECMD OF PDDIAGRAMME
  THEN BEGIN
         LET CIECLE       OF PDDIAG_TRACE = CIECLE       OF PDDIAGRAMME
         LET DIANUMDIA002 OF PDDIAG_TRACE = DIANUMDIA002 OF PDDIAGRAMME
         LET DIANUMDIA001 OF PDDIAG_TRACE = DIANUMDIA001 OF PDDIAGRAMME
         LET PJTABR       OF PDDIAG_TRACE = PJTABR       OF PDDIAGRAMME
         LET DTRUSR       OF PDDIAG_TRACE = LOGONID
         LET DTRDAT       OF PDDIAG_TRACE = SYSDATE
         LET DTRHRE       OF PDDIAG_TRACE = SYSTIME / 10000
         LET DTRQTEANC    OF PDDIAG_TRACE = T_QTEANCIENNE
         LET DTRQTENOU    OF PDDIAG_TRACE = DIAQTECMD    OF PDDIAGRAMME
         PUT PDDIAG_TRACE RESET
  END
  IF DELETEDRECORD
  THEN BEGIN
        WHILE RETRIEVING PDDIAG_TRACE &
                         VIA CIECLE, DIANUMDIA002, DIANUMDIA001, PJTABR &
                         USING CIECLE OF PDDIAGRAMME, &
                               DIANUMDIA002 OF PDDIAGRAMME, &
                               DIANUMDIA001 OF PDDIAGRAMME, &
                               PJTABR OF PDDIAGRAMME
        BEGIN
info = dianumdia002 of pddiagramme
          DELETE PDDIAG_TRACE
;          PUT PDDIAG_TRACE RESET
        END
  END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  IF (COMSTU OF PDCOMMAN_INTERNE = "T") OR &
     (COMSTU OF PDCOMMAN_INTERNE = "A") OR &
     (COMSTU OF PDCOMMAN_INTERNE = "X")
  THEN ERROR = "Le status de la commande interne ne permet pas de modifier"

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDIAGRAMME &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDIAGRAMME &
     AND NOT NEWRECORD     OF PDDIAGRAMME &
     AND NOT DELETEDRECORD OF PDDIAGRAMME &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Validation sur le dernier caractère d'un diagramme
  ; N.B.: Cette validation est placée dans la PREUPDATE afin de permettre de
  ;       changer à la fois le numéro de ligne ou de commande et le dernier
  ;       caractère du numéro de commande
  ;
  IF ("01" = TYFCODFIN OF PDDETAIL_C_I AND "P" = DIANUM002 OF PDDIAGRAMME[7:1]) &
     OR &
     ("02" = TYFCODFIN OF PDDETAIL_C_I AND "B" = DIANUM002 OF PDDIAGRAMME[7:1])
  THEN BEGIN
    ERROR 3141 ; Ce numéro de diagramme est en conflit avec le fini.
  END

  ;
  ; Mise à jour des informations sur la mise à jour
  ;
  IF NOT NEWRECORD OF PDDIAGRAMME &
     AND &
     ALTEREDRECORD OF PDDIAGRAMME
  THEN BEGIN
    LET DIADATMAJ        OF PDDIAGRAMME = SYSDATE
    LET DIAHREMAJ        OF PDDIAGRAMME = SYSTIME / 10000
    LET DIAUSRMAJ        OF PDDIAGRAMME = LOGONID
  END
  ;
  ; Calcul du volume et de la surface du diagramme
  ;
  LET TZ_LARGEUR                        = DIALON          OF PDDIAGRAMME
  LET TZ_HAUTEUR                        = DIAHAU          OF PDDIAGRAMME
  LET TZ_EPAISSEUR                      = DIAEPA          OF PDDIAGRAMME
  DO  INTERNAL CALVOL

  ;
  ; On doit diviser le volume et la surface par deux si le diagramme
  ; est de type triangulaire.
  ;
  IF DIATYPDIA OF PDDIAGRAMME = "R"
  THEN BEGIN
    LET DIAVOLMC3        OF PDDIAGRAMME   = ROUND(TZ_VOLUME,4)
    LET DIASURMC2        OF PDDIAGRAMME   = ROUND(TZ_MC2,4)
  END
  ELSE BEGIN
    LET DIAVOLMC3        OF PDDIAGRAMME   = ROUND((TZ_VOLUME / 2),4)
    LET DIASURMC2        OF PDDIAGRAMME   = ROUND((TZ_MC2 / 2),4)
  END
  ;
  ; Mise à jour de l'indicateur de liaison d'une ligne de détail et un diagramme
  ;
  IF NOT DELETEDRECORD OF PDDIAGRAMME
  THEN BEGIN
    GET A_DCI_MAJ              &
      VIA     PJTABR,          &
              PJTANN,          &
              COMNUMCOMINT,    &
              DCINUMLIG,        &
              CIECLE           &
      USING   PJTABR           OF PDDIAGRAMME, &
              PJTANN           OF PDDIAGRAMME, &
              COMNUMCOMINT     OF PDDIAGRAMME, &
              DCINUMLIG        OF PDDIAGRAMME, &
              T_CIECLEMNU
    IF ACCESSOK
    THEN BEGIN
      LET DCILIADIA OF A_DCI_MAJ = "1"
      PUT A_DCI_MAJ RESET
    END
  END

  ;
  ; Destruction du diagramme
  ;
  DO INTERNAL MAINTIEN_QTE_COMMANDEE
END


;-------------------------------------------------------------------------------
;
; Procédure update
;

PROCEDURE UPDATE
BEGIN
  IF NEWRECORD OF PDDIAGRAMME
  THEN BEGIN
    LET T_NEWREC_DIA = "1"
  END
  ELSE BEGIN
    LET T_NEWREC_DIA = "0"
  END

  IF ALTEREDRECORD OF PDDIAGRAMME
  THEN BEGIN
    LET T_ALTREC_DIA = "1"
    LET T_SEQ_FIND   = "1"
    LET T_SEQ_PJTABR    = PJTABR    OF PDDIAGRAMME
    LET T_SEQ_COMNUMCOMINT = COMNUMCOMINT OF PDDIAGRAMME
    LET T_SEQ_DIANUM002 = DIANUM002 OF PDDIAGRAMME
  END
  ELSE BEGIN
    LET T_ALTREC_DIA = "0"
    LET T_SEQ_FIND   = "0"
    LET T_SEQ_PJTABR    = " "
    LET T_SEQ_COMNUMCOMINT = " "
    LET T_SEQ_DIANUM002 = " "
  END

IF T_DELREC_DIA = "1"
THEN BEGIN
 GET A_PDDIAGRAMME
 DELETE A_PDDIAGRAMME
 PUT A_PDDIAGRAMME RESET
END

  IF NOT DELETEDRECORD OF PDDIAGRAMME
   THEN LET T_DELREC_DIA = "0"

  PUT PDDIAGRAMME

  ;
  ; Création de la priorite diagramme
  ;
  IF T_NEWREC_DIA = "1"  &
     OR                 &
     T_NEWREC = "1"
  THEN BEGIN
    GET PDPRIORITE_DIAG OPTIONAL   &
      VIA   CIECLE,                &
            PRICODPRI,             &
            DIANUM002              &
      USING T_CIECLEMNU,           &
            "99" ,                 &
            DIANUM002 OF PDDIAGRAMME

    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE           OF PDPRIORITE_DIAG = CIECLE           OF PDDIAGRAMME
      LET PRICODPRI        OF PDPRIORITE_DIAG = "99"
      LET PJTABR           OF PDPRIORITE_DIAG = PJTABR           OF PDDIAGRAMME
      LET DIANUMDIA002     OF PDPRIORITE_DIAG = DIANUMDIA002     OF PDDIAGRAMME
      LET DIANUM002        OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME
      LET PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG = DIAQTECMD        OF PDDIAGRAMME
      LET PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG = DIAQTECMD        OF PDDIAGRAMME
      LET PDGSTU           OF PDPRIORITE_DIAG = "A"
      LET DCINUMLIG        OF PDPRIORITE_DIAG = DCINUMLIG        OF PDDIAGRAMME
      LET PJTANN           OF PDPRIORITE_DIAG = PJTANN           OF PDDIAGRAMME
      LET COMNUMCOMINT     OF PDPRIORITE_DIAG = COMNUMCOMINT     OF PDCOMMAN_INTERNE
      LET COMNUMCOM        OF PDPRIORITE_DIAG = PJTABR           OF PDDIAGRAMME + &
                                                PJTANN           OF PDDIAGRAMME + &
                                                COMNUMCOMINT     OF PDDIAGRAMME
      LET PJTNUMSEQ        OF PDPRIORITE_DIAG = PJTNUMSEQ        OF PDPROJET
      LET PJTNUMPRO        OF PDPRIORITE_DIAG = PJTABR          OF PDPROJET + &
                                                PJTANN          OF PDPROJET + &
                                                PJTNUMSEQ       OF PDPROJET
      PUT PDPRIORITE_DIAG
    END
    ELSE BEGIN
      ERROR "PROBLÈME TECHNIQUE 1"
    END
  END

  ;
  ; Modification de diagramme
  ;
  IF T_NEWREC_DIA = "0" &
     AND &
     T_ALTREC_DIA = "1" &
     AND &
     T_NEWREC <> "1"
  THEN BEGIN
    IF    T_OLDDIA = DIANUM002 &
       OR 0 = SIZE(TRUNCATE(T_OLDDIA))
    THEN BEGIN
      GET PDPRIORITE_DIAG OPTIONAL   &
        VIA   CIECLE,                &
              PRICODPRI,             &
              DIANUM002              &
        USING T_CIECLEMNU,           &
              "99" ,                 &
              DIANUM002 OF PDDIAGRAMME
      ;
      ; Si la quantité modifier est positive (on ajoute) on ajoute à la priorité
      ; de diagramme "99" (celle par défaut du diagramme)
      ; par contre si la quantité est négative (on en enlève) avant de diminuer
      ; la quantité on doit s'assurer que la priorité par défaut à une quantité
      ; suffisante pour procéder à la mise à jour le la quantité.
      ;

      IF ACCESSOK
      THEN BEGIN
        ;
        ; Ajout d'une quantité
        ;
        IF 0 < T_QTECMD
        THEN BEGIN
          LET PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG = PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG + &
                                                    T_QTECMD
          LET PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG = PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG + &
                                                    T_QTECMD
          LET DCINUMLIG        OF PDPRIORITE_DIAG = DCINUMLIG        OF PDDIAGRAMME
;modifier la priorite

          PUT PDPRIORITE_DIAG
        END
        ELSE BEGIN
          ;
          ; Vérification de la quantité avant la mise à jour de la quantité
          ;
          IF T_QTECMD <= PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG &
             AND &
             T_QTECMD <= PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG
          THEN BEGIN
            LET PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG = PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG + &
                                                      T_QTECMD
            LET PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG = PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG + &
                                                      T_QTECMD
            LET DCINUMLIG        OF PDPRIORITE_DIAG = DCINUMLIG        OF PDDIAGRAMME
;modifier la prio

            PUT PDPRIORITE_DIAG
          END
          ELSE BEGIN
            ERROR 3147 ; Le diagramme ne peut être modifié, modifier la planification d'abors
          END
        END
      END
    END
    ELSE BEGIN
      WHILE RETRIEVING PDPRIORITE_DIAG &
        VIA   CIECLE,                  &
              DIANUM002                &
        USING T_CIECLEMNU,             &
              T_OLDDIA
      BEGIN
        IF PRICODPRI OF PDPRIORITE_DIAG = "99"
        THEN BEGIN
          ;
          ; Ajout d'une quantité
          ;
          IF 0 < T_QTECMD
          THEN BEGIN
            LET DIANUM002        OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME
            LET DIANUMDIA002     OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME[3:5]
            LET PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG = PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG + &
                                                      T_QTECMD
            LET PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG = PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG + &
                                                      T_QTECMD
            LET DCINUMLIG        OF PDPRIORITE_DIAG = DCINUMLIG        OF PDDIAGRAMME
     ;modifier la prio

           PUT PDPRIORITE_DIAG
          END
          ELSE BEGIN
            ;
            ; Vérification de la quantité avant la mise à jour de la quantité
            ;
            IF T_QTECMD <= PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG &
               AND &
               T_QTECMD <= PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG
            THEN BEGIN
              LET DIANUM002        OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME
              LET DIANUMDIA002     OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME[3:5]
              LET PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG = PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG + &
                                                        T_QTECMD
              LET PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG = PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG + &
                                                        T_QTECMD
              LET DCINUMLIG        OF PDPRIORITE_DIAG = DCINUMLIG        OF PDDIAGRAMME
       ;modifier la prio

             PUT PDPRIORITE_DIAG
            END
            ELSE BEGIN
              ERROR 3147 ; Le diagramme ne peut être modifié, modifier la planification d'abors
            END
          END
        END
        ELSE BEGIN
          LET DIANUM002        OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME
          LET DIANUMDIA002     OF PDPRIORITE_DIAG = DIANUM002        OF PDDIAGRAMME[3:5]
          LET DCINUMLIG        OF PDPRIORITE_DIAG = DCINUMLIG        OF PDDIAGRAMME
   ;modifier la prio

         PUT PDPRIORITE_DIAG
        END
      END
    END
  END

  LET T_NEWREC = "0"
  LET T_NEWREC_DIA = "0"
  LET T_ALTREC_DIA = "0"
  LET T_DELREC_DIA = "0"

END


;-------------------------------------------------------------------------------
;
; Permet de réafficher les totaux
;
PROCEDURE POSTUPDATE
BEGIN
  DISPLAY DIAQTEPLADBTGRA OF VDIAQTE
  DISPLAY DIAQTEPLADBTUSC OF VDIAQTE
  DISPLAY DIAQTEPLADBTSCT OF VDIAQTE

  DISPLAY DIAQTEDBTGRA OF VDIAQTE
  DISPLAY DIAQTEDBTUSC OF VDIAQTE
  DISPLAY DIAQTEDBTSCT OF VDIAQTE

  DISPLAY DIAQTEPLAFINGRA OF VDIAQTE
  DISPLAY DIAQTEPLAFINUSC OF VDIAQTE
  DISPLAY DIAQTEPLAFINSCT OF VDIAQTE

  DISPLAY DIAQTEFINGRA OF VDIAQTE
  DISPLAY DIAQTEFINUSC OF VDIAQTE
  DISPLAY DIAQTEFINSCT OF VDIAQTE

  LET T_QTEANCIENNE = DIAQTECMD OF PDDIAGRAMME
END


;------------------------------------------------------------------------------
;
; Procédure de destruction
;

PROCEDURE DELETE
BEGIN
  IF    (DIAQTEDBTGRA <> 0 &
     OR &
        DIAQTEDBTUSC <> 0 &
     OR &
        DIAQTEDBTSCT <> 0 &
     OR &
        DIAQTEFINGRA <> 0 &
     OR &
        DIAQTEFINUSC <> 0 &
     OR &
        DIAQTEFINSCT <> 0 )
  THEN BEGIN
     LET DIASTU OF PDDIAGRAMME = "I"
     LET T_PJTABR_PANO = " "
     PUT PDDIAGRAMME
     DISPLAY CBIDSC
     DISPLAY DIASTU
  END
  ELSE BEGIN
;    DELETE PDDIAGRAMME
  LET T_DELREC_DIA = "1"
 WHILE RETRIEVING PDPRIORITE_DIAG VIA CIECLE,DIANUM002 USING &
                                      T_CIECLEMNU,DIANUM002 OF PDDIAGRAMME
 BEGIN
  DELETE PDPRIORITE_DIAG
  PUT    PDPRIORITE_DIAG RESET
 END
  WHILE RETRIEVING PDUNI_TRAV_DIA VIA CIECLE,DIANUM002 USING &
                                      T_CIECLEMNU,DIANUM002 OF PDDIAGRAMME
  BEGIN
    DELETE PDUNI_TRAV_DIA
    PUT PDUNI_TRAV_DIA
  END
    ;
    ; Recherche si d'autre diagramme sont liés à la ligne de détail
    ;
    GET A_DIA_VER OPTIONAL     &
      VIA     PJTABR,          &
              PJTANN,          &
              COMNUMCOMINT,    &
              DCINUMLIG,        &
              CIECLE            &
      USING   PJTABR           OF PDDIAGRAMME, &
              PJTANN           OF PDDIAGRAMME, &
              COMNUMCOMINT     OF PDDIAGRAMME, &
              DCINUMLIG        OF PDDIAGRAMME, &
              T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      GET A_DCI_MAJ              &
        VIA     PJTABR,          &
                PJTANN,          &
                COMNUMCOMINT,    &
                DCINUMLIG,        &
                CIECLE            &
        USING   PJTABR           OF PDDIAGRAMME, &
                PJTANN           OF PDDIAGRAMME, &
                COMNUMCOMINT     OF PDDIAGRAMME, &
                DCINUMLIG        OF PDDIAGRAMME, &
                T_CIECLEMNU
      LET DCILIADIA OF A_DCI_MAJ = ""
      PUT A_DCI_MAJ RESET
    END
  END
END




PROCEDURE PATH
  BEGIN
    REQUEST PJTABR OF PDDIAGRAMME
    IF PROMPTOK
      THEN REQUEST COMNUMCOMINT OF PDDIAGRAMME
    IF PROMPTOK
      THEN LET PATH = 1
    IF PATH = 0
      THEN BEGIN
        REQUEST DIANUM002 OF PDDIAGRAMME
        IF PROMPTOK
          THEN LET PATH = 2
        END
    IF PATH = 0
      THEN BEGIN
        LET PATH = 3
        END
    END
;-----------------------------------------------------------------------
PROCEDURE FIND
  BEGIN
    IF PATH = 1
      THEN BEGIN
        IF T_SEQ_FIND = "1"
        THEN GET PDDIAGRAMME_SEQ OPT

        ELSE GET PDDIAGRAMME VIA PJTABR , CIECLE , COMNUMCOMINT USING &
          PJTABR OF PDDIAGRAMME , T_CIECLEMNU , COMNUMCOMINT OF &
          PDDIAGRAMME orderby dianum002
      END
   IF PATH = 2
      THEN GET PDDIAGRAMME VIA CIECLE , DIANUM002 USING T_CIECLEMNU , &
          DIANUM002 OF PDDIAGRAMME orderby dianum002
    IF PATH = 3
      THEN GET PDDIAGRAMME VIA CIECLE USING T_CIECLEMNU

  END
BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************** Diagramme ***********************************
* Num. commande.: xx-xx-xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx           *
* Numéro ligne..: xxx    Epaisseur.....: xxxx                                  *
* Code granit...: xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
* Code fini.....: xx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
***************************** Résumé de diagramme ******************************
* No diagramme..: xxxxxxx       Statut........: x  xxxxxxxxxxxxxxxxxxxxxxxxx   *
* Réf. client...: xxxxxxxxxxxxxxx                                              *
* Longueur......: xxxx          Qte pl. dbt Gr: xxxxx   Qte pl.fini.Gr: xxxxx  *
* Hauteur.......: xxxx          Qte pl. dbt US: xxxxx   Qte pl.fini US: xxxxx  *
* Qte commandée.: xxxxxx        Qte pl. dbt SC: xxxxx   Qte pl.fini SC: xxxxx  *
*                                                                              *
* Qte débitée...: xxxxx         Qte dbt Gr....: xxxxx   Qte fini Gr...: xxxxx  *
* Qte emballée..: xxxxxx        Qté dbt US....: xxxxx   Qte fini US...: xxxxx  *
* Qte exp client: xxxxxx        Qte dbt SC....: xxxxx   Qte fini SC...: xxxxx  *
*                                                                              *
* Qte rejetée...: xxxxxx        Qte prêt US...: xxxxxx  Qte prêt SC...: xxxxxx *
* Qte rempl.....: xxxxxx        Qte expédié US: xxxxxx  Qte expédié SC: xxxxxx *
*                                                                              *
* Prix pièce....: x xxxxxxxxxx  Type diagramme: x       xxxxxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF
