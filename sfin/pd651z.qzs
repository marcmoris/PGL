;/T/ Liste de ce qui reste à emballer (SANS PRIORITÉ)
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-18
;
;    Description...: Liste des diagrammes qui reste à emballer
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;
;/M/ Programmeur...: Nancy Breton
;    Date modif....: 1999-01-18
;
;    Description...: Modification pour tenir compte des quantité rejeté lors de
;                    l'affichage des débitage fait. (sos #561).
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDDIAGRAMME &
  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE &
  LINK CIECLE OF PDDIAGRAMME ,&
       COMNUMCOM OF PDDIAGRAMME &
    TO CIECLE ,&
       COMNUMCOM OF PDCOMMAN_INTERNE &

  LINK CIECLE OF PDDIAGRAMME ,&
       COMNUMCOM OF PDDIAGRAMME ,&
       DCINUMLIG OF PDDIAGRAMME &
    TO CIECLE,&
       COMNUMCOM ,&
       DCINUMLIG OF PDDETAIL_C_I

CHOOSE CIECLE PARM , COMNUMCOM PARM

DEFINE D_RESTE_GRA INTEGER SIZE 04 = DIAQTEPLAFINGRA OF VDIAQTE  - DIAQTEPREGRA OF PDDIAGRAMME &
                                     IF 0 < DIAQTEPLAFINGRA OF VDIAQTE - DIAQTEPREGRA OF PDDIAGRAMME &
                                     ELSE 0
DEFINE D_RESTE_USC INTEGER SIZE 04 = DIAQTEPLAFINUSC OF VDIAQTE - DIAQTEPREUSC OF PDDIAGRAMME &
                                     IF 0 < DIAQTEPLAFINUSC OF VDIAQTE - DIAQTEPREUSC OF PDDIAGRAMME &
                                     ELSE 0
DEFINE D_RESTE_SCT INTEGER SIZE 04 = DIAQTEPLAFINSCT OF VDIAQTE - DIAQTEPRESCT OF PDDIAGRAMME &
                                     IF 0 < DIAQTEPLAFINSCT OF VDIAQTE - DIAQTEPRESCT OF PDDIAGRAMME &
                                     ELSE 0

SELECT IF (DIAQTEEMB OF PDDIAGRAMME < DIAQTECMD OF PDDIAGRAMME) &
       AND (COMSTU OF PDCOMMAN_INTERNE = "C" OR COMSTU OF PDCOMMAN_INTERNE = "P") &
       AND (D_RESTE_GRA > 0 OR D_RESTE_USC > 0 OR D_RESTE_SCT > 0)

SORT ON COMNUMCOM OF PDDIAGRAMME &
     ON DIANUM002 OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour afficher les quantités à emballer
;

DEFINE D_SUR_MC2 FLOAT SIZE 08 = DIASURMC2 OF PDDIAGRAMME

DEFINE D_QTE_PLA_GRA INTEGER SIZE 04 = DIAQTEPLAFINGRA OF VDIAQTE
DEFINE D_QTE_FIN_GRA INTEGER SIZE 04 = DIAQTEFINGRA OF VDIAQTE

DEFINE D_QTE_PLA_USC INTEGER SIZE 04 = DIAQTEPLAFINUSC OF VDIAQTE
DEFINE D_QTE_FIN_USC INTEGER SIZE 04 = DIAQTEFINUSC OF VDIAQTE

DEFINE D_QTE_PLA_SCT INTEGER SIZE 04 = DIAQTEPLAFINSCT OF VDIAQTE
DEFINE D_QTE_FIN_SCT INTEGER SIZE 04 = DIAQTEFINSCT OF VDIAQTE

DEFINE D_QTE_FAIT    INTEGER SIZE 04 = DIAQTEDBTGRA OF VDIAQTE - &
                                       DIAQTEREJ    OF PDDIAGRAMME

DEFINE D_SUR_PLA_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_GRA
DEFINE D_SUR_FIN_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_GRA
DEFINE D_SUR_RES_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_GRA

DEFINE D_SUR_PLA_USC FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_USC
DEFINE D_SUR_FIN_USC FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_USC
DEFINE D_SUR_RES_USC FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_USC

DEFINE D_SUR_PLA_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_SCT
DEFINE D_SUR_FIN_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_SCT
DEFINE D_SUR_RES_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_SCT

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste des diagrammes à emballer sans priorité"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD651"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitstd45 NOLIST
  SKIP 1 &
;
;HEADING AT COMNUMCOM &
;  SKIP PAGE &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDDIAGRAMME &
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDDIAGRAMME &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP 2 &
  TAB 003 "                *--------- Granicor --------*  *----- Usine secondaire ----*  *---- Sous-contractant -----*" &
  SKIP 1 &
  TAB 003 "                Debitage    Finition   Reste   Debitage   Finition   Reste    Debitage   Finition   Reste " &
  SKIP &
  TAB 001 "Diag.      Cmd    Plan Fait   Plan Fait  a Emb   Plan Fait  Plan Fait  a Emb.   Plan Fait  Plan Fait  a Emb. Gra Fi Épai Long Haut" &
  SKIP 2


FOOTING AT DIANUM002 &
  TAB 001 DIANUM002 OF PDDIAGRAMME &
  TAB 010 DIAQTECMD OF PDDIAGRAMME &
  TAB 017 DIAQTEPLADBTGRA OF VDIAQTE PICTURE "^^^^^" &
  TAB 023 D_QTE_FAIT PICTURE "^^^^^" &
  TAB 029 DIAQTEPLAFINGRA OF VDIAQTE PICTURE "^^^^^"  &
  TAB 035 DIAQTEFINGRA OF VDIAQTE PICTURE "^^^^^" &
  TAB 041 D_RESTE_GRA PICTURE "^^^^^" & ;SUBTOTAL RESET AT DIANUM002
  TAB 048 DIAQTEPLADBTUSC OF VDIAQTE PICTURE "^^^^^" &
  TAB 055 DIAQTEDBTUSC OF VDIAQTE PICTURE "^^^^^" &
  TAB 060 DIAQTEPLAFINUSC OF VDIAQTE PICTURE "^^^^^" &
  TAB 066 DIAQTEFINUSC OF VDIAQTE PICTURE "^^^^^" &
  TAB 072 D_RESTE_USC PICTURE "^^^^^" & ;SUBTOTAL RESET AT DIANUM002
  TAB 079 DIAQTEPLADBTSCT OF VDIAQTE PICTURE "^^^^^" &
  TAB 085 DIAQTEDBTSCT OF VDIAQTE PICTURE "^^^^^" &
  TAB 091 DIAQTEPLAFINSCT OF VDIAQTE PICTURE "^^^^^" &
  TAB 097 DIAQTEFINSCT OF VDIAQTE PICTURE "^^^^^" &
  TAB 103 D_RESTE_SCT PICTURE "^^^^^" & ;SUBTOTAL RESET AT DIANUM002
  TAB 110 TGRCODGRA OF PDDIAGRAMME &
  TAB 114 TYFCODFIN OF PDDIAGRAMME &
  TAB 117 DIAEPA OF PDDIAGRAMME &
  TAB 122 DIALON OF PDDIAGRAMME &
  TAB 127 DIAHAU OF PDDIAGRAMME

FOOTING AT COMNUMCOM &
  SKIP 3 &
  TAB 001 "                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*" &
  SKIP &
  TAB 001 "                                              Reste                               Reste                                 Reste " &
  SKIP &
  TAB 001 "                  Planifier        Fini       embal    Planifier        Fini      embal    Planifier        Fini       embal" &
  SKIP &
  TAB 001 "Total M2:" &
  TAB 017 D_SUR_PLA_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 029 D_SUR_FIN_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 041 D_SUR_RES_GRA SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 054 D_SUR_PLA_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 066 D_SUR_FIN_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 078 D_SUR_RES_USC SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 091 D_SUR_PLA_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 103 D_SUR_FIN_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 115 D_SUR_RES_SCT SUBTOTAL RESET AT COMNUMCOM PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  SKIP PAGE


;FINAL FOOTING &
;  SKIP 3 &
;  TAB 001 "                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*" &
;  SKIP &
;  TAB 001 "                  Planifier        Fini Reste embal    Planifier        Fini Reste embal    Planifier        Fini Reste embal" &
;  SKIP &
;  TAB 001 "Grand total M2:" &
;  TAB 017 D_SUR_PLA_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 029 D_SUR_FIN_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 041 D_SUR_RES_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 054 D_SUR_PLA_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 066 D_SUR_FIN_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 078 D_SUR_RES_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 091 D_SUR_PLA_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 103 D_SUR_FIN_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
;  TAB 115 D_SUR_RES_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6


BUILD pd651z


@IF FORMAT

PD650                                                      GRANICOR INC.                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num. commande.: XX-XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
Priorité......: 99 (AA/MM/JJ)                                         Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
                                                                      Critères esthé: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

                *--------- Granicor --------*  *----- Usine secondaire ----*  *---- Sous-contractant -----*
Diag.      Cmd  P dbt   Dbt P fin   Fin R emb  P dbt   Dbt P fin   fin R emb  P dbt   Dbt P fin   fin R emb  Gra Fi Épai Long Haut
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999


                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*
                  Planifier        Fini Reste embal    Planifier        Fini Reste embal    Planifier        Fini Reste embal
TOTAL M2 :      999999.9999 999999.9999 999999.9999  999999.9999 999999.9999 999999.9999  999999.9999 999999.9999 999999.9999


@ENDIF
