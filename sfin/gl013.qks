;/T/ Format de Haut et Bas de page
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 28 Juillet 1992
;
;/V3.00.02/ Alain Côté, 9 mars 1994, 197
;
;    Description..: Saisie des lignes de détail de la définition du Haut et
;                   du base de page d'un état financier. Ce détail permet de
;                   décrire le contenu et le formatage de l'entête (et du bas
;                   de page ) élément par élément.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gl013 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_FLGMNU

USE gl013.hlp NOLIST

USE ussectmp NOLIST
    "GL013"

USE usactecr NOLIST

;------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Compagnie du menu
TEMPORARY T_FLGMNU    CHARACTER SIZE 1 ; Provenance du menu.

;------------------------------------------------------------------------------

FILE GLEF_FORMAT  PRIMARY
  ACCESS  VIA     EFFCIECLE , &
                  EFFCLE      &
          USING   T_CIECLEMNU           , &
                  EFFCLE OF GLEF_FORMAT   &
          REQUEST EFFCLE

  ACCESS  VIA     EFFCIECLE &
          USING   T_CIECLEMNU &
          ORDERBY EFFCIECLE , &
                  EFFCLE

  ITEM    EFFCIECLE OF GLEF_FORMAT INITIAL T_CIECLEMNU

;
; Détail du format.
;
FILE GLEFF_LIGNE DETAIL OCCURS 15 NOITEM
  ACCESS  VIA   EFFCIECLE , &
                EFFCLE      &
          USING EFFCIECLE OF GLEF_FORMAT , &
                EFFCLE    OF GLEF_FORMAT   &
          ORDERBY EFFCIECLE , &
                  EFFCLE    , &
                  EFLTYPFOR , &
                  EFLLIGNUM , &
                  EFLELENUM

  ITEM    EFFCIECLE OF GLEFF_LIGNE  INITIAL EFFCIECLE OF GLEF_FORMAT FIXED
  ITEM    EFFCLE    OF GLEFF_LIGNE  INITIAL EFFCLE    OF GLEF_FORMAT FIXED

;
; Destruction global pour l'intégrité des données.
;
FILE GLEFF_LIGNE      DELETE &
                      ALIAS A_EFL_DELETE &
                      NOITEM
  ACCESS  VIA   EFFCIECLE , &
                EFFCLE      &
          USING EFFCIECLE OF GLEF_FORMAT , &
                EFFCLE    OF GLEF_FORMAT


;------------------------------------------------------------------------------

FILE VCBI_DSC DESIGNER OPEN READ

;------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU      IF T_FLGMNU = "C"
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_EFLTYPFOR CHARACTER SIZE 16 = "EFLTYPFOR"
TEMPORARY T_EFLTYPFOR CHARACTER SIZE 4
DEFINE    D_EFLELECOD CHARACTER SIZE 16 = "EFLELECOD"
TEMPORARY T_EFLELECOD CHARACTER SIZE 4
DEFINE    D_EFLELEPOS CHARACTER SIZE 16 = "EFLELEPOS"
TEMPORARY T_EFLELEPOS CHARACTER SIZE 4

TEMPORARY T_SILENT    CHARACTER SIZE 1

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Format de Haut et Bas de page " &
@ELSE
      " Top and Bottom page format " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,20) (,,25)

FIELD EFFCLE OF GLEF_FORMAT &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON GLEF_FORMAT &
               VIA   EFFCIECLE,EFFCLE &
               USING T_CIECLEMNU,EFFCLE OF GLEF_FORMAT &
               MESSAGE 307 ; Ce haut/bas de base existe déjà.

FIELD EFFDSC OF GLEF_FORMAT

SKIP

DRAW FROM ,1 TO ,80

SKIP 1

@IF FRANCAIS
TITLE "    No   No" AT ,2
SKIP
TITLE "H/B Lig Élem Label          Contenu Pos Col Texte" AT ,2
@ELSE
TITLE "    Lig Elem" AT ,2
SKIP
TITLE "H/B No   No  Label          Content Pos Col Text" AT ,2
@ENDIF

SKIP

ALIGN (2,2,3) (,,6) (,,11) (,,15) (,,32) (,,39) (,,42) (,,46)
CLUSTER OCCURS WITH GLEFF_LIGNE ID BASE 10

FIELD EFLTYPFOR OF GLEFF_LIGNE &
      LABEL " " &
      HIDDEN &
      REQUIRED &
      NOCHANGE &
      LOOKUP ON VCBI_DSC &
        VIA   XELNOM,CBICLE &
        USING D_EFLTYPFOR,EFLTYPFOR OF GLEFF_LIGNE &
        MESSAGE 322 ; Type de format inconnu

FIELD EFLLIGNUM OF GLEFF_LIGNE &
        REQUIRED

FIELD EFLELENUM OF GLEFF_LIGNE &
        REQUIRED

FIELD T_SILENT &
        SILENT &
        LOOKUP NOTON GLEFF_LIGNE &
          VIA   EFFCIECLE , &
                EFFCLE    , &
                EFLTYPFOR , &
                EFLLIGNUM , &
                EFLELENUM   &
          USING EFFCIECLE OF GLEFF_LIGNE , &
                EFFCLE    OF GLEFF_LIGNE , &
                EFLTYPFOR OF GLEFF_LIGNE , &
                EFLLIGNUM OF GLEFF_LIGNE , &
                EFLELENUM OF GLEFF_LIGNE   &
          MESSAGE 309 ; Cet élément éxiste déja dans la ligne de format

FIELD EFLELELAB OF GLEFF_LIGNE &
        FOR 1,15

FIELD EFLELECOD OF GLEFF_LIGNE &
        LOOKUP ON VCBI_DSC &
          VIA   XELNOM, CBICLE &
          USING D_EFLELECOD, EFLELECOD OF GLEFF_LIGNE &
          MESSAGE 310 ; Code d'élément invalide

FIELD EFLELEPOS OF GLEFF_LIGNE &
        LOOKUP ON VCBI_DSC &
          VIA   XELNOM, CBICLE &
          USING D_EFLELEPOS, EFLELEPOS OF GLEFF_LIGNE &
          MESSAGE 311 ; Code de position invalide

FIELD EFLELECOL OF GLEFF_LIGNE &
        BWZ

FIELD EFLELETXT OF GLEFF_LIGNE &
        FOR 1,33

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
; Pop-up sur le code bilingue du type de format
;

PROCEDURE INPUT EFLTYPFOR
BEGIN
  ;
  ; Valide si l'usager peut faire de la saisie.
  ;
  USE ussecent NOLIST

  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EFLTYPFOR = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_EFLTYPFOR, T_EFLTYPFOR
    LET FIELDTEXT = TRUNC(T_EFLTYPFOR)
  END
END

;-------------------------------------------------------------------------------
; Pop-up sur le code d'élément
;
PROCEDURE INPUT EFLELECOD
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EFLELECOD = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_EFLELECOD, T_EFLELECOD
    LET FIELDTEXT = TRUNC(T_EFLELECOD)
  END
END

;-------------------------------------------------------------------------------
; Pop-up sur le code de position de l'élément
;
PROCEDURE INPUT EFLELEPOS
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_EFLELEPOS = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_EFLELEPOS, T_EFLELEPOS
    LET FIELDTEXT = TRUNC(T_EFLELEPOS)
  END
END

;------------------------------------------------------------------------------
;
; Modification des données de la ligne
;
PROCEDURE DESIGNER 10
BEGIN
  ACCEPT EFLTYPFOR OF GLEFF_LIGNE
  ACCEPT EFLLIGNUM OF GLEFF_LIGNE
  ACCEPT EFLELENUM OF GLEFF_LIGNE
  EDIT T_SILENT
  ACCEPT EFLELELAB OF GLEFF_LIGNE
  ACCEPT EFLELECOD OF GLEFF_LIGNE
  ACCEPT EFLELEPOS OF GLEFF_LIGNE
  IF EFLELEPOS OF GLEFF_LIGNE = "P"
  THEN ACCEPT EFLELECOL OF GLEFF_LIGNE
  ELSE BEGIN
    LET EFLELECOL OF GLEFF_LIGNE = 0
    DISPLAY EFLELECOL
  END
  ACCEPT EFLELETXT OF GLEFF_LIGNE
END

;-------------------------------------------------------------------------------
;
; Entrée et ajout de nouvelles lignes du format
;
PROCEDURE APPEND
BEGIN
  ACCEPT EFLTYPFOR OF GLEFF_LIGNE
  ACCEPT EFLLIGNUM OF GLEFF_LIGNE
  ACCEPT EFLELENUM OF GLEFF_LIGNE
  EDIT T_SILENT
  ACCEPT EFLELELAB OF GLEFF_LIGNE
  ACCEPT EFLELECOD OF GLEFF_LIGNE
  ACCEPT EFLELEPOS OF GLEFF_LIGNE
  IF EFLELEPOS OF GLEFF_LIGNE = "P"
  THEN ACCEPT EFLELECOL OF GLEFF_LIGNE
  ELSE BEGIN
    LET EFLELECOL OF GLEFF_LIGNE = 0
    DISPLAY EFLELECOL
  END
  ACCEPT EFLELETXT OF GLEFF_LIGNE
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Saisie d'un nouveau format
;
PROCEDURE ENTRY
BEGIN
;
; Saisie des zones
;
  ACCEPT  EFFCLE OF GLEF_FORMAT
  ACCEPT  EFFDSC OF GLEF_FORMAT
  FOR GLEFF_LIGNE
    BEGIN
      PERFORM APPEND
    END
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF    DELETEDRECORD OF GLEF_FORMAT &
    AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF GLEF_FORMAT &
     AND NOT NEWRECORD     OF GLEF_FORMAT &
     AND NOT DELETEDRECORD OF GLEF_FORMAT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR GLEFF_LIGNE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF    DELETEDRECORD OF GLEFF_LIGNE &
      AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GLEFF_LIGNE &
       AND NOT NEWRECORD     OF GLEFF_LIGNE &
       AND NOT DELETEDRECORD OF GLEFF_LIGNE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
  ;
  ; Destruction du format.
  ;
  IF DELETEDRECORD OF GLEF_FORMAT
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le format est référé.
    ;
    RUN SCREEN gl013x MODE SAME &
                      PASSING GLEF_FORMAT , &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************ Format de Haut et Bas de page *************************
* Format........:  xxxx xxxxxxxxxxxxxxxxxxxx                                   *
********************************************************************************
*    No   No                                                                   *
*H/B Lig Élem Label          Contenu Pos Col Texte                             *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
* x  xxx  xx  xxxxxxxxxxxxxxx  xxxx   x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF
