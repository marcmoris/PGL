;/T/ Type de fini
;
;/P/ Programmeur..: René Bonneau
;    Date création: 1994-11-04
;
;    Description..: Ce panorama permet de saisir les types de fini pour
;                   les classes de granit.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd204b ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              FROM 07,01 TO 23,80          &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU,       &
                        T_CIENOM,          &
                        PDCLASSE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD204B"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd204b.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Classe de granit
;

FILE PDCLASSE         MASTER


;-------------------------------------------------------------------------------
;
; Type de fini par classe
;

FILE PDTYPE_FINI_CLAS PRIMARY &
                      NOITEM &
                      OCCURS 10

  ACCESS VIA     CIECLE,                                &
                 CLGCODCLA                              &
         USING   T_CIECLEMNU,                           &
                 CLGCODCLA        OF PDCLASSE           &
         ORDERBY CIECLE,                                &
                 CLGCODCLA,                             &
                 TYFCODFIN


  ITEM CIECLE    OF PDTYPE_FINI_CLAS   INITIAL T_CIECLEMNU
  ITEM CLGCODCLA OF PDTYPE_FINI_CLAS   INITIAL FIRST (CLGCODCLA OF PDCLASSE)


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI_LP   REFERENCE OCCURS WITH PDTYPE_FINI_CLAS

  ACCESS VIA   CIECLE,                           &
               TYFCODFIN                         &
         USING T_CIECLEMNU,                      &
               TYFCODFIN        OF PDTYPE_FINI_CLAS


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI      REFERENCE OCCURS WITH PDTYPE_FINI_CLAS

  ACCESS VIA   CIECLE,                           &
               TYFCODFIN                         &
         USING T_CIECLEMNU,                      &
               TYFCODFIN        OF PDTYPE_FINI_CLAS


;-------------------------------------------------------------------------------
;
; Variable servant a l'appel des dépisteurs
;

TEMPORARY T_TYFCODFIN CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
DRAW 3,1 TO 17,80   ; Remplace le Draw pour les écrans pleine page
;USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP 2

TITLE &
@IF FRANCAIS
      " Type de fini / classe " &
@ELSE
      " %%%Type de fini / classe " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP 1


TITLE  " Type de fini " at , 2


SKIP


ALIGN (02,02,03) (,06,07)


CLUSTER OCCURS WITH PDTYPE_FINI_CLAS ID BASE 05


  FIELD TYFCODFIN      OF PDTYPE_FINI_CLAS                 &
          HIDDEN                                           &
          NUMERIC                                          &
          REQUIRED                                         &
          LABEL " "                                        &
          LOOKUP ON PDTYPE_FINI_LP                         &
           MESSAGE 2911 & ; Ce type de fini n'existe pas
          ,NOTON PDTYPE_FINI_CLAS                          &
            VIA     CIECLE,                                &
                    CLGCODCLA,                             &
                    TYFCODFIN                              &
            USING   T_CIECLEMNU,                           &
                    CLGCODCLA        OF PDCLASSE,          &
                    TYFCODFIN        OF PDTYPE_FINI_CLAS   &
           MESSAGE 2901 ; Ce type de fini existe déjà


  FIELD TYFDSCFRA001   OF PDTYPE_FINI             &
          HIDDEN                                  &
          DISPLAY                                 &
          REFRESH                                 &
          LABEL " "


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST

END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur (popup) pour le champ
;

PROCEDURE INPUT TYFCODFIN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd128 MODE F               &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
  ELSE BEGIN
    IF 0 = SIZE(FIELDTEXT)
    THEN BEGIN
      LET FIELDTEXT = "00"
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDTYPE_FINI_CLAS &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDTYPE_FINI_CLAS &
     AND NOT NEWRECORD     OF PDTYPE_FINI_CLAS &
     AND NOT DELETEDRECORD OF PDTYPE_FINI_CLAS &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD
@IF FORMAT

**************************** Type de fini / classe *****************************
*                                                                              *
* Type de fini                                                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
* xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 *
*                                                                              *
********************************************************************************
@ENDIF
