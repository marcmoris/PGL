;/T/ 2.4.2 Enregistrer le sciage (Feuille de temps)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-05-15
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les feuilles de temps pour le sciage
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd218  ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD218"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd218.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Opérateur sciage                " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Opérateur sciage             " ACTION DESIGNER D001
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Clés de recherche pour les popup de consultations et les sous écrans
;


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_SILENT             CHARACTER SIZE 01
TEMPORARY T_NIV_HAU            INTEGER   SIZE 04
TEMPORARY T_NIV_BAS            INTEGER   SIZE 04
TEMPORARY T_HAUT_SCIE          INTEGER   SIZE 04
TEMPORARY T_CUBNET             FLOAT     SIZE 08
TEMPORARY T_POURCENT_BLOC_SCIE FLOAT     SIZE 08


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calcul d'un intervalle de temps
;

USE usdiftim NOLIST


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calcul de l'heure à partir d'un nombre de seconde
;

USE uscaltim NOLIST


;-------------------------------------------------------------------------------
;
; Sciage feuille de temps
;

FILE PDRAP_SCIAGE  PRIMARY &
                   NOITEM &
                   NODELETE

  ACCESS VIA     CIECLE,          &
                 RSCDATPRD,       &
                 QRTCODQRT        &
         USING   T_CIECLEMNU,                         &
                 RSCDATPRD        OF PDRAP_SCIAGE,    &
                 QRTCODQRT        OF PDRAP_SCIAGE     &
         REQUEST RSCDATPRD        OF PDRAP_SCIAGE,    &
                 QRTCODQRT        OF PDRAP_SCIAGE     &
         ORDERBY CIECLE,          &
                 RSCDATPRD,       &
                 QRTCODQRT


  ACCESS VIA     CIECLE,          &
                 RSCDATPRD        &
         USING   T_CIECLEMNU,                         &
                 RSCDATPRD        OF PDRAP_SCIAGE     &
         REQUEST RSCDATPRD        OF PDRAP_SCIAGE     &
         ORDERBY CIECLE,          &
                 RSCDATPRD,       &
                 QRTCODQRT


  ACCESS VIA     CIECLE           &
         USING   T_CIECLEMNU      &
         ORDERBY CIECLE,          &
                 RSCDATPRD,       &
                 QRTCODQRT

  ITEM CIECLE OF PDRAP_SCIAGE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Détail rapport de sciage
;

FILE PDDET_RAP_SCIAGE DETAIL OCCURS 12 TIMES &
                      NOITEM


  ACCESS VIA     CIECLE,          &
                 RSCDATPRD,       &
                 QRTCODQRT        &
         USING   T_CIECLEMNU,                         &
                 RSCDATPRD        OF PDRAP_SCIAGE,    &
                 QRTCODQRT        OF PDRAP_SCIAGE     &
         ORDERBY CIECLE,          &
                 RSCDATPRD,       &
                 QRTCODQRT,       &
                 RMSNUMSEQ

  ITEM CIECLE     OF PDDET_RAP_SCIAGE INITIAL T_CIECLEMNU
  ITEM RSCDATPRD  OF PDDET_RAP_SCIAGE INITIAL RSCDATPRD OF PDRAP_SCIAGE FIXED
  ITEM QRTCODQRT  OF PDDET_RAP_SCIAGE INITIAL QRTCODQRT OF PDRAP_SCIAGE FIXED

  ITEM DRSCUBNET OF PDDET_RAP_SCIAGE SUM INTO RSCMC3PRD OF PDRAP_SCIAGE



;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE SECONDARY OCCURS WITH PDDET_RAP_SCIAGE &
                             NODELETE &
                             NOITEM

  ACCESS VIA     CIECLE,          &
                 RMSNUMSEQ        &
         USING   T_CIECLEMNU,     &
                 RMSNUMSEQ        OF PDDET_RAP_SCIAGE


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC DESIGNER


;-------------------------------------------------------------------------------
;
; Bloc de granit
;

FILE PDBLOC DESIGNER


;-------------------------------------------------------------------------------
;
; Sciage de bloc
;

FILE PDSCIAGE_BLOCS DESIGNER


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT        OF PDRAP_SCIAGE


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Productivité du sciage " &
@ELSE
      " %%%Productivité du sciage " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (2,3,19)


FIELD RSCDATPRD        OF PDRAP_SCIAGE      FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date..........:"&
        HIDDEN ID 05                       &
        REQUIRED


ALIGN (2,3,19) (,,21)


FIELD QRTCODQRT        OF PDRAP_SCIAGE     &
label "Quart travail.:"&
        HIDDEN ID 10                       &
        REQUIRED                           &
        LOOKUP NOTON PDRAP_SCIAGE          &
          VIA    CIECLE,                   &
                 RSCDATPRD,                &
                 QRTCODQRT                 &
          USING  T_CIECLEMNU,               &
                 RSCDATPRD OF PDRAP_SCIAGE, &
                 QRTCODQRT OF PDRAP_SCIAGE  &
          MESSAGE 3150 ; Il y a une production déjà enregistrée pour cette date et quart de travail


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY


ALIGN (2,3,19)


FIELD RSCHREDEB        OF PDRAP_SCIAGE     &
label "Hre début.....:"&
        HIDDEN ID 15                       &
        REQUIRED


FIELD RSCHREFIN        OF PDRAP_SCIAGE     &
label "Hre fin.......:"&
        HIDDEN ID 20                       &
        REQUIRED


SKIP TO LINE 4


ALIGN (,48,64)


FIELD RSCMC3PRD        OF PDRAP_SCIAGE     &
label "M3............:"&
        DISPLAY                            &
        REFRESH


FIELD RSCHREPRD        OF PDRAP_SCIAGE     &
label "Hre...........:"&
        DISPLAY                            &
        REFRESH


FIELD RSCPRD           OF PDRAP_SCIAGE     &
label "Productivité..:"&
        DISPLAY                            &
        REFRESH


DRAW 08,2 TO 08,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Détail sciage " &
@ELSE
      " %%%Détail sciage " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 9


TITLE &
@IF FRANCAIS
      "                                  * Position -* " &
@ELSE
      "%%%                               * Position -* " &
@ENDIF
      AT ,2


SKIP TO LINE 10

TITLE &
@IF FRANCAIS
      "                  Montage   Scie  Début     Fin      Cubage " &
@ELSE
      "%%%               Montage   Scie  Début     Fin      Cubage " &
@ENDIF
      AT ,2


ALIGN (17,17,18) (,,31) (,,37) (,,45) (,,52)


CLUSTER OCCURS WITH PDDET_RAP_SCIAGE


FIELD RMSNUMSEQ        OF PDDET_RAP_SCIAGE &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED                           &
        LOOKUP ON PDRAP_MONTAGE            &
          MESSAGE 3152,  & ; Ce numéro de montage n'existe pas
        NOTON PDDET_RAP_SCIAGE      &
          VIA    RSCDATPRD,                &
                 QRTCODQRT,                &
                 RMSNUMSEQ,                &
                 CIECLE                    &
          USING  RSCDATPRD OF PDRAP_SCIAGE,     &
                 QRTCODQRT OF PDRAP_SCIAGE,     &
                 RMSNUMSEQ OF PDDET_RAP_SCIAGE, &
                 T_CIECLEMNU                   &
          MESSAGE 3151 ; "Le numéro de montage existe déjà"


FIELD SCINUMSCI        OF PDRAP_MONTAGE    &
        HIDDEN                             &
        DISPLAY


FIELD DRSPOSDEB        OF PDDET_RAP_SCIAGE &
        HIDDEN                             &
        REQUIRED size 4


FIELD DRSPOSFIN        OF PDDET_RAP_SCIAGE &
        HIDDEN                             &
        REQUIRED size 4


FIELD T_SILENT                             &
        SILENT


FIELD DRSCUBNET        OF PDDET_RAP_SCIAGE &
        HIDDEN                             &
        DISPLAY


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;----------------------------------------------------------------------
;
; Assigne une valeur par défaut pour le quart de travail
;

PROCEDURE INPUT QRTCODQRT
BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
    LET FIELDTEXT = "J"
  END
END


;-------------------------------------------------------------------------------
;
; Valide le numéro de montage
;

PROCEDURE EDIT RMSNUMSEQ
BEGIN
GET PDRAP_MONTAGE VIA     CIECLE,          &
                          RMSNUMSEQ        &
                  USING   T_CIECLEMNU,     &
                          RMSNUMSEQ        OF PDDET_RAP_SCIAGE

  IF RMOSTU OF PDRAP_MONTAGE <> "S"
  THEN BEGIN
    ERROR 3068 ; Le statut ne permet pas cette opération
  END
END


;-------------------------------------------------------------------------------
;
; Mise à jour des informations lors de la fin de sciage d'un montage
;

PROCEDURE PROCESS DRSPOSFIN
BEGIN
  IF DRSPOSFIN OF PDDET_RAP_SCIAGE = 0
  THEN BEGIN
    LET RMODATFINREE OF PDRAP_MONTAGE = RSCDATPRD OF PDRAP_SCIAGE
    LET RMOHREFINREE OF PDRAP_MONTAGE = RSCHREFIN OF PDRAP_SCIAGE
    LET RMOSTU OF PDRAP_MONTAGE = "T"
  END
END


;-------------------------------------------------------------------------------
;
; Calcul le cubage net
;

PROCEDURE PROCESS T_SILENT
BEGIN
  ;
  ; Calcul du cubage scier pour le montage
  ;
  LET T_CUBNET = 0
  WHILE RETRIEVING PDMONTAGE_BLOC &
    VIA   CIECLE,          &
          RMSNUMSEQ        &
    USING T_CIECLEMNU,     &
          RMSNUMSEQ OF PDDET_RAP_SCIAGE
  BEGIN
    ;
    ; Initialisation
    ;
    LET T_NIV_HAU = 0
    LET T_NIV_BAS = 0
    LET T_HAUT_SCIE = 0
    LET T_POURCENT_BLOC_SCIE = 0
    ;
    ; Calcul du niveau haut
    ;
    IF DRSPOSDEB OF PDDET_RAP_SCIAGE > MOBNIVHAU OF PDMONTAGE_BLOC
    THEN BEGIN
      LET T_NIV_HAU = MOBNIVHAU OF PDMONTAGE_BLOC
    END
    ELSE BEGIN
      LET T_NIV_HAU = DRSPOSDEB OF PDDET_RAP_SCIAGE
    END
    ;
    ; Calcul du niveau bas
    ;
    IF DRSPOSFIN OF PDDET_RAP_SCIAGE < MOBNIVBAS OF PDMONTAGE_BLOC
    THEN BEGIN
      LET T_NIV_BAS = MOBNIVBAS OF PDMONTAGE_BLOC
    END
    ELSE BEGIN
      LET T_NIV_BAS = DRSPOSFIN OF PDDET_RAP_SCIAGE
    END
    ;
    ; Calcul de la hauteur scié
    ;
    IF T_NIV_HAU <= T_NIV_BAS
    THEN BEGIN
      LET T_HAUT_SCIE = 0
    END
    ELSE BEGIN
      LET T_HAUT_SCIE = T_NIV_HAU - T_NIV_BAS
    END
    ;
    ; Calcul du pourcentage de sciage du bloc
    ;
    LET T_POURCENT_BLOC_SCIE = T_HAUT_SCIE / (MOBNIVHAU OF PDMONTAGE_BLOC - &
                                              MOBNIVBAS OF PDMONTAGE_BLOC)
    GET PDBLOC &
      VIA   CIECLE,          &
            BLONUMBLOAPR     &
      USING T_CIECLEMNU,     &
            BLONUMBLOAPR     OF PDMONTAGE_BLOC
    LET T_CUBNET = T_CUBNET + (T_POURCENT_BLOC_SCIE * BLOVOLMC3NET)
  END
  LET DRSCUBNET OF PDDET_RAP_SCIAGE = T_CUBNET
  ;
  ; Calcul des heures de production
  ;
  LET TZ_NBRSEC   = RSCDUR OF PDRAP_SCIAGE
  LET RSCHREPRD OF PDRAP_SCIAGE = DZ_HEURE_2
  DISPLAY RSCHREPRD OF PDRAP_SCIAGE
  ;
  ; Calcul de la productivité
  ;
  LET RSCPRD OF PDRAP_SCIAGE = (RSCMC3PRD OF PDRAP_SCIAGE / (RSCHREPRD OF PDRAP_SCIAGE / 100))
  DISPLAY RSCPRD OF PDRAP_SCIAGE
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Opérateur sciage
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd218a &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDRAP_SCIAGE        &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Mise à jour des informations lors de la fin de sciage d'un montage
;

PROCEDURE INTERNAL TRAITE_MONTAGE
BEGIN
  WHILE RETRIEVING PDMONTAGE_BLOC &
    VIA   CIECLE,          &
          RMSNUMSEQ        &
    USING T_CIECLEMNU,     &
          RMSNUMSEQ OF PDDET_RAP_SCIAGE
  BEGIN
    GET PDBLOC &
      VIA   CIECLE,          &
            BLONUMBLOAPR     &
      USING T_CIECLEMNU,     &
            BLONUMBLOAPR OF PDMONTAGE_BLOC OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      LET BLOSTU OF PDBLOC = "T"
      PUT PDBLOC
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRAP_SCIAGE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRAP_SCIAGE &
     AND NOT NEWRECORD     OF PDRAP_SCIAGE &
     AND NOT DELETEDRECORD OF PDRAP_SCIAGE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2


  FOR PDDET_RAP_SCIAGE
  BEGIN
    ;
    ; Mise à jour des informations lors de la fin de sciage d'un montage
    ;
    IF DRSPOSFIN OF PDDET_RAP_SCIAGE = 0
    THEN BEGIN

      DO INTERNAL TRAITE_MONTAGE

      GET PDSCIAGE_BLOCS &
        VIA   CIECLE,           &
              RMSNUMSEQ         &
        USING T_CIECLEMNU,      &
              RMSNUMSEQ OF PDDET_RAP_SCIAGE OPTIONAL
      IF ACCESSOK
      THEN BEGIN
        LET SCBDATSCI OF PDSCIAGE_BLOCS = RSCDATPRD OF PDRAP_SCIAGE
        PUT PDSCIAGE_BLOCS
      END
      ELSE BEGIN
        LET CIECLE    OF PDSCIAGE_BLOCS = T_CIECLEMNU
        LET RMSNUMSEQ OF PDSCIAGE_BLOCS = RMSNUMSEQ OF PDDET_RAP_SCIAGE
        LET SCBDATSCI OF PDSCIAGE_BLOCS = RSCDATPRD OF PDRAP_SCIAGE
        PUT PDSCIAGE_BLOCS
      END
    END
  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Gérer le sciage ********************************
* Date..........: xxxxxxxx                     M3............: xxxxxxxxx       *
* Quart travail.: x xxxxxxxxxxxxxxxxxxxxxxxxx  Hre...........: xxxxx           *
* Hre début.....: xxxxx                        Productivité..: xxxxxxxxxxx     *
* Hre fin.......: xxxxx                                                        *
******************************** Détail sciage *********************************
*                                  * Position -*                               *
*                  Montage   Scie  Début     Fin      Cubage                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
*                xxxxxxxxx    xx    xxxx    xxxx   xxxxxxxxx                   *
********************************************************************************
@ENDIF
