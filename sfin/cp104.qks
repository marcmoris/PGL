;/T/ Consultation des fournissseurs    FOUCIECLE, FOUCLE, FOCCIECLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 15 avril 1992
;
;    Description..: Permet de visionner et de choisir un fournisseur.
;                   Seul les fournisseurs actif et qui peuvent utiliser
;                   la compagnie du menu sont sélectionnés.
;
;
;    Paramètre....: FOUCIECLE ==> - compagnie du fournisseur.
;                                 - segment de la clé de recherche.
;
;                   FOUCLE    ==> - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;                                 - contien la valeur de retour.
;
;                   FOCCIECLE ==> - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN cp104 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_FOUCIECLE, &
                        T_FOUCLE   , &
                        T_FOCCIECLE

USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP104"

USE cp104.hlp NOLIST

TEMPORARY T_FOUCIECLE CHARACTER SIZE 4
TEMPORARY T_FOUCLE    CHARACTER SIZE 6
TEMPORARY T_FOCCIECLE CHARACTER SIZE 4


;-------------------------------------------------------------------------------
;
FILE VFOC_FOU         PRIMARY &
                      OCCURS 11

 ACCESS   VIA   FOUCIECLE, &
                FOUCLE   , &
                FOCCIECLE  &
        USING   T_FOUCIECLE, &
                T_FOUCLE   , &
                T_FOCCIECLE  &
        ORDERBY FOUCIECLE , &
                FOUCLE    , &
                FOCCIECLE

 SELECT IF FOUSTU OF VFOC_FOU = "A"

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST
USE usdrwfnd NOLIST

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH VFOC_FOU

FIELD FOUCLE    OF VFOC_FOU HIDDEN LABEL ">>"
FIELD FOUNOMABR OF VFOC_FOU

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_FOUCLE, "@")
  THEN LET T_FOUCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_FOUCLE = FOUCLE OF VFOC_FOU
  RETURN
END

BUILD
;
;*****************************************
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;* >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;*****************************************
