;/T/ Gestion des centres.
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 1 avril 1993
;
;    Description..: Cet écran permet de gérer les centres pour le holding.
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc006 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIENOM

USE ussectmp NOLIST     ; Variable pour la securite
    "MC006"

USE mc006.hlp NOLIST

USE usactecr NOLIST     ; Actionbar standard

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding

;-------------------------------------------------------------------------------
;
FILE MCCENTRE         PRIMARY &
                      OCCURS 17
  ACCESS REQUEST CENCLE OF MCCENTRE &
         VIA     CENCLE &
         USING   CENCLE OF MCCENTRE &
         ORDERED
  ACCESS REQUEST CENNOM OF MCCENTRE &
         VIA     CENNOM &
         USING   CENNOM OF MCCENTRE &
         ORDERED
  ACCESS SEQUENTIAL &
         ORDERBY CENCLE

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Centres " &
@ELSE
      " Centres " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

TITLE &
@IF FRANCAIS
      "Numéro  Description                              Descripton abrégée" &
@ELSE
      "Number  Description                              Abbreviated desc. " &
@ENDIF
      AT ,3

SKIP
ALIGN(3,2,3) (,,11) (,,52)
CLUSTER OCCURS WITH MCCENTRE ID BASE 05
FIELD CENCLE    OF MCCENTRE HIDDEN LABEL " " &
        REQUIRED NOCHANGE &
        LOOKUP NOTON MCCENTRE &
          VIA CENCLE &
          USING CENCLE OF MCCENTRE &
          MESSAGE 119 ; "Ce centre existe déjà."

FIELD CENNOM    OF MCCENTRE &
        REQUIRED
FIELD CENNOMABR OF MCCENTRE &
        DEFAULT CENNOM    OF MCCENTRE

CLUSTER

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT CENCLE
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR MCCENTRE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF MCCENTRE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF MCCENTRE &
       AND NOT NEWRECORD     OF MCCENTRE &
       AND NOT DELETEDRECORD OF MCCENTRE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'un centre.
    ;
    IF DELETEDRECORD OF MCCENTRE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si le centre est référé.
      ;
      RUN SCREEN mc006x MODE SAME &
                        PASSING MCCENTRE , &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Centres ************************************
*                                                                              *
* Numéro  Description                              Descripton abrégée          *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
* xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
@ENDIF
