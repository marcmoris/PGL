;/T/ Liste des blocs expédiés non-facturés par transporteur.
;
;/M/ Réalisé par.......: Marc Poulin     
;    Date..............: 06 avril 2000
;    Description.......: Correction du traitement afin qu'il fonctionne selon les spécifications du client.
;
;    Référence.........: Stabilisation du module Gestion de blocs (point 07).
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
; Configuration standard
;
USE ussetqzs NOLIST
;
; Déclaration des accès
;
ACCESS IFBLOC LINK  CIECLE    OF IFBLOC         , &
                    EXNCLE    OF IFBLOC           &
                TO  CIECLE                      , &
                    EXNCLE    OF IFEXPEDITION     & ; Table des expéditions de blocs

              LINK ("----"                      , & ; Table des blocs
                    FOUCLE    OF IFBLOC)          &
                 TO FOUCIECLE                   , &
                    FOUCLE    OF CPFOURNISSEUR      ; Table des fournisseurs (Transporteur)
;
; Sélection selon les paramètres saisis par l'utilisateur
;
CHOOSE CIECLE    PARM, & ; Code de compagnie
       FOUCLE    PARM, & ; Code de fournisseur (Transporteur)
       EXNCLE    PARM   ; Code d'expédition
;
; Sélection des expéditions non facturées
;
SELECT IFEXPEDITION IF CAPCLE OF IFEXPEDITION = " "
;
; Définition du numéro de commande et du numéro de bloc
;
DEFINE D_NUMCOM  CHARACTER SIZE 09 = PJTABR OF IFBLOC    &
                                   + "-"                 &
                                   + PJTANN OF IFBLOC    &
                                   + "-"                 &
                                   + COMNUMCOMINT     

DEFINE D_NUMBLC  CHARACTER SIZE 14 = TGRCODGRA OF IFBLOC &
                                   + IFCCLE    OF IFBLOC &
                                   + "-"                 &
                                   + BLCCLE    OF IFBLOC &
                                   + "-"                 &
                                   + BLCRED    OF IFBLOC
;
; Variables associées aux entêtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF342"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM
;
; Tri
;
SORT ON FOUCLE OF IFBLOC       & ; Code de fournisseur (Transporteur)
     ON EXNCLE OF IFBLOC       & ; Code d'expédition
     ON D_NUMBLC                 ; Numéro de bloc
;
; Entête du rpport - Impression 60 lignes par 132 colonnes
;
USE ustit132_60 NOLIST 
 SKIP 2                                     &
  TAB 001 "Transporteur:"                   &
  TAB 015 FOUCLE OF IFBLOC                  & ; Code de fournisseur (Transporteur)
  TAB 022 FOUNOM OF CPFOURNISSEUR           & ; Nom du fournisseur  (Transporteur)
 SKIP 2                                     &
  TAB 001 "  Expédition     Date     Client   Via    Numéro de bloc   Quantité (m3)" &
 SKIP 1                                     &
  TAB 001 "  ----------  ----------  ------  ------  --------------  --------------" &
 SKIP
;
; Détail du rapport
;
REPORT                                      &
  TAB 005 EXNCLE       OF IFBLOC            & ; Numéro d'expédition associé au bloc
          PRINT AT EXNCLE                   &
  TAB 015 EXNDAT       OF IFEXPEDITION      & ; Date d'expédition
          PRINT AT EXNCLE                   &
  TAB 027 CLICLE       OF IFEXPEDITION      & ; Client
          PRINT AT EXNCLE                   &
  TAB 035 FOUTRP       OF IFEXPEDITION      & ; Fournisseur (Via)
          PRINT AT EXNCLE                   &
  TAB 043 D_NUMBLC                          & ; Numéro de bloc
  TAB 059 BLCM3TOT     OF IFBLOC            & ; Quantité totale du bloc en mêtres cube
          PICTURE "^^^,^^^,^^^.^^"          &
          SIGNIFICANCE 4                    &
          NOBWZ                             &
  RESERVE 5 LINES
;
; Sous-total par numéro d'expédition
;
FOOTING AT EXNCLE                           &
  TAB 059 "--------------"                  &
 SKIP 1                                     &
  TAB 059 BLCM3TOT     OF IFBLOC            & ; Quantité totale du bloc en mêtres cube
          SUBTOTAL                          &
          PICTURE "^^^,^^^,^^^.^^"          &
          SIGNIFICANCE 4                    &
          NOBWZ                             &
 SKIP 2
;
; Total par code de fournisseur (Transporteur)
;
FOOTING AT FOUCLE                           &
  TAB 059 "=============="                  &
 SKIP 1                                     &
  TAB 059 BLCM3TOT     OF IFBLOC            & ; Quantité totale du bloc en mêtres cube
          SUBTOTAL                          &
          PICTURE "^^^,^^^,^^^.^^"          &
          SIGNIFICANCE 4                    &
          NOBWZ                             &
 SKIP PAGE


BUILD if342z


@IF FORMAT
IF342                                               GRANICOR INC. (Développement)                                     PAGE:   1
marcpoul                               Liste des blocs expédiés non-facturés par transporteur                      00/04/06  14:16

Transporteur: XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

                                                                                          
  Expédition     Date     Client   Via    Numéro de bloc   Quantité (m3)
  ----------  ----------  ------  ------  --------------  --------------
    99999     9999/99/99  XXXXXX  XXXXXX  999XX-999999-X  999,999,999.99
                                          999XX-999999-X  999,999,999.99
                                          999XX-999999-X  999,999,999.99
                                          999XX-999999-X  999,999,999.99
                                                          --------------
                                                          999,999,999.99

    99999     9999/99/99  XXXXXX  XXXXXX  999XX-999999-X  999,999,999.99
                                          999XX-999999-X  999,999,999.99
                                                          --------------
                                                          999,999,999.99

                                                          ==============
                                                          999,999,999.99
@ENDIF

