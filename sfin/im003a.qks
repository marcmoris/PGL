;
;/M/ François Déry, 7 mai 1999
;       Ajout du code d'usager dans la table temporaire d'affichage
;
;
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN im003a      NOMODE NOACTION FIELDMARK STARTUP &
                   FROM 2,1 TO 13,80 &
                   MESSAGE ON 24 &
                   HELP POPUP FROM 4,9 TO 20,72 &
                   RECEIVING T_CIECLEFND

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

;--------------------
; Fichier des projets
;--------------------
TEMPORARY T_IMMCLE    CHARACTER SIZE 12 RESET AT STARTUP
TEMPORARY T_IMMCLE2   CHARACTER SIZE 12 RESET AT STARTUP
TEMPORARY T_IMMSTUPAT CHARACTER SIZE 8

FILE IMIMMOBILISATION       REFERENCE

  ACCESS  VIA   CIECLE, &
                IMMCLE  &
          USING T_CIECLEFND, &
                T_IMMCLE



;--------------------
; Fichier des formats
;--------------------
TEMPORARY T_FRMCLE    CHARACTER SIZE 4  RESET AT STARTUP
TEMPORARY T_FRMCLE2   CHARACTER SIZE 4  RESET AT STARTUP
TEMPORARY T_FRMTYP    CHARACTER SIZE 1  RESET AT STARTUP

FILE GPFORMATS              REFERENCE

  ACCESS  VIA   FRMCLE  &
          USING T_FRMCLE

FILE IMIMMOBILISATION       DESIGNER &
                            ALIAS IMIMMOBILISATION_2

FILE GPFRM_AFFICHE          DESIGNER
FILE GPFRM_PROJETS          DESIGNER
FILE GPFOR_DETAILS          DESIGNER
FILE VIMM01                 DESIGNER

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW  1,1 TO 12,80

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1

ALIGN (3,3,19) (,,35)
FIELD T_IMMCLE &
      HIDDEN &
      LABEL "Immobilisation:" &
      REQUIRED &
      LOOKUP ON IMIMMOBILISATION &
            MESSAGE 491 ; Cette immobilisation n'existe pas ou est inactive.

FIELD IMMDSC  OF IMIMMOBILISATION &
      DISPLAY

SKIP 2

ALIGN (3,3,19) (,,25)

FIELD T_FRMCLE &
      HIDDEN &
      LABEL "Format........:" &
      REQUIRED &
      LOOKUP ON GPFORMATS MESSAGE 878

FIELD FRMDSC  OF GPFORMATS &
      DISPLAY


;-------------------------------------------------------------------------------
;
; Popup sur les immobilisations.
;
PROCEDURE INPUT T_IMMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IMMCLE2 = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN im102 MODE F &
                     PASSING T_CIECLEFND , &
                             T_IMMCLE2  WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_IMMCLE2 )
  END
END

PROCEDURE EDIT T_IMMCLE
BEGIN
   IF IMMTYP OF IMIMMOBILISATION <> "E"
   THEN ERROR 1503
END



;-------------------------------------------------------------------------------
;
; Popup sur les formats
;
PROCEDURE INPUT T_FRMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FRMCLE2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_FRMTYP  = "C"
    RUN SCREEN gp109 MODE F PASSING T_FRMCLE2, T_FRMTYP &
                                    WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_FRMCLE2)
  END
END


PROCEDURE EDIT T_FRMCLE
BEGIN
  IF FRMTYP OF GPFORMATS <> "C"
  THEN ERROR 885 ; Le format de présentation doit être comptable.
END

PROCEDURE INTERNAL LIGNE_SOULIGNE_TEXTE
BEGIN
   WHILE RETRIEVING GPFOR_DETAILS &
         VIA   FRMCLE,            &
               FRDTYP             &
         USING T_FRMCLE,          &
               "S"
   BEGIN
           LET USRCLE OF GPFRM_AFFICHE = LOGONID
           LET CIECLE OF GPFRM_AFFICHE = CIECLE OF IMIMMOBILISATION_2
           LET IMMCLE OF GPFRM_AFFICHE = IMMCLE OF IMIMMOBILISATION_2
           LET FRMCLE OF GPFRM_AFFICHE = T_FRMCLE
           LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF GPFOR_DETAILS
           LET FRDTYP OF GPFRM_AFFICHE = "S"
           LET FRAMNTBDG OF GPFRM_AFFICHE = 0
           LET FRAMNTENG OF GPFRM_AFFICHE = 0
           LET FRAMNTREE OF GPFRM_AFFICHE = 0
           LET FRABDG    OF GPFRM_AFFICHE = 0
           LET FRAREE    OF GPFRM_AFFICHE = 0
           PUT GPFRM_AFFICHE RESET
   END

   WHILE RETRIEVING GPFOR_DETAILS &
         VIA   FRMCLE,            &
               FRDTYP             &
         USING T_FRMCLE,          &
               "T"
   BEGIN
           LET USRCLE OF GPFRM_AFFICHE = LOGONID
           LET CIECLE OF GPFRM_AFFICHE = CIECLE OF IMIMMOBILISATION_2
           LET IMMCLE OF GPFRM_AFFICHE = IMMCLE OF IMIMMOBILISATION_2
           LET FRMCLE OF GPFRM_AFFICHE = T_FRMCLE
           LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF GPFOR_DETAILS
           LET FRDTYP OF GPFRM_AFFICHE = "T"
           LET FRAMNTBDG OF GPFRM_AFFICHE = 0
           LET FRAMNTENG OF GPFRM_AFFICHE = 0
           LET FRAMNTREE OF GPFRM_AFFICHE = 0
           LET FRABDG    OF GPFRM_AFFICHE = 0
           LET FRAREE    OF GPFRM_AFFICHE = 0
           PUT GPFRM_AFFICHE RESET
   END
END


PROCEDURE PREUPDATE
BEGIN
info = "debut" now
   ;----------------------------------------
   ; Destruction des fichiers temporaires
   ;----------------------------------------

   WHILE RETRIEVING GPFRM_PROJETS    &
              VIA   FRPUSR           &
              USING LOGONID[1:8]
   BEGIN
      DELETE GPFRM_PROJETS
      PUT GPFRM_PROJETS RESET
   END

   WHILE RETRIEVING GPFRM_AFFICHE VIA USRCLE USING LOGONID
   BEGIN
     DELETE GPFRM_AFFICHE
     PUT GPFRM_AFFICHE
   END
   WHILE RETRIEVING IMIMMOBILISATION_2 &
              VIA   CIECLE,          &
                    IMMCLE           &
              USING T_CIECLEFND,     &
                    T_IMMCLE
   BEGIN
           LET CIECLE OF GPFRM_PROJETS = CIECLE OF IMIMMOBILISATION_2
           LET IMMCLE OF GPFRM_PROJETS = IMMCLE OF IMIMMOBILISATION_2
           LET FRMCLE OF GPFRM_PROJETS = T_FRMCLE
           LET FRPUSR OF GPFRM_PROJETS = LOGONID[1:8]
           PUT GPFRM_PROJETS RESET

           DO LIGNE_SOULIGNE_TEXTE
   END

info = "2" now
   ;--------------------------------------------
   ; Ligne de calcul et de formule (SOUS-PROJET)
   ;--------------------------------------------
   WHILE RETRIEVING VIMM01 &
              VIA   CIECLE,     &
                    IMMCLE,     &
                    FRMCLE      &
              USING T_CIECLEFND,&
                    T_IMMCLE,   &
                    T_FRMCLE
   BEGIN
           GET GPFRM_AFFICHE &
               VIA   USRCLE, &
                     FRDCLE  &
               USING LOGONID, &
                     FRDCLE OF VIMM01 OPTIONAL
           IF NOT ACCESSOK
           THEN BEGIN
                LET USRCLE OF GPFRM_AFFICHE = LOGONID
                LET CIECLE OF GPFRM_AFFICHE = CIECLE OF VIMM01
                LET IMMCLE OF GPFRM_AFFICHE = IMMCLE OF VIMM01
                LET FRMCLE OF GPFRM_AFFICHE = FRMCLE OF VIMM01
                LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF VIMM01
                LET FRDTYP OF GPFRM_AFFICHE = "C"
           END

           IF FDOOPE OF VIMM01 = "+"
           THEN BEGIN
                LET FRAMNTENG OF GPFRM_AFFICHE = FRAMNTENG OF GPFRM_AFFICHE + &
                                                 V_MNTENG  OF VIMM01

                LET FRAMNTREE OF GPFRM_AFFICHE = FRAMNTREE OF GPFRM_AFFICHE + &
                                                 V_MNTREE  OF VIMM01

                LET FRAMNTBDG OF GPFRM_AFFICHE = FRAMNTBDG OF GPFRM_AFFICHE + &
                                                 V_MNTBDG  OF VIMM01
           END
           ELSE BEGIN
                LET FRAMNTENG OF GPFRM_AFFICHE = FRAMNTENG OF GPFRM_AFFICHE - &
                                                 V_MNTENG  OF VIMM01

                LET FRAMNTREE OF GPFRM_AFFICHE = FRAMNTREE OF GPFRM_AFFICHE - &
                                                 V_MNTREE  OF VIMM01

                LET FRAMNTBDG OF GPFRM_AFFICHE = FRAMNTBDG OF GPFRM_AFFICHE - &
                                                 V_MNTBDG  OF VIMM01
           END
           PUT GPFRM_AFFICHE RESET
   END
END

PROCEDURE POSTUPDATE
BEGIN
   RETURN
END

PROCEDURE ENTRY
BEGIN
  ACCEPT T_IMMCLE
  DISPLAY IMMDSC  OF IMIMMOBILISATION
  ACCEPT T_FRMCLE
  DISPLAY FRMDSC OF GPFORMATS
  PUSH UPDATE
END

PROCEDURE DELETE
  DISABLE


PROCEDURE APPEND
  DISABLE

PROCEDURE FIND
  DISABLE


BUILD
