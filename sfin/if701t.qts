;/T/ Transfert des cumulatifs clients par carrières
;
; Modifié par.: Guy Chabot le 25 août 1994
;
; Description.: Rendre cette procédure par période comptable en remplacement
;               du mois.
;
;/M/ François Déry, 25 mai 1999
;       Conversion vers unix/interbase.
;
USE ussetqts NOLIST
;
RUN if701t

GLOBAL TEMPORARY G_CIECLE CHARACTER SIZE 4 PARM

REQUEST SELECTION_PERIODE

ACCESS MCPERIODE_CTB

CHOOSE PECCLE PARM

TEMPORARY T_FLGFIN CHARACTER SIZE 1

  ITEM T_FLGFIN = "0"

SUBFILE WIF701 KEEP &
        INCLUDE PECCLE    OF MCPERIODE_CTB, &
                PECDATDEB OF MCPERIODE_CTB, &
                PECDATFIN OF MCPERIODE_CTB, &
                T_FLGFIN
;
REQUEST AJOUT_FACTURE_MOIS

ACCESS *WIF701 &
 LINK G_CIECLE, &
      PECCLE OF WIF701  &
   TO CIECLE, &
      PECCLE                            OF IFFACTURE &
 LINK G_CIECLE, &
      FCECLE OF IFFACTURE &
   TO CIECLE, &
      FCECLE                            OF IFFACTURE_DETAIL &
 LINK G_CIECLE, &
      "ANCO" &
   TO CIECLE, &
      ANNCODE                           OF IFANNEE_COURANTE

SORT ON CLTCLE OF IFFACTURE &
     ON CRRCLENUM OF IFFACTURE_DETAIL

DEF  TMP-VENTE-FOB INTEGER SIZE 4 = FLOOR(FDEM3PROD * FDEPRIXFOB / 100 + .5 )
DEF  TMP-VENTE-FAS INTEGER SIZE 4 = FLOOR(FDEM3PROD * FDEPRIXFAS / 100 + .5 )

OUTPUT IFCLIENT_CAR_CUM ADD AT CRRCLENUM
 ITEM CIECLE           OF IFCLIENT_CAR_CUM INIT CIECLE OF IFFACTURE
 ITEM CLTCLE           OF IFCLIENT_CAR_CUM INIT CLTCLE OF IFFACTURE
 ITEM CRRCLENUM        OF IFCLIENT_CAR_CUM INIT CRRCLENUM OF IFFACTURE_DETAIL
 ITEM CRRCLEANN        OF IFCLIENT_CAR_CUM INIT ANNNO OF IFANNEE_COURANTE
 ITEM CCAMOIS          OF IFCLIENT_CAR_CUM INIT NCONVERT(PECCLE OF WIF701)
 ITEM CCAQTEFAC        OF IFCLIENT_CAR_CUM SUB FDEM3PROD RESET AT CRRCLENUM
 ITEM CCAVENTEFOBCN    OF IFCLIENT_CAR_CUM SUB TMP-VENTE-FOB &
      IF DEVCLE OF IFFACTURE_DETAIL = "CAN"  RESET AT CRRCLENUM
 ITEM CCAVENTEFASCN    OF IFCLIENT_CAR_CUM SUB TMP-VENTE-FAS &
      IF DEVCLE OF IFFACTURE_DETAIL = "CAN"  RESET AT CRRCLENUM
 ITEM CCAVENTEFOBUS    OF IFCLIENT_CAR_CUM SUB TMP-VENTE-FOB &
      IF DEVCLE OF IFFACTURE_DETAIL = "USA"  RESET AT CRRCLENUM
 ITEM CCAVENTEFASUS    OF IFCLIENT_CAR_CUM SUB TMP-VENTE-FAS &
       IF DEVCLE OF IFFACTURE_DETAIL = "USA"  RESET AT CRRCLENUM

;
REQUEST CUMUL_AJUST_FACTURE

ACCESS *WIF701 &
 LINK G_CIECLE, &
      PECCLE &
   TO CIECLE, &
      PECCLE                            OF IFFACTURE &
 LINK G_CIECLE, &
      FCECLE &
   TO CIECLE, &
      FCECLE                            OF IFFACTURE_AJUST &
 LINK G_CIECLE, &
      "ANCO" &
   TO CIECLE, &
      ANNCODE                           OF IFANNEE_COURANTE &
 LINK G_CIECLE, &
      CLTCLE OF IFFACTURE &
   TO CIECLE, &
      CLTCLE                            OF IFCLIENT &
 LINK CLICIECLE OF IFCLIENT, &
      CLICLE OF IFCLIENT &
   TO CLICIECLE, &
      CLICLE                            OF CRCLIENT

SORT ON CLTCLE OF IFFACTURE &
     ON CRRCLENUM OF IFFACTURE

TEMP T-AJT-CN INTEGER SIZE 4
TEMP T-AJT-US INTEGER SIZE 4

ITEM T-AJT-CN SUB FAJAJUST IF DEVCLE OF CRCLIENT = "CAN" RESET AT CRRCLENUM
ITEM T-AJT-US SUB FAJAJUST IF DEVCLE OF CRCLIENT = "USA" RESET AT CRRCLENUM

OUTPUT IFCLIENT_CAR_CUM ADD UPDATE AT CRRCLENUM &
        VIA   CIECLE, CLTCLE, CRRCLENUM, CRRCLEANN, CCAMOIS &
        USING G_CIECLE, CLTCLE OF IFFACTURE, CRRCLENUM OF IFFACTURE, &
              ANNNO OF IFANNEE_COURANTE, NCONVERT( PECCLE OF WIF701 )
 ITEM CIECLE           OF IFCLIENT_CAR_CUM INIT CIECLE OF IFFACTURE
 ITEM CLTCLE           OF IFCLIENT_CAR_CUM INIT CLTCLE OF IFFACTURE
 ITEM CRRCLENUM        OF IFCLIENT_CAR_CUM INIT CRRCLENUM OF IFFACTURE
 ITEM CRRCLEANN        OF IFCLIENT_CAR_CUM INIT ANNNO OF IFANNEE_COURANTE
 ITEM CCAMOIS          OF IFCLIENT_CAR_CUM INIT NCONVERT( PECCLE OF WIF701 )
 ITEM CCAQTEFAC        OF IFCLIENT_CAR_CUM = CCAQTEFAC
 ITEM CCAVENTEFOBCN    OF IFCLIENT_CAR_CUM = CCAVENTEFOBCN
 ITEM CCAVENTEFASCN    OF IFCLIENT_CAR_CUM = CCAVENTEFASCN
 ITEM CCAVENTEFOBUS    OF IFCLIENT_CAR_CUM = CCAVENTEFOBUS
 ITEM CCAVENTEFASUS    OF IFCLIENT_CAR_CUM = CCAVENTEFASUS
 ITEM CCAAJUSTCN       OF IFCLIENT_CAR_CUM = CCAAJUSTCN + T-AJT-CN
 ITEM CCAAJUSTUS       OF IFCLIENT_CAR_CUM = CCAAJUSTUS + T-AJT-US

;
REQUEST CUMUL_CREDIT_DETAIL

ACCESS *WIF701 &
 LINK G_CIECLE, &
      PECCLE OF WIF701 &
   TO CIECLE, &
      PECCLE                            OF IFNOT_CRE_MAITRE &
 LINK G_CIECLE, &
      "ANCO" &
   TO CIECLE, &
      ANNCODE                           OF IFANNEE_COURANTE &
 LINK G_CIECLE, &
      CRECLE OF IFNOT_CRE_MAITRE &
   TO CIECLE, &
      CRECLE                            OF IFNOTE_CREDIT &
 LINK G_CIECLE, &
      FCECLE OF IFNOT_CRE_MAITRE &
   TO CIECLE, &
      FCECLE                            OF IFFACTURE &
 LINK G_CIECLE, &
      FCECLE OF IFFACTURE, &
      CRRCLENUM OF IFNOTE_CREDIT, &
      CRRCLEANN OF IFNOTE_CREDIT, &
      BLCCLE OF IFNOTE_CREDIT &
   TO CIECLE, &
      FCECLE, &
      CRRCLENUM, &
      CRRCLEANN, &
      BLCCLE                            OF IFFACTURE_DETAIL


SORT ON CLTCLE OF IFFACTURE &
     ON CRRCLENUM OF IFNOTE_CREDIT

DEF  TMP-M3-AJUS INTEGER SIZE 4 = FDEM3PROD - CREM3AJUS
DEF  TMP-RECL-FOB INTEGER SIZE 4 = FLOOR((FDEM3PROD - CREM3AJUS) * CREPRIXFOB / 100 + .5 )
DEF  TMP-RECL-FAS INTEGER SIZE 4 = FLOOR((FDEM3PROD - CREM3AJUS) * CREPRIXFAS / 100 + .5 )

OUTPUT IFCLIENT_CAR_CUM ADD UPDATE AT CRRCLENUM &
        VIA   CIECLE, CLTCLE, CRRCLENUM, CRRCLEANN, CCAMOIS &
        USING G_CIECLE, CLTCLE OF IFFACTURE, CRRCLENUM OF IFNOTE_CREDIT, &
              ANNNO OF IFANNEE_COURANTE, NCONVERT( PECCLE OF WIF701 )
 ITEM CIECLE         OF IFCLIENT_CAR_CUM INIT CIECLE OF IFNOT_CRE_MAITRE
 ITEM CLTCLE         OF IFCLIENT_CAR_CUM INIT CLTCLE OF IFFACTURE
 ITEM CRRCLENUM      OF IFCLIENT_CAR_CUM INIT CRRCLENUM OF IFNOTE_CREDIT
 ITEM CRRCLEANN      OF IFCLIENT_CAR_CUM INIT ANNNO OF IFANNEE_COURANTE
 ITEM CCAMOIS        OF IFCLIENT_CAR_CUM INIT NCONVERT( PECCLE OF WIF701 )
 ITEM CCANBBLRECL    OF IFCLIENT_CAR_CUM SUB CREQTEBLAJUS RESET AT CRRCLENUM
 ITEM CCAQTERECL     OF IFCLIENT_CAR_CUM SUB TMP-M3-AJUS RESET AT CRRCLENUM
 ITEM CCARECLFOBCN   OF IFCLIENT_CAR_CUM SUB TMP-RECL-FOB &
                        IF DEVCLE OF IFNOTE_CREDIT = "CAN"  RESET AT CRRCLENUM
 ITEM CCARECLFASCN   OF IFCLIENT_CAR_CUM SUB TMP-RECL-FAS &
                        IF DEVCLE OF IFNOTE_CREDIT = "CAN"  RESET AT CRRCLENUM
 ITEM CCARECLFOBUS   OF IFCLIENT_CAR_CUM SUB TMP-RECL-FOB &
                        IF DEVCLE OF IFNOTE_CREDIT = "USA"  RESET AT CRRCLENUM
 ITEM CCARECLFASUS   OF IFCLIENT_CAR_CUM SUB TMP-RECL-FAS &
                        IF DEVCLE OF IFNOTE_CREDIT = "USA"  RESET AT CRRCLENUM

;
REQUEST CUMUL_CREDIT_SIMPLE

ACCESS *WIF701 &
 LINK G_CIECLE, &
      PECCLE OF WIF701 &
   TO CIECLE, &
      PECCLE                            OF IFNOT_CRE_MAITRE &
 LINK G_CIECLE, &
      "ANCO" &
   TO CIECLE, &
      ANNCODE                           OF IFANNEE_COURANTE &
 LINK G_CIECLE, &
      FCECLE OF IFNOT_CRE_MAITRE &
   TO CIECLE, &
      FCECLE                            OF IFFACTURE &
 LINK G_CIECLE, &
      CLTCLE OF IFFACTURE &
   TO CIECLE, &
      CLTCLE                            OF IFCLIENT &
 LINK CLICIECLE OF IFCLIENT, &
      CLICLE OF IFCLIENT &
   TO CLICIECLE, &
      CLICLE                            OF CRCLIENT

SELECT IF NCMFACTOT <> 0

SORT ON CLTCLE OF IFFACTURE &
     ON CRRCLENUM OF IFFACTURE

TEMP T-FOB-CN INTEGER SIZE 4
TEMP T-FOB-US INTEGER SIZE 4

ITEM T-FOB-CN SUB NCMFACTOT IF DEVCLE OF CRCLIENT = "CAN" RESET AT CRRCLENUM
ITEM T-FOB-US SUB NCMFACTOT IF DEVCLE OF CRCLIENT = "USA" RESET AT CRRCLENUM

OUTPUT IFCLIENT_CAR_CUM ADD UPDATE AT CRRCLENUM &
        VIA   CIECLE, CLTCLE, CRRCLENUM, CRRCLEANN, CCAMOIS &
        USING G_CIECLE, CLTCLE OF IFFACTURE, CRRCLENUM OF IFFACTURE, &
              ANNNO OF IFANNEE_COURANTE, NCONVERT( PECCLE OF WIF701 )
 ITEM CIECLE          OF IFCLIENT_CAR_CUM INIT CIECLE OF IFNOT_CRE_MAITRE
 ITEM CLTCLE          OF IFCLIENT_CAR_CUM INIT CLTCLE OF IFFACTURE
 ITEM CRRCLENUM       OF IFCLIENT_CAR_CUM INIT CRRCLENUM OF IFFACTURE
 ITEM CRRCLEANN       OF IFCLIENT_CAR_CUM INIT ANNNO OF IFANNEE_COURANTE
 ITEM CCAMOIS         OF IFCLIENT_CAR_CUM INIT NCONVERT( PECCLE OF WIF701 )
 ITEM CCANBBLRECL     OF IFCLIENT_CAR_CUM INIT CCANBBLRECL
 ITEM CCAQTERECL      OF IFCLIENT_CAR_CUM INIT CCAQTERECL
 ITEM CCARECLFOBCN    OF IFCLIENT_CAR_CUM = CCARECLFOBCN
 ITEM CCARECLFOBUS    OF IFCLIENT_CAR_CUM = CCARECLFOBUS
 ITEM CCAAJUSTCN      OF IFCLIENT_CAR_CUM = CCAAJUSTCN - T-FOB-CN
 ITEM CCAAJUSTUS      OF IFCLIENT_CAR_CUM = CCAAJUSTUS - T-FOB-US

;
REQUEST MAJ_INDICATEUR

ACCESS *WIF701

OUTPUT *WIF701 UPDATE
  ITEM T_FLGFIN OF WIF701 FINAL "1"

BUILD
