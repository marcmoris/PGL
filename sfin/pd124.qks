;/T/ Commande interne
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1994-10-25
;
;    Description..: Permet de visionner et de choisir un une commande interne
;
;    Paramètres...: T_PJTABR          ==> Abréviation du projet
;                   T_PJTANN          ==> Année de la commande interne
;                   T_COMNUMCOMINT    ==> Commande interne
;
;    Attention....: La procédure find a été codé
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd124 NOMODE NOACTION FIELDMARK &
             FROM 11,45 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_PJTABR ,         &
                       T_PJTANN ,         &
                       T_COMNUMCOMINT,    &
                       T_COMNUMCOM,       &
                       T_CHAMP,           &
                       T_CIECLEMNU


TEMPORARY T_PJTABR          CHARACTER SIZE 02
TEMPORARY T_PJTANN          CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT    CHARACTER SIZE 03
TEMPORARY T_COMNUMCOM       CHARACTER SIZE 07
TEMPORARY T_CHAMP           INTEGER UNSIGNED SIZE 1
TEMPORARY T_CIECLEMNU       CHARACTER SIZE 04


USE pd124.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Bloc
;

FILE PDCOMMAN_INTERNE PRIMARY OCCURS 9

  ACCESS VIA     CIECLE,            &
                 PJTABR,            &
                 PJTANN,            &
                 COMNUMCOMINT       &
         USING   T_CIECLEMNU,       &
                 T_PJTABR,          &
                 T_PJTANN,          &
                 T_COMNUMCOMINT     &
         ORDERBY CIECLE,            &
                 PJTABR,            &
                 PJTANN,            &
                 COMNUMCOMINT


  ACCESS VIA     CIECLE,            &
                 COMNUMCOM          &
         USING   T_CIECLEMNU,       &
                 T_COMNUMCOM        &
         ORDERBY CIECLE,            &
                 COMNUMCOM


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
DRAW 1,1 TO 13,36       ; Remplace le usdrwfnd standard.
USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2

TITLE &
@IF FRANCAIS
      "  Comman. int.  Projet    C.I." &
@ELSE
      "% Comman. int.  Projet    C.I." &
@ENDIF
    AT ,4

SKIP TO LINE 3

ALIGN(3,3,06) (,09,11) (,13,14) (,18,18) (,30,30)


CLUSTER OCCURS WITH PDCOMMAN_INTERNE


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        HIDDEN LABEL ">>" DISPLAY


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        LABEL "-"


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        LABEL "-"


FIELD PJTNUMPRO        OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        LABEL " "

FIELD COMHREMAJATR OF PDCOMMAN_INTERNE PIC "^^^^" DISPLAY NOLABEL

CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_PJTABR , "@")
  THEN LET T_PJTABR = ""

  IF 0 <> INDEX(T_PJTANN , "@")
  THEN LET T_PJTANN = ""

  IF 0 <> INDEX(T_COMNUMCOMINT , "@")
  THEN LET T_COMNUMCOMINT = ""

  IF 0 <> INDEX(T_COMNUMCOM , "@")
  THEN LET T_COMNUMCOM = ""

END


;-------------------------------------------------------------------------------
;
; Procédure de recherche
;

PROCEDURE FIND
BEGIN
  ;
  ; Lors de l'appel on a passé seulement le premier parametre pour la recherche
  ;
  IF T_CHAMP = 1
  THEN BEGIN
    FOR PDCOMMAN_INTERNE
    BEGIN
      GET PDCOMMAN_INTERNE       &
        VIA     CIECLE,          &
                PJTABR           &
        USING   T_CIECLEMNU,     &
                T_PJTABR         &
        ORDERBY CIECLE,          &
                PJTABR ,         &
                PJTANN ,         &
                COMNUMCOMINT
    END
  END
  IF T_CHAMP = 2
  THEN BEGIN
    ;
    ; Lors de l'appel on a passé seulement les deux premier parametre
    ; pour la recherche
    ;
    FOR PDCOMMAN_INTERNE
    BEGIN
      GET PDCOMMAN_INTERNE     &
        VIA     CIECLE,        &
                PJTABR ,       &
                PJTANN         &
        USING   T_CIECLEMNU,   &
                T_PJTABR ,     &
                T_PJTANN       &
        ORDERBY CIECLE,        &
                PJTABR ,       &
                PJTANN ,       &
                COMNUMCOMINT
    END
  END
  IF T_CHAMP = 3
  THEN BEGIN
    ;
    ; Lors de l'appel on a passé les trois parametres pour la recherche
    ;
    FOR PDCOMMAN_INTERNE
    BEGIN
      GET PDCOMMAN_INTERNE     &
        VIA     CIECLE,        &
                PJTABR ,       &
                PJTANN ,       &
                COMNUMCOMINT   &
        USING   T_CIECLEMNU,   &
                T_PJTABR ,     &
                T_PJTANN ,     &
                T_COMNUMCOMINT &
        ORDERBY CIECLE,        &
                PJTABR ,       &
                PJTANN ,       &
                COMNUMCOMINT
    END
  END
  IF T_CHAMP = 4
  THEN BEGIN
    ;
    ; Lors de l'appel on a passé les trois parametres pour la recherche
    ;
    FOR PDCOMMAN_INTERNE
    BEGIN
      GET PDCOMMAN_INTERNE     &
        VIA     CIECLE,        &
                COMNUMCOM      &
        USING   T_CIECLEMNU,   &
                T_COMNUMCOM    &
        ORDERBY CIECLE,        &
                COMNUMCOM
    END
  END
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_PJTABR        = PJTABR             OF PDCOMMAN_INTERNE
  LET T_PJTANN        = PJTANN             OF PDCOMMAN_INTERNE
  LET T_COMNUMCOMINT  = COMNUMCOMINT       OF PDCOMMAN_INTERNE
  LET T_COMNUMCOM     = COMNUMCOM          OF PDCOMMAN_INTERNE
  RETURN
END

BUILD

@IF FORMAT

                                            ************************************
                                            *      Comman. int.   Projet       *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            * >>   xx-xx-xxx      xxxxxxxxx    *
                                            *                                  *
                                            ************************************
@ENDIF
