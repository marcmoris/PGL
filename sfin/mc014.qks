;/T/ Gestion des comptes de banque
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 6 juillet 1993
;
;    Description..: Cet écran permet la gestion des institutions bancaire
;                   servant aux C/P et à la paie.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc014 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIENOM

USE ussectmp NOLIST
    "MC014"

USE mc014.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIENOM CHARACTER SIZE 40; Nom de la compagnie-holding


;-------------------------------------------------------------------------------
;
;
FILE MCLIVRET_BQ      PRIMARY
  ACCESS REQUEST CIECLE    OF MCLIVRET_BQ, &
                 LBQCPTENC OF MCLIVRET_BQ  &
         VIA     CIECLE   , &
                 LBQCPTENC  &
         USING   CIECLE    OF MCLIVRET_BQ, &
                 LBQCPTENC OF MCLIVRET_BQ  &
         ORDERED

  ACCESS REQUEST CIECLE    OF MCLIVRET_BQ &
         VIA     CIECLE &
         USING   CIECLE    OF MCLIVRET_BQ &
         ORDERBY CIECLE, &
                 LBQCPTENC

  ACCESS SEQUENTIAL &
         ORDERBY CIECLE, &
                 LBQCPTENC

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      REFERENCE
  ACCESS VIA CIECLE &
         USING CIECLE OF MCLIVRET_BQ

;
; Validation du compte d'encaisse.
;
FILE VCPT_DSC         REFERENCE
  ACCESS VIA CPTCLE &
         USING LBQCPTENC OF MCLIVRET_BQ

;
; Validation de l'institution bancaire.
;
FILE MCINST_BANCAIRE  REFERENCE
  ACCESS VIA INBCLENUM &
         USING INBCLENUM OF MCLIVRET_BQ


;-------------------------------------------------------------------------------
;

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies

TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes

TEMPORARY T_INBCLENUM CHARACTER SIZE 3 ; Popup sur les institutions bancaire

TEMPORARY T_FLGDEL    CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Gestion des comptes de banque " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

ALIGN (3,3,19) (,,26)

FIELD CIECLE OF MCLIVRET_BQ HIDDEN ID 05 &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON MCCOMPAGNIE &
          MESSAGE 120 ; Cette compagnie n'existe pas.

FIELD CIENOM OF MCCOMPAGNIE &
        DISPLAY

FIELD LBQCPTENC OF MCLIVRET_BQ ID SAME &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON VCPT_DSC &
          MESSAGE 111, & ; Ce compte n'existe pas.
        NOTON MCLIVRET_BQ &
          VIA CIECLE   , &
              LBQCPTENC  &
          USING CIECLE    OF MCLIVRET_BQ, &
                LBQCPTENC OF MCLIVRET_BQ &
          MESSAGE 124 ; Ce compte existe déjà.

FIELD CPTDSC OF VCPT_DSC &
        DISPLAY

SKIP 1

FIELD INBCLENUM OF MCLIVRET_BQ HIDDEN ID 10 &
        REQUIRED &
        LOOKUP ON MCINST_BANCAIRE &
          MESSAGE 155; Cette institution bancaire n'existe pas.

FIELD INBNOM OF MCINST_BANCAIRE &
        DISPLAY

ALIGN (3,3,19)

FIELD LBQTST OF MCLIVRET_BQ HIDDEN ID 15 &
        REQUIRED

FIELD LBQNUMLIV OF MCLIVRET_BQ HIDDEN ID 20 &
        REQUIRED

SKIP 1

FIELD LBQADR1 OF MCLIVRET_BQ HIDDEN ID 25
FIELD LBQADR2 OF MCLIVRET_BQ ID SAME NOLABEL
FIELD LBQADR3 OF MCLIVRET_BQ ID SAME NOLABEL
FIELD LBQADR4 OF MCLIVRET_BQ ID SAME NOLABEL
FIELD LBQCP   OF MCLIVRET_BQ HIDDEN ID 30

SKIP 1

FIELD LBQTEL OF MCLIVRET_BQ HIDDEN ID 35

SKIP 1

FIELD LBQLONNUM OF MCLIVRET_BQ &
      HIDDEN ID 40 &
      REQUIRED

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT CIECLE
BEGIN
  ;
  ; Popup sur les compagnies.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 MODE F PASSING T_CIECLE
    LET FIELDTEXT = TRUNCATE( T_CIECLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les comptes comptable.
;
;
PROCEDURE INPUT LBQCPTENC
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R" ; Régulier
    LET T_CPTCATPAT = "EN" ; Catégorie
    LET T_PECCLE    = " "
    RUN SCREEN mc103 PASSING T_CPTCLE, T_CPTTYPPAT, T_CPTCATPAT, T_PECCLE MODE F
    LET FIELDTEXT = TRUNCATE( T_CPTCLE )
  END
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT INBCLENUM
BEGIN
  ;
  ; Popup sur les institutions bancaires
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_INBCLENUM = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc107 MODE F PASSING T_INBCLENUM
    LET FIELDTEXT = TRUNCATE( T_INBCLENUM )
  END
END


;-------------------------------------------------------------------------------
;
;
; Formatte le code postal.
;
;
PROCEDURE INPUT LBQCP
BEGIN
  USE uscpfmt NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT LBQTEL
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF MCLIVRET_BQ &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF MCLIVRET_BQ &
     AND NOT NEWRECORD     OF MCLIVRET_BQ &
     AND NOT DELETEDRECORD OF MCLIVRET_BQ &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction d'un compte de banque.
  ;
  IF DELETEDRECORD OF MCLIVRET_BQ
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le compte de banque est référée.
    ;
    RUN SCREEN mc014x MODE SAME &
                      PASSING MCLIVRET_BQ , &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************ Gestion des comptes de banque *************************
*                                                                              *
* Compagnie.....: xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
* Cpt encaisse..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
*                                                                              *
* Institution...: xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
* Transit.......: xxxxxxxxxxxx                                                 *
* Cpt bancaire..: xxxxxxxxxx                                                   *
*                                                                              *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Code postal...: xxxxxxx                                                      *
*                                                                              *
* Téléphone.....: xxxxxxxxxxxxx                                                *
*                                                                              *
* Lon. Num. chq.: xx                                                           *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
