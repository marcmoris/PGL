;/T/ Copie d'un format d'état financier
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 16 Avril 1993
;
;/V3.02.00/ Alain Côté, 6 avril 1994 , 199
;
;    Description..: Écran pour la copie d'un état financier. L'état
;                   financier d'origine doit exister et celui de destination
;                   ne doit pas exister.
;
;/M/ Modifié par..: Guy Chabot le 3 novembre 1994
;            but..: Permetttre la copie de détail de format si l'état
;                   financier cible existe déjà.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gl014  ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES FIND,CHANGE &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU


USE gl014.hlp NOLIST

USE ussectmp NOLIST
"GL014"

USE usactecr NOLIST


;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Compagnie du menu
TEMPORARY T_FLGMNU    CHARACTER SIZE 1 ; Provenance du menu.

;-------------------------------------------------------------------------------
;
; État financier à copier
;

FILE GLETAT_FINANCIER PRIMARY OCCURS 10

  ACCESS  VIA     EFICIECLE,EFICLE &
          USING   EFICIECLE OF GLETAT_FINANCIER, EFICLE OF GLETAT_FINANCIER &
          REQUEST EFICIECLE OF GLETAT_FINANCIER, EFICLE OF GLETAT_FINANCIER

  ACCESS  VIA     EFICIECLE &
          USING   EFICIECLE OF GLETAT_FINANCIER &
          ORDERBY EFICLE &
          REQUEST EFICIECLE OF GLETAT_FINANCIER

  ACCESS  SEQUENTIAL &
          ORDERBY EFICIECLE,EFICLE

;-------------------------------------------------------------------------------
;
; Validation si l'état financier cible existe.
;
FILE GLETAT_FINANCIER DESIGNER &
                      ALIAS A_EFI_VALID

  ITEM EFICIECLE OF A_EFI_VALID INITIAL T_CIECLEMNU FIXED

;-------------------------------------------------------------------------------
;
; Nouvel état financier
;
FILE GLETAT_FINANCIER DESIGNER &
                      ALIAS A_EFI_DEST

;  ITEM EFICIECLE    OF A_EFI_DEST INIT  T_CIECLEMNU FIXED
;  ITEM EFITYP       OF A_EFI_DEST FINAL EFITYP        OF GLETAT_FINANCIER
;  ITEM EFIUSRCRE    OF A_EFI_DEST FINAL LOGONID
;  ITEM EFIIMPINFSUP OF A_EFI_DEST FINAL EFIIMPINFSUP  OF GLETAT_FINANCIER
;  ITEM EFITITCIE    OF A_EFI_DEST FINAL EFITITCIE     OF GLETAT_FINANCIER
;  ITEM EFITITPER    OF A_EFI_DEST FINAL EFITITPER     OF GLETAT_FINANCIER
;  ITEM EFITITCOM    OF A_EFI_DEST FINAL EFITITCOM     OF GLETAT_FINANCIER
;  ITEM EFIFLGIMP    OF A_EFI_DEST FINAL EFIFLGIMP     OF GLETAT_FINANCIER
;  ITEM EFINBRLIG    OF A_EFI_DEST FINAL EFINBRLIG     OF GLETAT_FINANCIER
;  ITEM EFFCIECLE    OF A_EFI_DEST FINAL EFFCIECLE     OF GLETAT_FINANCIER
;  ITEM EFFCLE       OF A_EFI_DEST FINAL EFFCLE        OF GLETAT_FINANCIER

;-------------------------------------------------------------------------------
;
; Colonnes de l'état financier de départ
;

FILE GLEFI_COLONNE    DESIGNER
  ACCESS VIA   EFICIECLE,EFICLE &
         USING EFICIECLE OF GLETAT_FINANCIER, EFICLE OF GLETAT_FINANCIER

;-------------------------------------------------------------------------------
;
; Colonnes du nouvel état financier.
;

FILE GLEFI_COLONNE    DESIGNER &
                      ALIAS A_EFC_DEST

;-------------------------------------------------------------------------------
;
; Détail de l'état financier de départ
;

FILE GLEFI_DETAIL     DESIGNER
  ACCESS VIA   EFICIECLE,EFICLE &
         USING EFICIECLE OF GLETAT_FINANCIER, EFICLE OF GLETAT_FINANCIER

;-------------------------------------------------------------------------------
;
; Détail du nouvel état financier.
;

FILE GLEFI_DETAIL     DESIGNER &
                      ALIAS A_EFD_DEST

;
; Format des états-financiers
;
FILE GLEF_FORMAT DESIGNER ALIAS FORMAT_DEST

FILE GLEF_FORMAT DESIGNER

FILE GLEFF_LIGNE DESIGNER ALIAS EFF_DEST

FILE GLEFF_LIGNE DESIGNER

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU      IF T_FLGMNU = "C"
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)

TEMPORARY T_FLGSEL      CHARACTER SIZE 1 OCCURS WITH GLETAT_FINANCIER
TEMPORARY T_OCCURRENCE  INTEGER   SIZE 2 INITIAL -1 RESET AT MODE


;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
  " Copie d'un format d'état financier " &
@ELSE
  " Financial state format copy " &
@ENDIF
      CENTERED AT ,1

SKIP

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE &
@IF FRANCAIS
  "Choix de l'état financier à copier :" &
@ELSE
  "Choice of financial state to be copied :" &
@ENDIF
  AT ,3

SKIP 1

TITLE &
@IF FRANCAIS
  "Cie.   Code   Type   Titre                            Créateur" &
@ELSE
  "Co.    Cod.   Type   Title                            Author" &
@ENDIF
  AT ,7

CLUSTER OCCURS WITH GLETAT_FINANCIER ID BASE 10
ALIGN (6,5,7) (,,14) (,,22) (,,28) (,,61)

FIELD EFICIECLE OF GLETAT_FINANCIER &
      HIDDEN &
      LABEL " " &
      NOCHANGE &
      HILITE DISPLAY INVERSE IF T_FLGSEL = "1"

FIELD EFICLE    OF GLETAT_FINANCIER &
      DISPLAY &
      HILITE DISPLAY INVERSE IF T_FLGSEL = "1"

FIELD EFITYP    OF GLETAT_FINANCIER &
      DISPLAY &
      HILITE DISPLAY INVERSE IF T_FLGSEL = "1"

FIELD EFITITEF    OF GLETAT_FINANCIER &
      DISPLAY &
      HILITE DISPLAY INVERSE IF T_FLGSEL = "1"

FIELD EFIUSRCRE    OF GLETAT_FINANCIER &
      DISPLAY &
      HILITE DISPLAY INVERSE IF T_FLGSEL = "1"

CLUSTER

SKIP

TITLE &
@IF FRANCAIS
  "Etat financier à créer :" &
@ELSE
  "Financial state to create :" &
@ENDIF
  AT ,3

SKIP 1

ALIGN (,7,23)

FIELD EFICIECLE OF A_EFI_VALID &
      HIDDEN ID 50 &
      DISPLAY &
      PREDISPLAY &
      LABEL "Compagnie.....:"

ALIGN (5,7,23)

FIELD EFICLE OF A_EFI_VALID &
      HIDDEN &
      REQUIRED &
      LABEL "Code E/F......:"
;      LOOKUP NOTON A_EFI_DEST &
;             VIA   EFICIECLE,EFICLE &
;             USING EFICIECLE OF A_EFI_DEST,EFICLE OF A_EFI_DEST &
;             MESSAGE 268 ; Ce code existe déjà.

FIELD EFITITEF OF A_EFI_VALID &
      HIDDEN &
      REQUIRED


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Indique à l'usager que l'état financier cible existe et qu'il y aura une
; copie de détail de format ( plage de ligne ).
;
PROCEDURE EDIT EFICLE OF A_EFI_VALID
BEGIN
  GET A_EFI_VALID VIA    EFICIECLE, &
                         EFICLE     &
                  USING  EFICIECLE OF A_EFI_VALID, &
                         EFICLE    OF A_EFI_VALID  &
                  OPTIONAL
  IF ACCESSOK
  THEN WARNING 201 ; Cet état financier existe déjà il y aura copie de détail.
END

;-------------------------------------------------------------------------------
;
; Sélection de l'état financier à copier .
;
PROCEDURE DESIGNER 10
BEGIN
  IF T_FLGSEL <> "1"
  THEN LET T_FLGSEL = "1"
  ELSE LET T_FLGSEL = "0"

  DISPLAY EFICIECLE OF GLETAT_FINANCIER
  DISPLAY EFICLE    OF GLETAT_FINANCIER
  DISPLAY EFITYP    OF GLETAT_FINANCIER
  DISPLAY EFITITEF  OF GLETAT_FINANCIER
  DISPLAY EFIUSRCRE OF GLETAT_FINANCIER

  IF    T_OCCURRENCE <> -1 &
    AND T_OCCURRENCE <> OCCURRENCE
  THEN
    BEGIN
      FOR T_FLGSEL
      BEGIN
        IF T_OCCURRENCE = OCCURRENCE
        THEN LET T_FLGSEL = "0"
      END

      FOR GLETAT_FINANCIER
      BEGIN
        IF T_OCCURRENCE = OCCURRENCE
        THEN BEGIN
          DISPLAY EFICIECLE OF GLETAT_FINANCIER
          DISPLAY EFICLE    OF GLETAT_FINANCIER
          DISPLAY EFITYP    OF GLETAT_FINANCIER
          DISPLAY EFITITEF  OF GLETAT_FINANCIER
          DISPLAY EFIUSRCRE OF GLETAT_FINANCIER
        END
      END
      LET T_OCCURRENCE = OCCURRENCE
    END
  ELSE
    BEGIN
      LET T_OCCURRENCE = OCCURRENCE
    END

  IF T_FLGSEL = "1"
  THEN
    BEGIN
      ACCEPT EFICLE     OF A_EFI_VALID
      ACCEPT EFITITEF   OF A_EFI_VALID
    END
  ELSE
    BEGIN
      LET EFICLE   OF A_EFI_DEST = ""
      LET EFITITEF OF A_EFI_DEST = ""
      DISPLAY EFICLE   OF A_EFI_VALID
      DISPLAY EFITITEF OF A_EFI_VALID
    END

  IF T_FLGSEL = "1"
  THEN INFORMATION 206 ; Faites une mise à jour pour executer la copie.
END


;-------------------------------------------------------------------------------
;
; Procédure de copie de l'état financier
;
PROCEDURE INTERNAL COPIE_ETAT
BEGIN
; WHILE RETRIEVING GLEFI_COLONNE
; BEGIN
;   LET EFICIECLE OF A_EFC_DEST = EFICIECLE OF A_EFI_DEST
;   LET EFICLE    OF A_EFC_DEST = EFICLE    OF A_EFI_DEST
;   LET EFCNUMCOL OF A_EFC_DEST = EFCNUMCOL OF GLEFI_COLONNE
;   LET EFCTYPCOL OF A_EFC_DEST = EFCTYPCOL OF GLEFI_COLONNE
;   LET EFCANNCOL OF A_EFC_DEST = EFCANNCOL OF GLEFI_COLONNE
;   LET EFCLARCOL OF A_EFC_DEST = EFCLARCOL OF GLEFI_COLONNE
;   LET EFCENTCOL OF A_EFC_DEST = EFCENTCOL OF GLEFI_COLONNE
;   LET EFCCALCOL OF A_EFC_DEST = EFCCALCOL OF GLEFI_COLONNE
;   LET EFCIMPCOL OF A_EFC_DEST = EFCIMPCOL OF GLEFI_COLONNE
;   PUT A_EFC_DEST RESET
; END

  GET A_EFI_DEST VIA    EFICIECLE, &
                        EFICLE     &
                 USING  EFICIECLE OF A_EFI_VALID, &
                        EFICLE    OF A_EFI_VALID  OPTIONAL
  IF NOT ACCESSOK
  THEN BEGIN

    LET EFICIECLE    OF A_EFI_DEST = T_CIECLEMNU
    LET EFICLE       OF A_EFI_DEST = EFICLE        OF A_EFI_VALID
    LET EFITYP       OF A_EFI_DEST = EFITYP        OF GLETAT_FINANCIER
    LET EFIUSRCRE    OF A_EFI_DEST = LOGONID
    LET EFIIMPINFSUP OF A_EFI_DEST = EFIIMPINFSUP  OF GLETAT_FINANCIER
    LET EFITITCIE    OF A_EFI_DEST = EFITITCIE     OF GLETAT_FINANCIER
    LET EFITITPER    OF A_EFI_DEST = EFITITPER     OF GLETAT_FINANCIER
    LET EFITITCOM    OF A_EFI_DEST = EFITITCOM     OF GLETAT_FINANCIER
    LET EFIFLGIMP    OF A_EFI_DEST = EFIFLGIMP     OF GLETAT_FINANCIER
    LET EFINBRLIG    OF A_EFI_DEST = EFINBRLIG     OF GLETAT_FINANCIER
    LET EFFCIECLE    OF A_EFI_DEST = T_CIECLEMNU
    LET EFFCLE       OF A_EFI_DEST = EFFCLE        OF GLETAT_FINANCIER
    PUT A_EFI_DEST RESET

    WHILE RETRIEVING GLEFI_COLONNE
    BEGIN
      LET EFICIECLE OF A_EFC_DEST = EFICIECLE OF A_EFI_VALID
      LET EFICLE    OF A_EFC_DEST = EFICLE    OF A_EFI_VALID
      LET EFCNUMCOL OF A_EFC_DEST = EFCNUMCOL OF GLEFI_COLONNE
      LET EFCTYPCOL OF A_EFC_DEST = EFCTYPCOL OF GLEFI_COLONNE
      LET EFCANNCOL OF A_EFC_DEST = EFCANNCOL OF GLEFI_COLONNE
      LET EFCLARCOL OF A_EFC_DEST = EFCLARCOL OF GLEFI_COLONNE
      LET EFCENTCOL OF A_EFC_DEST = EFCENTCOL OF GLEFI_COLONNE
      LET EFCCALCOL OF A_EFC_DEST = EFCCALCOL OF GLEFI_COLONNE
      LET EFCIMPCOL OF A_EFC_DEST = EFCIMPCOL OF GLEFI_COLONNE
      PUT A_EFC_DEST RESET
    END
    ;
    ; Créer le format si celui-ci n'existe pas
    ;
    GET FORMAT_DEST VIA    EFFCIECLE, &
                           EFFCLE     &
                    USING  EFICIECLE OF GLETAT_FINANCIER, &
                           EFFCLE    OF GLETAT_FINANCIER OPTIONAL
    IF ACCESSOK
    THEN BEGIN
           GET GLEF_FORMAT VIA    EFFCIECLE, &
                                  EFFCLE     &
                           USING  T_CIECLEMNU, &
                                  EFFCLE OF GLETAT_FINANCIER OPTIONAL
           IF NOT ACCESSOK
           THEN BEGIN
                  LET EFFCIECLE OF GLEF_FORMAT = T_CIECLEMNU
                  LET EFFCLE    OF GLEF_FORMAT = EFFCLE OF GLETAT_FINANCIER
                  LET EFFDSC    OF GLEF_FORMAT = EFFDSC OF FORMAT_DEST
                  PUT GLEF_FORMAT RESET
                  WHILE RETRIEVING GLEFF_LIGNE VIA EFFCIECLE, EFFCLE &
                            USING EFICIECLE OF GLETAT_FINANCIER, &
                                  EFFCLE OF GLETAT_FINANCIER
                  BEGIN
                    LET EFFCIECLE OF EFF_DEST = T_CIECLEMNU
                    LET EFFCLE    OF EFF_DEST = EFFCLE OF GLEFF_LIGNE
                    LET EFLTYPFOR OF EFF_DEST = EFLTYPFOR OF GLEFF_LIGNE
                    LET EFLLIGNUM OF EFF_DEST = EFLLIGNUM OF GLEFF_LIGNE
                    LET EFLELENUM OF EFF_DEST = EFLELENUM OF GLEFF_LIGNE
                    LET EFLELECOD OF EFF_DEST = EFLELECOD OF GLEFF_LIGNE
                    LET EFLELEPOS OF EFF_DEST = EFLELEPOS OF GLEFF_LIGNE
                    LET EFLELECOL OF EFF_DEST = EFLELECOL OF GLEFF_LIGNE
                    LET EFLELELAB OF EFF_DEST = EFLELELAB OF GLEFF_LIGNE
                    LET EFLELETXT OF EFF_DEST = EFLELETXT OF GLEFF_LIGNE
                    PUT EFF_DEST RESET
                  END
                END

         END
  END
  ELSE BEGIN
    ;
    ; Si l'état financier existe il faut tout de même prendre le nouveau
    ; nombre de ligne.
    ;
    LET EFINBRLIG    OF A_EFI_DEST = EFINBRLIG     OF GLETAT_FINANCIER

    ;
    ; On doit détruire le détail de l'état financier cible.
    ;

    WHILE RETRIEVING A_EFD_DEST VIA   EFICIECLE, &
                                      EFICLE     &
                                USING EFICIECLE OF A_EFI_VALID, &
                                      EFICLE    OF A_EFI_VALID
    BEGIN
      DELETE A_EFD_DEST
      PUT A_EFD_DEST RESET
    END
    PUT A_EFI_DEST RESET
  END

  WHILE RETRIEVING GLEFI_DETAIL
  BEGIN
    LET EFICIECLE  OF A_EFD_DEST = EFICIECLE  OF A_EFI_VALID
    LET EFICLE     OF A_EFD_DEST = EFICLE     OF A_EFI_VALID
    LET EFDNUMLIG  OF A_EFD_DEST = EFDNUMLIG  OF GLEFI_DETAIL
    LET EFDCOD     OF A_EFD_DEST = EFDCOD     OF GLEFI_DETAIL
    LET CPTCLE     OF A_EFD_DEST = CPTCLE     OF GLEFI_DETAIL
    LET CIECLE     OF A_EFD_DEST = CIECLE     OF GLEFI_DETAIL
    LET CIENMC     OF A_EFD_DEST = CIENMC     OF GLEFI_DETAIL
    LET UNACLE     OF A_EFD_DEST = UNACLE     OF GLEFI_DETAIL
    LET CENCLE     OF A_EFD_DEST = CENCLE     OF GLEFI_DETAIL
    LET EFDDSC     OF A_EFD_DEST = EFDDSC     OF GLEFI_DETAIL
    LET EFDCOL1    OF A_EFD_DEST = EFDCOL1    OF GLEFI_DETAIL
    LET EFDCOL2    OF A_EFD_DEST = EFDCOL2    OF GLEFI_DETAIL
    LET EFDCOL3    OF A_EFD_DEST = EFDCOL3    OF GLEFI_DETAIL
    LET EFDCOL4    OF A_EFD_DEST = EFDCOL4    OF GLEFI_DETAIL
    LET EFDCOL5    OF A_EFD_DEST = EFDCOL5    OF GLEFI_DETAIL
    LET EFDCOL6    OF A_EFD_DEST = EFDCOL6    OF GLEFI_DETAIL
    LET EFDCOL7    OF A_EFD_DEST = EFDCOL7    OF GLEFI_DETAIL
    LET EFDCOL8    OF A_EFD_DEST = EFDCOL8    OF GLEFI_DETAIL
    LET EFDFLGIMP  OF A_EFD_DEST = EFDFLGIMP  OF GLEFI_DETAIL
    LET EFDRATB    OF A_EFD_DEST = EFDRATB    OF GLEFI_DETAIL
    LET EFDRATC    OF A_EFD_DEST = EFDRATC    OF GLEFI_DETAIL
    LET EFDRATD    OF A_EFD_DEST = EFDRATD    OF GLEFI_DETAIL
    LET EFDRATE    OF A_EFD_DEST = EFDRATE    OF GLEFI_DETAIL
    LET EFDRATF    OF A_EFD_DEST = EFDRATF    OF GLEFI_DETAIL
    LET EFDRATG    OF A_EFD_DEST = EFDRATG    OF GLEFI_DETAIL
    LET EFDRATH    OF A_EFD_DEST = EFDRATH    OF GLEFI_DETAIL
    LET EFDRATI    OF A_EFD_DEST = EFDRATI    OF GLEFI_DETAIL
    LET EFDRATJ    OF A_EFD_DEST = EFDRATJ    OF GLEFI_DETAIL
    LET EFDRATK    OF A_EFD_DEST = EFDRATK    OF GLEFI_DETAIL
    LET EFDRATL    OF A_EFD_DEST = EFDRATL    OF GLEFI_DETAIL
    LET EFDRATM    OF A_EFD_DEST = EFDRATM    OF GLEFI_DETAIL
    LET EFDRATN    OF A_EFD_DEST = EFDRATN    OF GLEFI_DETAIL
    LET EFDRATO    OF A_EFD_DEST = EFDRATO    OF GLEFI_DETAIL
    LET EFDRATP    OF A_EFD_DEST = EFDRATP    OF GLEFI_DETAIL
    LET EFDLIGREF1 OF A_EFD_DEST = EFDLIGREF1 OF GLEFI_DETAIL
    LET EFDLIGREF2 OF A_EFD_DEST = EFDLIGREF2 OF GLEFI_DETAIL
    LET EFDNUMLIGTMP OF A_EFD_DEST = 0
    PUT A_EFD_DEST RESET
  END
END

;-------------------------------------------------------------------------------
;
; Mise à jour des données
;
PROCEDURE PREUPDATE
BEGIN
  FOR GLETAT_FINANCIER
  BEGIN
    IF T_FLGSEL = "1"
    THEN BEGIN
      DO INTERNAL COPIE_ETAT
    END
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************** Copie d'un format d'état financier **********************
* Choix de l'état financier à copier :                                         *
*                                                                              *
*     Cie.   Code   Type   Titre                            Créateur           *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*     xxxx   xxxx    x     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxx       *
*                                                                              *
* Etat financier à créer :                                                     *
*                                                                              *
*     Compagnie.....: xxxx                                                     *
*     Code E/F......: xxxx                                                     *
*     Titre - E/F...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                           *
********************************************************************************
@ENDIF
