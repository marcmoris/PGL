;/P/ Detail de la facturation par commande
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 22-11-1999
;    Description....: Details des blocs factures par commande.   
;
;-------------------------------------------------------------------------------


USE ussetqzs NOLIST
SET NOBLANKS

ACCESS PDDETAIL_C_I &
 LINK CIECLE OF PDDETAIL_C_I, &
      PJTABR OF PDDETAIL_C_I, &
      PJTANN OF PDDETAIL_C_I, &
      COMNUMCOMINT                    OF PDDETAIL_C_I &
   TO CIECLE, &
      PJTABR, &
      PJTANN, &
      COMNUMCOMINT                    OF  IFBLOC &
 LINK CIECLE OF IFBLOC, &
      FTRNUMFAC OF IFBLOC &
   TO CIECLE, &
      FTRNUMFAC                      OF PDFACTURE 
      

 
DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""



CHOOSE CIECLE    PARM


SORT ON D_NUMCOM &
     ON FTRNUMFAC &
     ON D_NUMBLC 

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF340"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 003 "NO"               &
             TAB 009 "NO"               &
             TAB 025 "DATE "            &
             TAB 032 "NO"               &
             TAB 048 "CAT"              &
             TAB 067 "DIMENSIONS"       &
             TAB 073 "TOTAL"            &
             TAB 090 "NO"               &
SKIP 1                                  &
             TAB 003 "COMMANDE"         &
             TAB 012 "FACTURE"          &
             TAB 025 "FACTURE"          &
             TAB 032 "BLOC"             &
             TAB 049 "GRANIT"           &
             TAB 067 "LONG HAUT LARG"   &
             TAB 075 "M3"               &
             TAB 088 "EXP."             &
SKIP 1                                  &
             TAB 003 "----"             &
             TAB 008 "--------------"   &
             TAB 023 "--------"         &
             TAB 032 "--------------"   &
             TAB 047 "------"           &
             TAB 054 "------"           &
             TAB 061 "-----"            &
             TAB 067 "-----"            &
SKIP 2

REPORT                                  &
  TAB 003 D_NUMCOM PRINT AT D_NUMCOM    &
  TAB 008 FTRNUMFAC OF IFBLOC           &                         
  TAB 023 FTRDATFAC OF PDFACTURE        &
  TAB 033 D_NUMBLC                      &
  TAB 049 TGRCODGRA OF IFBLOC           &
  TAB 053 BLCVENDULONG OF IFBLOC        &
  TAB 058 BLCVENDUHAU OF IFBLOC         &
  TAB 063 BLCVENDULAR OF IFBLOC         &
  TAB 069 BLCM3TOT OF IFBLOC            &
  TAB 078 EXNCLE OF IFBLOC                   

FOOTING AT FTRNUMFAC OF IFBLOC                      &
        TAB 6 "--------"                            &
        TAB 34 "----------"                         &
SKIP 1                                              &
        TAB 1 COUNT PICTURE "^^^^^" "Blocs"         &
        TAB 20 "Sous-total:"                        &
        TAB 34 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
SKIP 2
FINAL FOOTING                                       &
        TAB 6 "--------"                            &
        TAB 34 "----------"                         &
SKIP 1                                              &
        TAB 1 COUNT PICTURE "^^^^^" "Blocs"         &
        TAB 22 "Total:"                             &
        TAB 34 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
SKIP 2
BUILD  if310z
