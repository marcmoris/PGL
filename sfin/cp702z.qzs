;/T/ Préparation des paiements
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Thomas BRENNEUR
;    Date Création.: 23 Juin 1993
;
;    Description...: Impression de
;                       . la liste des erreurs
;                       . la liste des paiements générés
;
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....

ACCESS *WCP702ER

DEFINE D_CIENOM CHAR SIZE 40 = PARM

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_WCP702ER CHARACTER SIZE 5 = "1" + PECCLE OF WCP702ER &
         IF PECCLE OF WCP702ER[1:1] < "8" &
         ELSE "0" + PECCLE OF WCP702ER

SORT ON CIECLE OF WCP702ER &
     ON D_PECCLE_WCP702ER &
     ON LCPCLE OF WCP702ER

;-------------------------------------------------------------------------------

DEFINE  DZ_NOMRAP1 CHARACTER SIZE 60 &
@IF FRANCAIS
        = "Préparation des paiements - erreur"
@ELSE
        = "Payment preparation - Error"
@ENDIF

DEFINE  DZ_NOMRAP2 CHARACTER SIZE 60 &
        = " "

DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP702ZA"        ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = D_CIENOM         ; Nom de la compagnie

;-------------------------------------------------------------------------------

USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP 2 &
@IF FRANCAIS
  TAB 110 "Période : " &
@ELSE
  TAB 110 "Period  : " &
@ENDIF
  TAB 120 PECCLE OF WCP702ER &
SKIP 2

;-------------------------------------------------------------------------------

NOREPORT

REPORT &
  SKIP 1 &
  TAB 001 &
@IF FRANCAIS
          "Erreur : " &
@ELSE
          "Error  : " &
@ENDIF
          T_MESERR OF WCP702ER


BUILD cp702za

;===============================================================================

ACCESS *WCP702PA &
  LINK  REFCIECLE OF WCP702PA, &
        REFCLETYP OF WCP702PA, &
        REFCLENUM OF WCP702PA, &
        RADCLE    OF WCP702PA  &
    TO  REFCIECLE, &
        REFCLETYP, &
        REFCLENUM, &
        RADCLE               OF MCREF_ADRESSE

DEFINE D_CIENOM CHAR SIZE 40 = PARM

;-------------------------------------------------------------------------------

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_WCP702PA CHARACTER SIZE 5 = "1" + PECCLE OF WCP702PA &
         IF PECCLE OF WCP702PA[1:1] < "8" &
         ELSE "0" + PECCLE OF WCP702PA

SORT ON CIECLE    OF WCP702PA &
     ON D_PECCLE_WCP702PA     &
     ON LCPCLE    OF WCP702PA &
     ON LBQCPTENC OF WCP702PA &
     ON REFCIECLE OF WCP702PA &
     ON REFCLETYP OF WCP702PA &
     ON REFCLENUM OF WCP702PA &
     ON RADCLE    OF WCP702PA &
     ON PAMCLE    OF WCP702PA

;-------------------------------------------------------------------------------

DEFINE  DZ_NOMRAP1 CHARACTER SIZE 60 &
@IF FRANCAIS
        = "Préparation des paiements - sommaire"
@ELSE
        = "Payment preparation - Summary"
@ENDIF

DEFINE  DZ_NOMRAP2 CHARACTER SIZE 60 &
        = " "

DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP702ZB"        ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = D_CIENOM         ; Nom de la compagnie

;-------------------------------------------------------------------------------

USE ustitstd NOLIST  ; Titre standard pour l'application
  SKIP 2 &
@IF FRANCAIS
    TAB 10  "Période comptable.: " &
@ELSE
    TAB 10  "Accounting period.: " &
@ENDIF
            PECCLE OF WCP702PA     &
@IF FRANCAIS
    TAB 70  "Numéro de lot.....: " &
@ELSE
    TAB 70  "Batch number......: " &
@ENDIF
            LCPCLE OF WCP702PA     &
SKIP 1 &
@IF FRANCAIS
    TAB 70  "Date de paiement..: " &
@ELSE
    TAB 70  "Payment date......: " &
@ENDIF
            LCPDATPAM OF WCP702PA &
SKIP 2 &
@IF FRANCAIS
  TAB 018 "Compte                                                      Numéro de    Montant du    Escompte" &
  SKIP &
  TAB 018 "d'encaisse  Référence                                       Paiement       paiement        pris" &
@ELSE
  TAB 018 "%%%pte                                                      Numéro de    Montant du    Escompte" &
  SKIP &
  TAB 018 "%%%%caisse  Référence                                       Paiement       paiement        pris" &
@ENDIF
  SKIP &
  TAB 018 "----------  ----------------------------------------------- --------- -------------- -----------" &
SKIP 2

HEADING AT LCPCLE &
  SKIP PAGE

REPORT &
  TAB 020 LBQCPTENC OF WCP702PA      PRINT AT LBQCPTENC &
  TAB 030 REFCLENUM OF WCP702PA      PRINT AT REFCLENUM &
  TAB 037 RADNOM    OF MCREF_ADRESSE PRINT AT RADCLE    &
  TAB 079 PAMCLE    OF WCP702PA                       &
  TAB 088 PAMMNT    OF WCP702PA                       &
          PICTURE "^^,^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5 BWZ &
  TAB 103 PAMMNTESC OF WCP702PA &
          PICTURE "^^^,^^^.^^ "    TRAILING "-" SIGNIFICANCE 5 BWZ

FOOT AT LCPCLE OF WCP702PA &
  TAB 088 "==============" &
  TAB 103 "===========" &
  SKIP 1 &
@IF FRANCAIS
  TAB 022 "Total du lot : " &
@ELSE
  TAB 022 "batch total : " &
@ENDIF
  TAB 088 PAMMNT    OF WCP702PA &
          SUBTOTAL BWZ PICTURE "^^,^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5 &
  TAB 103 PAMMNTESC OF WCP702PA &
          SUBTOTAL BWZ PICTURE "^^^,^^^.^^ "       TRAILING "-" SIGNIFICANCE 5

FINAL FOOTING &
  TAB 088 "==============" &
  TAB 103 "===========" &
  SKIP 1 &
@IF FRANCAIS
  TAB 022 "Total : " &
@ELSE
  TAB 022 "Total : " &
@ENDIF
  TAB 088 PAMMNT    OF WCP702PA &
          SUBTOTAL BWZ PICTURE "^^,^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5 &
  TAB 103 PAMMNTESC OF WCP702PA &
          SUBTOTAL BWZ PICTURE "^^^,^^^.^^ "       TRAILING "-" SIGNIFICANCE 5

BUILD cp702zb
;
;                 Compte                                                      Numéro de    Montant du    Escompte
;                 d'encaisse  Référence                                       Paiement       paiement        pris
;                 ----------  ----------------------------------------------- --------- -------------- -----------
;                   xxxxxx    xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx  99,999,999.99- 999,999.99-
