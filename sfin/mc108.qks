;/T/ Adresse Client,Employé,Fourniss.  REFCIECLE, REFCLETYP, REFCLENUM, TYPADR, RADCLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 20 mai 1993
;
;    Description..: Permet de visionner et de choisir une adresse
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
;
;/M/ Programmeur..: Claudie Doyon
;    Date ........: 09 décembre 1999
;    Description..: Modification du orderby
;
;
SCREEN mc108 NOMODE NOACTION FIELDMARK &
             FROM 11,27 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_REFCIECLE, &
                       T_REFCLETYP, &
                       T_REFCLENUM, &
                       T_TYPADR   , &
                       T_RADCLE

USE mc108.hlp NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_REFCIECLE CHARACTER SIZE 4
TEMPORARY T_REFCLETYP CHARACTER SIZE 1
TEMPORARY T_REFCLENUM CHARACTER SIZE 6
TEMPORARY T_TYPADR    CHARACTER SIZE 3
TEMPORARY T_RADCLE    INTEGER UNSIGNED SIZE 2

;-------------------------------------------------------------------------------
;

FILE MCREF_ADRESSE    PRIMARY
  ACCESS VIA REFCIECLE, &
             REFCLETYP, &
             REFCLENUM  &
        USING T_REFCIECLE, &
              T_REFCLETYP, &
              T_REFCLENUM  &
   ORDERBY   REFCIECLE, &
             REFCLETYP, &
             REFCLENUM,  &        
             RADCLE

 ;
 ; Compte à payer
 ;   PAM = paiement
 ;   ACH = achat
 ;   RET = retour de marchandise
 ; Compte à recevoir
 ;   FAC = facturation
 ;   LIV = livraison
 ;   ETA = état de compte
 ;
 SELECT IF (T_TYPADR = "PAM" AND RADPAMCP    OF MCREF_ADRESSE = "1") OR &
           (T_TYPADR = "ACH" AND RADACHCP    OF MCREF_ADRESSE = "1") OR &
           (T_TYPADR = "RET" AND RADRETCP    OF MCREF_ADRESSE = "1") OR &
           (T_TYPADR = "FAC" AND RADFACCR    OF MCREF_ADRESSE = "1") OR &
           (T_TYPADR = "LIV" AND RADLIVCR    OF MCREF_ADRESSE = "1") OR &
           (T_TYPADR = "ETA" AND RADETACPTCR OF MCREF_ADRESSE = "1") OR &
           T_TYPADR = ""


;-------------------------------------------------------------------------------
;
TEMPORARY T_TYPADRAFF CHARACTER SIZE 70 ; Pour afficher les types d'adresses

;-------------------------------------------------------------------------------
;
DRAW 1,1 TO 13,54

USE ushilite NOLIST

USE ustitoff NOLIST

SKIP TO 3

ALIGN(3,3,13) (,,18)

FIELD RADCLE    OF MCREF_ADRESSE HIDDEN  &
@IF FRANCAIS
        LABEL ">> Séq..:"
@ELSE
        LABEL ">> Seq..:"
@ENDIF

FIELD T_TYPADRAFF DISPLAY FOR 2,35

SKIP 1

ALIGN(,3,13)

FIELD RADNOM  OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "Nom.....:"
@ELSE
        LABEL "Name....:"
@ENDIF

FIELD RADADR1 OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "Adresse.:"
@ELSE
        LABEL "Address.:"
@ENDIF

FIELD RADADR2 OF MCREF_ADRESSE NOLABEL
FIELD RADADR3 OF MCREF_ADRESSE NOLABEL
FIELD RADADR4 OF MCREF_ADRESSE NOLABEL

ALIGN(,3,13) (,22,27)

FIELD RADCP   OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "CP......:"
@ELSE
        LABEL "PC......:"
@ENDIF

FIELD RADTEL  OF MCREF_ADRESSE &
@IF FRANCAIS
        LABEL "Tél:"
@ELSE
        LABEL "Tel:"
@ENDIF


;-------------------------------------------------------------------------------
;
; Affiche les types d'adresse sous une forme textuel.
;
;
PROCEDURE OUTPUT T_TYPADRAFF
BEGIN
  LET FIELDTEXT = ""

  IF RADPAMCP OF MCREF_ADRESSE = "1"
  THEN LET FIELDTEXT = "Paiement"

  IF RADACHCP OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Achat"
  END

  IF RADRETCP OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Retour marchandise"
  END

  IF RADFACCR OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Facturation"
  END

  IF RADLIVCR OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Livraison"
  END

  IF RADETACPTCR OF MCREF_ADRESSE = "1"
  THEN BEGIN
    IF FIELDTEXT <> ""
    THEN LET FIELDTEXT = FIELDTEXT + ", "
    LET FIELDTEXT = FIELDTEXT + "Etat de compte"
  END
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST  ;Formate le numéro de téléphone nord américain
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_RADCLE = RADCLE OF MCREF_ADRESSE
  RETURN
END

BUILD

;                          ******************************************************
;                          *                                                    *
;                          * >> Séq..: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *                                                    *
;                          * Nom.....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          * Adresse.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
;                          * CP......: xxxxxxx  Tél: xxxxxxxxxxxxx              *
;                          *                                                    *
;                          ******************************************************
