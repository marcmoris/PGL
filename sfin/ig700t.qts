;/T/ Inventaire physique - 2 étapes
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-06-03
;
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste d'erreur avant et non après et
;                   la liste d'erreur après et non avant pour toutes les
;                   compagnie de Granicor.
;
;    Description..: Cette procédure fait le transfert des localisations
;                   de l'inventaire physique au système de production.
;                   Elle met à jour le statut des tranches du système de
;                   production de Granicor.
;
;/M/ François Déry, 6 mai 1999
;       Ramener dans ce source le quiz IG700 qui crée les subfiles pour
;       faciliter la compilation.
;-------------------------------------------------------------------------------

USE ussetqts NOLIST

RUN ig700ta

REQUEST CREATION_SUBFILE1
;
; Sous-fichier des tranches présentes avant et non après
;
ACCESS PDTRANCHE                                        &
  LINK CIECLE,                                          &
       TRANUMTRA002                                     &
    TO CIECLE,                                          &
       TRANUMTRA002     OF INTRANCHE OPTIONAL           &
  LINK BLONUMBLOAPR     OF PDTRANCHE,                   &
       CIECLE           OF PDTRANCHE                    &
    TO BLONUMBLOAPR,                                    &
       CIECLE           OF PDBLOC OPTIONAL

CHOOSE CIECLE PARM

SELECT IF TRASTU OF PDTRANCHE = "I" AND &
          NOT RECORD INTRANCHE EXIST

SUBFILE WIG700Z1 KEEP &
  INCLUDE CIECLE        OF PDTRANCHE, &
          TRANUMTRA002  OF PDTRANCHE, &
          TYFCODFIN     OF PDTRANCHE, &
          TRALON        OF PDTRANCHE, &
          TRAHAU        OF PDTRANCHE, &
          TRAEPA        OF PDTRANCHE, &
          TRASURMC2     OF PDTRANCHE, &
          TGRCODGRA     OF PDTRANCHE, &
          BLONUMBLOAPR  OF PDBLOC

;
; Sous-fichier des tranches présentes après et non avant
;
REQUEST CREATION_SUBFILE2

ACCESS INTRANCHE                            &
  LINK CIECLE,                                  &
       TRANUMTRA002                             &
    TO CIECLE,                                  &
       TRANUMTRA002     OF PDTRANCHE OPTIONAL   &
  LINK BLONUMBLOAPR     OF PDTRANCHE,                   &
       CIECLE           OF PDTRANCHE                    &
    TO BLONUMBLOAPR,                                    &
       CIECLE           OF PDBLOC OPTIONAL

CHOOSE CIECLE PARM

DEF D_DATINT DATE       = PARM
DEF D_HEUFIN INT SIZE 4 = PARM

SELECT IF TRANUMTRA002 OF INTRANCHE <> "9999999" AND &
          ((NOT RECORD PDTRANCHE EXIST OR &
                     (RECORD PDTRANCHE EXIST AND &
                      (D_DATINT < TRADATCRE OF PDTRANCHE OR &
                      (D_DATINT = TRADATCRE  OF PDTRANCHE AND &
                       D_HEUFIN <= TRAHRECRE OF PDTRANCHE)))) &
           OR TRASTU OF PDTRANCHE <> "I")

SUBFILE WIG700Z2 KEEP &
  INCLUDE CIECLE        OF INTRANCHE, &
          TRANUMTRA002  OF INTRANCHE, &
          TYFCODFIN     OF INTRANCHE, &
          TRALON        OF PDTRANCHE, &
          TRAHAU        OF PDTRANCHE, &
          TRAEPA        OF PDTRANCHE, &
          TGRCODGRA     OF PDTRANCHE, &
          TRASURMC2     OF PDTRANCHE, &
          BLONUMBLOAPR  OF PDBLOC   , &
          ITRDESC       OF INTRANCHE


;---------------------------------------------------------------------------------------------
;QTE REJETER
;
;
REQUEST QTE_REJETER

ACCESS PDTRANCHE &
LINK CIECLE,TRANUMTRA002 &
TO   CIECLE,TRANUMTRA002 OF PDDET_REJET &

LINK CIECLE, REJNUMREJ OF PDDET_REJET &
TO   CIECLE, REJNUMREJ OF PDREJET &

LINK BLONUMBLOAPR     OF PDTRANCHE,                   &
     CIECLE           OF PDTRANCHE                    &
TO   BLONUMBLOAPR,                                    &
     CIECLE           OF PDBLOC OPTIONAL


CHOOSE CIECLE PARM
 
DEF D_DATDEB DATE       = PARM
DEF D_DATFIN DATE       = PARM

SELECT IF (D_DATDEB <= REJDATCRE OF PDREJET AND &
           D_DATFIN >= REJDATCRE OF PDREJET )

SUBFILE QTE_REJ KEEP  &
INCLUDE   CIECLE        OF PDTRANCHE, &
          TRANUMTRA002  OF PDTRANCHE, &
          TYFCODFIN     OF PDTRANCHE, &
          TRALON        OF PDTRANCHE, &
          TRAHAU        OF PDTRANCHE, &
          TRAEPA        OF PDTRANCHE, &
          TRASURMC2     OF PDTRANCHE, &
          TGRCODGRA     OF PDTRANCHE, &
          BLONUMBLOAPR  OF PDBLOC


;-----------------------------------------------------------------------------------------
; QTE REETIQUETER

REQUEST REETQ

ACCESS PDTRANCHE &

LINK BLONUMBLOAPR     OF PDTRANCHE,                   &
     CIECLE           OF PDTRANCHE                    &
TO   BLONUMBLOAPR,                                    &
     CIECLE           OF PDBLOC OPTIONAL


CHOOSE CIECLE PARM
 
DEF D_DATDEB DATE       = PARM
DEF D_DATFIN DATE       = PARM

SELECT IF (D_DATDEB <= TRADATINV OF PDTRANCHE AND &
           D_DATFIN >= TRADATINV OF PDTRANCHE )

SUBFILE QTE_REET KEEP  &
INCLUDE   CIECLE        OF PDTRANCHE, &
          TRANUMTRA002  OF PDTRANCHE, &
          TYFCODFIN     OF PDTRANCHE, &
          TRALON        OF PDTRANCHE, &
          TRAHAU        OF PDTRANCHE, &
          TRAEPA        OF PDTRANCHE, &
          TRASURMC2     OF PDTRANCHE, &
          TGRCODGRA     OF PDTRANCHE, &
          BLONUMBLOAPR  OF PDBLOC




       

BUILD
;

RUN ig700tb

REQUEST MAJ_STATUT_TRANCHE
;-------------------------

ACCESS *$PG_DATA/WIG700Z1 &
  LINK TRANUMTRA002     OF WIG700Z1,    &
       CIECLE           OF WIG700Z1     &
    TO TRANUMTRA002,                    &
       CIECLE           OF PDTRANCHE

OUTPUT PDTRANCHE UPDATE

  ITEM TRADATMAJ OF PDTRANCHE FINAL SYSDATE
  ITEM TRAHREMAJ OF PDTRANCHE FINAL SYSTIME / 10000
  ITEM TRAUSRMAJ OF PDTRANCHE FINAL "INVENTAIRE"
  ITEM TRASTU OF PDTRANCHE FINAL "T"

REQUEST MAJ_STATUT_TRANCHE2
;-------------------------

ACCESS *$PG_DATA/WIG700Z2 &
  LINK TRANUMTRA002     OF WIG700Z2,    &
       CIECLE           OF WIG700Z2     &
    TO TRANUMTRA002,                    &
       CIECLE           OF PDTRANCHE

OUTPUT PDTRANCHE UPDATE

  ITEM TRADATMAJ OF PDTRANCHE FINAL SYSDATE
  ITEM TRAHREMAJ OF PDTRANCHE FINAL SYSTIME / 10000
  ITEM TRAUSRMAJ OF PDTRANCHE FINAL "INVENTAIRE"
  ITEM TRASTU OF PDTRANCHE FINAL "I"

;===============================================================================
REQUEST TRANSFERT_LOCALISATION
;-----------------------------

ACCESS INTRANCHE                        &
  LINK TRANUMTRA002     OF INTRANCHE,   &
       CIECLE           OF INTRANCHE    &
    TO TRANUMTRA002,                    &
       CIECLE           OF PDTRANCHE

CHOOSE CIECLE PARM

SELECT IF TRANUMLOC OF PDTRANCHE <> ITRNUMLOC OF INTRANCHE

OUTPUT PDTRANCHE UPDATE

  ITEM TRANUMLOC OF PDTRANCHE FINAL ITRNUMLOC OF INTRANCHE

BUILD

