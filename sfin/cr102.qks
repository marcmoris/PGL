;/T/ Consultation des clients          CLICIECLE, CIECLE, CLICLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 2 juin 1993
;
;    Description..: Affiche les clients disponibles par compagnie, on prend
;                   la compagnie du menu.
;                   Seul les clients actif sont sélectionnés.
;
;
;    Paramètre....: CLICIECLE ==> - compagnie du client.
;                                 - segment de la clé de recherche.
;
;                   CIECLE    ==> - compagnie du menu.
;                                 - segment de la clé de recherche.
;
;                   CLICLE    ==> - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;                                 - contien la valeur de retour.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr102 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CLICLE

USE cr102.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CLICIECLE   CHARACTER SIZE 4 ;
TEMPORARY T_CIECLEMNU   CHARACTER SIZE 4 ;
TEMPORARY T_CLICLE      CHARACTER SIZE 6 ;


;-------------------------------------------------------------------------------
;
FILE VCLC_CLI         PRIMARY &
                      OCCURS 11

 ACCESS VIA CLICIECLE, &
            CLICLE   , &
            CIECLE     &
        USING T_CLICIECLE, &
              T_CLICLE   , &
              T_CIECLEMNU  &
        ORDERED

 SELECT IF CLISTU OF VCLC_CLI = "A"


;-------------------------------------------------------------------------------
;
USE usdrwfnd NOLIST
USE ushilite NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN (3,3,6) (,,13)

CLUSTER OCCURS WITH VCLC_CLI

FIELD CLICLE    OF VCLC_CLI HIDDEN LABEL ">>" &
        DISPLAY

FIELD CLINOMABR OF VCLC_CLI &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CLICLE, "@")
  THEN LET T_CLICLE = ""
END


;-------------------------------------------------------------------------------
;
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CLICLE = CLICLE OF VCLC_CLI
  RETURN
END

BUILD

;                                       *****************************************
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       *****************************************
