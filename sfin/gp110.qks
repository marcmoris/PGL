;/T/ Consultation Compte comptable     CPTCLE, CPTTYPPAT, CPTCATPAT,PECCLE
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 02-Mar-1992
;
;    Description..: Permet de visionner et de choisir un compte comptable
;
;    Paramètres...: CPTCLE    ==> - numéro de compte
;                                 - segment de la clé de recherche
;                                 - conient la valeur de retour
;
;                   CPTTYPPAT ==> - type de compte
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;                   CPTCATPAT ==> - catégorie de compte.
;                                 - critère de sélection sous la forme
;                                   de pattern. Ex: "AV|EN"
;
;                   PECCLE    ==> - période du document.
;                                 - critère de sélection.
;                                 - si ce champ est à blanc, alors
;                                   on ne tient pas compte de la période.
;
;/M/ Stephane Jackson, 98-08-20
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN gp110 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_CPTCLE   , &
                       T_CPTTYPPAT, &
                       T_CPTCATPAT, &
                       T_PECCLE

TEMPORARY T_CPTCLE    CHARACTER SIZE 6
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8
TEMPORARY T_PECCLE    CHARACTER SIZE 4

USE gp110.hlp NOLIST

;-------------------------------------------------------------------------------

FILE MCCOMPTE         PRIMARY &
                      OCCURS 11
  ACCESS VIA CPTCLE &
         USING T_CPTCLE &
         ORDERED

;  SELECT IF &
;    MATCHPATTERN(CPTTYP OF MCCOMPTE,TRUNC(T_CPTTYPPAT)) &
;    AND &
;    MATCHPATTERN(CPTCAT OF MCCOMPTE,TRUNC(T_CPTCATPAT)) &
;    AND &
;    ( &
;      ( T_PECCLE >= CPTPECDEBA OF MCCOMPTE &
;        AND &
;        ( T_PECCLE < CPTPECDEBI OF MCCOMPTE OR  &
;          0 = SIZE(TRUNC(CPTPECDEBI OF MCCOMPTE)) &
;        ) &
;      ) &
;      OR &
;      0 = SIZE(TRUNC(T_PECCLE)) &
;    )

; Ajustement pour le changement de siècle
  SELECT IF   &
   ( &
     ( &
       CPTPECDEBA OF MCCOMPTE[1:1] <> "0" &
       OR &
       CPTPECDEBI OF MCCOMPTE[1:1] <> "9" &
     ) &
   AND &
   ( &
     T_PECCLE[1:1] <> "9" &
     OR &
     CPTPECDEBA OF MCCOMPTE[1:1] <> "0" &
     OR &
     0 <> SIZE(TRUNC(CPTPECDEBI OF MCCOMPTE)) &
   ) &
     AND &
     ( &
       ( &
         ( &
           ( T_PECCLE >= CPTPECDEBA OF MCCOMPTE &
           AND &
             ( T_PECCLE < CPTPECDEBI OF MCCOMPTE OR  &
               0 = SIZE(TRUNC(CPTPECDEBI OF MCCOMPTE)) &
             ) &
           ) &
         )  &
        ) &
        OR &
        ( &
          T_PECCLE >= CPTPECDEBA OF MCCOMPTE &
          AND &
          T_PECCLE[1:1] = "9" &
          AND &
          CPTPECDEBI OF MCCOMPTE[1:1] = "0" &
          AND &
          CPTPECDEBA OF MCCOMPTE[1:1] = "9" &
        ) &
        OR &
        ( &
          T_PECCLE < CPTPECDEBI OF MCCOMPTE &
          AND &
          T_PECCLE[1:1] = "0" &
          AND &
          CPTPECDEBA OF MCCOMPTE[1:1] = "9" &
          AND &
          CPTPECDEBI OF MCCOMPTE[1:1] = "0" &
        ) &
        OR &
        ( &
          T_PECCLE[1:1] = "0" &
          AND &
          CPTPECDEBA OF MCCOMPTE[1:1] = "9" &
          AND &
          0 = SIZE(TRUNC(CPTPECDEBI OF MCCOMPTE)) &
        ) &
     ) &
     OR &
     0 = SIZE(TRUNC(T_PECCLE)) &
   ) &
   AND &
   MATCHPATTERN(CPTTYP OF MCCOMPTE,TRUNC(T_CPTTYPPAT)) &
   AND &
   MATCHPATTERN(CPTCAT OF MCCOMPTE,TRUNC(T_CPTCATPAT))





;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH MCCOMPTE

FIELD CPTCLE       OF MCCOMPTE HIDDEN LABEL ">>"
FIELD CPTDSCABRFRA OF MCCOMPTE DISPLAY

CLUSTER

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CPTCLE, "@")
  THEN LET T_CPTCLE = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_CPTCLE = CPTCLE OF MCCOMPTE
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxx        *
;                                       *****************************************
