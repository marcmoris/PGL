;/T/ Gestion des produits inventaire - Validation de la destruction
;
;/P/ Programmeur..: Nancy Marceau
;    Date création: 3 mars 1994
;
;    Description..: Cet écran vérifie si le produit d'inventaire reçu en 
;                   paramètre est référé dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar002ax NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING ARPRD_ENT , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Produits d'inventaire
;
FILE ARPRD_ENT   MASTER

;
; Pièces pour vérifier si le produit est référé. 
;

FILE ARCMD_DETAIL   REFERENCE

FILE ARREC_DETAIL   REFERENCE

FILE ARREQUISITION   REFERENCE

;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
; 
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN

  IF PREQTEPHY OF ARPRD_ENT <> 0
  THEN ERROR 1345  ; Destruction annulée, la quantité en main est différente de
                   ; zéro.
  
  GET ARCMD_DETAIL  &
    VIA   CIECLE, &
          PRDCLE  &
    USING CIECLE OF ARPRD_ENT, &
          PRDCLE OF ARPRD_ENT OPTIONAL

  IF ACCESSOK
  THEN ERROR 1340 ; Destruction annulée, le produit est reféré dans une commande.
  
 
  GET ARREC_DETAIL &
    VIA   CIECLE, &
          PRDCLE  &
    USING CIECLE OF ARPRD_ENT, &
          PRDCLE OF ARPRD_ENT OPTIONAL

  IF ACCESSOK
  THEN ERROR 1341 ; Destruction annulée, le produit est référé dans une réception 
                  

  GET ARREQUISITION &
    VIA   CIECLE, &
          PRDCLE  &
    USING CIECLE OF ARPRD_ENT, &
          PRDCLE OF ARPRD_ENT OPTIONAL

  IF ACCESSOK
  THEN ERROR 1343 ; Destruction annulée, le produit est référé dans une réquisition 
             
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
