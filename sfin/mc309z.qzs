;/T/ Périodes comptables - Compagnie
;
;;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 13 mai 1993
;
;    Description...: Ce rapport présente la liste période comptable - Compagnie.
;
;
;    Particularités:
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....


ACCESS MCPERIODE_CIE &
  LINK PECCLE OF MCPERIODE_CIE &
    TO PECCLE OF MCPERIODE_CTB OPTIONAL


DEFINE D_SELANNFIN CHARACTER SIZE 2 = PARM     ; Sélection selon une année financière (Une ou tous)


SELECT MCPERIODE_CIE IF &                      ; Sélection de la période (ou tous) sélectionné par l'usager
       0 = SIZE(TRUNC(D_SELANNFIN)) OR (D_SELANNFIN  = PECCLE OF MCPERIODE_CIE[1:2])


 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &        ; titre1  du rapport
@IF FRANCAIS
                "Périodes comptables - Compagnie"
@ELSE
                "%%%Périodes comptables - Compagnie"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "      ; titre2  du rapport
 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "MC309"  ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM     ; Nom de la compagnie

 USE usflgdet NOLIST ; Code (O/N) bilingue

 DEFINE D_PCCSTUHLD CHARACTER SIZE 3 = &        ; Code indiquant si fermer (Oui/Non)
    DZ_FLGAFFO IF PCCSTUHLD OF MCPERIODE_CIE = "1" ELSE &
    DZ_FLGAFFN IF PCCSTUHLD OF MCPERIODE_CIE = "0" ELSE " "

 DEFINE D_ANNFIN CHARACTER SIZE 2 = PECCLE OF MCPERIODE_CTB[1:2] ;Année financière de la période

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF MCPERIODE_CIE &
         IF PECCLE OF MCPERIODE_CIE[1:1] < "8" &
         ELSE "0" + PECCLE OF MCPERIODE_CIE

; Ajustement pour le changement de siècle
DEFINE D_ANNFIN_SIE CHARACTER SIZE 3 = "1" + D_ANNFIN[1:2] &
         IF D_ANNFIN[1:1] < "8" &
         ELSE "0" + D_ANNFIN[1:2]

 SORT ON  D_ANNFIN_SIE               , &         ; Année financière
          CIECLE OF MCPERIODE_CIE, &         ; Compagnie
          D_PECCLE_SIE            ; Période comptable

USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP 2 &
@IF FRANCAIS
  TAB 002 "                                                     -------G/L-------  -------C/R-------  -------C/P------ " &
SKIP &
  TAB 002 "                        Date       Date      Cie       Date               Date               Date           " &
SKIP &
  TAB 002 "Compagnie   Période     début      fin      fermée   fermeture  Statut  fermeture  Statut  fermeture  Statut" &
@ELSE
  TAB 002 "%%%                                                  -------G/L-------  -------C/R-------  -------C/P------ " &
SKIP &
  TAB 002 "%%%                     Date       Date      Cie       Date               Date               Date           " &
SKIP &
  TAB 002 "%%%pagnie   Période     début      fin      fermée   fermeture  Statut  fermeture  Statut  fermeture  Statut" &
@ENDIF
SKIP &
  TAB 002 "---------   -------   --------   --------   ------   ---------  ------  ---------  ------  ---------  ------"&
SKIP


REPORT &
  TAB 004 CIECLE    OF MCPERIODE_CIE &
  TAB 015 PECCLE    OF MCPERIODE_CIE &
  TAB 024 PECDATDEB OF MCPERIODE_CTB &
  TAB 036 PECDATFIN OF MCPERIODE_CTB &
  TAB 046 D_PCCSTUHLD                &
  TAB 055 PCCDATFERGL OF MCPERIODE_CIE &
  TAB 068 PCCSTUGL    OF MCPERIODE_CIE &
  TAB 074 PCCDATFERCR OF MCPERIODE_CIE &
  TAB 087 PCCSTUCR    OF MCPERIODE_CIE &
  TAB 093 PCCDATFERCP OF MCPERIODE_CIE &
  TAB 106 PCCSTUCP    OF MCPERIODE_CIE



FOOTING AT D_ANNFIN_SIE &
SKIP PAGE


BUILD mc309z


;                                                                                                   1         1         1         1
;         1         2         3         4         5         6         7         8         9         0         1         2         3
;123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
;MC309                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
;99/99/99 HH:MM xxxxxxxxxxxx                        Périodes comptables - compagnie
;                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
;
;                                                      -------G/L-------  -------C/R-------  -------C/P------
;                         Date       Date      Cie       Date               Date               Date
; Compagnie   Période     début      fin      fermée   fermeture  Statut  fermeture  Statut  fermeture  Statut
; ---------   -------   --------   --------   ------   ---------  ------  ---------  ------  ---------  ------
;   xxxx       xxxxx    xxxxxxxx   xxxxxxxx    xxx     xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x
;   xxxx       xxxxx    xxxxxxxx   xxxxxxxx    xxx     xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x
;   xxxx       xxxxx    xxxxxxxx   xxxxxxxx    xxx     xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x
;   xxxx       xxxxx    xxxxxxxx   xxxxxxxx    xxx     xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x
;   xxxx       xxxxx    xxxxxxxx   xxxxxxxx    xxx     xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x



