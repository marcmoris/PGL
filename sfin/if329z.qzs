;/P/ Rapport de la facturation sommaire
;
;/M/ Programmeur...: Bernard Samson
;    Date..........: 15/11/99
;    Description...: Facturation sommaire par période comptable
;
;----------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS PDFACTURE &
LINK   PECCLE OF PDFACTURE &
  TO   PECCLE                          OF MCPERIODE_CTB &
LINK   (CIECLE  OF PDFACTURE,  &
       CLICLE   OF PDFACTURE,  &
       "----")                 &
  TO   CIECLE,                 &
       CLICLE,		       &
       CLICIECLE                       OF VCLC_CLI

CHOOSE CIECLE PARM, &
       CLICLE PARM

DEFINE D_PECCLE CHARACTER SIZE 4 = PARM

;Numéro de commande
DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF PDFACTURE  &
                                    + "-"                 &
                                    + PJTANN OF PDFACTURE &
                                    + "-"                 &
                                    + COMNUMCOMINT OF PDFACTURE      &
                                    IF PJTABR OF PDFACTURE <> "" AND &
                                       PJTANN OF PDFACTURE <> "" AND &
                                       COMNUMCOMINT OF PDFACTURE <> "" &
                                    ELSE ""

;On choisie les factures de commandes de bloc seulement et si
; elles sont incluses dans la période comptable choisie

SELECT IF  PECCLE OF PDFACTURE = D_PECCLE &
           AND &
           FTRFLGBLC OF PDFACTURE = "BL"



SORT ON CLICLE OF PDFACTURE    &
     ON FTRNUMFAC OF PDFACTURE

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
                     "Période..: " + PECCLE OF MCPERIODE_CTB + &
                     " Du " + ASCII( PECDATDEB OF MCPERIODE_CTB, 8 )[3:2] + "/" + &
                             ASCII( PECDATDEB OF MCPERIODE_CTB, 8 )[5:2] + "/" + &
                             ASCII( PECDATDEB OF MCPERIODE_CTB, 8 )[7:2] + &
                    " Au " + ASCII( PECDATFIN OF MCPERIODE_CTB, 8 )[3:2] + "/" + &
                             ASCII( PECDATFIN OF MCPERIODE_CTB, 8 )[5:2] + "/" + &
                             ASCII( PECDATFIN OF MCPERIODE_CTB, 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF329"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
        TAB 005 "NO"   &
        TAB 013 "DATE"       &
        TAB 025 "NO"  	   &
        TAB 035 "NO"     &
        TAB 042 "TOTAL"    &
        TAB 052 "SOUS-TOTAL"    &
        TAB 066 "TOTAL"     &
        TAB 078 "TOTAL"     &
        TAB 091 "TOTAL"     &
SKIP 1                     &
        TAB 003 "FACTURE"   &
        TAB 012 "FACTURE"       &
        TAB 022 "COMMANDE"  	   &
        TAB 033 "CLIENT"     &
        TAB 044 "M3"    &
        TAB 053 "FACTURE"    &
        TAB 067 "TPS"     &
        TAB 079 "TVQ"     &
        TAB 090 "FACTURE"     &
SKIP 1 &
        TAB 003 "_______" &
        TAB 011 "________"                   &
        TAB 022 "_________"  		    &
        TAB 033 "______" 		    &
        TAB 041 "________"		    &
        TAB 052 "__________" 		    &
        TAB 064 "__________"		    &
        TAB 076 "__________"                &
        TAB 088 "___________"               &
SKIP 2
REPORT &
        TAB 003 FTRNUMFAC    OF PDFACTURE          		 &
        TAB 011 FTRDATFAC    OF PDFACTURE	                 &
        TAB 022 D_NUMCOM                    		         &
        TAB 033 CLICLE       OF PDFACTURE       	         &
        TAB 040 FTRVOLMC3BLO OF PDFACTURE 		         &
        TAB 052 FTRMNTBRU    OF PDFACTURE PICTURE "^^^,^^^.^^ "  &
        TAB 064 FTRMNTTPS    OF PDFACTURE PICTURE "^^^,^^^.^^ "  &
        TAB 076 FTRMNTTVQ    OF PDFACTURE PICTURE "^^^,^^^.^^ "  &
        TAB 087 FTRMNT       OF PDFACTURE PICTURE "^,^^^,^^^.^^ "  &

SKIP 1 						





FINAL FOOTING &
        TAB 003 "_____________"        			  &
        TAB 041 "____________"         			  &
        TAB 088 "_____________"        			  &
SKIP 1                                 			  &
 	TAB 001 COUNT "Factures"        		  &
        TAB 031 "Total:"               			  &
        TAB 041 FTRVOLMC3BLO SUBTOTAL                     &
        TAB 088 FTRMNT SUBTOTAL PICTURE "^^^,^^^,^^.^^"	

BUILD if329z
