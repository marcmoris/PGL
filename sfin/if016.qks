;/T/ Gestion des notes de crédits
;
;/M/ François Déry, 25 mai 1999
;       Conversion unix/interbase.
;
; 07-03-95 Prosig: Changer le facteur de conversion du poids Tone Métric ancien = 3.48 nouveau = 3.50
;
SCREEN if016 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND,ENTRY,CHANGE &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF016"

USE if016.hlp NOLIST

USE usactecr  NOLIST
ACTIONMENU LABEL "Traitement"
  MENUITEM LABEL "Impression de la note de crédit" ACTION DESIGNER D001
  MENUITEM LABEL "Duplicata de la note de crédit " ACTION DESIGNER D002
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE IFSEQUENCE IN DICT
                                                ; Le IN DICT est pour unix

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

;
FILE IFNOT_CRE_MAITRE PRIMARY  NODELETE
        ITEM NCMFIN OF IFNOT_CRE_MAITRE INIT "N"
        ITEM NCMANNUL OF IFNOT_CRE_MAITRE INIT "N"
        ITEM NCMIMP OF IFNOT_CRE_MAITRE INIT "N"
        ITEM CIECLE OF IFNOT_CRE_MAITRE INIT T_CIECLE
        ITEM NCMFACTOTCAN OF IFNOT_CRE_MAITRE FINAL NCMTOTALCANVENT OF IFNOT_CRE_MAITRE
        ITEM NCMTRANSFERT OF IFNOT_CRE_MAITRE INIT "N"
;
FILE IFNOTE_CREDIT DETAIL OCCURS 1  NOITEM

DEF TMP-M3-AJUS   INTEGER SIZE 4 = FLOOR(CREDIMHAUAJUS * &
                                            CREDIMLONAJUS * &
                                            CREDIMLARAJUS  / 10000 + .5)
        ITEM CREM3AJUS OF IFNOTE_CREDIT FINAL TMP-M3-AJUS
        ITEM CRECLE OF IFNOTE_CREDIT INIT CRECLE OF IFNOT_CRE_MAITRE FIXED
        ITEM CIECLE OF IFNOTE_CREDIT INIT T_CIECLE

FILE IFNOTE_CREDIT ALIAS TOT_NOT_CRE DESIGNER
  ACCESS VIA   CIECLE, &
               CRECLE  &
         USING T_CIECLE, &
               CRECLE OF IFNOT_CRE_MAITRE

FILE IFFACTURE REFERENCE
  ACCESS VIA   CIECLE, &
               FCECLE  &
         USING T_CIECLE, &
               FCECLE OF IFNOT_CRE_MAITRE

FILE IFFACTURE_DETAIL DESIGNER OCCURS WITH IFNOTE_CREDIT
  ACCESS VIA   CIECLE, &
               FCECLE, &
               CRRCLENUM, &
               CRRCLEANN, &
               BLCCLE &
         USING T_CIECLE, &
               FCECLE OF IFNOT_CRE_MAITRE, &
               CRRCLENUM OF IFNOTE_CREDIT, &
               CRRCLEANN OF IFNOTE_CREDIT, &
               BLCCLE OF IFNOTE_CREDIT
        ITEM CIECLE OF IFFACTURE_DETAIL INIT T_CIECLE

FILE IFBLOC DESIGNER OCCURS WITH IFNOTE_CREDIT NODELETE
  ACCESS VIA   CIECLE, &
               CRRCLENUM, &
               CRRCLEANN, &
               BLCCLE &
         USING T_CIECLE, &
               CRRCLENUM OF TOT_NOT_CRE, &
               CRRCLEANN OF TOT_NOT_CRE, &
               BLCCLE OF TOT_NOT_CRE
  ITEM CIECLE OF IFBLOC INIT T_CIECLE

FILE IFCONTRAT REFERENCE OCCURS WITH IFNOTE_CREDIT
  ACCESS VIA   CIECLE, &
               CLTCLE, &
               CRRCLENUM, &
               CRRCLEANN &
         USING T_CIECLE, &
               CLTCLE OF IFFACTURE, &
               CRRCLENUM OF IFFACTURE, &
               CRRCLEANN OF IFFACTURE

FILE IFCONTRAT_LIGNE DESIGNER NODELETE OCCURS WITH IFNOTE_CREDIT
  ACCESS VIA   CIECLE, &
               CLTCLE, &
               CRRCLENUM, &
               CRRCLEANN, &
               CATCLE, &
               COEDATDEB &
         USING T_CIECLE, &
               CLTCLE OF IFFACTURE, &
               CRRCLENUM OF IFFACTURE, &
               CRRCLEANN OF IFFACTURE, &
               CATCLE OF IFBLOC, &
               COEDATDEB OF IFBLOC

FILE IFCLIENT REFERENCE
  ACCESS VIA   CIECLE, &
               CLTCLE  &
         USING T_CIECLE, &
               CLTCLE OF IFFACTURE

FILE CRCLIENT         REFERENCE
  ACCESS VIA   CLICIECLE, &
               CLICLE &
         USING CLICIECLE OF IFCLIENT, &
               CLICLE OF IFCLIENT

;
; Table pour les adresses
;
FILE MCREF_ADRESSE    REFERENCE
 ACCESS VIA   REFCIECLE, &
              REFCLETYP, &
              REFCLENUM, &
              RADCLE &
        USING CLICIECLE OF CRCLIENT, &
              CLIREFTYP OF CRCLIENT, &
              CLICLE    OF CRCLIENT, &
              1
;
; Information du client par compagnie
;
FILE CRCLI_CIE        REFERENCE
  ACCESS VIA   CLICIECLE, &
               CLICLE, &
               CIECLE  &
         USING CLICIECLE OF IFCLIENT, &
               CLICLE OF IFCLIENT, &
               T_CIECLE
;
; % de taxe fédéral
;
FILE VTAX_DSC         REFERENCE ALIAS A_TAXFED
  ACCESS VIA   TAXCLETYP, &
               TAXCLECOD &
         USING CLCTAXTYPTPS OF CRCLI_CIE, &
               CLCTAXCODTPS OF CRCLI_CIE
;
; % de taxe provincial
;
FILE VTAX_DSC         REFERENCE ALIAS A_TAXPRO
  ACCESS VIA   TAXCLETYP, &
               TAXCLECOD &
         USING CLCTAXTYPTVP OF CRCLI_CIE, &
               CLCTAXCODTVP OF CRCLI_CIE

FILE IFSEQUENCE DESIGNER &
                TRANSACTION GEN_NUM_SEQ

FILE IFANNEE_COURANTE DESIGNER

FILE IFCARRIERE DESIGNER  OCCURS WITH IFNOTE_CREDIT

FILE IFBLOC_EXPED DESIGNER OCCURS WITH IFNOTE_CREDIT
     ITEM CIECLE OF IFBLOC_EXPED INIT T_CIECLE

FILE IFEXPEDITION DESIGNER OCCURS WITH IFNOTE_CREDIT
     ITEM CIECLE OF IFEXPEDITION INIT T_CIECLE

FILE MCPERIODE_CTB  DESIGNER
 SELECT IF (NCMDAT OF IFNOT_CRE_MAITRE <= PECDATFIN OF MCPERIODE_CTB AND &
            NCMDAT OF IFNOT_CRE_MAITRE >= PECDATDEB OF MCPERIODE_CTB)

FILE MCDEVISE DESIGNER


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

DEF TMP-TOT-FOB INTEGER SIZE 4 = FLOOR(FDEPRIXFOB OF IFFACTURE_DETAIL * TMP-M3-AJUS / 100 + .5 )
DEF TMP-TOT-FAS INTEGER SIZE 4 = FLOOR(FDEPRIXFAS OF IFFACTURE_DETAIL * TMP-M3-AJUS / 100 + .5 )
DEF TMP-TOTAL INTEGER SIZE 4 = TMP-TOT-FOB + TMP-TOT-FAS
DEF TMP-TOT-DIF INTEGER SIGNED SIZE 4 = FDETOTAL OF IFFACTURE_DETAIL - TMP-TOTAL
DEF TMP-M3-DIF INTEGER SIGNED SIZE 4 = FDEM3PROD OF IFFACTURE_DETAIL - TMP-M3-AJUS
TEMP TMP-FIN CHARACTER SIZE 1 INIT NCMFIN OF IFNOT_CRE_MAITRE
TEMP TMP-NCMANNUL CHARACTER SIZE 1 INIT NCMANNUL OF IFNOT_CRE_MAITRE

ITEM DEVCLE OF IFNOTE_CREDIT FINAL DEVCLE OF CRCLIENT
ITEM CRETOTAL OF IFNOTE_CREDIT FINAL TMP-TOTAL

ITEM NCMTOTALVENT OF IFNOT_CRE_MAITRE FINAL TMP-TOTAL

DEF T-SOUS-TOTAL   FLOAT SIZE 4 = TMP-TOT-DIF  + CREMNTTPSFED &
    OF IFNOTE_CREDIT
DEF T-TOTAL-CREDIT FLOAT SIZE 4 = T-SOUS-TOTAL + CREMNTTVQPRO &
    OF IFNOTE_CREDIT

DEFINE D_IMPNOT CHARACTER SIZE 7   = "IF324A" ; Impression de facture
DEFINE D_DUPNOT CHARACTER SIZE 7   = "IF325" ; duplicata de facture
;
; Liste des paramètres.
;
DEFINE D_LISPAR CHARACTER SIZE 204 = "F_CIECLEMNU     |F_CIENOM        |" + &
                                     "CRECLE          |"
DEFINE D_INDLON CHARACTER SIZE 112 = "001004|005040|045005"
DEFINE D_LISVAL CHARACTER SIZE 300 = T_CIECLE &
                                   + DZ_CIENOM    &
                                   + ASCII( CRECLE OF IFNOT_CRE_MAITRE , 5 )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Gestion des notes de credit " &
      CENTERED AT ,1
SKIP TO LINE 4

ALIGN (2,5,17)(,24,32)(,41,54)(,60,70)(,72,78)
FIELD CRECLE OF IFNOT_CRE_MAITRE &
        HIDDEN ID 1 &
        LABEL "No credit..:" &
        DISPLAY
FIELD NCMDAT OF IFNOT_CRE_MAITRE &
        NOCHANGE &
        DEFAULT SYSDATE &
        LABEL "Date..:"
FIELD NCMFIN OF IFNOT_CRE_MAITRE &
        LABEL "Code fin:"
FIELD NCMIMP OF IFNOT_CRE_MAITRE &
        LABEL "Code imp:" &
        DISPLAY
FIELD NCMANNUL OF IFNOT_CRE_MAITRE &
        LABEL "Annul:"

ALIGN (2,5,17)(,24,36)(,,38)(,,40)(,43,54)
FIELD  FCECLE OF IFNOT_CRE_MAITRE &
        HIDDEN ID 2 &
        NOCHANGE &
        REQUIRED &
        LABEL "No facture:"
FIELD CLTCLE OF IFFACTURE &
        LABEL "No contrat.:" &
        DISPLAY
FIELD CRRCLENUM OF IFFACTURE &
        DISPLAY
FIELD CRRCLEANN OF IFFACTURE &
        DISPLAY
FIELD FCEDAT OF IFFACTURE &
        LABEL "Date fact.:" &
        DISPLAY
SKIP

ALIGN(2,5,17)(,43,54)(,59,71)
FIELD PECCLE OF IFNOT_CRE_MAITRE &
        HIDDEN ID 3 &
        LABEL "No Période:" &
        DISPLAY
FIELD LCRCLE OF IFNOT_CRE_MAITRE &
        LABEL "No Lot....:" &
        DISPLAY
FIELD NCMTRANSFERT OF IFNOT_CRE_MAITRE &
        LABEL "Transférée:" &
        DISPLAY
SKIP
ALIGN (,5,17) (,43,54) (,59,71)
FIELD CLINOMABR OF CRCLIENT &
        LABEL "Nom client.:" &
        DISPLAY
FIELD CLILNG OF CRCLIENT &
        LABEL "Langue....:" &
        DISPLAY
FIELD DEVCLE OF CRCLIENT &
        LABEL "Devise....:" DISPLAY

ALIGN (,5,11)(,43,54)
FIELD FCEADR1 OF IFFACTURE  &
        LABEL "Adr.:"  DISPLAY
FIELD CONCONDITION1 OF IFCONTRAT &
        LABEL "Conditions:" DISPLAY
FIELD FCEADR2 OF IFFACTURE &
        LABEL "Fact:" DISPLAY
FIELD CONCONDITION2 OF IFCONTRAT &
        NOLABEL DISPLAY
FIELD FCEADR3 OF IFFACTURE &
        NOLABEL DISPLAY
FIELD FCECP OF IFFACTURE &
        LABEL "Code post:" &
        DISPLAY
SKIP

ALIGN(,5,19)
FIELD NCMCOM1 OF IFNOT_CRE_MAITRE &
        LABEL "Commentaires:"
FIELD NCMCOM2 OF IFNOT_CRE_MAITRE  &
        ID SAME NOLABEL
SKIP
TITLE "No bloc   Qte     Dimensions      Tot m3      FOB        FAS        Total   " AT ,4
SKIP
TITLE "          bloc  Long Haut Larg                                              " AT ,4
ALIGN(2,3,5)(,,7)(,8,9)(,,15)(,,21)(,25,27)(,31,33)(,,38)(,,48)(,,59)(,,70)
CLUSTER OCCURS WITH IFNOTE_CREDIT ID BASE 4
FIELD CRRCLENUM OF IFNOTE_CREDIT &
        HIDDEN &
        LABEL " " &
        REQUIRED &
        NOCHANGE
FIELD CRRCLEANN OF IFNOTE_CREDIT &
        HIDDEN &
        LABEL " " &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON IFCARRIERE &
          VIA   CIECLE, &
                CRRCLENUM, &
                CRRCLEANN  &
          USING T_CIECLE, &
                CRRCLENUM OF IFNOTE_CREDIT, &
                CRRCLEANN OF IFNOTE_CREDIT  &
          MESSAGE "*E* Cette carrière n'existe pas..."
FIELD BLCCLE OF IFNOTE_CREDIT &
        LABEL "-" &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFNOTE_CREDIT &
          VIA   CIECLE, &
                CRECLE, &
                CRRCLENUM, &
                CRRCLEANN, &
                BLCCLE &
          USING CIECLE OF IFNOTE_CREDIT, &
                CRECLE OF IFNOTE_CREDIT, &
                CRRCLENUM OF IFNOTE_CREDIT, &
                CRRCLEANN OF IFNOTE_CREDIT, &
                BLCCLE OF IFNOTE_CREDIT &
          MESSAGE "*E* Ce bloc est déjà sur cette note..."
FIELD CREQTEBLAJUS OF IFNOTE_CREDIT &
        HELP "*I* LES VALEURS POSSIBLES SONT 0 OU 1 "
FIELD FDEDIMLON OF IFFACTURE_DETAIL DISPLAY
FIELD FDEDIMHAU OF IFFACTURE_DETAIL LABEL "x" DISPLAY
FIELD FDEDIMLAR OF IFFACTURE_DETAIL LABEL "x" DISPLAY
FIELD FDEM3PROD OF IFFACTURE_DETAIL DISPLAY PIC "^^,^^^.^^"
FIELD FDEPRIXFOB OF IFFACTURE_DETAIL DISPLAY
FIELD FDEPRIXFAS OF IFFACTURE_DETAIL DISPLAY
FIELD FDETOTAL OF IFFACTURE_DETAIL DISPLAY
SKIP
TITLE "Ajustements: " AT ,4
ALIGN(,,21)(,25,27)(,31,33)(,,39)(,,48)(,,59)(,,70)
FIELD CREDIMLONAJUS OF IFNOTE_CREDIT   REQUIRED
FIELD CREDIMHAUAJUS OF IFNOTE_CREDIT LABEL "x"  REQUIRED
FIELD CREDIMLARAJUS OF IFNOTE_CREDIT LABEL "x"  REQUIRED
FIELD TMP-M3-AJUS PICTURE "^^,^^^.^^" DISPLAY
FIELD CREPRIXFOB OF IFNOTE_CREDIT DISPLAY
FIELD CREPRIXFAS OF IFNOTE_CREDIT DISPLAY
FIELD TMP-TOTAL PICTURE "^^^^^^.^^" DISPLAY
SKIP
HILITE TITLE OFF
TITLE "__________                     __________" AT ,38
SKIP
ALIGN (,,38)(,,69)
FIELD TMP-M3-DIF DISPLAY PICTURE "^^^^^^.^^" SIGNIFICANCE 3
FIELD TMP-TOT-DIF DISPLAY  PICTURE "^^^^^^^.^^" SIGNIFICANCE 3
SKIP

ALIGN(,42,46)(54,55,57)(,,69)
FIELD TAXPCT OF A_TAXFED NOID LABEL "TPS " PIC "^^^.^^%"
FIELD CREMNTTPSFED OF IFNOTE_CREDIT &
        HIDDEN ID 5 &
        LABEL " "
FIELD T-SOUS-TOTAL NOID NOLABEL DISPLAY PICTURE " ^^^^^^.^^"
SKIP

ALIGN (,5,26)(,42,46)(54,55,57)
FIELD CLCEXPTVP OF CRCLI_CIE SIZE 15 LABEL "No exemp. TX prov. :" DISPLAY
FIELD TAXPCT OF A_TAXPRO NOID LABEL "TVQ " PIC "^^^.^^%"
FIELD CREMNTTVQPRO OF IFNOTE_CREDIT &
        HIDDEN ID 6 &
        LABEL " "
TITLE "__________" AT ,69
SKIP

ALIGN (,61,69)
FIELD T-TOTAL-CREDIT NOID LABEL "Total :" DISPLAY  PICTURE "$^^^^^^.^^"
SKIP

ALIGN (2,5,53)
FIELD CREDISP OF IFNOTE_CREDIT &
        HIDDEN ID 7 &
        REQUIRED &
        LABEL "Credit pour bloc qui redevient disponible O/N: "
CLUSTER

PROCEDURE EDIT BLCCLE OF IFNOTE_CREDIT
BEGIN
  GET IFFACTURE_DETAIL OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR "*E* CE BLOC N'EXISTE PAS SUR LA FACTURE..."
END

PROCEDURE PROCESS CREDIMLONAJUS
;       GUY CHABOT ------vvvvvvvvvvvvvv  les deux of IFNOTE_CREDIT
BEGIN
        LET CREPRIXFOB OF IFNOTE_CREDIT = FDEPRIXFOB OF IFFACTURE_DETAIL
        LET CREPRIXFAS OF IFNOTE_CREDIT =  FDEPRIXFAS OF IFFACTURE_DETAIL
        DISPLAY CREPRIXFOB  OF IFNOTE_CREDIT
        DISPLAY CREPRIXFAS  OF IFNOTE_CREDIT
END

PROCEDURE EDIT FCECLE OF IFNOT_CRE_MAITRE
BEGIN
  GET IFFACTURE OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR "*E* CETTE FACTURE N'EXISTE PAS..."
  ELSE BEGIN
    IF FCEFIN = "N"
    THEN ERROR "*E* Cette facture n'est pas finale..."
    IF FCEANNUL = "O"
    THEN ERROR "*E* Cette facture est déjà annulée..."
  END
END
;
; Impression de la note de crédit
;
PROCEDURE DESIGNER D001
BEGIN
  IF ALTEREDRECORD OF IFNOT_CRE_MAITRE
  THEN ERROR "*E* Faire une mise a jour avant l'impression de la note de crédit"

  IF NCMIMP OF IFNOT_CRE_MAITRE = "O"
  THEN ERROR "*E* Cette note de crédit est déjà imprimée"

  IF     NCMFIN   OF IFNOT_CRE_MAITRE <> "O" &
     AND NCMANNUL OF IFNOT_CRE_MAITRE <> "O"
  THEN ERROR "*E* La note de crédit doit être finale pour être imprimée."

  RUN SCREEN gs091 &
             MODE E &
             PASSING D_IMPNOT, &
                     D_LISPAR, &
                     D_INDLON, &
                     D_LISVAL
  LET NCMIMP OF IFNOT_CRE_MAITRE = "O"
  PUT IFNOT_CRE_MAITRE
  DISPLAY NCMIMP OF IFNOT_CRE_MAITRE
END
;
; Duplicata de la note de crédit
;
PROCEDURE DESIGNER D002
BEGIN
  IF ALTEREDRECORD OF IFNOT_CRE_MAITRE
  THEN ERROR "*E* Faire une mise a jour avant l'impression de la note de crédit"

  IF    (     NCMFIN OF IFNOT_CRE_MAITRE = "O" &
          AND NCMIMP OF IFNOT_CRE_MAITRE = "O" &
        ) &
     OR NCMANNUL OF IFNOT_CRE_MAITRE = "O"
  THEN BEGIN
    RUN SCREEN gs091 &
             MODE E &
             PASSING D_DUPNOT, &
                     D_LISPAR, &
                     D_INDLON, &
                     D_LISVAL
  END
  ELSE ERROR "*E* La note de crédit doit être finale/imprimée ou annulée."
END

PROCEDURE PREUPDATE
BEGIN
  IF TMP-FIN = "O"
  THEN ERROR "*E* Mise à jour interdite car note de crédit finale"
  IF TMP-NCMANNUL = "O"
  THEN ERROR "*E* Mise à jour interdite car note de crédit annulée"
  IF NCMFIN = "O" AND NCMANNUL = "O"
  THEN ERROR "*E* On ne peut pas annuler une note de crédit finale"

  GET CRCLIENT OPTIONAL
  IF ACCESSOK
  THEN LET DEVCLE OF IFNOT_CRE_MAITRE = DEVCLE OF CRCLIENT

  GET MCDEVISE VIA DEVCLE USING DEVCLE OF IFNOT_CRE_MAITRE OPTIONAL
  IF ACCESSOK
  THEN LET DEVTAU OF IFNOT_CRE_MAITRE = DEVTAU OF MCDEVISE

  IF DEVCLE OF IFNOT_CRE_MAITRE = "CAN" OR DEVCLE OF IFNOT_CRE_MAITRE = ""
  THEN LET NCMTOTALCANVENT = TMP-TOTAL
  ELSE LET NCMTOTALCANVENT = &
           ROUND(TMP-TOTAL * DEVTAU OF IFNOT_CRE_MAITRE)

  IF NEWRECORD OF IFNOT_CRE_MAITRE
  THEN BEGIN
    START TRANSACTION GEN_NUM_SEQ
    GET IFSEQUENCE VIA   CIECLE, &
                         SEQCODE &
                   USING T_CIECLE, &
                         "FAC#" OPTIONAL
    LET CIECLE  OF IFSEQUENCE = T_CIECLE
    LET SEQCODE OF IFSEQUENCE = "FAC#"
    LET SEQNO   OF IFSEQUENCE = SEQNO OF IFSEQUENCE + 1
    LET CRECLE  OF IFNOT_CRE_MAITRE = SEQNO OF IFSEQUENCE
    PUT IFSEQUENCE
  END
END

PROCEDURE UPDATE
BEGIN
  IF NCMFIN = "N"
  THEN BEGIN
    PUT IFNOT_CRE_MAITRE
    PUT IFNOTE_CREDIT
  END
  ELSE BEGIN
    PUT IFNOT_CRE_MAITRE
    PUT IFNOTE_CREDIT
    WHILE RETRIEVING TOT_NOT_CRE
    BEGIN
      GET IFBLOC
      IF 0 = FCECLE OF IFBLOC
      THEN ERROR "Ce bloc n'est meme pas facturé..."
      ELSE BEGIN
        GET IFCONTRAT_LIGNE VIA   CIECLE, CLTCLE, CRRCLENUM, CRRCLEANN, CATCLE, &
                                  COEDATDEB &
                            USING T_CIECLE, CLTCLE OF IFBLOC, BLCCRRCLENUM OF IFBLOC, &
                                  BLCCRRCLEANN OF IFBLOC, CATCLE OF IFBLOC, &
                                  COEDATDEB OF IFBLOC
        GET IFFACTURE_DETAIL VIA   CIECLE, FCECLE, CRRCLENUM, CRRCLEANN, BLCCLE &
                             USING T_CIECLE, FCECLE OF IFNOT_CRE_MAITRE, &
                                   CRRCLENUM OF IFBLOC, CRRCLEANN OF IFBLOC, &
                                   BLCCLE OF IFBLOC OPTIONAL
        IF CREDISP OF TOT_NOT_CRE = "O"
        THEN BEGIN
          IF BLCINVINIT OF IFBLOC = "O "
          THEN BEGIN
            GET IFCARRIERE VIA   CIECLE, CRRCLENUM, CRRCLEANN &
                           USING T_CIECLE, CRRCLENUM OF IFBLOC, &
                                 CRRCLEANN OF IFBLOC OPTIONAL
            IF NOT ACCESSOK
            THEN ERROR "*E* Probleme de carrière..."
            ELSE BEGIN
              LET CRRM3INIAUJOU  OF IFCARRIERE = CRRM3INIAUJOU OF IFCARRIERE + &
                                                 FDEM3PROD OF IFFACTURE_DETAIL
              LET CRRNBBLOINIT OF IFCARRIERE = CRRNBBLOINIT OF IFCARRIERE + 1
              PUT IFCARRIERE
            END
          END
          GET IFBLOC_EXPED VIA   CIECLE, EXNCLE, CRRCLENUM, CRRCLEANN, BLCCLE &
                           USING T_CIECLE, EXNCLE OF IFBLOC, &
                                 CRRCLENUM OF TOT_NOT_CRE, CRRCLEANN OF TOT_NOT_CRE, &
                                 BLCCLE OF TOT_NOT_CRE OPTIONAL
          IF ACCESSOK
          THEN BEGIN
            GET IFEXPEDITION VIA   CIECLE, EXNCLE &
                             USING T_CIECLE, EXNCLE OF IFBLOC
            LET EXNTOTAL OF IFEXPEDITION = EXNTOTAL OF IFEXPEDITION - BLCM3TOT OF IFBLOC
            DELETE IFBLOC_EXPED
            LET COEQTEEXP = COEQTEEXP - BLCM3TOT OF IFBLOC
            PUT IFBLOC_EXPED
            PUT IFEXPEDITION
          END
          LET COEQTERECL = COEQTERECL + FDEM3PROD - CREM3AJUS OF TOT_NOT_CRE
          LET COENBBLRECL = COENBBLRECL + CREQTEBLAJUS OF TOT_NOT_CRE
          LET CLTCLE       OF IFBLOC = 0
          LET BLCCRRCLENUM OF IFBLOC = 0
          LET BLCCRRCLEANN OF IFBLOC = 0
          LET CATCLE       OF IFBLOC = ""
          LET COEDATDEB    OF IFBLOC = 0
          LET BATCLE       OF IFBLOC = 0
          LET BLCDATASSIG  OF IFBLOC = 0
          LET EXNCLE       OF IFBLOC = 0
          LET CRECLE       OF IFBLOC = 0
          LET FCECLE       OF IFBLOC = 0
        END
        ELSE BEGIN
          LET COEQTERECL = COEQTERECL + FDEM3PROD - CREM3AJUS  OF TOT_NOT_CRE
          LET COENBBLRECL = COENBBLRECL + CREQTEBLAJUS OF TOT_NOT_CRE
          LET BLCVENDUHAU  = CREDIMHAUAJUS  OF TOT_NOT_CRE
          LET BLCVENDULONG = CREDIMLONAJUS  OF TOT_NOT_CRE
          LET BLCVENDULAR = CREDIMLARAJUS  OF TOT_NOT_CRE
          LET BLCM3TOT = CREM3AJUS  OF TOT_NOT_CRE
          LET BLCTM OF IFBLOC = CREM3AJUS OF TOT_NOT_CRE * 3.50
          LET BLCTFINAL OF IFBLOC = CREM3AJUS OF TOT_NOT_CRE * 3.50 + BLCTMREG OF IFBLOC
          LET CRECLE OF IFBLOC = CRECLE OF TOT_NOT_CRE
        END
        PUT IFCONTRAT_LIGNE
        PUT IFBLOC
      END
      PUT TOT_NOT_CRE
    END
  END
END

PROCEDURE INTERNAL CALCUL-MNT-TAXE
BEGIN
    LET CREMNTTPSFED OF IFNOTE_CREDIT = ROUND(TMP-TOT-DIF * TAXPCT OF A_TAXFED )
    DISPLAY T-SOUS-TOTAL
    LET CREMNTTVQPRO OF IFNOTE_CREDIT = ROUND(T-SOUS-TOTAL * TAXPCT OF A_TAXPRO)
    DISPLAY T-TOTAL-CREDIT

    DISPLAY CREMNTTPSFED OF IFNOTE_CREDIT
    DISPLAY CREMNTTVQPRO OF IFNOTE_CREDIT
END


PROCEDURE DESIGNER 4
  BEGIN
    ACCEPT CRRCLENUM OF IFNOTE_CREDIT
    ACCEPT CRRCLEANN OF IFNOTE_CREDIT
    ACCEPT BLCCLE OF IFNOTE_CREDIT
    ACCEPT CREQTEBLAJUS OF IFNOTE_CREDIT
    DISPLAY FDEDIMLON OF IFFACTURE_DETAIL
    DISPLAY FDEDIMHAU OF IFFACTURE_DETAIL
    DISPLAY FDEDIMLAR OF IFFACTURE_DETAIL
    DISPLAY FDEM3PROD OF IFFACTURE_DETAIL
    DISPLAY FDEPRIXFOB OF IFFACTURE_DETAIL
    DISPLAY FDEPRIXFAS OF IFFACTURE_DETAIL
    DISPLAY FDETOTAL OF IFFACTURE_DETAIL
    ACCEPT CREDIMLONAJUS OF IFNOTE_CREDIT
    ACCEPT CREDIMHAUAJUS OF IFNOTE_CREDIT
    ACCEPT CREDIMLARAJUS OF IFNOTE_CREDIT
    DISPLAY TMP-M3-AJUS
    DISPLAY CREPRIXFOB OF IFNOTE_CREDIT
    DISPLAY CREPRIXFAS OF IFNOTE_CREDIT
    DISPLAY TMP-TOTAL
    DISPLAY TMP-M3-DIF
    DISPLAY TMP-TOT-DIF
    DO INTERNAL CALCUL-MNT-TAXE
    ACCEPT CREDISP OF IFNOTE_CREDIT
END


;
; SAISIE DU MONTANT DE TPS FEDERAL POUR CORRECTION
;
PROCEDURE DESIGNER 5
BEGIN
  ACCEPT CREMNTTPSFED OF IFNOTE_CREDIT
  DISPLAY T-SOUS-TOTAL
  LET CREMNTTVQPRO OF IFNOTE_CREDIT = ROUND(T-SOUS-TOTAL * TAXPCT OF A_TAXPRO )
  DISPLAY CREMNTTVQPRO OF IFNOTE_CREDIT
  DISPLAY T-TOTAL-CREDIT
END

;
; SAISIE DU MONTANT DE TVQ PROVINCIAL POUR CORRECTION
;
PROCEDURE DESIGNER 6
BEGIN
  ACCEPT CREMNTTVQPRO OF IFNOTE_CREDIT
  DISPLAY T-TOTAL-CREDIT
END




PROCEDURE POSTUPDATE
BEGIN
    DISPLAY CRECLE OF IFNOT_CRE_MAITRE
    LET TMP-NCMANNUL = NCMANNUL OF IFNOT_CRE_MAITRE
END

PROCEDURE PATH
  BEGIN
    REQUEST CRECLE OF IFNOT_CRE_MAITRE
    IF PROMPTOK
      THEN LET PATH = 1
    IF PATH = 0
      THEN LET PATH = 2
   END

PROCEDURE FIND
  BEGIN
    IF PATH = 1
      THEN GET IFNOT_CRE_MAITRE VIA CIECLE, CRECLE USING T_CIECLE , &
                        CRECLE OF IFNOT_CRE_MAITRE
    IF PATH = 2
      THEN GET IFNOT_CRE_MAITRE VIA CIECLE USING T_CIECLE
    LET TMP-FIN = NCMFIN OF IFNOT_CRE_MAITRE
    END

PROCEDURE DETAIL FIND
BEGIN
   FOR MISSING IFNOTE_CREDIT
   BEGIN
     GET IFNOTE_CREDIT VIA CIECLE, CRECLE &
                       USING T_CIECLE, CRECLE OF IFNOTE_CREDIT OPTIONAL
     GET IFFACTURE_DETAIL VIA CIECLE, FCECLE, CRRCLENUM, CRRCLEANN, BLCCLE &
                          USING T_CIECLE, FCECLE OF IFNOT_CRE_MAITRE, &
                                CRRCLENUM OF IFNOTE_CREDIT, CRRCLEANN OF IFNOTE_CREDIT, &
                                BLCCLE OF IFNOTE_CREDIT OPTIONAL
  END
END


PROCEDURE APPEND
  BEGIN
    ACCEPT CRRCLENUM OF IFNOTE_CREDIT
    ACCEPT CRRCLEANN OF IFNOTE_CREDIT
    ACCEPT BLCCLE OF IFNOTE_CREDIT
    ACCEPT CREQTEBLAJUS OF IFNOTE_CREDIT
    DISPLAY FDEDIMLON OF IFFACTURE_DETAIL
    DISPLAY FDEDIMHAU OF IFFACTURE_DETAIL
    DISPLAY FDEDIMLAR OF IFFACTURE_DETAIL
    DISPLAY FDEM3PROD OF IFFACTURE_DETAIL
    DISPLAY FDEPRIXFOB OF IFFACTURE_DETAIL
    DISPLAY FDEPRIXFAS OF IFFACTURE_DETAIL
    DISPLAY FDETOTAL OF IFFACTURE_DETAIL
    ACCEPT CREDIMLONAJUS OF IFNOTE_CREDIT
    ACCEPT CREDIMHAUAJUS OF IFNOTE_CREDIT
    ACCEPT CREDIMLARAJUS OF IFNOTE_CREDIT
    DISPLAY TMP-M3-AJUS
    DISPLAY CREPRIXFOB OF IFNOTE_CREDIT
    DISPLAY CREPRIXFAS OF IFNOTE_CREDIT
    DISPLAY TMP-TOTAL
    DISPLAY TMP-M3-DIF
    DISPLAY TMP-TOT-DIF
    DO INTERNAL CALCUL-MNT-TAXE
    ACCEPT CREDISP OF IFNOTE_CREDIT
    END


PROCEDURE ENTRY
  BEGIN
    DISPLAY CRECLE OF IFNOT_CRE_MAITRE
    ACCEPT NCMDAT OF IFNOT_CRE_MAITRE
    GET MCPERIODE_CTB SEQUENTIAL OPTIONAL
    LET PECCLE OF IFNOT_CRE_MAITRE = PECCLE OF MCPERIODE_CTB
    ACCEPT NCMFIN OF IFNOT_CRE_MAITRE
    DISPLAY NCMIMP OF IFNOT_CRE_MAITRE
    ACCEPT FCECLE OF IFNOT_CRE_MAITRE
    DISPLAY CLTCLE OF IFFACTURE
    DISPLAY CRRCLENUM OF IFFACTURE
    DISPLAY CRRCLEANN OF IFFACTURE
    DISPLAY FCEDAT OF IFFACTURE
    DISPLAY PECCLE OF IFNOT_CRE_MAITRE
    DISPLAY LCRCLE OF IFNOT_CRE_MAITRE
    DISPLAY NCMTRANSFERT OF IFNOT_CRE_MAITRE
    DISPLAY CLINOMABR OF CRCLIENT
    DISPLAY CLILNG OF CRCLIENT
    DISPLAY FCEADR1 OF IFFACTURE
    DISPLAY DEVCLE OF CRCLIENT
    DISPLAY FCEADR2 OF IFFACTURE
    DISPLAY CONCONDITION1 OF IFCONTRAT
    DISPLAY FCEADR3 OF IFFACTURE
    DISPLAY CONCONDITION2 OF IFCONTRAT
    DISPLAY FCECP OF IFFACTURE
    DISPLAY TAXPCT OF A_TAXFED
    DISPLAY TAXPCT OF A_TAXPRO
    DISPLAY CLCEXPTVP OF CRCLI_CIE

    ACCEPT NCMCOM1 OF IFNOT_CRE_MAITRE
    ACCEPT NCMCOM2 OF IFNOT_CRE_MAITRE
    FOR IFNOTE_CREDIT
      BEGIN
        PERFORM APPEND
        END
    END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************* Gestion des notes de credit **************************
*   No credit..:xxxxxx Date..: xxxxxxxx Code fin:    xx    Code imp: xxAnnul:x *
*   No facture: xxxxxx No contrat.:xxxxx  Date fact.:xxxxxxxx                  *
*   No Période: xxxxx                     No Lot....:xxxx Transférée: x        *
*   Nom client.:xxxxxxxxxxxxxxxxxxxx      Langue....:x    Devise....: xxx      *
*   Adr.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Conditions:xxxxxxxxxxxxxxxxxxxxxxxxx *
*   Fact: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             xxxxxxxxxxxxxxxxxxxxxxxxx *
*         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Code post: xxxxxxx                   *
*   Commentaires: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*  No bloc   Qte     Dimensions      Tot m3      FOB        FAS        Total   *
*            bloc  Long Haut Larg                                              *
*   xxx-xxxxx x     xxx x xxx x xxx   xxxxxxxxxxxxxxxx    xxxxxxx    xxxxxxxxx *
*  Ajustements:     xxx x xxx x xxx   xxxxxxxxxxxxxxxx    xxxxxxx    xxxxxxxxx *
*                                    __________                     __________ *
*                                    xxxxxxxxx                      xxxxxxxxxx *
*                                        TPS xxxxxxx    xxxxxxxxxxx xxxxxxxxxx *
*   No exemp. TX prov. : xxxxxxxxxxxxxxx TVQ xxxxxxx    xxxxxxxxxxx __________ *
*                                                           Total : xxxxxxxxxx *
*   Credit pour bloc qui redevient disponible O/N:  xx                         *
********************************************************************************
@ENDIF
