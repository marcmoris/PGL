;/T/ Ages des comptes à recevoir sommaire/détaillé
;
;/P/ Programmeur...: Alain Côté
;    Date Création.: 27 juillet 1993
;
;    Description...: Age des comptes à recevoir de type comptable, sert à avoir
;                    une vision global des soldes à recevoir et de balancer
;                    le compte ctb à recevoir avec le Grand Livre.
;
;                    Les transactions sont sélectionnées selon la période
;                    demandée par l'usager, les soldes sont répartie dans la
;                    bonne colonne selon la date due et la date de fin de
;                    période.
;
;                    Les transactions doivent être journalisées.
;
;    Paramètres....: Compte ctb à recevoir  un, plusieur
;                    Client                 Une, plusieurs ou tous
;                    Periode Comptable      Jusqu'à
;
;
;    Particularités: Le QTP sert à préparer un sous-fichier contenant les
;                    factures qui restent un solde.
;
;/M/ Programmeur...: Martin Bruyère
;    Date modif....: 25 juin 1996
;    Description...: Modifier selon demande du client (GRA01 9311 279)
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST

RUN cr321t


GLOBAL TEMPORARY G_DATFIN    DATE PARM        ; Date de fin.
GLOBAL TEMPORARY G_ITVCOD    CHARACTER SIZE 1 PARM
                                              ; Code identifiant l'interval
                                              ; choisi pour les colonnes.


;-------------------------------------------------------------------------------

REQUEST AGES_COMPTE_CAR

ACCESS CRCPT_A_REC &
  ;
  ; Client de la facture, pour avoir la catégorie...
  ;
  LINK REFCIECLE OF CRCPT_A_REC, &
       REFCLENUM OF CRCPT_A_REC  &
    TO CLICIECLE, &
       CLICLE OF                 CRCLIENT &
  ;
  ; Pour avoir le nom et le téléphone du contact client.
  ;
  LINK CLICIECLE OF CRCLIENT, &
       CLICLE    OF CRCLIENT, &
       1                      &
    TO CLICIECLE , &
       CLICLE    , &
       CCTCLE OF CRCLI_CONTACT OPTIONAL &
  ;
  ; Limite de crédit du client.
  ;
  LINK CLICIECLE OF CRCLIENT, &
       CLICLE    OF CRCLIENT, &
       CIECLE    OF CRCPT_A_REC &
    TO CLICIECLE , &
       CLICLE    , &
       CIECLE OF CRCLI_CIE &
  ;
  ; Nom du client/divers.
  ;
  LINK REFCIECLE OF CRCPT_A_REC, &
       REFCLETYP OF CRCPT_A_REC, &
       REFCLENUM OF CRCPT_A_REC, &
       RADCLE    OF CRCPT_A_REC  &
    TO REFCIECLE , &
       REFCLETYP , &
       REFCLENUM , &
       RADCLE OF                 MCREF_ADRESSE OPTIONAL &
 ;
 ; Cédule d'encaissement, date due, montant à recevoir.
 ;
 LINK CIECLE OF CRCPT_A_REC, &
      CARCLE OF CRCPT_A_REC  &
   TO CIECLE , &
      CARCLE OF                  CRCAR_CEDULE &
 ;
 ; Transactions associées à la cédule(ajust., encaissement...)
 ;
 LINK CIECLE    OF CRCAR_CEDULE, &
      CARCLE    OF CRCAR_CEDULE, &
      CCDCLELIG OF CRCAR_CEDULE  &
   TO CIECLE , &
      CARCLE , &
      CCDCLELIG OF               CRCAR_HISTO OPTIONAL


CHOOSE CIECLE    PARM, &  ; Compagnie du menu.
       CARCPTCAR PARM, &  ; Compte ctb à recevoir.
       REFCLENUM PARM     ; Numéro de clien.


SELECT CRCPT_A_REC IF     (    CARTYPECR OF CRCPT_A_REC = "FA" &
                            OR CARTYPECR OF CRCPT_A_REC = "CR" &
                            OR CARTYPECR OF CRCPT_A_REC = "NA" ) &
                      AND CARDAT    OF CRCPT_A_REC <= G_DATFIN &
                      AND CARDATJOU OF CRCPT_A_REC <> 0


SELECT CRCAR_HISTO IF     CRHDATJOU OF CRCAR_HISTO <> 0 &
                      AND CRHDAT    OF CRCAR_HISTO <= G_DATFIN


TEMPORARY T_CRHMNT  FLOAT SIZE 8 ; Sommation des trs. associées.
TEMPORARY T_SLD_TOT FLOAT SIZE 8 ; Solde de la ligne de cédule.
TEMPORARY T_TOT_REF FLOAT SIZE 8 ; Solde total pour le client


SORT ON CIECLE    OF CRCPT_A_REC , & ; No compagnie
        CARCPTCAR OF CRCPT_A_REC , & ; Compte ctb à recevoir
        REFCLENUM OF CRCPT_A_REC , & ; Numéro de client
        CARCMDCLI OF CRCPT_A_REC , & ; Num. ref. client (PROJET) <-- MB960625
        CARDAT    OF CRCPT_A_REC , & ; Date de la facture
        CARCLE    OF CRCPT_A_REC , & ; No facture
        CCDCLELIG OF CRCAR_CEDULE    ; No Cédule

;
; Si c'est un encaissement ou une mauvaise créance il faut soustraire
; le montant du montant brut.
;
ITEM T_CRHMNT = 0 - CRHMNTCAR OF CRCAR_HISTO &
                         IF    CRHTYPTRA OF CRCAR_HISTO = "EN" &
                            OR CRHTYPTRA OF CRCAR_HISTO = "MC" &
           ELSE CRHMNTCAR OF CRCAR_HISTO

;
; Solde de la cédule.
;
ITEM T_SLD_TOT SUBTOTAL T_CRHMNT &
               RESET AT CCDCLELIG TO CCDMNT OF CRCAR_CEDULE

ITEM T_TOT_REF SUBTOTAL T_SLD_TOT AT CCDCLELIG &
               RESET AT REFCLENUM

SUBFILE WCR321 KEEP &
               AT CCDCLELIG &
               IF T_SLD_TOT <> 0 &
INCLUDE CIECLE    OF CRCPT_A_REC   , &
        CARCLE    OF CRCPT_A_REC   , &
        CARCPTCAR OF CRCPT_A_REC   , &
        CARDAT    OF CRCPT_A_REC   , &
        REFCLENUM OF CRCPT_A_REC   , &
        CARMNT    OF CRCPT_A_REC   , & ; <-- MB960625
        CARCMDCLI OF CRCPT_A_REC   , & ; <-- MB960625
        RADTEL    OF MCREF_ADRESSE , &                     ; <-- SEBCHA960910
        RADNOM    OF MCREF_ADRESSE , &
        RADNOMABR OF MCREF_ADRESSE , &
        CLISTU    OF CRCLIENT      , &
        CLICAT    OF CRCLIENT      , &
        CCTNOM    OF CRCLI_CONTACT , &
        CCTTEL    OF CRCLI_CONTACT , &
        CLCLIMCRT OF CRCLI_CIE     , &
        CCDCLELIG OF CRCAR_CEDULE  , &
        CCDDATDUE OF CRCAR_CEDULE  , &
        G_DATFIN                   , &
        T_SLD_TOT                  , &
        T_TOT_REF                  , &
        G_ITVCOD


BUILD cr321t
