;/T/ 2.3.3 Planifier la production.
;
;/M/ Programmeur..: Claudie Doyon 1999-06-07
;    État.........: Terminer
;    Description..: J'ai placer la procedure input du champs
;                   dianum002 en commentaire pour corriger le
;                   problème de calcul de des quantités planifiés.
;                   Référence demande : 636.
;
;
;/M/ Programmeur..: Claudie Doyon 1999-12-06
;    Description..: Mis une partie du select en commentaire pour qu'il sélectionne les données
;                   même si les qtes. sont à zéro.
;
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-13
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   la production ( exclut le sciage).
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd212  ACTIONBAR                    &
              MODE LABEL "" AT 2,132       &
              NOACTION                     &
              FROM 1,1 TO 23,132           &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD212"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd212.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Priorité
;

FILE PDPRIORITE PRIMARY &
                NOITEM &
                NODELETE NOAPPEND

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 COMNUMCOMINT,    &
                 PRICODPRI        &
         USING   T_CIECLEMNU,                    &
                 PJTABR           OF PDPRIORITE, &
                 COMNUMCOMINT     OF PDPRIORITE, &
                 PRICODPRI        OF PDPRIORITE  &
         REQUEST PJTABR           OF PDPRIORITE, &
                 COMNUMCOMINT     OF PDPRIORITE, &
                 PRICODPRI        OF PDPRIORITE  &
         ORDERBY CIECLE,          &
                 PJTANN,          &
                 PJTABR,          &
                 COMNUMCOMINT,    &
                 PRICODPRI

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                    &
                 PJTABR           OF PDPRIORITE, &
                 COMNUMCOMINT     OF PDPRIORITE  &
         REQUEST PJTABR           OF PDPRIORITE, &
                 COMNUMCOMINT     OF PDPRIORITE  &
         ORDERBY CIECLE,          &
                 PJTANN,          &
                 PJTABR,          &
                 COMNUMCOMINT,    &
                 PRICODPRI

  ACCESS VIA     CIECLE,          &
                 PJTABR           &
         USING   T_CIECLEMNU,                    &
                 PJTABR           OF PDPRIORITE  &
         REQUEST PJTABR           OF PDPRIORITE  &
         ORDERBY CIECLE,          &
                 PJTANN,          &
                 PJTABR,          &
                 COMNUMCOMINT,    &
                 PRICODPRI

  ACCESS VIA     CIECLE         &
         USING   T_CIECLEMNU    &
         ORDERBY CIECLE,        &
                 PJTANN,        &
                 PJTABR,        &
                 COMNUMCOMINT,  &
                 PRICODPRI

  ITEM CIECLE OF PDPRIORITE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Priorité diagramme (Priorite MAJ)
;

FILE PDPRIORITE_DIAG DETAIL OCCURS 8 TIMES &
                            ALIAS A_PRD_PRIO_MAJ &
                            NOITEM &
                            TRANSACTION UPDATE

  ACCESS VIA     CIECLE,          &
                 COMNUMCOM,       &
                 PRICODPRI        &
         USING   T_CIECLEMNU,                    &
                 COMNUMCOM        OF PDPRIORITE, &
                 PRICODPRI        OF PDPRIORITE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DIANUMDIA002

  SELECT IF PDGSTU OF A_PRD_PRIO_MAJ = "A"
          ;  AND &
          ;  ( &
          ;  PDGQTEPLACPEGRA  OF A_PRD_PRIO_MAJ <> 0 &
          ;  OR &
          ;  PDGQTEPLAFINGRA  OF A_PRD_PRIO_MAJ <> 0 &
          ;  OR &
          ;  PDGQTEPLACPEUSC  OF A_PRD_PRIO_MAJ <> 0 &
          ;  OR &
          ;  PDGQTEPLAFINUSC  OF A_PRD_PRIO_MAJ <> 0 &
          ;  OR &
          ;  PDGQTEPLACPESCT  OF A_PRD_PRIO_MAJ <> 0 &
          ;  OR &
          ;  PDGQTEPLAFINSCT  OF A_PRD_PRIO_MAJ <> 0 &
          ;  )


  ITEM CIECLE           OF A_PRD_PRIO_MAJ INITIAL T_CIECLEMNU
  ITEM PDGSTU           OF A_PRD_PRIO_MAJ INITIAL "A"

  ITEM COMNUMCOMINT     OF A_PRD_PRIO_MAJ INITIAL COMNUMCOMINT     OF PDPRIORITE FIXED
  ITEM COMNUMCOM        OF A_PRD_PRIO_MAJ INITIAL COMNUMCOM        OF PDPRIORITE FIXED
  ITEM PJTABR           OF A_PRD_PRIO_MAJ INITIAL PJTABR           OF PDPRIORITE FIXED
  ITEM PJTANN           OF A_PRD_PRIO_MAJ INITIAL PJTANN           OF PDPRIORITE FIXED
  ITEM PJTNUMSEQ        OF A_PRD_PRIO_MAJ INITIAL PJTNUMSEQ        OF PDPRIORITE FIXED

  ITEM PJTNUMPRO        OF A_PRD_PRIO_MAJ INITIAL PJTABR           OF PDPRIORITE + &
                                                  PJTANN           OF PDPRIORITE + &
                                                  PJTNUMSEQ        OF PDPRIORITE FIXED

  ITEM PRICODPRI        OF A_PRD_PRIO_MAJ INITIAL PRICODPRI        OF PDPRIORITE


;-------------------------------------------------------------------------------
;
; Priorité diagramme (Priorite 99)
;

FILE PDPRIORITE_DIAG SECONDARY OCCURS WITH A_PRD_PRIO_MAJ &
                               ALIAS A_PRD_PRIO_DEF &
                               NOITEM &
                               TRANSACTION UPDATE NODELETE

  ACCESS VIA     CIECLE,          &
                 COMNUMCOM,       &
                 DIANUM002,       &
                 PRICODPRI        &
         USING   T_CIECLEMNU,                        &
                 COMNUMCOM        OF A_PRD_PRIO_MAJ, &
                 DIANUM002        OF A_PRD_PRIO_MAJ, &
                 "99"

  ITEM CIECLE           OF A_PRD_PRIO_MAJ   INITIAL T_CIECLEMNU
  ITEM PDGQTEPLACPEGRA  OF A_PRD_PRIO_MAJ   SUM NEGATIVE INTO PDGQTEPLACPEGRA  OF A_PRD_PRIO_DEF
  ITEM PDGQTEPLAFINGRA  OF A_PRD_PRIO_MAJ   SUM NEGATIVE INTO PDGQTEPLAFINGRA  OF A_PRD_PRIO_DEF


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE REFERENCE

  ACCESS VIA     CIECLE,          &
                 COMNUMCOM        &
         USING   T_CIECLEMNU,     &
                 COMNUMCOM        OF PDPRIORITE


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME REFERENCE OCCURS WITH A_PRD_PRIO_MAJ

  ACCESS VIA     CIECLE,          &
                 DIANUM002        &
         USING   T_CIECLEMNU,     &
                 DIANUM002        OF A_PRD_PRIO_MAJ


;-------------------------------------------------------------------------------
;
; Projet
;

FILE PDPROJET REFERENCE

  ACCESS VIA      CIECLE,             &
                  PJTABR              &
         USING    T_CIECLEMNU,        &
                  PJTABR          OF PDPRIORITE


;-------------------------------------------------------------------------------
;
;

USE usdrwecr132 NOLIST ; Draw pour les écrans pleine page 132
USE ustitstd132 NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Planification de la production " &
@ELSE
      " %%%Planification de la production " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (2,3,19) (21,21,22) (24,24,25)


FIELD PJTABR           OF PDPRIORITE       &
        HIDDEN ID 05                       &
        REQUIRED                           &
        LABEL "Num. commande.:"


FIELD PJTANN           OF PDPRIORITE       &
        HIDDEN ID SAME                     &
        OMIT ON ENTRY                      &
        LABEL "-"                          &
        NUMERIC


FIELD COMNUMCOMINT     OF PDPRIORITE       &
        HIDDEN ID SAME                     &
        REQUIRED                           &
        LABEL "-"                          &
        NUMERIC                            &
        LOOKUP ON PDCOMMAN_INTERNE         &
          VIA   CIECLE,                    &
                PJTABR ,                   &
                PJTANN ,                   &
                COMNUMCOMINT               &
          USING T_CIECLEMNU,                &
                PJTABR       OF PDPRIORITE, &
                PJTANN       OF PDPRIORITE, &
                COMNUMCOMINT OF PDPRIORITE  &
          MESSAGE 3081 ; Ce numéro de commande n'existe pas


ALIGN (,3,19) (,,26)


FIELD PJTABR           OF PDPROJET         &
label "Projet........:"&
        HIDDEN ID 15                       &
        DISPLAY


FIELD PJTNOM           OF PDPROJET         &
        DISPLAY


DRAW 07,2 TO 07,131


SKIP TO LINE 7


TITLE &
@IF FRANCAIS
      " Priorité " &
@ELSE
      " %%%Priorité " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 8


ALIGN (2,3,19)


FIELD PRICODPRI        OF PDPRIORITE       &
label "Code priorité.:"&
        HIDDEN ID 20                       &
        REQUIRED                           &
        NUMERIC                            &
        LOOKUP ON PDPRIORITE               &
          VIA     CIECLE,                  &
                  COMNUMCOM,               &
                  PRICODPRI                &
          USING   T_CIECLEMNU,                    &
                  COMNUMCOM        OF PDPRIORITE, &
                  PRICODPRI        OF PDPRIORITE  &
          MESSAGE 2978 ; Ce code de priorité n'existe pas


ALIGN (,3,19)


FIELD PRIDSC           OF PDPRIORITE       &
label "Description...:"&
        DISPLAY


FIELD PRIDATFIN        OF PDPRIORITE        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date fin......:"&
        DISPLAY


DRAW 11,2 TO 11,131


SKIP TO LINE 11

TITLE &
@IF FRANCAIS
      " Diagramme " &
@ELSE
      " %%%Diagramme " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 12


TITLE &
@IF FRANCAIS
      "                     *------------------ Coupe ------------------*     *----------------- Finition ----------------* " &
@ELSE
      "%%%                  *------------------ Coupe ------------------*     *----------------- Finition ----------------* " &
@ENDIF
      AT ,2


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
      "           Diagr.    Compagnie       Usine sec       Sous-trait        Compagnie       Usine sec       Sous-trait    " &
@ELSE
      "%%%        Diagr.    Compagnie       Usine sec       Sous-trait        Compagnie       Usine sec       Sous-trait    " &
@ENDIF
      AT ,2


SKIP TO LINE 14


TITLE &
@IF FRANCAIS
      "                     Plan.   Prod.   Plan.    Reçu   Plan.    Reçu     Plan.   Prod.   Plan.    Reçu   Plan.    Reçu " &
@ELSE
      "%%%                  Plan.   Prod.   Plan.    Reçu   Plan.    Reçu     Plan.   Prod.   Plan.    Reçu   Plan.    Reçu " &
@ENDIF
      AT ,2


SKIP TO LINE 15


ALIGN (11,12,13) (,,15) (,,23) (,,31) (,,39) (,,47) (,,55) (,,63) (,,73) (,,81) (,,89) (,,97) (,,105) (,,113)


CLUSTER OCCURS WITH A_PRD_PRIO_MAJ


FIELD DIANUM002        OF A_PRD_PRIO_MAJ   &
        HIDDEN ID 30                       &
        LABEL " "                          &
        REQUIRED                           &
        NOCHANGE                           &
        LOOKUP NOTON A_PRD_PRIO_MAJ        &
          VIA     CIECLE,                  &
                  COMNUMCOM,               &
                  PRICODPRI,               &
                  DIANUM002                &
          USING   T_CIECLEMNU,                       &
                  COMNUMCOM        OF PDPRIORITE,    &
                  PRICODPRI        OF PDPRIORITE,    &
                  DIANUM002        OF A_PRD_PRIO_MAJ &
          MESSAGE 2997 & ; Ce numéro de diagramme existe déjà
              ,ON PDDIAGRAMME              &
          VIA     CIECLE,                  &
                  DIANUM002                &
          USING   T_CIECLEMNU,                       &
                  DIANUM002        OF A_PRD_PRIO_MAJ &
          MESSAGE 2998 ; Ce numéro de diagramme n'existe pas


FIELD PDGQTEPLACPEGRA  OF A_PRD_PRIO_MAJ   &
        ID SAME                            &
        REQUIRED size 5


FIELD PDGQTECPEPRDGRA  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEPLACPEUSC  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTECPERCUUSC  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEPLACPESCT  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTECPERCUSCT  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEPLAFINGRA  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEFINPRDGRA  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEPLAFINUSC  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEFINRCUUSC  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEPLAFINSCT  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


FIELD PDGQTEFINRCUSCT  OF A_PRD_PRIO_MAJ   &
        DISPLAY size 5


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Assigne la valeur pour affichage du champ
;

PROCEDURE PROCESS PJTABR OF PDPRIORITE
BEGIN
  LET     PJTANN OF PDPRIORITE = PJTANN OF PDPROJET
  DISPLAY PJTANN OF PDPRIORITE
END


;-------------------------------------------------------------------------------
;
; Initialise les champs pour l'enregistrement PDPRIORITE
;

PROCEDURE PROCESS COMNUMCOMINT OF PDPRIORITE
BEGIN
  LET COMNUMCOM OF PDPRIORITE = PJTABR OF PDPRIORITE + &
                                PJTANN OF PDPRIORITE + &
                                COMNUMCOMINT OF PDPRIORITE
END


;PROCEDURE INPUT DIANUM002
;BEGIN
;  IF 5 >= SIZE(TRUNCATE(FIELDTEXT)) &
;     AND &
;     0 = INDEX(FIELDTEXT,"@") &
;     AND &
;     NOT FINDMODE
;  THEN BEGIN
;    LET FIELDTEXT = PJTABR OF PDPRIORITE + &
;                    TRUNCATE(FIELDTEXT[1:SIZE(TRUNCATE(FIELDTEXT))+1])
;  END
;END

;-------------------------------------------------------------------------------
;
; Initialise les champs de l'enregistrement A_PRD_PRIO_MAJ
;

PROCEDURE PROCESS DIANUM002 OF A_PRD_PRIO_MAJ
BEGIN
  LET PJTABR OF A_PRD_PRIO_MAJ       = DIANUM002 OF A_PRD_PRIO_MAJ[1:2]
  LET DIANUMDIA002 OF A_PRD_PRIO_MAJ = DIANUM002 OF A_PRD_PRIO_MAJ[3:5]
  LET DCINUMLIG OF A_PRD_PRIO_MAJ    = DCINUMLIG OF PDDIAGRAMME
  ;
  ; recherche l'occurence dans la table
  ;
  GET A_PRD_PRIO_DEF         &
    VIA     CIECLE,          &
            COMNUMCOM,       &
            DIANUM002,       &
            PRICODPRI        &
    USING   T_CIECLEMNU,                        &
            COMNUMCOM        OF PDPRIORITE,     &
            DIANUM002        OF A_PRD_PRIO_MAJ, &
            "99"
END


;-------------------------------------------------------------------------------
;
; Empèche de modifier la priorité 99 par l'utilisateur
;

PROCEDURE EDIT PDGQTEPLACPEGRA  OF A_PRD_PRIO_MAJ
BEGIN
  IF PRICODPRI OF A_PRD_PRIO_MAJ = "99"
  THEN BEGIN
    ERROR 3227; Cette priorité n'est pas modifiable
  END
END


;-------------------------------------------------------------------------------
;
; Calcul la différence entre l'ancienne quantité et la nouvelle quantité
;

PROCEDURE PROCESS PDGQTEPLACPEGRA  OF A_PRD_PRIO_MAJ
BEGIN
  IF 0 > PDGQTEPLACPEGRA  OF A_PRD_PRIO_DEF
  THEN BEGIN
    ERROR 3101 ; La quantité ne doit pas être suppérieur à la quantité commandé
  END
  LET PDGQTEPLAFINGRA  OF A_PRD_PRIO_MAJ     = PDGQTEPLACPEGRA  OF A_PRD_PRIO_MAJ
  DISPLAY PDGQTEPLAFINGRA OF A_PRD_PRIO_MAJ
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDPRIORITE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDPRIORITE &
     AND NOT NEWRECORD     OF PDPRIORITE &
     AND NOT DELETEDRECORD OF PDPRIORITE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD

@IF FORMAT

xxxx                                           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                            x
************************************************** Planification de la production **************************************************
* Num. commande.: xx-xx-xxx                                                                                                        *
* Projet........: xx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                  *
*                                                                                                                                  *
************************************************************* Priorité *************************************************************
* Code priorité.: xx                                                                                                               *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                         *
* Date fin......: xxxxxxxx                                                                                                         *
************************************************************ Diagramme *************************************************************
*                      *------------------ Coupe ------------------*   *----------------- Finition ----------------*               *
*            Diagr.    Compagnie       Usine sec       Sous-trait      Compagnie        Usine sec       Sous-trait                  *
*                      Plan    Prod    Plan    Recu    Plan    Recu    Plan    Prod    Plan    Recu    Plan    Recu                *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
*            xxxxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx   xxxxx               *
************************************************************************************************************************************
@ENDIF
