;/T/ Gestion des unités administratives.
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 1 avril 1993
;
;    Description..: Cet écran permet de gérer les unités administratives.
;
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc008 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP  &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIENOM

USE ussectmp NOLIST
    "MC008"

USE mc008.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding

;-------------------------------------------------------------------------------
;
;
FILE MCUNITE_ADM      PRIMARY NOITEMS
  ACCESS REQUEST CIECLE OF MCUNITE_ADM, &
                 UNACLE OF MCUNITE_ADM  &
         VIA     CIECLE, &
                 UNACLE &
         USING   CIECLE OF MCUNITE_ADM, &
                 UNACLE OF MCUNITE_ADM &
         ORDERED

  ACCESS REQUEST CIECLE OF MCUNITE_ADM &
         VIA     CIECLE &
         USING   CIECLE OF MCUNITE_ADM &
         ORDERBY CIECLE, &
                 UNACLE

  ACCESS SEQUENTIAL &
         ORDERBY CIECLE, &
                 UNACLE

;
; Validation du centre
;
FILE MCCENTRE         REFERENCE
  ACCESS VIA   CENCLE &
         USING CENCLE OF MCUNITE_ADM

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      REFERENCE
  ACCESS VIA   CIECLE &
         USING CIECLE OF MCUNITE_ADM


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_CIECLE CHARACTER SIZE 4 ; Popup des compagnie
TEMPORARY T_CENCLE CHARACTER SIZE 4 ; Popup des centres

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Unités administratives " &
@ELSE
      " Administrative Units   " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19) (,,25)
FIELD CIECLE      OF MCUNITE_ADM HIDDEN ID 05 &
        REQUIRED NOCHANGE &
        LOOKUP ON MCCOMPAGNIE &
          MESSAGE 120 ; Cette compagnie n'existe pas.

FIELD CIENOM      OF MCCOMPAGNIE &
        DISPLAY

FIELD UNACLE      OF MCUNITE_ADM HIDDEN ID 10 &
        REQUIRED NOCHANGE &
        LOOKUP NOTON MCUNITE_ADM &
          VIA UNACLE, &
              CIECLE  &
          USING UNACLE OF MCUNITE_ADM, &
                CIECLE OF MCUNITE_ADM &
          MESSAGE 121

SKIP 1

FIELD CENCLE      OF MCUNITE_ADM HIDDEN ID 15 &
        REQUIRED  &
        LOOKUP ON MCCENTRE &
          MESSAGE 108 ;Ce centre n'est pas définie

FIELD CENNOM      OF MCCENTRE    &
        DISPLAY

SKIP 1
ALIGN(3,3,19)
FIELD UNANOM      OF MCUNITE_ADM HIDDEN ID 20
FIELD UNANOMABR   OF MCUNITE_ADM HIDDEN ID 25 &
        DEFAULT UNANOM OF MCUNITE_ADM

SKIP 1
FIELD UNAADR1     OF MCUNITE_ADM HIDDEN ID 30
FIELD UNAADR2     OF MCUNITE_ADM HIDDEN ID SAME NOLABEL
FIELD UNAADR3     OF MCUNITE_ADM HIDDEN ID SAME NOLABEL
FIELD UNAADR4     OF MCUNITE_ADM HIDDEN ID SAME NOLABEL
FIELD UNACP       OF MCUNITE_ADM HIDDEN ID SAME

ALIGN(3,3,19) (40,40,56)
FIELD UNATEL      OF MCUNITE_ADM HIDDEN ID 35
FIELD UNAFAX      OF MCUNITE_ADM HIDDEN ID SAME

ALIGN(3,3,19)
FIELD UNATLX      OF MCUNITE_ADM HIDDEN ID 40

ALIGN(3,3,19) (,,30)
FIELD UNAEMPNOM   OF MCUNITE_ADM HIDDEN ID 45

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT CIECLE
BEGIN
  ;
  ; Popup sur les compagnies.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les centres
;
;
PROCEDURE INPUT CENCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CENCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc114 PASSING T_CENCLE MODE F
    LET FIELDTEXT = TRUNC(T_CENCLE)
  END
END

;-------------------------------------------------------------------------------
;
; Cette procedure formate le code postal.
;
;
PROCEDURE INPUT UNACP
BEGIN
  USE uscpfmt NOLIST  ;Fomate le code postal.
END

;-------------------------------------------------------------------------------
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT UNATEL
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END

;-------------------------------------------------------------------------------
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT UNAFAX
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF MCUNITE_ADM &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF MCUNITE_ADM &
     AND NOT NEWRECORD     OF MCUNITE_ADM &
     AND NOT DELETEDRECORD OF MCUNITE_ADM &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction d'une unité administrative.
  ;
  IF DELETEDRECORD OF MCUNITE_ADM
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si l'unité adm. est référée.
      ;
      RUN SCREEN mc008x MODE SAME &
                        PASSING MCUNITE_ADM , &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Unités administratives ****************************
*                                                                              *
* Compagnie.....: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Unité adm.....: xxxxxxxx                                                     *
*                                                                              *
* Centre........: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Code postal...: xxxxxxx                                                      *
* Téléphone.....: xxxxxxxxxxxxx        Fax...........: xxxxxxxxxxxxx           *
* Télex.........: xxxxxxxx                                                     *
* Responsable...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
