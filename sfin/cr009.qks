;/T/ Mauvaise créance
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 29 juin 1993
;
;    Description..: Cet écran permet traite que les radiations de mauvaise
;                   créance. La provision de mauvaise créance n'est pas
;                   supportée.
;
;                   il y a deux types de mauvaise créance, soit manuel ou
;                   soit automatique.
;
;                   Le type automatique ne permet aucune saisie d'imputation
;                   celle-ci généré à la journalisation. On crédite le compte
;                   à recevoir du client et on débite le compte de mauvaise
;                   créance définie dans le plan comptable.
;
;                   Le type manuel permet de saisir une imputation au débit,
;                   le crédit étant de CAR du client.
;
;-------------------------------------------------------------------------------
;**M Alain Côté, le 23 juin 1993
;**M
;**M Maintenance des champs modifiables après journalisation dans l'historique
;**m du grand livre.
;**m
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr009 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_PECCLEMNU, &
                       T_PECCLECOU, &
                       T_DPOCLEFND

TEMPORARY T_PECCLECOU_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_FIELDTEXT_SIE CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle


USE ussectmp  NOLIST
    "CR009"

USE cr009.hlp NOLIST

USE ustraecr NOLIST

USE usactecr NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Imputation                      " ACTION DESIGNER D001
  MENUITEM LABEL "Ventilation de factures         " ACTION DESIGNER D002
  MENUITEM LABEL "Ecriture comptable              " ACTION DESIGNER D003
  MENUITEM LABEL "Gestion des lots                " ACTION DESIGNER D004
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D005
@ELSE
ACTIONMENU LABEL "Subscreens"
  MENUITEM LABEL "Charge                          " ACTION DESIGNER D001
  MENUITEM LABEL "Invoice breakdown               " ACTION DESIGNER D002
  MENUITEM LABEL "Accounting entry                " ACTION DESIGNER D003
  MENUITEM LABEL "Batch verification              " ACTION DESIGNER D004
  MENUITEM LABEL "Verification                    " ACTION DESIGNER D005
@ENDIF


;-------------------------------------------------------------------------------
;
TEMPORARY T_CLICIECLE CHARACTER SIZE 4 ; Compagnie de la charte client
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Nom de la compagnie du menu.
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4 ; Période saisie au menu
TEMPORARY T_PECCLECOU CHARACTER SIZE 4 ; Période courante
TEMPORARY T_DPOCLEFND CHARACTER SIZE 12 ; Clé de recherche


;-------------------------------------------------------------------------------

;
; Les mauvaise créance sont saisie dans la relation CRDEPOT.
;
FILE CRDEPOT          PRIMARY NOITEM
  ;
  ; La procédure PATH et FIND on été écrite pour que l'écran soit appelé
  ; directement par l'analyse de compte.
  ;
  ;
  ; On sélectionne que les mauvaise créance, car la relation CRDEPOT contient
  ; aussi les encaissements.
  ;
  SELECT IF DPOTYPECR OF CRDEPOT = "MC"

  ITEM CIECLE    OF CRDEPOT INITIAL T_CIECLEMNU
  ITEM REFCLETYP OF CRDEPOT INITIAL "C"
  ITEM REFCIECLE OF CRDEPOT INITIAL T_CLICIECLE
  ITEM DPOTYPECR OF CRDEPOT INITIAL "MC"
  ITEM DPODATCRE OF CRDEPOT INITIAL SYSDATE
  ITEM DPOHRECRE OF CRDEPOT INITIAL SYSTIME / 10000
  ITEM DPOUSRCRE OF CRDEPOT INITIAL LOGONID
  ITEM DPOMNTENC OF CRDEPOT FINAL   DPOMNTPAM OF CRDEPOT
  ITEM DPOMNTFAC OF CRDEPOT FINAL   DPOMNTPAM OF CRDEPOT
  ;
  ; Le timbre de modification est mis à jour si l'usager modifie la relation
  ; CRDEPOT.
  ;
  ITEM DPODATMOD OF CRDEPOT FINAL   SYSDATE         &
                                        IF NOT NEWRECORD OF CRDEPOT AND &
                                           ALTEREDRECORD OF CRDEPOT
  ITEM DPOHREMOD OF CRDEPOT FINAL   SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF CRDEPOT AND &
                                           ALTEREDRECORD OF CRDEPOT
  ITEM DPOUSRMOD OF CRDEPOT FINAL   LOGONID         &
                                        IF NOT NEWRECORD OF CRDEPOT AND &
                                           ALTEREDRECORD OF CRDEPOT


;
; Destruction des fils lors de la destruction du père.
;
FILE CRDPO_IMPUTATION DELETE &
                      NOITEM
  ACCESS VIA CIECLE, &
             DPOCLE  &
         USING CIECLE OF CRDEPOT, &
               DPOCLE OF CRDEPOT
;
FILE CRCAR_HISTO      DELETE &
                      NOITEM
  ACCESS VIA CRHCLE1, &
             CRHCLE2, &
             CRHTYPECR &
         USING CIECLE    OF CRDEPOT, &
               DPOCLE    OF CRDEPOT, &
               DPOTYPECR OF CRDEPOT

;
; Numéro séquentiel des mauvaises créance, il est composé du numéro de client
; plus une séquence.
;
FILE CRDEPOT          DESIGNER ALIAS A_DPO_SEQ &
                      OPEN READ SHARE

;
; Validation de la période comptable.
;
FILE MCPERIODE_CTB    REFERENCE
  ACCESS VIA PECCLE &
         USING PECCLE OF CRDEPOT

;
; Validation du lot.
;
FILE CRLOT            REFERENCE
  ACCESS VIA CIECLE, &
             PECCLE, &
             LCRCLE &
         USING CIECLE OF CRDEPOT, &
               PECCLE OF CRDEPOT, &
               LCRCLE OF CRDEPOT

;
; Validation du numéro de client et on a besoin de certaines informations sur
; le client , comme le type, son statut...
;
FILE VCLC_CLI         REFERENCE
  ACCESS VIA CLICIECLE, &
             CLICLE   , &
             CIECLE   , &
             CLIREFTYP  &
         USING REFCIECLE OF CRDEPOT, &
               REFCLENUM OF CRDEPOT, &
               CIECLE    OF CRDEPOT, &
               REFCLETYP OF CRDEPOT

;
; Validation du type de mauvaise créance.
;
DEFINE    D_DPOTYPMAC CHARACTER SIZE 16 = "DPOTYPMAC"
TEMPORARY T_DPOTYPMAC CHARACTER SIZE  4
;
FILE VCBI_DSC         ALIAS A_CBI_DPOTYPMAC &
                      REFERENCE
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_DPOTYPMAC         , &
               DPOTYPMAC OF CRDEPOT

;-------------------------------------------------------------------------------
;

TEMPORARY T_LCRCLE    CHARACTER SIZE  4 ; Popup sur les lots
TEMPORARY T_CODMOD    CHARACTER SIZE  2 ; Popup sur les périodes
TEMPORARY T_PECCLE    CHARACTER SIZE  4 ; Popup sur les périodes et lots
TEMPORARY T_TYPECR    CHARACTER SIZE  2 ; Popup sur les lots

TEMPORARY T_REFCIECLE  CHARACTER SIZE  4 ; Popup sur les clients.
TEMPORARY T_REFCLENUM  CHARACTER SIZE  6 ; Popup sur les clients.
TEMPORARY T_REFCLETYP2 CHARACTER SIZE  1 ; Popup sur les clients.
TEMPORARY T_REFSTUPAT  CHARACTER SIZE  5 ; Popup sur les clients.

;
; Variable pour le sous-écran de consultation des écritures comptable
;
DEFINE D_HISCLE INTEGER UNSIGNED SIZE 4 = HISCLE OF CRDEPOT

;
; Si le lot est autorisé, sert au sous-écrans.
;
DEFINE D_LCRATRJOU  CHARACTER SIZE 1 = LCRATRJOU OF CRLOT
;
; Devise du client, sert au sous-écran CR009A.
;
DEFINE D_REFDEVCLE  CHARACTER SIZE 3 = DEVCLE OF VCLC_CLI
;
; Pour afficher le nom du client ou celui saisie dans le cas d'un client
; divers. Sert dans les sous-écran pour la même raison.
;
DEFINE D_DPONOM CHARACTER SIZE 40 = CLINOM OF VCLC_CLI &
                                    IF CLITYP    OF VCLC_CLI <> "D" &
                               ELSE DPONOM OF CRDEPOT

;
; Définition de l'en-tête, # compagnie et nom de la compagnie.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Mauvaises créances " &
@ELSE
      " Bad debt write-off " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

ALIGN(3,3,19) (,27,43) (,49,64) (,,75)

FIELD PECCLE OF CRDEPOT HIDDEN ID 05 &
label "Période ......:"&
        DEFAULT T_PECCLEMNU &
        DUPLICATE &
        NOCHANGE &
        LOOKUP ON MCPERIODE_CTB &
          MESSAGE 606 ; Cette periode est inexistante

FIELD LCRCLE    OF CRDEPOT &
label "Lot...........:"&
        REQUIRED  &
        DUPLICATE &
        NOCHANGE  &
        NUMERIC   &
        SIGNIFICANCE 4 &
        LOOKUP ON CRLOT &
          MESSAGE 622 ; Ce lot est inexistant

FIELD DPODATJOU OF CRDEPOT  FORMAT YYYYMMDD PATTERN "####/##/##" &
label "Journalisé le.:"&
        DISPLAY

FIELD DPOHREJOU OF CRDEPOT &
        DISPLAY &
        for ,5

SKIP 2

ALIGN(3,3,19) (,,21)

FIELD DPOTYPMAC OF CRDEPOT HIDDEN ID 10 &
label "Type..........:"&
        REQUIRED &
        NOCHANGE &
        LOOKUP ON A_CBI_DPOTYPMAC &
          MESSAGE 689 ; Code invalide.

FIELD CBIDSC OF A_CBI_DPOTYPMAC &
        DISPLAY

SKIP 2

ALIGN(3,3,19) (,49,65)

FIELD DPOCLE OF CRDEPOT HIDDEN ID 15 &
        DISPLAY &
@IF FRANCAIS
        LABEL "Numéro de M/C.:"
@ELSE
        LABEL "Write-off no..:"
@ENDIF


FIELD DPODAT OF CRDEPOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        REQUIRED &
@IF FRANCAIS
        LABEL "Date radiation:"
@ELSE
        LABEL Write-off date.:"
@ENDIF

SKIP 2

ALIGN(3,3,19) (,,21) (,,28)

FIELD REFCLENUM OF CRDEPOT HIDDEN ID 20 &
        LABEL "Client........:" &
        REQUIRED &
        LOOKUP ON VCLC_CLI &
          MESSAGE 624 ; Ce client/employé n'existe pas.

FIELD DPONOM    OF CRDEPOT &
        REQUIRED &
        IF CLITYP OF VCLC_CLI = "D"

SKIP 2

ALIGN(3,3,19)

FIELD DPODSC1 OF CRDEPOT HIDDEN ID 40 &
label "Description...:"&
        REQUIRED

FIELD DPODSC2 OF CRDEPOT &
        NOLABEL &
        ID SAME

SKIP 2

ALIGN(3,3,19) (,33,34)

FIELD DPOMNTPAM OF CRDEPOT HIDDEN ID 45 &
        DISPLAY &
        REFRESH &
@IF FRANCAIS
        LABEL "Montant radié.:"
@ELSE
        LABEL "Write-off amt.:"
@ENDIF

FIELD DEVCLE OF VCLC_CLI &
        LABEL "/" &
        DISPLAY

ALIGN (2) (5)

;
; Ventilation facture. Sert en mode ENTRY seulement.
;
SUBSCREEN cr009b HIDDEN ID 90 &
                 NOMARK &
                 AUTO   &
                 MODE E &
                 PASSING T_CIECLEMNU  , &
                         T_CIENOM     , &
                         D_LCRATRJOU  , &
                         D_DPONOM     , &
                         CRDEPOT
;
; Imputation. Type de mauvaise créance manuel.
;
SUBSCREEN cr009a HIDDEN ID 91 &
                 NOMARK &
                 AUTO &
                 IF DPOTYPMAC OF CRDEPOT = "M" &
                 MODE E &
                 PASSING T_CIECLEMNU  , &
                         T_CIENOM     , &
                         D_LCRATRJOU  , &
                         D_REFDEVCLE  , &
                         D_DPONOM     , &
                         CRDEPOT


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
  ;
  ; Si l'écran n'est pas appellé par le menu, alors on interdit la saisie,
  ; la destruction et la modification à l'usager
  ;
  IF T_DPOCLEFND <> ""
  THEN BEGIN
    LET TZ_SECENT = "0"
    LET TZ_SECDEL = "0"
    LET TZ_SECMOD = "0"
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les périodes.
;
;
PROCEDURE INPUT PECCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CODMOD = "CR"
    LET T_PECCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc109 MODE F PASSING T_CIECLEMNU, &
                                    T_PECCLE   , &
                                    T_CODMOD
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de la période.
;
;
PROCEDURE EDIT PECCLE
BEGIN
  ; Ajustement pour le changement de siècle
  IF FIELDTEXT[1:1] < "8"
  THEN LET T_FIELDTEXT_SIE = "1" + FIELDTEXT
  ELSE LET T_FIELDTEXT_SIE = "0" + FIELDTEXT

  ; Ajustement pour le changement de siècle
  IF T_PECCLECOU[1:1] < "8"
  THEN LET T_PECCLECOU_SIE = "1" + T_PECCLECOU
  ELSE LET T_PECCLECOU_SIE = "0" + T_PECCLECOU

  IF T_FIELDTEXT_SIE < T_PECCLECOU_SIE
  THEN ERROR 607 ; Cette période est fermée.
  ;
  IF FIELDTEXT <> T_PECCLECOU
  THEN WARNING 608 ; La période indiquée est différente de la période courante.
END

;-------------------------------------------------------------------------------
;
;
; Popup sur les lots.
;
;
PROCEDURE INPUT LCRCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYPECR = "MC"
    LET T_PECCLE = PECCLE OF CRDEPOT
    LET T_LCRCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cr101 MODE F PASSING T_CIECLEMNU, &
                                    T_TYPECR, &
                                    T_PECCLE, &
                                    T_LCRCLE
    LET FIELDTEXT = TRUNC(T_LCRCLE)
  END
  ;
  ; Force l'éxécution du LOOKUP sur le lot et de la procédure EDIT.
  ;
  IF     NOT FINDMODE &
     AND 0 = SIZE(FIELDTEXT) &
     AND 0 <> SIZE( TRUNCATE( LCRCLE OF CRDEPOT ) )
  THEN LET FIELDTEXT = LCRCLE OF CRDEPOT
END


;-------------------------------------------------------------------------------
;
;
; Validation du lot.
;
;
PROCEDURE EDIT LCRCLE
BEGIN
  IF LCRTYPECR OF CRLOT <> "MC"
  THEN ERROR 627 ; Ce lot n'est pas du bon type.
  ;
  IF LCRDATJOU OF CRLOT <> 0
  THEN ERROR 628 ; Le lot est déjà journalisé.
  ;
  IF LCRATRJOU OF CRLOT = "1"
  THEN ERROR 629 ; On ne peut pas utiliser un lot autorisé à la journalisation.
  ;
  IF LCRUSR OF CRLOT <> LOGONID
  THEN ERROR 630 ; Ce lot ne vous est pas destiné.
  ;
  IF LCRNBRTRSVER OF CRLOT = 0
  THEN ERROR 719 ; Saisie interdite, entrez d'abord vos montants vérifiés dans le lot.
  ;
  IF LCRDATVAL OF CRLOT <> 0
  THEN WARNING 631 ; Une liste de vérification a déjà été demandé pour ce lot.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les types de dépôt(bilingue).
;
;
PROCEDURE INPUT DPOTYPMAC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DPOTYPMAC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_DPOTYPMAC, T_DPOTYPMAC
    LET FIELDTEXT = TRUNCATE( T_DPOTYPMAC )
  END
END


;-------------------------------------------------------------------------------
;
;
; Valide la date de la mauvaise créance selon l'interval de date de la période.
;
;
PROCEDURE EDIT DPODAT
BEGIN
  IF FIELDVALUE < PECDATDEB OF MCPERIODE_CTB
  THEN ERROR 637 ; La date est inférieure à la date de début de la période

  IF FIELDVALUE > PECDATFIN OF MCPERIODE_CTB
  THEN ERROR 638 ; La date est supérieure à la date de fin de la période.
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les clients.
;
;
PROCEDURE INPUT REFCLENUM
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFCIECLE  = REFCIECLE OF CRDEPOT
    LET T_REFCLETYP2 = REFCLETYP OF CRDEPOT
    LET T_REFSTUPAT  = "A"
    LET T_REFCLENUM  = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc112 MODE F PASSING T_REFCIECLE , &
                                    T_REFCLETYP2, &
                                    T_REFCLENUM , &
                                    T_REFSTUPAT
    LET FIELDTEXT = TRUNC(T_REFCLENUM)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du statut du client.
;
;
PROCEDURE EDIT REFCLENUM
BEGIN
  ;
  ; On averti l'usager si la référence n'est pas active.
  ;
  IF CLISTU OF VCLC_CLI <> "A"
  THEN WARNING 632 ; Le client n'est pas actif.

END


;-------------------------------------------------------------------------------
;
;
; On affiche le nom du client et la devise. Assigne le numéro pour identifier
; la mauvaise créance.
;
;
PROCEDURE PROCESS REFCLENUM
BEGIN
  DISPLAY DPONOM OF CRDEPOT
  DISPLAY DEVCLE OF VCLC_CLI
  ;
  ; Le numéro de mauvaise créance est composé du numéro de client plus
  ; une séquence de deux chiffres.
  ; Le numéro de client peut avoir entre 1 et 6 caractères alphanumérique,
  ; donc pour cette raison le numéro de mauvaise créance peut avoir une
  ; longueur variable.
  ;
  ; exemple: Numéro de client   Numéro M/C
  ;              COT01       ==>   COT01#01
  ;              823456      ==>   823456#01
  ;
  WHILE RETRIEVING A_DPO_SEQ VIA CIECLE, &
                                 REFCLENUM, &
                                 REFCIECLE, &
                                 REFCLETYP, &
                                 DPOTYPECR  &
                             USING CIECLE    OF CRDEPOT, &
                                   REFCLENUM OF CRDEPOT, &
                                   REFCIECLE OF CRDEPOT, &
                                   REFCLETYP OF CRDEPOT, &
                                   DPOTYPECR OF CRDEPOT
  BEGIN
    IF DPOCLE OF A_DPO_SEQ > DPOCLE OF CRDEPOT
    THEN LET DPOCLE OF CRDEPOT = DPOCLE OF A_DPO_SEQ
  END

  LET DPOCLE OF CRDEPOT = TRUNCATE( REFCLENUM OF CRDEPOT) + &
                          "#" + &
                          ASCII( NCONVERT( DPOCLE OF CRDEPOT[ &
                            SIZE( TRUNCATE( REFCLENUM OF CRDEPOT ) ) + 2 : 2] &
                            ) + 1, 2)
  DISPLAY DPOCLE OF CRDEPOT
END


;-------------------------------------------------------------------------------
;
;
; Affiche le nom du client régulier ou celui saisie dans le cas
; d'un client divers.
;
;
PROCEDURE OUTPUT DPONOM
BEGIN
  LET FIELDTEXT = D_DPONOM
END



;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Si la mauvaise créance n'est pas journalisé le nom du client divers
; est modifiable.
;
;
PROCEDURE DESIGNER 20
BEGIN
  IF     DPODATJOU OF CRDEPOT = 0 &
     AND CLITYP    OF VCLC_CLI = "D"
  THEN ACCEPT DPONOM OF CRDEPOT
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran   Imputation
;
;
PROCEDURE DESIGNER D001
BEGIN
  IF DPOTYPMAC OF CRDEPOT = "M"
  THEN RUN SCREEN cr009a PASSING T_CIECLEMNU  , &
                                 T_CIENOM     , &
                                 D_LCRATRJOU  , &
                                 D_REFDEVCLE  , &
                                 D_DPONOM     , &
                                 CRDEPOT        &
                         MODE SAME
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran   Ventilation de factures
;
;
PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN cr009b PASSING T_CIECLEMNU  , &
                            T_CIENOM     , &
                            D_LCRATRJOU  , &
                            D_DPONOM     , &
                            CRDEPOT        &
                    MODE SAME
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran   Ecritures comptable
;
;
PROCEDURE DESIGNER D003
BEGIN
  ;
  ; Cette procédure empeche l'accès au sous-écran si l'écriture n'est pas
  ; journalisée.
  ;
  IF DPODATJOU OF CRDEPOT = 0
  THEN ERROR 646 ; L'écriture doit être journalisée pour accèder le sous-écran.

  RUN SCREEN mc090 MODE F &
                   PASSING D_HISCLE
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran   Gestion des lots.
;
;
PROCEDURE DESIGNER D004
BEGIN
  LET T_PECCLE = PECCLE OF CRDEPOT
  LET T_LCRCLE = LCRCLE OF CRDEPOT
  RUN SCREEN cr020 MODE F &
                   PASSING T_CIECLEMNU, &
                           T_CIENOM   , &
                           T_PECCLEMNU, &
                           T_PECCLECOU, &
                           T_PECCLE   , &
                           T_LCRCLE
END


;-------------------------------------------------------------------------------
;
;
; Sous-écran   Vérification
;
;
PROCEDURE DESIGNER D005
BEGIN
  RUN SCREEN cr009c PASSING CRDEPOT &
                    MODE SAME
END


;-------------------------------------------------------------------------------
;
;
; La procédure PATH suivante est nécessaire dans le cas où l'écran est
; appelé autre que par le menu.
;
;
PROCEDURE PATH
BEGIN
  IF T_DPOCLEFND <> ""
  THEN LET PATH = 99
  ELSE BEGIN
    REQUEST PECCLE OF CRDEPOT
    IF PROMPTOK
    THEN REQUEST LCRCLE OF CRDEPOT
    IF PROMPTOK
    THEN LET PATH = 1

    IF PATH = 0
    THEN BEGIN
      REQUEST PECCLE OF CRDEPOT
      IF PROMPTOK
      THEN LET PATH = 2
    END

    IF PATH = 0
    THEN BEGIN
      REQUEST REFCLENUM OF CRDEPOT
      IF PROMPTOK
      THEN LET PATH = 3
    END

    IF PATH = 0
    THEN BEGIN
      LET PATH = 4
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; La procédure FIND est pour gérer l'appel en sous-écran (PATH=99)
;
;
PROCEDURE FIND
BEGIN
  IF PATH = 1
  THEN GET CRDEPOT VIA CIECLE , &
                       PECCLE , &
                       LCRCLE   &
                   USING T_CIECLEMNU       , &
                         PECCLE OF CRDEPOT , &
                         LCRCLE OF CRDEPOT   &
                   ORDERBY CIECLE , &
                           DPOCLE

  IF PATH = 2
  THEN GET CRDEPOT VIA CIECLE , &
                       PECCLE   &
                   USING T_CIECLEMNU       , &
                         PECCLE OF CRDEPOT   &
                   ORDERBY CIECLE , &
                           DPOCLE

  IF PATH = 3
  THEN GET CRDEPOT VIA CIECLE    , &
                       REFCLENUM   &
                   USING T_CIECLEMNU          , &
                         REFCLENUM OF CRDEPOT   &
                   ORDERBY DPOCHQNUM

  IF PATH = 4
  THEN GET CRDEPOT VIA CIECLE &
                   USING T_CIECLEMNU &
                   ORDERBY CIECLE , &
                           DPOCLE

  IF PATH = 99
  THEN GET CRDEPOT VIA CIECLE , &
                       DPOCLE   &
                   USING T_CIECLEMNU , &
                         T_DPOCLEFND

END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CRDEPOT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CRDEPOT &
     AND NOT NEWRECORD     OF CRDEPOT &
     AND NOT DELETEDRECORD OF CRDEPOT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Validation de la destruction d'une transaction journalisé
  ;
  IF     DELETEDRECORD OF CRDEPOT &
     AND DPODATJOU     OF CRDEPOT <> 0
  THEN ERROR 716 ;Impossible de détruire une transaction journalisé
  ;
  ; Non modifiable Si le lot est autorisé ou journalisé
  ;
  IF     LCRATRJOU OF CRLOT = "1" &
     AND DPODATJOU OF CRDEPOT = 0
  THEN ERROR 713 ; Impossible de mettre à jour la transaction est autorisé.
END


BUILD


;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;****************************** Mauvaise créances *******************************
;*                                                                              *
;* Période ......: xxxxx   Lot...........: xxxx  Journalisé le.: xxxxxxxx xxxxx *
;*                                                                              *
;*                                                                              *
;* Type..........: x xxxxxxxxxxxxxxxxxxxxxxxxx                                  *
;*                                                                              *
;*                                                                              *
;* Numéro de M/C.: xxxxxxxxxxxx                  Date radiation: xxxxxxxx       *
;*                                                                              *
;*                                                                              *
;* Client........: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
;*                                                                              *
;*                                                                              *
;* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
;*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
;*                                                                              *
;*                                                                              *
;* Montant radié.: xxxxxxxxxxxxxx/xxx                                           *
;*                                                                              *
;********************************************************************************
