;/T/ Annulation de paiements - ventilation de factures
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 6 Juillet 1993
;
;    Description..: consultation de la ventilation d'une annulation de
;                   paiement par factures.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp009a   ACTIONBAR &
                MODE LABEL "" AT 2,80 &
                NOACTION FIELDMARK RETAIN STARTUP &
                MESSAGE ON 24 &
                HELP POPUP FROM 4,9 TO 20,72 &
                ACTIVITIES FIND &
                RECEIVING T_CIECLEMNU   , &
                          T_CIENOM      , &
                          CPPAIEMENT    , &
                          MCREF_ADRESSE

USE ustrasse NOLIST

USE ussectmp NOLIST
    "CP009A"

USE cp009a.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 RESET AT STARTUP ; compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom de la Cie

;-------------------------------------------------------------------------------
;
; Relation maitres provenant de l'écran principal contenants :
;
;     Le paiement,
;     L'adresse (dans le cas d'un fournisseur divers)
;
FILE CPPAIEMENT     MASTER
FILE MCREF_ADRESSE  MASTER

;-------------------------------------------------------------------------------
;
; Lecture de la ventilation de l'annulation de paiement
;
FILE CPCAP_HISTO  PRIMARY &
                  OCCURS 14 &
                  NOITEMS

  ACCESS  VIA   CPHCLE1, &
                CPHCLE2, &
                CPHCLE3  &
          USING CIECLE    OF CPPAIEMENT, &
                LBQCPTENC OF CPPAIEMENT, &
                PAMCLE    OF CPPAIEMENT

;-------------------------------------------------------------------------------

DEFINE D_REFNOM CHAR SIZE 15 = &
@IF FRANCAIS
 "Employé.......:" &
@ELSE
 "Employe.......:" &
@ENDIF
  IF REFCLETYP OF CPPAIEMENT = "E" ELSE &
@IF FRANCAIS
 "Fournisseur...:" &
@ELSE
 "Supplier......:" &
@ENDIF
  IF REFCLETYP OF CPPAIEMENT = "F"


DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE ustitstd NOLIST
USE usdrwecr NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Ventilation des documents " &
@ELSE
      " Document Breakdown " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (3,3,19) (,49,65)

FIELD PAMCLE OF CPPAIEMENT &
      PREDISPLAY

FIELD PAMMNT OF CPPAIEMENT &
      PREDISPLAY

SKIP

ALIGN (3,3,3) (,,19) (,,26)

FIELD D_REFNOM &
      PREDISPLAY

FIELD REFCLENUM OF CPPAIEMENT &
      PREDISPLAY

FIELD RADNOMABR OF MCREF_ADRESSE &
      DISPLAY &
      PREDISPLAY

SKIP

DRAW FROM ,1 TO ,80

SKIP 1

TITLE &
@IF FRANCAIS
      "       Numéro de       No          Montant     Escompte         Montant      " &
@ELSE
      "       %%%%%%%%%       %%%         %%%%%%%     %%%%%%%%         %%%%%%%      " &
@ENDIF
      AT ,2

SKIP

TITLE &
@IF FRANCAIS
      "       document      Cédule        à payer         pris            payé       " &
@ELSE
      "       %%%%%%%%      %%%%%%        %%%%%%%         %%%%            %%%%       " &
@ENDIF
      AT ,2

SKIP

ALIGN (2,3,8) (,,25) (,,31) (,,47) (,,60)

CLUSTER OCCURS WITH CPCAP_HISTO ID BASE 05

FIELD CAPCLE OF CPCAP_HISTO &
      HIDDEN &
      LABEL " "

FIELD CPCCLELIG OF CPCAP_HISTO

FIELD CPHMNTCAP OF CPCAP_HISTO

FIELD CPHMNTESC OF CPCAP_HISTO &
      PICTURE "^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5

FIELD CPHMNTPYE OF CPCAP_HISTO

CLUSTER

;-------------------------------------------------------------------------------
; *** Sécurité d'accès ***
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

BUILD
;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;************************** Ventilation des documents ***************************
;* No paiement...: xxxxxxx                       Mnt paiement..: xxxxxxxxxxxxxx *
;* xxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx                                  *
;********************************************************************************
;*       Numéro de       No          Montant     Escompte         Montant       *
;*       document      Cédule        à payer         pris            payé       *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;*      xxxxxxxxxxxx     xx    xxxxxxxxxxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxx      *
;********************************************************************************
