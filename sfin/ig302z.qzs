;/T/ Impression de l'inventaire physique des tranches .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-02
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste de validation de l'inventaire physique
;                   de tranche de granit de Granicor.
;
;    Paramètres....: Compagnie
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INTRANCHE                    &
  LINK TRANUMTRA002,                    &
       CIECLE                           &
    TO TRANUMTRA002,                    &
       CIECLE    OF PDTRANCHE OPTIONAL  &
  LINK TGRCODGRA OF PDTRANCHE,          &
       CIECLE    OF PDTRANCHE           &
    TO TGRCODGRA,                       &
       CIECLE    OF PDTYPE_GRANIT OPTIONAL

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG302"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEF DPAGEDEB INT SIZE 4 = PARM
DEF DPAGEFIN INT SIZE 4 = PARM

SELECT INTRANCHE IF ITRNUMPGE OF INTRANCHE >= DPAGEDEB AND &
                    ITRNUMPGE OF INTRANCHE <= DPAGEFIN

SORT ON ITRNUMPGE  OF INTRANCHE &
     ON ITRDATINV  OF INTRANCHE &
     ON ITRHREINV  OF INTRANCHE

DEFINE D_ITRDESC CHARACTER SIZE 55 = ITRDESC OF INTRANCHE[1:45]

DEFINE D_TGRDSCFRA001 CHARACTER SIZE 15 = TGRDSCFRA001 OF PDTYPE_GRANIT[1:15] ;
;                               IF TGRCODGRA OF PDTRANCHE = "41" OR &
;                                  TGRCODGRA OF PDTRANCHE = "56"    &
;                               ELSE ""

USE ustitgor132_45 NOLIST
   SKIP 3 &
     TAB 53 "DONNEES DE LA PAGE: " ITRNUMPGE OF INTRANCHE &
   SKIP 4

HEADING AT ITRNUMPGE SKIP PAGE &

  TAB 003 &
;                                                                                                 1         1         1         1
;       1         2         3         4         5         6         7         8         9         0         1         2         3
;34567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
"LOCALISATION  NUM. TRANCHE  COMMENTAIRE                                        TYPE_FINI  TYPE GRANIT  DESCRIPTION " &
SKIP 1 &
  TAB 003 &
"------------  ------------  -----------                                        ---------  -----------  ----------- "

REPORT &
  TAB 007 ITRNUMLOC                             &
  TAB 017 TRANUMTRA002    OF INTRANCHE          &
  TAB 031 D_ITRDESC                             &
  TAB 085 TYFCODFIN       OF PDTRANCHE          &
  TAB 097 TGRCODGRA       OF PDTRANCHE          &
  TAB 106 D_TGRDSCFRA001

FOOTING AT ITRNUMPGE SKIP 2 &
    "NOMBRE D'ENTREES POUR CETTE PAGE...............:" COUNT

FINAL FOOTING SKIP 2  &
    "GRAND TOTAL DE TRANCHES EN INVENTAIRE..........:" COUNT

BUILD ig302z
