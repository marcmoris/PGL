;/T/ Popup - cédule d'encaissement     CIECLE, CARCLE, CCDCLELIG
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 2 juin 1993
;
;    Paramètre....: CIECLE    ==> - compagnie du menu.
;                                 - segment de la clé de recherche.
;
;                   CARCLE    ==> - numéro du compte à recevoir.
;                                 - segment de le clé de recherche.
;
;                   CCDCLELIG ==> - numéro du cédule.
;                                 - contient la valeur de retour.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr107 NOMODE NOACTION FIELDMARK &
             FROM 11,36 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU, &
                       T_CARCLE   , &
                       T_CCDCLELIG

USE cr107.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Compagnie du menu.
TEMPORARY T_CARCLE    CHARACTER SIZE 15 ; Numéro de facture
TEMPORARY T_CCDCLELIG INTEGER   SIZE  2 ; Numéro de cédule

;-------------------------------------------------------------------------------

FILE VCAR_CCD         PRIMARY &
                      OCCURS 10
 ACCESS VIA CIECLE, &
            CARCLE  &
        USING T_CIECLEMNU, &
              T_CARCLE     &
        ORDERBY CIECLE, &
                CARCLE, &
                CCDCLELIG

;-------------------------------------------------------------------------------
;
; Montant du compte à recevoir.
;
DEFINE D_MNTCED FLOAT SIZE 8 =   CCDMNT      OF VCAR_CCD &
                               + V_CCDMNTAUG OF VCAR_CCD

;
; Solde à recevoir.
;
DEFINE D_SLDCED FLOAT SIZE 8 =  D_MNTCED - V_CCDMNTDIM OF VCAR_CCD

;-------------------------------------------------------------------------------

DRAW 1,1 TO 13,45

USE ushilite NOLIST

USE ustitoff NOLIST

SKIP TO 2

;         1         2         3         4         5         6         7
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;  Lig.  Due      Mnt prévue    Mnt encaissé
;   99 99/99/99 12345678901234 12345678901234
TITLE &
@IF FRANCAIS
      "Céd   Due             Brut     A recevoir" &
@ELSE
      "Schd  Due            Gross     Receivable" &
@ENDIF
   AT ,3

SKIP

ALIGN(3,2,3) (,,6) (,,17) (,,31)

CLUSTER OCCURS WITH VCAR_CCD

FIELD CCDCLELIG OF VCAR_CCD HIDDEN LABEL " " &
        DISPLAY size 2

FIELD CCDDATDUE OF VCAR_CCD  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY

FIELD D_MNTCED &
        DISPLAY &
        PICTURE "^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

FIELD D_SLDCED &
        PICTURE "^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CCDCLELIG = CCDCLELIG OF VCAR_CCD
  RETURN
END

BUILD

;                                   *********************************************
;                                   * Céd   Due             Brut     A recevoir *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   * xx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx *
;                                   *********************************************
