;/T/ Rapport de la génération des commandes d'achat à partir des réquisitions.
;
;/P/ Programmeur...: Guy Chabot
;    Date Création.: 22 septembre 1994
;
;    Description...: Permet de visionner les commandes d'achat générées à partir
;                    des réquisitions. 
;
;/M/ Francois Richard, 99-07-09
;
;    Ajustement pour le changement de siècle 
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....

ACCESS *WAR702ER

;
; Nom de la compagnie du menu.
;
DEFINE DZ_CIENOM CHAR SIZE 40 = PARM
;
; Titre de la liste.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS 
  " Génération de commande - erreur" 
@ELSE
  "%%%ting - error"
@ENDIF 
;
; Deuxième ligne du titre.
;
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "
;
; Nom de la liste.
; 
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "AR702ZA"


SORT ON T_MESERR


;-------------------------------------------------------------------------------

USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP 2 &
@IF FRANCAIS
    TAB 110 "Période : " &
@ELSE
    TAB 110 "Period  : " &
@ENDIF
 TAB 120 PECCLE &
SKIP 2 &
 TAB 010 "Description" &
SKIP 2


NOREPORT


REPORT &
   TAB 010 T_MESERR OF WAR702ER


BUILD ar702za

USE ussetqzs NOLIST

ACCESS *WAR702IM2  &
   ;
   ; Pour afficher le nom de l'entrepôt
   ; 
  LINK CIECLE OF WAR702IM2, &
       ENTCLE OF WAR702IM2  &
    TO CIECLE, &
       ENTCLE OF ARENTREPOT    & 
   ;
   ; Pour sélectionner la devise du fournisseur.
   ;
  LINK FOUCIECLE OF WAR702IM2, &
       FOUCLE    OF WAR702IM2, &
       CIECLE    OF WAR702IM2  &
    TO FOUCIECLE, &
       FOUCLE,    &
       FOCCIECLE OF VFOC_FOU     &
   
   ; 
   ; Pour afficher le nom du fournisseur divers
   ;
  LINK FOUCIECLE OF WAR702IM2, &
       REFCLETYP OF WAR702IM2, &
       FOUCLE    OF WAR702IM2, &
       RADCLE    OF WAR702IM2  &
    TO REFCIECLE, &
       REFCLETYP, &
       REFCLENUM, &
       RADCLE OF MCREF_ADRESSE    &
 
   ;
   ; Statut de la commande
   ;
  LINK ("CMDSTU",                 &
         "A")       &
    TO XELNOM,                    &
       CBICLE OF VCBI_DSC   OPTIONAL & 
 
   ;
   ; Devise du consolidé
   ;
  LINK ("1") &
    TO CIENMC OF MCHOLDING   

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Génération des commandes d'achat"
@ELSE
       "%%%ération des commandes d'achat"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       ""
@ELSE
       "%%%"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR702Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM 

DEFINE D_CBIDSC CHARACTER SIZE 15 = CBIDSC OF VCBI_DSC

; Ajustement pour le changement de siècle
DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = "1" + CMDCLE OF WAR702IM2 &
   IF CMDCLE OF WAR702IM2 < "8" &
   ELSE "0" + CMDCLE OF WAR702IM2

SORT ON CIECLE OF WAR702IM2 & ; Compagnie
     ON ENTCLE OF WAR702IM2 & ; Entrepôt
     ON DEVCLE OF VFOC_FOU  & ; Devise
     ON D_CMDCLE_TRI        & ; Numéro de commande
     ON CDECLE OF WAR702IM2

DEFINE D_RADNOM CHARACTER SIZE 40 = RADNOM OF MCREF_ADRESSE[1:40]

USE ustitstd NOLIST
  SKIP 2 &
@IF FRANCAIS
         TAB 001 "Devise............:" &
@ELSE
         TAB 001 "%%%ise............:" &
@ENDIF
         TAB 021 DEVCLE OF VFOC_FOU    &
  SKIP &
@IF FRANCAIS
         TAB 001 "Entrepôt..........:" &
@ELSE
         TAB 001 "%%%Entrepôt..........:" &
@ENDIF
         TAB 021 ENTCLE OF WAR702IM2 &
         TAB 027 ENTNOM OF ARENTREPOT &
  SKIP 2 &
@IF FRANCAIS
         TAB 001 "Commande                                                                                 Date     Montant" &
@ELSE
         TAB 001 "%%%" &
@ENDIF
  SKIP &
@IF FRANCAIS
         TAB 001 "d'achat    Item   Pér.  Fournisseur                                     Statut         Commande     brut        Réquisition   Item" &
@ELSE
         TAB 001 "%%%" &
@ENDIF
  SKIP   TAB 001 "                                                                                       aa/mm/jj" &
  SKIP   TAB 001 "--------- ------- ----- ----------------------------------------------- -------------- -------- --------------- ----------- -------" &
  SKIP 1

REPORT &
         TAB 001 CMDCLE       OF WAR702IM2 PRINT AT D_CMDCLE_TRI & 
         TAB 011 CDECLE       OF WAR702IM2                  &
         TAB 019 PECCLE       OF WAR702IM2 PICTURE "^^-^^"  &
         TAB 025 FOUCLE       OF WAR702IM2 &
         TAB 032 D_RADNOM                  &
         TAB 073 D_CBIDSC                  &
         TAB 088 CMDDAT       OF WAR702IM2 &
         TAB 097 REQMNTTOT    OF WAR702IM2 &
         TAB 114 REQCLE       OF WAR702IM2 &
         TAB 125 REQCLENUM    OF WAR702IM2 &
    RESERVE 6 LINES

FOOTING AT D_CMDCLE_TRI &
  SKIP 1 TAB 097 "---------------" &
  SKIP &
@IF FRANCAIS
         TAB 030 "Total pour la commande" &
@ELSE
         TAB 030 "%%%al pour la commande" &
@ENDIF
         TAB 054 CMDCLE OF WAR702IM2  &
         TAB 097 REQMNTTOT OF WAR702IM2 SUBTOTAL &
  SKIP   TAB 097 "---------------" &
  SKIP 3


FOOTING AT DEVCLE &
  SKIP 1 TAB 097 "---------------" &
  SKIP &
@IF FRANCAIS
         TAB 030 "Total pour l'entrepôt " &
@ELSE
         TAB 030 "%%%al pour l'entrepôt " &
@ENDIF
         TAB 052 ENTCLE OF WAR702IM2  &
@IF FRANCAIS
         TAB 056 " en devise " &
@ELSE
         TAB 056 "%%% devise " &
@ENDIF
         TAB 067 DEVCLE OF VFOC_FOU &
         TAB 097 REQMNTTOT OF WAR702IM2 SUBTOTAL &
  SKIP   TAB 097 "===============" &
  SKIP PAGE

BUILD ar702zb
@IF FORMAT
AR702                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                      Génération des commandes d'achat
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Devise............: xxx

Entrepôt..........: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx


Commande                                                                                Date      Montant
d'achat    Item   Pér.                  Fournisseur                         Statut     Commande     brut        Réquisition   Item
--------- ------- ----- ----------------------------------------------- -------------- -------- --------------- ----------- -------
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
                                                                                                ---------------
                             Total pour la commande  xxxxxxxxx                                  999,999,999.99-
                                                                                                ---------------
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
xxxxxxxxx xxx.xxx 99-99 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxx 99/99/99 999,999,999.99-  xxxxxxxxx  xxx.xxx
                                                                                                ---------------
                             Total pour la commande  xxxxxxxxx                                  999,999,999.99-
                                                                                                ---------------

                                                                                                ---------------
                             Total pour l'entrepôt xxxx en devise xxx                           999,999,999.99-
                                                                                                ===============
@ENDIF
