;/T/ 2.4.5. Enregistrer la finition - Arrêt de finition
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-12-07
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les arrêts pendant la finition des pièces de granit.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;/V/ Écran vérifier pour le passage à l'an 2000

;-------------------------------------------------------------------------------
SCREEN pd187a ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM,          &
                        PDFINITION


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_FINITION IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;

USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD187A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd187a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04
DEFINE    D_FINSTU    CHARACTER SIZE 16 = "FINSTU"
TEMPORARY T_FINSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable pour l'appel des dépisteur (pop-up)
;

TEMPORARY T_RAACODARR CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_SILENT CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calculd'un intervalle de temps
;

USE usdiftim NOLIST


;-------------------------------------------------------------------------------
;
; Finition
;

FILE PDFINITION       MASTER


;-------------------------------------------------------------------------------
;
; Arrêt de finition
;

FILE PDARRET_FINITION PRIMARY &
                      NOITEM &
                      OCCURS 8 TIMES

  ACCESS VIA     CIECLE,             &
                 FINNUMRAP           &
         USING   T_CIECLEMNU,                      &
                 FINNUMRAP           OF PDFINITION &
         ORDERBY CIECLE,             &
                 FINNUMRAP,          &
                 RAACODARR


  ITEM CIECLE    OF PDARRET_FINITION INITIAL T_CIECLEMNU
  ITEM FINNUMRAP OF PDARRET_FINITION INITIAL FINNUMRAP OF PDFINITION FIXED

  ITEM ARFDURSEC OF PDARRET_FINITION SUM INTO FINDURSECARR OF PDFINITION
  ITEM ARFDURSEC OF PDARRET_FINITION SUM NEGATIVE INTO FINDURSEC OF PDFINITION


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour la finition
;

FILE PDSEQ_FINITION DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Arrêt opération de surface validation des plages horaire pour le rapport
;

FILE PDARRET_FINITION DESIGNER ALIAS A_ARF_ARRET_FINI


  ACCESS VIA     CIECLE,          &
                 FINNUMRAP        &
         USING   T_CIECLEMNU,     &
                 FINNUMRAP        OF PDARRET_FINITION

  SELECT IF ((ARFHREDEB OF PDARRET_FINITION >= ARFHREDEB OF A_ARF_ARRET_FINI   &
              AND                                                              &
              ARFHREDEB OF PDARRET_FINITION < ARFHREFIN OF A_ARF_ARRET_FINI)  &
            OR                                                                 &
             (ARFHREFIN OF PDARRET_FINITION >= ARFHREDEB OF A_ARF_ARRET_FINI   &
              AND                                                              &
              ARFHREFIN OF PDARRET_FINITION <= ARFHREFIN OF A_ARF_ARRET_FINI)) &
            OR                                                                 &
             (ARFHREDEB OF PDARRET_FINITION <= ARFHREDEB OF A_ARF_ARRET_FINI   &
              AND                                                              &
              ARFHREFIN OF PDARRET_FINITION >= ARFHREFIN OF A_ARF_ARRET_FINI)


;-------------------------------------------------------------------------------
;
; Opérateur
;

FILE PDOPERATEUR REFERENCE

  ACCESS VIA     CIECLE,          &
                 OPENUMEMP        &
         USING   T_CIECLEMNU,     &
                 OPENUMEMP        OF PDFINITION


;-------------------------------------------------------------------------------
;
; Raison d'arrêt
;

FILE PDRAISON_ARRET  REFERENCE

  ACCESS VIA    CIECLE,           &
                RAACODARR         &
         USING  T_CIECLEMNU,      &
                RAACODARR        OF PDARRET_FINITION



;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDFINITION


;-------------------------------------------------------------------------------
;
; Variable necessaire au traitement
;

DEFINE D_PRNNOM CHARACTER SIZE 40 = TRUNCATE(OPEPNM OF PDOPERATEUR) + " " + TRUNCATE(OPENOM OF PDOPERATEUR)
DEFINE    D_OPENOM CHARACTER SIZE 40 = "Inconnu au système" &
                                    IF   OPEPNM OF PDOPERATEUR IS NULL &
                                         OR &
                                         OPENOM OF PDOPERATEUR IS NULL &
                                    ELSE TRUNCATE( OPEPNM OF PDOPERATEUR ) + &
                                         " " + &
                                         TRUNCATE( OPENOM OF PDOPERATEUR )


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Finition " &
@ELSE
      " %%%Finition " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (,3,19)


FIELD FINNUMRAP        OF PDFINITION       &
label "Numéro rapport:"&
        FIXED


FIELD FINDATPRD        OF PDFINITION        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        FIXED


ALIGN (,3,19) (,,26)


FIELD OPENUMEMP        OF PDFINITION       &
label "Numéro employé:"&
        FIXED


FIELD D_OPENOM                             &
        FIXED


ALIGN (,3,19) (,,21)


FIELD QRTCODQRT        OF PDFINITION       &
label "Quart.........:"&
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        FIXED                              &
        SIZE 19


ALIGN (,3,19)

FIELD MFICODMAC        OF PDFINITION       &
label "Code machine..:"&
        FIXED



SKIP TO LINE 4


ALIGN (,43,59)


FIELD FINHREDEB        OF PDFINITION       &
label "Hre départ....:"&
        FIXED


FIELD FINHREFIN        OF PDFINITION       &
label"Hre fin.......:"&
        FIXED


DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Arrêt finition " &
@ELSE
      " %%%Arrêt finition " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 12


TITLE &
@IF FRANCAIS
    "           Heure   Heure               " &
@ELSE
    "%%%        Heure   Heure               " &
@ENDIF
      AT ,2


SKIP


TITLE &
@IF FRANCAIS
    "    Code   début   fin     Description " &
@ELSE
    "%%% Code   début   fin     Description " &
@ENDIF
      AT ,2


ALIGN (5,5,6) (,,13) (,,21) (,,29)


CLUSTER OCCURS WITH PDARRET_FINITION ID BASE 40


FIELD RAACODARR        OF PDARRET_FINITION &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED                           &
        LOOKUP ON PDRAISON_ARRET           &
          MESSAGE 2976 ; Cette raison n'existe pas


FIELD ARFHREDEB        OF PDARRET_FINITION &
        HIDDEN                             &
        REQUIRED


FIELD ARFHREFIN        OF PDARRET_FINITION &
        HIDDEN                             &
        REQUIRED


FIELD T_SILENT                             &
        SILENT                             &
        LOOKUP NOTON PDARRET_FINITION      &
          VIA     CIECLE,                  &
                  FINNUMRAP,               &
                  ARFHREDEB,               &
                  ARFHREFIN                &
          USING   T_CIECLEMNU,                          &
                  FINNUMRAP        OF PDARRET_FINITION, &
                  ARFHREDEB        OF PDARRET_FINITION, &
                  ARFHREFIN        OF PDARRET_FINITION  &
          MESSAGE 2921 ; Ce numéro de ligne existe déjà


FIELD ARFDSCARR        OF PDARRET_FINITION &
        HIDDEN                             &
        DEFAULT RAADSCFRA OF PDRAISON_ARRET


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs raison d'arret
;

PROCEDURE INPUT RAACODARR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RAACODARR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd114 MODE F &
                     PASSING T_RAACODARR, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RAACODARR )
  END
END


;-------------------------------------------------------------------------------
;
; Valide les plages horraire des arrets
;

PROCEDURE EDIT ARFHREDEB
BEGIN
  ;
  ; Valide que le temps d'arret est compris dans l'intervale du rapport
  ;
  IF ARFHREDEB OF PDARRET_FINITION < FINHREDEB OF PDFINITION &
     OR                                                      &
     ARFHREDEB OF PDARRET_FINITION > FINHREFIN OF PDFINITION
  THEN BEGIN
    ERROR 3010 ; L'heure de début n'est pas compris dans l'intervale du rapport
  END
  ;
  ; Fait le test seulement si l'heure de fin a déjà été saisie
  ;
  IF ARFHREFIN OF PDARRET_FINITION <> 0
  THEN BEGIN
    ;
    ; Valide que l'intervalle n'est pas copris dans un déjà enregistrer
    ;
    GET A_ARF_ARRET_FINI OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide les plages horraire des arrets
;

PROCEDURE EDIT ARFHREFIN
BEGIN
  ;
  ; Valide que le temps d'arret est compris dans l'intervale du rapport
  ;
  IF ARFHREFIN OF PDARRET_FINITION < FINHREDEB OF PDFINITION &
     OR                                                      &
     ARFHREFIN OF PDARRET_FINITION > FINHREFIN OF PDFINITION
  THEN BEGIN
    ERROR 3011 ; L'heure de fin n'est pas compris dans l'intervale du rapport
  END
  ;
  ; Valide que l'intervalle n'est pas copris dans un déjà enregistrer
  ;
  GET A_ARF_ARRET_FINI OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  FOR PDARRET_FINITION
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDARRET_FINITION &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDARRET_FINITION &
       AND NOT NEWRECORD     OF PDARRET_FINITION &
       AND NOT DELETEDRECORD OF PDARRET_FINITION &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Calcul de la durée du traitement pour l'opération de surface
    ;
    LET TZ_DATDEB   = FINDATPRD        OF PDFINITION
    LET TZ_TMPDEB   = ASCII(ARFHREDEB  OF PDARRET_FINITION,4) + "00"
    LET TZ_DATFIN   = FINDATPRD        OF PDFINITION
    LET TZ_TMPFIN   = ASCII(ARFHREFIN  OF PDARRET_FINITION,4) + "00"

    LET ARFDURSEC OF PDARRET_FINITION = DZ_DIFSECTOT
  END
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = FINNUMRAP OF PDFINITION
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_FINITION OPTIONAL         &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_FINITION = T_CIECLEMNU
    END
    LET SEINUMRAP OF PDSEQ_FINITION = SEINUMRAP OF PDSEQ_FINITION + 1
    LET FINNUMRAP OF PDFINITION     = SEINUMRAP OF PDSEQ_FINITION
    PUT PDSEQ_FINITION
    DISPLAY FINNUMRAP OF PDFINITION
  END
END

  ;
  ; Validation des plages horraire pour les arrêts
  ;
PROCEDURE INTERNAL VALIDE_HEURE
BEGIN

  WHILE RETRIEVING A_ARF_ARRET_FINI
  BEGIN
      IF ( (ARFHREDEB OF PDARRET_FINITION >= ARFHREDEB OF A_ARF_ARRET_FINI    &
            AND                                                               &
            ARFHREDEB OF PDARRET_FINITION < ARFHREFIN OF A_ARF_ARRET_FINI )  &
          OR                                                                  &
           (ARFHREFIN OF PDARRET_FINITION >= ARFHREDEB OF A_ARF_ARRET_FINI    &
            AND                                                               &
            ARFHREFIN OF PDARRET_FINITION <= ARFHREFIN OF A_ARF_ARRET_FINI )) &
         OR                                                                   &
           (ARFHREDEB OF PDARRET_FINITION <= ARFHREDEB OF A_ARF_ARRET_FINI    &
            AND                                                               &
            ARFHREFIN OF PDARRET_FINITION >= ARFHREFIN OF A_ARF_ARRET_FINI )
      THEN BEGIN
        ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
      END
    END
END


;-------------------------------------------------------------------------------
;
; Update
;
PROCEDURE UPDATE
BEGIN
  PUT PDFINITION
  FOR PDARRET_FINITION
  BEGIN
    PUT PDARRET_FINITION
   ; DO INTERNAL VALIDE_HEURE
  END
END


BUILD

@IF FORMAT

2x                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    x
3--------------------------------- Finition -----------------------------------+
4                                                                              |
5 Numéro rapport: 999999999              Hre départ....: HH:MM                 |
6 Date product..: AA/MM/JJ               Hre fin.......: HH:MM                 |
7 Quart.........: X    xxxxxxx           Statut........: X  xxxxxxxxxxxxxxx    |
8 Code machine..: XXX  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                |
9                                                                              |
0------------------------------- Arrêt finition -------------------------------+
1                                                                              |
2           Heure   Heure                                                      |
3    Code   début   fin     Description                                        |
4    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
5    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
6    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
7    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
8    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
9    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
0    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
1    XXXX   HH:MM   HH:MM   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           |
2                                                                              |
3------------------------------------------------------------------------------+
@ENDIF
