;/P/ Rapport sur les comptes prix de revient (court)
;;
USE ussetqzs NOLIST

ACCESS *WPR305A &
  LINK CIECLE, PROCLE TO &
       CIECLE, PROCLE OF GPPROJET  &
  LINK G-CPTCLE1 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE1 OPTIONAL &
  LINK G-CPTCLE2 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE2 OPTIONAL &
  LINK G-CPTCLE3 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE3 OPTIONAL &
  LINK G-CPTCLE4 TO &
       CPTCLE OF VCPT_DSC ALIAS COMPTE4 OPTIONAL


DEFINE D-DESC1 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE1
DEFINE D-DESC2 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE2
DEFINE D-DESC3 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE3
DEFINE D-DESC4 CHARACTER SIZE 15 = CPTDSCABR OF COMPTE4



DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
  "Rapport sur les comptes au prix de revient"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "PR305"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

DEFINE D_MNT1 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE1 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT2 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE2 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT3 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE3 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT
DEFINE D_MNT4 INTEGER SIZE 4 = HIPMNTCRT - HIPMNTDBT IF CPTCODDC OF COMPTE4 = "C" &
                          ELSE HIPMNTDBT - HIPMNTCRT

DEFINE D-MNT1 INTEGER SIZE 4 = D_MNT1 IF CPTCLE = G-CPTCLE1 &
                               ELSE 0
DEFINE D-MNT2 INTEGER SIZE 4 = D_MNT2 IF CPTCLE = G-CPTCLE2 &
                               ELSE 0
DEFINE D-MNT3 INTEGER SIZE 4 = D_MNT3 IF CPTCLE = G-CPTCLE3 &
                               ELSE 0
DEFINE D-MNT4 INTEGER SIZE 4 = D_MNT4 IF CPTCLE = G-CPTCLE4 &
                               ELSE 0
DEFINE D-TOT  INTEGER SIZE 4 = D-MNT1 + D-MNT2 + D-MNT3 + D-MNT4

SORT ON CIECLE &
     ON PROCLE &
     ON REFCLENUM &
     ON HISDAT &
     ON HISNUMDOC

USE ustitstd NOLIST
  TAB 001 "Projet :" &
  TAB 010 PROCLE OF WPR305A &
  TAB 020 PRODSC OF GPPROJET &
SKIP 2 &
  TAB 001 "# Client" &
  TAB 010 "Date" &
  TAB 020 "# facture" &
  TAB 040 D-DESC1 &
  TAB 056 D-DESC2 &
  TAB 072 D-DESC3 &
  TAB 088 D-DESC4 &
  TAB 110 "Total" &
  TAB 120 "Source" &
SKIP &
  TAB 040 "(" &
  TAB 041 G-CPTCLE1 &
  TAB 047 ")" &
  TAB 056 "(" &
  TAB 057 G-CPTCLE2 &
  TAB 063 ")" &
  TAB 072 "(" &
  TAB 073 G-CPTCLE3 &
  TAB 080 ")" &
  TAB 088 "(" &
  TAB 089 G-CPTCLE4 &
  TAB 095 ")" &
SKIP 2


FOOT AT HISNUMDOC &
  TAB 001 REFCLENUM PRINT AT REFCLENUM &
  TAB 010 HISDAT &
  TAB 020 HISNUMDOC &
  TAB 040 D-MNT1 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 055 D-MNT2 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 070 D-MNT3 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 085 D-MNT4 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 100 D-TOT  SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 120 HISCODMOD

FOOT AT PROCLE &
  TAB 040 "--------------" &
  TAB 055 "--------------" &
  TAB 070 "--------------" &
  TAB 085 "--------------" &
  TAB 100 "--------------" &
SKIP &
  TAB 001 "Total" &
  TAB 007 PROCLE &
  TAB 040 D-MNT1 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 055 D-MNT2 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 070 D-MNT3 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 085 D-MNT4 SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
  TAB 100 D-TOT  SUBTOTAL PICTURE " ^^,^^^,^^^.^^" SIGNIF 4 &
SKIP PAGE

BUILD pr305z
