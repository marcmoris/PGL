;/T/ 2.4.4. Enregistrer le débitage - ARRÊT DÉBITAGE
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-11-30
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le débitage des pièces de granit.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd186b ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU ,      &
                        T_CIENOM,          &
                        PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_DEBITAGE IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD186B"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd186b.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;

DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04
DEFINE    D_DEBSTU    CHARACTER SIZE 16 = "DEBSTU"
TEMPORARY T_DEBSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour les dépisteurs (pop-up)
;

TEMPORARY T_RAACODARR CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_SILENT CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour le calculd'un intervalle de temps
;
USE usdiftim NOLIST


;-------------------------------------------------------------------------------
;
; Débitage
;

FILE PDDEBITAGE       MASTER


;-------------------------------------------------------------------------------
;
; Arrêt débitage
;

FILE PDARRET_DEBIT         PRIMARY &
                           NOITEM  &
                           OCCURS 8 TIMES

  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP        &
         USING   T_CIECLEMNU,                   &
                 DEBNUMRAP        OF PDDEBITAGE &
         ORDERBY CIECLE,          &
                 DEBNUMRAP,       &
                 ARDHREDEB,       &
                 ARDHREFIN


  ITEM CIECLE    OF PDARRET_DEBIT INITIAL T_CIECLEMNU
  ITEM DEBNUMRAP OF PDARRET_DEBIT INITIAL DEBNUMRAP OF PDDEBITAGE FIXED
  ITEM ARDDURSEC OF PDARRET_DEBIT SUM INTO DEBDURSECARR OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour le débitage
;

FILE PDSEQ_DEBITAGE  DESIGNER &
                     TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Arrêt débitage validation des plages horaire pour le rapport
;

FILE PDARRET_DEBIT DESIGNER ALIAS ARD_ARRET_DEBIT


  ACCESS VIA     CIECLE,          &
                 DEBNUMRAP        &
         USING   T_CIECLEMNU,     &
                 DEBNUMRAP        OF PDARRET_DEBIT

  SELECT &
      IF ( (ARDHREDEB OF PDARRET_DEBIT >= ARDHREDEB OF ARD_ARRET_DEBIT   &
            AND                                                            &
            ARDHREDEB OF PDARRET_DEBIT < ARDHREFIN OF ARD_ARRET_DEBIT)  &
          OR                                                               &
           (ARDHREFIN OF PDARRET_DEBIT >= ARDHREDEB OF ARD_ARRET_DEBIT   &
            AND                                                            &
            ARDHREFIN OF PDARRET_DEBIT <= ARDHREFIN OF ARD_ARRET_DEBIT)) &

         OR                                                                &
           (ARDHREDEB OF PDARRET_DEBIT <= ARDHREDEB OF ARD_ARRET_DEBIT   &
            AND                                                            &
            ARDHREFIN OF PDARRET_DEBIT >= ARDHREFIN OF ARD_ARRET_DEBIT)



;-------------------------------------------------------------------------------
;
; Machine débitage
;

FILE PDMACH_DEBITAGE REFERENCE

  ACCESS VIA     CIECLE,          &
                 MDECODMAC        &
         USING   T_CIECLEMNU,     &
                 MDECODMAC        OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT           OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_DEBSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_DEBSTU,                             &
               DEBSTU              OF PDDEBITAGE


;-------------------------------------------------------------------------------
;
; Raison d'arrêt
;

FILE PDRAISON_ARRET  REFERENCE OCCURS WITH PDARRET_DEBIT

  ACCESS VIA     CIECLE,          &
                 RAACODARR        &
         USING   T_CIECLEMNU,     &
                 RAACODARR        OF PDARRET_DEBIT


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Débitage " &
@ELSE
      " %%%Débitage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD DEBNUMRAP        OF PDDEBITAGE       &
label "Numéro rapport:"&
        DISPLAY                            &
        FIXED


FIELD DEBDATPRD        OF PDDEBITAGE        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date product..:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,24)


FIELD MDECODMAC        OF PDDEBITAGE       &
label "Code machine..:"&
        DISPLAY                            &
        FIXED


FIELD MDEDSC001        OF PDMACH_DEBITAGE  &
        DISPLAY                            &
        FIXED


SKIP TO LINE 5


ALIGN (,42,58)


FIELD DEBDEP           OF PDDEBITAGE       &
label "Départ........:"&
        DISPLAY                            &
        FIXED


FIELD DEBARR           OF PDDEBITAGE       &
label "Arrêt.........:"&
        DISPLAY                            &
        FIXED


SKIP TO LINE 8


ALIGN (,3,19) (,,24)


FIELD QRTCODQRT        OF PDDEBITAGE       &
label "Quart travail.:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY                            &
        FIXED                              &
        SIZE 18


ALIGN (,3,19) (,,24)


FIELD DEBSTU           OF PDDEBITAGE       &
label "Statu........:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_DEBSTU     &
        DISPLAY                            &
        FIXED                              &
        SIZE 18


DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Arrêt débitage " &
@ELSE
      " %%%Arrêt débitage " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
  "        Arret   Départ    Code Description " &
@ELSE
  "%%%     Arret   Départ    Code Description " &
@ENDIF
  AT ,2


SKIP TO LINE 14


ALIGN (9,9,10) (,18,18) (,27,28) (,32,33) (,,38)


CLUSTER OCCURS WITH PDARRET_DEBIT    ID BASE 35


FIELD ARDHREDEB        OF PDARRET_DEBIT    &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED


FIELD ARDHREFIN        OF PDARRET_DEBIT    &
        HIDDEN                             &
        NOLABEL                            &
        REQUIRED


FIELD T_SILENT                             &
        SILENT                             &
        LOOKUP NOTON PDARRET_DEBIT         &
          VIA     CIECLE,                  &
                  DEBNUMRAP,               &
                  ARDHREDEB,               &
                  ARDHREFIN                &
          USING   T_CIECLEMNU,                         &
                  DEBNUMRAP        OF PDARRET_DEBIT,   &
                  ARDHREDEB        OF PDARRET_DEBIT,   &
                  ARDHREFIN        OF PDARRET_DEBIT    &
          MESSAGE 2921 ; Ce numéro de ligne existe déjà


FIELD   RAACODARR      OF PDARRET_DEBIT    &
        NOLABEL                            &
        REQUIRED                           &
        LOOKUP ON PDRAISON_ARRET           &
          MESSAGE 2976 ; Cette raison n'existe pas


FIELD ARDDSCARR        OF PDARRET_DEBIT    &
        HIDDEN                             &
        NOLABEL



CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs raison d'arret
;

PROCEDURE INPUT RAACODARR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RAACODARR = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd114 MODE F &
                     PASSING T_RAACODARR, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RAACODARR )
  END
END


;-------------------------------------------------------------------------------
;
; Initialise le texte du code de raison
;

PROCEDURE INPUT ARDDSCARR
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) AND ENTRYMODE
  THEN BEGIN
   LET FIELDTEXT = RAADSCFRA        OF PDRAISON_ARRET
  END
END


;-------------------------------------------------------------------------------
;
; Valide les plages horraire des arrets
;

PROCEDURE EDIT ARDHREDEB
BEGIN
  ;
  ; Valide que le temps d'arret est compris dans l'intervale du rapport
  ;
  IF ARDHREDEB OF PDARRET_DEBIT < DEBDEP OF PDDEBITAGE &
     OR                                                &
     ARDHREDEB OF PDARRET_DEBIT > DEBARR OF PDDEBITAGE
  THEN BEGIN
    ERROR 3010 ; L'heure de début n'est pas compris dans l'intervale du rapport
  END
  ;
  ; Fait le test seulement si l'heure de fin a déjà été saisie
  ;
  IF ARDHREFIN OF PDARRET_DEBIT <> 0
  THEN BEGIN
    ;
    ; Calcul de la durée de l'arret
    ;
    LET TZ_DATDEB   = DEBDATPRD        OF PDDEBITAGE
    LET TZ_TMPDEB   = ASCII(ARDHREDEB  OF PDARRET_DEBIT,4) + "00"
    LET TZ_DATFIN   = DEBDATPRD        OF PDDEBITAGE
    LET TZ_TMPFIN   = ASCII(ARDHREFIN  OF PDARRET_DEBIT,4) + "00"

    LET ARDDURSEC OF PDARRET_DEBIT = DZ_DIFSECTOT
    ;
    ; Valide que l'intervalle n'est pas copris dans un déjà enregistrer
    ;
    GET ARD_ARRET_DEBIT OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
    END
  END
END


;-------------------------------------------------------------------------------
;
; Valide les plages horraire des arrets
;

PROCEDURE EDIT ARDHREFIN
BEGIN
  ;
  ; Valide que le temps d'arret est compris dans l'intervale du rapport
  ;
  IF ARDHREFIN OF PDARRET_DEBIT < DEBDEP OF PDDEBITAGE &
     OR                                                &
     ARDHREFIN OF PDARRET_DEBIT > DEBARR OF PDDEBITAGE
  THEN BEGIN
    ERROR 3011 ; L'heure de fin n'est pas compris dans l'intervale du rapport
  END
  ;
  ; Calcul de la durée de l'arret
  ;
  LET TZ_DATDEB   = DEBDATPRD        OF PDDEBITAGE
  LET TZ_TMPDEB   = ASCII(ARDHREDEB  OF PDARRET_DEBIT,4) + "00"
  LET TZ_DATFIN   = DEBDATPRD        OF PDDEBITAGE
  LET TZ_TMPFIN   = ASCII(ARDHREFIN  OF PDARRET_DEBIT,4) + "00"

  LET ARDDURSEC OF PDARRET_DEBIT = DZ_DIFSECTOT

  ;
  ; Valide que l'intervalle n'est pas copris dans un déjà enregistrer
  ;
  GET ARD_ARRET_DEBIT OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  FOR PDARRET_DEBIT
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDARRET_DEBIT &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDARRET_DEBIT &
       AND NOT NEWRECORD     OF PDARRET_DEBIT &
       AND NOT DELETEDRECORD OF PDARRET_DEBIT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

    ;
    ; Calcul de la durée du traitement pour l'opération
    ;

    LET TZ_DATDEB   = DEBDATPRD        OF PDDEBITAGE
    LET TZ_TMPDEB   = ASCII(ARDHREDEB  OF PDARRET_DEBIT,4) + "00"
    LET TZ_DATFIN   = DEBDATPRD        OF PDDEBITAGE
    LET TZ_TMPFIN   = ASCII(ARDHREFIN  OF PDARRET_DEBIT,4) + "00"

    LET ARDDURSEC OF PDARRET_DEBIT = DZ_DIFSECTOT

    IF ALTEREDRECORD OF PDARRET_DEBIT
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF DEBSTU OF PDDEBITAGE = "V"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "E" ; En cours , il faut reprendre la validation
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF DEBSTU OF PDDEBITAGE = "T"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "C"  ; Correction après transfert
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END

    IF NEWRECORD OF PDARRET_DEBIT
    THEN BEGIN
      ;
      ; Si le rapport à déjà été valider il faut reprendre la validation
      ;
      IF DEBSTU OF PDDEBITAGE = "V"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "E" ; En cours , il faut reprendre la validation
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
      ;
      ; Si le rapport à été transferré il devient en correction
      ;
      IF DEBSTU OF PDDEBITAGE = "T"
      THEN BEGIN
        LET DEBSTU OF PDDEBITAGE = "C"  ; Correction après transfert
        DISPLAY DEBSTU OF PDDEBITAGE
        DISPLAY CBIDSC OF A_CBI_DEBSTU
      END
    END
  END

  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = DEBNUMRAP OF PDDEBITAGE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_DEBITAGE OPTIONAL         &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_DEBITAGE = T_CIECLEMNU
    END
    LET SEQNUMSEQ OF PDSEQ_DEBITAGE = SEQNUMSEQ OF PDSEQ_DEBITAGE + 1
    LET DEBNUMRAP OF PDDEBITAGE     = SEQNUMSEQ OF PDSEQ_DEBITAGE
    PUT PDSEQ_DEBITAGE
    DISPLAY DEBNUMRAP OF PDDEBITAGE
  END

END

PROCEDURE INTERNAL VALIDE_HEURE
BEGIN
  ;
  ; Validation des plages horraire pour les arrêts
  ;
  WHILE RETRIEVING ARD_ARRET_DEBIT
  BEGIN
    IF (ARDHREDEB OF PDARRET_DEBIT <> ARDHREDEB OF ARD_ARRET_DEBIT    &
        AND                                                           &
        ARDHREFIN OF PDARRET_DEBIT <> ARDHREFIN OF ARD_ARRET_DEBIT)
    THEN BEGIN
      IF ( (ARDHREDEB OF PDARRET_DEBIT >= ARDHREDEB OF ARD_ARRET_DEBIT    &
            AND                                                           &
            ARDHREDEB OF PDARRET_DEBIT < ARDHREFIN OF ARD_ARRET_DEBIT)   &
          OR                                                              &
           (ARDHREFIN OF PDARRET_DEBIT >= ARDHREDEB OF ARD_ARRET_DEBIT    &
            AND                                                           &
            ARDHREFIN OF PDARRET_DEBIT <= ARDHREFIN OF ARD_ARRET_DEBIT))  &
         OR                                                               &
           (ARDHREDEB OF PDARRET_DEBIT <= ARDHREDEB OF ARD_ARRET_DEBIT    &
            AND                                                           &
            ARDHREFIN OF PDARRET_DEBIT >= ARDHREFIN OF ARD_ARRET_DEBIT)
      THEN BEGIN
        ERROR 3005 ; Cette plage horraire chevauche une plage déjà enregistrer
       END
    END
  END
END


;-------------------------------------------------------------------------------
;
; update
;
PROCEDURE UPDATE
BEGIN
  PUT PDDEBITAGE
  FOR PDARRET_DEBIT
  BEGIN
    PUT PDARRET_DEBIT
    ;DO INTERNAL VALIDE_HEURE
  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*********************************** Débitage ***********************************
*                                                                              *
* Numéro rapport: xx                      Arrêt.........: xxxxx                *
* Date product..: xxxxxxxx                Départ........: xxxxx                *
* Quart travail.: x    xxxxxxxxxxxxxxxxxxxxxxxxx                               *
* Code machine..: xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
*                                                                              *
******************************** Arrêt débitage ********************************
*                                                                              *
*                                                        Heure    Heure        *
*    Code    Description                                 début    fin          *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*    xxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx    xxxxx        *
*                                                                              *
********************************************************************************
@ENDIF
