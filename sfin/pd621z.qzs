;/T/ Liste des diagramme par commande interne. (Sans priorité)
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-14
;
;    Description...: Ce rapport permet d'imprimer la liste des diagrammes
;                    pour une commande interne
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS PDCOMMAN_INTERNE &
  LINK PJTABR ,&
       PJTANN, &
       COMNUMCOMINT ,&
       CIECLE &
  TO   PJTABR,&
       PJTANN,&
       COMNUMCOMINT,&
       CIECLE  OF PDDETAIL_C_I &

 LINK (PJTABR OF PDDETAIL_C_I,&
       PJTANN OF PDDETAIL_C_I,&
       COMNUMCOMINT OF PDDETAIL_C_I, &
       DCINUMLIG OF PDDETAIL_C_I,&
       CIECLE OF PDDETAIL_C_I, &
       "A") &
  TO   PJTABR,&
       PJTANN,&
       COMNUMCOMINT ,&
       DCINUMLIG,&
       CIECLE,&
       DIASTU OF PDDIAGRAMME &
  LINK PJTABR OF PDDIAGRAMME, &
       DIANUMDIA001 OF PDDIAGRAMME, &
       DIANUMDIA002 OF PDDIAGRAMME, &
       CIECLE OF PDDIAGRAMME &
    TO PJTABR, &
       DIANUMDIA001, &
       DIANUMDIA002, &
       CIECLE                           OF VDIAQTE



CHOOSE CIECLE PARM , COMNUMCOM PARM,COMSTU "C" "P"





DEFINE D_ETOILE CHARACTER SIZE 1 = "*" IF DIATYPDIA OF PDDIAGRAMME = "T" ELSE " "
DEFINE D_TROP   NUMERIC          = DIAQTEEMB OF PDDIAGRAMME - DIAQTECMD OF PDDIAGRAMME &
                                   IF DIAQTEEMB OF PDDIAGRAMME > DIAQTECMD OF PDDIAGRAMME &
                                   ELSE 0


SORT ON COMNUMCOM OF PDDIAGRAMME &
     ON DIANUM002 OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de planification par commande interne sans priorité"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD621"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

USE ustitstd45 NOLIST
  SKIP &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDDIAGRAMME &
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDDIAGRAMME &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP 2 &
  TAB 001 "                                                          *----- Compagnie -----*  *----- Usine sec. ----*  *----- Sous-cont. ----*" &
  SKIP &
  TAB 001 "T Gra  Diag.  Fi Épai Long Haut    Cmd   Fab   Emb   Exp   Plan   Dbt  Plan  Fini   Plan   Dbt  Plan  Fini   Plan   Dbt  Plan  Fini" &
  SKIP 2


REPORT &
  TAB 001 D_ETOILE &
  TAB 003 TGRCODGRA OF PDDIAGRAMME &
  TAB 008 DIANUMDIA002 OF PDDIAGRAMME &
  TAB 015 TYFCODFIN OF PDDIAGRAMME &
  TAB 018 DIAEPA OF PDDIAGRAMME &
  TAB 023 DIALON OF PDDIAGRAMME &
  TAB 028 DIAHAU OF PDDIAGRAMME &
  TAB 034 DIAQTECMD OF PDDIAGRAMME &
  TAB 040 DIAQTEDBT OF VDIAQTE PICTURE "^^^^^" &
  TAB 046 DIAQTEEMB OF PDDIAGRAMME &
  TAB 052 DIAQTEEXPCLI OF PDDIAGRAMME &
  TAB 059 DIAQTEPLADBTGRA OF VDIAQTE PICTURE "^^^^^" &
  TAB 065 DIAQTEDBTGRA    OF VDIAQTE PICTURE "^^^^^" &
  TAB 071 DIAQTEPLAFINGRA OF VDIAQTE PICTURE "^^^^^" &
  TAB 077 DIAQTEFINGRA    OF VDIAQTE PICTURE "^^^^^" &
  TAB 084 DIAQTEPLADBTUSC OF VDIAQTE PICTURE "^^^^^" &
  TAB 090 DIAQTEDBTUSC    OF VDIAQTE PICTURE "^^^^^" &
  TAB 096 DIAQTEPLAFINUSC OF VDIAQTE PICTURE "^^^^^" &
  TAB 102 DIAQTEFINUSC    OF VDIAQTE PICTURE "^^^^^" &
  TAB 109 DIAQTEPLADBTSCT OF VDIAQTE PICTURE "^^^^^" &
  TAB 115 DIAQTEDBTSCT    OF VDIAQTE PICTURE "^^^^^" &
  TAB 121 DIAQTEPLAFINSCT OF VDIAQTE PICTURE "^^^^^" &
  TAB 127 DIAQTEFINSCT    OF VDIAQTE PICTURE "^^^^^"

FOOTING AT COMNUMCOM &
SKIP PAGE

BUILD pd621z

@IF FORMAT

PD621                                                       GRANICOR INC                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX          Liste de planification par commande interne sans priorité

Num. commande.: XX-999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
Priorité......: XX  AA/MM/JJ                                          Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
                                                                      Critères esthé: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX


                                              *------ Granicor -----*  *----- Usine sec. ----*    *----- Sous-cont. ----*
Gra  T Diag.  Fini  Épai  Long  Haut    Cmd    Plan   Dbt  Plan  Fini    Plan   Dbt  Plan  Fini   Plan    Dbt  Plan  Fini    Trop
999  * XXXXX   99   9999  9999  9999  99999   99999 99999 99999 99999   99999 99999 99999 99999   99999 99999 99999 99999   99999
999  * XXXXX   99   9999  9999  9999  99999   99999 99999 99999 99999   99999 99999 99999 99999   99999 99999 99999 99999   99999
999  * XXXXX   99   9999  9999  9999  99999   99999 99999 99999 99999   99999 99999 99999 99999   99999 99999 99999 99999   99999
999  * XXXXX   99   9999  9999  9999  99999   99999 99999 99999 99999   99999 99999 99999 99999   99999 99999 99999 99999   99999
999  * XXXXX   99   9999  9999  9999  99999   99999 99999 99999 99999   99999 99999 99999 99999   99999 99999 99999 99999   99999
@ENDIF
