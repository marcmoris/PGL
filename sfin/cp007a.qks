;/T/ Gestion des paiements à effectuer - Paramètres de sélection
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 19 Juin 1993
;
;    Description..: Saisie des paramètres de recherche et sélection des
;                   documents à payer.
;
;-------------------------------------------------------------------------------
;
;/M/ Date modif...: 09-Mars-1994
;
;    Programmeur..: Guy Chabot
;
;    Description..: Ajout de CAPCIECLE dans l'appel du popup CP105 pour ne
;                   référer que les pièces de la même compagnie.
;
;                   Modification du ACCESS de la table CPCPT_A_PAYER pour
;                   n'avoir que les pièce de la compagnie.
;
;
;/M/ Modifié par..: Guy Chabot le 12 octobre 1994
;
;    Description..: Introduction du P.N.A. aux payables sur le même principe
;                   ou presque des recevables.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp007a   NOMODE NOACTION FIELDMARK STARTUP &
                FROM 8,1 TO 19,60 &
                MESSAGE ON 24 &
                AUTOUPDATE &
                HELP POPUP FROM 4,9 TO 20,72 &
                ACTIVITIES ENTRY, CHANGE &
                RECEIVING   T_CIECLEMNU, &
                            T_FOUCIECLE, &
                            T_EMPCIECLE, &
                            CPLOT

USE ussectmp NOLIST
    "CP007A"

USE cp007a.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie du menu
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4  ; Compagnie du fournisseur
TEMPORARY T_EMPCIECLE CHARACTER SIZE 4  ; Compagnie de l'employé

;-------------------------------------------------------------------------------
;
;
; Les paramètres de sélection sont placés dans le lot
;
;

FILE CPLOT MASTER

;-------------------------------------------------------------------------------

DEFINE D_FOUEMPCIECLE CHARACTER SIZE 4 &
        = T_EMPCIECLE IF LCPREFTYP = "E" ELSE &
          T_FOUCIECLE

;
; compagnie nécessaire pour rechercher dans les tables de transactions. quand
; il s'agit d'un employé, c'est qu'on gére des relevés de de dépense. Les
; relevés de dépense sont par compagnie tandis que les factures ou paiement
; auto. sont par fournisseur.
;
DEFINE D_CIECLE CHARACTER SIZE 4 &
        = T_FOUCIECLE IF LCPREFTYP = "F" ELSE &
          T_CIECLEMNU

;
; puisque les relevés de dépense sont par compagnie, la référence ne fait pas
; partie de la clé pour ce type de transaction.
;
DEFINE D_FOUCLE CHARACTER SIZE 6 &
        = LCPREFNUM IF LCPREFTYP = "F" ELSE &
          "      "

DEFINE D_REFNOM CHAR SIZE 15 = &
@IF FRANCAIS
        "Employé.......:" &
@ELSE
        "Employee......:" &
@ENDIF
        IF LCPREFTYP = "E" ELSE &
@IF FRANCAIS
        "Fournisseur...:"
@ELSE
        "Supplier......:"
@ENDIF

;-------------------------------------------------------------------------------
;
; Pour valider le numéro de fournisseur ou d'employé
;
FILE MCREFERENCE REFERENCE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM  &
          USING D_FOUEMPCIECLE    , &
                LCPREFTYP OF CPLOT, &
                LCPREFNUM OF CPLOT

;-------------------------------------------------------------------------------
;
; Pour valider le numéro de document
;
FILE CPCPT_A_PAYER REFERENCE

  ACCESS  VIA   FOUCIECLE, &
                FOUCLE   , &
                CAPCLE   , &
                CAPCIECLE  &
          USING D_CIECLE , &
                D_FOUCLE , &
                LCPCAPCLE OF CPLOT, &
                CIECLE    OF CPLOT

;-------------------------------------------------------------------------------
;
; Sélection des paiements
;
FILE CPSEL_CAP  DESIGNER            ; Cédules sélectionnées
FILE VCAP_SEL   DESIGNER OPEN READ  ; Cédules à sélectionner
FILE CPTERME    DESIGNER OPEN READ  ; Termes pour calculer l'escompte
FILE VFOC_FOU   DESIGNER OPEN READ  ; Fournisseur pour sélectionner suivant le
                                    ; compte d'encaisse

;-------------------------------------------------------------------------------
;
; Code bilingue sur le type de référence
;
DEFINE    D_LCPREFTYP CHARACTER SIZE 16 = "REFCLETYP"
TEMPORARY T_LCPREFTYP CHARACTER SIZE  4

FILE VCBI_DSC   REFERENCE &
                ALIAS A_CBI_LCPREFTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_LCPREFTYP, &
                LCPREFTYP OF CPLOT

;-------------------------------------------------------------------------------
;
; Code bilingue sur la catégorie du fournisseur
;
DEFINE    D_LCPFOUCAT CHARACTER SIZE 16 = "FOUCAT"
TEMPORARY T_LCPFOUCAT CHARACTER SIZE  4

FILE VCBI_DSC   REFERENCE &
                ALIAS A_CBI_LCPFOUCAT

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_LCPFOUCAT, &
                LCPFOUCAT OF CPLOT

;-------------------------------------------------------------------------------
;
; Code bilingue sur le type de document
;
DEFINE    D_LCPTYPDOC CHARACTER SIZE 16 = "LCPTYPDOC"
TEMPORARY T_LCPTYPDOC CHARACTER SIZE  4

FILE VCBI_DSC   REFERENCE &
                ALIAS A_CBI_LCPTYPDOC

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_LCPTYPDOC, &
                LCPTYPDOC OF CPLOT

;-------------------------------------------------------------------------------
;
; Consultation des comptes d'encaisses
;
;
FILE MCLIVRET_BQ REFERENCE

  ACCESS  VIA   CIECLE, &
                LBQCPTENC &
          USING T_CIECLEMNU, &
                LBQCPTENC OF CPLOT

FILE MCCOMPTE REFERENCE

  ACCESS  VIA   CPTCLE &
          USING LBQCPTENC OF CPLOT

;-------------------------------------------------------------------------------
;
; Consultation des devises
;
;
FILE MCDEVISE REFERENCE

  ACCESS  VIA   DEVCLE &
          USING LCPDEVCLE OF CPLOT

;-------------------------------------------------------------------------------


TEMPORARY T_LCPREFNUM CHARACTER SIZE 6  ;
TEMPORARY T_REFCIECLE CHARACTER SIZE 4
TEMPORARY T_CAPCIECLE CHARACTER SIZE 4
TEMPORARY T_REFCLETYP CHARACTER SIZE 1
TEMPORARY T_REFCLENUM CHARACTER SIZE 6
TEMPORARY T_REFSTUPAT CHARACTER SIZE 5 INITIAL "A"
TEMPORARY T_CIECLE    CHARACTER SIZE 4
TEMPORARY T_FOUCLE    CHARACTER SIZE 6
TEMPORARY T_CENCLE    CHARACTER SIZE 4
TEMPORARY T_TYPECRPAT CHARACTER SIZE 30
TEMPORARY T_FLGSLD    CHARACTER SIZE 1
TEMPORARY T_CAPCLE    CHARACTER SIZE 15
TEMPORARY T_DEVCLE    CHARACTER SIZE 3
TEMPORARY T_LBQCPTENC CHARACTER SIZE 6
TEMPORARY T_FLGMOD    CHARACTER SIZE 1


;
; Variable utilisées pour la sélection automatique.
;
TEMPORARY T_TMPLCPREFTYP CHARACTER SIZE 1
TEMPORARY T_TMPLCPREFNUM CHARACTER SIZE 6
TEMPORARY T_TMPLCPCAPCLE CHARACTER SIZE 15
TEMPORARY T_TMPLCPFOUCAT CHARACTER SIZE 4
TEMPORARY T_TMPLCPTYPDOC CHARACTER SIZE 2
TEMPORARY T_TMPLCPDEVCLE CHARACTER SIZE 3

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 12,60

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


ALIGN (3,3,19) (,,26)
FIELD LCPREFTYP OF CPLOT &
label "Type référence:" &
      ID 05 HIDDEN &
      REQUIRED &
      PREDISPLAY &
      LOOKUP ON A_CBI_LCPREFTYP &
      MESSAGE 439 ; Ce code est invalide.

FIELD CBIDSC OF A_CBI_LCPREFTYP &
      DISPLAY

HILITE DISPLAY OFF

ALIGN (,3,3) (,,19) (,,26)

FIELD D_REFNOM  &
      DISPLAY &
      PREDISPLAY &
      ID SAME &
      LABEL "               "

USE ushilite NOLIST     ; Hilite pour tout les écrans

FIELD LCPREFNUM OF CPLOT &
      ID SAME HIDDEN &
      PREDISPLAY

FIELD REFNOMABR OF MCREFERENCE &
      DISPLAY &
      PREDISPLAY

ALIGN (,3,19)

FIELD LCPCAPCLE OF CPLOT &
label "No document...:" &
      ID SAME &
      PREDISPLAY

ALIGN (,3,19) (,,26)

FIELD LCPFOUCAT OF CPLOT &
label "Catégorie.....:" &
      ID SAME &
      PREDISPLAY

FIELD CBIDSC OF A_CBI_LCPFOUCAT &
      DISPLAY &
      PREDISPLAY

FIELD LCPTYPDOC OF CPLOT &
label "Type document.:" &
      ID SAME &
      PREDISPLAY

FIELD CBIDSC OF A_CBI_LCPTYPDOC &
      DISPLAY &
      PREDISPLAY

ALIGN (,3,19)

FIELD LCPDATDEB OF CPLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
label "Date début....:" &
      ID SAME &
      PREDISPLAY

FIELD LCPDATFIN OF CPLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
label "Date de fin...:" &
      ID SAME &
      PREDISPLAY

FIELD LCPSELESC OF CPLOT &
label "Escompte uniq.:" &
      ID SAME &
      PREDISPLAY &
      SIZE 3

ALIGN (,3,19) (,,26)

FIELD LBQCPTENC OF CPLOT &
      label "Cpt encaisse..:" &
      ID SAME &
      PREDISPLAY

FIELD &
@IF FRANCAIS
      CPTDSCABRFRA OF MCCOMPTE &
@ELSE
      CPTDSCABRANG OF MCCOMPTE &
@ENDIF
      DISPLAY &
      PREDISPLAY

FIELD LCPDEVCLE OF CPLOT &
label "Devise........:" &
      ID SAME &
      PREDISPLAY

FIELD &
@IF FRANCAIS
      DEVDSCFRA OF MCDEVISE &
@ELSE
      DEVDSCANG OF MCDEVISE &
@ENDIF
      DISPLAY &
      PREDISPLAY

;-------------------------------------------------------------------------------
; *** Sécurité d'accès ***
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; CONSULTATION & VALIDATION DU TYPE DE REFERENCE
;
PROCEDURE INPUT LCPREFTYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LCPREFTYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_LCPREFTYP, T_LCPREFTYP
    LET FIELDTEXT = TRUNCATE(T_LCPREFTYP)
  END
END
;
; Seuls les fournisseurs ou employés sont acceptés
;
PROCEDURE EDIT LCPREFTYP
BEGIN
  IF    LCPREFTYP OF CPLOT <> "F" &
    AND LCPREFTYP OF CPLOT <> "E"
  THEN ERROR 553 ; Seuls les Fournisseurs et Employés sont autorisés ici.
  IF LCPREFTYP OF CPLOT <> OLDVALUE(LCPREFTYP OF CPLOT)
  THEN LET T_FLGMOD = "1"
  ELSE LET T_FLGMOD = "0"
END

PROCEDURE PROCESS LCPREFTYP
BEGIN
  IF T_FLGMOD = "1"
  THEN
    BEGIN
      LET LCPREFNUM OF CPLOT =""
      EDIT LCPREFNUM OF CPLOT
      DISPLAY LCPREFNUM OF CPLOT
      DISPLAY REFNOMABR OF MCREFERENCE
      LET LCPCAPCLE OF CPLOT =""
      EDIT LCPCAPCLE OF CPLOT
      DISPLAY LCPCAPCLE OF CPLOT
      LET LCPFOUCAT OF CPLOT =""
      EDIT LCPFOUCAT OF CPLOT
      DISPLAY LCPFOUCAT OF CPLOT
      DISPLAY CBIDSC OF A_CBI_LCPFOUCAT
      LET LCPTYPDOC OF CPLOT =""
      EDIT LCPTYPDOC OF CPLOT
      DISPLAY LCPTYPDOC OF CPLOT
      DISPLAY CBIDSC OF A_CBI_LCPTYPDOC
      LET LCPDATDEB OF CPLOT =0
      DISPLAY LCPDATDEB OF CPLOT
      LET LCPDATFIN OF CPLOT =0
      DISPLAY LCPDATFIN OF CPLOT
      LET LCPSELESC OF CPLOT ="0"
      EDIT LCPSELESC OF CPLOT
      DISPLAY LCPSELESC OF CPLOT
      LET LBQCPTENC OF CPLOT =""
      EDIT LBQCPTENC OF CPLOT
      DISPLAY LBQCPTENC OF CPLOT
@IF FRANCAIS
      DISPLAY CPTDSCABRFRA OF MCCOMPTE
@ELSE
      DISPLAY CPTDSCABRANG OF MCCOMPTE
@ENDIF
      LET LCPDEVCLE OF CPLOT =""
      EDIT LCPDEVCLE OF CPLOT
      DISPLAY LCPDEVCLE OF CPLOT
@IF FRANCAIS
      DISPLAY DEVDSCFRA OF MCDEVISE
@ELSE
      DISPLAY DEVDSCANG OF MCDEVISE
@ENDIF
    END
END


;-------------------------------------------------------------------------------
;
; CONSULTATION & VALIDATION DU NUMÉRO DE RÉFÉRENCE
;
;
PROCEDURE INPUT LCPREFNUM
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFCIECLE = D_FOUEMPCIECLE
    LET T_REFCLETYP = LCPREFTYP OF CPLOT
    LET T_LCPREFNUM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc112 MODE F &
        PASSING T_REFCIECLE, T_REFCLETYP, T_LCPREFNUM, T_REFSTUPAT
    LET FIELDTEXT = TRUNCATE(T_LCPREFNUM)
  END
END

;
; Validation du fournisseur ou employé
;
PROCEDURE EDIT LCPREFNUM
BEGIN
  IF 0 <> SIZE(TRUNCATE(LCPREFNUM OF CPLOT))
  THEN BEGIN
    GET MCREFERENCE OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 500 ; Ce numéro n'existe pas.

    IF REFSTU OF MCREFERENCE = "I" AND LCPREFTYP OF CPLOT = "F"
    THEN ERROR 446 ; Le fournisseur est inactif.

    IF REFSTU OF MCREFERENCE = "I" AND LCPREFTYP OF CPLOT = "E"
    THEN ERROR 504 ; Cet employé est inactif.

    IF REFFOURTU OF MCREFERENCE = "1" AND LCPREFTYP OF CPLOT = "F"
    THEN WARNING 447 ; Le fournisseur est présentement retenu.
  END
END


;-------------------------------------------------------------------------------
;
; CONSULTATION & VALIDATION DES DOCUMENTS
;
PROCEDURE INPUT LCPCAPCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    IF LCPREFTYP OF CPLOT = "F"
    THEN LET T_TYPECRPAT = "FA|CR|PA|NA"
    ELSE LET T_TYPECRPAT = "RD"
    LET T_CIECLE    = D_CIECLE
    LET T_FOUCLE    = D_FOUCLE
    LET T_CAPCIECLE = CIECLE OF CPLOT
    LET T_FLGSLD = "1" ;on affiche que les facture ayant un solde à payer
    LET T_CAPCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    IF    LCPREFTYP OF CPLOT = "E" &
      AND 0 <> SIZE (TRUNCATE(LCPREFNUM OF CPLOT))
    THEN LET T_REFCLENUM = LCPREFNUM OF CPLOT
    ELSE LET T_REFCLENUM = "@"

    RUN SCREEN cp105 MODE F PASSING T_CIECLE, T_FOUCLE, T_TYPECRPAT, &
                                    T_FLGSLD, T_CAPCLE, T_REFCLENUM, &
                                    T_CAPCIECLE
    LET FIELDTEXT = TRUNCATE(T_CAPCLE)
  END
END

PROCEDURE EDIT LCPCAPCLE
BEGIN
  IF 0 <> SIZE(TRUNCATE(LCPCAPCLE OF CPLOT))
  THEN BEGIN
    GET CPCPT_A_PAYER OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 500 ; Ce numéro n'existe pas.

    IF CAPRTU <> "0"
    THEN ERROR 502 ; "Ce document est présentement retenu..."
  END
END

;-------------------------------------------------------------------------------
;
; CONSULTATION DES CATÉGORIES DE FOURNISSEURS
;
;
;
PROCEDURE INPUT LCPFOUCAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LCPFOUCAT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_LCPFOUCAT, T_LCPFOUCAT
    LET FIELDTEXT = TRUNCATE(T_LCPFOUCAT)
  END
END

PROCEDURE EDIT LCPFOUCAT
BEGIN
  IF 0 <> SIZE(TRUNCATE(LCPFOUCAT OF CPLOT))
  THEN BEGIN
    GET A_CBI_LCPFOUCAT OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 409 ; cette categorie est inexistante
  END
END

;-------------------------------------------------------------------------------
;
; CONSULTATION DES TYPES DE DOCUMENTS
;
;
PROCEDURE INPUT LCPTYPDOC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LCPTYPDOC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_LCPTYPDOC, T_LCPTYPDOC
    LET FIELDTEXT = TRUNCATE(T_LCPTYPDOC)
  END
END

PROCEDURE EDIT LCPTYPDOC
BEGIN
  IF 0 <> SIZE(TRUNCATE(LCPTYPDOC OF CPLOT))
  THEN BEGIN
    GET A_CBI_LCPTYPDOC OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 409 ; cette categorie est inexistante
  END
END

;-------------------------------------------------------------------------------
;
; FLAG OUI/NON SUR LA SÉLECTION D'ESCOMPTE
;
;
PROCEDURE INPUT LCPSELESC
BEGIN
  USE usflginp NOLIST
END

PROCEDURE OUTPUT LCPSELESC
BEGIN
  USE usflgout3 NOLIST
END

;-------------------------------------------------------------------------------
;
; CONSULTATION DES COMPTES D'ENCAISSE
;
;
PROCEDURE INPUT LBQCPTENC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE    = T_CIECLEMNU
    LET T_LBQCPTENC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc110 PASSING T_CIECLE, T_LBQCPTENC MODE F
    LET FIELDTEXT = TRUNCATE(T_LBQCPTENC)
  END
END

PROCEDURE EDIT LBQCPTENC
BEGIN
  IF 0 <> SIZE(TRUNCATE(LBQCPTENC OF CPLOT))
  THEN BEGIN
    GET MCLIVRET_BQ OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 569 ; Ce compte n'est pas un compte d'encaisse.
  END
END

;-------------------------------------------------------------------------------
;
; CONSULTATION DES DEVISES
;
;
PROCEDURE INPUT LCPDEVCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DEVCLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc101 PASSING T_DEVCLE MODE F
    LET FIELDTEXT = TRUNCATE(T_DEVCLE)
  END
END

PROCEDURE EDIT LCPDEVCLE
BEGIN
  IF 0 <> SIZE(TRUNCATE(LCPDEVCLE OF CPLOT))
  THEN BEGIN
    GET MCDEVISE OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 418 ; Cette devise est inexistante.
  END
END

;-------------------------------------------------------------------------------
;
; VALIDATION DE LA SÉCURITÉ EN SAISIE
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
;
; SAISIE DES PARAMÈTRES
;
;
PROCEDURE ENTRY
BEGIN
  ;
  ; Si une sélection à déja été faite pour ce lot, on ne peut plus changer
  ; le type de référence. Ceci est fait pour ne pas mélanger des factures
  ; avec des relevés de dépenses car l'écran CP007B ne peut pas gérer les deux
  ; en même temps.
  ;
  IF LCPDATFIN OF CPLOT = 0
  THEN ACCEPT LCPREFTYP OF CPLOT
  DISPLAY CBIDSC OF A_CBI_LCPREFTYP
  DISPLAY D_REFNOM
  ACCEPT LCPREFNUM OF CPLOT
  DISPLAY REFNOMABR OF MCREFERENCE
  IF LCPREFNUM OF CPLOT <> ""
  THEN BEGIN
    ACCEPT LCPCAPCLE OF CPLOT
  END
  ELSE BEGIN
    LET LCPCAPCLE OF CPLOT = ""
    DISPLAY LCPCAPCLE OF CPLOT
    EDIT LCPCAPCLE OF CPLOT
  END
  ;
  ; Catégorie
  ;
  IF LCPREFTYP OF CPLOT = "F"
  THEN
    BEGIN
      ;
      ; Si l'usager à saisi un numéro de fournisseur on affiche les
      ; informations de sélections de ce fournisseur
      ;
      IF LCPREFNUM OF CPLOT <> ""
      THEN
        BEGIN
          LET LCPFOUCAT OF CPLOT = REFCAT OF MCREFERENCE
          DISPLAY LCPFOUCAT OF CPLOT
          EDIT LCPFOUCAT OF CPLOT
          GET VFOC_FOU &
              VIA   FOUCIECLE, &
                    FOUCLE   , &
                    FOCCIECLE  &
              USING D_FOUEMPCIECLE    , &
                    LCPREFNUM OF CPLOT, &
                    T_CIECLEMNU
          LET LBQCPTENC OF CPLOT = LBQCPTENC OF VFOC_FOU
          DISPLAY LBQCPTENC OF CPLOT
          EDIT LBQCPTENC OF CPLOT
          LET LCPDEVCLE OF CPLOT = DEVCLE OF VFOC_FOU
          DISPLAY LCPDEVCLE OF CPLOT
          EDIT LCPDEVCLE OF CPLOT
        END
      ELSE
        BEGIN
          ACCEPT LCPFOUCAT OF CPLOT
        END
    END
  ELSE
    BEGIN
      ;
      ; La catégorie ne s'applique pas pour les employés
      ;
      LET LCPFOUCAT OF CPLOT = ""
      DISPLAY LCPFOUCAT OF CPLOT
      EDIT LCPFOUCAT OF CPLOT
    END

  DISPLAY CBIDSC OF A_CBI_LCPFOUCAT

  ;
  ; type de document
  ;
  IF LCPCAPCLE OF CPLOT <> ""
  THEN
    BEGIN
      LET LCPTYPDOC OF CPLOT = CAPTYPECR OF CPCPT_A_PAYER
      DISPLAY LCPTYPDOC OF CPLOT
      EDIT LCPTYPDOC OF CPLOT
    END
  ELSE
    BEGIN
      IF LCPREFTYP = "E"
      THEN
        BEGIN
          LET LCPTYPDOC OF CPLOT = "RD"
          DISPLAY LCPTYPDOC OF CPLOT
          EDIT LCPTYPDOC OF CPLOT
        END
      ELSE
        BEGIN
          ACCEPT LCPTYPDOC OF CPLOT
        END
    END

  DISPLAY CBIDSC OF A_CBI_LCPTYPDOC

  ACCEPT LCPDATDEB OF CPLOT
  ACCEPT LCPDATFIN OF CPLOT
  IF LCPDATFIN = 0
  THEN ERROR 511 ; La date de fin est obligatoire...

  IF LCPDATPAM = 0 OR LCPCAPCLE <> ""
  THEN BEGIN
    LET LCPSELESC = "0"
    DISPLAY LCPSELESC
  END
  ELSE BEGIN
    ACCEPT LCPSELESC OF CPLOT
  END

  ;
  ; Si on à demandé un fournisseur ou un employé en particulier, alors on
  ; ne donne pas accès au compte d'encaisse et à la devise. Car le fournisseur
  ; possède son propre compte d'encaisse, et le fournisseur et l'employé ont
  ; chacun leur devise.
  ;
  IF LCPREFNUM OF CPLOT = ""
  THEN
    BEGIN
      IF LCPREFTYP OF CPLOT = "F"
      THEN
        BEGIN
          ACCEPT LBQCPTENC OF CPLOT
@IF FRANCAIS
          DISPLAY CPTDSCABRFRA OF MCCOMPTE
@ELSE
          DISPLAY CPTDSCABRANG OF MCCOMPTE
@ENDIF
        END
      ACCEPT LCPDEVCLE OF CPLOT
@IF FRANCAIS
      DISPLAY DEVDSCFRA OF MCDEVISE
@ELSE
      DISPLAY DEVDSCANG OF MCDEVISE
@ENDIF
    END
END

;-------------------------------------------------------------------------------
;
; Ajout de la ligne de cédule dans la liste des cédules sélectionnés
;
;

PROCEDURE INTERNAL PUT_CPSEL_CAP
BEGIN
  LET CIECLE    OF CPSEL_CAP  = CIECLE OF CPLOT
  LET PECCLE    OF CPSEL_CAP  = PECCLE    OF CPLOT
  LET LCPCLE    OF CPSEL_CAP  = LCPCLE    OF CPLOT
  LET FOUCIECLE OF CPSEL_CAP  = FOUCIECLE OF VCAP_SEL
  LET FOUCLE    OF CPSEL_CAP  = FOUCLE    OF VCAP_SEL
  LET CAPCLE    OF CPSEL_CAP  = CAPCLE    OF VCAP_SEL
  LET CPCCLELIG OF CPSEL_CAP  = CPCCLELIG OF VCAP_SEL
  LET SLCMNT    OF CPSEL_CAP  = V_CPCSLD  OF VCAP_SEL

  LET SLCMNTPYE OF CPSEL_CAP  = V_CPCSLD  OF VCAP_SEL &
                              - SLCMNTESC OF CPSEL_CAP

  LET LCPCUMMNT OF CPLOT      = LCPCUMMNT OF CPLOT &
                              + SLCMNTPYE OF CPSEL_CAP

  LET LCPCUMESC OF CPLOT      = LCPCUMESC OF CPLOT &
                              + SLCMNTESC OF CPSEL_CAP
  PUT CPSEL_CAP RESET
END

;-------------------------------------------------------------------------------

PROCEDURE INTERNAL TEST_CEDULE
BEGIN
  ;
  ; La cédule est sélectionnée et on peut calculer de l'escompte
  ; Note : pour calculer de l'escompte, la cédule doit provenir d'une
  ; facture (FA), celle-ci ne doit pas avoir été payé (ni totalement, ni en
  ; partie), et ses cédules de paiements ne doivent pas avoir été modifiées.
  ; Et évidement, les dates d'escomptes doivent correspondre avec la date de
  ; paiement.
  ;
  IF    CAPFLGCED   OF VCAP_SEL = "0"  &
    AND V_CAPMNTPYE OF VCAP_SEL = 0    &
    AND CAPTYPECR   OF VCAP_SEL = "FA" &
    AND (   (    CAPDATESC1 OF VCAP_SEL >= LCPDATDEB OF CPLOT &
             AND CAPDATESC1 OF VCAP_SEL <= LCPDATFIN OF CPLOT &
             AND CAPDATESC1 OF VCAP_SEL >= LCPDATPAM OF CPLOT ) &
         OR (    CAPDATESC2 OF VCAP_SEL >= LCPDATDEB OF CPLOT &
             AND CAPDATESC2 OF VCAP_SEL <= LCPDATFIN OF CPLOT &
             AND CAPDATESC2 OF VCAP_SEL >= LCPDATPAM OF CPLOT ))
  THEN
    IF    CAPDATESC1 OF VCAP_SEL >= LCPDATDEB OF CPLOT &
      AND CAPDATESC1 OF VCAP_SEL <= LCPDATFIN OF CPLOT &
      AND CAPDATESC1 OF VCAP_SEL >= LCPDATPAM OF CPLOT
    THEN BEGIN
      GET CPTERME &
        VIA   TERCLE &
        USING CAPTERESC1 OF VCAP_SEL &
        OPTIONAL
      IF ACCESSOK
      THEN BEGIN
        ;
        ; On peut calculer de l'escompte avec le premier terme
        ; d'escompte.
        ;
        LET SLCMNTESC OF CPSEL_CAP = ROUND (  CAPMNTNET OF VCAP_SEL &
                                            * TERPCT    OF CPTERME)
        DO INTERNAL PUT_CPSEL_CAP
      END
     END
    ELSE BEGIN
      GET CPTERME &
        VIA   TERCLE &
        USING CAPTERESC2 OF VCAP_SEL &
        OPTIONAL
      IF ACCESSOK
      THEN BEGIN
        ;
        ; On peut calculer de l'escompte avec le deuxiemme terme
        ; d'escompte.
        ;
        LET SLCMNTESC OF CPSEL_CAP = ROUND (  CAPMNTNET OF VCAP_SEL &
                                            * TERPCT    OF CPTERME)
        DO INTERNAL PUT_CPSEL_CAP
      END
    END
  ELSE
    ;
    ; La cédule est sélectionnée mais on ne peut pas
    ; calculer d'escompte.
    ;
    IF     LCPSELESC OF CPLOT = "0" &
       AND CPCDATDUE OF VCAP_SEL >= LCPDATDEB OF CPLOT &
       AND CPCDATDUE OF VCAP_SEL <= LCPDATFIN OF CPLOT
    THEN DO INTERNAL PUT_CPSEL_CAP
END

;-------------------------------------------------------------------------------

PROCEDURE INTERNAL CREER_SELECTION
BEGIN

  LET T_TMPLCPREFNUM = LCPREFNUM OF CPLOT
  LET T_TMPLCPREFTYP = LCPREFTYP OF CPLOT
  LET T_TMPLCPCAPCLE = LCPCAPCLE OF CPLOT
  LET T_TMPLCPFOUCAT = LCPFOUCAT OF CPLOT
  LET T_TMPLCPTYPDOC = LCPTYPDOC OF CPLOT
  LET T_TMPLCPDEVCLE = LCPDEVCLE OF CPLOT

  IF 0 = SIZE(TRUNCATE(T_TMPLCPREFNUM))
  THEN LET T_TMPLCPREFNUM = "@"

  IF 0 = SIZE(TRUNCATE(T_TMPLCPREFTYP))
  THEN LET T_TMPLCPREFTYP = "@"

  IF 0 = SIZE(TRUNCATE(T_TMPLCPCAPCLE))
  THEN LET T_TMPLCPCAPCLE = "@"

  IF 0 = SIZE(TRUNCATE(T_TMPLCPFOUCAT))
  THEN LET T_TMPLCPFOUCAT    = "@"

  IF 0 = SIZE(TRUNCATE(T_TMPLCPTYPDOC))
  THEN LET T_TMPLCPTYPDOC = "@"

  IF 0 = SIZE(TRUNCATE(T_TMPLCPDEVCLE))
  THEN LET T_TMPLCPDEVCLE    = "@"

  ; On parcours toutes les cédules de paiements correspondantes aux
  ; paramètres saisis par l'usager. La vue ne renvoie une cédule que
  ; si elle n'est pas encore sélectionnée ( par une autre sélection
  ; ou par un paiement ), et s'il reste encore un montant à payer.
  ; Par défaut on sélectionne toutes les pièces à payer. Mais si le flag
  ; "Escompte uniquement" est à OUI, alors on ne sélectionne que les
  ; pièces sur lesquelles on peut prendre de l'escompte.
  ;
  ; Note : L'escompte se calcule sur le montant NET et seulement si
  ;        la cédule de paiement n'a pas été altérée et si c'est le
  ;        premier paiement de la pièce.

  WHILE RETRIEVING VCAP_SEL &
      VIA   CAPCIECLE, &
            REFCLETYP, &
            REFCLENUM, &
            REFCAT   , &
            CAPTYPECR, &
            DEVCLE     &
      USING T_CIECLEMNU   , &
            T_TMPLCPREFTYP, &
            T_TMPLCPREFNUM, &
            T_TMPLCPFOUCAT, &
            T_TMPLCPTYPDOC, &
            T_TMPLCPDEVCLE
  BEGIN
    ;
    ; Si on paye des cédule venant de fournisseurs (c'est à dire tout sauf
    ; des cédules de relevés de dépenses) et que l'usager à demandé une
    ; sélection par compte de banque (LBQCPTENC) alors on retourne
    ; sur le fournisseur pour voir son compte de banque.
    ; Note : On ne peut pas mettre ce compte de banque dans la vue car
    ; on ne peut pas faire d'acces optionel avec du GDML (language d'Interbase)
    ;
    IF    LCPREFTYP OF CPLOT  = "F" &
      AND LCPREFNUM OF CPLOT  = "" &
      AND LBQCPTENC OF CPLOT <> ""
    THEN
      BEGIN
        ;
        ; Si le fournisseur existe et utilise le compte d'encaisse demandé
        ; pour la compagnie en cours, alors on sélectionne la cédule.
        ;
        GET VFOC_FOU &
          VIA       FOUCIECLE, &
                    FOUCLE   , &
                    FOCCIECLE, &
                    LBQCPTENC  &
          USING     FOUCIECLE OF VCAP_SEL, &
                    FOUCLE    OF VCAP_SEL, &
                    CAPCIECLE OF VCAP_SEL, &
                    LBQCPTENC OF CPLOT     &
          OPTIONAL
        IF ACCESSOK
        THEN DO INTERNAL TEST_CEDULE
      END
    ELSE
      DO INTERNAL TEST_CEDULE
  END
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPLOT &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CPLOT &
     AND NOT NEWRECORD     OF CPLOT &
     AND NOT DELETEDRECORD OF CPLOT &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  DO INTERNAL CREER_SELECTION
END

PROCEDURE POSTUPDATE
BEGIN
  RETURN
END

BUILD
@IF FORMAT

************************ Paramètres ************************
* Type référence: x      xxxxxxxxxxxxxxxxxxxxxxxxx         *
* xxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx              *
* No document...: xxxxxxxxxxxxxxx                          *
* Catégorie.....: xxxx   xxxxxxxxxxxxxxxxxxxxxxxxx         *
* Type document.: xx     xxxxxxxxxxxxxxxxxxxxxxxxx         *
* Date début....: xxxxxxxx                                 *
* Date de fin...: xxxxxxxx                                 *
* Escompte uniq.: xxx                                      *
* Cpt encaisse..: xxxxxx xxxxxxxxxxxxxxxxxxxx              *
* Devise........: xxx    xxxxxxxxxxxxxxx                   *
************************************************************

@ENDIF
