;/T/ Liste de débitage par commande interne (DÉTAILLÉ et RÉSUMÉ)
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-10
;
;    Description...: Ce rapport permet d'imprimer de débitage des diagrammes
;                    pour une commande interne.
;
;    Paramètres....: ...
;
;    Particularités: Ii y a 2 sources.
;/M/ Programmeur...:Rémy Demers
;/   Date..........:97-12-04
;
;    Description...:Modification mineur apporter sur les liens car le subfile
;                   est maintenant plus complet
;
;/ATTENTION........:LE SUBFILE PDTEMPO_SCIAGE DOIT ETRE DANS [EXF] AFFIN
;                   DE COMPILER CE PROG,ENSUIT VOUS POUVEZ LE SUPPRIMER
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDTEMPO_SCIAGE &

  LINK DIANUM002 OF PDTEMPO_SCIAGE , &
       CIECLE    OF PDTEMPO_SCIAGE , &
       PRICODPRI  OF PDTEMPO_SCIAGE  &
     TO DIANUM002 ,                 &
        CIECLE    , &
        PRICODPRI OF PDPRIORITE_DIAG &

  LINK PJTABR       OF PDPRIORITE_DIAG, &
       PJTANN       OF PDPRIORITE_DIAG, &
       COMNUMCOMINT OF PDPRIORITE_DIAG, &
       PRICODPRI    OF PDPRIORITE_DIAG, &
       CIECLE       OF PDPRIORITE_DIAG &
    TO PJTABR                      , &
       PJTANN                      , &
       COMNUMCOMINT                , &
       PRICODPRI                   , &
       CIECLE OF PDPRIORITE        &

   LINK DIANUM002 OF PDTEMPO_SCIAGE , &
        CIECLE    OF PDTEMPO_SCIAGE   &
     TO DIANUM002                  , &
        CIECLE    OF PDDIAGRAMME       &

  LINK PJTABR       OF PDDIAGRAMME, &
       PJTANN       OF PDDIAGRAMME, &
       COMNUMCOMINT OF PDDIAGRAMME, &
       CIECLE       OF PDDIAGRAMME &
    TO PJTABR, &
       PJTANN, &
       COMNUMCOMINT, &
       CIECLE       OF PDCOMMAN_INTERNE &

   LINK TGRCODGRA OF PDDIAGRAMME , &
        CIECLE    OF PDDIAGRAMME       &
     TO TGRCODGRA , &
        CIECLE OF PDTYPE_GRANIT     &

   LINK TYFCODFIN OF PDDIAGRAMME, &
        CIECLE OF PDDIAGRAMME       &
     TO TYFCODFIN , &
        CIECLE OF PDTYPE_FINI

DEFINE D_PRI_LIG CHAR SIZE 12 =   TGRCODGRA OF PDDIAGRAMME    &
                                + DCINUMLIG OF PDTEMPO_SCIAGE &
                                + TYFCODFIN OF PDDIAGRAMME    &
                                + ASCII(DIAEPA OF PDDIAGRAMME,4)

SORT  ON COMNUMCOM OF PDTEMPO_SCIAGE &
      ON PRICODPRI OF PDTEMPO_SCIAGE &
      ON D_PRI_LIG                   &
      ON DIANUM002 OF PDTEMPO_SCIAGE


;-------------------------------------------------------------------------------
;
;
;
;
DEFINE D_DIALON NUMERIC            = DIALON OF PDDIAGRAMME + 4
DEFINE D_DIAHAU NUMERIC            = DIAHAU OF PDDIAGRAMME + 4

DEFINE D_QTE_A_DBT_1 NUMERIC       = DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_SCIAGE &
                                     IF 0 < DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_SCIAGE &
                                     ELSE 0

DEFINE D_QTE_A_DBT_2 NUMERIC       = PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_SCIAGE &
                                     IF 0 < PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_SCIAGE &
                                     ELSE 0

DEFINE D_QTE_A_DBT NUMERIC         = D_QTE_A_DBT_1 IF D_QTE_A_DBT_1 < D_QTE_A_DBT_2 ELSE D_QTE_A_DBT_2

SELECT IF D_QTE_A_DBT > 0

DEFINE D_VOLMC2 FLOAT SIZE 08      = D_QTE_A_DBT * DIASURMC2 OF PDDIAGRAMME
DEFINE D_ETOILE CHARACTER SIZE 1   = "*" IF DIATYPDIA OF PDDIAGRAMME = "T" ELSE " "

DEFINE D_VIRG_1 CHARACTER SIZE 02  = " ," IF T_DESCR_2 <> " " ELSE " "
DEFINE D_VIRG_2 CHARACTER SIZE 02  = " ," IF T_DESCR_3 <> " " ELSE " "
DEFINE D_VIRG_3 CHARACTER SIZE 02  = " ," IF T_DESCR_4 <> " " ELSE " "
DEFINE D_VIRG_4 CHARACTER SIZE 02  = " ," IF T_DESCR_5 <> " " ELSE " "

DEFINE D_DESCR  CHARACTER SIZE 48  = TRUNCATE(T_DESCR_1) + &
                                     TRUNCATE(D_VIRG_1) + &
                                     TRUNCATE(T_DESCR_2) + &
                                     TRUNCATE(D_VIRG_2) + &
                                     TRUNCATE(T_DESCR_3) + &
                                     TRUNCATE(D_VIRG_3) + &
                                     TRUNCATE(T_DESCR_4) + &
                                     TRUNCATE(D_VIRG_4) + &
                                     TRUNCATE(T_DESCR_5) IF T_DESCR_1 <> " " &
                                     ELSE "EMBALLAGE"


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de débitage des diagrammes par commande interne"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD640ZA"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

USE ustitgor200_45 NOLIST

 SKIP &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDPRIORITE&
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDPRIORITE &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 001 "Type Granit...:" &
  TAB 017 TGRCODGRA OF PDDIAGRAMME &
  TAB 024 TGRDSCFRA001 OF PDTYPE_GRANIT &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 001 "Type fini.....:" &
  TAB 017 TYFCODFIN OF PDDIAGRAMME &
  TAB 024 TYFDSCFRA001 OF PDTYPE_FINI &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 001 "Épaisseur.....:" &
  TAB 017 DIAEPA OF PDDIAGRAMME &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP &
  TAB 001 "Priorité......:" &
  TAB 017 PRICODPRI OF PDPRIORITE &
  TAB 021 PRIDATFIN OF PDPRIORITE &
  SKIP 2 &
  TAB 001 "T  Diag.    Longueur      Hauteur       Quantité    Reste à faire" &
  SKIP 2


FOOTING AT DIANUM002 &
  TAB 001 D_ETOILE &
  TAB 004 DIANUMDIA002 OF PDDIAGRAMME &
  TAB 013 DIALON OF PDDIAGRAMME &
  TAB 018 "(" &
  TAB 019 D_DIALON PICTURE "^^^^" &
  TAB 023 ")" &
  TAB 027 DIAHAU OF PDDIAGRAMME &
  TAB 032 "(" &
  TAB 033 D_DIAHAU PICTURE "^^^^" &
  TAB 037 ")" &
  TAB 043 D_QTE_A_DBT PICTURE "^^^^^" &
  TAB 053 "__________________________________________________" &
  SKIP &
  TAB 053 "(" &
  TAB 054 D_DESCR &
  TAB 102 ")" &
  SKIP 2

FOOTING AT D_PRI_LIG &
  SKIP 2 &
  TAB 013 "M2 total :" &
  TAB 024  D_VOLMC2 SUBTOTAL OUTPUT SCALE 4 SIGNIFICANCE 6 PICTURE "^^^^^^.^^^^" &
  SKIP PAGE

FOOTING AT COMNUMCOM SKIP PAGE
FOOTING AT PRICODPRI SKIP PAGE

PAGE FOOTING &
TAB 003 "* Piece triangulaire....Ajouter 5mm aux dimensions"


BUILD pd640za

@IF FORMAT

PD640ZA                                                     GRANICOR INC                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX            Liste de débitage des diagrammes par commande interne

Num. commande.: XX-999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
Type Granit...: 999    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Épaisseur en +: 9999   Épaisseur en -: 9999
Type fini.....: 99     XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Dimension en +: 9999   Dimension en -: 9999
Épaisseur.....: 9999                                                  Critères esthé:
Priorité......: 99  AA/MM/JJ

T  Diag.    Longueur      Hauteur       Quantité    Reste à faire

*  XXXXX    9999 (9999)   9999 (9999)     99999     __________________________________________________
                                                    (XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)

*  XXXXX    9999 (9999)   9999 (9999)     99999     __________________________________________________
                                                    (XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)

*  XXXXX    9999 (9999)   9999 (9999)     99999     __________________________________________________
                                                    (XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)

*  XXXXX    9999 (9999)   9999 (9999)     99999     __________________________________________________
                                                    (XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)

*  XXXXX    9999 (9999)   9999 (9999)     99999     __________________________________________________
                                                    (XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)

            M2 total : ^^^^^^.^^^^        99999 Mcx

@ENDIF

;/T/ Résumé de débitage par commande interne
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-18
;
;    Description...: Ce rapport permet d'imprimer de débitage des diagrammes
;                    pour une commande interne.
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date.............: 10 mai 2000
;    Description......: Dans le cadre de la correction du calcul des quantités à débiter si associées à plus d'une priorité, ajustement 
;                       du calcul des quantités totales.
;
;    Référence........: Demande de changement 000053.
;    
;    Signet...........: MP-00-05-10_D53.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDTEMPO_SCIAGE &

  LINK DIANUM002 OF PDTEMPO_SCIAGE , &
       CIECLE    OF PDTEMPO_SCIAGE , &
       PRICODPRI  OF PDTEMPO_SCIAGE  &
     TO DIANUM002 ,                 &
        CIECLE    , &
        PRICODPRI OF PDPRIORITE_DIAG &

  LINK PJTABR       OF PDPRIORITE_DIAG, &
       PJTANN       OF PDPRIORITE_DIAG, &
       COMNUMCOMINT OF PDPRIORITE_DIAG, &
       PRICODPRI    OF PDPRIORITE_DIAG, &
       CIECLE       OF PDPRIORITE_DIAG &
    TO PJTABR                      , &
       PJTANN                      , &
       COMNUMCOMINT                , &
       PRICODPRI                   , &
       CIECLE OF PDPRIORITE        &

   LINK DIANUM002 OF PDTEMPO_SCIAGE , &
        CIECLE    OF PDTEMPO_SCIAGE   &
     TO DIANUM002                  , &
        CIECLE    OF PDDIAGRAMME       &

  LINK PJTABR       OF PDDIAGRAMME, &
       PJTANN       OF PDDIAGRAMME, &
       COMNUMCOMINT OF PDDIAGRAMME, &
       CIECLE       OF PDDIAGRAMME &
    TO PJTABR, &
       PJTANN, &
       COMNUMCOMINT, &
       CIECLE       OF PDCOMMAN_INTERNE &

   LINK TGRCODGRA OF PDDIAGRAMME , &
        CIECLE    OF PDDIAGRAMME       &
     TO TGRCODGRA , &
        CIECLE OF PDTYPE_GRANIT     &

   LINK TYFCODFIN OF PDDIAGRAMME, &
        CIECLE OF PDDIAGRAMME       &
     TO TYFCODFIN , &
        CIECLE OF PDTYPE_FINI

;DEFINE D_PRI_LIG CHAR SIZE 6 = TGRCODGRA OF PDDIAGRAMME + DCINUMLIG OF PDTEMPO_SCIAGE
DEFINE D_PRI_LIG CHAR SIZE 12 =   TGRCODGRA OF PDDIAGRAMME    &
                                + DCINUMLIG OF PDTEMPO_SCIAGE &
                                + TYFCODFIN OF PDDIAGRAMME    &
                                + ASCII(DIAEPA OF PDDIAGRAMME,4)


SORT ON COMNUMCOM &
     ON PRICODPRI &
     ON D_PRI_LIG



;-------------------------------------------------------------------------------
;
;
;
;
DEFINE D_DIALON NUMERIC            = DIALON OF PDDIAGRAMME + 4
DEFINE D_DIAHAU NUMERIC            = DIAHAU OF PDDIAGRAMME + 4

DEFINE D_QTE_A_DBT_1 NUMERIC       = DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_SCIAGE &         ; DIAQTEEMB OF PDDIAGRAMME &
                                     IF 0 < DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_SCIAGE &  ; DIAQTEEMB OF PDDIAGRAMME &
                                     ELSE 0

DEFINE D_QTE_A_DBT_2 NUMERIC       = PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_SCIAGE &         ;PDGQTECPEPRDGRA  OF PDPRIORITE_DIAG &
                                     IF 0 < PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_SCIAGE &         ;PDGQTECPEPRDGRA  OF PDPRIORITE_DIAG &
                                     ELSE 0

DEFINE D_QTE_A_DBT NUMERIC         = D_QTE_A_DBT_1 IF D_QTE_A_DBT_1 < D_QTE_A_DBT_2 ELSE D_QTE_A_DBT_2

SELECT IF D_QTE_A_DBT > 0

DEFINE D_VOLMC2 FLOAT SIZE 08      = D_QTE_A_DBT * DIASURMC2 OF PDDIAGRAMME
DEFINE D_ETOILE CHARACTER SIZE 1   = "*" IF DIATYPDIA OF PDDIAGRAMME = "T" ELSE " "

DEFINE D_VIRG_1 CHARACTER SIZE 02  = " ," IF T_DESCR_2 <> " " ELSE " "
DEFINE D_VIRG_2 CHARACTER SIZE 02  = " ," IF T_DESCR_3 <> " " ELSE " "
DEFINE D_VIRG_3 CHARACTER SIZE 02  = " ," IF T_DESCR_4 <> " " ELSE " "
DEFINE D_VIRG_4 CHARACTER SIZE 02  = " ," IF T_DESCR_5 <> " " ELSE " "

DEFINE D_DESCR  CHARACTER SIZE 48  = TRUNCATE(T_DESCR_1) + &
                                     TRUNCATE(D_VIRG_1) + &
                                     TRUNCATE(T_DESCR_2) + &
                                     TRUNCATE(D_VIRG_2) + &
                                     TRUNCATE(T_DESCR_3) + &
                                     TRUNCATE(D_VIRG_3) + &
                                     TRUNCATE(T_DESCR_4) + &
                                     TRUNCATE(D_VIRG_4) + &
                                     TRUNCATE(T_DESCR_5) IF T_DESCR_1 <> " " &
                                     ELSE "EMBALLAGE"


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de débitage des diagrammes par commande interne"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD640ZB"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitstd NOLIST
 ; SKIP 1

;PAGE HEADING &
;skip &
;  TAB 001 SYSDATE &
;  TAB 012 SYSTIME &
;  TAB 120 "Page.:" &
;  TAB 126 SYSPAGE &
; SKIP &
;  TAB 001 "PD640ZA" &
;  TAB 010 LOGONID &
 SKIP &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDPRIORITE &
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDPRIORITE &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP 2 &
  TAB 001 "Prio Type granit                              Type fini                                Épai     Surface   Qte" &
  SKIP 2


FOOTING AT D_PRI_LIG &
SKIP &
  TAB 003 PRICODPRI OF PDTEMPO_SCIAGE &
  TAB 006 TGRDSCFRA001 OF PDTYPE_GRANIT &
  TAB 047 TYFDSCFRA001 OF PDTYPE_FINI &
  TAB 088 DIAEPA OF PDDIAGRAMME &
  TAB 093 D_VOLMC2 SUBTOTAL OUTPUT SCALE 4 SIGNIFICANCE 6 PICTURE "^^^^^^.^^^^" &
  TAB 105 D_QTE_A_DBT SUBTOTAL PICTURE "^^^^^"

HEADING AT COMNUMCOM SKIP PAGE

FINAL FOOTING &
 TAB 093 "-----------" &
 TAB 105 "-----" &
 SKIP &
 TAB 093 D_VOLMC2 SUBTOTAL OUTPUT SCALE 4 SIGNIFICANCE 6 PICTURE "^^^^^^.^^^^" &
 TAB 105 D_QTE_A_DBT SUBTOTAL PICTURE "^^^^^" ; MP-00-05-10_D53.


BUILD pd640zb

@IF FORMAT

PD640ZB                                                     GRANICOR INC                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX            Liste de débitage des diagrammes par commande interne

Num. commande.: XX-999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
                                                                      Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
                                                                      Critères esthé:

Prio Type granit                              Type fini                                Épai     Surface   Qte

  99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 9999 999999.9999 99999
  99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 9999 999999.9999 99999
  99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 9999 999999.9999 99999
  99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 9999 999999.9999 99999
  99 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 9999 999999.9999 99999

            M2 total :                                                                      999999.9999 99999     Mcx

@ENDIF
