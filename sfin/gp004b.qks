;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de projet - Information opération sommaire (FORMAT).
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp004b ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPFRM_AFFICHE, &
                        GPPROJETS,     &
                        T_FRDDSC, &
                        T_MNTREE, &
                        T_MNTBDG, &
                        T_MNTDIS, &
                        T_UNMABR


TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_FRDDSC    CHARACTER SIZE 45
TEMPORARY T_MNTREE    CHARACTER SIZE 17
TEMPORARY T_MNTBDG    CHARACTER SIZE 17
TEMPORARY T_MNTDIS    CHARACTER SIZE 17
TEMPORARY T_UNMABR    CHARACTER SIZE 3

USE ussectmp NOLIST     ; Variable pour la securite
    "GP004B"            ; Nom de l'écran

USE gp004b.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran



FILE GPFRM_AFFICHE          MASTER



;------------------------------------
; Fichier fiche de projet
;------------------------------------
FILE GPPROJETS              MASTER



FILE GPFRM_DETAILS          PRIMARY   &
                            OCCURS 12 &
                            NOITEMS

   ACCESS SEQUENTIAL
   SELECT IF PRJCLE OF GPFRM_DETAILS <> " "


FILE GPPRO_ACTIVITE         REFERENCE

  ACCESS  VIA   CIECLE, &
                PRJCLE, &
                PRACLE  &
          USING CIECLE OF GPFRM_DETAILS, &
                PRJCLE OF GPFRM_DETAILS, &
                PRACLE OF GPFRM_DETAILS OPTIONAL


;-------------------------------
; Fichier unité de travail
;-------------------------------
FILE GPUNITE_TRAVAIL        REFERENCE

  ACCESS  VIA   UNTCLE  &
          USING UNTCLE OF GPFRM_DETAILS



;-------------------------------
; Fichier unité de mesure
;-------------------------------
FILE GPUNITE_MESURE         REFERENCE

  ACCESS  VIA   UNMCLE  &
          USING UNMCLE OF GPUNITE_TRAVAIL


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPUNITE_TRAVAIL


FILE GPPRO_BDG_UNT          DESIGNER
FILE GPPRA_BDG_UNT          DESIGNER
;-------------------------------------------------------------------------------
;
DEFINE D_MNTDIS      FLOAT SIZE 8 = FDEBDG OF GPFRM_DETAILS - &
                                    FDEREE OF GPFRM_DETAILS

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_CIECLE     CHAR*4
TEMPORARY T_PRJCLE     CHAR*8
TEMPORARY T_PRACLE     CHAR*3
TEMPORARY T_CPTCLE     CHAR*6
TEMPORARY T_UNACLE     CHAR*8
TEMPORARY T_UNTCLE     CHAR*4

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Détail de la ligne du format de présentation " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,28) (,92,103) (,,114)
FIELD PRJCLE    OF GPPRO_ACTIVITE                     &
label "Projet.....:" &
      DISPLAY

FIELD PRJDSC1   OF GPPROJETS                          &
      DISPLAY

FIELD PRJPBCDATAPP OF GPPROJETS                        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
      LABEL "Date app.:"                              &
      DISPLAY

FIELD PRJPBCUSRAPP OF GPPROJETS                       &
      DISPLAY

ALIGN (,3,19) (,,28)
FIELD PRACLE    OF GPFRM_DETAILS                      &
      DISPLAY

FIELD PRADSC1   OF GPPRO_ACTIVITE                     &
      DISPLAY

SKIP 1
TITLE &
@IF FRANCAIS
      "Titre"  &
@ELSE
      " %%% " &
@ENDIF
      AT ,3


TITLE &
@IF FRANCAIS
      "Budget" &
@ELSE
      " %%% " &
@ENDIF
      AT ,65

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,90

TITLE &
@IF FRANCAIS
      "Disponibilité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,106


SKIP

ALIGN (,,3) (,,57) (,,75) (,,80) (,,98) (,,105) (,,123)

FIELD T_FRDDSC                      DISPLAY
FIELD T_MNTBDG                      DISPLAY
FIELD T_UNMABR                      DISPLAY
FIELD T_MNTREE                      DISPLAY
FIELD T_UNMABR                      DISPLAY
FIELD T_MNTDIS                      DISPLAY
FIELD T_UNMABR                      DISPLAY

DRAW 9,1 TO 9,132

SKIP TO 10

TITLE &
@IF FRANCAIS
      "Unité de travail" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Compte  Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,36

TITLE &
@IF FRANCAIS
      "Budget" &
@ELSE
      " %%% " &
@ENDIF
      AT ,75

TITLE &
@IF FRANCAIS
      "Réel         Disponibilité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,99

SKIP

ALIGN (,,3) (,,9) (,,36) (,,44) (,,65) (,,83) (,,87) (,,105) (,,109) (,,127)

CLUSTER OCCURS WITH GPFRM_DETAILS

FIELD UNTCLE OF GPFRM_DETAILS                         &
      DISPLAY

FIELD UNTDSC OF GPUNITE_TRAVAIL                       &
      DISPLAY                                         &
      SIZE 25

FIELD CPTCLE OF GPUNITE_TRAVAIL                       &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD FDEBDG OF GPFRM_DETAILS                      &
      DISPLAY

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

FIELD FDEREE OF GPFRM_DETAILS                      &
      DISPLAY

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

FIELD D_MNTDIS                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^^^ "                     &
      TRAILING SIGN "-" SIGNIFICANCE 5

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

CLUSTER



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST

  CLOSE GPFRM_DETAILS
  RUN SCREEN gp004c MODE F PASSING GPFRM_AFFICHE
  REFRESH ALL
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD LIST DETAIL
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

