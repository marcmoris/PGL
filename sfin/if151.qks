;/T/ Couleur de Granit(carte de bloc)
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 16 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base   
;                   concernant la couleur de granit  
;
;
SCREEN if151 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF151"

;USE if151.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFCOULEUR    IN DICT PRIMARY NOITEM &
                      OCCURS 16 TIMES

  ACCESS VIA     CIECLE,   &
                 CLRCLE    &
         USING   T_CIECLE, &
                 CLRCLE    &  
         REQUEST CLRCLE    &
         ORDERBY CLRCLE  

  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY CLRCLE

  ITEM CIECLE OF IFCOULEUR INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Couleur de granit (carte de bloc) " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST


TITLE "Couleur" AT ,5
TITLE "Desc. francaise" AT ,18
TITLE "Desc. anglaise" AT ,46
SKIP

ALIGN(5,6,7) (,,18) (,,46)
CLUSTER OCCURS WITH IFCOULEUR
FIELD CLRCLE OF IFCOULEUR &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFCOULEUR &
          VIA   CIECLE, &
                CLRCLE  &
          USING T_CIECLE, &
                CLRCLE OF IFCOULEUR &
          MESSAGE 3236 ; PD336 Cette couleur existe déjà

FIELD CLRDSCFRA OF IFCOULEUR &
      REQUIRED

FIELD CLRDSCANG OF IFCOULEUR 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Valeur par defaut pour la description anglaise
;
PROCEDURE INPUT CLRDSCANG
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = CLRDSCFRA OF IFCOULEUR
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un code de couleur
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN if151x MODE SAME        &
                   PASSING IFCOULEUR, &
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3244 ; PD344 Impossible de supprimer cette couleur,
                     ; elle est d\351j\340 utilis\351e
   ELSE DELETE IFCOULEUR
END
BUILD
