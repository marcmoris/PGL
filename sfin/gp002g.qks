;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de sous-projet - Information comptable sommaire.
;
;/M/François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction pour unix.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp002g ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

TRANSACTION T_PAC_VISUEL COMMIT ON UPDATE &
                         READ WRITE &
                         RESERVING FOR SHARE WRITE GPPRA_BDG_CTB IN DICT

USE ussectmp NOLIST     ; Variable pour la securite
    "GP002G"            ; Nom de l'écran

USE gp002g.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier fiche de projet
;------------------------------------
FILE GPPRO_ACTIVITE         MASTER



;----------------------------
; Fichier budget comptable
;----------------------------
FILE GPPRA_BDG_CTB          PRIMARY   &

     TRANSACTION T_PAC_VISUEL FOR PROCESS, QUERY, UPDATE &

                            OCCURS 15 &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE, &
                  CPTCLE, &
                  UNACLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE, &
                  CPTCLE OF GPPRA_BDG_CTB,  &
                  UNACLE OF GPPRA_BDG_CTB   &
          REQUEST CPTCLE, &
                  UNACLE  &
          ORDERBY CPTCLE, &
                  UNACLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE, &
                  CPTCLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE, &
                  CPTCLE OF GPPRA_BDG_CTB &
          REQUEST CPTCLE  &
          ORDERBY CPTCLE, &
                  UNACLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE  &
          ORDERBY CPTCLE, &
                  UNACLE


;-------------------------------
; Fichier unité de travail
;-------------------------------
FILE GPPROJETS              REFERENCE

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE OF GPPRO_ACTIVITE, &
                PRJCLE OF GPPRO_ACTIVITE



;-------------------------------
; Fichier unité administrative
;-------------------------------
FILE MCUNITE_ADM            REFERENCE

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE OF GPPRA_BDG_CTB, &
                UNACLE OF GPPRA_BDG_CTB


;-----------------------------------------
; Fichier charte de l'unite administrative
;-----------------------------------------
FILE MCCHARTE_UNA           REFERENCE

  ACCESS  VIA   CPTCLE, &
                CIECLE, &
                UNACLE  &
          USING CPTCLE OF GPPRA_BDG_CTB, &
                CIECLE OF GPPRA_BDG_CTB, &
                UNACLE OF GPPRA_BDG_CTB


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPPRA_BDG_CTB


;-------------------------------------------------------------------------------
;

DEFINE D_MNTTOT      FLOAT SIZE 8 = PACMNTENG OF GPPRA_BDG_CTB + &
                                    PACMNTREE OF GPPRA_BDG_CTB

DEFINE D_MNTBDG      FLOAT SIZE 8 = PACMNTBDG    OF GPPRA_BDG_CTB      &
                                 IF PRAPACDATAPP OF GPPRO_ACTIVITE = 0 &
                               ELSE PACMNTBDGREV OF GPPRA_BDG_CTB

DEFINE D_MNTDIS      FLOAT SIZE 8 = D_MNTBDG - D_MNTTOT


DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_CIECLE     CHAR*4
TEMPORARY T_PRJCLE     CHAR*8
TEMPORARY T_PRACLE     CHAR*3
TEMPORARY T_CPTCLE     CHAR*6
TEMPORARY T_UNACLE     CHAR*8

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Information comptable sommaire " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,28) (,92,103) (,,114)
FIELD PRJCLE    OF GPPRO_ACTIVITE                     &
label "Projet....:"&
      DISPLAY

FIELD PRJDSC1   OF GPPROJETS                          &
      DISPLAY

FIELD PRAPACDATAPP OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##"&
      LABEL "Date app.:"                              &
      DISPLAY

FIELD PRAPACUSRAPP OF GPPRO_ACTIVITE                  &
      DISPLAY

ALIGN (,3,19) (,,28)
FIELD PRACLE    OF GPPRO_ACTIVITE                     &
      DISPLAY

FIELD PRADSC1   OF GPPRO_ACTIVITE                     &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Compte Descrption            Unité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Engagé" &
@ELSE
      " %%% " &
@ENDIF
      AT ,50

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,70

TITLE &
@IF FRANCAIS
      "Total" &
@ELSE
      " %%% " &
@ENDIF
      AT ,87

TITLE &
@IF FRANCAIS
      "Budget" &
@ELSE
      " %%% " &
@ENDIF
      AT ,104

TITLE &
@IF FRANCAIS
      "Disponibilité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,115

SKIP

ALIGN (3,2,3) (,,10) (,,32) (,,42) (,,60) (,,78) (,,96) (,,114)

CLUSTER OCCURS WITH GPPRA_BDG_CTB

FIELD CPTCLE OF GPPRA_BDG_CTB                         &
      LABEL " "                                       &
      HIDDEN                                          &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD UNACLE OF GPPRA_BDG_CTB                         &
      DISPLAY

FIELD PACMNTENG OF GPPRA_BDG_CTB

FIELD PACMNTREE OF GPPRA_BDG_CTB

FIELD D_MNTTOT                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

FIELD D_MNTBDG                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

FIELD D_MNTDIS                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

CLUSTER


;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (ENGAGE)
;
;
PROCEDURE INPUT PACMNTENG
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    IF 0 <> INDEX(FIELDTEXT,"*")
    THEN BEGIN
      LET T_CIECLE = CIECLE OF GPPRA_BDG_CTB
      LET T_PRJCLE = PRJCLE OF GPPRA_BDG_CTB
      LET T_PRACLE = PRACLE OF GPPRA_BDG_CTB
      LET T_CPTCLE = CPTCLE OF GPPRA_BDG_CTB
      LET T_UNACLE = UNACLE OF GPPRA_BDG_CTB
      RUN SCREEN gp002i MODE F PASSING T_CIECLEMNU, T_CIENOM, &
                                       GPPRA_BDG_CTB,         &
                                       GPPRO_ACTIVITE,        &
                                       GPPROJETS
      LET FIELDTEXT = "0"
    END
    ELSE BEGIN
      LET FIELDTEXT = "0"
      INFO MESS 842; La modification de cet élément n'est pas permise.
    END
  END
END

PROCEDURE EDIT PACMNTENG
BEGIN
   LET FIELDVALUE = OLDVALUE(PACMNTENG OF GPPRA_BDG_CTB)
END

;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (REEL)
;
;
PROCEDURE INPUT PACMNTREE
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    IF 0 <> INDEX(FIELDTEXT,"*")
    THEN BEGIN
      LET T_CIECLE = CIECLE OF GPPRA_BDG_CTB
      LET T_PRJCLE = PRJCLE OF GPPRA_BDG_CTB
      LET T_PRACLE = PRACLE OF GPPRA_BDG_CTB
      LET T_CPTCLE = CPTCLE OF GPPRA_BDG_CTB
      LET T_UNACLE = UNACLE OF GPPRA_BDG_CTB
      RUN SCREEN gp002h MODE F PASSING T_CIECLEMNU, T_CIENOM, &
                                       GPPRA_BDG_CTB,         &
                                       GPPRO_ACTIVITE,        &
                                       GPPROJETS
      LET FIELDTEXT = "0"
    END
    ELSE BEGIN
      LET FIELDTEXT = "0"
      INFO MESS 842; La modification de cet élément n'est pas permise.
    END
  END
END

PROCEDURE EDIT PACMNTREE
BEGIN
   LET FIELDVALUE = OLDVALUE(PACMNTREE OF GPPRA_BDG_CTB)
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

