;/T/ Notes générales 
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 
;
;    Description..: Permet la saisie de note descriptive générale.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar154 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM


USE ar154.hlp NOLIST

USE ussectmp NOLIST     ; Variable pour la sécurité 
"AR154"                 ; Nom de l'écran

USE usactecr  NOLIST     

USE ustrasse NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie

DEFINE    DZ_CIECLE   CHARACTER SIZE 04 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_NGECLE    CHARACTER SIZE 2 RESET AT MODE
;-------------------------------------------------------------------------------
;
; Table primaire.
;
FILE ARNOTE_GENERALE     PRIMARY &
                        NOITEM
  ACCESS VIA     NGECLE     &
         USING   NGECLE     & 
         REQUEST NGECLE     &
         ORDERBY NGECLE  

  ACCESS SEQUENTIAL &
         ORDERBY NGECLE

FILE ARNOTE_GEN_DET      DETAIL  &
                        OCCURS 15 &
                        NOITEM

  ACCESS VIA     NGECLE                    &
         USING   NGECLE OF ARNOTE_GENERALE &
         ORDERBY NGECLE   , &
                 NGDSEQ

  ITEM NGECLE OF ARNOTE_GEN_DET INITIAL NGECLE OF ARNOTE_GENERALE

USE usdrwecr NOLIST     
USE ustitstd NOLIST
USE ushilite NOLIST     

TITLE &
@IF FRANCAIS
      " Notes générales " &
@ELSE
      " %%%es générales " &
@ENDIF
      CENTERED AT 3,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(2,3,19) (,,25)

FIELD NGECLE OF ARNOTE_GENERALE    & 
        HIDDEN                       &
        LABEL "Note générale.:"      &
        REQUIRED NOCHANGE NOCORRECT  &
        LOOKUP NOTON ARNOTE_GENERALE &
          VIA   NGECLE          &
          USING NGECLE OF ARNOTE_GENERALE &
          MESSAGE 1497

FIELD NGETIT OF ARNOTE_GENERALE &
        REQUIRED 

SKIP 1
TITLE " Séq. Description                             Impression" &
AT ,02

ALIGN(2,2,3) (,,8) (,,51)

CLUSTER OCCURS WITH ARNOTE_GEN_DET  ID BASE 10

FIELD NGDSEQ OF ARNOTE_GEN_DET  &
        HIDDEN    &
        LABEL " " &
        REQUIRED  

FIELD NGDDSC OF ARNOTE_GEN_DET 
FIELD NGDIMP OF ARNOTE_GEN_DET  SIZE 3

CLUSTER

;-------------------------------------------------------------------------------
;
; Pour la transformation du flag d'impression en 1 et 0
;
PROCEDURE INPUT NGDIMP
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour afficher le falg sous la forme Oui ou Non
;
PROCEDURE OUTPUT NGDIMP
BEGIN
  USE usflgout3 NOLIST
END


PROCEDURE INPUT NGECLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_NGECLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN ar120 MODE F PASSING T_NGECLE
    LET FIELDTEXT = T_NGECLE
  END
  LET FIELDTEXT = TRUNCATE( FIELDTEXT )
END

PROCEDURE INPUT NGETIT
BEGIN
  LET FIELDTEXT = TRUNCATE( FIELDTEXT )
END
  

;
; Procédure servant à modifier la première occurrence d'un cluster
; avec Powerhouse Client.
;
PROCEDURE DESIGNER PH01
BEGIN
  ACCEPT NGDSEQ OF ARNOTE_GEN_DET
  ACCEPT NGDDSC OF ARNOTE_GEN_DET
  ACCEPT NGDIMP OF ARNOTE_GEN_DET
END


;
; Procédure servant à détruire la première occurrence d'un cluster
; avec Powerhouse Client.
;
PROCEDURE DESIGNER PH02
BEGIN
  DELETE ARNOTE_GEN_DET
END


PROCEDURE PREUPDATE
BEGIN
  FOR ARNOTE_GEN_DET
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF ARNOTE_GEN_DET  &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF ARNOTE_GEN_DET  &
       AND NOT NEWRECORD     OF ARNOTE_GEN_DET  &
       AND NOT DELETEDRECORD OF ARNOTE_GEN_DET  &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

  END
END


BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Notes générales ********************************
*                                                                              *
* Note générale.: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
* Séq. Description                             Impression                      *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
* xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxx                          *
********************************************************************************
@ENDIF
