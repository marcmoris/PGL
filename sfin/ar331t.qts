;/T/ Solde des engagements / Détaillé.
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 4 mai 1994
;
;    Description...: Ce rapport permet de consulter pour les commandes d'achat
;                    les montants engagés et désengagés jusqu'à une période
;                    déterminée.
;
;    Paramètres....: Compagnie
;                    Numéro de commande (Borne début,<retour>=tous)
;                    Numéro de commande (Borne fin)
;                    Période comptable  (Obligatoire)
;
;/M/ Modifié par...: Guy Chabot 04 octobre 1994
;            but...: Ajouter un paramètre pour les soldes à zéro seulement
;
;/M/ Modifié par...: Steeve Duguay
;            but...: Changer la notion d'engagement pour gestion projet.
;
;/M/ Pascal Tremblay, 98-01-19
;
;    Ajustement pour le changement de siècle
;
;/M/ Francois Richard, 99-08-09
;
;    Ajustement pour le changement de siècle
;-------------------------------------------------------------------------------

USE ussetqts NOLIST   ; set process nolimit


RUN ar331t

REQUEST EXTRAIRE_ENGAGEMENT_COMMANDE

ACCESS MCHISTO_ENG    &

  LINK HENCLE OF MCHISTO_ENG        &
    TO HENCLE OF MCHIS_ECR_ENG   &

   ;
   ; Pour référer à la commande d'achat
   ;
  LINK HENCLE1 OF MCHISTO_ENG, &
       HENCLE2 OF MCHISTO_ENG[1:9], &
       "000" &
    TO CIECLE, &
       CMDCLE, &
       CMDAJT OF ARCOMMANDE   &

   ;
   ; Pour avoir le nom du fournisseur.
   ;
  LINK REFCIECLE OF MCHISTO_ENG, &
       REFCLETYP OF MCHISTO_ENG, &
       REFCLENUM OF MCHISTO_ENG  &
    TO REFCIECLE, &
       REFCLETYP, &
       REFCLENUM OF MCREFERENCE   &

   ;
   ; Pour avoir la devise du fournisseur.
   ;
  LINK REFCIECLE OF MCHISTO_ENG, &
       REFCLENUM OF MCHISTO_ENG, &
       HENCIECLE OF MCHISTO_ENG  &
    TO FOUCIECLE, &
       FOUCLE,    &
       FOCCIECLE OF VFOC_FOU    


CHOOSE HENCLE1 PARM

DEFINE D_CMDCLEDEB  CHARACTER SIZE 9 = PARM
DEFINE D_CMDCLEFIN  CHARACTER SIZE 9 = PARM 

DEFINE D_PECCLE CHARACTER SIZE 4 = PARM  ; Période comptable
DEFINE D_SLDZER CHARACTER SIZE 1 = PARM  ; Solde à zéro.

; Ajustement pour le changement de siècle
DEFINE D_CMDCLEDEB_SEL CHARACTER SIZE 10 = "1" + D_CMDCLEDEB &
   IF D_CMDCLEDEB[1:1] < "8" &
   ELSE "0" + D_CMDCLEDEB

; Ajustement pour le changement de si\350cle
DEFINE D_CMDCLEFIN_SEL CHARACTER SIZE 10 = "1" + D_CMDCLEFIN &
   IF D_CMDCLEFIN[1:1] < "8" &    
   ELSE "0" + D_CMDCLEFIN

; Ajustement pour le changement de siècle
DEFINE D_HENCLE2_SEL CHARACTER SIZE 10 = "1" + HENCLE2 OF MCHISTO_ENG[1:9] &
   IF HENCLE2 OF MCHISTO_ENG[1:1] < "8" &
   ELSE "0" + HENCLE2 OF MCHISTO_ENG[1:9]

SELECT MCHISTO_ENG IF &
    (D_CMDCLEDEB_SEL <= D_HENCLE2_SEL OR D_CMDCLEDEB = " ") AND &
    (D_CMDCLEFIN_SEL >= D_HENCLE2_SEL OR D_CMDCLEFIN = " ")

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_MCHIS CHARACTER SIZE 5 = "1" + PECCLE OF MCHIS_ECR_ENG &
         IF PECCLE OF MCHIS_ECR_ENG[1:1] < "8" &
         ELSE "0" + PECCLE OF MCHIS_ECR_ENG

; Ajustement pour le changement de siècle
DEFINE D_DEF_PECCLE CHARACTER SIZE 5 = "1" + D_PECCLE  &
         IF D_PECCLE [1:1] < "8" &
         ELSE "0" + D_PECCLE 

SELECT MCHIS_ECR_ENG IF D_PECCLE_MCHIS <= D_DEF_PECCLE

TEMPORARY T_MNTENG FLOAT SIZE 8
TEMPORARY T_MNTDES FLOAT SIZE 8

SORT ON HENCLE1 OF MCHISTO_ENG   & 
     ON CMDCLE  OF ARCOMMANDE    & 
     ON CPTCLE  OF MCHIS_ECR_ENG & 
     ON UNACLE  OF MCHIS_ECR_ENG

ITEM T_MNTENG SUBTOTAL HEEMNTDBT OF MCHIS_ECR_ENG RESET AT UNACLE
ITEM T_MNTDES SUBTOTAL HEEMNTCRT OF MCHIS_ECR_ENG RESET AT UNACLE


SUBFILE WAR331B AT UNACLE &
  INCLUDE CIECLE       OF MCHIS_ECR_ENG, &
          HENCLE3      OF MCHISTO_ENG,   &
          HENCLE2      OF MCHISTO_ENG,   &
          CMDCLE       OF ARCOMMANDE,    &
          CMDSTU       OF ARCOMMANDE,    &
          CMDMNTTOT    OF ARCOMMANDE,    &
          FOUCLE       OF ARCOMMANDE, &
          PECCLE       OF MCHIS_ECR_ENG, &
          REFCLENUM    OF MCHISTO_ENG,   &
          HENDAT       OF MCHISTO_ENG,   &
          CPTCLE       OF MCHIS_ECR_ENG, &
          UNACLE       OF MCHIS_ECR_ENG, & 
          T_MNTENG ALIAS ENGMNTENG, &
          T_MNTDES ALIAS ENGMNTDES, &  
          REFNOM       OF MCREFERENCE ALIAS RADNOM, &
          DEVCLE       OF VFOC_FOU, &
          D_PECCLE, &
          D_SLDZER

REQUEST SELECTION

ACCESS *WAR331B 

SELECT IF ENGMNTENG <> ENGMNTDES OR D_SLDZER = "1"

SUBFILE WAR331A KEEP &
        INCLUDE WAR331B

BUILD 
