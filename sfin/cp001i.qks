;/T/ Contacts
;
;/P/ Programmeur..: Alain Côté / Adapté pour les C.P. par Thomas BRENNEUR
;    Date création: 7 Juin 1993
;
;    Description..: Cet écran permet de gérer les contacts des fournisseurs
;                   Le contact numéro un du fournisseur n'est pas modifiable,
;                   ici. Il doit être modifié dans l'écran maitre.
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp001i ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 09,01 TO 23,80 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING CPFOURNISSEUR

USE ustrasse   NOLIST

USE ussectmp   NOLIST
    "CP001I"

USE cp001i.hlp NOLIST

USE usactecr   NOLIST

;-------------------------------------------------------------------------------

FILE CPFOURNISSEUR  MASTER

;-------------------------------------------------------------------------------
;
; Contacts chez le fournisseur.
;
FILE CPFOU_CONTACT    PRIMARY &
                      NOITEM  &
                      OCCURS 5

  ACCESS  VIA     FOUCIECLE, &
                  FOUCLE     &
          USING   FOUCIECLE OF CPFOURNISSEUR, &
                  FOUCLE    OF CPFOURNISSEUR  &
          ORDERBY FOUCIECLE, &
                  FOUCLE, &
                  CTFCLE

  ITEM FOUCIECLE OF CPFOU_CONTACT INITIAL FOUCIECLE OF CPFOURNISSEUR
  ITEM FOUCLE    OF CPFOU_CONTACT INITIAL FOUCLE    OF CPFOURNISSEUR

;-------------------------------------------------------------------------------

DRAW 02,01 TO 02,79
DRAW 02,01 TO 15,01
DRAW 15,01 TO 15,80
DRAW 03,80 TO 15,80

USE ushilite NOLIST

SKIP 1

TITLE &
@IF FRANCAIS
      " Contacts " &
@ELSE
      " Contacts " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

TITLE &
@IF FRANCAIS
      "                                                                Fax /" &
@ELSE
      "                                                                Fax /" &
@ENDIF
      at ,3

SKIP

TITLE &
@IF FRANCAIS
  "No Titre                Nom                  Téléphone    Poste Céllulaire" &
@ELSE
  "No Title                Name                 Telephone    Ext.  %%cell" &
@ENDIF
  at ,3

CLUSTER OCCURS WITH CPFOU_CONTACT ID BASE 10
ALIGN(3,2,3) (,,6) (,,27) (,,48) (,,62) (,,67)

FIELD CTFCLE OF CPFOU_CONTACT &
      HIDDEN LABEL " " &
      REQUIRED &
      NOCHANGE &
      LOOKUP NOTON CPFOU_CONTACT &
          VIA   FOUCIECLE, &
                FOUCLE   , &
                CTFCLE     &
          USING FOUCIECLE OF CPFOU_CONTACT, &
                FOUCLE    OF CPFOU_CONTACT, &
                CTFCLE OF CPFOU_CONTACT &
          MESSAGE 456 ; Ce numéro de contact existe déjà.

FIELD CTFTIT OF CPFOU_CONTACT

FIELD CTFNOM OF CPFOU_CONTACT

FIELD CTFTEL OF CPFOU_CONTACT

FIELD CTFTELPOS OF CPFOU_CONTACT

FIELD CTFFAX OF CPFOU_CONTACT

SKIP

ALIGN (,,67)

FIELD CTFTELCEL OF CPFOU_CONTACT

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Cette procedure valide la sécurité de saisie.
;
PROCEDURE INPUT CTFCLE
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CTFTEL
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CTFFAX
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CTFTELCEL
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valider que le Contact "1" ne soit pas modifié.
;
;
PROCEDURE DESIGNER 10
BEGIN
  IF CTFCLE OF CPFOU_CONTACT <> 1
  THEN BEGIN
    ACCEPT CTFTIT    OF CPFOU_CONTACT
    ACCEPT CTFNOM    OF CPFOU_CONTACT
    ACCEPT CTFTEL    OF CPFOU_CONTACT
    ACCEPT CTFTELPOS OF CPFOU_CONTACT
    ACCEPT CTFFAX    OF CPFOU_CONTACT
    ACCEPT CTFTELCEL OF CPFOU_CONTACT
  END
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR CPFOU_CONTACT
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF CPFOU_CONTACT &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF CPFOU_CONTACT &
       AND NOT NEWRECORD     OF CPFOU_CONTACT &
       AND NOT DELETEDRECORD OF CPFOU_CONTACT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Le timbre de modification est mis à jour si l'usager modifie
    ; la table CPFOU_CONTACT.
    ;
    IF        NEWRECORD OF CPFOU_CONTACT &
       OR ALTEREDRECORD OF CPFOU_CONTACT &
       OR DELETEDRECORD OF CPFOU_CONTACT
    THEN BEGIN
      LET FOUDATMOD OF CPFOURNISSEUR = SYSDATE
      LET FOUHREMOD OF CPFOURNISSEUR = SYSTIME / 10000
      LET FOUUSRMOD OF CPFOURNISSEUR = LOGONID
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; Valider que le Contact "1" ne soit pas détruit
;
;
PROCEDURE DELETE
BEGIN
  IF CTFCLE OF CPFOU_CONTACT <> 1
  THEN DELETE CPFOU_CONTACT
END


BUILD
;
;*********************************** Contacts **********************************x
;*                                                                 Fax /        *
;* No Titre                Nom                  Téléphone    Poste Céllulaire   *
;* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
;*                                                                 xxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
;*                                                                 xxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
;*                                                                 xxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
;*                                                                 xxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx xxxxxxxxxxxxx*
;*                                                                 xxxxxxxxxxxxx*
;********************************************************************************
