;/T/ Gerrer les tranches (Rejet)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-27
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur les rejets de tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd181h ACTIONBAR                     &
             MODE LABEL "" AT 2,80          &
             NOACTION                       &
             ACTIVITIES FIND                &
             FIELDMARK RETAIN STARTUP       &
             HELP POPUP FROM 4,9 TO 20,72   &
             RECEIVING  T_CIECLEMNU ,       &
                        T_CIENOM ,          &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD181H"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd181h.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Détail de rejet
;

FILE PDDET_REJET PRIMARY &
                 NOITEM &
                 NODELETE &
                 NOAPPEND

  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE

         ITEM CIECLE OF PDDET_REJET INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Rejet
;

FILE PDREJET REFERENCE

  ACCESS VIA     CIECLE,                      &
                 REJNUMREJ                    &
         USING   T_CIECLEMNU,                 &
                 REJNUMREJ OF PDDET_REJET


;-------------------------------------------------------------------------------
;
; Raison de rejet
;
FILE PDRAISON_REJET REFERENCE

  ACCESS VIA     CIECLE,                      &
                 RRECODREJ                    &
         USING   T_CIECLEMNU,                 &
                 RRECODREJ OF PDDET_REJET


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Rejet " &
@ELSE
      " %%%Rejet " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO LINE 5

ALIGN (,3,19)

FIELD REJNUMREJ        OF PDREJET          &
label "No rejet......:"&
        DISPLAY


FIELD REJDATREJ        OF PDREJET           FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date rejet....:"&
        DISPLAY


FIELD REJDSC001        OF PDREJET          &
label "Description...:"&
        DISPLAY


ALIGN (,,19)


FIELD REJDSC002        OF PDREJET          &
        DISPLAY


FIELD REJDSC003        OF PDREJET          &
        DISPLAY


SKIP 1


ALIGN (,3,19)


FIELD RRECODREJ        OF PDDET_REJET      &
label "Code rejet....:"&
        DISPLAY


FIELD RREDSC001        OF PDRAISON_REJET   &
label "Description...:"&
        DISPLAY


ALIGN (,,19)


FIELD RREDSC001        OF PDRAISON_REJET   &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Rejet *************************************
*                                                                              *
* No rejet......: xxxxxxxxx                                                    *
* Date rejet....: xxxxxxxx                                                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Code rejet....: xx                                                           *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
