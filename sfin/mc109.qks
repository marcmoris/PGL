;/T/ Cons. Période comptable - cie     CIECLE, PECCLE, T_MOD
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 07-Avr-1992
;
;    Description..: Permet de visionner et de choisir les période comptable
;                   par compagnie et selon le module,
;
;
;    Paramètres...: CIECLE    ==> - numéro de compagnie
;                                 - segment de la clé de recherche
;
;                   PECCLE    ==> - numéro de période
;                                 - segment de la clé de recherche
;                                 - peut contenir la valeur "@"
;                                 - contient la valeur de retour
;
;                   T_MOD     ==> - code du module(GL,CP,CR)
;                                 - critère de sélection.
;
;-------------------------------------------------------------------------------
; /A/ Adaptation Nancy Marceau, 22 mars 1994
;       - Adaptation module Achats/Réceptions et inventaire

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc109 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLE, &
                       T_PECCLE, &
                       T_MOD

USE mc109.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4
TEMPORARY T_PECCLE CHARACTER SIZE 4
TEMPORARY T_MOD    CHARACTER SIZE 2

;-------------------------------------------------------------------------------
;
FILE MCPERIODE_CIE    PRIMARY &
                      OCCURS 11
  ACCESS VIA CIECLE, &
             PECCLE  &
         USING T_CIECLE, &
               T_PECCLE  &
         ORDERBY PECCLE

  SELECT IF (PCCSTUCP OF MCPERIODE_CIE <> "F" AND T_MOD = "CP") OR &
            (PCCSTUGL OF MCPERIODE_CIE <> "F" AND T_MOD = "GL") OR &
            (PCCSTUCR OF MCPERIODE_CIE <> "F" AND T_MOD = "CR") OR &
            (PCCSTUAR OF MCPERIODE_CIE <> "F" AND T_MOD = "AR") OR &
            (PCCSTUIN OF MCPERIODE_CIE <> "F" AND T_MOD = "IN")

;
; Pour afficher les dates des périodes.
;
FILE MCPERIODE_CTB    REFERENCE &
                      OCCURS WITH MCPERIODE_CIE
  ACCESS VIA PECCLE &
         USING PECCLE OF MCPERIODE_CIE

;-------------------------------------------------------------------------------
;
DEFINE D_PCCSTU CHARACTER SIZE 1 = PCCSTUCP OF MCPERIODE_CIE IF T_MOD = "CP" &
                          ELSE     PCCSTUCR OF MCPERIODE_CIE IF T_MOD = "CR" &
                          ELSE     PCCSTUGL OF MCPERIODE_CIE IF T_MOD = "GL" &
                          ELSE     PCCSTUAR OF MCPERIODE_CIE IF T_MOD = "AR" &
                          ELSE     PCCSTUIN OF MCPERIODE_CIE IF T_MOD = "IN" &
                          ELSE     "*"

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

SKIP TO 2

USE ustitoff NOLIST

ALIGN(3,3,6) (,,13) (,,24) (,,36)

CLUSTER OCCURS WITH MCPERIODE_CIE

FIELD PECCLE    OF MCPERIODE_CIE HIDDEN LABEL ">>"
FIELD PECDATDEB OF MCPERIODE_CTB  FORMAT YYYYMMDD PATTERN "####/##/##" DISPLAY
FIELD PECDATFIN OF MCPERIODE_CTB  FORMAT YYYYMMDD PATTERN "####/##/##" DISPLAY
FIELD D_PCCSTU  DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_PECCLE, "@")
  THEN LET T_PECCLE = ""
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_PECCLE = PECCLE OF MCPERIODE_CIE
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       * >> xxxxx  xxxxxxxx  xxxxxxxx     x    *
;                                       *****************************************
