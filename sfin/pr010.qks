;/T/ Cummulatif Projet

;/P/ Programmeur..: Marc Morissette
;    Date Création: 13 Oct 1993
;
;    Description..: Cummulatif projet
;
;
;/M/ Modifié par..: Guy Chabot le 07 novembre 1994
;            but..: Utiliser une vue au lieu des tables pour la sommation des
;                   projets.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr010 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "PR010"             ; Nom de l'écran

USE pr010.hlp nolist ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran

ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Sommaire des transactions  "  ACTION DESIGNER D001
  MENUITEM LABEL "Impression du cumulatif    "  ACTION DESIGNER D002

TEMPORARY T_PROCLEFND CHARACTER SIZE 8 RESET AT STARTUP
TEMPORARY T_FORCLEFND CHARACTER SIZE 5 RESET AT STARTUP

FILE VPRO01 PRIMARY OCCURS 13 TIMES

SELECT IF "C" <> FODTYP OF VPRO01 OR &
           0  <> (V_MNTCUM1 OF VPRO01 + V_MNTCUM2 OF VPRO01)

FILE GPPROJET REFERENCE
  ACCESS VIA CIECLE, PROCLE USING T_CIECLEMNU, PROCLE OF VPRO01

FILE GPFORMAT REFERENCE
  ACCESS VIA FORCLE USING PROFRM1 OF GPPROJET

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )
DEFINE D_XPRNOM CHAR SIZE 7 = "PR314"
DEFINE D_LISPAR CHAR SIZE 204 = "F_CIECLE        |F_PROCLE        |F_FORCLE        |F_CIENOM        "
DEFINE D_INDLON CHAR SIZE 112 = "001004|005008|013005|018040"
DEFINE D_LISVAL CHAR SIZE 300 = CIECLE + PROCLE + FORCLE + T_CIENOM



TEMPORARY T_PROCLEPOP CHARACTER SIZE 8
TEMPORARY T_PROSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.

;
; Les trois define servent a transformer du numerique en caractere
; tout en conservant le picture.
;
;
DEFINE D_MNT  FLOAT      SIZE 8 = V_MNTCUM1 OF VPRO01 + V_MNTCUM2 OF VPRO01

DEFINE D_MNT1 CHARACTER SIZE 15 = "$" + &
                         ASCII(ABS(D_MNT)/100) + &
                         "." + &
                         ASCII(MOD(ABS(D_MNT),100),2)

DEFINE D_MNT2 CHARACTER SIZE 18 = D_MNT1 IF 100000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:2] + "," + D_MNT1[3:13] &
                          IF 1000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:3] + "," + D_MNT1[4:12] &
                          IF 10000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:4] + "," + D_MNT1[5:11] &
                          IF 100000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:2] + "," + D_MNT1[3:3] + "," + D_MNT1[6:10] &
                          IF 1000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:3] + "," + D_MNT1[4:3] + "," + D_MNT1[7:9] &
                          IF 10000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:4] + "," + D_MNT1[5:3] + "," + D_MNT1[8:8] &
                          IF 100000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:2] + "," + D_MNT1[3:3] + "," + D_MNT1[6:3] + &
                          "," + D_MNT1[9:7] &
                          IF 1000000000000 > ABS(D_MNT) &
                     ELSE D_MNT1[1:3] + "," + D_MNT1[4:3] + "," + D_MNT1[7:3] + &
                          "," + D_MNT1[10:6]

DEFINE D_MNT3 CHARACTER SIZE 20 = "--------------------" IF FODTYP = "S" &
                        ELSE "" IF FODTYP = "E" &
                        ELSE RJ(PACK("<" + D_MNT2 + ">")) IF D_MNT < 0 &
                        ELSE RJ(D_MNT2)
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Cummulatif Projet " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP
ALIGN (,2,11)(,,25)
FIELD PROCLE  OF VPRO01 &
        DISPLAY &
        LABEL "Projet :"
FIELD PRODSC  OF GPPROJET &
        DISPLAY SIZE 50
FIELD PROFRM1 OF GPPROJET &
        DISPLAY &
        LABEL "Format :"
FIELD FORDSC OF GPFORMAT     DISPLAY
SKIP 1
TITLE "Description                                          Montant" AT ,2
SKIP
ALIGN (,,2)(,,54)
CLUSTER OCCURS WITH VPRO01
FIELD FODDSC OF VPRO01 DISPLAY
FIELD D_MNT3 DISPLAY
CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE PATH
BEGIN
  RUN SCREEN pr010a MODE F &
        PASSING T_CIECLEMNU, &
                T_PROCLEFND, &
                T_FORCLEFND
END


PROCEDURE FIND
BEGIN
  FOR VPRO01
  BEGIN
    GET VPRO01   VIA      CIECLE, &
                          PROCLE, &
                          FORCLE  &
                 USING    T_CIECLEMNU, &
                          T_PROCLEFND, &
                          T_FORCLEFND  &
                 ORDERBY  CIECLE, PROCLE, FORCLE, FODLIG
  END
END


PROCEDURE DESIGNER D001
BEGIN
  LET T_PROCLEFND = PROCLE OF VPRO01
  RUN SCREEN pr014 PASSING T_CIECLEMNU, T_CIENOM, T_PROCLEFND MODE F
END

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN gs090 MODE E PASSING D_XPRNOM,D_LISPAR,D_INDLON,D_LISVAL
END

BUILD
