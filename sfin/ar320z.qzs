;/T/ Liste des réquisitions d'achat / Détaillée par fournisseur
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 2 mai 1994
;
;    Description...: Ce rapport présente les réquisitions d'achat pour
;                    les entrepôts désirés.
;
;    Paramètres....: Compagnie 
;                    Entrepôt           (Un, plusieurs ou tous)
;                    Numéro réquisition (Borne de début, <retour>=tous)
;                    Numéro réquisition (Borne de fin)
;                    Statut             (P=préliminaire, A=approuvée, 
;                                        C=complète,X=annulée,<retour>=tous)    
;                     
;
;/M/   Caroline Kaine, 12 nov 96, Modif. format passe de 180 a 175
;
;/M/ Programmeur.......: Francois Richard
;    Date modification.: 15 Juin 1999
;    Description.......: Migration Achat/Réception/Inventaire de l'environnement
;                        BOC
;------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *WAR320A  &
  LINK CIECLE OF WAR320A, &
       ENTCLE OF WAR320A &
    TO CIECLE, &
       ENTCLE OF ARENTREPOT   &
  
  LINK T_FOUCIECLE , &
       T_REFCLETYP , &
       T_FOUCLE    , &
       T_RADCLE      &
    TO REFCIECLE, &
       REFCLETYP, &
       REFCLENUM, &
       RADCLE OF MCREF_ADRESSE   OPTIONAL &
  
  LINK T_FOUCIECLE OF WAR320A, &
       T_FOUCLE    OF WAR320A, &
       CIECLE    OF WAR320A &
    TO FOUCIECLE, &
       FOUCLE,    &
       FOCCIECLE OF VFOC_FOU   OPTIONAL & 

   ;
   ; Statut de la réquisition
   ;
  LINK ("REQSTUAPP",                 &
         REQSTUAPP OF WAR320A)       &
    TO XELNOM,                       &
       CBICLE OF VCBI_DSC     OPTIONAL & 
 
   ;
   ; Devise du consolidé
   ;
  LINK ("1") &
    TO CIENMC OF MCHOLDING    



; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des réquisitions d'achat / Détaillée par fournisseur"
@ELSE
       "%%%Liste des réquisitions d'achat / Détaillée par fournisseur"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       ""
@ELSE
       "%%%"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR320Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM 


USE usflgdet NOLIST ;Flag Bilingue (O/N)

;
;Indique si la réquisition est une soumission
;
DEFINE D_REQFLGSOU CHARACTER SIZE 3 &
        = DZ_FLGAFFO IF REQFLGSOU OF WAR320A <> "0" ELSE &
          DZ_FLGAFFN 

 ; Statuts de réquisition demandés
 ;
DEFINE D_CBIDSC1 CHARACTER SIZE 15 = "Approuvée " &
  IF 0 <> INDEX (D_REQSTUAPP OF WAR320A,"A") OR  D_REQSTUAPP OF WAR320A = "" &
    ELSE ""

DEFINE D_CBIDSC2 CHARACTER SIZE 15 = "Complète "&
  IF 0 <> INDEX (D_REQSTUAPP OF WAR320A,"C") OR  D_REQSTUAPP OF WAR320A = "" &
    ELSE ""

DEFINE D_CBIDSC3 CHARACTER SIZE 15 = "Préliminaire " &
  IF 0 <> INDEX (D_REQSTUAPP OF WAR320A,"P") OR  D_REQSTUAPP OF WAR320A = "" &
    ELSE ""

DEFINE D_CBIDSC4 CHARACTER SIZE 15 = "Annulée " &
  IF 0 <> INDEX (D_REQSTUAPP OF WAR320A,"X") OR  D_REQSTUAPP OF WAR320A = "" &
    ELSE ""

DEFINE D_CBIDSCDEM CHARACTER SIZE 70 = &
  PACK (TRUNCATE(D_CBIDSC1) + " " + &
        TRUNCATE(D_CBIDSC2) + " " + &
        TRUNCATE(D_CBIDSC3) + " " + &
        TRUNCATE(D_CBIDSC4))


DEFINE D_CBIDSC CHARACTER SIZE 15 = CBIDSC OF VCBI_DSC


DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING OR &
             0 = SIZE(TRUNCATE(FOUCLE OF WAR320A)) &
          ELSE "/" + DEVCLE OF VFOC_FOU

DEFINE D_RADNOM CHARACTER SIZE 40 = RADNOM OF MCREF_ADRESSE[1:40]

SORT ON CIECLE    OF WAR320A &  ; Compagnie
     ON ENTCLE    OF WAR320A &  ; Entrepôt
     ON REQCLE    OF WAR320A &  ; Numéro réquisition
     ON REQSTUAPP OF WAR320A &  ; Statut réquisition
     ON FOUCLE    OF WAR320A &  ; Fournisseur
     ON ADECLE    OF WAR320A &  ; Adresse d'expédition
     ON REQCLENUM    OF WAR320A    ; Numéro d'item du produit


USE ustit175 NOLIST
  SKIP 2 &
@IF FRANCAIS
TAB 001 "Entrepôt..........:" &
@ELSE 
TAB 001 "%%%Entrepôt..........:" &
@ENDIF

TAB 021 ENTCLE OF WAR320A    &
TAB 027 ENTNOM OF ARENTREPOT &
  SKIP  &
@IF FRANCAIS
TAB 001 "Statuts demandés..:" &
@ELSE
TAB 001 "%%%Statuts demandés..:" &
@ENDIF

TAB 021 D_CBIDSCDEM &
  SKIP 2 


HEADING AT REQCLE &
@IF FRANCAIS
  TAB 001 "No.                                     ------ Dates -----                                                   " &
@ELSE
  TAB 001 "%%%No.                                     ------ Dates -----                                                   " &
@ENDIF

SKIP &
@IF FRANCAIS
  TAB 001 "réquisition Statut                      Approuvée  Requise    " &
@ELSE
  TAB 001 "%%%réquisition Statut                      Approuvée  Requise    " &
@ENDIF
SKIP &
  TAB 001 "                                         aa/mm/jj  aa/mm/jj" &
SKIP &
  TAB 001 "----------- ---------------             ---------- -------- " &
SKIP 1 & 
  TAB 002 REQCLE OF WAR320A &
  TAB 013 D_CBIDSC    &
  TAB 041 REQDATAPP OF WAR320A &
  TAB 052 REQDATREQ OF WAR320A &
SKIP 2




HEADING AT ADECLE &
@IF FRANCAIS
  TAB 001 "Fournisseur                                     Adr. exp.   Nombre d'items    Montant brut " &
@ELSE
  TAB 001 "%%%Fournisseur                                     Adr. exp.   Nombre d'items    Montant brut " &
@ENDIF

SKIP &
  TAB 001 "----------------------------------------------- ---------   --------------  ---------------" &
SKIP 1 &
  TAB 001 FOUCLE    OF WAR320A &        
  TAB 008 D_RADNOM             &
  TAB 052 ADECLE    OF WAR320A &
  TAB 068 RQFNBRITE OF WAR320A &
  TAB 077 RQFMNTTOT OF WAR320A &
  TAB 092 D_DEVCLE  &
SKIP &

@IF FRANCAIS
  TAB 001 "Item    Sou. Produit                                               Qté com.    Prix uni.          TPS        TVQ    Escompte          Total  Compte Unité    Projet S-projet" &
@ELSE
  TAB 001 "%%%"
@ENDIF
SKIP &
  TAB 001 "------- ---- ----------------------------------------------------- ----------- ------------ ---------- ---------- ---------- --------------- ------ -------- ------ --------" &
SKIP 2
REPORT &
  TAB 001 REQCLENUM    OF WAR320A      &
  TAB 010 D_REQFLGSOU &
  TAB 014 PRDCLE    OF WAR320A      &
  TAB 027 REQDSC    OF WAR320A      &
  TAB 068 REQQTEREQ OF WAR320A      &
    PICTURE "^^,^^^.^^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 6        &
    BWZ                   &   
  TAB 080 REQPRIUNI OF WAR320A      &
    PICTURE "^^^,^^^.^^^^"          &
  TAB 093 REQMNTTPS OF WAR320A      &  
    PICTURE "^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
  TAB 104 REQMNTTVQ OF WAR320A      &   
    PICTURE "^^,^^^.^^ "  &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
  TAB 115 REQMNTESC OF WAR320A      &
    PICTURE "^^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
  TAB 126 REQMNTTOT OF WAR320A      &
  TAB 142 CPTCLE OF WAR320A      &
  TAB 149 UNACLE OF WAR320A      &
  TAB 158 PRJCLE OF WAR320A      &
  TAB 168 PRACLE OF WAR320A      &
  TAB 177 IMMCLE OF WAR320A   

FOOTING AT ADECLE &  
  SKIP &
TAB 093 "---------- ---------- ---------- ---------------" &
  SKIP &
TAB 093 REQMNTTPS OF WAR320A      SUBTOTAL &
    PICTURE "^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
TAB 104 REQMNTTVQ OF WAR320A      SUBTOTAL &
    PICTURE "^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
TAB 115 REQMNTESC OF WAR320A      SUBTOTAL &
    PICTURE "^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
TAB 126 REQMNTTOT OF WAR320A      SUBTOTAL &
TAB 141 D_DEVCLE &
  SKIP 2 

FOOTING AT FOUCLE &  
  SKIP &
TAB 093 "---------- ---------- ---------- ---------------" &
  SKIP &
TAB 063  "Totaux pour le fournisseur :" &
TAB 092 REQMNTTPS OF WAR320A      SUBTOTAL &
    PICTURE "^^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
TAB 103 REQMNTTVQ OF WAR320A      SUBTOTAL &
    PICTURE "^^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
TAB 115 REQMNTESC OF WAR320A      SUBTOTAL &
    PICTURE "^^^,^^^.^^ " &
    TRAILING SIGN "-"     &
    SIGNIFICANCE 5        &
    BWZ                   &
TAB 126 REQMNTTOT OF WAR320A      SUBTOTAL &
TAB 141 D_DEVCLE &
  SKIP 2 

FOOTING AT REQCLE &
   SKIP PAGE

BUILD ar320z
@IF FORMAT
         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20
12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

Entrepôt..........: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Statuts demandés..: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

No.                                     ------ Dates -----                                                   
réquisition Statut                      Approuvée  Requise    
----------- ---------------             ---------- --------
 xxxxxxxxx  xxxxxxxxxxxxxxx             99/99/99   99/99/99

Fournisseur                                     Adr. exp.   Nombre d'items    Montant brut 
----------------------------------------------- ---------   --------------  ---------------
xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx           xxx      999,999,999.99-/xxx

Item    Sou. Produit                                               Qté com.    Prix uni.          TPS        TVQ    Escompte          Total  Compte Unité    Projet Activité 
------- ---- ----------------------------------------------------- ----------- ------------ ---------- ---------- ---------- --------------- ------ -------- ------ --------
999.999  xxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999- 999,999.9999 99,999.99- 99,999.99- 99,999.99- 999,999,999.99- xxxxxx xxxxxxxx xxxxx  xxxxxx
999.999  xxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999- 999,999.9999 99,999.99- 99,999.99- 99,999.99- 999,999,999.99- xxxxxx xxxxxxxx xxxxx  xxxxxx
999.999  xxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999,999- 999,999.9999 99,999.99- 99,999.99- 99,999.99- 999,999,999.99- xxxxxx xxxxxxxx xxxxx  xxxxxx
                                                                                            ---------- ---------- ---------- ---------------
                                                                                            99,999.99- 99,999.99- 99,999.99- 999,999,999.99-/xxx 
                                                                                            ---------- ---------- ---------- ---------------
                                                         Totaux pour le fournisseur :       99,999.99- 99,999.99- 99,999.99- 999,999,999.99-/xxx 
                                                                               
@ENDIF
