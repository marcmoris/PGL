;/T/ Valorisation des produits inventoriés par entrepôt / Sommaire
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 10 mai 1994
;
;    Description...: Ce rapport permet de présenter la valorisation des
;                    produits inventoriés par entrepôt.
;
;    Paramètres....: Compagnie
;                    Code de produit (Borne de début)
;                    Code de produit (Borne de fin)
;                    Catégorie       (Un,plusieurs,tous)
;
;    Particularités.: On ne choisit que les produits de type Inventorié et
;                     Non inventorié.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS VPRE_PRD    &
  ;
  ; Pour avoir le nom de l'entrepôt
  ;
 LINK CIECLE OF VPRE_PRD, &
      ENTCLE OF VPRE_PRD  &
   TO CIECLE, &
      ENTCLE OF ARENTREPOT   &
  ;
  ; Catégorie de produit
  ;
 LINK ("PRDCAT", &
        PRDCAT OF VPRE_PRD) &
   TO XELNOM,                   &
      CBICLE OF VCBI_DSC   ALIAS A_CBI_PRDCAT OPTIONAL



CHOOSE CIECLE PARM,       &  ; Compagnie du menu
       PRDCLE PARM RANGE, &  ; Code de produit
       PRDCAT PARM,       &  ; Catégorie de produit
       PRDTYP "I"            ; Type de produit Inventorié


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Valorisation des produits inventoriés / Sommaire"
@ELSE
       "%%%Valorisation des produits inventoriés / Sommaire"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       ""
@ELSE
       ""
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR351Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; Valorisation au dernier coûtant
;
DEFINE D_VALDERCOU FLOAT SIZE 8 = ( PREQTEPHY OF VPRE_PRD * &
                                    PREDERCOU OF VPRE_PRD ) / 100000


SORT ON CIECLE OF VPRE_PRD & ; Compagnie
     ON ENTCLE OF VPRE_PRD   ; Entrepôt



USE ustitstd NOLIST
  SKIP 2 &
@IF FRANCAIS
TAB 001 "                                                  Valorisation        Valorisation " &
@ELSE
TAB 001 "%%%                                                  Valorisation        Valorisation " &
@ENDIF
  SKIP &
@IF FRANCAIS
TAB 001 "Entrepôt                                         au côut moyen    au dernier coûtant " &
@ELSE
TAB 001 "%%%Entrepôt                                         au côut moyen    au dernier coûtant " &
@ENDIF
  SKIP 2



FOOTING AT ENTCLE &
TAB 001 ENTCLE OF VPRE_PRD   &
TAB 006 ENTNOM OF ARENTREPOT &
TAB 048 PREVAL OF VPRE_PRD SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
TAB 068 D_VALDERCOU SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
  SKIP


FINAL FOOTING &
  SKIP 2 &
TAB 048 "---------------   -----------------" &
  SKIP &
TAB 048 PREVAL OF VPRE_PRD SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
TAB 068 D_VALDERCOU SUBTOTAL &
  PICTURE "^^^,^^^,^^^.^^ " &
  TRAILING SIGN "-"    &
  SIGNIFICANCE 5       &
  BWZ                  &
  SKIP &
TAB 048 "---------------   -----------------"


BUILD ar351z


@IF FORMAT
         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20
12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
AR351                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                           Page: 999
99/99/99 HH:MM xxxxxxxxxxxx                  Valorisation des produits inventoriés par entrepôt / Sommaire
                                             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

                                                  Valorisation        Valorisation
Entrepôt                                         au côut moyen    au dernier coûtant
---------------------------------------------   ---------------   ------------------
xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-      999,999,999.99-
xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-      999,999,999.99-
xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-      999,999,999.99-
xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   999,999,999.99-      999,999,999.99-
                                                ---------------      ---------------
                                                999,999,999.99-      999,999,999.99-
                                                ---------------      ---------------
@ENDIF
