;/T/ Gerrer les tranches (Sciage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-26
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur le sciage de la tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd181f ACTIONBAR                     &
             MODE LABEL "" AT 2,80          &
             NOACTION                       &
             ACTIVITIES FIND                &
             FIELDMARK RETAIN STARTUP       &
             HELP POPUP FROM 4,9 TO 20,72   &
             RECEIVING  T_CIECLEMNU ,       &
                        T_CIENOM ,          &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD181F"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd181f.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER



;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC PRIMARY &
                    NOITEM &
                    NODELETE &
                    NOAPPEND

  ACCESS VIA     CIECLE,                         &
                 BLONUMGRA001APR,                &
                 BLONUMGRA002APR,                &
                 BLORECAPR                       &
         USING   T_CIECLEMNU,                    &
                 BLONUMGRA001APR OF PDTRANCHE,   &
                 BLONUMGRA002APR OF PDTRANCHE,   &
                 BLORECAPR       OF PDTRANCHE

         ITEM CIECLE OF PDMONTAGE_BLOC INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE REFERENCE

  ACCESS VIA     CIECLE,                         &
                 RMSNUMSEQ                       &
         USING   T_CIECLEMNU,                    &
                 RMSNUMSEQ OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                         &
                 SCINUMSCI                       &
         USING   T_CIECLEMNU,                    &
                 SCINUMSCI OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Sciage bloc
;

FILE PDSCIAGE_BLOCS REFERENCE

  ACCESS VIA     CIECLE,                         &
                 RMSNUMSEQ                       &
         USING   T_CIECLEMNU,                    &
                 RMSNUMSEQ OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Sciage " &
@ELSE
      " %%%Sciage " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19) (,40,56)


FIELD BLONUMBLOAPR     OF PDTRANCHE        &
label "No. bloc......:" &
        DISPLAY


FIELD SCBDATSCI        OF PDSCIAGE_BLOCS    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date sciage...:"&
        DISPLAY


ALIGN (,3,19)


FIELD SCBDSC001        OF PDSCIAGE_BLOCS   &
label "Description...:"&
        DISPLAY


ALIGN (,,19)



FIELD SCBDSC002        OF PDSCIAGE_BLOCS   &
        ID SAME                            &
        DISPLAY


FIELD SCBDSC003        OF PDSCIAGE_BLOCS   &
        ID SAME                            &
        DISPLAY



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************************ Sciage ************************************
*                                                                              *
* No. bloc......: xxxxx-xxxxx-x        Date sciage...: xxxxxxxx                *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
