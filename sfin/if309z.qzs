;/T/ Liste des blocs assignés par commande
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme et pour qu'il sorte par commande au lieu
;		      de par contrat.
;
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC

CHOOSE CIECLE    PARM, &
       PJTABR    PARM

DEFINE D-DATE-DEB DATE = PARM
DEFINE D-DATE-FIN DATE = PARM

DEFINE D-CPT-BLOC  INTEGER SIZE 1 = 1 IF BLCDATREC <> 0 ELSE 0
DEFINE D-BLCM3TOT  INTEGER SIZE 4 = BLCM3TOT IF BLCDATREC <> 0 ELSE 0
DEFINE D-BLCTFINAL INTEGER SIZE 4 = BLCTFINAL IF BLCDATREC <> 0 ELSE 0


SELECT IF  ( 0 <> SIZE(TRUNC(PJTABR OF IFBLOC)) &
             AND  0 <> SIZE(TRUNC(PJTANN OF IFBLOC)) &
             AND  0 <> SIZE(TRUNC(COMNUMCOMINT OF IFBLOC)) &
           )      &
       AND                                      &
           (    COEDATDEB OF IFBLOC >= D-DATE-DEB &
                OR D-DATE-DEB = 0 &
             ) &
          AND (    COEDATDEB OF IFBLOC  <= D-DATE-FIN &
                OR D-DATE-FIN = 0 &
              )

DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN  OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT OF IFBLOC &
                                     IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""






SORT ON D_NUMCOM &
     ON CATCLE   &
     ON COEDATDEB &
     ON D_NUMBLC

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" &
                        IF     D-DATE-DEB = 0 &
                           AND D-DATE-FIN = 0 &
        ELSE "Entre le : " + ASCII( D-DATE-DEB , 8 )[3:2] + "/" + &
                             ASCII( D-DATE-DEB , 8 )[5:2] + "/" + &
                             ASCII( D-DATE-DEB , 8 )[7:2] + &
             " et : "      + ASCII( D-DATE-FIN , 8 )[3:2] + "/" + &
                             ASCII( D-DATE-FIN , 8 )[5:2] + "/" + &
                             ASCII( D-DATE-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF309"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 6 "NO"                    &
             TAB 14 "CAT"                   &
             TAB 18 "COUL" 		   &
             TAB 23 "QUAL"                 &
             TAB 30 "DATE"                 &
             TAB 40 "NO"                   &
             TAB 57 "DATE"                 &
             TAB 68 "DIMENSIONS "          &
             TAB 83 "TOTAL"                &
             TAB 96 "T/M"                  &
             TAB 104 "NO"                   &
             TAB 112 "NO"                   &
             TAB 119 "DATE DU"              &
SKIP 1 &
             TAB 3  "COMMANDE"             &
             TAB 30 "DEBUT"                &
             TAB 39 "BLOC"                 &
             TAB 57 "PROD"                 &
             TAB 66 "LONG HAUT LARG"       &
             TAB 84 "M3"                   &
             TAB 104 "EXP"                  &
             TAB 112 "FACT."                &
             TAB 119 "TRANSPORT"            &
SKIP 1 &
             TAB 3  "_________"               &
             TAB 14 "___"                  &
             TAB 18 "____"                &
             TAB 23 "____"               &
             TAB 29 "_______"             &
             TAB 39 "______________"          &
             TAB 55 "________"             &
             TAB 65 "________________"        &
             TAB 83 "______"               &
             TAB 95 "_____"                &
             TAB 102 "______"               &
             TAB 110 "_______"              &
             TAB 119 "________"            &
SKIP 2
REPORT  D_NUMCOM PRINT AT D_NUMCOM &
        CATCLE OF IFBLOC                         &
        CLRCLE OF IFBLOC                         &
        QLTCLE OF IFBLOC                        &
        COEDATDEB OF IFBLOC                &
        D_NUMBLC                                &
        BLCDAT OF IFBLOC                       &
        BLCVENDULONG OF IFBLOC                 &
        BLCVENDUHAU OF IFBLOC                  &
        BLCVENDULAR OF IFBLOC                  &
        BLCM3TOT OF IFBLOC                     &
        BLCTFINAL OF IFBLOC                    &
        EXNCLE OF IFBLOC                         &
        FTRNUMFAC OF IFBLOC                         &
        BLCDATREC OF IFBLOC

FOOTING AT D_NUMCOM &
        TAB 39 "______________"            &
        TAB 79 "__________"         &
        TAB 91 "_________"          &
SKIP 1  &
        TAB 36 COUNT PICTURE "^^^^"  "Blocs" &
        TAB 67 "Sous-total:"                &
        TAB 81 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
        TAB 92 BLCTFINAL SUBTOTAL PICTURE "^^^^^.^^" &
SKIP 2

FINAL FOOTING  &
SKIP 2  &
        TAB 39 "______________"            &
        TAB 79 "__________"         &
        TAB 91 "_________"          &
SKIP 1  &
        TAB 3 "Grand total:"        &
        TAB 36 COUNT PICTURE "^^^^" "Blocs"        &
        TAB 80 BLCM3TOT SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 91 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 2
BUILD  if309z
