;/T/ Consultation des codes bilingue   XELNOM, CBICLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 27 février 1992
;
;    Description..: Permet de visionner et de choisir un code bilingue
;
;    Paramètres...: XELNOM    ==> - nom de la variable
;                                 - segment de la clé de recherche
;                                 - Ex: FOUCAT, CPTNIVDET
;
;                   CBICLE    ==> - code
;                                 - segment de la clé de recherche
;                                 - peut contenir la valeur @
;                                 - contient la valeur de retour.
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc102 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             NOCOMMIT &
             RECEIVING D_XELNOM, T_CBICLE

USE mc102.hlp NOLIST

USE ustrasse  NOLIST ; Transaction QUERY pour les sous-écrans


DEFINE    D_XELNOM CHARACTER SIZE 16
TEMPORARY T_CBICLE CHARACTER SIZE 4

FILE VCBI_DSC         PRIMARY &
                      OCCURS 11
 ACCESS VIA XELNOM, CBICLE &
        USING D_XELNOM, T_CBICLE &
        ORDERBY CBICLE
  ;
  ; On ne peut pas saisir le type d'écriture "automatique".
  ;
  SELECT IF (( XELNOM    OF VCBI_DSC = "LGLTYPECR" OR    &
               XELNOM    OF VCBI_DSC = "ECJTYPECR" ) AND &
             CBICLE OF VCBI_DSC <> "AU" AND &
             CBICLE OF VCBI_DSC <> "RF" ) OR &
            (XELNOM OF VCBI_DSC <> "LGLTYPECR" AND &
             XELNOM OF VCBI_DSC <> "ECJTYPECR")

USE ushilite NOLIST
USE usdrwfnd NOLIST

SKIP TO 2

ALIGN(3,3,6)(,,11)
CLUSTER OCCURS WITH VCBI_DSC

FIELD CBICLE    OF VCBI_DSC HIDDEN LABEL ">>" DISPLAY
FIELD CBIDSC    OF VCBI_DSC DISPLAY SIZE 30

CLUSTER

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CBICLE, "@")
  THEN LET T_CBICLE = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_CBICLE = CBICLE OF VCBI_DSC
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       *****************************************
