;/T/ Validation sommaire des paiements annulés
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 9 février 1994
;
;    Description...: Liste de validation des lots des paiements annulés
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
SET NOBLANK           ; Empêche l'impression de ligne blanche

;
; Subfile contenant les paiements à imprimer
;

ACCESS *WCP310A &

  ;
  ; Récupération de la référence à qui est destiné le paiement
  ;

  LINK REFCIECLE OF WCP310A , &
       REFCLETYP OF WCP310A , &
       REFCLENUM OF WCP310A , &
       RADCLE    OF WCP310A   &
    TO REFCIECLE            , &
       REFCLETYP            , &
       REFCLENUM            , &
       RADCLE               OF MCREF_ADRESSE &
  OPTIONAL &


  LINK REFCIECLE OF WCP310A , &
       REFCLETYP OF WCP310A , &
       REFCLENUM OF WCP310A   &
    TO REFCIECLE            , &
       REFCLETYP            , &
       REFCLENUM OF MCREFERENCE OPTIONAL &


  LINK CIECLE OF WCP310A  , &
       CLICLE OF WCP310A  &
    TO CLICIECLE ,&
       CLICLE OF CRCLIENT OPTIONAL

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_310A CHARACTER SIZE 5 = "1" + PECCLE OF WCP310A &
         IF PECCLE OF WCP310A[1:1] < "8" &
         ELSE "0" + PECCLE OF WCP310A

SORT ON CIECLE    OF WCP310A, &
        D_PECCLE_310A, &
        LCPCLE    OF WCP310A, &
        REFCLETYP OF WCP310A, &
        REFCLENUM OF WCP310A, &
        RADCLE    OF WCP310A, &
        LBQCPTENC OF WCP310A, &
        PAMCLE    OF WCP310A, &
        PAMDEPDIR OF WCP310A, &
        CAPCLE    OF WCP310A, &
        CPITIMSTP OF WCP310A



;-----------------------------------------------------------------------------------------------------------------------------------

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 & ; Premier titre du rapport
@IF FRANCAIS
        = "Validation sommaire des paiements annulés"
@ELSE
        = "%%%Validation sommaire des paiements annulés"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 & ; Deuxiemme titre du rapport
        = " "

DEFINE DZ_NUMRAP  CHARACTER SIZE 12 & ; numéro du rapport
        = "CP310"

DEFINE DZ_CIENOM  CHARACTER SIZE 40 & ; Nom de la compagnie
       = PARM

USE usflgdet NOLIST ;Flag Bilingue (O/N)

;
;Indique si le lot est hors balance
;
DEFINE D_HRSBLC   CHARACTER SIZE 3 &
        = DZ_FLGAFFO IF LCPNBRTRSERR OF WCP310A <> 0 ELSE &
          DZ_FLGAFFN


;
;Indique si le chèque est de type dépense direct ou fournisseur
;
DEFINE  D_PAMDEPDIR CHARACTER SIZE 4 &
        = "Dir." IF PAMDEPDIR OF WCP310A <> "0" ELSE &
          "Fou."


DEFINE D_MNTNET INTEGER SIZE 4 &
       = CPHMNTPYE OF WCP310A &
           IF PAMDEPDIR OF WCP310A = "0" ELSE &
             PAMMNT - PAMMNTCTI - PAMMNTRTI

;
; Indique si le lot est journalisé
;
DEFINE  D_JOURNALISE CHAR SIZE 12 &
@IF FRANCAIS
        = "Journalisé" &
@ELSE
        = "Posted" &
@ENDIF
        IF LCPDATJOU OF WCP310A <> 0 ELSE ""


;
; Affichage d'une "*" en face de chaque transactions dont l'imputation comptable
; ne balance pas avec le montant total.
;
DEFINE  D_BALANCE  CHARACTER SIZE 1 &
        = "*" IF PAMMNT OF WCP310A <> PAMCUMMNT OF WCP310A ELSE ""




USE ustit220 NOLIST  ; Titre standard pour l'application a 220
SKIP 2 &
@IF FRANCAIS
    TAB 001 "Lot....: "   &
@ELSE
    TAB 001 "Batch..: "   &
@ENDIF
    TAB 011 LCPCLE OF WCP310A &
    TAB 016 LCPDSC OF WCP310A &
    TAB 061 "Hors balance = " &
    TAB 076 D_HRSBLC          &
  SKIP &
@IF FRANCAIS
    TAB 001 "Période: " &
@ELSE
    TAB 001 "Period.: " &
@ENDIF
    TAB 010 PECCLE OF WCP310A &
    TAB 061 D_JOURNALISE      &
  SKIP 2 &
TAB 001 "  Numéro Numéro  Numéro  Date     Compte   Type                      Numéro" &
TAB 152 "         SS-            Unité                           Co Co Refact" &
  SKIP 1 &
TAB 001 "* fourn. chèque  annul.  annul.   encaisse paie.  Montant chèque Dev facture" &
TAB 084 "Montant facture    Escompte         CTI         RTI     Montant net " &
TAB 152 "Projet   prj Immobilis.   adm.     Compte       Montant TF TP client" &
  SKIP 1 &
TAB 001 "- ------ ------- ------- -------- -------- ----- --------------- --- ------------" &
TAB 083 " --------------- ----------- ----------- ----------- ---------------" &
TAB 152 "-------- --- ------------ -------- ------ ------------- -- -- ------"



REPORT &
    TAB 001  D_BALANCE                    PRINT AT PAMCLE &
    TAB 003  REFCLENUM   OF WCP310A       PRINT AT REFCLENUM &
    TAB 010  RADNOMABR   OF MCREF_ADRESSE PRINT AT RADCLE &
    TAB 051  PAMDSC1     OF WCP310A       PRINT AT CAPCLE &
    TAB 092  PAMDSC2     OF WCP310A       PRINT AT CAPCLE &
  SKIP 1 &
    TAB 010  PAMPAMCLEANN OF WCP310A PRINT AT PAMCLE &
    TAB 018  PAMCLE       OF WCP310A PRINT AT PAMCLE &
    TAB 026  PAMDAT       OF WCP310A PRINT AT PAMCLE &
    TAB 035  LBQCPTENC    OF WCP310A PRINT AT PAMCLE &
    TAB 044  D_PAMDEPDIR             PRINT AT PAMCLE &
    TAB 050  PAMMNT       OF WCP310A SUBTOTAL &
      PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ PRINT AT PAMCLE  &
    TAB 066  DEVCLE      OF MCREFERENCE PRINT AT PAMCLE &

    TAB 070  CAPCLE      OF WCP310A PRINT AT CAPCLE &
    TAB 084  CAPMNT      OF WCP310A SUBTOTAL &
       PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ PRINT AT CAPCLE   &
    TAB 100  CPHMNTESC OF WCP310A SUBTOTAL &
       PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ PRINT AT CAPCLE  &
    TAB 112  PAMMNTCTI   OF WCP310A SUBTOTAL &
       PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ PRINT AT CAPCLE &
    TAB 124  PAMMNTRTI   OF WCP310A SUBTOTAL &
       PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ PRINT AT CAPCLE &
    TAB 136  D_MNTNET               SUBTOTAL &
       PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ PRINT AT CAPCLE &

    TAB 152  PRJCLE      OF WCP310A &
    TAB 161  PRACLE      OF WCP310A &
    TAB 165  IMMCLE      OF WCP310A &
    TAB 178  UNACLE      OF WCP310A &
    TAB 187  CPTCLE      OF WCP310A &
    TAB 194  CPIMNT      OF WCP310A  PICTURE "^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
    TAB 208  CPITAXCODTPS OF WCP310A &
    TAB 211  CPITAXCODTVQ OF WCP310A &
    TAB 214  CLICLE      OF CRCLIENT &
 SKIP


FOOTING AT LCPCLE &
  SKIP 2 &
TAB 001 "                          Montant cumulé            Montant brut" &
TAB 084 "Montant facture    Escompte         CTI         RTI     Montant net" &
TAB 192 " Montant imputé " &
 SKIP &
TAB 001 "                         ---------------         ---------------" &
TAB 083 " --------------- ----------- ----------- ----------- ---------------" &
TAB 192 "---------------" &
  SKIP &
TAB 001 "Total du lot " &
TAB 014 LCPCLE    OF WCP310A &
TAB 025 LCPCUMMNT OF WCP310A  SUBTOTAL AT LCPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
TAB 050 PAMMNT    OF WCP310A  SUBTOTAL AT PAMCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
TAB 084 CAPMNT    OF WCP310A  SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 100 CPHMNTESC OF WCP310A SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 112 PAMMNTCTI OF WCP310A  SUBTOTAL AT PAMCLE PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 124 PAMMNTRTI OF WCP310A  SUBTOTAL AT PAMCLE PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 136 D_MNTNET              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 192 CPIMNT    OF WCP310A  SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
  SKIP &
TAB 001 "                         ---------------         ---------------" &
TAB 083 " --------------- ----------- ----------- ----------- ---------------" &
TAB 192 "---------------" &
  SKIP PAGE


FINAL FOOTING &
  SKIP 2 &
TAB 001 "                          Montant cumulé            Montant brut" &
TAB 084 "Montant facture    Escompte         CTI         RTI     Montant net" &
TAB 192 " Montant imputé " &
 SKIP &
TAB 001 "                         ---------------         ---------------" &
TAB 083 " --------------- ----------- ----------- ----------- ---------------" &
TAB 192 "---------------" &
  SKIP &
TAB 001 "Total pour tous les lots" &
TAB 025 LCPCUMMNT OF WCP310A  SUBTOTAL AT LCPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
TAB 050 PAMMNT    OF WCP310A  SUBTOTAL AT PAMCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 &
TAB 084 CAPMNT    OF WCP310A  SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 100 CPHMNTESC OF WCP310A SUBTOTAL AT CAPCLE PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 112 PAMMNTCTI OF WCP310A  SUBTOTAL AT PAMCLE PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 124 PAMMNTRTI OF WCP310A  SUBTOTAL AT PAMCLE PICTURE "^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 136 D_MNTNET              SUBTOTAL AT CAPCLE PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
TAB 192 CPIMNT    OF WCP310A  SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " SIGNIFICANCE 4 BWZ &
  SKIP &
TAB 001 "                         ---------------         ---------------" &
TAB 083 " --------------- ----------- ----------- ----------- ---------------" &
TAB 192 "---------------"


BUILD cp310z

@IF FORMAT

                                                                                                   1         1         1         1         1         1         1         1         1         1         2          2        2
         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0          1        2
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890012345678901234567890
CP310                                                                     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                         Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                                                  Validation sommaire des paiements
                                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Lot....:  xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     Hors balance = xxx  Journalisé
Période: 99-99

  Numéro Numéro  Numéro  Date     Compte   Type                      Numéro                                                                                    SS-            Unité                           Co Co Refact
* fourn. chèque  annul.  annul.   encaisse paie.  Montant chèque Dev facture      Montant facture    Escompte         CTI         RTI     Montant net Projet   prj Équipement adm.     Compte         Montant TF TP client
- ------ ------- ------- -------- -------- ----- --------------- --- ------------ --------------- ----------- ----------- ----------- --------------- -------- --- ---------- -------- ------ --------------- -- -- ------
x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
         xxxxxxx xxxxxxx 99/99/99  xxxxxx   xxx  999,999,999.99- xxx xxxxxxxxxxxx 999,999,999.99- 999,999.99- 999,999.99- 999,999.99- 999,999,999.99- xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx

                                                                     xxxxxxxxxxxx 999,999,999.99- 999,999.99- 999,999.99- 999,999.99- 999,999,999.99- xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx

x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
         xxxxxxx xxxxxxx 99/99/99  xxxxxx   xxx  999,999,999.99- xxx xxxxxxxxxxxx 999,999,999.99- 999,999.99- 999,999.99- 999,999.99- 999,999,999.99- xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx

                                                                     xxxxxxxxxxxx 999,999,999.99- 999,999.99- 999,999.99- 999,999.99- 999,999,999.99- xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx
                                                                                                                                                      xxxxxxxx xxx xxxxxxxxxx xxxxxxxx xxxxxx 999,999,999.99- xx xx xxxxxx



                          Montant cumulé            Montant brut                  Montant facture    Escompte         CTI         RTI     Montant net                                          Montant imputé
                         ---------------         ---------------                  --------------- ----------- ----------- ----------- ---------------                                         ---------------
Total du lot xxxx        999,999,999.99-         999,999,999.99-                  999,999,999.99- 999,999.99- 999,999.99- 999,999.99- 999,999,999.99-                                         999,999,999.99-
                         ---------------         ---------------                  --------------- ----------- ----------- ----------- ---------------                                         ---------------


                          Montant cumulé            Montant brut                  Montant facture    Escompte         CTI         RTI     Montant net                                          Montant imputé
                         ---------------         ---------------                  --------------- ----------- ----------- ----------- ---------------                                         ---------------
Total pour tous les lots 999,999,999.99-         999,999,999.99-                  999,999,999.99- 999,999.99- 999,999.99- 999,999.99- 999,999,999.99-                                         999,999,999.99-
                         ---------------         ---------------                  --------------- ----------- ----------- ----------- ---------------                                         ---------------



@ENDIF
