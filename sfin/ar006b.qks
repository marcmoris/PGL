;/T/ Réquisition par fournisseur
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 13 Avril 1994
;
;    Description..: 
;  
;-------------------------------------------------------------------------------
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar006b ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              ACTIVITIES FIND &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU  , &
                        T_CIENOM     , &
                        T_PECCLEMNU  , &
                        T_FOUCIECLE  , &
                        ARREQUISITION

USE ussectmp NOLIST     ; Variable pour la sécurité
    "AR006B"            ; Nom de l'écran

USE ar006b.hlp NOLIST   ; Use servant à la documentation de l'écran
                      

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-écrans

USE ustrasse NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_FOUCIECLE CHARACTER SIZE 04 ; # compagnie fourn.
TEMPORARY T_PECCLEMNU CHARACTER SIZE 04 ; Période courante ou subséquente.

DEFINE    DZ_CIECLE   CHARACTER SIZE 04 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Table maitre passer en parametre.
;
FILE ARREQUISITION        MASTER

;-------------------------------------------------------------------------------
;
; Table primare.
;
FILE ARREQ_FOU           PRIMARY &
                        NOITEM

  ACCESS VIA    CIECLE   , &
                REQCLE   , &
                FOUCIECLE, &
                FOUCLE     &
         USING  CIECLE    OF ARREQUISITION , &
                REQCLE    OF ARREQUISITION , &
                T_FOUCIECLE, &
                FOUCLE    OF ARREQ_FOU    &
        REQUEST FOUCLE    OF ARREQ_FOU

  ACCESS VIA    CIECLE   , &
                REQCLE   , &
                FOUCIECLE  &
         USING  CIECLE    OF ARREQUISITION , &
                REQCLE    OF ARREQUISITION , &
                T_FOUCIECLE &
        ORDERBY CIECLE    , &
                REQCLE    , &
                FOUCIECLE , &
                FOUCLE

;-------------------------------------------------------------------------------
; 
; Validation du numéro d'adresse et ajout de la nouvelle adresse pour les
; fournisseur divers.
;
FILE MCREF_ADRESSE     SECONDARY &
                      NOITEM &
                      NODELETE

  ACCESS  VIA   REFCIECLE, &
                REFCLETYP, &
                REFCLENUM, &
                RADCLE &
          USING FOUCIECLE OF ARREQ_FOU, &
                REFCLETYP OF ARREQ_FOU, &
                FOUCLE    OF ARREQ_FOU, &
                RADCLE    OF ARREQ_FOU

;-------------------------------------------------------------------------------
;
; Permet d'avoir l'information du fournisseur pour la cie de transaction.
;
FILE VFOC_FOU            REFERENCE &
                        NOITEM

  ACCESS VIA   FOUCIECLE, &
               FOUCLE   , &
               FOCCIECLE  &
         USING FOUCIECLE OF ARREQ_FOU, &
               FOUCLE    OF ARREQ_FOU, &
               CIECLE    OF ARREQ_FOU

;-------------------------------------------------------------------------------
;
; Pour la présentation de la devise ou non.
;
FILE MCHOLDING           REFERENCE &
                        NOITEM
  ACCESS VIA   CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;
; Fichier pour génération automatique du détail de soumission.
;
FILE MCDEVISE            REFERENCE

  ACCESS VIA    DEVCLE &
         USING  DEVCLE OF VFOC_FOU

;-------------------------------------------------------------------------------
;
; Déclaration des variables temporaire.
;

TEMPORARY T_FOUCLE    CHARACTER SIZE 06 ; Pour le Pop Up du fournisseur.

DEFINE    D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING OR &
             0 = SIZE(TRUNCATE(FOUCLE OF ARREQ_FOU)) &
          ELSE "/" + DEVCLE OF VFOC_FOU

;-------------------------------------------------------------------------------

DRAW 3,01 TO 13,80

USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Réquisition " &
@ELSE
      "%%%quisition " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           

SKIP 1

ALIGN(3,3,19) (,,26)

FIELD FOUCLE    OF ARREQ_FOU &
        HIDDEN ID 05 

FIELD RADNOM    OF MCREF_ADRESSE 
FIELD RADCLE    OF ARREQ_FOU &
        HIDDEN ID 10 &
        BWZ &
@IF FRANCAIS
        LABEL "Adresse achat.:" 
@ELSE
        LABEL "%%%esse achat.:" 
@ENDIF

FIELD RADADR1   OF MCREF_ADRESSE &
        DISPLAY

SKIP 1

ALIGN(3,3,19) (,,34) (,43,59)

FIELD RQFMNTTOT OF ARREQ_FOU &
        HIDDEN ID 15 &
        DISPLAY
FIELD D_DEVCLE &
        DISPLAY
FIELD RQFMNTNET OF ARREQ_FOU &
        DISPLAY

ALIGN(3,3,19) (,43,59)

FIELD RQFMNTTPS OF ARREQ_FOU &
        HIDDEN ID 20 &
        DISPLAY
FIELD RQFMNTCTI OF ARREQ_FOU &
        DISPLAY
FIELD RQFMNTTVQ OF ARREQ_FOU &
        HIDDEN ID 25 &
        DISPLAY
FIELD RQFMNTRTI OF ARREQ_FOU &
        DISPLAY
FIELD RQFMNTESC OF ARREQ_FOU &
        HIDDEN ID 30 &
        DISPLAY

SKIP TO GROUP 2

FIELD RQFNBRITE OF ARREQ_FOU &
        DISPLAY

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END
;-------------------------------------------------------------------------------
;
; Pour le choix du fournisseur
;
PROCEDURE INPUT FOUCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FOUCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cp104 MODE F PASSING T_FOUCIECLE, T_FOUCLE, T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE(T_FOUCLE)
  END
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************* Réquisition **********************************
*                                                                              *
* Fournisseur...: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
* Adresse achat.: xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
*                                                                              *
* Montant brut..: xxxxxxxxxxxxxxxxxxx     Montant net...: xxxxxxxxxxxxxxx      *
* Montant TPS...: xxxxxxxxxxxxxxx         Montant CTI...: xxxxxxxxxxxxxxx      *
* Montant TVQ...: xxxxxxxxxxxxxxx         Montant RTI...: xxxxxxxxxxxxxxx      *
* Montant esc...: xxxxxxxxxxxxxxx                                              *
*                                         Nbre items....: xxx                  *
********************************************************************************
@ENDIF
