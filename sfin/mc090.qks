;/T/ Cons. des écritures comptable     HISCLE
;
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 15 avril 1992
;
;    Description..: Consultation des ventilation comptable généré par le
;                   traitement d'une journalisation.
;
;
;    Paramètre....: HISCLE = Numéro d'écriture
;

;/V/ Écran vérifier pour le passage à l'an 2000

;-------------------------------------------------------------------------------
SCREEN mc090 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK &
             FROM 11,1 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING D_HISCLE

USE ussectmp  NOLIST
    "MC090"

USE mc090.hlp NOLIST

USE usactecr  NOLIST

USE ustrasse  NOLIST

;-------------------------------------------------------------------------------
;

DEFINE D_HISCLE INTEGER UNSIGNED SIZE 4

;-------------------------------------------------------------------------------
;

FILE MCHIS_ECR        PRIMARY &
                      OCCURS 8 &
                      NOITEM

  ACCESS VIA HISCLE &
         USING D_HISCLE

;
; Affiche la description du compte.
;
FILE VCPT_DSC         REFERENCE
  ACCESS VIA CPTCLE &
         USING CPTCLE OF MCHIS_ECR


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO 13, 1
DRAW FROM  3,80 TO 13,80
DRAW FROM 13, 1 TO 13,80

SKIP 1

TITLE &
@IF FRANCAIS
 " Ecriture comptable " &
@ELSE
 " Accounting Entries " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

TITLE &
@IF FRANCAIS
  "                                                   Montant         Montant" &
@ELSE
  "                                                   %%%%%%%         %%%%%%%" &
@ENDIF
      AT ,3

SKIP

TITLE &
@IF FRANCAIS
  "Compte Description           Unité adm.              débit          crédit" &
@ELSE
  "Accnt  Description           Adm. unit               debit          credit" &
@ENDIF
  AT ,3

SKIP

ALIGN (3,2,3) (,,10) (,,32) (,,49) (,,65)

CLUSTER OCCURS WITH MCHIS_ECR

FIELD CPTCLE    OF MCHIS_ECR ID 01 HIDDEN LABEL " "

FIELD CPTDSCABR OF VCPT_DSC DISPLAY

FIELD UNACLE    OF MCHIS_ECR

FIELD HECMNTDBT OF MCHIS_ECR &
      BWZ

FIELD HECMNTCRT OF MCHIS_ECR &
      BWZ

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

BUILD
@IF FORMAT

****************************** Ecriture comptable *****************************x
*                                                    Montant         Montant   *
* Compte Description           Unité adm.              débit          crédit   *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
* xxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxx         xxxxxxxxxxxxx   xxxxxxxxxxxxx  *
********************************************************************************
@ENDIF
