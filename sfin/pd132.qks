;/T/ Dépisteur Facture
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-10-24
;
;    Description..: Permet de visionner et de choisir une facture
;
;    Paramètres...: T_FTRNUMFAC ==> Numéro de facture
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd132 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_FTRNUMFAC,&
                       T_CIECLEMNU


TEMPORARY T_FTRNUMFAC CHARACTER SIZE 07
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd132.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Facture
;

FILE PDFACTURE      PRIMARY OCCURS 10

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 FTRNUMFAC

SELECT IF MATCHPATTERN(TRUNCATE(ASCII(FTRNUMFAC OF PDFACTURE)),TRUNCATE(T_FTRNUMFAC))


;-------------------------------------------------------------------------------
;
; Lien facture - bon de livraison
;

FILE PDFACT_BON_LIVR REFERENCE OCCURS WITH PDFACTURE

  ACCESS VIA   CIECLE,       &
               FTRNUMFAC     &
         USING T_CIECLEMNU,  &
               FTRNUMFAC OF PDFACTURE


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,40 ; Remplace le DRAW standard

USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2


TITLE &
@IF FRANCAIS
      "   Facture T Date       Expédition " &
@ELSE
      "%%% Facture T Date     Expédition "
@ENDIF
      AT ,2


SKIP


ALIGN (03,03,05) (,,13) (,,15) (,,26) (,,36)


CLUSTER OCCURS WITH PDFACTURE


FIELD FTRNUMFAC OF PDFACTURE HIDDEN LABEL ">>" DISPLAY SIZE 7


FIELD FTRTYPFAC OF PDFACTURE HIDDEN SIZE 1


FIELD FTRDATFAC OF PDFACTURE  FORMAT YYYYMMDD PATTERN "####/##/##" HIDDEN


FIELD EXPNUMEXP OF PDFACT_BON_LIVR HIDDEN SIZE 9


FIELD BLINUMBLI OF PDFACT_BON_LIVR HIDDEN NUM SIZE 2


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_FTRNUMFAC , "@")
  THEN LET T_FTRNUMFAC = ""
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_FTRNUMFAC = ascii(FTRNUMFAC OF PDFACTURE,7)
  RETURN

END

BUILD


@IF FORMAT

**************************************
*    Facture T Date     Expédition   *
* >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         * >> xxxxxxx c AA/MM/JJ xxxxxxxxx-xx *
                                         **************************************
@ENDIF
