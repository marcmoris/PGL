;/T/ Consultation d'une ligne de commande
;
;/P/ Programmeur..: Francois Richard
;    Date Creation: 12 octobre 1999
;
;    Description..: Permet de visionner et de choisir une ligne de detail
;                   d'une commande
;
;    Parametres...: T_CIECLE        = Compagnie
;                   T_PJTABR        = Abréviation projet
;                   T_PJTANN        = Année du projet
;                   T_COMNUMCOMINT  = Numéro de commande
;                   T_DCINUMLIG     = Numéro de ligne de commande
;                   T_TPDTYPPRD     = Type de produit
;                   T_TGRCODGRA     = Code de granite
;
;-------------------------------------------------------------------------------

SCREEN if111 NOMODE NOACTION FIELDMARK &
             FROM 11,24 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_PJTABR, &
                        T_PJTANN, &
                        T_COMNUMCOMINT, &
                        T_DCINUMLIG, &
                        T_TPDTYPPRD, &
                        T_TGRCODGRA

USE ustrasse NOLIST

USE ussectmp NOLIST
    "IF111"

;USE if111.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE       CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_PJTABR       CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_PJTANN       CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 3 RESET AT STARTUP
TEMPORARY T_DCINUMLIG    CHARACTER SIZE 3 RESET AT STARTUP
TEMPORARY T_TPDTYPPRD    CHARACTER SIZE 2 RESET AT STARTUP
TEMPORARY T_TGRCODGRA    CHARACTER SIZE 3 RESET AT STARTUP

;-------------------------------------------------------------------------------

FILE PDDETAIL_C_I PRIMARY OCCURS 10

  ACCESS  VIA     CIECLE,         &
                  PJTABR,         &
                  PJTANN,         &
                  COMNUMCOMINT,   &
                  DCINUMLIG,      &
                  TPDTYPPRD,      &
                  TGRCODGRA       &
          USING   T_CIECLE,       &
                  T_PJTABR,       &
                  T_PJTANN,       &
                  T_COMNUMCOMINT, &
                  T_DCINUMLIG,    &
                  T_TPDTYPPRD,    &
                  T_TGRCODGRA     &
          ORDERBY PJTABR,         &
                  PJTANN,         &
                  COMNUMCOMINT,   &
                  DCINUMLIG,      &
                  TPDTYPPRD,      &
                  TGRCODGRA

;-------------------------------------------------------------------------------

USE ushilite NOLIST

DRAW 1,1 TO 13,57

USE ustitoff NOLIST

SKIP 1

SKIP

TITLE "No comm"       AT ,5
TITLE "Lig"           AT ,16
TITLE "Prd"           AT ,21
TITLE "Typ"           AT ,25
TITLE "Quantité"      AT ,29
TITLE "Cat  Qlt  Clr" AT ,43

ALIGN (2,3,5)(,7,8)(,10,11)(,,16)(,,22)(,,25)(,,29)(,,43)(,,48)(,,53)

CLUSTER OCCURS WITH PDDETAIL_C_I

FIELD PJTABR OF PDDETAIL_C_I       &
      DISPLAY                      &
      HIDDEN                       &
      LABEL ">>"

FIELD PJTANN OF PDDETAIL_C_I       &
      LABEL "-"                    &
      DISPLAY

FIELD COMNUMCOMINT OF PDDETAIL_C_I &
      LABEL "-"                    &
      DISPLAY

FIELD DCINUMLIG OF PDDETAIL_C_I    &
      DISPLAY

FIELD TPDTYPPRD OF PDDETAIL_C_I    &
      DISPLAY

FIELD TGRCODGRA OF PDDETAIL_C_I    &
      DISPLAY

FIELD DCIQTECMD OF PDDETAIL_C_I    &
      DISPLAY

FIELD CATCLE OF PDDETAIL_C_I       &
      DISPLAY

FIELD QLTCLE OF PDDETAIL_C_I       &
      DISPLAY

FIELD CLRCLE OF PDDETAIL_C_I       &
      DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_DCINUMLIG, "@")
  THEN LET T_DCINUMLIG = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_DCINUMLIG = DCINUMLIG OF PDDETAIL_C_I
  RETURN
END

BUILD

