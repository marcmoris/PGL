;/T/ Ligne de la facture
;
; ECRAN: FAC_DET.QKS
; FONC.: AFFICHAGE DES LIGNES DE LA FACTURE, IL EST POSSIBLE DETRUIRE
;        DES LIGNES MAIS IL EST IMPOSSIBLE D'EN AJOUTE OU D'EN MODIFIES
;
;/M/ François Déry, 25 mai 1999
;       Conversion unix/interbase.
;
SCREEN if009a ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND,DELETE &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM, &
                       IFFACTURE

USE ussectmp  NOLIST
    "IF009A"

USE if009a.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFFACTURE MASTER

FILE IFFACTURE_DETAIL PRIMARY OCCURS 16  NOITEM
  ACCESS VIA   CIECLE, &
               FCECLE  &
         USING T_CIECLE, &
               FCECLE OF IFFACTURE

FILE IFBLOC REFERENCE OCCURS WITH IFFACTURE_DETAIL
  ACCESS VIA   CIECLE, &
               CRRCLENUM, &
               CRRCLEANN, &
               BLCCLE &
         USING T_CIECLE, &
               CRRCLENUM OF IFFACTURE_DETAIL, &
               CRRCLEANN OF IFFACTURE_DETAIL, &
               BLCCLE OF IFFACTURE_DETAIL

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      "Detail de la facture" &
      CENTERED AT ,1
SKIP TO LINE 4
TITLE "No bloc    Dimensions        M3        Prix     Prix    No   Dev   Total  " AT ,4
SKIP
TITLE "         LongxHautxLarg     Total      FOB      FAS     Exp               " AT ,4
ALIGN (2,3,5)(,,7)(,8,9)(,,15)(,19,21)(,25,27)(,,31)(,,39)(,,48)(,,58)(,,65)(,,69)
CLUSTER OCCURS WITH IFFACTURE_DETAIL
FIELD CRRCLENUM OF IFFACTURE_DETAIL &
        HIDDEN &
        LABEL " "
FIELD CRRCLEANN OF IFFACTURE_DETAIL
FIELD BLCCLE  OF IFFACTURE_DETAIL LABEL "-"
FIELD FDEDIMLON OF IFFACTURE_DETAIL
FIELD FDEDIMHAU OF IFFACTURE_DETAIL LABEL "x"
FIELD FDEDIMLAR OF IFFACTURE_DETAIL LABEL "x"
FIELD FDEM3PROD OF IFFACTURE_DETAIL
FIELD FDEPRIXFOB OF IFFACTURE_DETAIL PICT " ^^^^.^^"
FIELD FDEPRIXFAS OF IFFACTURE_DETAIL
FIELD EXNCLE OF IFBLOC
FIELD DEVCLE OF IFFACTURE_DETAIL
FIELD FDETOTAL OF IFFACTURE_DETAIL  NOENTRY NOCHANGE
CLUSTER

PROCEDURE PREUPDATE
BEGIN
   IF FCEFIN OF IFFACTURE = "O"
   THEN ERROR "*E* LA IFFACTURE EST DEJA FINALE..."
END

;
; On recalcule les montants de taxes lors du retour a l'ecran
; IF-009 dans la procedure designer 8
;
PROCEDURE DELETE
BEGIN
  IF NOT DELETEDRECORD OF IFFACTURE_DETAIL
  THEN LET FCETOTAL OF IFFACTURE = FCETOTAL OF IFFACTURE - FDETOTAL
  DELETE IFFACTURE_DETAIL
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************Detail de la facture******************************
*  No bloc    Dimensions        M3        Prix     Prix    No   Dev   Total    *
*           LongxHautxLarg     Total      FOB      FAS     Exp                 *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*   xxx-xxxxx xxx x xxx x xxx xxxxxx  xxxxxxxx xxxxxxx   xxxxxx xxx xxxxxxxxx  *
*                                                                              *
********************************************************************************
@ENDIF
