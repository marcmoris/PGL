;/T/ Consultation ligne de réception CIECLE, RECCLE, REDCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 20 avril 1994
;
;    Description..: Permet de visionner et de choisir une ligne de
;                   facture.
;
;    Paramètre....: CIECLE ==> - compagnie de la réception. 
;                              - segment de la clé de recherche.
;
;                   RECCLE ==> - numéro de réception.
;                              - segment de la clé de recherche.
;
;                   REDCLE ==> - segment de la clé de recherche.
;                              - peut contenir le caractère '@'.
;                              - contient la valeur de retour.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar113 NOMODE NOACTION FIELDMARK &
             FROM 11,18 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_RECCLE, &
                        T_DEICLE
                     

USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR113"

USE ar113.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Compagnie de la réception.
TEMPORARY T_RECCLE CHARACTER SIZE 12 ; Numéro de réception.
TEMPORARY T_DEICLE CHARACTER SIZE 7  ; Numéro d'item.


;-------------------------------------------------------------------------------
;
FILE ARREC_DETAIL      PRIMARY &
                      OCCURS 10

 ACCESS   VIA     CIECLE, &
                  RECCLE  &
          USING   T_CIECLE, &
                  T_RECCLE  &
          ORDERBY CIECLE , &
                  RECCLE , &
                  REDCLE

  ; On ne sélectionne que les lignes de réception saisies ou sélectionnées dont
  ; la facturation n'est pas terminée.
  ;
 SELECT IF (   REDSTU OF ARREC_DETAIL = "S" &
            OR REDSTU OF ARREC_DETAIL = ""  &
           ) &
           AND REDQTEREC OF ARREC_DETAIL > REDQTEFAC OF ARREC_DETAIL
  
           

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST 	

DRAW 1,1 TO 13,63

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

@IF FRANCAIS
    TITLE "Item    Produit       Qté rec   Qté fac      Prix" AT 2,7
@ELSE 
   TITLE "%%%Item    Produit       Qté rec   Qté fac      Prix" AT 2,7
@ENDIF


ALIGN(3,3,6)(,,14) (,,27) (,,37) (,,47)

CLUSTER OCCURS WITH ARREC_DETAIL

FIELD REDCLE    OF ARREC_DETAIL HIDDEN LABEL ">>"
FIELD PRDCLE    OF ARREC_DETAIL
FIELD REDQTEREC OF ARREC_DETAIL &
  PICTURE "^^^^^.^^^"
FIELD REDQTEFAC OF ARREC_DETAIL &
  PICTURE "^^^^^.^^^"
FIELD REDPRIUNI OF ARREC_DETAIL
 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_DEICLE, "@")
  THEN LET T_DEICLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_DEICLE = ASCII (REDCLE OF ARREC_DETAIL,6)[1:3] + "." + &
                 ASCII (REDCLE OF ARREC_DETAIL,6)[4:6]   
  RETURN
END

BUILD
@IF FORMAT
                 ***************************************************************
                 *     Item    Produit       Qté rec   Qté fac      Prix       *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 ***************************************************************
@ENDIF
