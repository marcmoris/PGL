;/T/ Entrepôt
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 17 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base
;                   concernant les entrepôt
;
;
SCREEN if156 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF156"

;USE if156.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFENTREPOT    IN DICT PRIMARY NOITEM 

  ACCESS VIA     CIECLE,   &
                 ETRCLE    &
         USING   T_CIECLE, &
                 ETRCLE    &
         REQUEST ETRCLE    &
         ORDERBY ETRCLE

  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY ETRCLE

  ITEM CIECLE OF IFENTREPOT INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
; Codes bilingues du statut de l'entrepôt
;
DEFINE    D_ETRSTU CHARACTER SIZE 16 = "ETRSTU"
TEMPORARY T_ETRSTU CHARACTER SIZE 4

FILE VCBI_DSC          REFERENCE ALIAS A_CBI_ETRSTU 
  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_ETRSTU, &
                ETRSTU OF IFENTREPOT

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Entrepôt " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

ALIGN (2,3,21) (,43,59) (,,61)
FIELD ETRCLE OF IFENTREPOT           &
      LABEL "Code entrepôt...:"   &
      HIDDEN                         &
      REQUIRED                       &
      NOCHANGE                       &
      NULL VALUE NOT ALLOWED         &
      LOOKUP NOTON IFENTREPOT        &
         VIA   CIECLE ,              &
               ETRCLE                &
         USING CIECLE OF IFENTREPOT, &
               ETRCLE OF IFENTREPOT  &
         MESSAGE 3241 ; PD341 Cet entrepot existe deja

FIELD ETRSTU OF IFENTREPOT         &
      REQUIRED                     &
      NULL VALUE NOT ALLOWED       &
      HIDDEN                       &
      LOOKUP ON A_CBI_ETRSTU       &
        VIA   XELNOM,              &
              CBICLE               &
        USING D_ETRSTU,            &
              ETRSTU OF IFENTREPOT &
        MESSAGE 3242 ; Ce statut d'entrepot est invalide.

FIELD CBIDSC OF A_CBI_ETRSTU &
        SIZE 10              &
        DISPLAY

SKIP 1

ALIGN (2,3,24)  
FIELD ETRDSCFRA001 OF IFENTREPOT &
      REQUIRED                   &
      NULL VALUE NOT ALLOWED     &
      HIDDEN                     &
      LABEL "Desc. francaise.: 1."

ALIGN (,21,24)
FIELD ETRDSCFRA002 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED     &
      HIDDEN                     &
      LABEL "2." 

FIELD ETRDSCFRA003 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED     &
      HIDDEN                     &
      LABEL "3."                 

ALIGN (2,3,24)
FIELD ETRDSCANG001 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED     &
      HIDDEN                     &
      LABEL "Desc. anglaise..: 1."

ALIGN (,21,24)
FIELD ETRDSCANG002 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED     &
      HIDDEN                     &
      LABEL "2."                 

FIELD ETRDSCANG003 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED     &
      HIDDEN                     &
      LABEL "3."      

TITLE "Caractéristiques" AT 14,3
TITLE "----------------" AT 15,3

SKIP 
ALIGN(3,3,3)
FIELD ETRCAR001 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED  &
      HIDDEN

ALIGN(,,3)
FIELD ETRCAR002 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED  &
      HIDDEN

FIELD ETRCAR003 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED  &
      HIDDEN

FIELD ETRCAR004 OF IFENTREPOT &
      NULL VALUE NOT ALLOWED  &
      HIDDEN
SKIP

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue statut de l'entrepot
;
;
PROCEDURE INPUT ETRSTU
BEGIN

  ; Valeur par defaut du statut
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = "O"

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ETRSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_ETRSTU, T_ETRSTU &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_ETRSTU)
  END
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un code de qualite
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN if156x MODE SAME &
                   PASSING IFENTREPOT, &
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN BEGIN
      WARNING 3248 ; PD348 Impossible de supprimer cet entrepot,
                   ; il est maintenant inactif
      LET ETRSTU OF IFENTREPOT = "F"
      DISPLAY ETRSTU OF IFENTREPOT   
      DISPLAY CBIDSC OF A_CBI_ETRSTU
   END
   ELSE DELETE IFENTREPOT
END

BUILD
