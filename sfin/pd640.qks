;/T/ Liste de débitage par commande interne
;
;/P/ Programmeur..: rémy demers
;    Date création: 1997-12-02
;
;    Description..:
;
;/M/ François Déry, 6 mai 1999
;       Remplacer le subfile de paramètre à transmettre au quick par un fichier
;       séquentiel du dictionnaire.
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par......: Marc Poulin     
;    Date.............: 10 mai 2000
;    Description......: Correction du calcul des quantités à débiter si associées à plus d'une priorité.
;
;    Référence........: Demande de changement 000053.
;    
;    Signet...........: MP-00-05-10_D53.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd640 MENU

;-------------------------------------------------------------------------------
;
; Variables reçu en paramètres
;
TEMPORARY T_CIECLE   CHARACTER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIANUM002 CHARACTER SIZE 7 ;RESET AT STARTUP
TEMPORARY T_SUMREJET    INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_COMNUMCOM  CHARACTER SIZE 7 ;RESET AT STARTUP

TEMPORARY T_DIAGRAMME CHARACTER SIZE 7 ;RESET AT STARTUP
;-------------------------------------------------------------------------------
;
FILE PDEMBAL_DIAG IN DICT DESIGNER

;-------------------------------------------------------------------------------
FILE PDDET_DEBITAGE IN DICT DESIGNER
;
;-------------------------------------------------------------------------------
FILE PDDIAGRAMME IN DICT DESIGNER
;
FILE VDIAQTE          IN DICT DESIGNER
  ACCESS VIA   PJTABR, &
               DIANUMDIA001, &
               DIANUMDIA002, &
               CIECLE &
         USING PJTABR OF PDDIAGRAMME, &
               DIANUMDIA001 OF PDDIAGRAMME, &
               DIANUMDIA002 OF PDDIAGRAMME, &
               CIECLE OF PDDIAGRAMME

;
;-------------------------------------------------------------------------------
;
FILE PDTEMPO_SCIAGE DESIGNER
;-------------------------------------------------------------------------------
FILE PDPARAM_TMP    DESIGNER
;-------------------------------------------------------------------------------
FILE PDCAISSON DESIGNER
;-------------------------------------------------------------------------------
FILE MCCOMPAGNIE DESIGNER
;-------------------------------------------------------------------------------
;file *wtrace designer
;-------------------------------------------------------------------------------
;
;variable de travail
TEMPORARY T_PDGQTEPLACPEGRA INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_EMDQTEPRD       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DDEQTEFAB       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIAQTEEMB       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIAQTEDBT       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIAQTEREJ       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIADBTREJ       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIAEMBREJ       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIAQTECMD       INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_REJSUM          INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_SOMDDEQTEFAB    INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_SOMEMDQTEPRD    INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIFFEMB         INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_TEMPO_DIAQTEEMB INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_DIFFDBT         INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_TEMPO_DIAQTEDBT INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_PRDREJ          INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_FABREJ          INTEGER SIZE 4 ;RESET AT STARTUP
TEMPORARY T_FLGSC CHAR SIZE 1
TEMPORARY T_DIFFSC INTEGER SIZE 4 ;RESET AT STARTUP

;-------------------------------------------------------------

TEMPORARY T_PRICODPRI  INTEGER   SIZE 08 RESET AT STARTUP ; MP-00-05-10_D53.
TEMPORARY T_RESIDUEL   INTEGER   SIZE 08 RESET AT STARTUP ; MP-00-05-10_D53.
TEMPORARY T_CIRCULAIRE INTEGER   SIZE 02 RESET AT STARTUP ; MP-00-05-10_D53.
TEMPORARY T_LAST_PRIO  CHARACTER SIZE 02 RESET AT STARTUP ; MP-00-05-10_D53.

;-------------------------------------------------------------
;
PROCEDURE INTERNAL REJET_DEBITAGE
BEGIN
  IF T_SUM OF PDTEMPO_SCIAGE <> 0 AND T_DIAQTEREJ <> 0
  THEN BEGIN
    LET T_SUMREJET = T_DIAQTEREJ
    LET T_REJSUM = T_DIAQTEREJ - T_SUM OF PDTEMPO_SCIAGE
    IF T_REJSUM  > 0
    THEN BEGIN
      LET T_DIAQTEREJ = T_DIAQTEREJ - T_SUM OF PDTEMPO_SCIAGE
      LET T_SUM OF PDTEMPO_SCIAGE = T_SUM OF PDTEMPO_SCIAGE - T_SUMREJET
      IF T_SUM OF PDTEMPO_SCIAGE < 0
      THEN LET T_SUM OF PDTEMPO_SCIAGE =0
    END
    IF T_REJSUM  = 0
    THEN BEGIN
      LET T_SUM OF PDTEMPO_SCIAGE = T_SUM OF PDTEMPO_SCIAGE - T_SUMREJET
      LET T_DIAQTEREJ = 0
      LET T_DDEQTEFAB = 0
    END
    IF T_REJSUM < 0
    THEN BEGIN
; a verifier
;            LET T_DDEQTEFAB = T_SUM OF PDTEMPO_SCIAGE - T_DIAQTEREJ
      LET T_SUM OF PDTEMPO_SCIAGE    = T_SUM OF PDTEMPO_SCIAGE  - T_DIAQTEREJ
      LET T_DIAQTEREJ = 0

    END
  END
END
;-------------------------------------------------------------
;
PROCEDURE INTERNAL PRIO
BEGIN
  WHILE RETRIEVING PDTEMPO_SCIAGE VIA DIANUM002 ,CIECLE USING T_DIANUM002,T_CIECLE
  BEGIN
    IF  PRICODPRI OF PDEMBAL_DIAG <= PRICODPRI OF PDTEMPO_SCIAGE AND 0 < T_EMDQTEPRD AND 0 < PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE
    THEN BEGIN
      LET T_SUM OF PDTEMPO_SCIAGE           = T_SUM           OF PDTEMPO_SCIAGE + T_EMDQTEPRD    + T_RESIDUEL  ; MP-
      LET PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE = PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE - T_EMDQTEPRD    - T_RESIDUEL  ; MP-

;     -------------------------------------------------------------------------------
;     MP-

      LET T_RESIDUEL = 0 ; MP-

      IF T_SUM OF PDTEMPO_SCIAGE >= TEMDIAQTEDBT OF PDTEMPO_SCIAGE                     
      THEN BEGIN
        IF T_SUM OF PDTEMPO_SCIAGE > TEMDIAQTEDBT OF PDTEMPO_SCIAGE                    
        THEN LET T_RESIDUEL = T_SUM OF PDTEMPO_SCIAGE - TEMDIAQTEDBT OF PDTEMPO_SCIAGE 
        
        LET T_SUM           OF PDTEMPO_SCIAGE = TEMDIAQTEDBT OF PDTEMPO_SCIAGE         
        LET PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE = 0                                      
      END
;     -------------------------------------------------------------------------------


      IF T_SUM OF PDTEMPO_SCIAGE >= D_PDGQTE OF PDTEMPO_SCIAGE
      THEN BEGIN
        LET T_SUM OF PDTEMPO_SCIAGE = D_PDGQTE OF PDTEMPO_SCIAGE
        PUT PDTEMPO_SCIAGE
 
        BREAK
      END
      ELSE BEGIN
        LET T_EMDQTEPRD = 0
        PUT PDTEMPO_SCIAGE
      END
    END
  END
END

;---------------------------------------------------------------
;
PROCEDURE INTERNAL PRIO_DEBITAGE
BEGIN
  WHILE RETRIEVING PDTEMPO_SCIAGE VIA DIANUM002  , &
                                      CIECLE       &
                                USING T_DIANUM002, & 
                                      T_CIECLE     &
                              ORDERBY PRICODPRI    ; MP-00-05-10_D53.
  BEGIN
    IF T_PRICODPRI <= NCONVERT(PRICODPRI OF PDTEMPO_SCIAGE) ; MP-00-05-10_D53.
    THEN BEGIN
      LET T_DIAGRAMME = DIANUM002 OF PDTEMPO_SCIAGE

      IF ((DDECODPRI OF PDDET_DEBITAGE <= PRICODPRI OF PDTEMPO_SCIAGE AND T_CIRCULAIRE = 0)   OR &
          (DDECODPRI OF PDDET_DEBITAGE >  PRICODPRI OF PDTEMPO_SCIAGE AND T_CIRCULAIRE = 1)) AND &
           0                           <  T_DDEQTEFAB
      THEN BEGIN
        LET T_SUM OF PDTEMPO_SCIAGE = (T_SUM OF PDTEMPO_SCIAGE + T_DDEQTEFAB) &
                                    +  T_RESIDUEL  ; MP-00-05-10_D53.
        LET T_RESIDUEL = 0 ; MP-00-05-10_D53.

        DO INTERNAL REJET_DEBITAGE

        IF T_SUM OF PDTEMPO_SCIAGE >= TEMDIAQTEDBT OF PDTEMPO_SCIAGE   ; MP-00-05-10_D53.
        THEN BEGIN
          IF T_SUM OF PDTEMPO_SCIAGE > TEMDIAQTEDBT OF PDTEMPO_SCIAGE  ; MP-00-05-10_D53.
          THEN LET T_RESIDUEL = T_SUM OF PDTEMPO_SCIAGE - TEMDIAQTEDBT OF PDTEMPO_SCIAGE ; MP-00-05-10_D53.

          LET T_SUM OF PDTEMPO_SCIAGE = TEMDIAQTEDBT OF PDTEMPO_SCIAGE ; MP-00-05-10_D53.

          IF PRICODPRI OF PDTEMPO_SCIAGE = T_LAST_PRIO AND T_CIRCULAIRE = 0 ; MP-00-05-10_D53.
          THEN BEGIN
            LET T_PRICODPRI  = 1 ; MP-00-05-10_D53.
            LET T_CIRCULAIRE = 1 ; MP-00-05-10_D53.
          END
          ELSE LET T_PRICODPRI = NCONVERT(PRICODPRI OF PDTEMPO_SCIAGE) + 1  ; MP-00-05-10_D53.

          LET PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE = 0
          PUT PDTEMPO_SCIAGE
          
          BREAK
        END
        ELSE BEGIN
          LET PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE = PDGQTEPLACPEGRA OF PDTEMPO_SCIAGE - T_DDEQTEFAB
          LET T_DDEQTEFAB = 0
          PUT PDTEMPO_SCIAGE
        END
      END
    END
  END
END

;---------------------------------------------------------------
PROCEDURE INTERNAL BOUCLE
BEGIN
  LET T_RESIDUEL = 0 ; MP-

  WHILE RETRIEVING PDEMBAL_DIAG VIA DIANUM002,CIECLE USING T_DIANUM002, T_CIECLE ORDERBY DIANUM002,CIECLE,PRICODPRI
  BEGIN
    LET T_EMDQTEPRD = EMDQTEPRD OF PDEMBAL_DIAG
    DO INTERNAL PRIO
  END
END
;---------------------------------------------------------------------------
PROCEDURE INTERNAL BOUCLE_DEBITAGE
BEGIN
  LET T_PRICODPRI  = 1 ; MP-00-05-10_D53.
  LET T_RESIDUEL   = 0 ; MP-00-05-10_D53.
  LET T_CIRCULAIRE = 0 ; MP-00-05-10_D53.

  GET PDTEMPO_SCIAGE VIA DIANUM002  , &
                         CIECLE       &
                   USING T_DIANUM002, & 
                         T_CIECLE     &
                 ORDERBY PRICODPRI DESCENDING ; MP-00-05-10_D53.

  LET T_LAST_PRIO = PRICODPRI OF PDTEMPO_SCIAGE ; MP-00-05-10_D53.

  WHILE RETRIEVING PDDET_DEBITAGE VIA DIANUM002  , &
                                      CIECLE       &
                                USING T_DIANUM002, &
                                      T_CIECLE     &
                              ORDERBY DIANUM002  , & ; MP-00-05-10_D53.
                                      CIECLE     , & ; MP-00-05-10_D53.
                                      DDECODPRI  , & ; MP-00-05-10_D53.
                                      DEBNUMRAP  , & ; MP-00-05-10_D53.
                                      DDENUMLIG      ; MP-00-05-10_D53.
  BEGIN
    IF DDESTU OF PDDET_DEBITAGE = "T"
    THEN BEGIN
      LET T_DDEQTEFAB = DDEQTEFAB OF PDDET_DEBITAGE
      DO INTERNAL PRIO_DEBITAGE
    END
  END
END
;---------------------------------------------------------------------------
PROCEDURE INTERNAL SOMDDEQTEFAB
BEGIN
   LET T_SOMDDEQTEFAB = 0
   WHILE RETRIEVING PDDET_DEBITAGE VIA DIANUM002, CIECLE &
                                 USING T_DIANUM002, T_CIECLE
    BEGIN
      IF DDESTU OF PDDET_DEBITAGE = "T"

      THEN LET T_SOMDDEQTEFAB = T_SOMDDEQTEFAB + DDEQTEFAB OF PDDET_DEBITAGE
     END

END
;---------------------------------------------------------------------------
PROCEDURE INTERNAL SOMEMDQTEPRD
BEGIN
   LET T_FLGSC = "0"
   LET T_SOMEMDQTEPRD = 0
   WHILE RETRIEVING PDEMBAL_DIAG VIA DIANUM002, CIECLE &
                                 USING T_DIANUM002, T_CIECLE
     BEGIN
     GET PDCAISSON OPT &
     VIA    CIECLE       , &
            CAINUMCAI      &
     USING  T_CIECLE     , &
            CAINUMCAI OF PDEMBAL_DIAG
     IF ACCESSOK
     THEN BEGIN
       GET MCCOMPAGNIE OPT &
         VIA    CIECLE &
         USING  T_CIECLE
       IF ACCESSOK
       THEN BEGIN

      IF CAISITEMB OF PDCAISSON = CIESITENT OF MCCOMPAGNIE
        THEN LET T_SOMEMDQTEPRD = T_SOMEMDQTEPRD + EMDQTEPRD OF PDEMBAL_DIAG
        ELSE LET T_FLGSC = "1"
       END
     END
  END
END
;---------------------------------------------------------------
PROCEDURE INTERNAL MISE_A_JOUR_EMBAL
BEGIN
 WHILE RETRIEVING PDTEMPO_SCIAGE VIA DIANUM002 ,CIECLE USING T_DIANUM002,T_CIECLE
     BEGIN
        IF T_FLGSC = "0"
        THEN BEGIN
         IF PRICODPRI OF PDTEMPO_SCIAGE = "99"
         THEN LET T_SUM = T_DIAQTEEMB
         ELSE LET T_SUM = T_DIAQTECMD
         PUT PDTEMPO_SCIAGE
        END
        ELSE BEGIN
         IF PRICODPRI OF PDTEMPO_SCIAGE = "99"
         THEN BEGIN
            LET T_DIFFSC = T_DIAQTECMD - D_PDGQTE OF PDTEMPO_SCIAGE
            LET T_SUM = T_DIFFSC + T_SOMEMDQTEPRD
            IF D_PDGQTE OF PDTEMPO_SCIAGE < T_DIAQTECMD
            THEN LET T_SUM = T_SOMEMDQTEPRD
         END
         ELSE LET T_SUM = T_DIAQTECMD
         PUT PDTEMPO_SCIAGE
        END
    END
END
;---------------------------------------------------------------
PROCEDURE INTERNAL MISE_A_JOUR_DBT
BEGIN
 WHILE RETRIEVING PDTEMPO_SCIAGE VIA DIANUM002,CIECLE USING T_DIANUM002,T_CIECLE
     BEGIN
         IF PRICODPRI OF PDTEMPO_SCIAGE = "99"
         THEN LET T_SUM = T_DIADBTREJ
         ELSE LET T_SUM = T_DIAQTECMD
;         LET T_SUM = T_DIAQTEDBT
         PUT PDTEMPO_SCIAGE
     END
END
;----------------------------------------------------------
procedure internal fait
BEGIN
 WHILE RETRIEVING PDTEMPO_SCIAGE VIA DIANUM002 ,CIECLE USING T_DIANUM002,T_CIECLE
     BEGIN
          let t_sum = t_diaqtecmd
          let t_rejet = 0
          put PDTEMPO_SCIAGE
     end
end


;---------------------------------------------------------------
PROCEDURE INITIALIZE
BEGIN
  WHILE RETRIEVING PDPARAM_TMP SEQUENTIAL
  BEGIN
;let t_trace of wtrace = "01"
;put wtrace reset
    LET T_CIECLE    = CIECLE OF PDPARAM_TMP
    LET T_DIANUM002 = DIANUM002 OF PDPARAM_TMP
    LET T_COMNUMCOM = COMNUMCOM OF PDPARAM_TMP

    GET PDDIAGRAMME VIA CIECLE    , &
                        DIANUM002   &
                  USING T_CIECLE   ,&
                        T_DIANUM002

    GET VDIAQTE

    LET T_DIAQTEEMB = DIAQTEEMB OF PDDIAGRAMME
    LET T_DIAQTEDBT = DIAQTEDBT OF VDIAQTE
    LET T_DIAQTEREJ = DIAQTEREJ OF PDDIAGRAMME
    LET T_DIAQTECMD = DIAQTECMD OF PDDIAGRAMME
    LET T_DIAEMBREJ = DIAQTEEMB OF PDDIAGRAMME - DIAQTEREJ OF PDDIAGRAMME
    LET T_DIADBTREJ = DIAQTEDBT OF VDIAQTE     - DIAQTEREJ OF PDDIAGRAMME

    IF T_DIADBTREJ >= T_DIAQTECMD OR T_DIAQTEEMB >= T_DIAQTECMD
    THEN BEGIN
;let t_trace of wtrace = "02"
;put wtrace reset
      DO INTERNAL FAIT
    END
    ELSE BEGIN
      IF T_DIAQTEEMB <> 0 AND T_DIAQTEEMB >= T_DIADBTREJ
      THEN BEGIN
;let t_trace of wtrace = "03"
;put wtrace reset
        LET T_SOMEMDQTEPRD = 0

        DO INTERNAL SOMEMDQTEPRD

        IF T_SOMEMDQTEPRD < T_DIAQTEEMB
        THEN BEGIN
;let t_trace of wtrace = "04"
;put wtrace reset
          DO INTERNAL MISE_A_JOUR_EMBAL
        END
        ELSE BEGIN
;let t_trace of wtrace = "05"
;put wtrace reset
          DO INTERNAL BOUCLE
        END
      END
      ELSE BEGIN
        IF T_DIADBTREJ <> 0
        THEN BEGIN
;let t_trace of wtrace = "06"
;put wtrace reset
          LET T_SOMDDEQTEFAB = 0

          DO INTERNAL SOMDDEQTEFAB

          LET T_FABREJ = T_SOMDDEQTEFAB - T_DIAQTEREJ

          IF T_FABREJ < T_DIADBTREJ
          THEN BEGIN
;let t_trace of wtrace = "07"
;put wtrace reset
            DO INTERNAL MISE_A_JOUR_DBT
          END
          ELSE BEGIN
;let t_trace of wtrace = "08"
;put wtrace reset
            DO INTERNAL BOUCLE_DEBITAGE
          END
        END
      END
    END
  END
  
  RETURN
END


BUILD
