;/T/ 2.5.6 Facturation (Gestion de blocs) / Produit dimension.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-03-13
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les ventes directes de Produit de dimension.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin
;    Date..............: 05 avril 2000
;    Description.......: Incrémentation du numéro de facture par compagnie.
;
;    Référence.........: Stabilisation du module Gestion de blocs (point 2).
;
;    Signet............: MP-00-04-05_S02.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd225c ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDFACTURE


;-------------------------------------------------------------------------------
;
; Déclaration de la transaction pour donner un numéro séquentiel
;

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_FACTURE IN DICT


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD225C"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd225c.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Commentaire produit dimension   " ACTION DESIGNER D001 MARK
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Commentaire produit dimension" ACTION DESIGNER D001 MARK
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_TYPE_PROD CHARACTER SIZE 02 INITIAL "PD" RESET AT STARTUP ; Type de produit traiter par l'écran


;-------------------------------------------------------------------------------
;
; Variable temporaire du programme pour les pop-up
;

TEMPORARY T_TGRCODGRA        CHARACTER SIZE 03 ; Type de granit
TEMPORARY T_TYFCODFIN        CHARACTER SIZE 02 ; Type de fini
TEMPORARY T_PDILON           INTEGER UNSIGNED  &
                                       SIZE 04 ; Longueur
TEMPORARY T_PDILAR           INTEGER UNSIGNED  &
                                       SIZE 04 ; Largeur
TEMPORARY T_PDIEPA           INTEGER UNSIGNED  &
                                       SIZE 04 ; Épaisseur

TEMPORARY T_TPDTYPPRD        CHARACTER SIZE 04 ; Type de produit
TEMPORARY T_UNMCODUNM        CHARACTER SIZE 04 ; Unité de mesure


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

;TEMPORARY T_SILENT           CHARACTER SIZE 01

;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;

USE usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du prix d'une ligne de détail bon de commande
;
USE usvarprigra NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum des pièces de granit
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Temporaires necessaires au calcul des taxes
;

USE ustaxtmp NOLIST


;-------------------------------------------------------------------------------
;
; Facture
;

FILE PDFACTURE MASTER


;-------------------------------------------------------------------------------
;
; Facture produit de dimension
;

FILE PDPROD_FACTURE PRIMARY &
                    NOITEM &
                    OCCURS 11 TIMES

  ACCESS VIA     CIECLE,           &
                 FTRNUMFAC         &
         USING   T_CIECLEMNU,                           &
                 FTRNUMFAC         OF PDFACTURE         &
         ORDERBY CIECLE,           &
                 FTRNUMFAC,        &
                 CAINUMCAI,        &
                 PDILON,           &
                 PDIHAU,           &
                 PDIEPA,           &
                 TGRCODGRA,        &
                 TYFCODFIN


  ITEM CIECLE    OF PDPROD_FACTURE INITIAL  T_CIECLEMNU
  ITEM FTRNUMFAC OF PDPROD_FACTURE INITIAL  FTRNUMFAC OF PDFACTURE FIXED
  ITEM PFAMNT    OF PDPROD_FACTURE SUM INTO FTRMNTBRU OF PDFACTURE


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les factures
;

FILE PDSEQ_FACTURE DESIGNER TRANSACTION NO_SEQ
  ACCESS VIA CIECLE USING T_CIECLEMNU ; MP-00-04-05_S02.

;-------------------------------------------------------------------------------
;
; Produit de dimension
;

;FILE PDPROD_DIMENSION SECONDARY &
;                      NOITEM &
;                      NODELETE &
;                      OCCURS WITH PDPROD_FACTURE

;  ACCESS VIA     CIECLE,          &
;                 PDILON,          &
;                 PDIHAU,          &
;                 PDIEPA,          &
;                 TGRCODGRA,       &
;                 TYFCODFIN        &
;         USING   T_CIECLEMNU,                        &
;                 PDILON           OF PDPROD_FACTURE, &
;                 PDIHAU           OF PDPROD_FACTURE, &
;                 PDIEPA           OF PDPROD_FACTURE, &
;                 TGRCODGRA        OF PDPROD_FACTURE, &
;                 TYFCODFIN        OF PDPROD_FACTURE

;  ITEM CIECLE    OF PDPROD_DIMENSION INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Caisson
;

FILE PDCAISSON       REFERENCE

  ACCESS VIA   CIECLE,          &
               CAINUMCAI        &
         USING T_CIECLEMNU,     &
               CAINUMCAI OF PDPROD_FACTURE


;-------------------------------------------------------------------------------
;
; Caisson
;

FILE PDCAISSON DESIGNER ALIAS A_CAI_MAJ


;-------------------------------------------------------------------------------
;
; Emballage - caisson produit demension
;

FILE PDEMBAL_PROD_DIM REFERENCE

  ACCESS VIA   CIECLE,     &
               CAINUMCAI   &
         USING T_CIECLEMNU,&
               CAINUMCAI OF PDPROD_FACTURE


;-------------------------------------------------------------------------------
;
; Client
;

FILE VCLC_CLI        REFERENCE

  ACCESS VIA     CLICIECLE,        &
                 CLICLE,           &
                 CIECLE            &
         USING   "----",                                &
                 CLICLE            OF PDFACTURE,        &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Unité de mesure (Vente)
;

FILE PDUNITE_MESURE REFERENCE &
                    ALIAS UNM_VENTE &
                    OCCURS WITH PDPROD_FACTURE

  ACCESS VIA     CIECLE,          &
                 UNMCODUNM        &
         USING   T_CIECLEMNU,     &
                 PFAUNMVEN        OF PDPROD_FACTURE


;-------------------------------------------------------------------------------
;
; Type de produit
;

FILE PDTYPE_PRODUIT DESIGNER

  ACCESS VIA     CIECLE,          &
                 TPDTYPPRD        &
         USING   T_CIECLEMNU,     &
                 T_TYPE_PROD


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA   CIECLE,            &
               TGRCODGRA          &
         USING T_CIECLEMNU,       &
               TGRCODGRA OF PDPROD_FACTURE


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE

  ACCESS VIA   CIECLE,            &
               TYFCODFIN          &
         USING T_CIECLEMNU,       &
               TYFCODFIN OF PDPROD_FACTURE


;-------------------------------------------------------------------------------
;
; Conversion unité mesure de commande à unité mesure production
;

FILE PDCONVERT_UN_M DESIGNER


;-------------------------------------------------------------------------------
;
; Validation des codes de taxes fédéral.
;

FILE MCTAXE           REFERENCE &
                      ALIAS A_TAX_TPS

  ACCESS  VIA   TAXCLETYP,       &
                TAXCLECOD        &
          USING FTRTAXTYPTPS     OF PDFACTURE,        &
                FTRTAXCODTPS     OF PDFACTURE


;-------------------------------------------------------------------------------
;
; Validation des codes de taxes provincial.
;

FILE MCTAXE           REFERENCE &
                      ALIAS A_TAX_TVP

  ACCESS  VIA   TAXCLETYP,       &
                TAXCLECOD        &
          USING FTRTAXTYPTVQ     OF PDFACTURE,        &
                FTRTAXCODTVQ     OF PDFACTURE


;-------------------------------------------------------------------------------
FILE PDLOT_FACTURE DESIGNER
;-------------------------------------------------------------------------------
FILE PDLOT_TRANSFERT DESIGNER
;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Facturation bloc " &
@ELSE
      " %%%Ventes directes " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD FTRNUMFAC        OF PDFACTURE        &
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,28)


FIELD CLICLE           OF PDFACTURE        &
        DISPLAY                            &
        FIXED


FIELD CLINOM           OF VCLC_CLI         &
        FIXED


DRAW 8,2 TO 8,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Produit dimension " &
@ELSE
      " %%%Produit dimension " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      "   Caisson Gra Fi Long Haut Épais  Qte  Surface Prix vente U/M     Montant  C" &
@ELSE
      "%%%Caisson Gra Fi Long Haut Épais  Qte  Surface Prix vente U/M     Montant  C" &
@ENDIF
      AT ,2

;ALIGN (02,02,03) (,,13) (,,17) (,,20) (,,25) (,,30) (,,35) (,,41) (,,51) (,,61) (,,64) (,,78)
ALIGN (02,02,02) (,,12) (,,16) (,,19) (,,24) (,,29) (,,34) (,,41) (,,51) (,,61) (,,64) (,,78)


CLUSTER OCCURS WITH PDPROD_FACTURE ID BASE 90


FIELD CAINUMCAI        OF PDPROD_FACTURE   &
        HIDDEN                             &
        LABEL " "                          &
        BWZ                                &
        LOOKUP ON PDCAISSON                &
          MESSAGE 2029 ; Ce numéro de caisson n'existe pas


FIELD TGRCODGRA        OF PDPROD_FACTURE   &
        HIDDEN                             &
        NUMERIC                            &
        LOOKUP ON PDTYPE_GRANIT            &
          MESSAGE 1810 ; Ce type de granit n'existe pas


FIELD TYFCODFIN        OF PDPROD_FACTURE   &
        HIDDEN                             &
        NUMERIC                            &
        LOOKUP ON PDTYPE_FINI              &
          MESSAGE 1811 ; Ce type de fini n'existe pas


FIELD PDILON           OF PDPROD_FACTURE   &
        HIDDEN


FIELD PDIHAU           OF PDPROD_FACTURE   &
        HIDDEN


FIELD PDIEPA           OF PDPROD_FACTURE   &
        HIDDEN


;FIELD T_SILENT                             &
;        SILENT                             &
;        LOOKUP ON PDPROD_DIMENSION         &
;          VIA     CIECLE,                  &
;                  PDILON,                  &
;                  PDIHAU,                  &
;                  PDIEPA,                  &
;                  TGRCODGRA,               &
;                  TYFCODFIN                &
;          USING   T_CIECLEMNU,                        &
;                  PDILON           OF PDPROD_FACTURE, &
;                  PDIHAU           OF PDPROD_FACTURE, &
;                  PDIEPA           OF PDPROD_FACTURE, &
;                  TGRCODGRA        OF PDPROD_FACTURE, &
;                  TYFCODFIN        OF PDPROD_FACTURE  &
;          MESSAGE  1818 ; Ce produit n'existe pas


FIELD PFAQTE           OF PDPROD_FACTURE   &
        HIDDEN


FIELD   PDISURMC2     OF PDPROD_FACTURE &
        HIDDEN                             &
;        PICTURE "^^^.^^^^ " &
;        OUTPUT SCALE 4 SIGNI 7 &
        DISPLAY


FIELD PFAPRIVEN        OF PDPROD_FACTURE   &
        HIDDEN                             &
        PICTURE "^,^^^.^^ "                &
        TRAILING SIGN "-"                   &
        REQUIRED


FIELD PFAUNMVEN        OF PDPROD_FACTURE   &
        HIDDEN                             &
        ID SAME                            &
        DEFAULT UNMCODUNM OF PDTYPE_PRODUIT &
        LOOKUP ON UNM_VENTE                &
          MESSAGE 1928 ; Cette unité de mesure n'existe pas


FIELD PFAMNT           OF PDPROD_FACTURE   &
        HIDDEN                             &
        PICTURE "^,^^^,^^^.^^ "            &
        TRAILING SIGN "-"                  &
        DISPLAY


FIELD PFACOM           OF PDPROD_FACTURE   &
        HIDDEN                             &
        ID SAME


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du prix d'une ligne de détail bon commande
;

USE uscalprigra NOLIST


;-------------------------------------------------------------------------------
;
; Valide les dimensions du produit de dimension
;

PROCEDURE EDIT PDILON
BEGIN
  ;
  ; Valide les dimension minimum et maximum pour un diagramme
  ;
  IF (FIELDVALUE        > T_DIM_MAX_LAR_PD OR &
      FIELDVALUE        < T_DIM_MIN_LAR_PD)
  THEN BEGIN
    ERROR 1862 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions du produit de dimension
;

PROCEDURE EDIT PDIHAU
BEGIN
  ;
  ; Valide les dimension minimum et maximum pour un diagramme
  ;
  IF (FIELDVALUE        > T_DIM_MAX_HAU_PD OR &
      FIELDVALUE        < T_DIM_MIN_HAU_PD)
  THEN BEGIN
    ERROR 1862 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions du produit de dimension
;

PROCEDURE EDIT PDIEPA
BEGIN
  ;
  ; Valide les dimension minimum et maximum pour un diagramme
  ;
  IF (FIELDVALUE        > T_DIM_MAX_EPA_PD OR &
      FIELDVALUE        < T_DIM_MIN_EPA_PD)
  THEN BEGIN
    ERROR 1862 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Valide la qte
;

;PROCEDURE EDIT PFAQTE
;BEGIN
;  ;
;  ; Valide la partie décimale.
;  ;
;  USE usvaldec4 NOLIST
;
;  LET PDIQTE      OF PDPROD_DIMENSION = PDIQTE      OF PDPROD_DIMENSION + &
;                                        OLDVALUE(PFAQTE      OF PDPROD_FACTURE) - &
;                                        FIELDVALUE
;
;  LET FACSURMC2PD OF PDFACTURE        = FACSURMC2PD OF PDFACTURE - &
;                                        OLDVALUE(PFAQTE OF PDPROD_FACTURE) * PDISURMC2 OF PDPROD_DIMENSION + &
;                                        FIELDVALUE * PDISURMC2 OF PDPROD_DIMENSION
;
;END
;----------------------------------------------------------------------
PROCEDURE EDIT PFAQTE
BEGIN

 LET PDISURMC2 OF PDPROD_FACTURE =  (PDILON OF PDPROD_FACTURE &
                                   * PDIHAU OF PDPROD_FACTURE &
                                   * PFAQTE OF PDPROD_FACTURE )/1000000
 DISPLAY PDISURMC2 OF PDPROD_FACTURE
END
;-------------------------------------------------------------------------------
;
; Valide le format du champ
;

PROCEDURE EDIT PFAPRIVEN
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec NOLIST
END


;-------------------------------------------------------------------------------
;
; Calcul du prix
;

PROCEDURE PROCESS PFAPRIVEN
BEGIN
  ;
  ; Recherche l'unité de mesuse par défaut pour le type de produit
  ;
  GET PDTYPE_PRODUIT OPTIONAL &
    VIA     CIECLE,           &
            TPDTYPPRD         &
    USING   T_CIECLEMNU,      &
            T_TYPE_PROD
  IF NOT ACCESSOK
  THEN BEGIN
    ERROR 1863 ; Ce code de produit n'existe pas
  END

  ;
  ; Recherche une occurence contenant le facteur par défaut et le facteur
  ; saisie au panorama
  ;
  GET PDCONVERT_UN_M OPTIONAL &
    VIA    CIECLE,           &
           CUMCODUNMINT,     &
           CUMCODUNMEXT      &
    USING  T_CIECLEMNU,                         &
           UNMCODUNM         OF PDTYPE_PRODUIT, &
           PFAUNMVEN         OF PDPROD_FACTURE
  IF NOT ACCESSOK &
     AND &
     0 <> SIZE (TRUNCATE ( PFAUNMVEN ) )
  THEN BEGIN
    ERROR 2004 ; Le facteur de conversion n'existe pas pour cette combinaison
  END

  ;
  ; Calcul du prix total précédant pour la ligne de détail
  ;
  LET TZ_CPG_COD_PRD          = TPDTYPPRD         OF PDTYPE_PRODUIT
  LET TZ_CPG_FAC_CON          = CUMFACCON         OF PDCONVERT_UN_M
  LET TZ_CPG_PRI_UNI          = PFAPRIVEN         OF PDPROD_FACTURE
  LET TZ_CPG_UNM              = UNMCODUNM         OF PDTYPE_PRODUIT
  LET TZ_CPG_VOL_MC3          = 0
  LET TZ_CPG_VOL_MC2          = PDISURMC2
  LET TZ_CPG_QTE              = PFAQTE
  DO INTERNAL CALPRIGRA
  LET     PFAMNT OF PDPROD_FACTURE = TZ_CPG_PRI_TOT
  DISPLAY PFAMNT OF PDPROD_FACTURE
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;

PROCEDURE INPUT PFAUNMVEN
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que l'unité de mesure saisie peut être utiliser pour le calcul du prix
;

PROCEDURE PROCESS PFAUNMVEN
BEGIN
  ;
  ; Recherche l'unité de mesuse par défaut pour le type de produit
  ;
  GET PDTYPE_PRODUIT OPTIONAL &
    VIA     CIECLE,           &
            TPDTYPPRD         &
    USING   T_CIECLEMNU,      &
            T_TYPE_PROD
  IF NOT ACCESSOK
  THEN BEGIN
    ERROR 1863 ; Ce code de produit n'existe pas
  END

  ;
  ; Recherche une occurence contenant le facteur par défaut et le facteur
  ; saisie au panorama
  ;
  GET PDCONVERT_UN_M OPTIONAL &
    VIA    CIECLE,           &
           CUMCODUNMINT,     &
           CUMCODUNMEXT      &
    USING  T_CIECLEMNU,                         &
           UNMCODUNM         OF PDTYPE_PRODUIT, &
           PFAUNMVEN         OF PDPROD_FACTURE
  IF NOT ACCESSOK
  THEN BEGIN
    ERROR 2004 ; Le facteur de conversion n'existe pas pour cette combinaison
  END


  ;
  ; Calcul du prix total précédant pour la ligne de détail
  ;
  LET TZ_CPG_COD_PRD          = TPDTYPPRD         OF PDTYPE_PRODUIT
  LET TZ_CPG_FAC_CON          = CUMFACCON         OF PDCONVERT_UN_M
  LET TZ_CPG_PRI_UNI          = PFAPRIVEN         OF PDPROD_FACTURE
  LET TZ_CPG_UNM              = UNMCODUNM         OF PDTYPE_PRODUIT
  LET TZ_CPG_VOL_MC3          = 0
  LET TZ_CPG_VOL_MC2          = PDISURMC2
  LET TZ_CPG_QTE              = PFAQTE
  DO INTERNAL CALPRIGRA
  LET     PFAMNT OF PDPROD_FACTURE = TZ_CPG_PRI_TOT
  DISPLAY PFAMNT OF PDPROD_FACTURE
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;

PROCEDURE INPUT PFACOM
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;
PROCEDURE OUTPUT PFACOM
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du sous-écran de commentaire pour le produit de dimension
;

PROCEDURE PROCESS PFACOM
BEGIN
  IF "0" <> PFACOM OF PDPROD_FACTURE
  THEN BEGIN
    RUN SCREEN  pd225g &
                PASSING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDFACTURE,          &
                        PDPROD_FACTURE      &
                MODE SAME
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE INTERNAL SAISI_DETAIL
BEGIN

  ACCEPT CAINUMCAI OF PDPROD_FACTURE

  IF 0 <> SIZE(TRUNCATE(CAINUMCAI OF PDPROD_FACTURE))
  THEN BEGIN
    LET TGRCODGRA OF PDPROD_FACTURE   = TGRCODGRA OF PDEMBAL_PROD_DIM
    LET TYFCODFIN OF PDPROD_FACTURE   = TYFCODFIN OF PDEMBAL_PROD_DIM
    LET PDILON    OF PDPROD_FACTURE   = PDILON    OF PDEMBAL_PROD_DIM
    LET PDIHAU    OF PDPROD_FACTURE   = PDIHAU    OF PDEMBAL_PROD_DIM
    LET PDIEPA    OF PDPROD_FACTURE   = PDIEPA    OF PDEMBAL_PROD_DIM
    LET PFAQTE    OF PDPROD_FACTURE   = EMPQTEPRD OF PDEMBAL_PROD_DIM
    DISPLAY TGRCODGRA OF PDPROD_FACTURE
    DISPLAY TYFCODFIN OF PDPROD_FACTURE
    DISPLAY PDILON    OF PDPROD_FACTURE
    DISPLAY PDIHAU    OF PDPROD_FACTURE
    DISPLAY PDIEPA    OF PDPROD_FACTURE
    DISPLAY PFAQTE    OF PDPROD_FACTURE
  END
  ELSE BEGIN
    ACCEPT TGRCODGRA OF PDPROD_FACTURE
    ACCEPT TYFCODFIN OF PDPROD_FACTURE
    ACCEPT PDILON    OF PDPROD_FACTURE
    ACCEPT PDIHAU    OF PDPROD_FACTURE
    ACCEPT PDIEPA    OF PDPROD_FACTURE
    ACCEPT PFAQTE    OF PDPROD_FACTURE
  END
;  EDIT T_SILENT
  DISPLAY PDISURMC2
  ACCEPT PFAPRIVEN OF PDPROD_FACTURE
  ACCEPT PFAUNMVEN OF PDPROD_FACTURE
  DISPLAY PFAMNT OF PDPROD_FACTURE
  ACCEPT PFACOM OF PDPROD_FACTURE
END


PROCEDURE APPEND
BEGIN
  DO INTERNAL SAISI_DETAIL
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE ENTRY
BEGIN
  FOR PDPROD_FACTURE
  BEGIN
    PERFORM APPEND
  END
END


;-------------------------------------------------------------------------------
;
; Saisie d'une ligne de détail
;

PROCEDURE DESIGNER 90
BEGIN
  DO INTERNAL SAISI_DETAIL
END


;-------------------------------------------------------------------------------
;
; Commentaire produit dimension
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd225g &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDFACTURE,          &
                      PDPROD_FACTURE      &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDPROD_FACTURE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDPROD_FACTURE &
     AND NOT NEWRECORD     OF PDPROD_FACTURE &
     AND NOT DELETEDRECORD OF PDPROD_FACTURE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2


;
 ;--validation du status du lot
 ;
  GET PDLOT_FACTURE OPTIONAL &
    VIA   CIECLE,          &
          FTRNUMFAC        &
    USING T_CIECLEMNU,     &
          FTRNUMFAC        OF PDFACTURE
  IF ACCESSOK
  THEN BEGIN
   GET PDLOT_TRANSFERT OPTIONAL &
    VIA   CIECLE,          &
          LTRNUMLOT        &
    USING CIECLE,     &
          LTRNUMLOT        OF PDLOT_FACTURE
    IF ACCESSOK
    THEN BEGIN
     IF LTRSTU OF PDLOT_TRANSFERT = "T"
      THEN ERROR 2

    END
  END


  ;
  ; Calcul de la surface vendu
  ;
  LET FTRSURMC2PD OF PDFACTURE = 0.00
  FOR PDPROD_FACTURE
  BEGIN
    IF NOT DELETEDRECORD OF PDPROD_FACTURE
    THEN BEGIN
      LET FTRSURMC2PD OF PDFACTURE  = FTRSURMC2PD OF PDFACTURE + &
                                      (PFAQTE OF PDPROD_FACTURE * &
                                       PDISURMC2 OF PDPROD_FACTURE)
      IF 0 <> SIZE(TRUNCATE(CAINUMCAI OF PDPROD_FACTURE))
      THEN BEGIN
        GET A_CAI_MAJ &
          VIA   CIECLE,   &
                CAINUMCAI &
          USING T_CIECLEMNU,   &
                CAINUMCAI OF PDPROD_FACTURE
        IF ACCESSOK
        THEN BEGIN
          LET CAISTU OF A_CAI_MAJ = "V" ; Vendu
          PUT A_CAI_MAJ RESET
        END
      END
    END
  END
  ;
  ; Calcul des taxes.
  ;

  LET TZ_MNTTXB = FTRMNTBRU OF PDFACTURE

  ;
  ; Use standart pour le calcul des taxes
  ;
  USE ustaxcal NOLIST

  LET FTRMNTTPS OF PDFACTURE = TZ_MNTTPS
  LET FTRMNTTVQ OF PDFACTURE = TZ_MNTTVP
  LET FTRMNT    OF PDFACTURE = FTRMNTBRU OF PDFACTURE + &
                               FTRMNTTPS OF PDFACTURE + &
                               FTRMNTTVQ OF PDFACTURE
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = FTRNUMFAC OF PDFACTURE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_FACTURE OPTIONAL                      ; MP-00-04-05_S02.
    LET CIECLE        OF PDSEQ_FACTURE = T_CIECLEMNU; MP-00-04-05_S02.
    LET SEFNUMRAP     OF PDSEQ_FACTURE = SEFNUMRAP OF PDSEQ_FACTURE + 1
    LET FTRNUMFAC     OF PDFACTURE     = SEFNUMRAP OF PDSEQ_FACTURE
    PUT PDSEQ_FACTURE
    DISPLAY FTRNUMFAC OF PDFACTURE
  END

END


;-------------------------------------------------------------------------------
;
; Permet de faire afficher le numéro de séquence de la facture
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY FTRNUMFAC OF PDFACTURE
    WARNING = " "
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE DELETE
BEGIN
  LET FTRSURMC2PD OF PDFACTURE = 0.00
  IF NOT DELETEDRECORD OF PDPROD_FACTURE
  THEN BEGIN
    LET FTRSURMC2PD OF PDFACTURE  = FTRSURMC2PD OF PDFACTURE - &
                                    (PFAQTE OF PDPROD_FACTURE * &
                                     PDISURMC2  OF PDPROD_FACTURE)
    IF 0 <> SIZE(TRUNCATE(CAINUMCAI OF PDPROD_FACTURE))
    THEN BEGIN
      GET A_CAI_MAJ &
        VIA   CIECLE,   &
              CAINUMCAI &
        USING T_CIECLEMNU,  &
              CAINUMCAI OF PDPROD_FACTURE
      IF ACCESSOK
      THEN BEGIN
        LET CAISTU OF A_CAI_MAJ = "I" ; Inventaire
        PUT A_CAI_MAJ RESET
      END
    END
  END
  DELETE PDPROD_FACTURE
END
;-----------------------------------------------------------------


BUILD DETAIL LIST

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Ventes directes ********************************
*                                                                              *
* No. facture...: xxxxxxxxx                                                    *
* Client........: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
****************************** Produit dimension *******************************
*                                                                              *
*   Caisson Gra Fi Long Haut Épais  Qte  Surface Prix vente U/M     Montant  C *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
* 999999999 999 99 9999 9999 9999 99999 9999.9999 9,999.99- xx 9,999,999.99- x *
*                                                                              *
********************************************************************************
@ENDIF
