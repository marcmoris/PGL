;/T/ Gerrer les tranches (Opération de surface)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-27
;
;    Description..: Le but de cette unité de traitement est d'informer
;                   l'utilisateur sur les opérations de surface d'une tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd181j ACTIONBAR                     &
             MODE LABEL "" AT 2,80          &
             NOACTION                       &
             ACTIVITIES FIND                &
             FIELDMARK RETAIN STARTUP       &
             HELP POPUP FROM 4,9 TO 20,72   &
             RECEIVING  T_CIECLEMNU ,       &
                        T_CIENOM ,          &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD181J"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd181j.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Détail opération de surface
;

FILE PDDET_OPER_SURF PRIMARY &
                     NOITEM &
                     NODELETE &
                     NOAPPEND

  ACCESS VIA     CIECLE,                      &
                 TRANUMTRA002                 &
         USING   T_CIECLEMNU,                 &
                 TRANUMTRA002 OF PDTRANCHE    &
         ORDERBY CIECLE,                      &
                 OSUNUMRAP


         ITEM CIECLE OF PDDET_OPER_SURF INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Opération de surface
;

FILE PDOPERATION_SURF REFERENCE

  ACCESS VIA     CIECLE,                      &
                 OSUNUMRAP                    &
         USING   T_CIECLEMNU,                 &
                 OSUNUMRAP    OF PDDET_OPER_SURF


;-------------------------------------------------------------------------------
;
; Type de fini (AVANT)
;
FILE PDTYPE_FINI REFERENCE ALIAS FINI_AVANT

  ACCESS VIA     CIECLE,                      &
                 TYFCODFIN                    &
         USING   T_CIECLEMNU,                 &
                 DOSCODFIN001     OF PDDET_OPER_SURF


;-------------------------------------------------------------------------------
;
; Type de fini (AVANT)
;
FILE PDTYPE_FINI REFERENCE ALIAS FINI_APRES

  ACCESS VIA     CIECLE,                      &
                 TYFCODFIN                    &
         USING   T_CIECLEMNU,                 &
                 DOSCODFIN002     OF PDDET_OPER_SURF


;-------------------------------------------------------------------------------
;
; Machine opération surface
;

FILE PDMACH_OP_SURF  REFERENCE

  ACCESS VIA     CIECLE,                       &
                 MOPCODMAC                     &
         USING   T_CIECLEMNU,                  &
                 MOPCODMAC        OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Opération de surface " &
@ELSE
      " %%%Opération de surface " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO LINE 5


ALIGN (,3,19)


FIELD OSUNUMRAP        OF PDDET_OPER_SURF  &
label "Numéro rapport:"&
        DISPLAY


FIELD OSUDATPRD        OF PDOPERATION_SURF  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date oper surf:"&
        DISPLAY


FIELD OSUDEP           OF PDOPERATION_SURF &
label "Heure départ..:"&
        DISPLAY


FIELD OSUARR           OF PDOPERATION_SURF &
label "Heure arrêt...:"&
        DISPLAY


ALIGN (,3,19) (,,23)


FIELD MOPCODMAC        OF PDOPERATION_SURF &
label "Code machine..:"&
        DISPLAY


FIELD MOPDSC001        OF PDMACH_OP_SURF   &
        DISPLAY


ALIGN (,3,19)


FIELD DOSNUMLIG        OF PDDET_OPER_SURF  &
label "Numéro ligne..:"&
        DISPLAY


ALIGN (,3,19) (,,23)


FIELD DOSCODFIN001     OF PDDET_OPER_SURF  &
label "Code fini AV..:"&
        DISPLAY


FIELD TYFDSCFRA001     OF FINI_AVANT       &
        DISPLAY


FIELD DOSCODFIN002     OF PDDET_OPER_SURF  &
label "Code fini AP..:"&
        DISPLAY


FIELD TYFDSCFRA001     OF FINI_APRES       &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Opération de surface *****************************
*                                                                              *
* Numéro rapport: xxxxxxxxxxx                                                  *
* Date oper surf: xxxxxxxx                                                     *
* Heure départ..: xxxxx                                                        *
* Heure arrêt...: xxxxx                                                        *
* Code machine..: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                 *
* Numéro ligne..: xxx                                                          *
* Code fini AV..: xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                 *
* Code fini AP..: xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                 *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
