;/T/ Impression de l'inventaire physique des caissons .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-01
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste de validation de l'inventaire physique
;                   de caisson de granit de Granicor.
;
;    Paramètres....: Compagnie
;
;/M/ François Déry, 4 mai 1999
;       Remplacer l'élément PJTABR par PJTNUMPRO.
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INCAISSON

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG303"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEF DPAGEDEB INT SIZE 4 = PARM
DEF DPAGEFIN INT SIZE 4 = PARM

SELECT INCAISSON IF ICANUMPGE OF INCAISSON >= DPAGEDEB AND &
                    ICANUMPGE OF INCAISSON <= DPAGEFIN

SORT ON ICANUMPGE  OF INCAISSON &
     ON ICADATINV  OF INCAISSON &
     ON ICAHREINV  OF INCAISSON &
     ON CAINUMCAI  OF INCAISSON

USE ustitstd45 NOLIST
   SKIP 3 &
     TAB 53 "DONNEES DE LA PAGE: " ICANUMPGE OF INCAISSON &
   SKIP 4

HEADING AT ICANUMPGE SKIP PAGE &

  TAB 003 &
;                                                                                                 1         1         1         1
;       1         2         3         4         5         6         7         8         9         0         1         2         3
;34567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
"LOCALISATION   PROJET      CAISSON   COMMENTAIRE " &
SKIP 1 &
  TAB 003 &
"------------   ------      -------   -----------"

REPORT &
  TAB 007 ICANUMLOC OF INCAISSON &
  TAB 017 PJTNUMPRO OF INCAISSON &
  TAB 029 CAINUMCAI OF INCAISSON &
  TAB 040 ICADESC   OF INCAISSON

FOOTING AT ICANUMPGE SKIP 2 &
    "NOMBRE D'ENTREES POUR CETTE PAGE...............:" COUNT

FINAL FOOTING SKIP 2  &
    "GRAND TOTAL DE CAISSONS EN INVENTAIRE..........:" COUNT

BUILD ig303z
