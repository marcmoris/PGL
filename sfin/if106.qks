;/T/ Consultation de la catégorie de granit
;
;/P/ Programmeur..: Francois Richard
;    Date Creation: 29 septembre 1999
;
;    Description..: Permet de visionner et de choisir la catégorie de granit
;
;    Parametres...: T_CIECLE        = Compagnie
;                   T_CATCLE        = Catégorie
;
;-------------------------------------------------------------------------------

SCREEN if106 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE   , &
                        T_CATCLE


USE ustrasse NOLIST

USE ussectmp NOLIST
    "IF106"

;USE if106.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLE    CHARACTER SIZE 4 RESET AT STARTUP
TEMPORARY T_CATCLE    CHARACTER SIZE 3 RESET AT STARTUP

;-------------------------------------------------------------------------------

FILE IFCATEGORIE  PRIMARY OCCURS 10

  ACCESS  VIA     CIECLE,     &
                  CATCLE      &
          USING   T_CIECLE,   &
                  T_CATCLE    &
          ORDERBY CATCLE

;-------------------------------------------------------------------------------

USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP 1

TITLE "Code" AT ,5
TITLE "Description" AT ,11

SKIP

ALIGN (2,2,5) (,,11)

CLUSTER OCCURS WITH IFCATEGORIE

FIELD CATCLE OF IFCATEGORIE      &
      DISPLAY                    &
      HIDDEN                     &
      LABEL ">>"

FIELD CATDSCFRA OF IFCATEGORIE &
      SIZE 28                  &
      DISPLAY

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CATCLE, "@")
  THEN LET T_CATCLE = ""
END

PROCEDURE DESIGNER 1
BEGIN
  LET T_CATCLE = CATCLE OF IFCATEGORIE
  RETURN
END

BUILD
