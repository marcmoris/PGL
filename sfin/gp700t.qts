;/T/ Gestion de projets - Approbation des budgets comptables d'un projet.
;
;/P/ Programmeur...: Steeve Duguay
;    Date création.: 14 mars 1995
;
;    Description...: Programme qui permet d'approuver les budgets comptables
;                    d'un projet.
;------------------------------------------------------------------------------

USE ussetqts NOLIST   ; permet de faire les SET PROCESS NOLIMIT....

RUN gp700t

;-------------------------------------------------------------------------------
;
; Approbation du projet
;
REQUEST APPROBATION

ACCESS GPPROJETS &

       LINK CIECLE OF GPPROJETS, &
            PRJCLE OF GPPROJETS  &
         TO CIECLE,              &
            PRJCLE OF GPPRO_BDG_CTB OPTIONAL


CHOOSE CIECLE PARM, &                    ; Compagnie
       PRJCLE PARM

SORT ON CIECLE OF GPPROJETS, &
        PRJCLE OF GPPROJETS, &
        CPTCLE OF GPPRO_BDG_CTB, &
        UNACLE OF GPPRO_BDG_CTB

SELECT GPPROJETS IF PRJPBCDATAPP OF GPPROJETS = 0

OUTPUT GPPROJETS     UPDATE AT PRJCLE
  ITEM PRJPBCDATAPP OF GPPROJETS     FINAL SYSDATE
  ITEM PRJPBCUSRAPP OF GPPROJETS     FINAL LOGONID

OUTPUT GPPRO_BDG_CTB UPDATE AT UNACLE IF RECORD GPPRO_BDG_CTB EXISTS
  ITEM PBCMNTBDGREV OF GPPRO_BDG_CTB = PBCMNTBDG OF GPPRO_BDG_CTB


SUBFILE WGP700 AT PRJCLE INCLUDE &
        GPPROJETS

;-------------------------------------------------------------------------------
;
; Approbation du projet et de ses sous-projets.
;
REQUEST APPROBATION_2

ACCESS *WGP700 &

       LINK CIECLE OF WGP700,        &
            PRJCLE OF WGP700         &
         TO CIECLE,                  &
            PRJCLE OF GPPRO_ACTIVITE &

       LINK CIECLE OF GPPRO_ACTIVITE,&
            PRJCLE OF GPPRO_ACTIVITE,&
            PRACLE OF GPPRO_ACTIVITE &
         TO CIECLE,                  &
            PRJCLE,                  &
            PRACLE OF GPPRA_BDG_CTB OPTIONAL


SORT ON CIECLE OF WGP700, &
        PRJCLE OF WGP700, &
        PRACLE OF GPPRO_ACTIVITE,&
        CPTCLE OF GPPRA_BDG_CTB, &
        UNACLE OF GPPRA_BDG_CTB

OUTPUT GPPRO_ACTIVITE UPDATE AT PRACLE
  ITEM PRAPACDATAPP OF GPPRO_ACTIVITE FINAL SYSDATE
  ITEM PRAPACUSRAPP OF GPPRO_ACTIVITE FINAL LOGONID

OUTPUT GPPRA_BDG_CTB UPDATE AT UNACLE IF RECORD GPPRA_BDG_CTB EXISTS
  ITEM PACMNTBDGREV OF GPPRA_BDG_CTB = PACMNTBDG OF GPPRA_BDG_CTB

BUILD
