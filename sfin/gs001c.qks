;/T/ Copie d'un usager
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 17 septembre 1993
;
;    Description..: Permet de copier la sécurité d'un autre usager à un autre
;                   usager.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gs001c NOMODE NOACTION FIELDMARK &
              FROM 6,15 TO 16,64 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY &
@IF UNIX
              TRANSACTION MODE DUAL &
@ENDIF
              RECEIVING GSUSAGER


USE ussectmp NOLIST
"GS001C"

USE gs001c.hlp NOLIST

USE ustrasse   NOLIST ; Transaction QUERY pour les sous-écrans.


;-------------------------------------------------------------------------------
;
; Usager d'origine.
;
FILE GSUSAGER         MASTER

;
; Usager de destination.
;
FILE GSUSAGER         PRIMARY &
                      ALIAS A_GSUSAGER &
                      NOITEM
  ITEM USRDATFIN OF A_GSUSAGER FINAL   USRDATFIN OF GSUSAGER
  ITEM XLOCLE    OF A_GSUSAGER FINAL   XLOCLE    OF GSUSAGER

;
; Lecture de la sécurité par programme de l'usger d'origine.
;
FILE GSUSAGER_PROG    DESIGNER &
                      OPEN READ
  ACCESS VIA   USRCLE     &
         USING USRCLE OF GSUSAGER

;
; Lecture de la sécurité par compagnie de l'usger d'origine.
;
FILE GSSEC_USR        DESIGNER &
                      OPEN READ
  ACCESS VIA USRCLE       &
          USING USRCLE OF GSUSAGER

;
; Copie de la sécurité des programmes pour l'usager destination.
;
FILE GSUSAGER_PROG    DESIGNER &
                      ALIAS A_GSUSAGER_PROG

;
; Copie de la sécurité compagnie.
;
FILE GSSEC_USR        DESIGNER &
                      ALIAS A_GSSEC_USR

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 11,50
DRAW 3,7 TO  6,44

TITLE &
@IF FRANCAIS
      " Copie d'un usager " &
@ELSE
      " User Copy " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST

SKIP 2

TITLE &
@IF FRANCAIS
      "Entrez le nouveau code et le nom " &
@ELSE
      "Enter the code and the name of the" &
@ENDIF
      AT ,9
SKIP

TITLE &
@IF FRANCAIS
      "de l'usager cible.                " &
@ELSE
      "user where it's to be copied.     " &
@ENDIF
      AT ,9
SKIP 2

ALIGN(2,3,19)
FIELD USRCLE    OF A_GSUSAGER HIDDEN &
        REQUIRED &
        LOOKUP NOTON A_GSUSAGER &
          VIA USRCLE     &
          USING USRCLE   OF A_GSUSAGER &
          MESSAGE 3   ; Ce code existe déjà.

FIELD USREMPNOM OF A_GSUSAGER HIDDEN SIZE 30 &
        REQUIRED

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; La copie se fait dans la procédure ENTRY. Cet écran est appelé en mode ENTRY.
;
;
PROCEDURE ENTRY
BEGIN
  ACCEPT USRCLE    OF A_GSUSAGER
  ACCEPT USREMPNOM OF A_GSUSAGER
  ;
  ; Copie la sécurité par programme.
  ;
  WHILE RETRIEVING GSUSAGER_PROG
  BEGIN
    LET USRCLE     OF A_GSUSAGER_PROG = USRCLE    OF A_GSUSAGER
    LET XPRGNR     OF A_GSUSAGER_PROG = XPRGNR    OF GSUSAGER_PROG
    LET XPRNOM     OF A_GSUSAGER_PROG = XPRNOM    OF GSUSAGER_PROG
    LET USPFLGACS  OF A_GSUSAGER_PROG = USPFLGACS OF GSUSAGER_PROG
    LET USPFLGENT  OF A_GSUSAGER_PROG = USPFLGENT OF GSUSAGER_PROG
    LET USPFLGDEL  OF A_GSUSAGER_PROG = USPFLGDEL OF GSUSAGER_PROG
    LET USPFLGMOD  OF A_GSUSAGER_PROG = USPFLGMOD OF GSUSAGER_PROG
    PUT A_GSUSAGER_PROG RESET
  END
  ;
  ; Copie de la sécurité par compagnie.
  ;
  WHILE RETRIEVING GSSEC_USR
  BEGIN
    LET USRCLE    OF A_GSSEC_USR = USRCLE   OF A_GSUSAGER
    LET CIECLE    OF A_GSSEC_USR = CIECLE   OF GSSEC_USR
    PUT A_GSSEC_USR RESET
  END
  ;
  ; Ajout du nouvel usager.
  ;
  PUT A_GSUSAGER

  RETURN
END

BUILD
@IF FORMAT


               *************** Copie d'un usager ****************
               *                                                *
               *     **************************************     *
               *     * Entrez le nouveau code et le nom   *     *
               *     * de l'usager cible.                 *     *
               *     **************************************     *
               *                                                *
               * Usager........: xxxxxxxxxxxx                   *
               * Nom usager....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
               *                                                *
               **************************************************
@ENDIF
