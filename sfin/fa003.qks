;/T/ Définition un trajet / produit
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 14 décembre 1994
;
;    Description..:
;
;
;/M/ Marie-Josée Hamel, 96.02.06, ajouter l'appel automatique des sous écrans
;/M/ Marie-Josée Hamel, 96.03.21, ajouter le flag description modifiable

;/V/ Écran vérifier pour le passage à l'an 2000

;------------------------------------------------------------------------------
SCREEN fa003 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "FA003"

USE fa003.hlp NOLIST

USE usactecr  NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Grille de tarification      " ACTION DESIGNER D001
  MENUITEM LABEL "Segment                     " ACTION DESIGNER D002
  MENUITEM LABEL "Historique des factures     " ACTION DESIGNER D003
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%lle de tarification      " ACTION DESIGNER D001
  MENUITEM LABEL "%%%ment                     " ACTION DESIGNER D002
  MENUITEM LABEL "%%%torique des factures     " ACTION DESIGNER D003
@ENDIF

;-------------------------------------------------------------------------------
;
TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie - client
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

;-------------------------------------------------------------------------------
;
; Élément de tarification
;
FILE FATRAJET_PRODUIT  PRIMARY &
                       NOITEMS

  ACCESS  VIA     CIECLE, &
                  TRPCLE  &
          USING   T_CIECLEMNU, &
                  TRPCLE       &
          REQUEST TRPCLE &
          ORDERBY TRPCLE

  ACCESS  VIA     CIECLE, &
                  TRPTYP  &
          USING   T_CIECLEMNU, &
                  TRPTYP       &
          REQUEST TRPTYP &
          ORDERBY TRPCLE

  ACCESS  VIA     CIECLE &
          USING   T_CIECLEMNU &
          ORDERBY TRPCLE

  ITEM CIECLE    OF FATRAJET_PRODUIT INITIAL T_CIECLEMNU

;------------------------------------
; Validation du type trajet / produit
;------------------------------------
DEFINE    D_TRPTYP CHARACTER SIZE 16 = "TRPTYP"
TEMPORARY T_TRPTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS A_CBI_TRPTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_TRPTYP, &
                TRPTYP OF FATRAJET_PRODUIT


;------------------------------------
; Validation du statut
;------------------------------------
DEFINE    D_TRPSTU CHARACTER SIZE 16 = "TRPSTU"
TEMPORARY T_TRPSTU CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS A_CBI_TRPSTU

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_TRPSTU, &
                TRPSTU OF FATRAJET_PRODUIT


;-----------------------------
; Validation du port d'origine
;-----------------------------
TEMPORARY T_PORORI CHARACTER SIZE 4

FILE FAPORT                 REFERENCE &
                            ALIAS FAPORT_PORORI

  ACCESS  VIA   FPOCLE  &
          USING TRPPORORI OF FATRAJET_PRODUIT


;----------------------------------
; Validation du port de destination
;----------------------------------
TEMPORARY T_PORDES CHARACTER SIZE 4

FILE FAPORT                 REFERENCE &
                            ALIAS FAPORT_PORDES

  ACCESS  VIA   FPOCLE  &
          USING TRPPORDES OF FATRAJET_PRODUIT

;--------------------------------------
; Validation de la catégorie de produit
;--------------------------------------
DEFINE    D_TRPCAT CHARACTER SIZE 16 = "CATPRD"
TEMPORARY T_TRPCAT CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS A_CBI_TRPCAT

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_TRPCAT, &
                TRPCAT OF FATRAJET_PRODUIT


FILE FAGRILLE_TARIF DELETE

  ACCESS  VIA     CIECLE, &
                  TRPCLE  &
          USING   T_CIECLEMNU, &
                  TRPCLE


FILE FASEGMENT    DELETE

  ACCESS  VIA     CIECLE, &
                  TRPCLE  &
          USING   T_CIECLEMNU, &
                  TRPCLE

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL       CHARACTER SIZE 1 ; Flag pour la destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Trajet / Produit " &
@ELSE
      "%%%Trajet / Produit " &
@ENDIF
      CENTERED AT ,1

TITLE "/km" AT 18,36

SKIP TO 5

USE ustitoff NOLIST


ALIGN (3,3,19) (,,24) (,50,66) (,,68)
FIELD TRPTYP OF FATRAJET_PRODUIT &
      HIDDEN ID 10 &
      REQUIRED NOCHANGE &
      LOOKUP ON A_CBI_TRPTYP                    &
        MESSAGE 2712; Ce type de produit est inexistant

FIELD CBIDSC    OF A_CBI_TRPTYP                 &
      DISPLAY

FIELD TRPSTU OF FATRAJET_PRODUIT &
      LOOKUP ON A_CBI_TRPSTU                    &
        MESSAGE 2712; Ce type de produit est inexistant

FIELD CBIDSC    OF A_CBI_TRPSTU                 &
      DISPLAY SIZE 10

SKIP 1

ALIGN (3,3,19)
FIELD TRPCLE OF FATRAJET_PRODUIT &
      HIDDEN ID 15 &
      REQUIRED NOCHANGE &
      LOOKUP NOTON FATRAJET_PRODUIT &
        VIA   CIECLE, &
              TRPCLE  &
        USING T_CIECLEMNU, &
              TRPCLE OF FATRAJET_PRODUIT &
        MESSAGE 2713; Ce trajet / produit existe déjà

FIELD TRPDSCFRA    OF FATRAJET_PRODUIT &
      LABEL "Desc. fra.....:" &
      ID SAME

FIELD TRPDSCANG    OF FATRAJET_PRODUIT &
      LABEL "Desc. ang.....:" &
      ID SAME

FIELD TRPDSCMOD     OF FATRAJET_PRODUIT SIZE 3 &
      ID SAME

FIELD TRPIMP        OF FATRAJET_PRODUIT SIZE 3 &
      ID SAME

SKIP 1

ALIGN (3,3,19) (,,24)
FIELD TRPPORORI OF FATRAJET_PRODUIT                   &
      HIDDEN ID 20                                    &
      REQUIRED                                        &
      IF TRPTYP OF FATRAJET_PRODUIT = "T"             &
      LOOKUP ON FAPORT_PORORI                         &
        MESSAGE 2714; Ce port est inexistant

FIELD FPODSCFRA OF FAPORT_PORORI                      &
      ID SAME                                         &
      DISPLAY

FIELD TRPPORDES OF FATRAJET_PRODUIT                   &
      HIDDEN ID 25                                    &
      REQUIRED                                        &
      IF TRPTYP OF FATRAJET_PRODUIT = "T"             &
      LOOKUP ON FAPORT_PORDES                         &
        MESSAGE 2714; Ce port est inexistant

FIELD FPODSCFRA OF FAPORT_PORDES                      &
      ID SAME                                         &
      DISPLAY

SKIP 1

ALIGN (3,3,19) (,,24) (,50,66)
FIELD TRPCAT    OF FATRAJET_PRODUIT             &
      HIDDEN ID 30                              &
      REQUIRED                                  &
      LOOKUP ON A_CBI_TRPCAT                    &
        MESSAGE 2715; Cette catégorie de produit est inexistante

FIELD CBIDSC    OF A_CBI_TRPCAT                 &
      ID SAME                                   &
      DISPLAY

SKIP 1

ALIGN (3,3,19)
FIELD TRPSEG        OF FATRAJET_PRODUIT SIZE 3 &
      IF TRPTYP OF FATRAJET_PRODUIT = "T"      &
      HIDDEN ID 35

ALIGN (3,3,19) (,50,66)
FIELD TRPDIS        OF FATRAJET_PRODUIT     &
      IF TRPTYP OF FATRAJET_PRODUIT = "T"   &
      HIDDEN ID 40

FIELD TRPCAPPAS     OF FATRAJET_PRODUIT     &
      IF TRPTYP OF FATRAJET_PRODUIT = "T"   &
      ID SAME

ALIGN (,50,66)
FIELD TRPCAPVEH     OF FATRAJET_PRODUIT     &
      IF TRPTYP OF FATRAJET_PRODUIT = "T"   &
      ID SAME

ALIGN (3,3,19)
FIELD TRPCOM1       OF FATRAJET_PRODUIT &
      HIDDEN ID 45

ALIGN (,,19)
FIELD TRPCOM2       OF FATRAJET_PRODUIT &
      ID SAME



SUBSCREEN fa003a AUTO &
            HIDDEN NOLABEL &
            PASSING &
              T_CIECLEMNU, &
              T_CIENOM   , &
              FATRAJET_PRODUIT &
                MODE SAME

SUBSCREEN fa003b AUTO &
            HIDDEN NOLABEL &
            IF TRPSEG OF FATRAJET_PRODUIT = "1" &
            PASSING &
              T_CIECLEMNU, &
              T_CIENOM   , &
              FATRAJET_PRODUIT &
                MODE SAME



;-------------------------------------------------------------------------------
;
; Pour la transformation du flag  en 1 et 0
;
PROCEDURE INPUT TRPDSCMOD
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour afficher le falg sous la forme Oui ou Non
;
PROCEDURE OUTPUT TRPDSCMOD
BEGIN
  USE usflgout3 NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour la transformation du flag d'impression en 1 et 0
;
PROCEDURE INPUT TRPIMP
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour afficher le falg sous la forme Oui ou Non
;
PROCEDURE OUTPUT TRPIMP
BEGIN
  USE usflgout3 NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour la transformation du flag d'impression en 1 et 0
;
PROCEDURE INPUT TRPSEG
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Pour afficher le falg sous la forme Oui ou Non
;
PROCEDURE OUTPUT TRPSEG
BEGIN
  USE usflgout3 NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue type trajet / produit
;
;
PROCEDURE INPUT TRPTYP
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TRPTYP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TRPTYP, T_TRPTYP &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_TRPTYP)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue statut
;
;
PROCEDURE INPUT TRPSTU
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TRPSTU = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TRPSTU, T_TRPSTU &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_TRPSTU)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue port d'origine
;
;
PROCEDURE INPUT TRPPORORI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PORORI = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN fa104 MODE F PASSING T_PORORI &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PORORI)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue port de destination
;
;
PROCEDURE INPUT TRPPORDES
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PORDES = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN fa104 MODE F PASSING T_PORDES &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PORDES)
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le code bilingue catégorie de produit
;
;
PROCEDURE INPUT TRPCAT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TRPCAT = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TRPCAT, T_TRPCAT &
                            WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_TRPCAT)
  END
END


PROCEDURE PROCESS TRPDSCFRA
BEGIN
   IF TRPDSCANG OF FATRAJET_PRODUIT = " "
   THEN BEGIN
        LET TRPDSCANG OF FATRAJET_PRODUIT = TRPDSCFRA OF FATRAJET_PRODUIT
        DISPLAY TRPDSCANG
   END
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran des grilles de tarifications.
;
PROCEDURE DESIGNER D001
BEGIN
  IF NEWRECORD     OF FATRAJET_PRODUIT OR &
     ALTEREDRECORD OF FATRAJET_PRODUIT OR &
     DELETEDRECORD OF FATRAJET_PRODUIT
  THEN ERROR 2716 ; Faire mise à jour avant d'aller au sous-écran

  RUN SCREEN fa003a PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        FATRAJET_PRODUIT &
                MODE F
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran des segements.
;
PROCEDURE DESIGNER D002
BEGIN
  IF NEWRECORD     OF FATRAJET_PRODUIT OR &
     ALTEREDRECORD OF FATRAJET_PRODUIT OR &
     DELETEDRECORD OF FATRAJET_PRODUIT
  THEN ERROR 2716 ; Faire mise à jour avant d'aller au sous-écran

  IF TRPSEG OF FATRAJET_PRODUIT <> "1"
  THEN ERROR 2724 ; Le trajet / produit doit avoir l'option du segment multiple

  RUN SCREEN fa003b PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        FATRAJET_PRODUIT &
                MODE F
END



;-------------------------------------------------------------------------------
;
; Appel du sous-écran des historiques.
;
PROCEDURE DESIGNER D003
BEGIN
  IF NEWRECORD     OF FATRAJET_PRODUIT OR &
     ALTEREDRECORD OF FATRAJET_PRODUIT OR &
     DELETEDRECORD OF FATRAJET_PRODUIT
  THEN ERROR 2716 ; Faire mise à jour avant d'aller au sous-écran

  RUN SCREEN fa003c PASSING &
                        T_CIECLEMNU, &
                        T_CIENOM   , &
                        FATRAJET_PRODUIT &
                MODE F
END


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
    ;
    ; Validation de la destruction
    ;
    IF DELETEDRECORD OF FATRAJET_PRODUIT &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification
    ;
    IF    ALTEREDRECORD     OF FATRAJET_PRODUIT &
      AND NOT NEWRECORD     OF FATRAJET_PRODUIT &
      AND NOT DELETEDRECORD OF FATRAJET_PRODUIT &
      AND TZ_SECMOD = "0"
    THEN ERROR 2

    ;
    ; Destruction de l'élément de tarification
    ;
    IF DELETEDRECORD OF FATRAJET_PRODUIT
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si le trajet / produit est
      ; référée dans la définition des grilles de tarifications et dans
      ; le segment d'un produit.
      ;
      RUN SCREEN fa003x MODE SAME &
                        PASSING FATRAJET_PRODUIT, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
END

PROCEDURE DESIGNER 10
BEGIN
  ACCEPT  TRPSTU OF FATRAJET_PRODUIT
  DISPLAY CBIDSC OF A_CBI_TRPSTU
END

PROCEDURE DESIGNER 20
BEGIN
  IF TRPTYP OF FATRAJET_PRODUIT = "T"
  THEN ACCEPT  TRPPORORI OF FATRAJET_PRODUIT
  ELSE DISPLAY TRPPORORI OF FATRAJET_PRODUIT
  DISPLAY FPODSCFRA OF FAPORT_PORORI
END

PROCEDURE DESIGNER 25
BEGIN
  IF TRPTYP OF FATRAJET_PRODUIT = "T"
  THEN ACCEPT  TRPPORDES OF FATRAJET_PRODUIT
  ELSE DISPLAY TRPPORDES OF FATRAJET_PRODUIT
  DISPLAY FPODSCFRA OF FAPORT_PORDES
END

PROCEDURE DESIGNER 35
BEGIN
  IF TRPTYP OF FATRAJET_PRODUIT = "T"
  THEN ACCEPT  TRPSEG OF FATRAJET_PRODUIT
  ELSE DISPLAY TRPSEG OF FATRAJET_PRODUIT
END

PROCEDURE DESIGNER 40
BEGIN
  IF TRPTYP OF FATRAJET_PRODUIT = "T"
  THEN ACCEPT  TRPDIS OF FATRAJET_PRODUIT
  ELSE DISPLAY TRPDIS OF FATRAJET_PRODUIT

  IF TRPTYP OF FATRAJET_PRODUIT = "T"
  THEN ACCEPT  TRPCAPPAS OF FATRAJET_PRODUIT
  ELSE DISPLAY TRPCAPPAS OF FATRAJET_PRODUIT

  IF TRPTYP OF FATRAJET_PRODUIT = "T"
  THEN ACCEPT  TRPCAPVEH OF FATRAJET_PRODUIT
  ELSE DISPLAY TRPCAPVEH OF FATRAJET_PRODUIT
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Trajet / Produit *******************************
*                                                                              *
* Type..........: x    xxxxxxxxxxxxxxxxxxxxxxxxx Statut........: x xxxxxxxxxx  *
*                                                                              *
* No produit....: xxx                                                          *
* Desc. fra.....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Desc. ang.....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Desc. modif...: xxx                                                          *
* Imprimer......: xxx                                                          *
*                                                                              *
* Port origine..: xxxx xxxxxxxxxxxxxxxxxxxxxxxxx                               *
* Port dest.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                              *
* Catégorie.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                              *
* Segment mult..: xxx                                                          *
* Distance......: xxxxxxxxxxxxxxxx               Cap. passagers: xxxxxxxxxxx   *
*                                                     véhicules: xxxxxxxxxxx   *
* Commentaire...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF
