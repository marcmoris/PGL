;/T/ 2.5.4 Impression des bons de livraisons.
;
;/P/ Programmeur..: Denis Pouliot
;    Date Création: 1995-06-06
;
;    Description..: Imprimer les formulaire de bons de livraison PD226Z.
;
;       *** NOTE : Un bon de livraison ne se fait que pour des diagrammes
;                  ou des tranches.
;
;***********************************************************************************************************************************
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 07 mars 2000
;    Description.......: Modifier le point d'impression des indicateurs (*) "prepaid" et "collect".
;
;    Référence.........: Demande de changement 000023.
;
;    Signet............: MP-00-03-07_D23.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 08 mars 2000
;    Description.......: Ajuster le point d'impression afin l'usager n'est plus à le déplacer de 16 positions.
;
;    Référence.........: Demande directe 999999.
;
;    Signet............: MP-00-03-08_DD999999.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 19 février 2002
;    Description.......: Imprimer les numéro de téléphone et de fax du contact/client.
;
;    Référence.........: Demande 200216.
;
;    Signet............: MP-02-02-19_D200216.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;USE ussetqzs  nolist

;SET PAGE LENGTH 65 WIDTH 85
SET PAGE LENGTH 62 WIDTH 132
SET REPORT DEVICE DISC NAME $PRFICIMP NOLIMIT
SET FORMFEED NOHEAD NOCLOSE

ACCESS PDBON_LIVR_HIST &

  LINK CIECLE    OF PDBON_LIVR_HIST      , &  ; Informations sur l'expédition
       EXPNUMEXP OF PDBON_LIVR_HIST        &
  TO   CIECLE                           , &
       EXPNUMEXP OF PDEXPEDITION          &

  LINK CIECLE    OF PDBON_LIVR_HIST      , &
       EXPNUMEXP OF PDBON_LIVR_HIST      , &
       BLINUMBLI OF PDBON_LIVR_HIST        &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI OF PDBON_LIVRAISON       &

LINK CIECLE OF PDBON_LIVR_HIST,&    ; info commande remplacement
     COMNUMCOM OF PDBON_LIVR_HIST &
  TO CIECLE,&
     COMNUMCOM OF PDCOMMAN_INTERNE &

  LINK CIECLE       OF PDBON_LIVR_HIST   , &
       PJTABR       OF PDBON_LIVRAISON    &  ; Informations sur le projet
  TO   CIECLE                           , &
       PJTABR       OF PDPROJET           &

  LINK ("----", & ; CIECLE      OF PDBON_LIVR_HIST   , &  ; Adresse de facturation
        CLICLE      OF PDCOMMAN_INTERNE , &
        "C"                             , &
        COMNUMADRFAC OF PDCOMMAN_INTERNE) &
  TO   REFCIECLE                        , &
       REFCLENUM                        , &
       REFCLETYP                        , &
       RADCLE        OF MCREF_ADRESSE     &

  LINK ("----", & ; CIECLE      OF PDBON_LIVR_HIST   , &  ; Adresse de livraison
        CLICLE       OF PDBON_LIVRAISON , &
        "C"                             , &
        COMNUMADRLIV OF PDBON_LIVRAISON)  &
  TO   REFCIECLE                        , &
       REFCLENUM                        , &
       REFCLETYP                        , &
       RADCLE       OF MCREF_ADRESSE      ALIAS A_RAD_ADRLIVRAISON OPTIONAL &

  LINK ("----", & ; CIECLE       OF PDBON_LIVR_HIST   , &  ; Informations sur le transporteur
       FOUCLE OF PDEXPEDITION)             &
  TO   FOUCIECLE                        , &
       FOUCLE OF CPFOURNISSEUR            &

  LINK CIECLE       OF PDBON_LIVR_HIST   , &  ; Informations sur conteneur
       EXPNUMEXP OF PDBON_LIVR_HIST      , &
       BLINUMBLI OF PDBON_LIVR_HIST        &
  TO   CIECLE                           , &
       EXPNUMEXP                        , &
       BLINUMBLI OF PDCONTENEUR           OPTIONAL &

;a verifie lien conteneur

  LINK FOUCIECLE OF PDCONTENEUR,&
       FOUCLE    OF PDCONTENEUR &
    TO FOUCIECLE ,&
       FOUCLE OF CPFOURNISSEUR ALIAS A_FOU OPT &

  LINK (CIECLE      OF PDBON_LIVR_HIST   , &
        "M2"                            , &
        "P2"  )                           &
  TO   CIECLE                           , &
       CUMCODUNMINT                     , &
       CUMCODUNMEXT OF PDCONVERT_UN_M     ALIAS A_CUM_P2 &

  LINK (CIECLE      OF PDBON_LIVR_HIST   , &
        "M3"                            , &
        "P3"  )                           &
  TO   CIECLE                           , &
       CUMCODUNMINT                     , &
       CUMCODUNMEXT OF PDCONVERT_UN_M     ALIAS A_CUM_P3 &

  LINK (CIECLE      OF PDBON_LIVR_HIST   , &
        "KG"                            , &
        "LB"  )                           &
  TO   CIECLE                           , &
       CUMCODUNMINT                     , &
       CUMCODUNMEXT OF PDCONVERT_UN_M     ALIAS A_CUM_LB &

 LINK          CLICIECLE OF PDCOMMAN_INTERNE,       &
               CLICLE    OF PDCOMMAN_INTERNE,       &
               CCTCLE    OF PDCOMMAN_INTERNE        &
         TO    CLICIECLE        ,&
               CLICLE           ,&
               CCTCLE           OF CRCLI_CONTACT  OPT


CHOOSE CIECLE    PARM ,&
       EXPNUMEXP PARM ,&
       BLINUMBLI PARM


DEFINE D_TRI_BON CHARACTER SIZE 11 =   ASCII(EXPNUMEXP OF PDBON_LIVR_HIST,9) &
                                     + BLINUMBLI OF PDBON_LIVR_HIST
                              ; Permet d'effectuer le tri PAR bon de livraison


SORTED ON D_TRI_BON


;
; Variables
;

DEFINE D_PREPAID CHAR SIZE 1 = "*" IF EXPPREPAI OF PDEXPEDITION = "O" ELSE " " 
DEFINE D_COLLECT CHAR SIZE 1 = "*" IF EXPPREPAI OF PDEXPEDITION = "N" ELSE " "



DEFINE D_ADRMEME  CHARACTER SIZE 20 = "                MÊME" &
                                      IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F" &
                                      ELSE "                SAME"

DEFINE D_NOMLIV   CHARACTER SIZE 20 = RADNOMABR OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_ADRLIVR1 CHARACTER SIZE 40 = RADADR1 OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_ADRLIVR2 CHARACTER SIZE 40 = RADADR2 OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_ADRLIVR3 CHARACTER SIZE 40 = RADADR3 OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                       ELSE D_ADRMEME

DEFINE D_ADRLIVR4 CHARACTER SIZE 40 = RADADR4 OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_ADRLIVCP CHARACTER SIZE 40 = RADCP OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_RESPLIVR CHARACTER SIZE 20 = RADCNT OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_TELLIVR CHARACTER SIZE 20 = RADTEL OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_FAXLIVR CHARACTER SIZE 20 = RADFAX OF A_RAD_ADRLIVRAISON &
                                      IF    COMNUMADRLIV OF PDBON_LIVRAISON  &
                                         <> COMNUMADRFAC OF PDCOMMAN_INTERNE &
                                      ELSE " "

DEFINE D_SURP2  FLOAT SIZE 8 =   BLHTOT_SURMC2 OF PDBON_LIVR_HIST &
                               * CUMFACCON OF A_CUM_P2

DEFINE D_VOLP3  FLOAT SIZE 8 =   BLHTOT_VOLMC3 OF PDBON_LIVR_HIST &
                               * CUMFACCON OF A_CUM_P3

DEFINE D_POIDLB FLOAT SIZE 8 =   (BLHTOT_POIDKG2 OF PDBON_LIVR_HIST &
                               * CUMFACCON OF A_CUM_LB) / 10000

DEFINE D_FOUNOMABR CHAR SIZE 25 = FOUNOMABR OF CPFOURNISSEUR IF RECORD PDCONTENEUR EXIST ELSE " "

DEFINE D_CTNNUMVOY CHAR SIZE 20 = "V." + CTNNUMVOY OF PDCONTENEUR

DEFINE D_CTNNOMBAT CHAR SIZE 20 = CTNNOMBAT OF PDCONTENEUR

DEFINE D_AJUS_COD CHARACTER SIZE 05 = "[1e"  ; MP-00-03-08_DD999999.
DEFINE D_PITCH_06 CHARACTER SIZE 05 = "^[[1z" ; MP-00-03-08_DD999999.
DEFINE D_PITCH_08 CHARACTER SIZE 05 = "^[[2z" ; MP-00-03-08_DD999999.

HEADING AT D_TRI_BON &
;   TAB 001 "." &
; SKIP 2 &
;   TAB 01  D_PITCH_08 D_AJUS_COD D_PITCH_06 & ; MP-00-03-08_DD999999.
    TAB 01  D_AJUS_COD                       & ; MP-00-03-08_DD999999.
    TAB 78  EXPDATEXP                        & ; SYSDATE &
  SKIP 3 &
    TAB 45  COMNOM OF PDCOMMAN_INTERNE   &

  SKIP 3 &
    TAB 51  CLICLE    OF PDBON_LIVRAISON &  ; Client, projet, commande
    TAB 69  PJTABR OF PDBON_LIVR_HIST "-" PJTANN OF PDBON_LIVR_HIST &

  SKIP 1 &
    TAB 57  COMNUMCOM OF PDBON_LIVR_HIST &

  SKIP 3 &
    TAB 05 RADNOMABR OF MCREF_ADRESSE   &  ;  Adresse de facturation et de livraison
    TAB 44 D_NOMLIV                     &

  SKIP 1 &
    TAB 05 RADADR1 OF MCREF_ADRESSE     &
    TAB 44 D_ADRLIVR1                   &

  SKIP 1 &
    TAB 05 RADADR2 OF MCREF_ADRESSE     &
    TAB 44 D_ADRLIVR2                   &

  SKIP 1 &
    TAB 05 RADADR3 OF MCREF_ADRESSE     &
    TAB 44 D_ADRLIVR3                   &

  SKIP 1 &
    TAB 05 RADADR4 OF MCREF_ADRESSE     &
    TAB 44 D_ADRLIVR4                   &

  SKIP 1 &
    TAB 05 RADCP   OF MCREF_ADRESSE     &
    TAB 44 D_ADRLIVCP                   &

  SKIP 1 &
    TAB 10 CCTNOM OF CRCLI_CONTACT      &
    TAB 50 D_RESPLIVR                   &

  SKIP 1 &
    TAB 9  CCTTEL OF CRCLI_CONTACT      & ; MP-02-02-19_D200216.
    TAB 27 CCTFAX OF CRCLI_CONTACT      & ; MP-02-02-19_D200216.
    TAB 50 D_TELLIVR                    &
    TAB 66 D_FAXLIVR                    &

  SKIP 3 &
    TAB 05 COMNUMLETCRECLI OF PDCOMMAN_INTERNE & ; Lettre de crédit, échéance,
                                                 ; détails et conteneur
    TAB 60 BLHDESCPRDBON OF PDBON_LIVR_HIST      &

  SKIP 1 &
    TAB 05 COMNUMLETCREGRA OF PDCOMMAN_INTERNE &

  SKIP 1 &
    TAB 05 COMDATECHLETCRE OF PDCOMMAN_INTERNE &
    TAB 71 BLIQTECAI       OF PDBON_LIVR_HIST PICTURE "^,^^^" &

  SKIP 1 &
    TAB 71 BLHTOT_QTEPRD    OF PDBON_LIVR_HIST PICTURE "^,^^^" &

  SKIP 1 &
    TAB 17 CTNNUMCTR OF PDCONTENEUR     &

  SKIP 1 &
    TAB 17 D_FOUNOMABR  &; FOUNOMABR OF CPFOURNISSEUR  &   Transporteur
    TAB 50 BLHTOT_SURMC2 OF PDBON_LIVR_HIST &
                        OUTPUT SCALE 4    &
                        PICTURE "^^^,^^^.^^^^" &
    TAB 71 D_SURP2      OUTPUT SCALE 4    &
                        PICTURE "^^^,^^^.^^^^" &

  SKIP 1 &
    TAB 17 CTNLIGMAR    OF PDCONTENEUR     &
    TAB 50 BLHTOT_VOLMC3 OF PDBON_LIVR_HIST  &
                        OUTPUT SCALE 4     &
                        PICTURE "^^^,^^^.^^^^" &
    TAB 71 D_VOLP3      OUTPUT SCALE 4      &
                        PICTURE "^^^,^^^.^^^^" &

  SKIP 1 &
    TAB 17 D_CTNNOMBAT   &
    TAB 50 BLHTOT_POIDKG2 OF PDBON_LIVR_HIST  &
                        OUTPUT SCALE 0     &
                        PICTURE "^^,^^^,^^^.^^^^" &
    TAB 69 D_POIDLB     OUTPUT SCALE 4     &
                        PICTURE "^^,^^^,^^^.^^^^ " &
                        TRAILING SIGN "-" &

  SKIP 1 &
    TAB 17 D_CTNNUMVOY &

  SKIP 1 &
    TAB 17 CTNADRRAM OF PDCONTENEUR &

  SKIP 1 &
    TAB 17 CTNPORCHG OF PDCONTENEUR  &

  SKIP 1 &
    TAB 17 CTNPORTRF OF PDCONTENEUR &

  SKIP 1 &
    TAB 17 CTNPORDCH OF PDCONTENEUR  &

  SKIP 2 &
    TAB 14 CTNNUMCON OF PDCONTENEUR  &

  SKIP 1 &
    TAB 14 CTNTYPCON OF PDCONTENEUR  &

  SKIP 1 &
    TAB 14 CTNNUMSCE OF PDCONTENEUR  &

  SKIP 1 &
     TAB 14 BLHTOT_POIDKG2 OF PDBON_LIVR_HIST &
                         OUTPUT SCALE 0    &
                         PICTURE "^^,^^^,^^^.^^^^" &

  SKIP 5 &
    TAB 08 FOUNOMABR OF CPFOURNISSEUR &    ; Transporteur

  SKIP 1 &
    TAB 018 D_PREPAID   & ; MP-00-03-07_D23.
    TAB 035 D_COLLECT ; & ; MP-00-03-07_D23.
;  SKIP 17 &

PAGE FOOTING &
;SKIP 17 &
   TAB 69 EXPNUMEXP OF PDEXPEDITION    &
                    PICTURE "^^^^^^^^^" &
    TAB 78 "-"                          &
    TAB 79 BLINUMBLI OF PDBON_LIVRAISON &
    TAB 81 "-" &
    TAB 82 BLIAMM OF PDBON_LIVRAISON

BUILD pd226z
