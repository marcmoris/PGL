;/T/ 2.4.2 Enregistrer les défauts de tranches.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-19
;
;    Description..: Cette unité de traitement permet d'enregistrer les
;                   défauts de tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd184b ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              ACTIVITIES FIND,ENTRY,CHANGE  &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDTRANCHE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD184B"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd184b.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable pour l'appel des pop-up
;
TEMPORARY T_DTRCODDEF CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Bloc
;
FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Tranche défaut
;
FILE PDTRANCHE_DEF  PRIMARY &
                    NOITEM  &
                    OCCURS 10

  ACCESS VIA     CIECLE,                               &
                 TRANUMTRA002                          &
         USING   T_CIECLEMNU,                          &
                 TRANUMTRA002    OF PDTRANCHE          &
         ORDERBY CIECLE,                               &
                 TRANUMTRA002,                         &
                 DTRCODDEF

  ITEM CIECLE          OF PDTRANCHE_DEF  INITIAL T_CIECLEMNU
  ITEM TRANUMTRA002    OF PDTRANCHE_DEF  INITIAL TRANUMTRA002    OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Défaut de tranche
;

FILE PDDEF_DE_TRANCHE REFERENCE

  ACCESS VIA     CIECLE,                         &
                 DTRCODDEF                       &
         USING   T_CIECLEMNU,                    &
                 DTRCODDEF OF PDTRANCHE_DEF      &
         ORDERBY CIECLE,                         &
                 DTRCODDEF


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Tranche " &
@ELSE
      " %%%Tranche " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 5

ALIGN (,3,19)


FIELD TRANUMTRA002 OF PDTRANCHE_DEF           &
        DISPLAY                               &
        LABEL "No tranche....:"               &
        NUMERIC                               &
        PREDISPLAY REFRESH


DRAW 7,1 TO 7,80

SKIP TO LINE 7

TITLE &
@IF FRANCAIS
      " Défauts " &
@ELSE
      " %%%Défauts " &
@ENDIF
      CENTERED AT ,1

SKIP TO LINE 9

TITLE &
@IF FRANCAIS
      "Code de défaut  Description " &
@ELSE
      "%%%Code de défaut  Description " &
@ENDIF
      AT  ,14

SKIP TO LINE 11

ALIGN (2,,19) (,,30)


CLUSTER OCCURS WITH PDTRANCHE_DEF ID BASE 5


FIELD DTRCODDEF        OF PDTRANCHE_DEF    &
        HIDDEN                             &
        REQUIRED                           &
        LOOKUP                             &
          ON FILE PDDEF_DE_TRANCHE         &
            VIA     CIECLE,                &
                    DTRCODDEF              &
            USING   T_CIECLEMNU,           &
                    DTRCODDEF OF PDTRANCHE_DEF  &
            MESSAGE 2954 & ; Ce code de défaut n'existe pas
          , NOTON FILE PDTRANCHE_DEF       &
              VIA     CIECLE,              &
                      TRANUMTRA002,        &
                      DTRCODDEF            &
              USING   T_CIECLEMNU,                          &
                      TRANUMTRA002    OF PDTRANCHE_DEF,     &
                      DTRCODDEF       OF PDTRANCHE_DEF      &
              MESSAGE 2956 ; Ce code de défaut existe déjà pour cette tranche


FIELD DTRDSCFRA        OF PDDEF_DE_TRANCHE &
        DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du pop-up pour le choix des défauts.
;

PROCEDURE INPUT DTRCODDEF
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DTRCODDEF = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd116 MODE F &
                     PASSING T_DTRCODDEF, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DTRCODDEF )
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  FOR PDTRANCHE_DEF
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDTRANCHE_DEF &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDTRANCHE_DEF &
       AND NOT NEWRECORD     OF PDTRANCHE_DEF &
       AND NOT DELETEDRECORD OF PDTRANCHE_DEF &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END


BUILD


@IF FORMAT

2xxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
3*********************************** Sciage ************************************
4                                                                              *
5 No de montage.: xxxxxxxxxxx                    Date sciage...: xxxxxxxx      *
6 Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
7                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
8                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
9 No bloc.......: xxxxx-xxxxx-x                                                *
0                                                                              *
1*******************************************************************************
2                                                                              *
3     No tranche   Larg    Haut    Epais   Surface   Fini  Localisation        *
4     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
5     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
6     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
7     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
8     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
9     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
0     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
1     xxxxxxxxx    xxxxx   xxxxx   xxxxx   xxxxxxx   xxx        xxx            *
2                                                                              *
3*******************************************************************************
@ENDIF
