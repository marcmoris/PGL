;/T/ Impression de l'inventaire physique des blocs .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-01
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste de validation de l'inventaire physique de bloc
;                   de granit de Granicor.
;
;    Paramètres....: Compagnie
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INBLOC       &
  LINK TGRCODGRA,       &
       CIECLE           &
    TO TGRCODGRA,       &
       CIECLE    OF PDTYPE_GRANIT  OPT

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG301"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEF DPAGEDEB INT SIZE 4 = PARM
DEF DPAGEFIN INT SIZE 4 = PARM

SELECT INBLOC IF IBLNUMPGE OF INBLOC >= DPAGEDEB AND &
                 IBLNUMPGE OF INBLOC <= DPAGEFIN

SORT ON IBLNUMPGE  OF INBLOC &
     ON IBLDATINV  OF INBLOC &
     ON IBLHREINV  OF INBLOC

DEFINE D_IBLDESC CHARACTER SIZE 55 = IBLDESC OF INBLOC[1:55]

DEFINE D_TGRDSCFRA001 CHARACTER SIZE 15 = TGRDSCFRA001 OF PDTYPE_GRANIT[1:15]

USE ustitgor132_45 NOLIST
   SKIP 3 &
     TAB 53 "DONNEES DE LA PAGE: " IBLNUMPGE OF INBLOC &
   SKIP 4

HEADING AT IBLNUMPGE SKIP PAGE &

  TAB 003 &
;                                                                                                 1         1         1         1
;       1         2         3         4         5         6         7         8         9         0         1         2         3
;34567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
"LOCALISATION  NUM. BLOC      COMMENTAIRE                                               TYPE GRANIT  DESCRIPTION " &
SKIP 1 &
  TAB 003 &
"------------  ---------      -----------                                               -----------  ----------- "

REPORT &
  TAB 007 IBLNUMLOC &
  TAB 017 BLONUMGRA001APR OF INBLOC &
  TAB 021 "-" &
  TAB 022 BLONUMGRA002APR OF INBLOC &
  TAB 027 "-" &
  TAB 028 BLORECAPR       OF INBLOC &
  TAB 032 D_IBLDESC                     &
  TAB 094 TGRCODGRA       OF INBLOC &
  TAB 103 D_TGRDSCFRA001

FOOTING AT IBLNUMPGE SKIP 2 &
    "NOMBRE D'ENTREES POUR CETTE PAGE..............:" COUNT

FINAL FOOTING SKIP 2  &
    "GRAND TOTAL DE BLOCS EN INVENTAIRE............:" COUNT

BUILD ig301z
