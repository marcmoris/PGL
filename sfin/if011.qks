;/T/ Contrat
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
SCREEN if011 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM, &
                       T_CLICIECLE

USE ussectmp  NOLIST
    "IF011"

USE if011.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE    CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_CLICIECLE CHARACTER SIZE 4  ; compagnie du client
;-------------------------------------------------------------------------------
;
FILE IFCONTRAT        IN DICT PRIMARY NOITEM
  ACCESS VIA     CIECLE, &
                 CLTCLE, &
                 CRRCLENUM, &
                 CRRCLEANN  &
         USING   T_CIECLE, &
                 CLTCLE OF IFCONTRAT, &
                 CRRCLENUM OF IFCONTRAT, &
                 CRRCLEANN OF IFCONTRAT  &
         REQUEST CLTCLE OF IFCONTRAT, &
                 CRRCLENUM OF IFCONTRAT, &
                 CRRCLEANN OF IFCONTRAT  &
         ORDERBY CIECLE, &
                 CLTCLE, &
                 CRRCLENUM, &
                 CRRCLEANN
  ACCESS VIA     CIECLE, &
                 CLTCLE, &
                 CRRCLENUM &
         USING   T_CIECLE, &
                 CLTCLE OF IFCONTRAT, &
                 CRRCLENUM OF IFCONTRAT &
         REQUEST CLTCLE OF IFCONTRAT, &
                 CRRCLENUM OF IFCONTRAT &
         ORDERBY CIECLE, &
                 CLTCLE, &
                 CRRCLENUM, &
                 CRRCLEANN
  ACCESS VIA     CIECLE, &
                 CLTCLE &
         USING   T_CIECLE, &
                 CLTCLE OF IFCONTRAT &
         REQUEST CLTCLE OF IFCONTRAT &
         ORDERBY CIECLE, &
                 CLTCLE, &
                 CRRCLENUM, &
                 CRRCLEANN
  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY CIECLE, &
                 CLTCLE, &
                 CRRCLENUM, &
                 CRRCLEANN
  ITEM CIECLE    OF IFCONTRAT INITIAL T_CIECLE
;
;
FILE IFCONTRAT_LIGNE  IN DICT DETAIL NOITEM &
                              OCCURS 4 TIMES
  ACCESS VIA   CIECLE, &
               CLTCLE, &
               CRRCLENUM, &
               CRRCLEANN &
         USING T_CIECLE, &
               CLTCLE    OF IFCONTRAT, &
               CRRCLENUM OF IFCONTRAT, &
               CRRCLEANN OF IFCONTRAT
  ITEM CIECLE    OF IFCONTRAT_LIGNE INITIAL T_CIECLE
  ITEM CLTCLE    OF IFCONTRAT_LIGNE INITIAL CLTCLE OF IFCONTRAT
  ITEM CRRCLENUM OF IFCONTRAT_LIGNE INITIAL CRRCLENUM OF IFCONTRAT
  ITEM CRRCLEANN OF IFCONTRAT_LIGNE INITIAL CRRCLEANN OF IFCONTRAT
;
FILE IFCLIENT         IN DICT REFERENCE
  ACCESS VIA   CIECLE, &
               CLTCLE  &
         USING T_CIECLE, &
               CLTCLE OF IFCONTRAT

FILE CRCLIENT         IN DICT REFERENCE
  ACCESS VIA   CLICIECLE, &
               CLICLE &
         USING CLICIECLE OF IFCLIENT, &
               CLICLE OF IFCLIENT
;;
FILE IFCARRIERE       IN DICT REFERENCE
  ACCESS VIA   CIECLE, &
               CRRCLENUM, &
               CRRCLEANN  &
         USING CIECLE OF IFCONTRAT, &
               CRRCLENUM OF IFCONTRAT, &
               CRRCLEANN OF IFCONTRAT
;
FILE IFCATEGORIE      IN DICT REFERENCE
  ACCESS VIA   CIECLE, &
               CATCLE  &
         USING CIECLE OF IFCONTRAT_LIGNE, &
               CATCLE OF IFCONTRAT_LIGNE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Gestion des contrats " &
      CENTERED AT ,1

SKIP TO LINE 4

USE ustitoff NOLIST

ALIGN (2,5,19)(,,21)(,,23)(,26,32)(,44,56)
FIELD CLTCLE OF IFCONTRAT &
        HIDDEN &
        LABEL "No contrat..:" &
        NOCHANGE &
        REQUIRED &
        LOOKUP ON IFCLIENT &
          MESSAGE "E* Numéro de client inexistant..."
FIELD CRRCLENUM OF IFCONTRAT &
        NOCHANGE &
        REQUIRED
FIELD CRRCLEANN OF IFCONTRAT &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFCONTRAT &
          VIA   CIECLE, &
                CLTCLE, &
                CRRCLENUM, &
                CRRCLEANN  &
          USING CIECLE OF IFCONTRAT, &
                CLTCLE OF IFCONTRAT, &
                CRRCLENUM OF IFCONTRAT, &
                CRRCLEANN OF IFCONTRAT  &
          MESSAGE "E* Ce contrat existe déjà...", &
                ON IFCARRIERE &
          MESSAGE "E* Numéro de carrière inexistante..."
FIELD CONDAT OF IFCONTRAT &
        REQUIRED &
        NOCHANGE &
        LABEL "Date:"
FIELD CLINOMABR OF CRCLIENT &
        DISPLAY &
        LABEL "Nom Client:"

ALIGN(,5,19)(,44,56)
FIELD CRRDSC OF IFCARRIERE &
        DISPLAY &
        LABEL "Nom granit..:"
FIELD DEVCLE OF CRCLIENT &
        DISPLAY &
        LABEL "Devise....:"

ALIGN(2,5,19)
FIELD CONDSC1 OF IFCONTRAT &
        HIDDEN &
        LABEL "Description.:"
FIELD CONDSC2 OF IFCONTRAT &
        ID SAME &
        NOLABEL

ALIGN(40,43,55)
FIELD CONCONDITION1 OF IFCONTRAT &
        HIDDEN &
        LABEL "Conditions:"
FIELD CONCONDITION2 OF IFCONTRAT &
        ID SAME &
        NOLABEL

ALIGN(2,5,27)
FIELD CONADREXP1 OF IFCONTRAT &
        HIDDEN &
        REQUIRED &
        LABEL "Adresse de livraison:"
FIELD CONADREXP2 OF IFCONTRAT &
        REQUIRED &
        ID SAME &
        NOLABEL
FIELD CONADREXP3 OF IFCONTRAT &
        ID SAME &
        NOLABEL
FIELD CONCP      OF IFCONTRAT &
        ID SAME &
        NOLABEL
SKIP
TITLE "CAT DATE      QTE      QTE       QTE        QTE      PRIX     PRIX  NO REF" AT ,3
SKIP
TITLE "ST DEB/FIN   CMD M3  ASSIGNEE TRANSPORTE FACT/RECL   FOB      FAS   CLIENT" AT ,3

CLUSTER OCCURS WITH IFCONTRAT_LIGNE
ALIGN (2,3,5)(,,7)(,,16)(,,25)(,,35)(,,44)(,,53)(,,61)(,,70)
FIELD CATCLE OF IFCONTRAT_LIGNE &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP ON IFCATEGORIE &
          MESSAGE "E* Cette catégorie est inexistante..."
FIELD COEDATDEB OF IFCONTRAT_LIGNE &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFCONTRAT_LIGNE &
          VIA   CIECLE, &
                CLTCLE, &
                CRRCLENUM, &
                CRRCLEANN, &
                CATCLE, &
                COEDATDEB &
          USING CIECLE OF IFCONTRAT_LIGNE, &
                CLTCLE OF IFCONTRAT_LIGNE, &
                CRRCLENUM OF IFCONTRAT_LIGNE, &
                CRRCLEANN OF IFCONTRAT_LIGNE, &
                CATCLE OF IFCONTRAT_LIGNE, &
                COEDATDEB OF IFCONTRAT_LIGNE &
          MESSAGE "E* Cette ligne de contrat existe déjà..."
FIELD COEQTECMD OF IFCONTRAT_LIGNE
FIELD COEQTEPROD OF IFCONTRAT_LIGNE &
        DISPLAY ON ENTRY
FIELD COEQTEEXP OF IFCONTRAT_LIGNE &
        DISPLAY ON ENTRY
FIELD COEQTEFAC OF IFCONTRAT_LIGNE &
        DISPLAY
FIELD COEPRIXFOB OF IFCONTRAT_LIGNE
FIELD COEPRIXFAS OF IFCONTRAT_LIGNE
FIELD COEREFCLI OF IFCONTRAT_LIGNE
ALIGN(,,5)(,,7)(,,44)
FIELD COESTATUT OF IFCONTRAT_LIGNE
FIELD COEDATFIN OF IFCONTRAT_LIGNE
FIELD COEQTERECL OF IFCONTRAT_LIGNE
CLUSTER

PROCEDURE DETAIL DELETE
BEGIN
  IF     COEQTECMD OF IFCONTRAT_LIGNE = 0  &
     AND COEQTEPROD OF IFCONTRAT_LIGNE = 0
  THEN DELETE IFCONTRAT_LIGNE
  ELSE ERROR "*E* Destruction interdite car les qtés n'égale pas 0..."
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Gestion des contrats *****************************
*   No contrat..: xxxxx  Date: xxxxxxxx    Nom Client: xxxxxxxxxxxxxxxxxxxx    *
*   Nom granit..: xxxxxxxxxxxxxxx          Devise....: xxx                     *
*   Description.: xxxxxxxxxxxxxxxxxxxx                                         *
*                 xxxxxxxxxxxxxxxxxxxx    Conditions: xxxxxxxxxxxxxxxxxxxxxxxxx*
*                                                     xxxxxxxxxxxxxxxxxxxxxxxxx*
*   Adresse de livraison: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                       *
*                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                       *
*                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                       *
*                         xxxxxxx                                              *
* CAT DATE      QTE      QTE       QTE        QTE      PRIX     PRIX  NO REF   *
* ST DEB/FIN   CMD M3  ASSIGNEE TRANSPORTE FACT/RECL   FOB      FAS   CLIENT   *
*   x xxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxx xxxxxxxx xxxxxxx xxxxxxx  xxxxxxxxxx*
*   x xxxxxxxx                             xxxxxxxx                            *
*   x xxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxx xxxxxxxx xxxxxxx xxxxxxx  xxxxxxxxxx*
*   x xxxxxxxx                             xxxxxxxx                            *
*   x xxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxx xxxxxxxx xxxxxxx xxxxxxx  xxxxxxxxxx*
*   x xxxxxxxx                             xxxxxxxx                            *
*   x xxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxx xxxxxxxx xxxxxxx xxxxxxx  xxxxxxxxxx*
*   x xxxxxxxx                             xxxxxxxx                            *
********************************************************************************
@ENDIF
