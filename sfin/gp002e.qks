;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche de sous-projet - Information opération sommaire.
;
;/M/François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction pour unix.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp002e ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRO_ACTIVITE

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

TRANSACTION T_PAU_VISUEL COMMIT ON UPDATE &
                         READ WRITE &
                         RESERVING FOR SHARE WRITE GPPRA_BDG_UNT in dict

USE ussectmp NOLIST     ; Variable pour la securite
    "GP002E"            ; Nom de l'écran

USE gp002e.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier fiche de projet
;------------------------------------
FILE GPPRO_ACTIVITE         MASTER



;----------------------------
; Fichier budget d'opération
;----------------------------
FILE GPPRA_BDG_UNT          PRIMARY   &

     TRANSACTION T_PAU_VISUEL FOR PROCESS, QUERY, UPDATE &

                            OCCURS 15 &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE, &
                  UNTCLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE, &
                  UNTCLE OF GPPRA_BDG_UNT   &
          REQUEST UNTCLE &
          ORDERBY UNTCLE

  ACCESS  VIA     CIECLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   CIECLE OF GPPRO_ACTIVITE, &
                  PRJCLE OF GPPRO_ACTIVITE, &
                  PRACLE OF GPPRO_ACTIVITE  &
          ORDERBY UNTCLE


;-------------------------------
; Fichier unité de travail
;-------------------------------
FILE GPPROJETS              REFERENCE

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING CIECLE OF GPPRO_ACTIVITE, &
                PRJCLE OF GPPRO_ACTIVITE



;-------------------------------
; Fichier unité de travail
;-------------------------------
FILE GPUNITE_TRAVAIL        REFERENCE

  ACCESS  VIA   UNTCLE  &
          USING UNTCLE OF GPPRA_BDG_UNT



;-------------------------------
; Fichier unité de mesure
;-------------------------------
FILE GPUNITE_MESURE         REFERENCE

  ACCESS  VIA   UNMCLE  &
          USING UNMCLE OF GPUNITE_TRAVAIL


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPUNITE_TRAVAIL


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

DEFINE D_MNTBDG      FLOAT SIZE 8 = PAUBDG       OF GPPRA_BDG_UNT      &
                                 IF PRAPAUDATAPP OF GPPRO_ACTIVITE = 0 &
                               ELSE PAUBDGREV    OF GPPRA_BDG_UNT

DEFINE D_MNTDIS      FLOAT SIZE 8 = D_MNTBDG - PAUREE OF GPPRA_BDG_UNT


USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Information opération sommaire " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,28) (,93,104) (,,116)
FIELD PRJCLE    OF GPPRO_ACTIVITE                     &
label "Projet...:" &
      DISPLAY

FIELD PRJDSC1   OF GPPROJETS                          &
      DISPLAY

FIELD PRAPAUDATAPP OF GPPRO_ACTIVITE                   FORMAT YYYYMMDD &
PATTERN "####/##/##"&
      LABEL "Date app.:"                              &
      DISPLAY

FIELD PRAPAUUSRAPP OF GPPRO_ACTIVITE                  &
      DISPLAY

ALIGN (,3,19) (,,28)
FIELD PRACLE    OF GPPRO_ACTIVITE                     &
      DISPLAY

FIELD PRADSC1   OF GPPRO_ACTIVITE                     &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Unité de travail" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Compte  Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,36

TITLE &
@IF FRANCAIS
      "Budget" &
@ELSE
      " %%% " &
@ENDIF
      AT ,75

TITLE &
@IF FRANCAIS
      "Réel         Disponibilité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,99

SKIP

ALIGN (,,3) (,,9) (,,36) (,,44) (,,65) (,,83) (,,87) (,,105) (,,109) (,,127)

CLUSTER OCCURS WITH GPPRA_BDG_UNT

FIELD UNTCLE OF GPPRA_BDG_UNT                         &
      DISPLAY

FIELD UNTDSC OF GPUNITE_TRAVAIL                       &
      DISPLAY                                         &
      SIZE 25

FIELD CPTCLE OF GPUNITE_TRAVAIL                       &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD D_MNTBDG                                        &
      PICTURE "^^^,^^^,^^^.^^^^ "                     &
      TRAILING SIGN "-" SIGNIFICANCE 7 BWZ            &
      DISPLAY

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

FIELD PAUREE OF GPPRA_BDG_UNT                         &
      DISPLAY

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

FIELD D_MNTDIS                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^^^ "                     &
      TRAILING SIGN "-" SIGNIFICANCE 5

FIELD UNMABR OF GPUNITE_MESURE                        &
      DISPLAY                                         &
      PICTURE "/^^"

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

