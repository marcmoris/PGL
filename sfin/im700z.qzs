;/T/ Programme de transfert des amortissement au Grand Livre
;
;/P/ Programmeur..: Alain Côté
;    Date création: 24 février 1994
;
;    Description..: Impression des messages d'erreurs provenant du QTP qui
;                   précède.
;                   Impression de chaque écriture avec le détail par
;                   immobilisations
;
;/V3.02.00/ Alain Côté, 24 février 1994, Immobilisatioin
;
;/M/ Francois Richard, 1999-07-12
;
;    Ajustement pour le changement de siècle
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

;
; Impression des erreurs.
;
ACCESS *WIM700E

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Transfert des écritures d'amortissement au Grand Livre"
@ELSE
       "%%%Transfert des écritures d'amortissement au Grand Livre"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des erreurs"
@ELSE
       "%%%Liste des erreurs"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM700A"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


USE ustitstd NOLIST  ; Titre standard
 SKIP 2 &
TAB 003 "                                  Description" &
 SKIP &
TAB 003 "--------------------------------------------------------------------------------" &
 SKIP 2


NOREPORT

REPORT T_MESERR

BUILD im700za


;-------------------------------------------------------------------------------
;
;
ACCESS *WIM700I

;
; On sélectionne les débits pour éviter d'avoir les montants en double.
; Le sous-fichier contient une ligne débit et une ligne crédit, l'information
; sur l'immobilisation est la même dans les deux cas, voir le traitement du
; sous-fichier dans le QTP.
;
SELECT WIM700I IF T_CODDC = "D"

;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Transfert des écritures d'amortissement au Grand Livre"
@ELSE
       "%%%Transfert des écritures d'amortissement au Grand Livre"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM700B"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; Solde de l'immobilisation. Montant non amorti avant traitement moin
; l'amortissement de la période.
;
DEFINE D_SLDIMM   FLOAT SIZE 8 = IMIMNTNONAMT OF WIM700I - &
                                 IMIMNTAMT    OF WIM700I

; Ajustement pour le changement de siècle
DEFINE D_ECJCLENUM_TRI CHARACTER SIZE 10 = "1" + ECJCLENUM OF WIM700I &
     IF ECJCLENUM OF WIM700I[1:1] < "8" &
     ELSE "0" + ECJCLENUM OF WIM700I

SORT ON D_ECJCLENUM_TRI &
     ON IMMCLE    OF WIM700I


USE ustitstd NOLIST  ; Titre standard
  SKIP 2 &
TAB 001 &
@IF FRANCAIS
        "Lot au G/L....:" &
@ELSE
        "%%%Lot au G/L.:" &
@ENDIF
TAB 017 T_LGLCLE OF WIM700I &
 SKIP &
TAB 001 &
@IF FRANCAIS
        "Période.......:" &
@ELSE
        "Period........:" &
@ENDIF
TAB 017 G_PECCLE OF WIM700I &
          PICTURE "^^-^^" &
SKIP 2 &
TAB 001 &
@IF FRANCAIS
        "    Numéro        Numéro                                                    Montant" &
@ELSE
        "%%% Numéro        Numéro                                                    Montant" &
@ENDIF
 SKIP 1 &
TAB 001 &
@IF FRANCAIS
        "   écriture   Immobilisation                Description                    non amorti    Amortissement       Solde" &
@ELSE
        "%%%écriture   Immobilisation                Description                    non amorti    Amortissement       Solde" &
@ENDIF
  SKIP 1 &
TAB 001 "  ----------  --------------  ----------------------------------------   -------------   -------------   -------------" &
  SKIP 2


REPORT &
  TAB 003 ECJCLENUM OF WIM700I &
            PRINT AT D_ECJCLENUM_TRI &
  TAB 016 IMMCLE    OF WIM700I &
  TAB 031 IMMDSC    OF WIM700I &
  TAB 074 IMIMNTNONAMT OF WIM700I &
  TAB 090 IMIMNTAMT    OF WIM700I &
  TAB 106 D_SLDIMM &
            PICTURE "^^,^^^,^^^.^^" &
            SIGNIFICANCE 4

FOOTING AT D_ECJCLENUM_TRI &
  TAB 089 "--------------" &
 SKIP 1 &
  TAB 067 &
@IF FRANCAIS
          "Total de l'écriture:" &
@ELSE
          "%%%al de l'écriture:" &
@ENDIF
  TAB 089 IMIMNTAMT OF WIM700I &
            SUBTOTAL &
            PICTURE "^^^,^^^,^^^.^^" &
            SIGNIFICANCE 4 &
  SKIP 3 &
  RESERV 4

BUILD im700zb
@IF FORMAT

IM700A                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx            Transfert des écritures d'amortissement au Grand-Livre
                                                      Liste des erreurs

                                    Description
  --------------------------------------------------------------------------------
  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx



IM700B                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx            Transfert des écritures d'amortissement au Grand-Livre
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Lot au G/L....: xxxx
Période.......: xx-xx

    Numéro        Numéro                                                    Montant
   écriture   Immobilisation                Description                    non amorti    Amortissement       Solde
  ----------  --------------  ----------------------------------------   -------------   -------------   -------------
  xxxxxxxxxx   xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
                                                                                        --------------
                                                                  Total de l'écriture:  999,999,999.99

  xxxxxxxxxx   xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
               xxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx   xxxxxxxxxxxxx
                                                                                        --------------
                                                                  Total de l'écriture:  999,999,999.99
@ENDIF
