;/T/ Liste des produit en B/O (Commandes en souffrances)
;
;/P/ Programmeur...: Patrick Langlois
;    Date Création.: 5 Aôut 1999
;
;    Description...: Impression des commandes en souffrance 
;
;    Paramètres....: Compagnie
;                       Flag pour date ulterieure (Voir intervention 1999-10-14)
;
;/M/675/Marc Trudel/1999-10-14
;  Etat..........: Termin\351
;  Description...: GRA01
;                  On demande un param\350tre de plus pour savoir s'il veulent
;                  avoir les commandes en souffrances dont la date de livraison
;                  est ult\351rieure \340 la date du jour.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS ARCOMMANDE            &
  LINK ("----"             , &
       FOUCLE OF ARCOMMANDE) &
    TO FOUCIECLE           , &
       FOUCLE                    OF CPFOURNISSEUR OPT &    
  LINK CIECLE OF ARCOMMANDE, &
       CMDCLE OF ARCOMMANDE, &
       CMDAJT OF ARCOMMANDE  &
    TO CIECLE              , &
       CMDCLE              , &
       CMDAJT                    OF ARCMD_DETAIL 


CHOOSE CIECLE PARM, &    
       FOUCLE PARM

DEFINE D_FLGDATE CHARACTER SIZE 1 = PARM  ; Si "1" on veut les commandes en  
                                     ; souffrance dont la date de livraison
                                     ; prevue est  ulterieure a la date du jour.

DEFINE D_QTEBO FLOAT SIZE 8 = CDEQTECOM OF ARCMD_DETAIL - CDEQTEREC OF ARCMD_DETAIL

;
; Commande approuvé: aucune réception encore effectuée 
;
SELECT IF ( ( CMDSTU OF ARCOMMANDE = "A" OR CMDSTU OF ARCOMMANDE = "P") AND &
            CDEDATLIVPRE OF ARCMD_DETAIL < SYSDATE AND &
            D_FLGDATE = "0" ) OR &
          ( ( CMDSTU OF ARCOMMANDE = "A" OR CMDSTU OF ARCOMMANDE = "P") AND &
            D_FLGDATE = "1" )

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Listes des commandes en souffrance"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR349Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


SORT ON CIECLE OF ARCOMMANDE, &
        FOUCLE OF ARCOMMANDE, &
        CMDCLE OF ARCOMMANDE, &
        PRDCLE OF ARCMD_DETAIL

USE ustitstd NOLIST
  SKIP 2 &
TAB 001 "Numéro       Date      Date                                                            Quantité                     " &
  SKIP 1 &
TAB 001 "Commande   Commande  Liv. Prévu No produit   Description                               Commandée Fourn. Nom" &
  SKIP 1 &
TAB 001 "--------- ---------- ---------- ------------ ---------------------------------------- ---------- ------ ---------------------------" &
SKIP 

                                                             
REPORT                                 &
  TAB 001 CMDCLE       OF ARCOMMANDE   &
          PRINT AT CMDCLE              &
  TAB 011 CMDDAT       OF ARCOMMANDE   & 
          PRINT AT CMDCLE              &
  tab 022 CDEDATLIVPRE OF ARCMD_DETAIL & 
  TAB 033 PRDCLE       OF ARCMD_DETAIL &
  TAB 046 CDEDSCPRD    OF ARCMD_DETAIL &
  TAB 087 CDEQTECOM    OF ARCMD_DETAIL &
          PICTURE "^^,^^^.^^^ "        &
          TRAILING SIGN "-"            &
          SIGNIFICANCE 6               &
          BWZ                          &
  TAB 098 FOUCLE       OF ARCOMMANDE   &
  TAB 105 FOUNOM       OF CPFOURNISSEUR &
          PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^" 

  

BUILD ar349z


@IF FORMAT
Numéro       Date      Date                                                            Quantité                     
Commande   Commande  Liv. Prévu No produit   Description                               Commandée Fourn. Nom
--------- ---------- ---------- ------------ ---------------------------------------- ---------- ------ ---------------------------
xxxxxxxxx 9999/99/99 9999/99/99 xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 99,999.999 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxx
@ENDIF
