;/T/ Clients/compagnie - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 9 août 1993
;
;    Description..: Cet écran vérifie si le client-compagnie reçu en paramètre
;                   est référé dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr001ax NOMODE &
               ACTION LABEL " " &
               FROM 23,70 TO 23,80 &
               RECEIVING CRCLI_CIE , &
                         T_FLGDEL


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Clients - compagnie
;
FILE CRCLI_CIE        MASTER

;
; Comptes à recevoir.
;
FILE CRCPT_A_REC      REFERENCE

;
; Encaissement/mauvaise créance.
;
FILE CRDEPOT          REFERENCE

;
; Mémo sur le client.
;
FILE CRMEMO           REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans les comptes à recevoir.
  ;
  GET CRCPT_A_REC VIA CIECLE    , &
                      REFCIECLE , &
                      REFCLENUM   &
                USING CIECLE    OF CRCLI_CIE , &
                      CLICIECLE OF CRCLI_CIE , &
                      CLICLE    OF CRCLI_CIE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 723 ; Destruction annulée, cette information est référée par un
                 ; compte à recevoir.

  ;
  ; On vérifie dans les encaissements et les mauvaises créances.
  ;
  GET CRDEPOT VIA CIECLE    , &
                  REFCIECLE , &
                  REFCLENUM , &
                  REFCLETYP   &
              USING CIECLE    OF CRCLI_CIE , &
                    CLICIECLE OF CRCLI_CIE , &
                    CLICLE    OF CRCLI_CIE , &
                    "C"                      &
              OPTIONAL
  IF ACCESSOK
  THEN ERROR 724 ; Destruction annulée, cette information est référée par un
                 ; encaissement/mauvaise créance.

  ;
  ; On vérifie dans les mémos.
  ;
  GET CRMEMO VIA CIECLE    , &
                 CLICIECLE , &
                 CLICLE      &
             USING CIECLE    OF CRCLI_CIE , &
                   CLICIECLE OF CRCLI_CIE , &
                   CLICLE    OF CRCLI_CIE   &
             OPTIONAL
  IF ACCESSOK
  THEN ERROR 725 ; Destruction annulée, cette information est référée par un
                 ; mémo.
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
