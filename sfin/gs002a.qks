;/T/ Paramètre d'éxécution
;
;/P/ Prosig informatique inc.
;
;
;Cet écran sert a entrer les parametres qui sont saisis pour les rapports
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gs002a ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 7,9 TO 20,72 &
              RECEIVING GSPROGRAMME &
              FOR 15 ON 9

USE usactecr NOLIST
@IF FRANCAIS
  ACTIONMENU LABEL "Procédure"
     MENUITEM LABEL "Copy " ACTION DESIGNER COPY
@ELSE
  ACTIONMENU LABEL "%%%%"
     MENUITEM LABEL "Copy" ACTION DESIGNER COPY
@ENDIF

DEF DZNOMECRAN CHAR SIZE  7 = "GS002A"
DEF DZTITLE    CHAR SIZE 30 = "Paramètres d'éxécution"

FILE GSPROGRAMME MASTER

FILE GSPR_DETAIL PRIMARY OCCURS 11 NOITEM &
                 OPEN EACH LEVEL

  ACCESS VIAINDEX XPD_R01 USING XPRNOM OF GSPROGRAMME

  ITEM XPRNOM        OF GSPR_DETAIL INIT XPRNOM OF GSPROGRAMME
  ITEM XPDUSRMOD     OF GSPR_DETAIL INIT LOGONID FIXED
  ITEM XPDDATMOD     OF GSPR_DETAIL INIT SYSDATE FIXED

FILE GSPR_DETAIL DESIGNER ALIAS PROG_CODE_LIRE
FILE GSPR_DETAIL DESIGNER ALIAS PROG_CODE_COPY

TEMP T-PRG-NOM  CHAR SIZE 7   ; Pour la copie de PROG_CODE
TEMP T-NUMLIG   INT  SIZE 2 RESET AT STARTUP ; Pour incrémenter automatiquement
                                             ; les no de ligne
TEMP T-IND      INT  SIZE 2   ; Indice de la position du nom

;
; Longueur du nom de programme, il est de 6 si le tiret fait partie du nom
; sinon il est de 5(voir les normes de programmation)
;
DEFINE D-LONG INT SIZE 2 = 6 IF "-" = XPRNOM OF GSPROGRAMME[3:1] &
                      ELSE 5

;
;
DESCRIPTION OF SCREEN &
" " ,&
"************************************************************************* ",&
" NOTES EXPLICATIVES CONCERNANT LES NORMES POUR LA SAISIE DES PARAMETRES   ",&
"                                                                          ",&
"************************************************************************* ",&
"                                                                          ",&
" Définition des codes de saisie                                           ",&
"                                                                          ",&
" D_ = define optionel                                                     ",&
" DU = define obligatoire (unique)                                         ",&
" CU = choose obligatoire (unique)                                         ",&
" CO = choose obligatoire (un ou plusieur)                                 ",&
" C_ = choose optionel (all)                                               ",&
" CR = choose range (de...à)                                               ",&
"                                                                          ",&
" DESCRIPTION DE LA SYNTAXE DE LA TABLE GSPR_DETAIL (%)                        ",&
"                                                                          ",&
"         STRUCTURE : %C_EMPNO L=No employé. V=valeur1|valeur2|...|valeur5 ",&
"                      - ----- ------------- ----------------------------- ",&
"                      |   |       |                        |              ",&
" Code de saisie <-----'   |       |                        |              ",&
" Paramètre      <---------'       |                        |              ",&
" Libellé        <-----------------'                        |              ",&
" Restrictions   <------------------------------------------'              ",&
"                                                                          ",&
"************************************************************************* ",&
"  ",&
" INFORMATIONS COMPLEMENTAIRES A LA SAISIE DU PARAMETRE                    ",&
"  ",&
"     %__INFO  Message d'information au bas de l'ecran                     ",&
"              EX: %__INFO Retour pour continuer                           ",&
"                  affichera ** Retour pour continuer ** au bas de l'écran.",&
"                                                                          ",&
"     %__DISP  Message d'information en dessous du paramètre à saisir      ",&
"              EX: %__DISP Entrez un ou plusieurs numéros d'employes       ",&
"                  %C_EMPNO L=Numero de l'employé                          ",&
"              On obtient                                                  ",&
"                         ------------------------------------------------ ",&
"                         | Numero de l'employe : ________________________ ",&
"              message -> | Entrez un ou plusieurs numeros d'employes      ",&
"                         | Type de sélection...: xxxxxxxxx                ",&
"                         ------------------------------------------------ ",&
"    ",&
"************************************************************************* ",&
" NOTES : ",&
"       - Les 'underscore' cites representent des blancs dans GSPR_DETAIL.     ",&
" ",&
"       - Il n'est pas necessaire d'ajouter des '....' ou un ':' a la suite ",&
"         du LABEL a saisir car ceux-ci seront ajoutes automatiquement      ",&
"         lors de l'execution de l'ecran.                                   ",&
"  ",&
"       - On ne tient pas compte de l'ordre concernant V= et L= dans        ",&
"         GSPR_DETAIL.                                                          ",&
"                                                                           ",&
"       - Le libelle fourni par 'L=' vient remplacer celui issue de la      ",&
"         table XXELEMENT. Il est absolument necessaire de mettre un point ",&
"         '.' a la fin du libelle pour indiquer sa fin sinon le libelle     ",&
"         contiendra tous les autres caracteres de la ligne de GSPR_DETAIL.     ",&
"                   EX: %C_EMPNO L=No employé.                              ",&
"  ",&
"       - Avec l'utilisation de 'V=', on peut fournir un maximum de cinq    ",&
"         valeur possibles chacune etant separee par un '|'.                ",&
"  ",&
"       - L'utilisation de %__DISP et %__INFO n'affecte que le parametre    ",&
"         subsequent a la ligne du %__DISP ou %__INFO.                      ",&
"  ",&
"  ",&
"*************************************************************************"

USE ushilite NOLIST

DRAW 2,1 TO 15,80

@IF FRANCAIS
    TITLE " Paramètres d'Exécution " CENTERED AT 2,1
@ELSE
    TITLE " Execution Parameters " CENTERED AT 2,1
@ENDIF

SKIP

@IF FRANCAIS
    TITLE "Lg    Paramètre " AT ,3
@ELSE
    TITLE "Wd    Parameter " AT ,3
@ENDIF

ALIGN (2,2,3)(,,9)
CLUSTER OCCURS WITH GSPR_DETAIL
  FIELD XPDNUMLIG OF GSPR_DETAIL LABEL " " HIDDEN BWZ
  FIELD XPDCMD OF GSPR_DETAIL
CLUSTER
;
;
; Assigne un numéro de ligne automatiquement avec un incrément de 2.
;
;
PROCEDURE INPUT XPDNUMLIG
BEGIN
  IF FIELDTEXT = "" AND &
     NEWRECORD OF GSPR_DETAIL AND &
     XPDNUMLIG OF GSPR_DETAIL = 0
  THEN BEGIN
    LET FIELDTEXT = ASCII(T-NUMLIG + 2,2)
  END
END
;
;
; Conserve le numéro de ligne le plus élévé qui a été saisie
;
;
PROCEDURE PROCESS XPDNUMLIG
BEGIN
  IF T-NUMLIG < XPDNUMLIG OF GSPR_DETAIL
  THEN LET T-NUMLIG = XPDNUMLIG OF GSPR_DETAIL
END

;
;
; Sert à copier le code d'un autre programme
;
;
PROCEDURE DESIGNER COPY NODATA
BEGIN
  INFO  "Entrez le nom du programme d'origine:"
  PROMPT XPDCMD INTO T-PRG-NOM
  IF T-PRG-NOM = XPRNOM OF GSPROGRAMME
  THEN ERROR "*E Impossible de copier ce code "

  WHILE RETRIEVING PROG_CODE_LIRE VIA XPRNOM USING T-PRG-NOM ORDERBY XPDNUMLIG
  BEGIN
    LET XPRNOM    OF PROG_CODE_COPY = XPRNOM   OF GSPROGRAMME
    LET XPDNUMLIG  OF PROG_CODE_COPY = XPDNUMLIG OF PROG_CODE_LIRE
    LET XPDCMD  OF PROG_CODE_COPY = XPDCMD OF PROG_CODE_LIRE
    LET XPDUSRMOD OF PROG_CODE_COPY = LOGONID
    LET XPDDATMOD OF PROG_CODE_COPY = SYSDATE
    ;
    ; Recherche les EXE MMXXX et change la valeur par la nouvelle
    ;
    LET T-IND = IND(XPDCMD OF PROG_CODE_COPY, TRUNC(T-PRG-NOM[1:D-LONG]))
    IF 0 <> T-IND
    THEN BEGIN
      LET XPDCMD OF PROG_CODE_COPY = &
          XPDCMD OF PROG_CODE_COPY[1: T-IND - 1] + &
          TRUNC(XPRNOM OF GSPROGRAMME[1:D-LONG]) + &
          XPDCMD OF PROG_CODE_COPY[T-IND + &
            SIZE(TRUNC(T-PRG-NOM[1:D-LONG])) :70]
    END
    ;
    ; Recherche les DELETE W-XXXXX et change le nom du fichier
    ; a effacer par le nouveau
    ; Le nom à chercher doit être sans tiret.
    ;
    LET T-IND = IND(XPDCMD OF PROG_CODE_COPY, &
                    (T-PRG-NOM[1:2] + T-PRG-NOM[D-LONG - 2:3]))
    IF 0 <> T-IND
    THEN BEGIN
      LET XPDCMD OF PROG_CODE_COPY = &
          XPDCMD OF PROG_CODE_COPY[1: T-IND - 1] + &
          XPRNOM OF GSPROGRAMME[1:2] + &
          XPRNOM OF GSPROGRAMME[D-LONG - 2:3] + &
          XPDCMD OF PROG_CODE_COPY[T-IND + 5 :70]
    END
    PUT PROG_CODE_COPY RESET
  END
END

BUILD






