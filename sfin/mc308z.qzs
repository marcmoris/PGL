;/T/ Périodes comptables - Holding
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 13 mai 1993
;
;    Description...: Ce rapport présente la liste période comptable - holding.
;
;
;    Particularités:
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....


ACCESS MCPERIODE_CTB


DEFINE D_SELANNFIN CHARACTER SIZE 2 = PARM     ; Sélection selon une année financière (Une ou tous)


SELECT MCPERIODE_CTB IF &                      ; Sélection de la période (ou tous) sélectionné par l'usager
       0 = SIZE(TRUNC(D_SELANNFIN)) OR (D_SELANNFIN  = PECCLE OF MCPERIODE_CTB[1:2])


 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &        ; titre1  du rapport
@IF FRANCAIS
                "Périodes comptables - holding"
@ELSE
                "%%%Périodes comptables - holding"
@ENDIF
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "      ; titre2  du rapport
 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "MC308"  ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM     ; Nom de la compagnie


USE usflgdet NOLIST ; Code (O/N) bilingue

 DEFINE D_PECRGL CHARACTER SIZE 3 = &        ; Code indiquant si Régulisation(Oui/Non)
    DZ_FLGAFFO IF PECRGL OF MCPERIODE_CTB = "1" ELSE &
    DZ_FLGAFFN IF PECRGL OF MCPERIODE_CTB = "0" ELSE " "

 DEFINE D_ANNFIN CHARACTER SIZE 2 = PECCLE OF MCPERIODE_CTB[1:2] ;Année financière de la période

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF MCPERIODE_CTB &
         IF PECCLE OF MCPERIODE_CTB[1:1] < "8" &
         ELSE "0" + PECCLE OF MCPERIODE_CTB

; Ajustement pour le changement de siècle
DEFINE D_ANNFIN_SIE CHARACTER SIZE 3 = "1" + D_ANNFIN[1:2] &
         IF D_ANNFIN[1:1] < "8" &
         ELSE "0" + D_ANNFIN[1:2]

SORT ON  D_ANNFIN_SIE        , &          ; Année finacière
         D_PECCLE_SIE                 ; Période comptable


USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP 2 &
@IF FRANCAIS
  TAB 002 "                                                 -------G/L-------  -------C/R-------  -------C/P-------" &
SKIP &
  TAB 002 "                             Date       Date       Date               Date               Date           " &
SKIP &
  TAB 002 "Période   Régulisation       début      fin      fermeture  Statut  fermeture  Statut  fermeture  Statut" &
@ELSE
  TAB 002 "%%%                                              -------G/L-------  -------C/R-------  -------C/P-------" &
SKIP &
  TAB 002 "%%%                          Date       Date       Date               Date               Date           " &
SKIP &
  TAB 002 "%%%iode   Régulisation       début      fin      fermeture  Statut  fermeture  Statut  fermeture  Statut" &
@ENDIF
SKIP &
  TAB 002 "-------   --------------   --------   --------   ---------  ------  ---------  ------  ---------  ------" &
SKIP



REPORT &
  TAB 003 PECCLE    OF MCPERIODE_CTB &
  TAB 018 D_PECRGL                   &
  TAB 029 PECDATDEB OF MCPERIODE_CTB &
  TAB 040 PECDATFIN OF MCPERIODE_CTB &
  TAB 051 PECDATFERGL OF MCPERIODE_CTB &
  TAB 064 PECSTUGL    OF MCPERIODE_CTB &
  TAB 051 PECDATFERCR OF MCPERIODE_CTB &
  TAB 064 PECSTUCR    OF MCPERIODE_CTB &
  TAB 051 PECDATFERCP OF MCPERIODE_CTB &
  TAB 064 PECSTUCP    OF MCPERIODE_CTB



FOOTING AT D_ANNFIN_SIE &
SKIP PAGE



BUILD mc308z

;                                                                                                   1         1         1         1
;         1         2         3         4         5         6         7         8         9         0         1         2         3
;123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
;MC308                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
;99/99/99 HH:MM xxxxxxxxxxxx                         Périodes comptables - holding
;                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
;
;                                                  -------G/L-------  -------C/R-------  -------C/P-------
;                              Date       Date       Date               Date               Date
; Période   Régulisation       début      fin      fermeture  Statut  fermeture  Statut  fermeture  Statut
; -------   --------------   --------   --------   ---------  ------  ---------  ------  ---------  ------
;  xxxxx         xxx         xxxxxxxx   xxxxxxxx   xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x
;  xxxxx         xxx         xxxxxxxx   xxxxxxxx   xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x
;  xxxxx         xxx         xxxxxxxx   xxxxxxxx   xxxxxxxx     x     xxxxxxxx     x     xxxxxxxx     x



