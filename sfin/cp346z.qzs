;/T/ Listes des chèques conciliés
;
;/P/ Programmeur...: Chantal Martineau / Adapté pour PGL par Thomas BRENNEUR
;    Date Création.: 12 Aout 1993
;
;    Description...: Consultation des chèques conciliés
;
;    Paramètres....:
;
;-------------------------------------------------------------------------------
USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....

;
; Lecture des chèques conciliés.
;
ACCESS  CPCCL_DETAIL &

  ;
  ; Lecture de la description du compte d'encaisse et de sa devise
  ;
  LINK  LBQCPTENC OF CPCCL_DETAIL &
    TO  CPTCLE                      OF VCPT_DSC &

  ;
  ; On retrouve les informations du chèque
  ;
  LINK  (CIECLE   OF CPCCL_DETAIL, &
        LBQCPTENC OF CPCCL_DETAIL, &
        PAMCLE    OF CPCCL_DETAIL, &
        "CH"                     , &
        "0")                      &
    TO  CIECLE   , &
        LBQCPTENC, &
        PAMCLE   , &
        PAMTYPECR, &
        PAMCODANN                   OF CPPAIEMENT &

  ;
  ; Lecture de la référence du paiement.
  ;

  LINK  REFCIECLE OF CPPAIEMENT, &
        REFCLETYP OF CPPAIEMENT, &
        REFCLENUM OF CPPAIEMENT, &
        RADCLE    OF CPPAIEMENT  &
    TO  REFCIECLE, &
        REFCLETYP, &
        REFCLENUM, &
        RADCLE                      OF VRAD_01 &

  ;
  ; On retrouve l'écriture du chèque au grand livre, pour aller chercher
  ; la ligne contenant le compte de banque. Ceci permet de retrouver le
  ; montant après conversion si le chèque était en devise.
  ;

  LINK  HISCLE    OF CPPAIEMENT, &
        LBQCPTENC OF CPPAIEMENT  &
    TO  HISCLE, &
        CPTCLE                      OF MCHIS_ECR &

  LINK  HISCLE    OF MCHIS_ECR &
    TO  HISCLE                      OF MCHISTO


CHOOSE  CIECLE    PARM       , &  ; Compagnie du menu
        LBQCPTENC PARM       , &  ; Compte d'encaisse
        CPDDAT    PARM RANGE      ; Date de conciliation

SORT ON CIECLE    OF CPCCL_DETAIL, &
        LBQCPTENC OF CPCCL_DETAIL, &
        PAMNUMPIM OF CPPAIEMENT

;-------------------------------------------------------------------------------

;
; Titre du rapport
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
        "Liste des chèques concilié"
@ELSE
        "List of reconciled cheques"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "

;
; Numéro du rapport
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP346Z"

;
; numéro du rapport
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; On affiche la devise du chèque seulement si elle est differente de la
; devise du grand livre
;
DEFINE  D_DEVCLE CHARACTER SIZE 4 &
        = "/" &
        + DEVCLE OF VRAD_01 &
          IF DEVCLE OF VRAD_01 <> DEVCLE OF MCHISTO


;-------------------------------------------------------------------------------

USE ustitstd NOLIST  ; Titre standard pour l'application
  SKIP 2 &
@IF FRANCAIS
    TAB 001 "Compte Encaisse :" &
@ELSE
    TAB 001 "%%%pte Encaisse :" &
@ENDIF
    TAB 019 LBQCPTENC OF CPCCL_DETAIL &
    TAB 026 CPTDSC    OF VCPT_DSC     &
@IF FRANCAIS
    TAB 067 "Devise :" &
@ELSE
    TAB 067 "%%%ise :" &
@ENDIF
    TAB 076 DEVCLE OF VCPT_DSC &
  SKIP 2 &
@IF FRANCAIS
    TAB 001 "  No           No                                                            Date                          Montant" &
@ELSE
    TAB 001 "%%%o           No                                                            Date                          Montant" &
@ENDIF
  SKIP &
@IF FRANCAIS
    TAB 001 "chèque   chèque pré-impr.                Employé / Fournisseur              Concil.        Montant         Devise G/L" &
@ELSE
    TAB 001 "%%%que   chèque pré-impr.                Employé / Fournisseur              Concil.        Montant         Devise G/L" &
@ENDIF
  SKIP &
    TAB 001 "-------  ----------------  -----------------------------------------------  --------  -----------------  --------------"

REPORT &
    TAB 001 PAMCLE    OF CPPAIEMENT   &
    TAB 014 PAMNUMPIM OF CPPAIEMENT   &
    TAB 028 REFCLENUM OF CPPAIEMENT   &
    TAB 035 RADNOM    OF VRAD_01      &
    TAB 077 CPDDAT    OF CPCCL_DETAIL &
    TAB 087 PAMMNT    OF CPPAIEMENT   &
    TAB 100 D_DEVCLE                  &
    TAB 106 HECMNTCRT OF MCHIS_ECR    PICTURE "^^^,^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5

FOOTING AT LBQCPTENC &
    TAB 001 " " &
  SKIP &
    TAB 106 "==============" &
  SKIP &
    TAB 106 HECMNTCRT OF MCHIS_ECR    SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING "-" SIGNIFICANCE 5 &
  SKIP &
    TAB 106 "--------------" &
  SKIP PAGE

BUILD cp346z
@IF FORMAT

CP346                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                        Liste des chèques conciliés
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Compte Encaisse : 123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Devise : xxx

  No           No                                                            Date                          Montant
chèque   chèque pré-impr.                Employé / Fournisseur              Concil.        Montant         Devise G/L
-------  ----------------  -----------------------------------------------  --------  -----------------  --------------
1234567      12345678      123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99,999,999.99/xxx  999,999,999.99
1234567      12345678      123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99,999,999.99/xxx  999,999,999.99
1234567      12345678      123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99,999,999.99/xxx  999,999,999.99
1234567      12345678      123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99,999,999.99/xxx  999,999,999.99
1234567      12345678      123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/99  99,999,999.99/xxx  999,999,999.99

                                                                                                         ==============
                                                                                                         999,999,999.99
                                                                                                         --------------

@ENDIF
