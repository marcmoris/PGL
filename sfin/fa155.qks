;/T/ Facturation
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 2 mai 1995
;
;    Description..: Permet la saisie les paramètres de passerelle

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN fa155 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM


USE fa155.hlp NOLIST

USE ussectmp NOLIST     ; Variable pour la sécurité
"FA155"                 ; Nom de l'écran

USE usactecr  NOLIST

USE ustrasse NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 ; Numéro de la compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie

DEFINE    DZ_CIECLE   CHARACTER SIZE 04 = T_CIECLEMNU
DEFINE    DZ_CIENOM   CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FGNCLE    CHARACTER SIZE 2 RESET AT MODE
;-------------------------------------------------------------------------------
;
; Table primaire.
;
FILE FAFAR_IMP_GEN      PRIMARY

  ACCESS VIA     CIECLE       &
         USING   T_CIECLEMNU

  ITEM CIECLE OF FAFAR_IMP_GEN   INITIAL T_CIECLEMNU


;
; Validation de l'unité administrative.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_REVENU

  ACCESS VIA CIECLE, &
             UNACLE &
         USING T_CIECLEMNU           , &
               FIGUNAREV OF FAFAR_IMP_GEN


;
; Validation de l'unité administrative.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_ESCOMPTE

  ACCESS VIA CIECLE, &
             UNACLE &
         USING T_CIECLEMNU           , &
               FIGUNAESC OF FAFAR_IMP_GEN


;
; Validation de l'unité administrative.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_PAIEMENT

  ACCESS VIA CIECLE, &
             UNACLE &
         USING T_CIECLEMNU           , &
               FIGUNAPAI OF FAFAR_IMP_GEN


;
; Validation du projet.
;
FILE GPPROJETS         REFERENCE

  ACCESS VIA CIECLE, &
             PRJCLE  &
         USING T_CIECLEMNU           , &
               PRJCLE OF FAFAR_IMP_GEN

;
; Validation de l'activité.
;
FILE GPPRO_ACTIVITE       REFERENCE
  ACCESS VIA CIECLE, &
             PRJCLE, &
             PRACLE  &
         USING T_CIECLEMNU            , &
               PRJCLE OF FAFAR_IMP_GEN, &
               PRACLE OF FAFAR_IMP_GEN


;
; Valide l'immobilisation et affiche la description.
;
TEMPORARY T_IMMCLE CHARACTER SIZE 12 ; Popup sur les immobilisations.

FILE IMIMMOBILISATION REFERENCE

  ACCESS  VIA   CIECLE, &
                IMMCLE &
          USING T_CIECLEMNU            , &
                IMMCLE OF FAFAR_IMP_GEN

  SELECT IF IMMSTU OF IMIMMOBILISATION = "A"


;
; Valide l'immobilisation et affiche la description.
;
FILE IMIMMOBILISATION REFERENCE &
                      ALIAS IMIMMOBILISATION2

  ACCESS  VIA   CIECLE, &
                IMMCLE &
          USING T_CIECLEMNU            , &
                FIGIMMCLE OF FAFAR_IMP_GEN

  SELECT IF IMMSTU OF IMIMMOBILISATION2 = "A"

;
; Validation du numéro de client et on a besoin de certaines informations sur
; le client , comme les termes d'encaissements, le statut, le type...
;
FILE VCLC_CLI         REFERENCE
  ACCESS VIA CLICIECLE, &
             CLICLE   , &
             CIECLE     &
         USING "----"                    , &
               FIGCLICLE OF FAFAR_IMP_GEN, &
               T_CIECLEMNU


FILE FAFAR_IMP_GEN      DESIGNER &
                        ALIAS FAFAR_IMP_GEN2 &
                        NOITEMS



TEMPORARY T_PRJCLE    CHARACTER SIZE 8 ; Popup sur les PRJjets.
TEMPORARY T_PRJSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.
TEMPORARY T_PRACLE    CHARACTER SIZE 3 ; Popup sur les activités.
TEMPORARY T_PRASTUPAT CHARACTER SIZE 5 ; Popup sur les activités.
TEMPORARY T_CLICIECLE CHARACTER SIZE 4 ; Popup sur les Clients
TEMPORARY T_CLICLE    CHARACTER SIZE 6 ; Popup sur les clients
TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur les unités adm.


USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

TITLE &
@IF FRANCAIS
      " Paramètres de passerelle " &
@ELSE
      " %%%amètres de passerelle " &
@ENDIF
      CENTERED AT 3,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

HILITE TITLE UNDERLINE

TITLE &
@IF FRANCAIS
    "Imputation de paiement" &
@ELSE
    "Imputation de paiement" &
@ENDIF
  AT ,3


SKIP

ALIGN (3,3,19) (,,32)

FIELD PRJCLE       OF FAFAR_IMP_GEN &
      HIDDEN               &
      REQUIRED             &
      LOOKUP ON GPPROJETS                             &
            VIA CIECLE,                               &
                PRJCLE                                &
          USING T_CIECLEMNU,                          &
                PRJCLE OF FAFAR_IMP_GEN               &
        MESSAGE 2731

FIELD PRJDSC1      OF GPPROJETS &
      DISPLAY SIZE 40

FIELD PRACLE       OF FAFAR_IMP_GEN &
      HIDDEN               &
      REQUIRED             &
      LOOKUP ON GPPRO_ACTIVITE                        &
            VIA CIECLE,                               &
                PRJCLE,                               &
                PRACLE                                &
          USING T_CIECLEMNU,                          &
                PRJCLE OF FAFAR_IMP_GEN,              &
                PRACLE OF FAFAR_IMP_GEN               &
        MESSAGE 2732

FIELD PRADSC1      OF GPPRO_ACTIVITE &
      DISPLAY SIZE 40

FIELD IMMCLE       OF FAFAR_IMP_GEN &
        HIDDEN                     &
        REQUIRED                   &
        LOOKUP ON IMIMMOBILISATION &
            MESSAGE 491 ; Cette immobilisation n'existe pas ou est inactive.

FIELD IMMDSC       OF IMIMMOBILISATION &
      DISPLAY

FIELD FIGUNAPAI    OF FAFAR_IMP_GEN &
      HIDDEN &
      REQUIRED &
      LABEL "Unité adm.....:" &
      LOOKUP ON A_UNA_PAIEMENT &
          MESSAGE 1317 ; Cette unité administrative n'existe pas.

FIELD UNANOM       OF A_UNA_PAIEMENT &
      DISPLAY

SKIP 2

TITLE &
@IF FRANCAIS
    "Imputation de revenu" &
@ELSE
    "Imputation de revenu" &
@ENDIF
  AT ,3

SKIP

FIELD FIGUNAREV    OF FAFAR_IMP_GEN &
      HIDDEN &
      REQUIRED &
      LABEL "Unité adm.....:" &
      LOOKUP ON A_UNA_REVENU &
          MESSAGE 1317 ; Cette unité administrative n'existe pas.

FIELD UNANOM        OF A_UNA_REVENU &
      DISPLAY


FIELD FIGIMMCLE     OF FAFAR_IMP_GEN &
        HIDDEN                     &
        REQUIRED                   &
        LOOKUP ON IMIMMOBILISATION2 &
            MESSAGE 491 ; Cette immobilisation n'existe pas ou est inactive.

FIELD IMMDSC       OF IMIMMOBILISATION2 &
      DISPLAY


SKIP 2

TITLE &
@IF FRANCAIS
    "Imputation d'escompte" &
@ELSE
    "Imputation d'escompte" &
@ENDIF
  AT ,3

SKIP

FIELD FIGUNAESC    OF FAFAR_IMP_GEN &
      HIDDEN &
      REQUIRED &
      LABEL "Unité adm.....:" &
      LOOKUP ON A_UNA_ESCOMPTE &
          MESSAGE 1317 ; Cette unité administrative n'existe pas.

FIELD UNANOM        OF A_UNA_ESCOMPTE &
      DISPLAY

SKIP 2

FIELD FIGCLICLE    OF FAFAR_IMP_GEN &
        HIDDEN &
        REQUIRED &
        LOOKUP ON VCLC_CLI &
          MESSAGE 624  ; Ce client n'existe pas.

FIELD CLINOM       OF VCLC_CLI &
      DISPLAY

;-------------------------------------------------------------------------------
;
; Popup sur les client.
;
PROCEDURE INPUT FIGCLICLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CLICIECLE = "----"
    LET T_CLICLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN cr102 MODE F PASSING T_CLICIECLE, T_CIECLEMNU, T_CLICLE
    LET FIELDTEXT = TRUNC(T_CLICLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation du client.
;
;
PROCEDURE EDIT FIGCLICLE
BEGIN
  IF CLISTU OF VCLC_CLI = "I"
  THEN ERROR 632 ; Le client est inactif.
END

;-------------------------------------------------------------------------------
;
; Validation du projet, le projet est optionnel dans la ligne de commande.
;
PROCEDURE INPUT PRJCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJSTUPAT = "A"
    LET T_PRJCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp104 MODE F PASSING T_CIECLEMNU, &
                                    T_PRJCLE   , &
                                    T_PRJSTUPAT WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PRJCLE)
  END
END


;-------------------------------------------------------------------------------
;
; Validation du projet, le projet est optionnel dans la ligne de commande.
;
PROCEDURE EDIT PRJCLE
BEGIN
  IF FIELDTEXT <> " "
  THEN BEGIN
       IF PRJSTU OF GPPROJETS <> "A"
       THEN ERROR 1386 ; Ce projet n'est pas actif.
  END
END

;----------------------------------------------------------------
;
; Popup sur les activités.
;
PROCEDURE INPUT PRACLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJCLE    = PRJCLE OF FAFAR_IMP_GEN
    LET T_PRASTUPAT = "A"
    LET T_PRACLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gp108 MODE F PASSING T_CIECLEMNU, &
                                    T_PRJCLE   , &
                                    T_PRACLE   , &
                                    T_PRASTUPAT WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PRACLE)
  END
END


;-------------------------------------------------------------------------------
;
; Validation de l'activité, l'activité est optionnelle dans la ligne de réquisition.
;
PROCEDURE EDIT PRACLE
BEGIN
  IF FIELDTEXT <> " "
  THEN BEGIN
       IF PRASTU OF GPPRO_ACTIVITE <> "A"
       THEN ERROR 1388 ; Cette activité est inactive.
  END
END

;-------------------------------------------------------------------------------
; Pop up pour la catégorie d'équipement
;
PROCEDURE INPUT IMMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IMMCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN im102 MODE F &
                     PASSING T_CIECLEMNU , &
                             T_IMMCLE  WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_IMMCLE )
  END
END

PROCEDURE EDIT IMMCLE
BEGIN
   IF IMMTYP OF IMIMMOBILISATION <> "E" AND &
      FIELDTEXT <> " "
   THEN ERROR 1503
END

;-------------------------------------------------------------------------------
; Pop up pour la catégorie d'équipement
;
PROCEDURE INPUT FIGIMMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_IMMCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN im102 MODE F &
                     PASSING T_CIECLEMNU , &
                             T_IMMCLE  WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE( T_IMMCLE )
  END
END

PROCEDURE EDIT FIGIMMCLE
BEGIN
   IF IMMTYP OF IMIMMOBILISATION2 <> "E" AND &
      FIELDTEXT <> " "
   THEN ERROR 1503
END

;-------------------------------------------------------------------------------
;
; Dépisteur de l'unité administrative et obligation du lookup.
;
PROCEDURE INPUT FIGUNAPAI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
END

;-------------------------------------------------------------------------------
;
; Dépisteur de l'unité administrative et obligation du lookup.
;
PROCEDURE INPUT FIGUNAREV
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
END

;-------------------------------------------------------------------------------
;
; Dépisteur de l'unité administrative et obligation du lookup.
;
PROCEDURE INPUT FIGUNAESC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLEMNU, T_UNACLE WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_UNACLE)
  END
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE PREENTRY
BEGIN
   GET FAFAR_IMP_GEN2 &
       VIA   CIECLE &
       USING T_CIECLEMNU OPTIONAL
   IF ACCESSOK
   THEN ERROR 2745
END


PROCEDURE PREUPDATE
BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF FAFAR_IMP_GEN  &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF FAFAR_IMP_GEN  &
       AND NOT NEWRECORD     OF FAFAR_IMP_GEN  &
       AND NOT DELETEDRECORD OF FAFAR_IMP_GEN  &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

END


BUILD LIST DETAIL
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
*************************** Paramètres de passerelle ***************************
*                                                                              *
* Imputation de paiement                                                       *
* Projet........: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Sous-projet...: xxx          xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Immobilisation: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Unité adm.....: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
*                                                                              *
* Imputation de revenu                                                         *
* Unité adm.....: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
* Immobilisation: xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
*                                                                              *
* Imputation d'escompte                                                        *
* Unité adm.....: xxxxxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
*                                                                              *
* Client........: xxxxxx       xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
*                                                                              *
********************************************************************************
@ENDIF
