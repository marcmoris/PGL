;/T/ Gestion des institutions bancaires
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 6 juille 1993
;
;    Description..: Cet écran permet la gestion des institutions bancaire
;                   servant aux C/P et à la paie.
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc007 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIENOM

USE ussectmp NOLIST
    "MC007"

USE mc007.hlp NOLIST

USE usactecr NOLIST


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie holding

;-------------------------------------------------------------------------------
;
;
FILE MCINST_BANCAIRE  PRIMARY &
                      OCCURS 17
  ACCESS REQUEST INBCLENUM OF MCINST_BANCAIRE &
         VIA     INBCLENUM  &
         USING   INBCLENUM OF MCINST_BANCAIRE &
         ORDERED
  ACCESS SEQUENTIAL &
         ORDERBY INBCLENUM


;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Institutions bancaires " &
@ELSE
      " Banking institution "   &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP 1

TITLE &
@IF FRANCAIS
  "Numéro   Nom de l'institution                      Nom abrégé" &
@ELSE
  "Number   Institution name                          Abreviate name" &
@ENDIF
  AT ,4

SKIP

ALIGN (6,3,6) (,,13) (,,55)

CLUSTER OCCURS WITH MCINST_BANCAIRE ID BASE 10

FIELD INBCLENUM OF MCINST_BANCAIRE HIDDEN LABEL " " &
        REQUIRED &
        NOCHANGE &
        LOOKUP NOTON MCINST_BANCAIRE &
          VIA INBCLENUM &
          USING INBCLENUM OF MCINST_BANCAIRE &
          MESSAGE 115   ;Cette Combinaison Numéro succursale & No transit exite déjà

FIELD INBNOM    OF MCINST_BANCAIRE &
        REQUIRED

FIELD INBNOMABR OF MCINST_BANCAIRE &
        DEFAULT INBNOM

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR MCINST_BANCAIRE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF MCINST_BANCAIRE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF MCINST_BANCAIRE &
       AND NOT NEWRECORD     OF MCINST_BANCAIRE &
       AND NOT DELETEDRECORD OF MCINST_BANCAIRE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction d'une institutions bancaire.
    ;
    IF DELETEDRECORD OF MCINST_BANCAIRE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si l'institution est référée.
      ;
      RUN SCREEN mc007x MODE SAME &
                        PASSING MCINST_BANCAIRE , &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Institutions bancaires ****************************
*                                                                              *
*  Numéro   Nom de l'institution                      Nom abrégé               *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
*    xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx     *
********************************************************************************
@ENDIF
