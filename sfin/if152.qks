;/T/ Qualité de granit
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 17 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base   
;                   concernant la qualité de granit  
;
;
SCREEN if152 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF152"

;USE if152.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFQUALITE    IN DICT PRIMARY NOITEM &
                      OCCURS 16 TIMES

  ACCESS VIA     CIECLE,   &
                 QLTCLE    &
         USING   T_CIECLE, &
                 QLTCLE    &  
         REQUEST QLTCLE    &
         ORDERBY QLTCLE  

  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY QLTCLE

  ITEM CIECLE OF IFQUALITE INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1 
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Qualité de granit " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST


TITLE "Qualité" AT ,5
TITLE "Desc. francaise" AT ,18
TITLE "Desc. anglaise" AT ,46
SKIP

ALIGN(5,6,7) (,,18) (,,46)
CLUSTER OCCURS WITH IFQUALITE
FIELD QLTCLE OF IFQUALITE &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFQUALITE &
          VIA   CIECLE, &
                QLTCLE  &
          USING T_CIECLE, &
                QLTCLE OF IFQUALITE &
          MESSAGE 3237 ; PD337 Cette qualité existe déjà

FIELD QLTDSCFRA OF IFQUALITE &
      REQUIRED

FIELD QLTDSCANG OF IFQUALITE 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Valeur par defaut pour la description anglaise
;
PROCEDURE INPUT QLTDSCANG
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = QLTDSCFRA OF IFQUALITE
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un code de qualite
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN if152x MODE SAME &
                   PASSING IFQUALITE, &
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3245 ; PD345 Impossible de supprimer cette qualité,
                     ; elle est d\351j\340 utilis\351e
   ELSE DELETE IFQUALITE
END
BUILD
