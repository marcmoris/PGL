;/T/ 2.4.3 Enregistrer les opérations de surface (dét. opér. surf. produit dimension)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-28
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les opérations de surface pour les produits de dimension.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd185d ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD185D"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd185d.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4   ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_QRTCODQRT CHARACTER SIZE 16 = "QRTCODQRT"
TEMPORARY T_QRTCODQRT CHARACTER SIZE 04
DEFINE    D_OSUSTU       CHARACTER SIZE 16 = "OSUSTU"
TEMPORARY T_OSUSTU       CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_SILENT           CHARACTER SIZE 1


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum des pièces de granit
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Opération de surface
;

FILE PDOPERATION_SURF  MASTER


;-------------------------------------------------------------------------------
;
; Détail opération surface
;

FILE PDDET_OPER_SURF PRIMARY &
                     NOITEM  &
                     OCCURS 7

  ACCESS VIA     CIECLE,          &
                 OSUNUMRAP,       &
                 TPDTYPPRD        &
         USING   T_CIECLEMNU ,                         &
                 OSUNUMRAP        OF PDOPERATION_SURF, &
                 "PD"                                  &
         ORDERBY CIECLE,          &
                 OSUNUMRAP,       &
                 DOSLON,          &
                 DOSHAU,          &
                 DOSEPA,          &
                 TGRCODGRA,       &
                 DOSCODFIN001


  ITEM CIECLE           OF PDDET_OPER_SURF INITIAL T_CIECLEMNU
  ITEM OSUNUMRAP        OF PDDET_OPER_SURF INITIAL OSUNUMRAP        OF PDOPERATION_SURF
  ITEM TPDTYPPRD        OF PDDET_OPER_SURF INITIAL "PD"
  ITEM DOSSTU           OF PDDET_OPER_SURF INITIAL "E" ; En cours


;-------------------------------------------------------------------------------
;
; Produit dimension
;

FILE PDPROD_DIMENSION      REFERENCE &
                           ALIAS  PDI_AVANT &
                           OCCURS WITH FILE PDDET_OPER_SURF

  ACCESS VIA     CIECLE,          &
                 PDILON,          &
                 PDIHAU,          &
                 PDIEPA,          &
                 TGRCODGRA,       &
                 TYFCODFIN        &
         USING   T_CIECLEMNU,                         &
                 DOSLON           OF PDDET_OPER_SURF, &
                 DOSHAU           OF PDDET_OPER_SURF, &
                 DOSEPA           OF PDDET_OPER_SURF, &
                 TGRCODGRA        OF PDDET_OPER_SURF, &
                 DOSCODFIN001     OF PDDET_OPER_SURF


  ITEM DOSLON           OF PDDET_OPER_SURF INITIAL PDILON           OF PDI_AVANT
  ITEM DOSHAU           OF PDDET_OPER_SURF INITIAL PDIHAU           OF PDI_AVANT
  ITEM DOSEPA           OF PDDET_OPER_SURF INITIAL PDIEPA           OF PDI_AVANT
  ITEM TGRCODGRA        OF PDDET_OPER_SURF INITIAL TGRCODGRA        OF PDI_AVANT
  ITEM DOSCODFIN001     OF PDDET_OPER_SURF INITIAL TYFCODFIN        OF PDI_AVANT


;-------------------------------------------------------------------------------
;
; Produit dimension (avec le nouveau fini)
;

FILE PDPROD_DIMENSION      DESIGNER  ALIAS  PDI_APRES

  ACCESS VIA     CIECLE,          &
                 PDILON,          &
                 PDIHAU,          &
                 PDIEPA,          &
                 TGRCODGRA,       &
                 TYFCODFIN        &
         USING   T_CIECLEMNU,                         &
                 DOSLON           OF PDDET_OPER_SURF, &
                 DOSHAU           OF PDDET_OPER_SURF, &
                 DOSEPA           OF PDDET_OPER_SURF, &
                 TGRCODGRA        OF PDDET_OPER_SURF, &
                 DOSCODFIN001     OF PDDET_OPER_SURF

  ITEM PDILON           OF PDI_APRES FINAL PDILON           OF PDI_AVANT
  ITEM PDIHAU           OF PDI_APRES FINAL PDIHAU           OF PDI_AVANT
  ITEM PDIEPA           OF PDI_APRES FINAL PDIEPA           OF PDI_AVANT
  ITEM PDIQTE           OF PDI_APRES FINAL PDIQTE           OF PDI_AVANT
  ITEM TGRCODGRA        OF PDI_APRES FINAL TGRCODGRA        OF PDI_AVANT
  ITEM PDITYPPRD        OF PDI_APRES FINAL "PD"

  ITEM DOSCODFIN002     OF PDDET_OPER_SURF FINAL   TYFCODFIN        OF PDI_APRES


;-------------------------------------------------------------------------------
;
; Machine opération de surface
;

FILE PDMACH_OP_SURF  REFERENCE

  ACCESS VIA     CIECLE,          &
                 MOPCODMAC        &
         USING   T_CIECLEMNU,     &
                 MOPCODMAC        OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_OSUSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_OSUSTU,                             &
               OSUSTU              OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Code bilingue (quart de travail)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_QRTCODQRT

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_QRTCODQRT,                          &
               QRTCODQRT        OF PDOPERATION_SURF


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI   REFERENCE ALIAS TYF_FINI_APRES

  ACCESS VIA     CIECLE,          &
                 TYFCODFIN        &
         USING   T_CIECLEMNU,     &
                 TYFCODFIN        OF PDI_APRES


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Opération de surface " &
@ELSE
      " %%%Opération de surface " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19) (,42,58)


FIELD OSUNUMRAP        OF PDOPERATION_SURF &
LABEL "Numéro rapport:"&
        FIXED


FIELD OSUDEP           OF PDOPERATION_SURF &
LABEL "Heure départ..:"&
        FIXED


FIELD OSUDATPRD        OF PDOPERATION_SURF  FORMAT YYYYMMDD &
PATTERN "####/##/##"&
LABEL "Date oper surf:"&
        FIXED


FIELD OSUARR           OF PDOPERATION_SURF &
LABEL "Heure arrêt...:"&
        FIXED


ALIGN (,3,19) (,,24)


FIELD MOPCODMAC        OF PDOPERATION_SURF &
LABEL "Code machine..:"&
        FIXED


FIELD MOPDSC001        OF PDMACH_OP_SURF   &
        FIXED


FIELD QRTCODQRT        OF PDOPERATION_SURF &
LABEL "Quart travail.:"&
        FIXED


FIELD CBIDSC           OF A_CBI_QRTCODQRT  &
        DISPLAY                            &
        FIXED


FIELD OSUSTU           OF PDOPERATION_SURF &
LABEL "Statut.......:"&
        FIXED


FIELD CBIDSC           OF A_CBI_OSUSTU     &
        DISPLAY                            &
        FIXED


DRAW 11,2 TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Produit dimension " &
@ELSE
      " %%%Produit dimension " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13

TITLE &
@IF FRANCAIS
    "                                        Fini   Épai.   Fini" &
@ELSE
    "%%%                                     Fini   Épai.   Fini" &
@ENDIF
     AT ,2

SKIP

TITLE &
@IF FRANCAIS
    "     Long.    Haut.    Épai.   Granit   avant  res.    après  Err" &
@ELSE
    "%%%  Long.    Haut.    Épai.   Granit   avant  res.    après  Err" &
@ENDIF
     AT ,2

SKIP TO LINE 15


ALIGN (06,06,07) (,,16) (,,25) (,,34) (,,42) (,,49) (,,58) (,,65)


CLUSTER OCCURS WITH PDDET_OPER_SURF  ID BASE 40


FIELD DOSLON           OF PDDET_OPER_SURF  &
        HIDDEN                             &
        LABEL " "                          &
        REQUIRED NOCHANGE SIZE 4


FIELD DOSHAU           OF PDDET_OPER_SURF  &
        HIDDEN                             &
        REQUIRED NOCHANGE       SIZE 4


FIELD DOSEPA           OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        REQUIRED NOCHANGE   SIZE 4


FIELD TGRCODGRA        OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        REQUIRED NOCHANGE


FIELD DOSCODFIN001     OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        REQUIRED NOCHANGE


FIELD DOSEPARES        OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE                            size 4


FIELD DOSCODFIN002     OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE


FIELD T_SILENT                             &
        SILENT


FIELD DOSACCERR        OF PDDET_OPER_SURF  &
        HIDDEN                             &
        NOLABEL                            &
        DUPLICATE


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST





;-------------------------------------------------------------------------------
;
; Appel du dépisteur (pop-up) pour le champ.
;

;PROCEDURE INPUT PDILAR
;BEGIN
;  IF 0 <> INDEX(FIELDTEXT,"*")
;  THEN BEGIN
;    LET T_COD          = 0 ; Toutes les tranches
;    LET T_TRANUMTRA002 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
;    RUN SCREEN pd122 MODE F &
;                     PASSING T_TRANUMTRA002, &
;                             T_COD, &
;                             T_CIECLEMNU
;    LET FIELDTEXT = TRUNCATE( T_TRANUMTRA002 )
;  END
;END


;-------------------------------------------------------------------------------
;
; Initialise la valeur du champ epaisseur résultante
;

PROCEDURE INPUT DOSEPARES
BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
    LET FIELDTEXT = ASCII(PDIEPA OF PDI_AVANT)
  END
END


;-------------------------------------------------------------------------------
;
; Assigne une valeur par défauts pour le champ
;

PROCEDURE INPUT DOSCODFIN002

BEGIN
  IF 0 = SIZE(FIELDTEXT)
  THEN BEGIN
    LET DOSCODFIN002     OF PDDET_OPER_SURF = TYFCODFIN        OF PDI_AVANT
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le fini de surface ou l'epaisseur resultante ont été modifié
;

PROCEDURE EDIT T_SILENT
BEGIN
  IF DOSEPARES        OF PDDET_OPER_SURF = PDIEPA           OF PDI_AVANT &
     AND &
     DOSCODFIN002     OF PDDET_OPER_SURF = TYFCODFIN        OF PDI_AVANT
  THEN BEGIN
    ERROR 3027 ; Ni l'épaisseur, ni le fini de surface n'ont été modifié
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDET_OPER_SURF &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDET_OPER_SURF &
     AND NOT NEWRECORD     OF PDDET_OPER_SURF &
     AND NOT DELETEDRECORD OF PDDET_OPER_SURF &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR PDDET_OPER_SURF
  BEGIN
    ;
    ; Valide les dimension minimum et maximum pour un produit dimension
    ;
    IF DOSEPARES        OF PDDET_OPER_SURF  > T_DIM_MAX_EPA_PD &
       OR &
       DOSEPARES        OF PDDET_OPER_SURF  < T_DIM_MIN_EPA_PD
    THEN BEGIN
      ERROR 2962 ; Cette dimension est hors des limites
    END
    ;
    ; Calcul du volume commandé précedant
    ;
    LET TZ_LARGEUR              = DOSLON           OF PDDET_OPER_SURF
    LET TZ_HAUTEUR              = DOSHAU           OF PDDET_OPER_SURF
    LET TZ_EPAISSEUR            = DOSEPA           OF PDDET_OPER_SURF
    DO INTERNAL CALVOL
    LET PDIVOLMC3        OF PDI_APRES = TZ_VOLUME
    LET PDISURMC2        OF PDI_APRES = TZ_MC2
  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Opération de surface *****************************
*                                                                              *
* Numéro rapport: xxxxxxxxxxx            Heure départ..: xxxxx                 *
* Date oper surf: xxxxxxxx               Heure arrêt...: xxxxx                 *
* Code machine..: xxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                *
* Quart travail.: x    xxxxxxxxxxxxxxxxxxxxxxxxx                               *
* Osustu          x    xxxxxxxxxxxxxxxxxxxxxxxxx                               *
*                                                                              *
****************************** Produit dimension *******************************
*                                                                              *
*                                        Fini   Épai.   Fini                   *
*     Long.    Haut.    Épai.   Granit   avant  res.    après  Err             *
*     xxxx     xxxx     xxxx     xxx     xx     xxxx     xx     x              *
*     xxxx     xxxx     xxxx     xxx     xxxx     xx     xx     x              *
*     xxxx     xxxx     xxxx     xxx     xxxx     xx     xx     x              *
*     xxxx     xxxx     xxxx     xxx     xxxx     xx     xx     x              *
*     xxxx     xxxx     xxxx     xxx     xxxx     xx     xx     x              *
*     xxxx     xxxx     xxxx     xxx     xxxx     xx     xx     x              *
*     xxxx     xxxx     xxxx     xxx     xxxx     xx     xx     x              *
*                                                                              *
********************************************************************************
@ENDIF
