;/T/ Consultation des Factures CLICIECLE,CLICLE,TYPECRPAT,FLGSLD,CIECLE,FARCLE
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 7 mai 1992
;
;    Description..: Permet de visionner et de choisir un compte à recevoir.
;                   Seul les factures journsalisées seront sélectionnées.
;
;
;    Paramètre....: CLICIECLE ==> - compagnie du client.
;                                 - segment de la clé de recherche.
;
;                   CLICLE    ==> - numéro de client.
;                                 - segment de la clé de recherche.
;
;                   TYPECRPAT ==> - Type de facture.
;                                 - critère de sélection
;                                 - sous la forme de pattern
;
;                   FLGSLD    ==> - Indicateur de sélection.
;                                 - critère de sélection
;                                 - si = 1, facture à recevoir ou solde <> 0
;                                   si = 0, toutes les factures sans tenir
;                                   compte du solde.
;
;                   CIELCE    ==> - compagnie du solde du client.
;
;                   FARCLE    ==> - numéro du compte à recevoir(facture).
;                                 - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;                                 - contient la valeur de retour.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN fa105 NOMODE NOACTION FIELDMARK &
             FROM 11,20 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CLICIECLE, &
                       T_CLICLE   , &
                       T_TYPECRPAT, &
                       T_FLGSLD   , &
                       T_CIECLEMNU, &
                       T_FARCLE

USE fa105.hlp NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CLICIECLE CHARACTER SIZE 4 ; Compagnie du client.
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Compagnie du menu.
TEMPORARY T_CLICLE    CHARACTER SIZE 6 ; Numéro de client
TEMPORARY T_TYPECRPAT CHARACTER SIZE 11; Type de facture
TEMPORARY T_FLGSLD    CHARACTER SIZE 1 ; Flag sur le solde
TEMPORARY T_FARCLE    CHARACTER SIZE 12; Numéro de facture


;-------------------------------------------------------------------------------
;
FILE VFAR_SLD         PRIMARY &
                      OCCURS 10

  ACCESS VIA REFCIECLE, &
             REFCLENUM, &
             CIECLE   , &
             FARCLE     &
         USING T_CLICIECLE, &
               T_CLICLE   , &
               T_CIECLEMNU, &
               T_FARCLE     &
         ORDERBY CIECLE, &
                 FARCLE

  SELECT IF     FARDATJOU OF VFAR_SLD <> 0 &
            AND MATCHPATTERN(FARTYPECR OF VFAR_SLD,TRUNCATE(T_TYPECRPAT)) &
            AND ( &
                  (     0 <> (  FARMNT    OF VFAR_SLD &
                            + V_FARMNTAJT OF VFAR_SLD &
                            + V_FARMNTCRT OF VFAR_SLD &
                            - FARMNTREC   OF VFAR_SLD ) &
                    AND T_FLGSLD = "1" &
                  ) &
                  OR  &
                  T_FLGSLD = "0" &
                )


;-------------------------------------------------------------------------------
;
;
; Montant du compte à recevoir.
;
DEFINE D_MNTFAC FLOAT SIZE 8 =   FARMNT      OF VFAR_SLD &
                               + V_FARMNTAJT OF VFAR_SLD

;
; Solde à recevoir.
;
DEFINE D_SLDFAC FLOAT SIZE 8 =  D_MNTFAC &
                                + V_FARMNTCRT OF VFAR_SLD &
                                - FARMNTREC   OF VFAR_SLD


;-------------------------------------------------------------------------------

DRAW 1,1 TO 13,60

USE ushilite NOLIST

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP TO 2

TITLE &
@IF FRANCAIS
      "Numéro         Date            Brut     A recevoir" &
@ELSE
      "Number         Date          $ Gross  $ Receivable" &
@ENDIF
      AT 2,6

ALIGN(3,3,6) (,,19) (,,30) (,,44)

CLUSTER OCCURS WITH VFAR_SLD

FIELD FARCLE    OF VFAR_SLD HIDDEN LABEL ">>" &
        DISPLAY

FIELD FARDAT    OF VFAR_SLD  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY

FIELD D_MNTFAC &
        DISPLAY &
        PICTURE "^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

FIELD D_SLDFAC &
        PICTURE "^,^^^,^^^.^^ " &
        TRAILING SIGN "-" &
        SIGNIFICANCE 5

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_FARCLE, "@")
  THEN LET T_FARCLE = ""
END

;-------------------------------------------------------------------------------
;
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_FARCLE = FARCLE OF VFAR_SLD
  RETURN
END

BUILD

;                       *********************************************************
;                       *    Numéro         Date            Brut     A recevoir *
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       * >> xxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxx xxxxxxxxxxxxxx*
;                       *********************************************************
