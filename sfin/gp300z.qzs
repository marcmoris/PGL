;/T/ Suivi des budget comptable (détaillé)
;
;/P/ Programmeur...: Steeve Duguay
;    Date Création.: 1995-03-13
;
;    Description...: Le Rapport Budget Suivis des budget comptable (détaillé)
;
;
;-------------------------------------------------------------------------------

 USE ussetqzs NOLIST   ; permet de faire les SET PROCESS NOLIMIT....
 SET NOBLANK           ; Empêche l'impression de ligne blanche


ACCESS GPPRA_BDG_CTB &

  LINK CIECLE OF GPPRA_BDG_CTB,         &
       PRJCLE OF GPPRA_BDG_CTB          &
    TO CIECLE,                          &
       PRJCLE OF GPPROJETS              &

  LINK CIECLE OF GPPRA_BDG_CTB,         &
       PRJCLE OF GPPRA_BDG_CTB,         &
       PRACLE OF GPPRA_BDG_CTB          &
    TO CIECLE,                          &
       PRJCLE,                          &
       PRACLE OF GPPRO_ACTIVITE         &

  LINK CPTCLE OF GPPRA_BDG_CTB          &
    TO CPTCLE OF VCPT_DSC      OPTIONAL &

  LINK NACCLE OF VCPT_DSC               &
    TO NACCLE OF VNAC_DSC OPTIONAL      &

  LINK CIECLE OF GPPRA_BDG_CTB,         &
       UNACLE OF GPPRA_BDG_CTB          &
    TO CIECLE                 ,         &
       UNACLE OF MCUNITE_ADM   OPTIONAL


CHOOSE CIECLE PARM, &                    ; Compagnie
       PRJCLE PARM RANGE, &              ; Projet
       PRACLE PARM RANGE, &              ; Sous-projet
       CPTCLE PARM RANGE, &              ; Compte
       UNACLE PARM                       ; Unité

SELECT GPPRA_BDG_CTB IF PACMNTBDG    OF GPPRA_BDG_CTB <> 0 OR &
                        PACMNTBDGREV OF GPPRA_BDG_CTB <> 0

 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Suivi des budgets comptables (Détaillé)"
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "             ; titre2  du rapport
 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GP300"         ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM            ; Nom de la compagnie

DEFINE D_BDGREV     FLOAT SIZE 8 = PACMNTBDG OF GPPRA_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 4     &
                              ELSE 0

DEFINE D_BDGDEP     FLOAT SIZE 8 = PACMNTBDG OF GPPRA_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 5     &
                              ELSE 0

DEFINE D_REEREV     FLOAT SIZE 8 = PACMNTBDGREV OF GPPRA_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 4        &
                              ELSE 0

DEFINE D_REEDEP     FLOAT SIZE 8 = PACMNTBDGREV OF GPPRA_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 5        &
                              ELSE 0

DEFINE D_PRJDSC CHARACTER SIZE 30 = PRJDSC1 OF GPPROJETS
DEFINE D_PRADSC CHARACTER SIZE 30 = PRADSC1 OF GPPRO_ACTIVITE

SORT ON CIECLE OF GPPRA_BDG_CTB,  &
        PRJCLE OF GPPRA_BDG_CTB,  &
        PRACLE OF GPPRA_BDG_CTB,  &
        UNACLE OF GPPRA_BDG_CTB,  &
        CPTCLE OF GPPRA_BDG_CTB


 USE ustit220 NOLIST  ; Titre standard pour l'application
SKIP 2&
 TAB 042 "Sous-" &
 TAB 153 "---------Budget initial---------" &
 TAB 189 "----------Budget réel-----------" &
 SKIP &
 TAB 002 "Projet" &
 TAB 010 "Description" &
 TAB 042 "Projet" &
 TAB 052 "Description" &
 TAB 081 "Unité Adm." &
 TAB 093 "Description" &
 TAB 119 "Compte" &
 TAB 127 "Description" &
 TAB 153 "----Revenu-----" &
 TAB 170 "----Dépense----" &
 TAB 189 "----Revenu-----" &
 TAB 206 "----Dépense----" &
SKIP 2

FOOTING AT CPTCLE &
  TAB 001 PRJCLE       OF GPPRA_BDG_CTB   PRINT AT PRJCLE &
  TAB 010 D_PRJDSC                        PRINT AT PRJCLE &
  TAB 043 PRACLE       OF GPPRA_BDG_CTB   PRINT AT PRACLE &
  TAB 048 D_PRADSC                        PRINT AT PRACLE &
  TAB 082 UNACLE       OF GPPRA_BDG_CTB &
  TAB 092 UNANOM       OF MCUNITE_ADM   &
  TAB 119 CPTCLE       OF GPPRA_BDG_CTB &
  TAB 126 CPTDSC       OF VCPT_DSC      &
  TAB 153 D_BDGREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 170 D_BDGDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 189 D_REEREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 206 D_REEDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 RESERVE 7 LINES


FOOTING AT PRACLE &
  TAB 153 "---------------" &
  TAB 170 "---------------" &
  TAB 189 "---------------" &
  TAB 206 "---------------" &
  SKIP &
  TAB 126 "Total du sous-projet :" &
  TAB 153 D_BDGREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 170 D_BDGDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 189 D_REEREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 206 D_REEDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
      SKIP 2


FOOTING AT PRJCLE &
  TAB 153 "---------------" &
  TAB 170 "---------------" &
  TAB 189 "---------------" &
  TAB 206 "---------------" &
  SKIP &
  TAB 126 "Total du projet      :" &
  TAB 153 D_BDGREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 170 D_BDGDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 189 D_REEREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 206 D_REEDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
      SKIP PAGE



BUILD gp300za


;/T/ Suivi des budget comptable (sommaire)
;
;/P/ Programmeur...: Steeve Duguay
;    Date Création.: 1995-03-13
;
;    Description...: Le Rapport Budget Suivis des budget comptable (sommaire)
;
;
;-------------------------------------------------------------------------------

 USE ussetqzs NOLIST   ; permet de faire les SET PROCESS NOLIMIT....
 SET NOBLANK           ; Empêche l'impression de ligne blanche


ACCESS GPPRO_BDG_CTB &

  LINK CIECLE OF GPPRO_BDG_CTB,         &
       PRJCLE OF GPPRO_BDG_CTB          &
    TO CIECLE,                          &
       PRJCLE OF GPPROJETS              &

  LINK CPTCLE OF GPPRO_BDG_CTB          &
    TO CPTCLE OF VCPT_DSC      OPTIONAL &

  LINK NACCLE OF VCPT_DSC               &
    TO NACCLE OF VNAC_DSC OPTIONAL      &

  LINK CIECLE OF GPPRO_BDG_CTB,         &
       UNACLE OF GPPRO_BDG_CTB          &
    TO CIECLE                 ,         &
       UNACLE OF MCUNITE_ADM   OPTIONAL


SELECT GPPRO_BDG_CTB IF PBCMNTBDG    OF GPPRO_BDG_CTB <> 0 OR &
                        PBCMNTBDGREV OF GPPRO_BDG_CTB <> 0

CHOOSE CIECLE PARM, &                    ; Compagnie
       PRJCLE PARM RANGE, &              ; Projet
       CPTCLE PARM RANGE, &              ; Compte
       UNACLE PARM                       ; Unité

 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Suivi des budgets comptables (Sommaire)"
 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "             ; titre2  du rapport
 DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GP300"         ; numéro du rapport
 DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM            ; Nom de la compagnie

DEFINE D_BDGREV     FLOAT SIZE 8 = PBCMNTBDG OF GPPRO_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 4     &
                              ELSE 0

DEFINE D_BDGDEP     FLOAT SIZE 8 = PBCMNTBDG OF GPPRO_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 5     &
                              ELSE 0

DEFINE D_REEREV     FLOAT SIZE 8 = PBCMNTBDGREV OF GPPRO_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 4        &
                              ELSE 0

DEFINE D_REEDEP     FLOAT SIZE 8 = PBCMNTBDGREV OF GPPRO_BDG_CTB &
                                IF NACCLE OF VCPT_DSC = 5        &
                              ELSE 0

DEFINE D_PRJDSC CHARACTER SIZE 30 = PRJDSC1 OF GPPROJETS

SORT ON CIECLE OF GPPRO_BDG_CTB,  &
        PRJCLE OF GPPRO_BDG_CTB,  &
        UNACLE OF GPPRO_BDG_CTB,  &
        CPTCLE OF GPPRO_BDG_CTB


 USE ustit220 NOLIST  ; Titre standard pour l'application
SKIP 2&
 TAB 123 "---------Budget initial---------" &
 TAB 159 "----------Budget réel-----------" &
 SKIP &
 TAB 002 "Projet" &
 TAB 010 "Description" &
 TAB 051 "Unité Adm." &
 TAB 063 "Description" &
 TAB 089 "Compte" &
 TAB 097 "Description" &
 TAB 123 "----Revenu-----" &
 TAB 140 "----Dépense----" &
 TAB 159 "----Revenu-----" &
 TAB 176 "----Dépense----" &
SKIP 2

FOOTING AT CPTCLE &
  TAB 001 PRJCLE       OF GPPRO_BDG_CTB   PRINT AT PRJCLE &
  TAB 010 D_PRJDSC                        PRINT AT PRJCLE &
  TAB 052 UNACLE       OF GPPRO_BDG_CTB &
  TAB 062 UNANOM       OF MCUNITE_ADM   &
  TAB 089 CPTCLE       OF GPPRO_BDG_CTB &
  TAB 096 CPTDSC       OF VCPT_DSC      &
  TAB 123 D_BDGREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 140 D_BDGDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 159 D_REEREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 176 D_REEDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 RESERVE 5 LINES


FOOTING AT PRJCLE &
  TAB 123 "---------------" &
  TAB 140 "---------------" &
  TAB 159 "---------------" &
  TAB 176 "---------------" &
  SKIP &
  TAB 096 "Total du projet      :" &
  TAB 123 D_BDGREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 140 D_BDGDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 159 D_REEREV SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
                            SIGNIFICANCE 5 &
  TAB 176 D_REEDEP SUBTOTAL PICTURE "^^^,^^^,^^^.^^ " TRAILING SIGN "-" &
      SKIP PAGE



BUILD gp300zb
