;/T/ Liste des clients détaillé
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 93-06-10
;
;    Description...: Liste des clients détaillé
;
;    Paramètres....: Compagnie-client  1 seule
;                    Compagnie-menu    1 seule
;                    Client            1, plusieurs, tous
;                    Catégorie         1, plusieurs, tous
;                    Statut            1, plusieurs, tous
;                    Ordre Alpha-Num
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS


ACCESS VCLC_CLI &
  ;
  ; Adresse du client.
  ;
  LINK ( CLICIECLE    OF VCLC_CLI, &
         CLIREFTYP    OF VCLC_CLI, &
         CLICLE       OF VCLC_CLI, &
         1 )                       &
    TO REFCIECLE               , &
       REFCLETYP               , &
       REFCLENUM               , &
       RADCLE                  OF MCREF_ADRESSE ALIAS A_RAD_CLIENT OPTIONAL &
  ;
  ; Adresse d'état de compte.
  ;
  LINK CLICIECLE    OF VCLC_CLI, &
       CLIREFTYP    OF VCLC_CLI, &
       CLICLE       OF VCLC_CLI, &
       CLCADRETACPT OF VCLC_CLI  &
    TO REFCIECLE               , &
       REFCLETYP               , &
       REFCLENUM               , &
       RADCLE                  OF MCREF_ADRESSE ALIAS MC_REF_ADRETA OPTIONAL &
  ;
  ; Description du statut.
  ;
  LINK ("CLISTU"               , &
       CLISTU OF VCLC_CLI)       &
    TO XELNOM                  , &
       CBICLE                  OF VCBI_DSC ALIAS VCBI_CLISTU OPTIONAL &
  ;
  ; Description de la catégorie.
  ;
  LINK ("CLICAT"               , &
       CLICAT OF VCLC_CLI)       &
    TO XELNOM                  , &
       CBICLE                  OF VCBI_DSC ALIAS VCBI_CLICAT OPTIONAL &
  ;
  ; Descrition du compte à recevoir.
  ;
  LINK CLCCPTCAR OF VCLC_CLI     &
    TO CPTCLE                  OF VCPT_DSC ALIAS  VCPT_CPTCAR OPTIONAL &
  ;
  ; Description du compte d'encaisse.
  ;
  LINK LBQCPTENC OF VCLC_CLI     &
    TO CPTCLE                  OF VCPT_DSC ALIAS  VCPT_CPTENC OPTIONAL &
  ;
  ; Contacts du clients.
  ;
  LINK CLICIECLE    OF VCLC_CLI, &
       CLICLE       OF VCLC_CLI  &
    TO CLICIECLE               , &
       CLICLE                  OF CRCLI_CONTACT OPTIONAL



CHOOSE CLICIECLE PARM, &              ; Compagnie client
       CIECLE    PARM, &              ; Compagnie menu
       CLICLE    PARM, &              ; Client
       CLICAT    PARM, &              ; Catégorie
       CLISTU    PARM                 ; Statut



;
; Ordre du tri : 1 - Nom client
;                2 - Numéro client
;
DEFINE D_TYP_TRI CHAR SIZE 1  = PARM

;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
                    "Liste des clients / détaillé"
@ELSE
                    "%%%Liste des clients / détaillé"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
                    "Par ordre de nom"    IF D_TYP_TRI="1" &
               ELSE "Par ordre de numéro" IF D_TYP_TRI="2"
@ELSE
                    "In Name Order"       IF D_TYP_TRI="1" &
               ELSE "In Number Order"     IF D_TYP_TRI="2"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CR302"

;
; Nom de la compagnie du menu.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

;
; On imprime l'adresse état compte seulement si elle différente de l'adresse
; du client.
;
; Adresse état de compte - ligne 1
;
DEFINE D_ADR_1 CHAR SIZE 66 = &
@IF FRANCAIS
                              "Adresse état compte : " + &
@ELSE
                              "%%%Adresse état compte : " + &
@ENDIF
                              RADADR1   OF MC_REF_ADRETA &
                              IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; Adresse état de compte - ligne 2
;
DEFINE D_ADR_2 CHAR SIZE 40 = RADADR2   OF MC_REF_ADRETA &
                              IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; Adresse état de compte - ligne 3
;
DEFINE D_ADR_3 CHAR SIZE 40 = RADADR3   OF MC_REF_ADRETA &
                              IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; Adresse état de compte - ligne 4
;
DEFINE D_ADR_4 CHAR SIZE 40 = RADADR4   OF MC_REF_ADRETA &
                              IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; Adresse état de compte - code postal
;
DEFINE D_CP       CHAR SIZE 7  = RADCP     OF MC_REF_ADRETA &
                                 IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; Adresse état de compte - téléphone
;
DEFINE D_TEL      CHAR SIZE 19 = &
@IF FRANCAIS
                                 "Tél.: "   &
@ELSE
                                 "Tel.: "   &
@ENDIF
                                 + RADTEL OF MC_REF_ADRETA &
                                 IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; Adresse état de compte - Fax
;
DEFINE D_FAX      CHAR SIZE 19 = "Fax.: " + RADFAX    OF  MC_REF_ADRETA &
                                 IF 1 <> CLCADRETACPT OF VCLC_CLI

;
; On imprime l'en-tête des contacts seulement s'il y en a de présent.
;
DEFINE D_ENT_CONTACT1 CHARACTER SIZE 95 = &
@IF FRANCAIS
  "      Contact               Titre           Téléphone   Poste      Fax       Cellulaire  "&
@ELSE
  "%%%   Contact               Titre           Téléphone   Poste      Fax       Cellulaire  "&
@ENDIF
  IF RECORD CRCLI_CONTACT EXIST ELSE " "

;
; On imprime l'en-tête des contacts seulement s'il y en a de présent.
;
DEFINE D_ENT_CONTACT2 CHARACTER SIZE 95 = &
  "-------------------- -------------------- ------------- ----- ------------- -------------" &
  IF RECORD CRCLI_CONTACT EXIST ELSE " "

;
; Assigne le numéro ou nom du client dans variable du tri
;
DEFINE D_TRI   CHAR SIZE 40 = CLINOM OF VCLC_CLI IF D_TYP_TRI ="1" ELSE &
                              CLICLE OF VCLC_CLI IF D_TYP_TRI ="2"


;
; Ordre du tri (Nom client ou Numéro client)
;
SORT ON D_TRI


;
; Impresseion du titre de la liste.
;
USE ustitstd NOLIST
 SKIP 2

HEADING AT D_TRI &
  TAB 001 "----------------------------------------------------------------------" &
  TAB 071 "--------------------------------------------------------------" &
 SKIP &
@IF FRANCAIS
  TAB 001 "No client.:" &
@ELSE
  TAB 001 "%%%client.:" &
@ENDIF
  TAB 013 CLICLE OF VCLC_CLI &
  TAB 020 CLINOM OF VCLC_CLI &
@IF FRANCAIS
  TAB 061 "Enrg. TPS:" &
@ELSE
  TAB 061 "Enrg. TPS:" &
@ENDIF
  TAB 072 CLCEXPTPS OF VCLC_CLI &
@IF FRANCAIS
  TAB 088 "Compte CAR.....:" &
@ELSE
  TAB 088 "%%%pte CAR.....:" &
@ENDIF
  TAB 105 CLCCPTCAR OF VCLC_CLI &
  TAB 112 CPTDSCABR OF VCPT_CPTCAR &
 SKIP &
  TAB 020 CLINOMABR OF VCLC_CLI &
@IF FRANCAIS
  TAB 061 "Enrg. TVQ:" &
@ELSE
  TAB 061 "Enrg. TVQ:" &
@ENDIF
  TAB 072 CLCEXPTVP OF VCLC_CLI &
@IF FRANCAIS
  TAB 088 "Compte encaisse:" &
@ELSE
  TAB 088 "%%%pte encaisse:" &
@ENDIF
  TAB 105 LBQCPTENC OF VCLC_CLI &
  TAB 112 CPTDSCABR OF VCPT_CPTENC &
SKIP &
@IF FRANCAIS
  TAB 020 "Catégorie:" &
@ELSE
  TAB 020 "%%%:" &
@ENDIF
  TAB 031 CBIDSC OF VCBI_CLICAT &
@IF FRANCAIS
  TAB 061 "Statut...:" &
@ELSE
  TAB 061 "%%%.:" &
@ENDIF
  TAB 072 CBIDSC OF VCBI_CLISTU &
@IF FRANCAIS
  TAB 105 "Limite crédit..: " &
@ELSE
  TAB 105 "Credit Limit...: " &
@ENDIF
  TAB 122 CLCLIMCRT OF VCLC_CLI  &
 SKIP &
@IF FRANCAIS
  TAB 020 "Terme net:" &
@ELSE
  TAB 020 "%%%me net:" &
@ENDIF
  TAB 031 CLCTERNET OF VCLC_CLI &
@IF FRANCAIS
  TAB 039 "Terme esc(1):" &
@ELSE
  TAB 039 "%%%me esc(1):" &
@ENDIF
  TAB 053 CLCTERESC1 OF VCLC_CLI &
@IF FRANCAIS
  TAB 061 "Terme esc(2):" &
@ELSE
  TAB 061 "%%%me esc(2):" &
@ENDIF
  TAB 075 CLCTERESC2 OF VCLC_CLI &
@IF FRANCAIS
  TAB 083 "Terme frais.:" &
@ELSE
  TAB 083 "%%%me frais.:" &
@ENDIF
  TAB 097 CLCTERFRS OF VCLC_CLI &
@IF FRANCAIS
  TAB 105 "Devise.........:" &
@ELSE
  TAB 105 "%%%Devise......:" &
@ENDIF
  TAB 122 DEVCLE OF VCLC_CLI &
SKIP 2 &

@IF FRANCAIS
  TAB 003 "Adresse : " &
@ELSE
  TAB 003 "Payment : " &
@ENDIF
  TAB 013 RADADR1    OF A_RAD_CLIENT  &
  TAB 062 D_ADR_1                      &
  SKIP 1 &
  TAB 013 RADADR2    OF A_RAD_CLIENT  &
  TAB 084 D_ADR_2                   &
  SKIP 1 &
  TAB 013 RADADR3    OF A_RAD_CLIENT  &
  TAB 084 D_ADR_3                   &
  SKIP 1 &
  TAB 013 RADADR4    OF A_RAD_CLIENT  &
  TAB 084 D_ADR_4                   &
  SKIP 1 &
  TAB 013 RADCP      OF A_RAD_CLIENT  &
  TAB 084 D_CP                         &
  SKIP 2 &
@IF FRANCAIS
  TAB 003 "Tél.: " &
@ELSE
  TAB 003 "Tel.: " &
@ENDIF
  TAB 009 RADTEL     OF A_RAD_CLIENT  &
  TAB 023 "Fax.: " &
  TAB 029 RADFAX     OF A_RAD_CLIENT  &
  TAB 062 D_TEL                       &
  TAB 082 D_FAX                        &
  SKIP 2


REPORT &
  TAB 003 D_ENT_CONTACT1 PRINT AT D_TRI &
SKIP &
  TAB 003 D_ENT_CONTACT2 PRINT AT D_TRI &
SKIP &
  TAB 003 CCTNOM OF CRCLI_CONTACT &
  TAB 024 CCTTIT OF CRCLI_CONTACT &
  TAB 045 CCTTEL OF CRCLI_CONTACT &
  TAB 059 CCTTELPOS OF CRCLI_CONTACT &
  TAB 065 CCTFAX   OF CRCLI_CONTACT &
  TAB 079 CCTTELCEL OF CRCLI_CONTACT

BUILD cr302z
@IF FORMAT

                                                                                                   1         1         1         1
         1         2         3         4         5         6         7         8         9         0         1         2         3
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
CR302                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                       Liste des clients  / détaillé
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx(1)

------------------------------------------------------------------------------------------------------------------------------------
No client.: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Enrg. TPS: xxxxxxxxxxxxxxx Compte CAR.....: 123456 xxxxxxxxxxxxxxxxxxxx
                   xxxxxxxxxxxxxxxxxxxx                     Enrg. TVQ: xxxxxxxxxxxxxxx Compte encaisse: 123456 xxxxxxxxxxxxxxxxxxxx
                   Catégorie: xxxxxxxxxxxxxxxxxxxxxxxxx     Statut...: xxxxxxxxxxxxxxxxxxxxxxxxx        Limite crédit..: 999,999,999
                   Terme Net: xxxx    Terme esc(1): xxxx    Terme esc(2): xxxx    Terme frais.: xxxx    Devise.........: xxx

  Adresse : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx         Adresse état compte : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx (2)
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxx-xxx                                                                xxx-xxx

  Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx                    Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx

        Contact               Titre           Téléphone   Poste      Fax       Cellulaire
  -------------------- -------------------- ------------- ----- ------------- -------------
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
-----------------------------------------------------------------------------------------------------------------------------------
No client.: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Enrg. TPS: xxxxxxxxxxxxxxx Compte CAR.....: 123456 xxxxxxxxxxxxxxxxxxxx
                   xxxxxxxxxxxxxxxxxxxx                     Enrg. TVQ: xxxxxxxxxxxxxxx Compte encaisse: 123456 xxxxxxxxxxxxxxxxxxxx
                   Catégorie: xxxxxxxxxxxxxxxxxxxxxxxxx     Statut...: xxxxxxxxxxxxxxxxxxxxxxxxx        Limite crédit..: 999,999,999
                   Terme Net: xxxx    Terme esc(1): xxxx    Terme esc(2): xxxx    Terme frais.: xxxx    Devise.........: xxx

  Adresse : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx         Adresse état compte : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx (2)
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxx-xxx                                                                xxx-xxx

  Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx                    Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx

        Contact               Titre           Téléphone   Poste      Fax       Cellulaire
  -------------------- -------------------- ------------- ----- ------------- -------------
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
-----------------------------------------------------------------------------------------------------------------------------------
No client.: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Enrg. TPS: xxxxxxxxxxxxxxx Compte CAR.....: 123456 xxxxxxxxxxxxxxxxxxxx
                   xxxxxxxxxxxxxxxxxxxx                     Enrg. TVQ: xxxxxxxxxxxxxxx Compte encaisse: 123456 xxxxxxxxxxxxxxxxxxxx
                   Catégorie: xxxxxxxxxxxxxxxxxxxxxxxxx     Statut...: xxxxxxxxxxxxxxxxxxxxxxxxx        Limite crédit..: 999,999,999
                   Terme Net: xxxx    Terme esc(1): xxxx    Terme esc(2): xxxx    Terme frais.: xxxx    Devise.........: xxx

  Adresse : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx         Adresse état compte : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx (2)
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                               xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxx-xxx                                                                xxx-xxx

  Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx                    Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx

        Contact               Titre           Téléphone   Poste      Fax       Cellulaire
  -------------------- -------------------- ------------- ----- ------------- -------------
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
-----------------------------------------------------------------------------------------------------------------------------------
 (1) par ordre alphabétique ou par ordre numérique
 (2) Impression de l'adresse d'état de compte si différent de l'adresse du client.
@ENDIF
