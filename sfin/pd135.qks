;/T/ Dépisteur numéro de caisson
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-12-18
;
;    Description..: Permet de visionner et de choisir un caisson
;
;    Paramètres...: T_CAINUMCAI ==> Numéro de caisson
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd135 NOMODE NOACTION FIELDMARK &
             FROM 11,30 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_CAINUMCAI,&
                       T_CIECLEMNU


TEMPORARY T_CAINUMCAI CHARACTER SIZE 09
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd135.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Caisson
;

FILE PDCAISSON PRIMARY OCCURS 10

  ACCESS VIA     CIECLE,               &
                 CAINUMCAI             &
         USING   T_CIECLEMNU,          &
                 T_CAINUMCAI           &
         ORDERBY CIECLE,               &
                 CAINUMCAI


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,50 ; Remplace le DRAW standard

USE ustitoff NOLIST     ; Pour placer le TITLE OFF


SKIP TO 2


TITLE &
@IF FRANCAIS
      "    Caisson   S E Ent Emb Date Fab.  Date emb. " &
@ELSE
      "%%% Caisson   S E Ent Emb Date Fab. Date emb. " &
@ENDIF
      AT ,2


SKIP


ALIGN (03,03,06) (,,16) (,,18) (,,20) (,,24) (,,28) (,,39)


CLUSTER OCCURS WITH PDCAISSON


FIELD CAINUMCAI        OF PDCAISSON        &
        HIDDEN LABEL ">>"                  &
        DISPLAY                            &
        NUMERIC


FIELD CAISTU           OF PDCAISSON        &
        DISPLAY


FIELD CAIETA           OF PDCAISSON        &
        DISPLAY


FIELD CAISITENT        OF PDCAISSON        &
        DISPLAY


FIELD CAISITEMB        OF PDCAISSON        &
        DISPLAY


FIELD CAIDATFAB        OF PDCAISSON         FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY


FIELD CAIDATEMB        OF PDCAISSON         FORMAT YYYYMMDD PATTERN "####/##/##" &
        DISPLAY


CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_CAINUMCAI , "@")
  THEN BEGIN
    LET T_CAINUMCAI = ""
  END
END


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_CAINUMCAI = CAINUMCAI OF PDCAISSON
  RETURN
END

BUILD


@IF FORMAT

                             **************************************************
                             *    Caisson   S E Ent Emb Date Fab. Date emb.   *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             * >> xxxxxxxxx x x xx  xx  xxxxxxxx  xxxxxxxx    *
                             **************************************************
@ENDIF
