;/T/ Etiquette - Adresse
;
;/P/  Programmeur......... : Stéphane Allen
;     Date de creation.... : 12 Janvier 1995
;
;     Description......... : Permet de faire des étiquettes de fournisseurs
;                            avec leurs noms, adresses et le numero de code postal
;
;     Parametre........... : Aucun
;
;-------------------------------------------------------
;
; Etiquettes (3.5 x 1 pouces) - 3 colonnes
;
; Trié par type et numéro

USE ussetqzs.qzs NOLIST

SET REPORT SPACING 1
SET PAGE WIDTH 44 IMAGE 3 LEN 8

SET FORMFEED
SET NOHEAD

ACCESS CPFOU_CIE                    &
LINK   FOUCLE    OF CPFOU_CIE       &
  TO   FOUCLE    OF CPFOURNISSEUR   &

LINK (FOUCIECLE OF CPFOURNISSEUR ,  &
      "F"                        ,  &
      FOUCLE    OF CPFOURNISSEUR ,  &
      1)                            &
  TO REFCIECLE,   &
     REFCLETYP,   &
     REFCLENUM,   &
     RADCLE OF MCREF_ADRESSE

CHOOSE FOCCIECLE  PARM  , &
       FOUCLE     PARM

select cpfournisseur if FOUSTU of cpfournisseur <> "I" ; Ne pas sélectionner les fournisseurs inactifs

SORT ON FOUCLE

REPORT                              &
TAB 001 FOUNOM     OF CPFOURNISSEUR &
SKIP 1                              &
TAB 001 RADADR1    OF MCREF_ADRESSE &
SKIP 1                              &
TAB 001 RADADR2    OF MCREF_ADRESSE &
SKIP 1                              &
TAB 001 RADADR3    OF MCREF_ADRESSE &
SKIP 1                              &
TAB 001 RADADR4    OF MCREF_ADRESSE &
SKIP 1                              &
TAB 001 RADCP      OF MCREF_ADRESSE &
TAB 030 REFCLENUM  OF MCREF_ADRESSE &
SKIP PAGE

BUILD cp320z
