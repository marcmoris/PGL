;/T/ Dimension de ventes
;
;/P/ Programmeur..: René Bonneau
;    Date création: 1994-11-04
;
;    Description..: Ce panorama permet de saisir les dimension de ventes pour
;                   les classes de granit.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd204c ACTIONBAR                    &
              MODE LABEL "" AT 2,80        &
              FROM 07,01 TO 23,80          &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU,       &
                        T_CIENOM,          &
                        PDCLASSE


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD204C"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd204c.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Classe de granit
;

FILE PDCLASSE         MASTER


;-------------------------------------------------------------------------------
;
; Dimension ventes
;

FILE PDDIM_VENTE_CLAS PRIMARY &
                      NOITEM &
                      OCCURS 11

  ACCESS VIA     CIECLE,                                &
                 CLGCODCLA                              &
         USING   T_CIECLEMNU,                           &
                 CLGCODCLA        OF PDCLASSE           &
         ORDERBY CIECLE,                                &
                 CLGCODCLA,                             &
                 DIVEPAVNT

  ITEM CIECLE    OF PDDIM_VENTE_CLAS   INITIAL T_CIECLEMNU
  ITEM CLGCODCLA OF PDDIM_VENTE_CLAS   INITIAL FIRST (CLGCODCLA OF PDCLASSE)


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDDIMEN_VENTES   REFERENCE OCCURS WITH PDDIM_VENTE_CLAS

  ACCESS VIA   CIECLE,                            &
               DIVEPAVNT                          &
         USING T_CIECLEMNU,                       &
               DIVEPAVNT        OF PDDIM_VENTE_CLAS


;-------------------------------------------------------------------------------
;
DRAW 3,1 TO 17,80   ; Remplace le Draw pour les écrans pleine page
;USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP 2

TITLE &
@IF FRANCAIS
      " Dimension ventes " &
@ELSE
      " %%%Dimension ventes " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP 1


ALIGN (2,3,19) (,30,46) (,57,73)


CLUSTER OCCURS WITH PDDIM_VENTE_CLAS ID BASE 05


  FIELD DIVEPAVNT      OF PDDIM_VENTE_CLAS                 &
          HIDDEN                                           &
          REQUIRED                                         &
          LOOKUP ON PDDIMEN_VENTES                         &
           MESSAGE 1923 & ; Cette dimension de ventes n'existe pas
          ,NOTON PDDIM_VENTE_CLAS                          &
            VIA     CIECLE,                                &
                    CLGCODCLA,                             &
                    DIVEPAVNT                              &
            USING   T_CIECLEMNU,                           &
                    CLGCODCLA        OF PDCLASSE,          &
                    DIVEPAVNT        OF PDDIM_VENTE_CLAS   &
           MESSAGE 1920 ; Cet demension de ventes existe déjà


  FIELD DIVEPAMIN      OF PDDIMEN_VENTES          &
          HIDDEN                                  &
          DISPLAY                                 &
          REFRESH


  FIELD DIVEPAMAX      OF PDDIMEN_VENTES          &
          HIDDEN                                  &
          DISPLAY                                 &
          REFRESH



CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDIM_VENTE_CLAS &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDIM_VENTE_CLAS &
     AND NOT NEWRECORD     OF PDDIM_VENTE_CLAS &
     AND NOT DELETEDRECORD OF PDDIM_VENTE_CLAS &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD
@IF FORMAT

******************************* Dimension ventes *******************************
*                                                                              *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
* Épais. ventes.: xxxxx      Épais. min....: xxxxx      Épaisseur max.: xxxxx  *
*                                                                              *
********************************************************************************
@ENDIF
