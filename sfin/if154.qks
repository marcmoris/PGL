;/T/ Lieu de destination
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 17 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base
;                   concernant le lieu de destination
;
;
SCREEN if154 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF154"

;USE if154.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFLIEU_DEST    IN DICT PRIMARY NOITEM &
                      OCCURS 16 TIMES

  ACCESS VIA     CIECLE,   &
                 LIDCLE    &
         USING   T_CIECLE, &
                 LIDCLE    &
         REQUEST LIDCLE    &
         ORDERBY LIDCLE

  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY LIDCLE

  ITEM CIECLE OF IFLIEU_DEST INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1
;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Lieu de destination " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST


TITLE "Lieu" AT ,6
TITLE "Desc. francaise" AT ,18
TITLE "Desc. anglaise" AT ,46
SKIP

ALIGN(5,6,7) (,,18) (,,46)
CLUSTER OCCURS WITH IFLIEU_DEST
FIELD LIDCLE OF IFLIEU_DEST &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFLIEU_DEST &
          VIA   CIECLE, &
                LIDCLE  &
          USING T_CIECLE, &
                LIDCLE OF IFLIEU_DEST &
          MESSAGE 3239 ; PD339 Ce lieu de chargement existe déjà

FIELD LIDDSCFRA OF IFLIEU_DEST &
      REQUIRED

FIELD LIDDSCANG OF IFLIEU_DEST

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Valeur par defaut pour la description anglaise
;
PROCEDURE INPUT LIDDSCANG
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = LIDDSCFRA OF IFLIEU_DEST 
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un lieu de destination
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN if154x MODE SAME &
                   PASSING IFLIEU_DEST, &
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3247 ; PD347 Impossible de supprimer ce lieu de destination,
                     ; il est d\351j\340 utilis\351e
   ELSE DELETE IFLIEU_DEST
END
BUILD
