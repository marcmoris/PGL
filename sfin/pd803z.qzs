;/T/ Liste de vérification des tranches par bloc
;
;/P/ Programmeur.....: Richard Brochu
;    Date création...: 1997-10-07
;    Description.....: Liste de vérification des tranches par bloc
;
;-----------------------------------------------------------------
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS PDMONTAGE_BLOC &

  LINK CIECLE          OF PDMONTAGE_BLOC,    &
       BLONUMGRA001APR OF PDMONTAGE_BLOC,    &
       BLONUMGRA002APR OF PDMONTAGE_BLOC,    &
       BLORECAPR       OF PDMONTAGE_BLOC     &
    TO CIECLE,                               &
       BLONUMGRA001APR,                      &
       BLONUMGRA002APR,                      &
       BLORECAPR       OF PDTRANCHE          &

  LINK (CIECLE          OF PDTRANCHE,         &
       TRANUMTRA002    OF PDTRANCHE         ,&
       "1")                                   &
    TO CIECLE,                               &
       TRANUMTRA002                         ,&
       DTRPOS          OF PDTRANCHE_DEF      &
                    ALIAS PDTRANCHE_DEF1 OPT &

  LINK (CIECLE          OF PDTRANCHE,         &
       TRANUMTRA002    OF PDTRANCHE     ,    &
       "2")                                   &
    TO CIECLE,                               &
       TRANUMTRA002                         ,&
       DTRPOS          OF PDTRANCHE_DEF      &
                    ALIAS PDTRANCHE_DEF2 OPT

CHOOSE CIECLE PARM , BLONUMBLOAPR PARM

SORT ON CIECLE       OF PDMONTAGE_BLOC &
     ON BLONUMBLOAPR OF PDMONTAGE_BLOC &
     ON RMSNUMSEQ    OF PDMONTAGE_BLOC &
     ON TRANUMTRA002 OF PDTRANCHE

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de vérification de saisie des tranches par bloc" ; Premier titre du rapport
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " "                                                     ; Deuxiemme titre du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD803"                                                 ; Numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM                                                    ; Nom de la compagnie

USE ustitstd45 NOLIST
  SKIP 2 &
  TAB 003 "# BLOC" &
  TAB 016 "# MONTAGE" &
  TAB 030 "# TRANCHE" &
  TAB 044 "LONG" &
  TAB 053 "HAUT" &
  TAB 062 "EPAIS" &
  TAB 072 "DEFAUTS" &
  SKIP 2

REPORT &
  TAB 001 BLONUMBLOAPR OF PDMONTAGE_BLOC PRINT AT BLONUMBLOAPR &
  TAB 014 RMSNUMSEQ    OF PDMONTAGE_BLOC PRINT AT RMSNUMSEQ    &
  TAB 031 TRANUMTRA002 OF PDTRANCHE                            &
  TAB 044 TRALON       OF PDTRANCHE                            &
  TAB 053 TRAHAU       OF PDTRANCHE                            &
  TAB 062 TRAEPA       OF PDTRANCHE                            &
  TAB 073 DTRCODDEF    OF PDTRANCHE_DEF1                       &
  TAB 076 DTRCODDEF    OF PDTRANCHE_DEF2

FOOTING AT BLONUMBLOAPR &
SKIP PAGE

BUILD pd803z
