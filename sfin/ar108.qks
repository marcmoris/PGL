;/T/ Consultation des commandes d'achat CIECLE, ENTCLE, CMDCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 21 mars 1994
;
;    Description..: Permet de visionner et de choisir une commande d'achat.
;
;    Paramètre....: CIECLE ==> - compagnie de la commande
;                              - segment de la clé de recherche.
;
;                   ENTCLE ==> - compagnie de la commande
;                              - segment de la clé de recherche.
;
;                   CMDCLE ==> - segment de la clé de recherche.
;                              - peut contenir le caractère '@'.
;                              - contient la valeur de retour.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar108 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE   , &
                        T_ENTCLE   , &
                        T_CMDCLE   , &
                        T_FOUCLE
 
USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR108"

USE ar108.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4 ; Compagnie de l'entrepôt
TEMPORARY T_CMDCLE CHARACTER SIZE 9 ; Code de commande
TEMPORARY T_ENTCLE CHARACTER SIZE 4 ; Code d'entrepôt
TEMPORARY T_FOUCLE CHARACTER SIZE 6 ; Code fournisseur

;-------------------------------------------------------------------------------
;
FILE ARCOMMANDE        PRIMARY &
                      OCCURS 11

 ACCESS   VIA     CIECLE, &
                  CMDCLE, &
                  CMDAJT  &
          USING   T_CIECLE, &
                  T_CMDCLE, &
                  "000"     &
          ORDERBY CIECLE , &
                  CMDCLE 

  SELECT IF     ( T_ENTCLE = ENTCLE OF ARCOMMANDE OR T_ENTCLE = "" ) &
            AND ( T_FOUCLE = FOUCLE OF ARCOMMANDE OR T_FOUCLE = "" )

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST     
USE usdrwfnd NOLIST     

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(3,3,6)(,,16) (,,24)

CLUSTER OCCURS WITH ARCOMMANDE

FIELD CMDCLE    OF ARCOMMANDE HIDDEN LABEL ">>"
FIELD FOUCLE    OF ARCOMMANDE
FIELD CMDCOM    OF ARCOMMANDE SIZE 15


CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_CMDCLE, "@")
  THEN LET T_CMDCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_CMDCLE = CMDCLE OF ARCOMMANDE
  RETURN
END

BUILD
@IF FORMAT
                                       *****************************************
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       * >> xxxxxxxxx xxxxxx  xxxxxxxxxxxxxxx  *
                                       *****************************************
@ENDIF
