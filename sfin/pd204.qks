;/T/ Classe de granit
;
;/P/ Programmeur..: René Bonneau
;    Date création: 1994-11-03
;
;    Description..: Ce panorama permet de saisir les classes de granit.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd204 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD204"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd204.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimun
;

USE usdimprd NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Type de granit / classe      " ACTION DESIGNER D001
  MENUITEM LABEL "Type de fini / classe        " ACTION DESIGNER D002
  MENUITEM LABEL "Dimension ventes             " ACTION DESIGNER D003
  MENUITEM LABEL "Vérification                 " ACTION DESIGNER D004
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Type de granit / classe   " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Type de fini / classe     " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Dimension ventes          " ACTION DESIGNER D003
  MENUITEM LABEL "%%%Vérification              " ACTION DESIGNER D004
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Classe de granit
;

FILE PDCLASSE         PRIMARY NOITEM

  ACCESS VIA     CIECLE,                       &
                 CLGCODCLA                     &
         USING   T_CIECLEMNU,                  &
                 CLGCODCLA        OF PDCLASSE  &
         REQUEST CLGCODCLA        OF PDCLASSE  &
         ORDERBY CIECLE,                       &
                 CLGCODCLA

  ACCESS VIA     CIECLE         &
         USING   T_CIECLEMNU    &
         ORDERBY CIECLE,        &
                 CLGCODCLA


  ITEM   CIECLE       OF PDCLASSE INITIAL T_CIECLEMNU
  ITEM   CLGDATCRE    OF PDCLASSE INITIAL SYSDATE
  ITEM   CLGHRECRE    OF PDCLASSE INITIAL SYSTIME / 10000
  ITEM   CLGUSRCRE    OF PDCLASSE INITIAL LOGONID

  ITEM   CLGDATMOD    OF PDCLASSE FINAL SYSDATE &
                                        IF NOT NEWRECORD OF PDCLASSE AND &
                                           ALTEREDRECORD     OF PDCLASSE
  ITEM   CLGHREMOD    OF PDCLASSE FINAL SYSTIME / 10000 &
                                        IF NOT NEWRECORD OF PDCLASSE AND &
                                           ALTEREDRECORD     OF PDCLASSE
  ITEM   CLGUSRMOD    OF PDCLASSE FINAL LOGONID &
                                        IF NOT NEWRECORD OF PDCLASSE AND &
                                           ALTEREDRECORD     OF PDCLASSE


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP

TITLE &
@IF FRANCAIS
      " Classe de granit " &
@ELSE
      " %%%Classe de granit " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (2,3,19)


FIELD CLGCODCLA        OF PDCLASSE                &
label "Code classe...:"&
        HIDDEN ID 05                              &
        REQUIRED                                  &
        LOOKUP NOTON PDCLASSE                     &
          MESSAGE 3022 ; Cette classe de granit existe déjà


FIELD CLGDATFIN        OF PDCLASSE                 FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date fin......:"&
        HIDDEN ID 10                              &
        REQUIRED


FIELD CLGDSCFRA        OF PDCLASSE                &
label "Descr français:"&
        HIDDEN ID 15                              &
        REQUIRED


FIELD CLGDSCANG        OF PDCLASSE                &
label "Descr anglais.:"&
        HIDDEN ID 20                              &
        REQUIRED



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

END


;-------------------------------------------------------------------------------
;
; Type de granit / classe
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd204a &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDCLASSE            &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Type de fini / classe
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd204b &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDCLASSE            &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Dimension ventes
;

PROCEDURE DESIGNER D003
BEGIN
  RUN SCREEN  pd204c &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDCLASSE            &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Vérification classe de granit
;

PROCEDURE DESIGNER D004
BEGIN
  RUN SCREEN  pd204d &
              PASSING   PDCLASSE,       &
                        T_CIECLEMNU,    &
                        T_CIENOM        &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDCLASSE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1

  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDCLASSE &
     AND NOT NEWRECORD     OF PDCLASSE &
     AND NOT DELETEDRECORD OF PDCLASSE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END


;-------------------------------------------------------------------------------
;
; Cette procedure permet de ne pas effacer l'ecran après l'UPDATE
;

PROCEDURE POSTUPDATE
BEGIN
  IF CORRECTMODE
  THEN BEGIN
    DISPLAY CLGCODCLA        OF PDCLASSE
    WARNING = " "
  END
END


BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Classe de granit *******************************
*                                                                              *
* Code classe...: xxx                                                          *
* Date fin......: xxxxxxxx                                                     *
* Descr français: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Descr anglais.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
