;/T/ Paramètres initiaux(Holding)
;
;/P/ Programmeur..: Guy Chaobt
;    Date Création: 20-Feb-1992
;
;    Description..: Cet écran permet de gérer l'ensemble de l'application
;                   avec tous ses flags. (notion de projet...)
;                   Les valeurs dans cet écran sont non modifiable.
;
;* Numérotation..: x ('S' OU 'A')            Non modifiable
;*                   si S alors les écritures de journal
;*                              sont saisies par l'usager
;*                   si A alors les écritures de journal
;*                              sont toujours générées
;*                              automatiquement par le systeme
;* Identifiant...: x                         Non modifiable
;*                   si Numérotation S alors obligatoire
;*                   si Numérotation A alors ne s'applique pas
;*
;-------------------------------------------------------------------------------
;
; /M/ Nancy Marceau , 94/02/24, Adaptation module Achats et Réceptions

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc001 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_FLGHLDOK, &
                       T_CIENOM

USE ussectmp NOLIST
    "MC001"

USE mc001.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le Holding est présent.
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding


FILE MCHOLDING PRIMARY NOITEM NODELETE
 ACCESS VIA CIENMC &
        USING "1"
 ITEM CIENMC OF MCHOLDING INITIAL "1"

;
; Validation de la devise.
;
FILE VDEV_DSC         REFERENCE
 ACCESS VIA   DEVCLE &
        USING DEVCLE OF MCHOLDING

;
; Validation du type de charte de compte et du type d'écriture.
;
FILE VCBI_DSC DESIGNER OPEN READ

;-------------------------------------------------------------------------------
;
TEMPORARY T_DEVCLE CHARACTER SIZE 3 ; Popup sur les devises
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_HLDCHTCPTRLT CHARACTER SIZE 16 = "HLDCHTCPTRLT"
TEMPORARY T_HLDCHTCPTRLT CHARACTER SIZE 4
DEFINE    D_HLDECRAUT    CHARACTER SIZE 16 = "HLDECRAUT"
TEMPORARY T_HLDECRAUT    CHARACTER SIZE 4

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Paramètres initiaux " &
@ELSE
      " %%%Paramètres initiaux " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP TO 6

ALIGN (26,26,42) (49,49,65)
FIELD HLDNTNPRO       OF MCHOLDING HIDDEN ID 05 REQUIRED NOCHANGE &
        LOOKUP NOTON MCHOLDING &
          VIA CIENMC &
          USING "1"  &
          MESSAGE 101

FIELD HLDCHRSUB       OF MCHOLDING HIDDEN ID 10 &
        NOENTRY
SKIP 1

ALIGN(26,26,42)
FIELD HLDINTCIEBALCIE OF MCHOLDING HIDDEN ID 20   REQUIRED NOCHANGE
SKIP 1

ALIGN (26,26,42) (49,49,65)
FIELD HLDCHTCPT       OF MCHOLDING HIDDEN ID 25 &
        REQUIRED

FIELD HLDCHTCPTRLT    OF MCHOLDING HIDDEN ID SAME &
        REQUIRED &
        IF HLDCHTCPT OF MCHOLDING = "1" &
        LOOKUP ON VCBI_DSC &
          VIA XELNOM, &
              CBICLE &
          USING D_HLDCHTCPTRLT, &
                HLDCHTCPTRLT OF MCHOLDING &
          MESSAGE 112 ; Code invalide

ALIGN(26,26,42)
FIELD HLDCHTCLI       OF MCHOLDING HIDDEN ID 30 REQUIRED NOCHANGE
FIELD HLDCHTFOU       OF MCHOLDING HIDDEN ID 32 REQUIRED NOCHANGE
FIELD HLDCHTEMP       OF MCHOLDING HIDDEN ID 33 REQUIRED NOCHANGE
SKIP 1

ALIGN(26,26,42)(,,47)
FIELD DEVCLE          OF MCHOLDING HIDDEN ID 35 &
        REQUIRED NOCHANGE  &
        LOOKUP ON VDEV_DSC &
          MESSAGE 103
FIELD DEVDSC          OF VDEV_DSC  DISPLAY

ALIGN(26,26,42)
FIELD HLDNBRPERCTB    OF MCHOLDING HIDDEN ID 40 REQUIRED NOCHANGE
SKIP 1

ALIGN (26,26,42) (49,49,65)
FIELD HLDECRAUT       OF MCHOLDING HIDDEN ID 45 &
        REQUIRED NOCHANGE  &
        LOOKUP ON VCBI_DSC &
          VIA XELNOM, &
              CBICLE &
          USING D_HLDECRAUT, &
                HLDECRAUT OF MCHOLDING &
          MESSAGE 112 ; Code invalide
FIELD HLDECRCOD       OF MCHOLDING HIDDEN ID SAME REQUIRED IF HLDECRAUT = "S" &
                                                  NOCHANGE

SKIP 1

ALIGN (26,26,42) (49,49,65)
FIELD HLDREQACH      OF MCHOLDING HIDDEN ID 50 &
  REQUIRED &
  NOCHANGE

FIELD HLDCMDACH      OF MCHOLDING HIDDEN ID SAME &
  REQUIRED &
  NOCHANGE

SKIP

ALIGN (26,26,42)
FIELD HLDMOVINV      OF MCHOLDING HIDDEN ID 55  &
  REQUIRED &
  NOCHANGE


@IF FRANCAIS
 TITLE "Options            :" AT  6,3
 TITLE "Méthode Comptable  :" AT  9,3
 TITLE "Gestion Chartes    :" AT 11,3
 TITLE "Ecriture de journal:" AT 19,3
 TITLE "Numérotation achat :" AT 21,3
 TITLE "Num. inventaire    :" AT 22,3
@ELSE
 TITLE "Options            :" AT  6,3
 TITLE "Accounting Method  :" AT  9,3
 TITLE "Charts Management  :" AT 11,3
 TITLE "General Ledger     :" AT 19,3
 TITLE "%Numérotation achat:" AT 21,3
 TITLE "%Num. inventaire   :" AT 22,3
@ENDIF


;-------------------------------------------------------------------------------
;
;
; Pour la vérification de la sécurit
;
;
PROCEDURE INITIALIZE
BEGIN
  IF T_FLGHLDOK = "1"
  THEN BEGIN
    USE ussececr NOLIST
  END
END

;-------------------------------------------------------------------------------
;
;
; utilisation de notion de projet
;
;
PROCEDURE INPUT HLDNTNPRO
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDNTNPRO
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Balancement des ecritures inter-cie au Grand Livre.
;
;
PROCEDURE INPUT HLDINTCIEBALCIE
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDINTCIEBALCIE
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
;
; utilisation d'une charte compte (compte + unite + [cen])
;
;
PROCEDURE INPUT HLDCHTCPT
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Si on n'utilise pas la charte de compte on place la relation de charte
; à blanc.
;
;
PROCEDURE PROCESS HLDCHTCPT
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "0"
  THEN BEGIN
    LET     HLDCHTCPTRLT OF MCHOLDING = " "
    DISPLAY HLDCHTCPTRLT OF MCHOLDING
  END
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDCHTCPT
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Appel du popup pour le type de charte de compte.
;
;
PROCEDURE INPUT HLDCHTCPTRLT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_HLDCHTCPTRLT = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_HLDCHTCPTRLT, T_HLDCHTCPTRLT
    LET FIELDTEXT = TRUNC(T_HLDCHTCPTRLT)
  END
END

;-------------------------------------------------------------------------------
;
;
; utilisation d'une charte de client (au niveau de la compagnie = oui)
;                                    (au niveau du holding      = non)
;
;
PROCEDURE INPUT HLDCHTCLI
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDCHTCLI
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Utilistaion de la charte de fournisseurs (au niveau des compagnies = oui)
;                                          (au niveau du holding     = non)
;
;
PROCEDURE INPUT HLDCHTFOU
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDCHTFOU
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Utilisation de la charte des employes (au niveau des compagnies = oui)
;                                       (au niveau du holding     = non)
;
;
PROCEDURE INPUT HLDCHTEMP
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDCHTEMP
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Aide au choix de la devise pour la consultaion.
;
;
PROCEDURE INPUT DEVCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DEVCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc101 PASSING T_DEVCLE MODE F
    LET FIELDTEXT = TRUNC(T_DEVCLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Appel du popup pour le type de numérotation des écritures de journal.
;
;
PROCEDURE INPUT HLDECRAUT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_HLDECRAUT = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_HLDECRAUT, T_HLDECRAUT
    LET FIELDTEXT = TRUNC(T_HLDECRAUT)
  END
END

;-------------------------------------------------------------------------------
;
;
; Si on utilise la numérotation automatique des écritures,
; on met le code d'identifiant à blanc.
;
;
PROCEDURE PROCESS HLDECRAUT
BEGIN
  IF HLDECRAUT OF MCHOLDING = "A"
  THEN BEGIN
    LET HLDECRCOD OF MCHOLDING = " "
    DISPLAY HLDECRCOD OF MCHOLDING
  END
END


;-------------------------------------------------------------------------------
;
;
;
PROCEDURE INPUT HLDECRCOD
BEGIN
  IF 0 = SIZE(TRUNC(FIELDTEXT)) AND HLDECRAUT OF MCHOLDING = "S"
  THEN ERROR 102
END

;-------------------------------------------------------------------------------
; Numérotaion achat réquisition
;
PROCEDURE INPUT HLDREQACH
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDREQACH
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
; Numérotaion achat commande
;
PROCEDURE INPUT HLDCMDACH
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDCMDACH
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
; Numérotaion inventaire mouvement
;
PROCEDURE INPUT HLDMOVINV
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE OUTPUT HLDMOVINV
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 25
BEGIN
  ACCEPT HLDCHTCPT OF MCHOLDING
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN ACCEPT HLDCHTCPTRLT OF MCHOLDING
END

;-------------------------------------------------------------------------------
;
;
; Si la numérotation des écritures du journal général est de type Saisie,
; le code qui intifie les écritures automatique doit être saisie.
;
;
PROCEDURE DESIGNER 45
BEGIN
  ACCEPT HLDECRAUT OF MCHOLDING
  IF HLDECRAUT OF MCHOLDING = "S"
  THEN BEGIN
    ACCEPT HLDECRCOD OF MCHOLDING
  END
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF MCHOLDING &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF MCHOLDING &
     AND NOT NEWRECORD     OF MCHOLDING &
     AND NOT DELETEDRECORD OF MCHOLDING &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Paramètres initiaux ******************************
*                                                                              *
*                                                                              *
* Options            :   Notion projet.: x                                     *
*                        Caractère Subs: xxxxxxxxxxxxxxxxxxxx                  *
*                                                                              *
* Méthode Comptable  :   Bal. compagnie: x                                     *
*                                                                              *
* Gestion Chartes    :   Charte compte.: x      Relation cpt..: x              *
*                        Charte client.: x                                     *
*                        Charte fournis: x                                     *
*                        Charte employé: x                                     *
*                                                                              *
*                        Code devise...: xxx  xxxxxxxxxxxxxxx                  *
*                        Nombre période: xx                                    *
*                                                                              *
* Ecriture de journal:   Numérotation..: x      Identifiant...: x              *
*                                                                              *
* Numérotation achat :   Réquisition...: x      Commande......: x              *
* Num. inventaire    :   Mouvement.....: x                                     *
********************************************************************************
@ENDIF
