;/T/ Immobilisation - Informations comptables
;
;/P/ Programmeur..: Alain Côté
;    Date création: 21 février 1994
;
;/M/ François Déry, 30 avril 1999
;       Modifier l'accèss au fichier de séquence car il a
;       été converti en format interbase au lieu d'indexé.
;
;/V3.02.00/ Alain Côté, 21 février 1994, Immobilisation
;
;    Description..: Cet écran sert à inscrire les comptes comptables qui sont
;                   affectés lors d'une transaction avec une immobilisation.
;
;                   Le champ 'Actif' est le compte dans lequel sera affecté
;                   le montant initial de l'immobilisation lors d'une
;                   aliénation.
;
;                   Le champ 'Amortissement accumulé' est le compte créditeur
;                   de l'amortissement.
;
;                   Le champ 'Dépense amortissement' est le compte débiteur de
;                   l'amortissement.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001a SLAVE &
              MODE LABEL "" AT 1,80 NOACTION &
              FIELDMARK RETAIN STARTUP &
              FROM 11,1 TO 23,80       &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING IMIMMOBILISATION , &
                        T_CIECLEMNU      , &
                        T_CIENOM

;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "IM001A"

;
; Documentation de l'écran.
;
USE im001a.hlp NOLIST


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.
;
; Sert à la génération du numéro de séquence
;
TRANSACTION GEN_NUM_SEQ READ WRITE &
                        RESERVING FOR PROTECTED WRITE IMIMM_SEQ IN DICT
                                                ; Le IN DICT est pour unix


;-------------------------------------------------------------------------------
;
FILE IMIMMOBILISATION MASTER

;
; Validation du compte d'actif.
;
FILE VCPT_DSC         REFERENCE
  ACCESS VIA   CPTCLE &
         USING IMMCPTACT OF IMIMMOBILISATION

;
; Validation du compte d'amortissement accumulé.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_AMTACC
  ACCESS VIA   CPTCLE &
         USING IMMCPTAMTACC OF IMIMMOBILISATION

;
; Validation du compte dépense d'amortissement.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_DEPAMT
  ACCESS VIA   CPTCLE &
         USING IMMCPTDEPAMT OF IMIMMOBILISATION

;
; Validation de l'unité administrative du compte d'actif.
;
FILE MCUNITE_ADM      REFERENCE
  ACCESS VIA   CIECLE , &
               UNACLE   &
         USING CIECLE    OF IMIMMOBILISATION , &
               IMMUNAACT OF IMIMMOBILISATION

;
; Validation de l'unité administrative du compte d'amortissement accumulé.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_AMTACC
  ACCESS VIA   CIECLE , &
               UNACLE   &
         USING CIECLE       OF IMIMMOBILISATION , &
               IMMUNAAMTACC OF IMIMMOBILISATION


;
; Validation de l'unité administrative du compte de dépense d'amortissement.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_DEPAMT
  ACCESS VIA   CIECLE , &
               UNACLE   &
         USING CIECLE       OF IMIMMOBILISATION , &
               IMMUNADEPAMT OF IMIMMOBILISATION

;
; Validation de la charte de compte.
;
FILE MCCHARTE_UNA     DESIGNER &
                      OPEN READ SHARE

;
; Numéro séquentiel des immobilisations, fichier indexé.
;
FILE IMIMM_SEQ        DESIGNER &
                      TRANSACTION GEN_NUM_SEQ
  ACCESS VIA   CIECLE &
         USING CIECLE OF IMIMM_SEQ
  ITEM CIECLE OF IMIMM_SEQ INITIAL CIECLE OF IMIMMOBILISATION

;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA   CIENMC &
         USING "1"


;-------------------------------------------------------------------------------
;
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes et unité adm.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes
TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur les unités adm.

TEMPORARY T_CPTUNAACT_SIL    CHARACTER SIZE 1 ; Validation charte de compte.
TEMPORARY T_CPTUNAAMTACC_SIL CHARACTER SIZE 1 ; Validation charte de compte.
TEMPORARY T_CPTUNADEPAMT_SIL CHARACTER SIZE 1 ; Validation charte de compte.


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 01,01 TO 01,79
DRAW 01,01 TO 13,01
DRAW 13,01 TO 13,80
DRAW 02,80 TO 13,80

TITLE &
@IF FRANCAIS
      " Informations comptables " &
@ELSE
      " %%%Informations comptables " &
@ENDIF
      AT ,1 CENTERED

HILITE TITLE OFF

SKIP 1

TITLE &
@IF FRANCAIS
      "Compte Description          Unité    Description" &
@ELSE
      "%%%pte Description          Unité    Description" &
@ENDIF
      AT ,19

SKIP 1

ALIGN (19,3,19) (,,26) (,,47) (,,56)

FIELD IMMCPTACT OF IMIMMOBILISATION ID 05 HIDDEN &
        REQUIRED &
        LOOKUP ON VCPT_DSC &
         MESSAGE 1615 ; Ce compte comptable n'existe pas.

FIELD CPTDSCABR OF VCPT_DSC &
        DISPLAY

FIELD IMMUNAACT OF IMIMMOBILISATION &
        LOOKUP ON MCUNITE_ADM &
          MESSAGE 1614 ; Cette unité administrative n'existe pas.

FIELD UNANOMABR OF MCUNITE_ADM &
        DISPLAY

FIELD T_CPTUNAACT_SIL &
        SILENT

SKIP 1

FIELD IMMCPTAMTACC OF IMIMMOBILISATION ID 10 HIDDEN &
        REQUIRED &
        LOOKUP ON A_CPT_AMTACC &
          MESSAGE 1615 ; Ce compte comptable n'existe pas

FIELD CPTDSCABR OF A_CPT_AMTACC &
        DISPLAY

FIELD IMMUNAAMTACC OF IMIMMOBILISATION &
        REQUIRED &
        LOOKUP ON A_UNA_AMTACC &
          MESSAGE 1614 ; Cette unité administrative n'existe pas.

FIELD UNANOMABR OF A_UNA_AMTACC &
        DISPLAY

FIELD T_CPTUNAAMTACC_SIL &
        SILENT

SKIP 1

FIELD IMMCPTDEPAMT OF IMIMMOBILISATION ID 15 HIDDEN &
        REQUIRED &
        LOOKUP ON A_CPT_DEPAMT &
         MESSAGE 1615 ; Ce compte comptable n'existe pas.

FIELD CPTDSCABR OF A_CPT_DEPAMT &
        DISPLAY

FIELD IMMUNADEPAMT OF IMIMMOBILISATION &
        REQUIRED &
        LOOKUP ON A_UNA_DEPAMT &
          MESSAGE 1614 ; Cette unité administrative n'existe pas.

FIELD UNANOMABR OF A_UNA_DEPAMT &
        DISPLAY

FIELD T_CPTUNADEPAMT_SIL &
        SILENT


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Compte comptable d'actif, popup sur les comptes.
;
;
PROCEDURE INPUT IMMCPTACT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Unité adm. du compte de gain, popup sur les unités administratives.
;
;
PROCEDURE INPUT IMMUNAACT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = IMMCPTACT OF IMIMMOBILISATION
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F &
                          PASSING T_CIECLEMNU , &
                                  T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
      ELSE RUN SCREEN mc116 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE( T_UNACLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT T_CPTUNAACT_SIL
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA CPTCLE , &
                         CIECLE , &
                         UNACLE   &
                     USING IMMCPTACT OF IMIMMOBILISATION , &
                           CIECLE    OF IMIMMOBILISATION , &
                           IMMUNAACT OF IMIMMOBILISATION   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1620 ; La combinaison compte/unité administrative n'existe pas.
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; Compte comptable d'actif, popup sur les comptes.
;
;
PROCEDURE INPUT IMMCPTAMTACC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Unité adm. du compte de gain, popup sur les unités administratives.
;
;
PROCEDURE INPUT IMMUNAAMTACC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = IMMCPTAMTACC OF IMIMMOBILISATION
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F &
                          PASSING T_CIECLEMNU , &
                                  T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
      ELSE RUN SCREEN mc116 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE( T_UNACLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT T_CPTUNAAMTACC_SIL
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA CPTCLE , &
                         CIECLE , &
                         UNACLE   &
                     USING IMMCPTAMTACC OF IMIMMOBILISATION , &
                           CIECLE       OF IMIMMOBILISATION , &
                           IMMUNAAMTACC OF IMIMMOBILISATION   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1620 ; La combinaison compte/unité administrative n'existe pas.
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; Compte comptable d'actif, popup sur les comptes.
;
;
PROCEDURE INPUT IMMCPTDEPAMT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Unité adm. du compte de gain, popup sur les unités administratives.
;
;
PROCEDURE INPUT IMMUNADEPAMT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = IMMCPTDEPAMT OF IMIMMOBILISATION
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F &
                          PASSING T_CIECLEMNU , &
                                  T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
      ELSE RUN SCREEN mc116 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE( T_UNACLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de la charte de compte.
; La charte de compte peut être soit incluse, ou soit excluse.
;
;
PROCEDURE EDIT T_CPTUNADEPAMT_SIL
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "1"
  THEN BEGIN
    GET MCCHARTE_UNA VIA CPTCLE , &
                         CIECLE , &
                         UNACLE   &
                     USING IMMCPTDEPAMT OF IMIMMOBILISATION , &
                           CIECLE       OF IMIMMOBILISATION , &
                           IMMUNADEPAMT OF IMIMMOBILISATION   &
                     OPTIONAL
    ;
    ; Si la charte est incluse, le lien est obligatoire.
    ;
    IF NOT ACCESSOK
    THEN BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN ERROR 1620 ; La combinaison compte/unité administrative n'existe pas.
    END
  END
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF   DELETEDRECORD OF IMIMMOBILISATION &
    AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification
  ;
  IF    ALTEREDRECORD OF IMIMMOBILISATION &
    AND NOT NEWRECORD OF IMIMMOBILISATION &
    AND NOT DELETEDRECORD OF IMIMMOBILISATION &
    AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; On assigne un numéro à la fiche d'immobilisation.
  ;
  IF "" = TRUNCATE( IMMCLE OF IMIMMOBILISATION )
  THEN BEGIN
    START TRANSACTION GEN_NUM_SEQ
    GET IMIMM_SEQ OPTIONAL
    LET IMSNUMSEQ OF IMIMM_SEQ = ASCII( &
                                        NCONVERT(IMSNUMSEQ OF IMIMM_SEQ) + 1 , &
                                        IMSLONNUM OF IMIMM_SEQ )
    LET IMMCLE    OF IMIMMOBILISATION = IMSNUMSEQ OF IMIMM_SEQ
    PUT IMIMM_SEQ
    COMMIT TRANSACTION GEN_NUM_SEQ
  END
END

BUILD
@IF FORMAT

*************************** Informations comptables ***************************x
*                                                                              *
*                 Compte Description          Unité    Description             *
*                                                                              *
* Actif.........: xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
* Amort.accumulé: xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
* Dépense amort.: xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
