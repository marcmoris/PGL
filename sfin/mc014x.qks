;/T/ Comptes de banque - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 1 septembre 1993
;
;    Description..: Cet écran vérifie si le compte de banque reçu en paramètre
;                   est référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc014x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCLIVRET_BQ , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Compte de banque.
;
FILE MCLIVRET_BQ      MASTER

;
; Fournisseur par compagnie.
;
FILE CPFOU_CIE        REFERENCE

;
; Compte à payer - Paramètre par compagnie.
;
FILE CPCIE_PARAM      REFERENCE

;
; Paiement.
;
FILE CPPAIEMENT       REFERENCE

;
; Conciliation des chèques.
;
FILE CPCONCIL_CHQ     REFERENCE

;
; Numérotation des chèques.
;
FILE CPRENUM_PAM      REFERENCE

;
; Client par compagnie.
;
FILE CRCLI_CIE        REFERENCE

;
; Encaissements.(encaissement sans client)
;
FILE CRDEPOT          REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans les fournisseurs par compagnie.
  ;
  GET CPFOU_CIE VIA FOCCIECLE , &
                    LBQCPTENC   &
                USING CIECLE    OF MCLIVRET_BQ , &
                      LBQCPTENC OF MCLIVRET_BQ   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 159 ; Destruction annulée, cette information est référée par un
                 ; fournisseur-compagnie.

  ;
  ; On vérifie dans les paramètres par compagnie des C/P.
  ;
  GET CPCIE_PARAM VIA CIECLE    , &
                      LBQCPTENC   &
                  USING CIECLE    OF MCLIVRET_BQ , &
                        LBQCPTENC OF MCLIVRET_BQ   &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 172 ; Destruction annulée, cette information est référée dans les
                 ; paramètres des C/P.

  ;
  ; On vérifie dans les paiements. Un paiement n'est pas toujours lié à
  ; un fournisseur, un paiement fait à un employé par exemple.
  ;
  GET CPPAIEMENT VIA CIECLE    , &
                     LBQCPTENC   &
                USING CIECLE    OF MCLIVRET_BQ , &
                      LBQCPTENC OF MCLIVRET_BQ   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 161 ; Destruction annulée, cette information est référée par un
                 ; paiement.

  ;
  ; On vérifie dans la conciliation des chèques.
  ;
  GET CPCONCIL_CHQ VIA CIECLE    , &
                       LBQCPTENC   &
                   USING CIECLE    OF MCLIVRET_BQ , &
                         LBQCPTENC OF MCLIVRET_BQ   &
                   OPTIONAL
  IF ACCESSOK
  THEN ERROR 173 ; Destruction annulée, cette information est référée dans
                 ; la conciliation.

  ;
  ; On vérifie dans la table de numérotation des chèques.
  ;
  GET CPRENUM_PAM VIA CIECLE    , &
                      LBQCPTENC   &
                  USING CIECLE    OF MCLIVRET_BQ , &
                        LBQCPTENC OF MCLIVRET_BQ   &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 174 ; Destruction annulée, cette information est référée par
                 ; une numérotation de chèque.

  ;
  ; On vérifie dans les clients par compagnie.
  ;
  GET CRCLI_CIE VIA CIECLE    , &
                    LBQCPTENC   &
                USING CIECLE    OF MCLIVRET_BQ , &
                      LBQCPTENC OF MCLIVRET_BQ   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 162 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.

  ;
  ; On vérifie dans les encaissements, due au encaissement qui ne sont pas
  ; lié à un client.
  ;
  GET CRDEPOT VIA CIECLE    , &
                  LBQCPTENC   &
              USING CIECLE    OF MCLIVRET_BQ , &
                    LBQCPTENC OF MCLIVRET_BQ   &
              OPTIONAL
  IF ACCESSOK
  THEN ERROR 175 ; Destruction annulée, cette information est référée par un
                 ; encaissement.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
