;/T/ Liste de vérification de saisie des caissons
;
;/P/ Programmeur.....: Martin Bruyère
;    Date création...: 1996-06-13
;    Description.....: Liste de vérification de saisie des caissons
;
;-------------------------------------------------------------------------------
CANCEL CLEAR

USE ussetqzs NOLIST


ACCESS PDCAISSON                       &

  LINK CIECLE       OF PDCAISSON,      &
       CAINUMCAI    OF PDCAISSON       &
    TO CIECLE,                         &
       CAINUMCAI    OF PDEMBAL_DIAG    OPTIONAL &

  LINK CIECLE       OF PDCAISSON,      &
       DIANUM002    OF PDEMBAL_DIAG    &
    TO CIECLE,                         &
       DIANUM002    OF PDDIAGRAMME     OPTIONAL &

  LINK CIECLE       OF PDCAISSON,      &
       CAINUMCAI    OF PDCAISSON       &
    TO CIECLE,                         &
       CAINUMCAI    OF PDEMBAL_TRANCHE OPTIONAL &

  LINK CIECLE       OF PDCAISSON,      &
       TRANUMTRA002 OF PDEMBAL_TRANCHE &
    TO CIECLE,                         &
       TRANUMTRA002 OF PDTRANCHE       OPTIONAL &

  LINK CIECLE       OF PDCAISSON,      &
       PJTNUMPRO    OF PDCAISSON       &
    TO CIECLE,                         &
       PJTNUMPRO    OF PDPROJET        OPTIONAL


DEFINE D_NUMPRODUIT       CHARACTER SIZE 07 = DIANUM002    OF PDEMBAL_DIAG   IF TPDTYPPRD OF PDCAISSON = "DI" &
                                         ELSE TRANUMTRA002 OF PDEMBAL_TRANCHE

CHOOSE CIECLE PARM , CAINUMCAI PARM


SORT ON CAINUMCAI &
     ON D_NUMPRODUIT



;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste de vérification de saisie des caissons" ; Premier titre du rapport
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""                                             ; Deuxiemme titre du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD801"                                        ; Numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM                                           ; Nom de la compagnie


;-------------------------------------------------------------------------------
;
; Définition des éléments variables
;


DEFINE D_TRGCODGRA CHARACTER SIZE 03 = TGRCODGRA OF PDDIAGRAMME IF TPDTYPPRD OF PDCAISSON = "DI" &
                                  ELSE TGRCODGRA OF PDTRANCHE

DEFINE D_TYFCODFIN CHARACTER SIZE 02 = TYFCODFIN OF PDDIAGRAMME IF TPDTYPPRD OF PDCAISSON = "DI" &
                                  ELSE TYFCODFIN OF PDTRANCHE

DEFINE D_EPAIS     INTEGER SIGNED SIZE 04 = DIAEPA OF PDDIAGRAMME IF TPDTYPPRD OF PDCAISSON = "DI" &
                                       ELSE EMTEPAVEN OF PDEMBAL_TRANCHE

DEFINE D_LONG      INTEGER SIGNED SIZE 04 = DIALON OF PDDIAGRAMME IF TPDTYPPRD OF PDCAISSON = "DI" &
                                       ELSE EMTLONVEN OF PDEMBAL_TRANCHE

DEFINE D_HAUT      INTEGER SIGNED SIZE 04 = DIAHAU OF PDDIAGRAMME IF TPDTYPPRD OF PDCAISSON = "DI" &
                                       ELSE EMTHAUVEN OF PDEMBAL_TRANCHE

DEFINE D_QTE       INTEGER SIGNED SIZE 04 = EMDQTEPRD OF PDEMBAL_DIAG IF TPDTYPPRD OF PDCAISSON = "DI" &
                                       ELSE EMTQTEPRD OF PDEMBAL_TRANCHE

DEFINE D_PRICODPRI        CHARACTER SIZE 02 = PRICODPRI OF PDEMBAL_DIAG IF TPDTYPPRD OF PDCAISSON = "DI" &
                               ELSE ""


USE ustitstd45 NOLIST
  SKIP 2 &
  TAB 001 "                                                    TRANCHE/" &
  SKIP   &
  TAB 001 "CAISSON   PROJET                                    DIAGRAMME  GRANIT  FINI   ÉPAIS   LONG    HAUT       QTE   PRIORITE" &
  SKIP


REPORT &
  TAB 001 CAINUMCAI OF PDCAISSON PRINT AT CAINUMCAI &
  TAB 011 PJTNOM    OF PDPROJET  PRINT AT CAINUMCAI &
  TAB 055 D_NUMPRODUIT           &
  TAB 066 D_TRGCODGRA            &
  TAB 073 D_TYFCODFIN            &
  TAB 079 D_EPAIS PICTURE "^^^^" &
  TAB 087 D_LONG  PICTURE "^^^^" &
  TAB 095 D_HAUT  PICTURE "^^^^" &
  TAB 103 D_QTE   PICTURE "^^^^^^" &
  TAB 112 D_PRICODPRI

FOOTING AT CAINUMCAI             &
   SKIP 2                        &
   TAB 80 "TOTAL.........:"      &
   TAB 103 D_QTE SUBTOTAL  PICTURE "^^^^^^" &
   SKIP 2


BUILD pd801z

@IF FORMAT

                                                    TRANCHE/
CAISSON   PROJET                                    DIAGRAMME  GRANIT  FINI   ÉPAIS   LONG    HAUT       QTE PRIORITE
XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX    XXXXXXX    XXX    XX    XXXX    XXXX    XXXX    XXXXXX XX



@ENDIF
