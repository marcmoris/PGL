;/T/ Gestion des unités de mesures
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 14 décembre 1994
;
;    Description..: Définition des unités de mesures. Ces unités
;                   de mesures sont communes à toutes les compagnies.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gp153 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "GP153"

USE gp153.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Numéro compagnie
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom du holding

;-------------------------------------------------------------------------------
;
; Unités de mesures
;
FILE GPUNITE_MESURE   PRIMARY &
                      OCCURS 16 &
                      NOITEMS

  ACCESS  VIA     UNMCLE &
          USING   UNMCLE OF GPUNITE_MESURE &
          REQUEST UNMCLE &
          ORDERBY UNMCLE

  ACCESS  VIA     UNMABR &
          USING   UNMABR OF GPUNITE_MESURE &
          REQUEST UNMABR &
          ORDERBY UNMCLE

  ACCESS  SEQUENTIAL &
          ORDERBY UNMCLE

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Unités de mesures " &
@ELSE
      "%%%Unités de mesures " &
@ENDIF
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST

TITLE &
@IF FRANCAIS
    "Code Description                      Abréviation    Nombre de décimal" &
@ELSE
    "Cod  Description                      Abreviation    Decimal number"    &
@ENDIF
  AT 5,8
SKIP TO 6

ALIGN(8,6,8)(,,13)(,,50)(,,68)

CLUSTER OCCUR WITH GPUNITE_MESURE  ID BASE 10

FIELD UNMCLE OF GPUNITE_MESURE  &
      LABEL " " HIDDEN &
      REQUIRED NOCHANGE &
      LOOKUP NOTON GPUNITE_MESURE  &
        VIA   UNMCLE &
        USING UNMCLE OF GPUNITE_MESURE &
        MESSAGE 815; Cette unités de mesure existe déjà

FIELD UNMDSC       OF GPUNITE_MESURE

FIELD UNMABR       OF GPUNITE_MESURE

FIELD UNMNBRDEC    OF GPUNITE_MESURE &
      DEFAULT 2
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GPUNITE_MESURE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF DELETEDRECORD OF GPUNITE_MESURE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification
    ;
    IF    ALTEREDRECORD     OF GPUNITE_MESURE &
      AND NOT NEWRECORD     OF GPUNITE_MESURE &
      AND NOT DELETEDRECORD OF GPUNITE_MESURE &
      AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Destruction de l'unité de mesure
    ;
    IF DELETEDRECORD OF GPUNITE_MESURE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si le type d'unites de travail est
      ; référée dans la définition de unités de travail.
      ;
      RUN SCREEN gp153x MODE SAME &
                        PASSING GPUNITE_MESURE, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Unités de mesures *******************************
*                                                                              *
*      Code Description                     Abréviation    Nombre de décimal   *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*      xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xx                xx           *
*                                                                              *
********************************************************************************
@ENDIF
