;/T/ 2.4.1 Enregistrer le montage.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-17
;
;    Description..: Cette unité de traitement permet de consulter les
;                   informations
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd183e ACTIONBAR                    &
             MODE LABEL "" AT 2,80         &
             NOACTION                      &
             ACTIVITIES FIND,ENTRY,CHANGE  &
             FIELDMARK RETAIN STARTUP      &
             HELP POPUP FROM 4,9 TO 20,72  &
             RECEIVING T_CIECLEMNU,        &
                       T_CIENOM,           &
                       PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD183E"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd183e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_RMOSTU    CHARACTER SIZE 16 = "RMOSTU"
TEMPORARY T_RMOSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_SILENT CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC    MASTER


;-------------------------------------------------------------------------------
;
; Sciage bloc
;

FILE PDSCIAGE_BLOCS  PRIMARY &
                     NOITEM

  ACCESS VIA   CIECLE,           &
               RMSNUMSEQ         &
         USING T_CIECLEMNU,      &
               RMSNUMSEQ         OF PDMONTAGE_BLOC

  ITEM RMSNUMSEQ         OF PDSCIAGE_BLOCS  FINAL RMSNUMSEQ         OF PDMONTAGE_BLOC
  ITEM CIECLE OF PDSCIAGE_BLOCS INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE REFERENCE

  ACCESS VIA   CIECLE,           &
               RMSNUMSEQ         &
         USING T_CIECLEMNU,      &
               RMSNUMSEQ         OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                              &
                 SCINUMSCI                            &
         USING   T_CIECLEMNU,                         &
                 SCINUMSCI        OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_RMOSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_RMOSTU,                             &
               RMOSTU           OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Rapport de montage " &
@ELSE
      " %%%Rapport de montage " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD RMSNUMSEQ        OF PDRAP_MONTAGE    &
label "No de montage.:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,25)


FIELD RMOSTU           OF PDRAP_MONTAGE    &
label "Date prod.....:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_RMOSTU     &
        DISPLAY                            &
        FIXED


ALIGN (,3,19)


FIELD RMOPRI           OF PDRAP_MONTAGE    &
label "Priorité......:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,25)


FIELD SCINUMSCI       OF PDRAP_MONTAGE     &
label "Numéro scie...:"&
        DISPLAY                            &
        FIXED


FIELD SCIDSC001        OF PDSCIES          &
        DISPLAY                            &
        FIXED


DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Sciage bloc " &
@ELSE
      " %%%Sciage bloc " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 12


FIELD SCBDATSCI        OF PDSCIAGE_BLOCS    FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date sciage...:"&
        HIDDEN ID 45                       &
        REQUIRED


FIELD SCBDSC001        OF PDSCIAGE_BLOCS   &
label "Description...:"&
        HIDDEN ID 50


ALIGN (2,,19)


FIELD SCBDSC002        OF PDSCIAGE_BLOCS   &
        HIDDEN ID 55


FIELD SCBDSC003        OF PDSCIAGE_BLOCS   &
        HIDDEN ID 60




;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDSCIAGE_BLOCS &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDSCIAGE_BLOCS &
     AND NOT NEWRECORD     OF PDSCIAGE_BLOCS &
     AND NOT DELETEDRECORD OF PDSCIAGE_BLOCS &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Rapport de montage ******************************
*                                                                              *
* No de montage.: xxxxxxxxxxx                                                  *
* Date prod.....: xxxxxxxx                                                     *
* Numéro scie...: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Priorité......: xx                                                           *
*                                                                              *
********************************* Sciage bloc **********************************
*                                                                              *
* No bloc.......: xxxxx-xxxxx-x                                                *
*                                                                              *
* Date sciage...: xxxxxxxx                                                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
