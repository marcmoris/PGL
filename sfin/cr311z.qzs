;/T/ Journal des ventes sommaire
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Josée Bédard
;    Date Création.: 30 mai 1994
;
;    Description...: Impression du journal des ventes sommaires. Le journal
;                    inclus les factures, les crédits, les notes de crédits et
;                    les ajustements. On imprime que les transactions qui sont
;                    journalisées.
;
;    Paramètres....: WCR311
;
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *WCR311 &
  ;
  ; Pour avoir la description du compte comptable du compte à recevoir.
  ;
  LINK CARCPTCAR OF WCR311 &
    TO CPTCLE OF                 VCPT_DSC ALIAS A_CPT_CAR &
  ;
  ; Description du compte venant de l'écriture.
  ;
  LINK CPTCLE OF WCR311 &
    TO CPTCLE OF                 VCPT_DSC   OPTIONAL &
  ;
  ; Description de l'unité administrative venant de l'écriture.
  ;
  LINK CIECLE   OF WCR311 , &
       UNACLE   OF WCR311   &
    TO CIECLE                , &
       UNACLE OF                 MCUNITE_ADM OPTIONAL

SELECT WCR311 IF "1" = T_FLGIMP OF WCR311

;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Journal des ventes - Sommaire"
@ELSE
       "%%%Journal des ventes - Sommaire"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " Sommaire comptable"

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "CR311A"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE4 CHARACTER SIZE 5 = "1" + PECCLE OF WCR311 &
         IF PECCLE OF WCR311[1:1] < "8" &
         ELSE "0" + PECCLE OF WCR311

SORT ON CIECLE    OF WCR311 & ; Numéro de compagnie.
     ON D_PECCLE_SIE4 & ; Période ctb.
     ON CARCPTCAR OF WCR311 & ; Compte à recevoir.
     ON CPTCLE    OF WCR311 & ; Compte ctb
     ON UNACLE    OF WCR311

;
; Imression de l'en-tête.
;
USE ustitstd NOLIST
SKIP 2 &
@IF FRANCAIS
 TAB 001 "Période:"   &
@ELSE
 TAB 001 "Period :"   &
@ENDIF
 TAB 010 PECCLE OF WCR311 &
SKIP 1 &
@IF FRANCAIS
 TAB 001 "C.A.R..:" &
@ELSE
 TAB 001 "A / R..:" &
@ENDIF
 TAB 010 CARCPTCAR OF WCR311 &
 TAB 017 CPTDSC    OF A_CPT_CAR &
SKIP 2 &
 TAB 005 &
@IF FRANCAIS
"               Compte                                 Unité administrative                      Débit            Crédit" &
@ELSE
"%%%       Compte                 Unité administrative          Débit           Crédit" &
@ENDIF
SKIP &
 TAB 005 &
"------------------------------------------ ------------------------------------------------- --------------- ---------------"

FOOTING AT UNACLE &
 TAB 005 CPTCLE    OF WCR311 &
 TAB 012 CPTDSC    OF VCPT_DSC  &
 TAB 048 UNACLE    OF WCR311 &
 TAB 057 UNANOM    OF MCUNITE_ADM &
 TAB 098 ECRMNTDBT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
 TAB 114 ECRMNTCRT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ


FOOTING AT CARCPTCAR &
  TAB 098 "---------------" &
  TAB 114 "---------------" &
SKIP &
  TAB 098 ECRMNTDBT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " &
  TAB 114 ECRMNTCRT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " &
SKIP &
  TAB 098 "===============" &
  TAB 114 "===============" &
SKIP PAGE

BUILD cr311za

@IF FORMAT
@ENDIF

;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *WCR311 &
  ;
  ; Pour avoir la description du projet.
  ;
  LINK CIECLE OF WCR311, PRJCLE OF WCR311 &
    TO CIECLE, PRJCLE OF                 GPPROJETS &
  ;
  ; Description de l'activité.
  ;
  LINK CIECLE OF WCR311, PRJCLE OF WCR311, PRACLE OF WCR311 &
    TO CIECLE, PRJCLE, PRACLE OF                 GPPRO_ACTIVITE OPTIONAL

SELECT WCR311 IF "2" = T_FLGIMP OF WCR311

;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Journal des ventes - Sommaire"
@ELSE
       "%%%Journal des ventes - Sommaire"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " Sommaire par projet sous-projet"

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "CR311B"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE5 CHARACTER SIZE 5 = "1" + PECCLE OF WCR311 &
         IF PECCLE OF WCR311[1:1] < "8" &
         ELSE "0" + PECCLE OF WCR311

SORT ON CIECLE    OF WCR311 & ; Numéro de compagnie.
     ON D_PECCLE_SIE5 & ; Période ctb.
     ON PRJCLE    OF WCR311 & ; Projet.
     ON PRACLE    OF WCR311  ; Sous-Projet

;
; Imression de l'en-tête.
;
USE ustitstd NOLIST
SKIP 2 &
@IF FRANCAIS
 TAB 001 "Période:"   &
@ELSE
 TAB 001 "Period :"   &
@ENDIF
 TAB 010 PECCLE OF WCR311 &
SKIP 2

HEADING AT PRJCLE &
 SKIP 2 &
@IF FRANCAIS
 TAB 001 "Projet.:" &
@ELSE
 TAB 001 "Project:" &
@ENDIF
 TAB 010 PRJCLE OF WCR311 &
 TAB 019 PRJDSC1 OF GPPROJETS &
SKIP 2 &
 TAB 005 &
@IF FRANCAIS
"                          Activité                                             Débit           Crédit" &
@ELSE
"%%%       Activité                                             Débit           Crédit" &
@ENDIF
SKIP &
 TAB 005 &
"-------------------------------------------------------------------------- --------------- ---------------"

FOOTING AT PRACLE &
 TAB 005 PRACLE    OF WCR311    &
 TAB 009 PRADSC1   OF GPPRO_ACTIVITE &
 TAB 080 ECRMNTDBT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
 TAB 096 ECRMNTCRT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ

FOOTING AT PRJCLE &
  TAB 080 "---------------" &
  TAB 096 "---------------" &
SKIP &
 TAB 080 ECRMNTDBT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
 TAB 096 ECRMNTCRT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
SKIP &
  TAB 080 "===============" &
  TAB 096 "==============="

FOOTING AT D_PECCLE_SIE5 SKIP PAGE

BUILD cr311zb

@IF FORMAT
@ENDIF

;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS *WCR311 &
  ;
  ; Description de l'équipement.
  ;
  LINK CIECLE OF WCR311, IMMCLE OF WCR311 &
    TO CIECLE, IMMCLE OF IMIMMOBILISATION OPTIONAL


SELECT WCR311 IF "2" = T_FLGIMP OF WCR311

;
; Titre du rapport.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Journal des ventes - Sommaire"
@ELSE
       "%%%Journal des ventes - Sommaire"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = " Sommaire par équipement "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "CR311C"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE6 CHARACTER SIZE 5 = "1" + PECCLE OF WCR311 &
         IF PECCLE OF WCR311[1:1] < "8" &
         ELSE "0" + PECCLE OF WCR311

SORT ON CIECLE    OF WCR311 & ; Numéro de compagnie.
     ON D_PECCLE_SIE6 & ; Période ctb.
     ON IMMCLE    OF WCR311

;
; Imression de l'en-tête.
;
USE ustitstd NOLIST
SKIP 2 &
@IF FRANCAIS
 TAB 001 "Période..:"   &
@ELSE
 TAB 001 "Period   :"   &
@ENDIF
 TAB 012 PECCLE OF WCR311 &
SKIP 2 &
 TAB 005 &
@IF FRANCAIS
"                  Immobilisation                           Débit           Crédit      " &
@ELSE
"%%%               Immobilisation                           Débit           Crédit      " &
@ENDIF
SKIP &
 TAB 005 &
"-----------------------------------------------------      --------------- ---------------"

FOOTING AT IMMCLE &
 TAB 005 IMMCLE    OF WCR311 &
 TAB 018 IMMDSC    OF IMIMMOBILISATION &
 TAB 061 ECRMNTDBT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
 TAB 077 ECRMNTCRT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ

FOOTING AT D_PECCLE_SIE6 &
  TAB 061 "---------------" &
  TAB 077 "---------------" &
SKIP &
 TAB 061 ECRMNTDBT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
 TAB 077 ECRMNTCRT OF WCR311 SUBTOTAL &
                                PICTURE "^^^,^^^,^^^.^^ " BWZ &
SKIP &
  TAB 061 "===============" &
  TAB 077 "===============" &
SKIP PAGE

BUILD cr311zc

@IF FORMAT
                                                                                                   1         1         1         1         1         1         1         1         1
         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

CR311                                                                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                             Page:9999
99/99/99 HH:MM xxxxxxxxxxxx                                                        Journal des ventes
                                                            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Période..: 99-99
Catégorie: 12
                                       Equipement                                    Débit           Crédit
-------------------------------------------------------------------------------  --------------  --------------
12345678 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999,999.99-  99,999,999.99-
12345678 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999,999.99-  99,999,999.99-
12345678 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999,999.99-  99,999,999.99-

                                                           ---------------
                                                  Total :  999,999,999.99-
                                                           ===============

@ENDIF
