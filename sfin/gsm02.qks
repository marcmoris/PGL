;/T/ Menu général - Choix Listes et rapports Pour Utilitaire
;
;/P/ Programmeur..: Chantal Martineau
;    Date Création: 14 septembre 1992
;
;    Description..: Menu général pour la présentation des choix de
;                   listes et rapports ou de procédures pour Utilitaire
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gsm02 &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES FIND &
             RECEIVING D_TITMOD   , D_XMOCOD

USE ussectmp NOLIST ; Variables pour la sécurité
"GSM02"                 ; Nom de l'écran

; Pour afficher la description du module
;
DEFINE D_XMOCOD CHARACTER SIZE 4  ; Code pour rapports ou procédures
DEFINE D_TITMOD CHARACTER SIZE 40 ; Nom du module


TEMPORARY T_XMONUM INTEGER   SIZE 2 RESET AT STARTUP INIT 0      ; Ordre des rapports

FILE GSMODULE PRIMARY
  ACCESS VIAINDEX XMO_U02 USING D_XMOCOD,T_XMONUM
  ACCESS VIAINDEX XMO_U02 USING D_XMOCOD


FILE GSPROGRAMME SECONDARY OCCURS 13
  ACCESS VIAINDEX XPR_R01 USING XMOCOD


@IF FRANCAIS
  DEFINE T_TITRE    CHAR*70 = XMOTITFRA
  DEFINE D-XPRDSC   CHAR SIZE 78 = &
         CENTER(XPRDSCFRA OF GSPROGRAMME + "                  ")
@ELSE
  DEFINE T_TITRE    CHAR*70 = XMOTITANG
  DEFINE D-XPRDSC   CHAR SIZE 78 = &
         CENTER(XPRDSCANG OF GSPROGRAMME + "                  ")
@ENDIF

DEFINE D_LISPAR char      size 204 =  " "

DEFINE D_INDLON CHARACTER SIZE 112 =  " "

DEFINE D_LISVAL CHARACTER SIZE 300 = "  "

DEFINE D_XPRNOM CHARACTER SIZE 7 = XPRNOM OF GSPROGRAMME

USE ushilite NOLIST
HILITE ALL DEFAULT
;USE USDRWRAP NOLIST
USE ustitoff NOLIST

HILITE DISPLAY OFF
HILITE DISPLAY HALFTONE
FIELD D_TITMOD FIXED NOLABEL DATA AT 2,21


HILITE DISPLAY HALFTONE UNDERLINE


USE ushilite NOLIST
SKIP TO 8
ALIGN(,,2)(,,5)
FIELD T_XMONUM LABEL REQUIRED LOOKUP ON GSMODULE &
             VIAINDEX XMO_U02 USING XMOCOD OF GSMODULE, T_XMONUM &
             PICTURE "^^"
FIELD T_TITRE NOLABEL

SKIP TO 10
HILITE DISPLAY HALFTONE
ALIGN (,4,7) (,,69)
CLUSTER OCCURS WITH GSPROGRAMME
@IF FRANCAIS
  FIELD XPRDSCFRA OF GSPROGRAMME HIDDEN NOCHANGE LABEL " "  ID 1
@ELSE
  FIELD XPRDSCANG OF GSPROGRAMME HIDDEN NOCHANGE LABEL " "  ID 1
@ENDIF
  FIELD XPRNOM    OF GSPROGRAMME SIZE 8    NOCHANGE
CLUSTER

;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
  ;
  ; si inter-cie alors on prend le Holding
  ;
END

;-------------------------------------------------------------------------------
;
; Pour centrer le nom du module
;
;
PROCEDURE OUTPUT D_TITMOD
BEGIN
  LET FIELDTEXT = CENTER(FIELDTEXT)
END

PROCEDURE INPUT T_XMONUM
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    RUN SCREEN gs105 MODE F PASSING D_XMOCOD,T_XMONUM
    LET FIELDTEXT = ASCII(T_XMONUM)
  END
END

;-------------------------------------------------------------------------------
;
; Pour centrer le titre du sous-module
;
;
PROCEDURE OUTPUT T_TITRE
BEGIN
  LET FIELDTEXT = CENTER(FIELDTEXT)
END

PROCEDURE DESIGNER 1
BEGIN
  IF XPRNOM OF GSPROGRAMME <> ""
  THEN BEGIN
   RUN SCREEN gs090 MODE E PASSING D_XPRNOM, &
                                   D_LISPAR, &
                                   D_INDLON, &
                                   D_LISVAL
  END
END

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

PROCEDURE PATH
  BEGIN
    IF (0 = SIZE(ASCII(T_XMONUM)) OR 0 = T_XMONUM)
    THEN
      BEGIN
       LET PATH = 2
      END
    ELSE
      BEGIN
        REQUEST T_XMONUM
        IF PROMPTOK
          THEN LET PATH = 1
       END
    IF PATH = 0
      THEN BEGIN
        LET PATH = 2
        END
    END

PROCEDURE FIND
  BEGIN
    IF PATH = 1
      THEN GET GSMODULE VIAINDEX XMO_U02 USING D_XMOCOD , T_XMONUM
    IF PATH = 2
      THEN GET GSMODULE VIAINDEX XMO_U02 USING D_XMOCOD
    FOR GSPROGRAMME
      BEGIN
        GET GSPROGRAMME OPTIONAL
        END
    LET T_XMONUM = XMONUM
    END

BUILD

