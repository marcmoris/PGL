;/T/ Menu Grand Livre
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 24 février 1992
;
;    Description..: Menu principal du module Grand Livre.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN glm01 MENU &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES CHANGE &
             RECEIVING T_FLGHLDOK

USE ussectmp NOLIST
    "GLM01"

USE glm01.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le holding est présent.

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      DESIGNER &
                      OPEN READ

;
; Pour valider la periode comptable.
;
FILE MCPERIODE_CIE    DESIGNER &
                      OPEN READ

;
; Pour afficher les dates de la période courante au grand livre.
;
FILE MCPERIODE_CTB    DESIGNER &
                      OPEN READ

;
; Pour la securite sur les compagnies.
;
FILE VSEC_USR         DESIGNER &
                      OPEN READ

;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu.
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Période du menu
TEMPORARY T_PECDATDEB DATE          RESET AT STARTUP ; Date de début de période
TEMPORARY T_PECDATFIN DATE          RESET AT STARTUP ; Date de fin de période

;
TEMPORARY T_PECCLECOU CHARACTER SIZE 4  RESET AT STARTUP ; Période courante

TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les périodes
TEMPORARY T_MOD       CHARACTER SIZE 2 ; Code de module, pour le Popup sur
                                       ; les périodes
;
; Indique la variable suivante indique que nous somme dans le menu du Grand
; Livre par compagnie.
;
TEMPORARY T_FLGMNU    CHARACTER SIZE 1 INITIAL "C" RESET AT STARTUP

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies
;
; Variable passer à l'écran GL020.
;
TEMPORARY T_PECCLEFND CHARACTER SIZE 4
TEMPORARY T_LGLCLEFND CHARACTER SIZE 4
;
; Variable passer à l'écran GL001
;
TEMPORARY T_ECJCLENUMFND CHARACTER SIZE 8
TEMPORARY T_ECJCLESEQFND CHARACTER SIZE 2
;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "GLL@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "GLP@" ; Code pour les procédures


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST ; Hilite pour tout les écrans

DRAW 1,20 TO 3,61
DRAW 2,1  TO 2,20
DRAW 2,61 TO 2,80

DRAW 2,1  TO 7,1
DRAW 7,1  TO 7,80
DRAW 2,80 TO 7,80

DRAW 5,1  TO 5,80

DRAW 7,40 TO 19,40
DRAW 19,1 TO 19,80

TITLE &
@IF FRANCAIS
      "G R A N D   L I V R E" &
@ELSE
      "G E N E R A L   L E D G E R" &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

ALIGN(4,4,20) (,,26)
FIELD T_CIECLEMNU HIDDEN ID 05 &
        FIXED            &
        UPSHIFT REQUIRED &
        PATTERN "((^|#)>)|!0" &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Company......:"  &
@ENDIF
        LOOKUP ON MCCOMPAGNIE &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 207, & ; Cette compagnie n'existe pas

          ON VSEC_USR &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 217 ; Vous n'avez pas accès à cette compagnie.

FIELD T_CIENOM &
        FIXED

SKIP 1

ALIGN(4,4,13)(,30,42)(,58,68)
FIELD T_PECCLEMNU ID SAME &
        FIXED            &
@IF FRANCAIS
        LABEL "Période:" &
@ELSE
        LABEL "Period.:" &
@ENDIF
        PICTURE "^^-^^" &
        LOOKUP ON MCPERIODE_CIE &
          VIA CIECLE, &
              PECCLE &
          USING T_CIECLEMNU, &
                T_PECCLEMNU  &
          MESSAGE 222 ; Cette période n'existe pas


FIELD T_PECDATDEB FIXED FORMAT YYYYMMDD PATTERN "####/##/##" &
@IF FRANCAIS
        LABEL "Date début:"
@ELSE
        LABEL "Start Date:"
@ENDIF

FIELD T_PECDATFIN FIXED FORMAT YYYYMMDD PATTERN "####/##/##"  &
@IF FRANCAIS
        LABEL "Date fin:"
@ELSE
        LABEL "End Date:"
@ENDIF


SKIP TO 9

ALIGN (2,2)
SUBSCREEN gl020 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_PECCLECOU, &
                        T_FLGMNU   , &
                        T_PECCLEFND, &
                        T_LGLCLEFND  &
@IF FRANCAIS
                LABEL "Gestion des lots      "
@ELSE
                LABEL "Batch Management      "
@ENDIF

SKIP 1

SUBSCREEN gl002 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Autorisation des lots "
@ELSE
                LABEL "Batch authorization   "
@ENDIF

SKIP 1

SUBSCREEN gl001 HIDDEN &
                PASSING T_CIECLEMNU    , &
                        T_CIENOM       , &
                        T_PECCLEMNU    , &
                        T_PECCLECOU    , &
                        T_FLGMNU       , &
                        T_ECJCLENUMFND , &
                        T_ECJCLESEQFND   &
@IF FRANCAIS
                LABEL "Journal général       "
@ELSE
                LABEL "General Journal       "
@ENDIF

SKIP 1

SUBSCREEN glm03 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Ecritures automatiques"
@ELSE
                LABEL "Automatic Entries     "
@ENDIF


SKIP TO 9


ALIGN (42,42)

SUBSCREEN glm02 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU     &
@IF FRANCAIS
                LABEL "Etats financiers                "
@ELSE
                LABEL "Financial Statements            "
@ENDIF


SKIP 1

SUBSCREEN gl004 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU     &
@IF FRANCAIS
                LABEL "Analyse de compte               "
@ELSE
                LABEL "Account Analysis                "
@ENDIF

SKIP 1

SUBSCREEN gl006 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_FLGMNU     &
@IF FRANCAIS
                LABEL "Analyse des engagements         "
@ELSE
                LABEL "%%%                             "
@ENDIF

SKIP 1

SUBSCREEN gl003 HIDDEN  &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Budgets                         "
@ELSE
                LABEL "Budgets                         "
@ENDIF

SKIP 1

SUBSCREEN gl005 HIDDEN  &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Disponibilité et soldes réels   "
@ELSE
                LABEL "Quick Assets and Actual Balances"
@ENDIF


SKIP TO 20

ALIGN(31,31)
SUBSCREEN glm05 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L, &
                        T_CIECLEMNU, &
                        T_PECCLEMNU, &
                        T_CIENOM   , &
                        T_PECDATDEB, &
                        T_PECDATFIN  &
@IF FRANCAIS
                LABEL " Listes et rapports "
@ELSE
                LABEL " Lists and Reports  "
@ENDIF

SUBSCREEN glm05 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P, &
                        T_CIECLEMNU, &
                        T_PECCLEMNU, &
                        T_CIENOM   , &
                        T_PECDATDEB, &
                        T_PECDATFIN  &
@IF FRANCAIS
                LABEL "    Procédures      "
@ELSE
                LABEL "    Procedures      "
@ENDIF

SUBSCREEN glm04 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_PECDATDEB, &
                        T_PECDATFIN &
@IF FRANCAIS
                LABEL "Informations de base"
@ELSE
                LABEL " Basic Information  "
@ENDIF

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le holding n'est pas défini on retourne l'usager au menu
  ; principal.
  ;
  IF T_FLGHLDOK = "0"
  THEN RETURN

  USE ussececr NOLIST

  ACCEPT T_CIECLEMNU
  ACCEPT T_PECCLEMNU
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
  ;
  ; A cause de l'option requis, si l'usager fait un retour et qu'il y a
  ; une valeur on remet la même.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_CIECLEMNU))
  THEN LET FIELDTEXT = T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;
; On conserve le nom de la compagnie du menu.
;
;
PROCEDURE PROCESS T_CIECLEMNU
BEGIN
  LET T_CIENOM = CIENOM OF MCCOMPAGNIE
  DISPLAY T_CIENOM
  ;
  ; On va chercher la periode courante du module G/L.
  ;
  GET MCPERIODE_CIE VIA CIECLE, &
                        PCCSTUGL &
                    USING T_CIECLEMNU, &
                          "C" &
                    OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 229 ; Il n'y a pas de période courante de définie.
  ELSE LET T_PECCLECOU = PECCLE OF MCPERIODE_CIE
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT T_PECCLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MOD = "GL"
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN  mc109 PASSING T_CIECLEMNU, T_PECCLE, T_MOD  MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
  ;
  ; Si l'usager ne saisie pas de periode alors on prend la période courante
  ; par défaut.
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) AND &
     0 = SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLECOU
  ;
  ; On forme la validation de la période.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLEMNU
END

;-------------------------------------------------------------------------------
;
;
; Validation du statut de la période.
;
;
PROCEDURE EDIT T_PECCLEMNU
BEGIN
  ;
  IF PCCSTUGL OF MCPERIODE_CIE = "F"
  THEN ERROR 223 ; La période est fermée.
  ;
  IF PCCSTUGL OF MCPERIODE_CIE <> "C"
  THEN WARNING 321 ; La période saisie n'est pas la courante.
END

;-------------------------------------------------------------------------------
;
;
; Conserve les dates de la période et les affiches.
;
;
PROCEDURE PROCESS T_PECCLEMNU
BEGIN
  GET MCPERIODE_CTB VIA PECCLE &
                    USING T_PECCLEMNU
  LET T_PECDATDEB = PECDATDEB OF MCPERIODE_CTB
  LET T_PECDATFIN = PECDATFIN OF MCPERIODE_CTB
  DISPLAY T_PECDATDEB
  DISPLAY T_PECDATFIN
END

PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_CIECLEMNU
  ACCEPT T_PECCLEMNU
END

BUILD
@IF FORMAT


                   ******************************************
********************         G R A N D   L I V R E          ********************
*                  ******************************************                  *
*  Compagnie.....: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
********************************************************************************
*  Période: xxxxx            Date début: xxxxxxxx          Date fin: xxxxxxxx  *
********************************************************************************
                                       *
 Gestion des lots                      * Etats financiers
                                       *
 Autorisation des lots                 * Analyse de compte
                                       *
 Journal général                       * Budgets
                                       *
 Ecritures automatiques                * Disponibilité et soldes réels
                                       *
                                       *
                                       *
********************************************************************************
                               Listes et rapports
                                  Procédures
                              Informations de base
@ENDIF
