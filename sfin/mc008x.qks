;/T/ Unité adm. - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 11 août 1993
;
;    Description..: Cet écran vérifie si l'unité adm. reçu en paramètre est
;                   référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc008x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCUNITE_ADM , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Unité administrative.
;
FILE MCUNITE_ADM      MASTER

;
; Solde du Grand Livre.
;
FILE MCSOLDE_ANNUEL   REFERENCE

;
; Historique des projets.
;
FILE MCHIS_PROJET     REFERENCE

;
; Compagnie.
;
FILE MCCOMPAGNIE      REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans les soldes du Grand Livre.
  ;
  GET MCSOLDE_ANNUEL VIA CIECLE , &
                         UNACLE   &
                     USING CIECLE OF MCUNITE_ADM , &
                           UNACLE OF MCUNITE_ADM   &
                     OPTIONAL
  IF ACCESSOK
  THEN ERROR 166 ; Destruction annulée, cette information est référée dans
                 ; les soldes au G/L.

  ;
  ; On vérifie dans l'historique des projets.
  ;
  GET MCHIS_PROJET VIA CIECLE , &
                       UNACLE   &
                   USING CIECLE OF MCUNITE_ADM , &
                         UNACLE OF MCUNITE_ADM   &
                   OPTIONAL
  IF ACCESSOK
  THEN ERROR 167 ; Destruction annulée, cette information est référée par
                 ; l'historique des projets.

  ;
  ; On vérifie dans les compagnie.
  ;
  GET MCCOMPAGNIE VIA CIECLE    , &
                      CIEUNABIL   &
                     USING CIECLE OF MCUNITE_ADM , &
                           UNACLE OF MCUNITE_ADM   &
                     OPTIONAL
  IF ACCESSOK
  THEN ERROR 168 ; Destruction annulée, cette information est référée par
                 ; une compagnie.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
