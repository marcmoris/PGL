;/T/ Gestion des immobilisations
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 19 décembre 1994
;
;    Description..: Fiche des immobilisations - Information comptable sommaire.
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN im001j ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        IMIMMOBILISATION

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ussectmp NOLIST     ; Variable pour la securite
    "IM001J"            ; Nom de l'écran

USE im001j.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;------------------------------------
; Fichier fiche des immobilisations
;------------------------------------
FILE IMIMMOBILISATION       MASTER



;----------------------------
; Fichier budget comptable
;----------------------------
FILE IMIMM_BDG_CTB          PRIMARY   &
                            OCCURS 15 &
                            NOITEMS

  ACCESS  VIA     CIECLE, &
                  IMMCLE, &
                  CPTCLE, &
                  UNACLE  &
          USING   CIECLE OF IMIMMOBILISATION, &
                  IMMCLE OF IMIMMOBILISATION, &
                  CPTCLE OF IMIMM_BDG_CTB,  &
                  UNACLE OF IMIMM_BDG_CTB   &
          REQUEST CPTCLE, &
                  UNACLE  &
          ORDERBY CPTCLE, &
                  UNACLE

  ACCESS  VIA     CIECLE, &
                  IMMCLE, &
                  CPTCLE  &
          USING   CIECLE OF IMIMMOBILISATION, &
                  IMMCLE OF IMIMMOBILISATION, &
                  CPTCLE OF IMIMM_BDG_CTB &
          REQUEST CPTCLE  &
          ORDERBY CPTCLE, &
                  UNACLE

  ACCESS  VIA     CIECLE, &
                  IMMCLE  &
          USING   CIECLE OF IMIMMOBILISATION, &
                  IMMCLE OF IMIMMOBILISATION  &
          ORDERBY CPTCLE, &
                  UNACLE


;-------------------------------
; Fichier unité administrative
;-------------------------------
FILE MCUNITE_ADM            REFERENCE

  ACCESS  VIA   CIECLE, &
                UNACLE  &
          USING CIECLE OF IMIMM_BDG_CTB, &
                UNACLE OF IMIMM_BDG_CTB


;-----------------------------------------
; Fichier charte de l'unite administrative
;-----------------------------------------
FILE MCCHARTE_UNA           REFERENCE

  ACCESS  VIA   CPTCLE, &
                CIECLE, &
                UNACLE  &
          USING CPTCLE OF IMIMM_BDG_CTB, &
                CIECLE OF IMIMM_BDG_CTB, &
                UNACLE OF IMIMM_BDG_CTB


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF IMIMM_BDG_CTB


;-------------------------------------------------------------------------------
;

DEFINE D_MNTTOT      FLOAT SIZE 8 = IBCMNTENG OF IMIMM_BDG_CTB + &
                                    IBCMNTREE OF IMIMM_BDG_CTB

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_CIECLE     CHAR*4
TEMPORARY T_IMMCLE     CHAR*8
TEMPORARY T_CPTCLE     CHAR*6
TEMPORARY T_UNACLE     CHAR*8

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Information comptable sommaire " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN (,3,19) (,,35)
FIELD IMMCLE    OF IMIMMOBILISATION                   &
      DISPLAY

FIELD IMMDSC    OF IMIMMOBILISATION                   &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
      "Compte Descrption            Unité     Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Engagé" &
@ELSE
      " %%% " &
@ENDIF
      AT ,75

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,98

TITLE &
@IF FRANCAIS
      "Total" &
@ELSE
      " %%% " &
@ENDIF
      AT ,119

SKIP

ALIGN (3,2,3) (,,10) (,,32) (,,42) (,,67) (,,88) (,,110)

CLUSTER OCCURS WITH IMIMM_BDG_CTB

FIELD CPTCLE OF IMIMM_BDG_CTB                         &
      LABEL " "                                       &
      HIDDEN                                          &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD UNACLE OF IMIMM_BDG_CTB                         &
      DISPLAY

FIELD UNANOMABR OF MCUNITE_ADM &
      DISPLAY

FIELD IBCMNTENG OF IMIMM_BDG_CTB

FIELD IBCMNTREE OF IMIMM_BDG_CTB

FIELD D_MNTTOT                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

CLUSTER


;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (ENGAGE)
;
;
PROCEDURE INPUT IBCMNTENG
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    IF 0 <> INDEX(FIELDTEXT,"*")
    THEN BEGIN
      LET T_CIECLE = CIECLE OF IMIMM_BDG_CTB
      LET T_IMMCLE = IMMCLE OF IMIMM_BDG_CTB
      LET T_CPTCLE = CPTCLE OF IMIMM_BDG_CTB
      LET T_UNACLE = UNACLE OF IMIMM_BDG_CTB
      RUN SCREEN im001l MODE F PASSING T_CIECLEMNU, T_CIENOM, &
                                       IMIMM_BDG_CTB,         &
                                       IMIMMOBILISATION
      LET FIELDTEXT = "0"
    END
    ELSE BEGIN
      LET FIELDTEXT = "0"
      INFO MESS 842; La modification de cet élément n'est pas permise.
    END
  END
END

PROCEDURE EDIT IBCMNTENG
BEGIN
   LET FIELDVALUE = OLDVALUE(IBCMNTENG OF IMIMM_BDG_CTB)
END

;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (REEL)
;
;
PROCEDURE INPUT IBCMNTREE
BEGIN
  IF 0 <> SIZE(FIELDTEXT)
  THEN BEGIN
    IF 0 <> INDEX(FIELDTEXT,"*")
    THEN BEGIN
      LET T_CIECLE = CIECLE OF IMIMM_BDG_CTB
      LET T_IMMCLE = IMMCLE OF IMIMM_BDG_CTB
      LET T_CPTCLE = CPTCLE OF IMIMM_BDG_CTB
      LET T_UNACLE = UNACLE OF IMIMM_BDG_CTB
      RUN SCREEN im001k MODE F PASSING T_CIECLEMNU, T_CIENOM, &
                                       IMIMM_BDG_CTB,         &
                                       IMIMMOBILISATION
      LET FIELDTEXT = "0"
    END
    ELSE BEGIN
      LET FIELDTEXT = "0"
      INFO MESS 842; La modification de cet élément n'est pas permise.
    END
  END
END

PROCEDURE EDIT IBCMNTREE
BEGIN
   LET FIELDVALUE = OLDVALUE(IBCMNTREE OF IMIMM_BDG_CTB)
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

