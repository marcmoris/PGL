;/T/ Consultation des notes générales
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 2 mai 1995
;
;    Description..: Permet de visionner et de choisir une note générale.

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN fa004d  NOMODE NOACTION FIELDMARK &
               FROM 11,14 TO 23,80 &
               HELP POPUP FROM 4,9 TO 20,72 &
               RECEIVING  FAFAR_NOTE



USE fa004d.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE FAFAR_NOTE         MASTER

FILE FANOTE_GENERALE    PRIMARY &
                        OCCURS 10

  ACCESS SEQUENTIAL &
         ORDERBY FGNCLE

FILE FAFAR_NOTE         DESIGNER &
                        ALIAS FAFAR_NOTE2


FILE FANOTE_GEN_DET     DESIGNER


TEMPORARY T_NUMLIG    INTEGER SIGNED SIZE 2

USE ushilite NOLIST

DRAW 1,1 TO 13,56

USE ustitoff NOLIST

SKIP TO 2

ALIGN (3,3,6)(,,12)

CLUSTER OCCURS WITH FANOTE_GENERALE

FIELD FGNCLE    OF FANOTE_GENERALE HIDDEN LABEL ">>"
FIELD FGNTIT    OF FANOTE_GENERALE

CLUSTER


;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_NUMLIG = 0
  WHILE RETRIEVING FAFAR_NOTE2 &
        VIA     CIECLE   , &
                FARCLE   , &
                FFDCLELIG  &
        USING   CIECLE    OF FAFAR_NOTE, &
                FARCLE    OF FAFAR_NOTE, &
                FFDCLELIG OF FAFAR_NOTE  &
        ORDERBY CIECLE   , &
                FARCLE   , &
                FFDCLELIG, &
                FFNSEQ
  BEGIN
     IF T_NUMLIG < FFNSEQ OF FAFAR_NOTE2
     THEN LET T_NUMLIG = FFNSEQ OF FAFAR_NOTE2
  END

  WHILE RETRIEVING FANOTE_GEN_DET &
        VIA      FGNCLE &
        USING    FGNCLE OF FANOTE_GENERALE &
        ORDERBY  FGNCLE, &
                 FNDSEQ
  BEGIN
     LET T_NUMLIG = T_NUMLIG + 1
     LET CIECLE    OF FAFAR_NOTE2 = CIECLE    OF FAFAR_NOTE
     LET FARCLE    OF FAFAR_NOTE2 = FARCLE    OF FAFAR_NOTE
     LET FFDCLELIG OF FAFAR_NOTE2 = FFDCLELIG OF FAFAR_NOTE
     LET FFNSEQ    OF FAFAR_NOTE2 = T_NUMLIG
     LET FFNDSC    OF FAFAR_NOTE2 = FNDDSC    OF FANOTE_GEN_DET
     LET FFNIMP    OF FAFAR_NOTE2 = FNDIMP    OF FANOTE_GEN_DET
     PUT FAFAR_NOTE2 RESET
  END
  RETURN
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE DELETE
  DISABLE


PROCEDURE UPDATE
  DISABLE

BUILD LIST DETAIL
@IF FORMAT
             ********************************************************
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             * >> xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
             *                                                      *
             ********************************************************
@ENDIF
