;/T/ Redimension de tranche.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-27
;
;    Description..: Le but de cette unité de traitement est de redimensionner
;                   une tranche.
;
;/M/ François Déry, 12 mai 1999
;       Ajout de la transaction NO_SEQ car elle était référée sans être déclaré
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd182a ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM ,         &
                       PDREDIM_TRANCHE,   &
                       PDTRANCHE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD182A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd182a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST



@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Défaut de tranche               " ACTION DESIGNER D001
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D002
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Défaut de tranche            " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D002
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_RED_TRAN IN DICT

;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Redimension de tranche
;

FILE PDREDIM_TRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Tranche
;

FILE PDTRANCHE MASTER


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les bons de commande
;

FILE PDSEQ_RED_TRAN DESIGNER &
                    TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; TRANCHE inscription de la/des nouvelles tranches
;

FILE PDTRANCHE PRIMARY &
            NOITEM  &
            ALIAS TRA_NEW

  ACCESS VIA     CIECLE,                               &
                 TRANUMTRA002                          &
         USING   T_CIECLEMNU,                          &
                 TRANUMTRA002    OF PDTRANCHE          &
         ORDERBY CIECLE,                               &
                 TRANUMTRA002


  ITEM CIECLE           OF TRA_NEW INITIAL T_CIECLEMNU

  ITEM TRANUMTRA002     OF TRA_NEW INITIAL TRANUMTRA002     OF PDTRANCHE

  ITEM TRACODPRD        OF TRA_NEW INITIAL TRACODPRD        OF PDTRANCHE
  ITEM TRASTU           OF TRA_NEW INITIAL TRASTU           OF PDTRANCHE
  ITEM TRADATCRE        OF TRA_NEW INITIAL SYSDATE
  ITEM TRAHRECRE        OF TRA_NEW INITIAL SYSTIME / 10000
  ITEM TRAUSRCRE        OF TRA_NEW INITIAL LOGONID
  ITEM TGRCODGRA        OF TRA_NEW INITIAL TGRCODGRA        OF PDTRANCHE
  ITEM TYFCODFIN        OF TRA_NEW INITIAL TYFCODFIN        OF PDTRANCHE
  ITEM BLONUMGRA001APR  OF TRA_NEW INITIAL BLONUMGRA001APR  OF PDTRANCHE
  ITEM BLONUMGRA002APR  OF TRA_NEW INITIAL BLONUMGRA002APR  OF PDTRANCHE
  ITEM BLORECAPR        OF TRA_NEW INITIAL BLORECAPR        OF PDTRANCHE
  ITEM BLONUMBLOAPR     OF TRA_NEW INITIAL BLONUMBLOAPR     OF PDTRANCHE


;-------------------------------------------------------------------------------
;
; Tranche vérification du volume total de la/des nouvelles tranches
;

FILE PDTRANCHE DESIGNER &
               ALIAS TRA_VALI


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TGRCODGRA                       &
         USING   T_CIECLEMNU,                    &
                 TGRCODGRA OF TRA_NEW


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE

  ACCESS VIA     CIECLE,                         &
                 TYFCODFIN                       &
         USING   T_CIECLEMNU,                    &
                 TYFCODFIN OF TRA_NEW


;-------------------------------------------------------------------------------
;
; Réception tranche
;

FILE PDREC_TRANCHE REFERENCE

  ACCESS VIA     CIECLE,                               &
                 TRANUMTRA002                          &
         USING   T_CIECLEMNU,                          &
                 TRANUMTRA002    OF PDTRANCHE          &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 TRANUMTRA002


;-------------------------------------------------------------------------------
;
; Réception
;

FILE PDRECEPTION REFERENCE

  ACCESS VIA     CIECLE,                               &
                 RCTNUMREC                             &
         USING   T_CIECLEMNU,                          &
                 RCTNUMREC OF PDREC_TRANCHE            &
         ORDERBY CIECLE,                               &
                 RCTNUMREC,                            &
                 BCMNUMBCM


;-------------------------------------------------------------------------------
;
; Bon de commande
;

FILE PDBON_COMMANDE REFERENCE

  ACCESS VIA     CIECLE,                               &
                 BCMNUMBCM                             &
         USING   T_CIECLEMNU,                          &
                 BCMNUMBCM OF PDRECEPTION


;-------------------------------------------------------------------------------
;
; Détail du bon de commande
;

FILE PDDET_BON_COMMAN REFERENCE

  ACCESS VIA     CIECLE,                         &
                 BCMNUMBCM,                      &
                 PODNUMLIG                       &
         USING   T_CIECLEMNU,                    &
                 BCMNUMBCM OF PDBON_COMMANDE,    &
                 PODNUMLIG OF PDREC_TRANCHE


;-------------------------------------------------------------------------------
;
; Fournisseurs
;

FILE VFOC_FOU REFERENCE
  ACCESS VIA     FOUCLE,                      &
                 FOCCIECLE,                   &
                 FOCUSISEC                    &
         USING   FOUCLE    OF PDBON_COMMANDE, &
                 T_CIECLEMNU,                 &
                 "N"


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
;
;

TEMPORARY T_MC2       FLOAT SIZE 8 RESET AT STARTUP ; Surface de la tranche
TEMPORARY T_MC2_PERE  FLOAT SIZE 8 RESET AT STARTUP ; Surface de la tranche a redimensionner
TEMPORARY T_MC2_TOT   FLOAT SIZE 8 RESET AT STARTUP ; Total des surfaces des tranches

;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Redimensionnement de tranche " &
@ELSE
      " %%%Redimensionnement de tranche " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19) (,53,69)

FIELD TRANUMTRA002     OF PDTRANCHE        &
        HIDDEN ID 05                       &
        DISPLAY                            &
        FIXED


FIELD TRASTU           OF PDTRANCHE        &
        HIDDEN ID 10                       &
        DISPLAY                            &
        FIXED                              &
        REFRESH


ALIGN (,3,19) (,28,44) (,53,69)


FIELD TRALON           OF PDTRANCHE        &
        HIDDEN ID 15                       &
        DISPLAY                            &
        FIXED


FIELD TRAHAU           OF PDTRANCHE        &
        HIDDEN ID 20                       &
        DISPLAY                            &
        FIXED


FIELD TRAEPA           OF PDTRANCHE        &
        HIDDEN ID 25                       &
        DISPLAY                            &
        FIXED


ALIGN (2,3,19) (52,53,69)


FIELD T_MC2_PERE                           &
        HIDDEN ID 30                       &
        DISPLAY                            &
        LABEL "M2 brut.......:"            &
        SIGNIFICANCE 6                     &
        PICTURE "^^.^^^^"


FIELD RCTNUMREC OF PDRECEPTION                &
        HIDDEN ID 35                          &
        DISPLAY                               &
        FIXED


ALIGN (,3,19) (27,,28)


FIELD FOUCLE OF VFOC_FOU                      &
        HIDDEN ID 40                          &
        DISPLAY                               &
        FIXED                                 &
        LABEL "Fournisseur...:"


FIELD FOUNOM OF VFOC_FOU                      &
        HIDDEN ID 45                          &
        DISPLAY                               &
        FIXED


DRAW 10,2 TO 10,79


TITLE &
@IF FRANCAIS
      " Nouvelle tranche " &
@ELSE
      " %%%Nouvelle tranche " &
@ENDIF
      CENTERED AT 10,1


SKIP TO LINE 12


ALIGN (2,3,19) (52,53,69)


FIELD TRANUMTRA002     OF TRA_NEW          &
        HIDDEN ID 50                       &
        REQUIRED NOCHANGE                  &
        LOOKUP NOTON TRA_NEW               &
          VIA     CIECLE,                  &
                  TRANUMTRA002             &
          USING   T_CIECLEMNU,             &
                  TRANUMTRA002 OF TRA_NEW  &
          MESSAGE 2939 ; Ce numéro de tranche existe déjà


FIELD TRASTU           OF TRA_NEW          &
        HIDDEN ID 55                       &
        DEFAULT "I"


SKIP 1

ALIGN (,3,19)(,,25)

FIELD TGRCODGRA        OF TRA_NEW          &
        HIDDEN ID 60                       &
        NUMERIC                            &
        DISPLAY                            &
        BWZ


FIELD TGRDSCFRA001     OF PDTYPE_GRANIT    &
        DISPLAY


FIELD TYFCODFIN        OF TRA_NEW          &
        HIDDEN ID 65                       &
        NUMERIC                            &
        DISPLAY                            &
        BWZ


FIELD TYFDSCFRA001     OF PDTYPE_FINI      &
        DISPLAY


SKIP 1


ALIGN (2,3,19)


FIELD TRALON           OF TRA_NEW          &
        HIDDEN ID 70                       &
        REQUIRED


FIELD TRAHAU           OF TRA_NEW          &
        HIDDEN ID 75                       &
        REQUIRED


FIELD TRAEPA           OF TRA_NEW          &
        HIDDEN ID 80                       &
        REQUIRED


ALIGN (,3,19)


FIELD T_MC2                                   &
        HIDDEN ID 85                          &
        DISPLAY                               &
        LABEL "Surface M2....:"               &
        SIGNIFICANCE 6                        &
        PICTURE "^^.^^^^"


;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
  ;
  ; Calcul de a surface de la tranche à redimensionner (Père)
  ;
  LET TZ_LARGEUR   = TRALON    OF PDTRANCHE
  LET TZ_HAUTEUR   = TRAHAU    OF PDTRANCHE
  LET TZ_EPAISSEUR = TRAEPA    OF PDTRANCHE
  DO INTERNAL CALVOL
  LET T_MC2_PERE = TZ_MC2
  DISPLAY T_MC2_PERE
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions brut de la tranche.
; N.B.: on ne peut pas avoir de bloc plus petit que 500 mm. et plus grand que
;       4500 mm.
;

PROCEDURE EDIT TRALON
BEGIN
  IF TRALON    OF TRA_NEW < T_DIM_MIN_LON_TR OR &
     TRALON    OF TRA_NEW > T_DIM_MAX_LON_TR
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume brut
;

PROCEDURE PROCESS TRALON
BEGIN
  LET TZ_LARGEUR   = TRALON    OF TRA_NEW
  LET TZ_HAUTEUR   = TRAHAU    OF TRA_NEW
  LET TZ_EPAISSEUR = TRAEPA    OF TRA_NEW
  DO INTERNAL CALVOL
  LET T_MC2 = TZ_MC2
  DISPLAY T_MC2
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions brut du bloc
; N.B.: on ne peut pas avoir de bloc plus petit que 500 mm. et plus grand que
;       4500 mm.
;

PROCEDURE EDIT TRAHAU
BEGIN
  IF TRAHAU    OF TRA_NEW < T_DIM_MIN_HAU_TR OR &
     TRAHAU    OF TRA_NEW > T_DIM_MAX_HAU_TR
  THEN BEGIN
    ERROR 2947 ; Cette dimension hors des dimensions limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume brut
;

PROCEDURE PROCESS TRAHAU
BEGIN
  LET TZ_LARGEUR   = TRALON    OF TRA_NEW
  LET TZ_HAUTEUR   = TRAHAU    OF TRA_NEW
  LET TZ_EPAISSEUR = TRAEPA    OF TRA_NEW
  DO INTERNAL CALVOL
  LET T_MC2 = TZ_MC2
  DISPLAY T_MC2
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de valider les dimensions brut du bloc
; N.B.: on ne peut pas avoir de bloc plus petit que 500 mm. et plus grand que
;       4500 mm.
;

PROCEDURE EDIT TRAEPA
BEGIN
  IF TRAEPA    OF TRA_NEW < T_DIM_MIN_EPA_TR OR &
     TRAEPA    OF TRA_NEW > T_DIM_MAX_EPA_TR
  THEN BEGIN
    ERROR 2962 ; Cette dimension hors des dimensions limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul du volume brut
;

PROCEDURE PROCESS TRAEPA
BEGIN
  LET TZ_LARGEUR   = TRALON    OF TRA_NEW
  LET TZ_HAUTEUR   = TRAHAU    OF TRA_NEW
  LET TZ_EPAISSEUR = TRAEPA    OF TRA_NEW
  DO INTERNAL CALVOL
  LET T_MC2 = TZ_MC2
  DISPLAY T_MC2
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
  ;
  ; Valide que le bloc est en inventaire
  ;
END


;-------------------------------------------------------------------------------
;
; Calcul du volume du bloc à redimesionner
;
PROCEDURE POSTFIND
BEGIN
  ;
  ; Pour être redimensionner le bloc doit avoir le statut "I" invetaire
  ;
  IF TRASTU OF PDTRANCHE <> "I"
  THEN BEGIN
    ERROR 2942 ; Ce statut est invalide
  END
  ;
  ; Calcul du volume brut du bloc
  ;
  LET TZ_LARGEUR   = TRALON OF TRA_NEW
  LET TZ_HAUTEUR   = TRAHAU OF TRA_NEW
  LET TZ_EPAISSEUR = TRAEPA OF TRA_NEW
  DO INTERNAL CALVOL
  LET T_MC2 = TZ_MC2
  DISPLAY T_MC2
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF TRA_NEW &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF TRA_NEW &
     AND NOT NEWRECORD     OF TRA_NEW &
     AND NOT DELETEDRECORD OF TRA_NEW &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; On valide que le total du volume des fils de dépasse pas le volume du père
  ;
  LET T_MC2_TOT = 0

  WHILE RETRIEVING TRA_VALI                       &
    VIA     CIECLE,                               &
            TRANUMTRA001                          &
    USING   T_CIECLEMNU,                          &
            TRANUMTRA002 OF PDTRANCHE
  BEGIN
    ;
    ; Calcul del la surface des nouvelles tranches déjà dans la base de donnée
    ;
    IF NOT DELETED
    THEN BEGIN
      LET TZ_LARGEUR   = TRALON OF TRA_VALI
      LET TZ_HAUTEUR   = TRAHAU OF TRA_VALI
      LET TZ_EPAISSEUR = TRAEPA OF TRA_VALI
      DO INTERNAL CALVOL
      LET T_MC2_TOT = T_MC2_TOT + TZ_MC2
    END
  END

  ;
  ; Calcul du volume brut du bloc en traitement
  ;

  IF NOT DELETED
  THEN BEGIN
    LET TZ_LARGEUR   = TRALON OF TRA_NEW
    LET TZ_HAUTEUR   = TRAHAU OF TRA_NEW
    LET TZ_EPAISSEUR = TRAEPA OF TRA_NEW
    DO INTERNAL CALVOL
    LET T_MC2_TOT = T_MC2_TOT + TZ_MC2
  END


  ;-----------------------------------------------------------------------------
  ;
  ; Valide que la surface des nouvelles tranches ne dépassa pas la surface
  ; de la tranche père
  ;

  IF T_MC2_TOT > 0.00
  THEN BEGIN
    LET TRASTU OF PDTRANCHE = "T"
  END
  ELSE BEGIN
    IF T_MC2_TOT > T_MC2_PERE
    THEN BEGIN
      ERROR 2961 ; La surface de la tranche redimensionner est plus grande que la surface de la tranche de départ
    END
  END


  ;-----------------------------------------------------------------------------
  ; Évaluation des champs indiquant une mise à jour du bloc.
  ;
  IF NOT NEWRECORD OF TRA_NEW AND &
     ALTEREDRECORD OF TRA_NEW
  THEN BEGIN
    LET TRADATMAJ     OF TRA_NEW = SYSDATE
    LET TRAHREMAJ     OF TRA_NEW = SYSTIME / 10000
    LET TRAUSRMAJ     OF TRA_NEW = LOGONID
  END

  ;-----------------------------------------------------------------------------
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = RDTNUMRED        OF PDREDIM_TRANCHE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_RED_TRAN OPTIONAL         &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_RED_TRAN = T_CIECLEMNU
    END
    LET RTQNUM    OF PDSEQ_RED_TRAN  = RTQNUM OF PDSEQ_RED_TRAN + 1
    LET RDTNUMRED OF PDREDIM_TRANCHE = RTQNUM OF PDSEQ_RED_TRAN
    PUT PDSEQ_RED_TRAN
  END
END

;-------------------------------------------------------------------------------
;
; Défauts de tranche
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd181a &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        TRA_NEW         &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Vérification
;
PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd181l &
              PASSING   T_CIECLEMNU,    &
                        T_CIENOM,       &
                        TRA_NEW         &
              MODE F
END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************* Redimensionnement de tranche *************************
*                                                                              *
* No. tranche...: xxxxxxxxx                         Statut........: x          *
* Largeur.......: xxxxx    Longueur......: xxxxx    Épaisseur.....: xxxxx      *
* M2 brut.......: xxxxxxx                           No. réception.: xxxxxxxxx  *
* Fournisseur...: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
******************************* Nouvelle tranche *******************************
*                                                                              *
* No. tranche...: xxxxxxxxx                         Statut........: x          *
*                                                                              *
* Code granit...: xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
* Code fini.....: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
* Largeur.......: xxxxx                                                        *
* Longueur......: xxxxx                                                        *
* Épaisseur.....: xxxxx                                                        *
* Surface M2....: xxxxxxx                                                      *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
