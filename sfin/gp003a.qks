;/M/ François Déry, 7 mai 1999
;       Ajout de l'usager dans le fichier de consultation.
;
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN gp003a      NOMODE NOACTION FIELDMARK STARTUP &
                   FROM 2,1 TO 13,80 &
                   MESSAGE ON 24 &
                   HELP POPUP FROM 4,9 TO 20,72 &
                   RECEIVING T_CIECLEFND

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST


TEMPORARY T_PECDEB    CHARACTER SIZE 4  RESET AT STARTUP
TEMPORARY T_PECFIN    CHARACTER SIZE 4  RESET AT STARTUP

;--------------------
; Fichier des projets
;--------------------
TEMPORARY T_PRJCLE    CHARACTER SIZE 8  RESET AT STARTUP ; Numero de projet
TEMPORARY T_PRJCLE2   CHARACTER SIZE 8  RESET AT STARTUP ; Numero de projet
TEMPORARY T_PRJSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.

FILE GPPROJETS              REFERENCE

  ACCESS  VIA   CIECLE, &
                PRJCLE  &
          USING T_CIECLEFND, &
                T_PRJCLE



;-------------------------
; Fichier des sous-projets
;-------------------------
TEMPORARY T_PRACLE    CHARACTER SIZE 3  RESET AT STARTUP
TEMPORARY T_PRACLE2   CHARACTER SIZE 3  RESET AT STARTUP
TEMPORARY T_PRASTUPAT CHARACTER SIZE 5 ; Popup sur les activités.

FILE GPPRO_ACTIVITE         REFERENCE

  ACCESS  VIA   CIECLE, &
                PRJCLE, &
                PRACLE  &
          USING T_CIECLEFND, &
                T_PRJCLE,    &
                T_PRACLE



;--------------------
; Fichier des formats
;--------------------
TEMPORARY T_FRMCLE    CHARACTER SIZE 4  RESET AT STARTUP
TEMPORARY T_FRMCLE2   CHARACTER SIZE 4  RESET AT STARTUP
TEMPORARY T_FRMTYP    CHARACTER SIZE 1  RESET AT STARTUP

FILE GPFORMATS              REFERENCE

  ACCESS  VIA   FRMCLE  &
          USING T_FRMCLE

FILE GPPROJETS              DESIGNER &
                            ALIAS GPPROJETS_2

FILE GPPRO_ACTIVITE         DESIGNER &
                            ALIAS GPPRO_ACTIVITE_2

FILE GPFRM_AFFICHE          DESIGNER
FILE GPFRM_PROJETS          DESIGNER
FILE GPFOR_DETAILS          DESIGNER
FILE VPRA01                 DESIGNER
FILE VPRJ01                 DESIGNER

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW  1,1 TO 12,80

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1

ALIGN (3,3,19) (,,30)
FIELD T_PRJCLE &
      HIDDEN &
      LABEL "Projet........:" &
      REQUIRED &
      LOOKUP ON GPPROJETS MESSAGE 857

FIELD PRJDSC1 OF GPPROJETS &
      DISPLAY SIZE 45

ALIGN (,,30)
FIELD PRJDSC2 OF GPPROJETS &
      DISPLAY SIZE 45

SKIP 1

ALIGN (3,3,19) (,,30)
FIELD T_PRACLE &
      HIDDEN &
      LABEL "Sous-projet...:" &
      NOCHANGE &
      LOOKUP ON GPPRO_ACTIVITE MESSAGE 877

FIELD PRADSC1 OF GPPRO_ACTIVITE &
      DISPLAY SIZE 45


ALIGN (,,30)
FIELD PRADSC2 OF GPPRO_ACTIVITE &
      DISPLAY SIZE 45

SKIP 1

ALIGN (3,3,19) (,30,47)
FIELD T_PECDEB &
      HIDDEN &
      PATTERN = "(##((0(1|2|3|4|5|6|7|8|9|))|1(0|1|2|3|4)))|!0" &
      PICTURE "^^-^^" &
      LABEL "Période début.:" &
      NOCHANGE

FIELD T_PECFIN &
      ID SAME &
      PATTERN = "(##((0(1|2|3|4|5|6|7|8|9|))|1(0|1|2|3|4)))|!0" &
      PICTURE "^^-^^" &
      IF T_PECDEB <> "  " &
      LABEL "Période fin...:" &
      NOCHANGE


SKIP 1

ALIGN (3,3,19) (,,25)

FIELD T_FRMCLE &
      HIDDEN &
      LABEL "Format........:" &
      REQUIRED &
      LOOKUP ON GPFORMATS MESSAGE 878

FIELD FRMDSC  OF GPFORMATS &
      DISPLAY


;-------------------------------------------------------------------------------
;
; Popup sur les projets.
;
PROCEDURE INPUT T_PRJCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRJCLE2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PRJSTUPAT = "A|I"
    RUN SCREEN gp104 MODE F PASSING T_CIECLEFND, &
                                    T_PRJCLE2  , &
                                    T_PRJSTUPAT  &
                                    WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PRJCLE2)
  END
END

;PROCEDURE EDIT T_PRJCLE
;BEGIN
;   IF PRJSTU OF GPPROJETS <> "A"
;   THEN ERROR 862; Le statut du projet doit être actif.
;END



;-------------------------------------------------------------------------------
;
; Popup sur les sous-projets.
;
PROCEDURE INPUT T_PRACLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PRACLE2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_PRASTUPAT = "A|I"
    RUN SCREEN gp108 MODE F PASSING T_CIECLEFND, &
                                    T_PRJCLE   , &
                                    T_PRACLE2  , &
                                    T_PRASTUPAT  &
                                    WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_PRACLE2)
  END
END

;PROCEDURE EDIT T_PRACLE
;BEGIN
;   IF PRASTU OF GPPRO_ACTIVITE <> "A"
;   THEN ERROR 876; Le statut du sous-projet doit être actif.
;END



PROCEDURE INPUT T_PECFIN
BEGIN
   IF T_PECDEB <> " " AND T_PECDEB > FIELDTEXT
   THEN ERROR 888; La période de fin doit être supérieure ou égale
                 ; à la période de début.
END


;-------------------------------------------------------------------------------
;
; Popup sur les formats
;
PROCEDURE INPUT T_FRMCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_FRMCLE2 = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_FRMTYP  = "C"
    RUN SCREEN gp109 MODE F PASSING T_FRMCLE2, T_FRMTYP &
                                    WINDOW WIDTH CONSTANT WHEN CALLING
    LET FIELDTEXT = TRUNCATE(T_FRMCLE2)
  END
END


PROCEDURE EDIT T_FRMCLE
BEGIN
  IF FRMTYP OF GPFORMATS <> "C"
  THEN ERROR 885 ; Le format de présentation doit être comptable.
END

PROCEDURE INTERNAL LIGNE_SOULIGNE_TEXTE
BEGIN
   WHILE RETRIEVING GPFOR_DETAILS &
         VIA   FRMCLE,            &
               FRDTYP             &
         USING T_FRMCLE,          &
               "S"
   BEGIN
      IF 0 <> SIZE(TRUNCATE(T_PRACLE))
      THEN BEGIN
           LET USRCLE OF GPFRM_AFFICHE = LOGONID
           LET CIECLE OF GPFRM_AFFICHE = CIECLE OF GPPRO_ACTIVITE_2
           LET PRJCLE OF GPFRM_AFFICHE = PRJCLE OF GPPRO_ACTIVITE_2
           LET PRACLE OF GPFRM_AFFICHE = PRACLE OF GPPRO_ACTIVITE_2
           LET FRMCLE OF GPFRM_AFFICHE = T_FRMCLE
           LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF GPFOR_DETAILS
           LET FRDTYP OF GPFRM_AFFICHE = "S"
           LET FRAMNTBDG OF GPFRM_AFFICHE = 0
           LET FRAMNTENG OF GPFRM_AFFICHE = 0
           LET FRAMNTREE OF GPFRM_AFFICHE = 0
           LET FRABDG    OF GPFRM_AFFICHE = 0
           LET FRAREE    OF GPFRM_AFFICHE = 0
           PUT GPFRM_AFFICHE RESET
      END
      ELSE BEGIN
           LET USRCLE OF GPFRM_AFFICHE = LOGONID
           LET CIECLE OF GPFRM_AFFICHE = CIECLE OF GPPROJETS
           LET PRJCLE OF GPFRM_AFFICHE = PRJCLE OF GPPROJETS
           LET PRACLE OF GPFRM_AFFICHE = " "
           LET FRMCLE OF GPFRM_AFFICHE = T_FRMCLE
           LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF GPFOR_DETAILS
           LET FRDTYP OF GPFRM_AFFICHE = "S"
           LET FRAMNTBDG OF GPFRM_AFFICHE = 0
           LET FRAMNTENG OF GPFRM_AFFICHE = 0
           LET FRAMNTREE OF GPFRM_AFFICHE = 0
           LET FRABDG    OF GPFRM_AFFICHE = 0
           LET FRAREE    OF GPFRM_AFFICHE = 0
           PUT GPFRM_AFFICHE RESET
      END
   END

   WHILE RETRIEVING GPFOR_DETAILS &
         VIA   FRMCLE,            &
               FRDTYP             &
         USING T_FRMCLE,          &
               "T"
   BEGIN
      IF 0 <> SIZE(TRUNCATE(T_PRACLE))
      THEN BEGIN
           LET USRCLE OF GPFRM_AFFICHE = LOGONID
           LET CIECLE OF GPFRM_AFFICHE = CIECLE OF GPPRO_ACTIVITE_2
           LET PRJCLE OF GPFRM_AFFICHE = PRJCLE OF GPPRO_ACTIVITE_2
           LET PRACLE OF GPFRM_AFFICHE = PRACLE OF GPPRO_ACTIVITE_2
           LET FRMCLE OF GPFRM_AFFICHE = T_FRMCLE
           LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF GPFOR_DETAILS
           LET FRDTYP OF GPFRM_AFFICHE = "T"
           LET FRAMNTBDG OF GPFRM_AFFICHE = 0
           LET FRAMNTENG OF GPFRM_AFFICHE = 0
           LET FRAMNTREE OF GPFRM_AFFICHE = 0
           LET FRABDG    OF GPFRM_AFFICHE = 0
           LET FRAREE    OF GPFRM_AFFICHE = 0
           PUT GPFRM_AFFICHE RESET
      END
      ELSE BEGIN
           LET USRCLE OF GPFRM_AFFICHE = LOGONID
           LET CIECLE OF GPFRM_AFFICHE = CIECLE OF GPPROJETS
           LET PRJCLE OF GPFRM_AFFICHE = PRJCLE OF GPPROJETS
           LET PRACLE OF GPFRM_AFFICHE = " "
           LET FRMCLE OF GPFRM_AFFICHE = T_FRMCLE
           LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF GPFOR_DETAILS
           LET FRDTYP OF GPFRM_AFFICHE = "T"
           LET FRAMNTBDG OF GPFRM_AFFICHE = 0
           LET FRAMNTENG OF GPFRM_AFFICHE = 0
           LET FRAMNTREE OF GPFRM_AFFICHE = 0
           LET FRABDG    OF GPFRM_AFFICHE = 0
           LET FRAREE    OF GPFRM_AFFICHE = 0
           PUT GPFRM_AFFICHE RESET
      END
   END
END


PROCEDURE PREUPDATE
BEGIN
   ;----------------------------------------
   ; Destruction des fichiers temporaires
   ;----------------------------------------

   WHILE RETRIEVING GPFRM_PROJETS    &
              VIA   FRPUSR           &
              USING LOGONID[1:8]
   BEGIN
      DELETE GPFRM_PROJETS
      PUT GPFRM_PROJETS RESET
   END

   WHILE RETRIEVING GPFRM_AFFICHE VIA   USRCLE &
                                  USING LOGONID
   BEGIN
     DELETE GPFRM_AFFICHE
     PUT GPFRM_AFFICHE
   END

   IF 0 <> SIZE(TRUNCATE(T_PRACLE))
   THEN BEGIN
        WHILE RETRIEVING GPPRO_ACTIVITE_2 &
              VIA   CIECLE,          &
                    PRJCLE,          &
                    PRACLE           &
              USING T_CIECLEFND,     &
                    T_PRJCLE,        &
                    T_PRACLE
        BEGIN
           LET CIECLE OF GPFRM_PROJETS = CIECLE OF GPPRO_ACTIVITE_2
           LET PRJCLE OF GPFRM_PROJETS = PRJCLE OF GPPRO_ACTIVITE_2
           LET PRACLE OF GPFRM_PROJETS = PRACLE OF GPPRO_ACTIVITE_2
           LET FRMCLE OF GPFRM_PROJETS = T_FRMCLE
           LET FRPUSR OF GPFRM_PROJETS = LOGONID[1:8]
           PUT GPFRM_PROJETS RESET

           DO LIGNE_SOULIGNE_TEXTE
        END
   END
   ELSE BEGIN
        WHILE RETRIEVING GPPROJETS_2 &
              VIA   CIECLE,          &
                    PRJCLE           &
              USING T_CIECLEFND,     &
                    T_PRJCLE
        BEGIN
           LET CIECLE OF GPFRM_PROJETS = CIECLE OF GPPROJETS_2
           LET PRJCLE OF GPFRM_PROJETS = PRJCLE OF GPPROJETS_2
           LET PRACLE OF GPFRM_PROJETS = " "
           LET FRMCLE OF GPFRM_PROJETS = T_FRMCLE
           LET FRPUSR OF GPFRM_PROJETS = LOGONID[1:8]
           PUT GPFRM_PROJETS RESET

           DO LIGNE_SOULIGNE_TEXTE
        END
   END

   IF 0 <> SIZE(TRUNCATE(T_PRACLE))
   THEN BEGIN
        ;--------------------------------------------
        ; Ligne de calcul et de formule (SOUS-PROJET)
        ;--------------------------------------------
        WHILE RETRIEVING VPRA01 &
              VIA   CIECLE,     &
                    PRJCLE,     &
                    PRACLE,     &
                    FRMCLE      &
              USING T_CIECLEFND,&
                    T_PRJCLE,   &
                    T_PRACLE,   &
                    T_FRMCLE
        BEGIN
           GET GPFRM_AFFICHE &
               VIA   USRCLE, &
                     FRDCLE  &
               USING LOGONID, &
                     FRDCLE OF VPRA01 OPTIONAL
           IF NOT ACCESSOK
           THEN BEGIN
                LET USRCLE OF GPFRM_AFFICHE = LOGONID
                LET CIECLE OF GPFRM_AFFICHE = CIECLE OF VPRA01
                LET PRJCLE OF GPFRM_AFFICHE = PRJCLE OF VPRA01
                LET PRACLE OF GPFRM_AFFICHE = PRACLE OF VPRA01
                LET FRMCLE OF GPFRM_AFFICHE = FRMCLE OF VPRA01
                LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF VPRA01
                LET FRDTYP OF GPFRM_AFFICHE = "C"
           END

           IF FDOOPE OF VPRA01 = "+"
           THEN BEGIN
                LET FRAMNTENG OF GPFRM_AFFICHE = FRAMNTENG OF GPFRM_AFFICHE + &
                                                 V_MNTENG  OF VPRA01

                LET FRAMNTREE OF GPFRM_AFFICHE = FRAMNTREE OF GPFRM_AFFICHE + &
                                                 V_MNTREE  OF VPRA01

                LET FRAMNTBDG OF GPFRM_AFFICHE = FRAMNTBDG OF GPFRM_AFFICHE + &
                                                 V_MNTBDG  OF VPRA01        + &
                                                 V_MNTBDGREV OF VPRA01
           END
           ELSE BEGIN
                LET FRAMNTENG OF GPFRM_AFFICHE = FRAMNTENG OF GPFRM_AFFICHE - &
                                                 V_MNTENG  OF VPRA01

                LET FRAMNTREE OF GPFRM_AFFICHE = FRAMNTREE OF GPFRM_AFFICHE - &
                                                 V_MNTREE  OF VPRA01

                LET FRAMNTBDG OF GPFRM_AFFICHE = FRAMNTBDG OF GPFRM_AFFICHE - &
                                                 V_MNTBDG  OF VPRA01        - &
                                                 V_MNTBDGREV OF VPRA01
           END
           PUT GPFRM_AFFICHE RESET
        END
   END
   ELSE BEGIN
        ;----------------------------------------
        ; Ligne de calcul et de formule (PROJET)
        ;----------------------------------------
        WHILE RETRIEVING VPRJ01 &
              VIA   CIECLE,     &
                    PRJCLE,     &
                    FRMCLE      &
              USING T_CIECLEFND,&
                    T_PRJCLE,   &
                    T_FRMCLE
        BEGIN
           GET GPFRM_AFFICHE &
               VIA   USRCLE, &
                     FRDCLE  &
               USING LOGONID, &
                     FRDCLE OF VPRJ01 OPTIONAL
           IF NOT ACCESSOK
           THEN BEGIN
                LET USRCLE OF GPFRM_AFFICHE = LOGONID
                LET CIECLE OF GPFRM_AFFICHE = CIECLE OF VPRJ01
                LET PRJCLE OF GPFRM_AFFICHE = PRJCLE OF VPRJ01
                LET PRACLE OF GPFRM_AFFICHE = " "
                LET FRMCLE OF GPFRM_AFFICHE = FRMCLE OF VPRJ01
                LET FRDCLE OF GPFRM_AFFICHE = FRDCLE OF VPRJ01
                LET FRDTYP OF GPFRM_AFFICHE = "C"
           END

           IF FDOOPE OF VPRJ01 = "+"
           THEN BEGIN
                LET FRAMNTENG OF GPFRM_AFFICHE = FRAMNTENG OF GPFRM_AFFICHE + &
                                                 V_MNTENG OF VPRJ01

                LET FRAMNTREE OF GPFRM_AFFICHE = FRAMNTREE OF GPFRM_AFFICHE + &
                                                 V_MNTREE OF VPRJ01

                LET FRAMNTBDG OF GPFRM_AFFICHE = FRAMNTBDG OF GPFRM_AFFICHE + &
                                                 V_MNTBDG OF VPRJ01         + &
                                                 V_MNTBDGREV OF VPRJ01
           END
           ELSE BEGIN
                LET FRAMNTENG OF GPFRM_AFFICHE = FRAMNTENG OF GPFRM_AFFICHE - &
                                                 V_MNTENG OF VPRJ01

                LET FRAMNTREE OF GPFRM_AFFICHE = FRAMNTREE OF GPFRM_AFFICHE - &
                                                 V_MNTREE OF VPRJ01

                LET FRAMNTBDG OF GPFRM_AFFICHE = FRAMNTBDG OF GPFRM_AFFICHE - &
                                                 V_MNTBDG OF VPRJ01         - &
                                                 V_MNTBDGREV OF VPRJ01
           END
           PUT GPFRM_AFFICHE RESET
        END
   END
END

PROCEDURE POSTUPDATE
BEGIN
   RETURN
END

PROCEDURE ENTRY
BEGIN
  ACCEPT T_PRJCLE
  DISPLAY PRJDSC1 OF GPPROJETS
  DISPLAY PRJDSC2 OF GPPROJETS
  ACCEPT T_PRACLE
  DISPLAY PRADSC1 OF GPPRO_ACTIVITE
  DISPLAY PRADSC2 OF GPPRO_ACTIVITE

  ACCEPT T_PECDEB
  IF T_PECDEB <> " "
  THEN ACCEPT T_PECFIN

  ACCEPT T_FRMCLE
  DISPLAY FRMDSC OF GPFORMATS
  PUSH UPDATE
END

PROCEDURE DELETE
  DISABLE


PROCEDURE APPEND
  DISABLE

PROCEDURE FIND
  DISABLE


BUILD
