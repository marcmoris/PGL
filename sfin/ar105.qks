;/T/ Consultation des lots             TYPECR, CIECLE, PECCLE, LCPCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 28 mars 1994
;
;    Description..: Permet de visionner et de choisir un lot. 
;                   Les lots sont sélectionnés par usager(TZ_LOGONID).
;
;
;    Paramètre....: TYPECR ==> si <> blanc, seul les lots du même type seront
;                              sélectionnés, si = blanc tous les lots seront
;                              sélectionnés
;
;                   CIECLE ==> Compagnie, segment de la clé de recherche.
;
;                   PECCLE ==> segment de la clé de recherche.
;
;                   LARCLE ==> segment de la clé de recherche, peut contenir
;                              le caractére '@'.
;
;/M/ Pascal Tremblay, 98-01-21
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar105 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_TYPECR, &
                        T_CIECLE, &
                        T_PECCLE, &
                        T_LARCLE

USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR105"

USE ar105.hlp NOLIST

TEMPORARY T_TYPECR   CHARACTER SIZE 2 ; Type du lot
TEMPORARY T_CIECLE   CHARACTER SIZE 4 ; Compagnie du lot
TEMPORARY T_PECCLE   CHARACTER SIZE 4 ; Période du lot
TEMPORARY T_LARCLE   CHARACTER SIZE 4 ; Numéro du lot

;-------------------------------------------------------------------------------
;
FILE ARLOT             PRIMARY &
                      OCCURS 10

 ACCESS VIA     CIECLE   , &
                PECCLE   , &
                LARCLE   , &
                LARUSR   , &
                LARDATJOU  &
        USING   T_CIECLE, &
                T_PECCLE, &
                T_LARCLE, &
                TZ_LOGONID,  &
                0         &
        ORDERBY CIECLE, &
                PECCLE, &
                LARCLE

 SELECT IF LARTYPECR OF ARLOT = T_TYPECR OR T_TYPECR = ""

;-------------------------------------------------------------------------------
;
; Pour afficher le type d'écriture(bilingue).
;
FILE VCBI_DSC          DESIGNER &
                      OPEN READ


;-------------------------------------------------------------------------------
;
DEFINE D_LARTYPECR CHAR SIZE 16 = "LARTYPECR"

USE ushilite NOLIST     
USE usdrwfnd NOLIST     

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

@IF FRANCAIS
    TITLE "Lot Type  Validé   T-Réel T-Prévue" AT 2,7
@ELSE
   TITLE "Batch Type  Date val.  Co. Centre" AT 2,5
@ENDIF

;-------------------------------------------------------------------------------

;ALIGN(3,3,6) (,,12) (,,16) (,,25) (,,33)
ALIGN(3,3,6) (,,12) (,,16) (,,27) (,,34)

CLUSTER OCCURS WITH ARLOT

FIELD LARCLE       OF ARLOT HIDDEN LABEL ">>"
FIELD LARTYPECR    OF ARLOT
FIELD LARDATVAL    OF ARLOT FORMAT YYYYMMDD 
FIELD LARNBRTRS    OF ARLOT &
      PICTURE "^,^^^ " TRAILING "-"
FIELD LARNBRTRSVER OF ARLOT &
      PICTURE "^,^^^ " TRAILING "-"

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enlève le @ si l'usager ne fait aucun choix à l'écran.
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_LARCLE, "@")
  THEN LET T_LARCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_LARCLE = LARCLE OF ARLOT
  RETURN
END

BUILD
@IF FORMAT
                                       *****************************************
                                       *     Lot Type  Validé  T-Réel T-Prévue *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       * >> xxxx  xx  xxxxxxxx xxxxxx  xxxxxx  *
                                       *****************************************
@ENDIF
