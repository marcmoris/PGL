;/T/ Lieu de chargement
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 17 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base   
;                   concernant le lieu de chargement  
;
;
SCREEN if153 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF153"

;USE if153.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFLIEU_CHARG    IN DICT PRIMARY NOITEM &
                      OCCURS 16 TIMES

  ACCESS VIA     CIECLE,   &
                 LICCLE    &
         USING   T_CIECLE, &
                 LICCLE    &  
         REQUEST LICCLE    &
         ORDERBY LICCLE  

  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY LICCLE

  ITEM CIECLE OF IFLIEU_CHARG INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1  

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Lieu de chargement " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST


TITLE "Lieu" AT ,6
TITLE "Desc. francaise" AT ,18
TITLE "Desc. anglaise" AT ,46
SKIP

ALIGN(5,6,7) (,,18) (,,46)
CLUSTER OCCURS WITH IFLIEU_CHARG
FIELD LICCLE OF IFLIEU_CHARG &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFLIEU_CHARG &
          VIA   CIECLE, &
                LICCLE  &
          USING T_CIECLE, &
                LICCLE OF IFLIEU_CHARG &
          MESSAGE 3238 ; PD338 Ce lieu de chargement existe déjà

FIELD LICDSCFRA OF IFLIEU_CHARG &
      REQUIRED

FIELD LICDSCANG OF IFLIEU_CHARG 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Valeur par defaut pour la description anglaise
;
PROCEDURE INPUT LICDSCANG
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = LICDSCFRA OF IFLIEU_CHARG
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'ecran de validation de la destruction d'un lieu de chargement
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'\351cran de validation
   RUN SCREEN if153x MODE SAME &
                   PASSING IFLIEU_CHARG, &
                           T_FLGDEL

   ; Si le code est utilis\351 on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3246 ; PD346 Impossible de supprimer ce lieu de chargement,
                     ; il est d\351j\340 utilis\351
   ELSE DELETE IFLIEU_CHARG
END

BUILD
