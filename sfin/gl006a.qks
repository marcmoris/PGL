
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN gl006a      NOMODE NOACTION FIELDMARK STARTUP &
                   FROM 2,1 TO 13,80 &
                   MESSAGE ON 24 &
                   AUTOUPDATE &
                   HELP POPUP FROM 4,9 TO 20,72 &
                   ACTIVITIES ENTRY &
                   RECEIVING T_FLGMNU, &
                             T_CIECLEMNU, &
                             T_PAR_PERDEB, &
                             T_PAR_PERFIN, &
                             T_PAR_MNT1, &
                             T_PAR_MNT2, &
                             T_CIECLE, &
                             T_UNACLE, &
                             T_CPTCLE, &
                             T_SANANN, &
                             T_REFCLETYP, &
                             T_REFCLENUM


USE ussectmp NOLIST     ; Variable pour la securite
    "GL006A"

USE gl006a.hlp NOLIST

;-------------------------------------------------------------------------------0
;
; Variables passer en paramètres a l'ecran.
;
TEMPORARY T_SANANN    CHARACTER SIZE 2
TEMPORARY T_CIECLE    CHARACTER SIZE 4
TEMPORARY T_UNACLE    CHARACTER SIZE 8
TEMPORARY T_CPTCLE    CHARACTER SIZE 6
TEMPORARY T_PECCLE    CHARACTER SIZE 4
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_REFCLETYP CHARACTER SIZE 1
TEMPORARY T_REFCLENUM CHARACTER SIZE 6
TEMPORARY T_FLGMNU    CHARACTER SIZE 1 ; Provenance du menu(Holding ou cie)

;
; Variables pour la saisie des paramètres de l'usager (Recherche)
;
TEMPORARY T_PAR_SANANN    CHARACTER SIZE 2 RESET AT MODE
TEMPORARY T_PAR_CIECLE    CHARACTER SIZE 4 RESET AT MODE
TEMPORARY T_PAR_UNACLE    CHARACTER SIZE 8 RESET AT MODE
TEMPORARY T_PAR_CPTCLE    CHARACTER SIZE 6 RESET AT MODE
TEMPORARY T_PAR_MNT1      FLOAT     SIZE 8 RESET AT MODE
TEMPORARY T_PAR_MNT2      FLOAT     SIZE 8 RESET AT MODE
TEMPORARY T_PAR_REFCLETYP CHARACTER SIZE 1 RESET AT MODE
TEMPORARY T_PAR_REFCLENUM CHARACTER SIZE 6 RESET AT MODE
TEMPORARY T_PAR_PERDEB    CHARACTER SIZE 2 RESET AT MODE
TEMPORARY T_PAR_PERFIN    CHARACTER SIZE 2 RESET AT MODE

;
; Sert à valider les codes bilingues et pour les sous-écrans de consultation
;
DEFINE    D_REFCLETYP     CHARACTER SIZE 16 = "REFCLETYP"
TEMPORARY T_REFCLETYP_POP CHARACTER SIZE 4
TEMPORARY T_CPTTYPPAT     CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT     CHARACTER SIZE 8 ; Popup sur les comptes

;------------------------------------------------------------------------------
;
; Pour le Flag sur les chartes universels.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING ("1")
;
; Nom de la compagnie
;
FILE MCCOMPAGNIE      REFERENCE
  ACCESS VIA CIECLE &
         USING T_PAR_CIECLE
;
; Unité administrative
;
FILE MCUNITE_ADM      REFERENCE
  ACCESS VIA CIECLE, &
             UNACLE &
         USING T_PAR_CIECLE, &
               T_PAR_UNACLE
;
; Si c'est une charte universel on prend le caractère de
; substitution pour la clé de la compagnie.
;
DEFINE D_REFCIECLE CHARACTER SIZE 4 = T_PAR_CIECLE &
                                        IF ( T_PAR_REFCLETYP = "E" AND &
                                             HLDCHTEMP OF MCHOLDING = "1") OR &
                                           ( T_PAR_REFCLETYP = "F" AND &
                                             HLDCHTFOU OF MCHOLDING = "1") OR &
                                           ( T_PAR_REFCLETYP = "C" AND &
                                             HLDCHTCLI OF MCHOLDING = "1") &
                                 ELSE HLDCHRSUB OF MCHOLDING
;
; Pour afficher le nom de la référence.
;
FILE MCREFERENCE      REFERENCE
  ACCESS VIA REFCIECLE, &
             REFCLETYP, &
             REFCLENUM &
         USING D_REFCIECLE , &
               T_PAR_REFCLETYP, &
               T_PAR_REFCLENUM
;
; Pour afficher la description du compte comptable.
;
FILE VCPT_DSC REFERENCE
  ACCESS VIA CPTCLE USING T_PAR_CPTCLE
;
; Code bilingue du type de référence.
;
FILE VCBI_DSC         REFERENCE
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_REFCLETYP, &
               T_PAR_REFCLETYP

;-------------------------------------------------------------------------------
;
; Popup sur les références.
;
TEMPORARY T_REFCLENUM_POP CHARACTER SIZE 6
TEMPORARY T_REFSTUPAT     CHARACTER SIZE 5 INITIAL "@"


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW  1,1 TO 12,80

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN (2,3,19) (,,24) (,45,53) (,,60)
SKIP 1
FIELD T_PAR_CIECLE HIDDEN ID 05 &
@IF FRANCAIS
        LABEL "Compagnie.....:"
@ELSE
        LABEL "Company.......:"
@ENDIF

FIELD CIENOMABR OF MCCOMPAGNIE

FIELD T_PAR_CPTCLE &
        REQUIRED &
@IF FRANCAIS
        LABEL "Compte:"
@ELSE
        LABEL "Accnt.:"
@ENDIF
FIELD CPTDSCABR OF VCPT_DSC &
        DISPLAY

SKIP 1

ALIGN (2,3,19) (,,28)
FIELD T_PAR_UNACLE HIDDEN ID 10 &
@IF FRANCAIS
        LABEL "Unité adm.....:"
@ELSE
        LABEL "Adm. unit.....:"
@ENDIF
FIELD UNANOMABR OF MCUNITE_ADM

SKIP 1

ALIGN(,3,19) (,24,39) (,45,50)
FIELD T_PAR_SANANN &
        NUMERIC &
        REQUIRED &
@IF FRANCAIS
        LABEL "Année.........:"
@ELSE
        LABEL "Year..........:"
@ENDIF
FIELD T_PAR_PERDEB &
        NUMERIC &
        SIGNIFICANCE 2 &
        DEFAULT "01" &
@IF FRANCAIS
        LABEL "Période début:"
@ELSE
        LABEL "Start period.:"
@ENDIF

FIELD T_PAR_PERFIN  &
        NUMERIC &
        SIGNIFICANCE 2 &
        DEFAULT T_PAR_PERDEB &
@IF FRANCAIS
        LABEL "Fin:"
@ELSE
        LABEL "End:"
@ENDIF

SKIP 1

ALIGN(,3,19) (,,21) (,,28)
FIELD T_PAR_REFCLETYP &
        UPSHIFT &
@IF FRANCAIS
        LABEL "Référence.....:"
@ELSE
        LABEL "Reference.....:"
@ENDIF

FIELD T_PAR_REFCLENUM &
        UPSHIFT
FIELD REFNOM OF MCREFERENCE

SKIP 1

ALIGN(,3,19) (,37,53)

FIELD T_PAR_MNT1 &
        DEFAULT -99999999999 &
        PICTURE " ^^^,^^^,^^^.^^ " &
        TRAILING SIGN ">" &
        LEADING  SIGN "<" &
        SIGNIFICANCE 5 &
        INPUT SCALE  2 &
        OUTPUT SCALE 0 &
@IF FRANCAIS
        LABEL "Montant >= ...:"
@ELSE
        LABEL "Amount >= ....:"
@ENDIF

FIELD T_PAR_MNT2 &
        DEFAULT 99999999999 &
        PICTURE " ^^^,^^^,^^^.^^ " &
        TRAILING SIGN ">" &
        LEADING  SIGN "<" &
        SIGNIFICANCE 5 &
        INPUT SCALE  2 &
        OUTPUT SCALE 0 &
@IF FRANCAIS
        LABEL "Montant <= ...:"
@ELSE
        LABEL "Amount <= ....:"
@ENDIF

;------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Pop-up pour le visionnement des compagnies
;
;
PROCEDURE INPUT T_PAR_CIECLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup des unités administrative.
;
;
PROCEDURE INPUT T_PAR_UNACLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    IF T_PAR_CIECLE = ""
    THEN LET T_CIECLE = "@"
    ELSE LET T_CIECLE = T_PAR_CIECLE
    RUN SCREEN mc104 PASSING T_CIECLE, T_UNACLE MODE F
    LET FIELDTEXT = TRUNC(T_UNACLE)
  END
END



;-------------------------------------------------------------------------------
;
;
; Aide au choix du compte ou pour la consultaion
;
;
PROCEDURE INPUT T_PAR_CPTCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Appel d'un pop-up pour visualiser le code pour le type de référence
; dans la langue de l'usager.
;
;
PROCEDURE INPUT T_PAR_REFCLETYP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_REFCLETYP_POP = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_REFCLETYP, T_REFCLETYP_POP
    LET FIELDTEXT = TRUNC(T_REFCLETYP)
  END
END

;-------------------------------------------------------------------------------
;
;
; Valiation du type de référence.
;
;
PROCEDURE EDIT T_PAR_REFCLETYP
BEGIN
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    GET VCBI_DSC OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 279 ; Code invalide
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les références(Fournisseur, employé et client)
;
;
PROCEDURE INPUT T_PAR_REFCLENUM
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = D_REFCIECLE
    LET T_REFCLENUM_POP = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
;??
    RUN SCREEN mc112 MODE F &
                     PASSING T_CIECLE, &
                             T_PAR_REFCLETYP, &
                             T_REFCLENUM_POP, &
                             T_REFSTUPAT
    LET FIELDTEXT = TRUNC(T_REFCLENUM_POP)
  END
END


;-------------------------------------------------------------------------------
;
PROCEDURE ENTRY
BEGIN
  LET T_PAR_CIECLE    = " "
  LET T_PAR_UNACLE    = " "
  LET T_PAR_CPTCLE    = " "
  LET T_PAR_SANANN    = " "
  LET T_PAR_PERDEB    = " "
  LET T_PAR_PERFIN    = " "
  LET T_PAR_REFCLETYP    = " "
  LET T_PAR_REFCLENUM = " "
  LET T_PAR_MNT1      = 0
  LET T_PAR_MNT2      = 0

  ;
  ; --- Compagnie ---
  ;
  IF T_FLGMNU = "C"
  THEN BEGIN
    LET T_PAR_CIECLE = T_CIECLEMNU
    DISPLAY T_PAR_CIECLE
    DISPLAY CIENOMABR OF MCCOMPAGNIE
  END
  ELSE BEGIN
    ACCEPT T_PAR_CIECLE
    DISPLAY CIENOMABR OF MCCOMPAGNIE
  END
  ;
  ACCEPT T_PAR_CPTCLE
  DISPLAY CPTDSCABR OF VCPT_DSC
  ;
  ACCEPT T_PAR_UNACLE
  DISPLAY UNANOMABR OF MCUNITE_ADM
  ;
  ACCEPT T_PAR_SANANN
  ACCEPT T_PAR_PERDEB
  ACCEPT T_PAR_PERFIN
  ;
  ACCEPT T_PAR_REFCLETYP
  ;
  ACCEPT T_PAR_REFCLENUM
  DISPLAY REFNOM OF MCREFERENCE
  ;
  ACCEPT T_PAR_MNT1
  ACCEPT T_PAR_MNT2

  IF 0 = SIZE(TRUNC(T_PAR_SANANN))
  THEN LET T_SANANN = "@"
  ELSE LET T_SANANN = T_PAR_SANANN

  IF 0 = SIZE(TRUNC(T_PAR_CPTCLE))
  THEN LET T_CPTCLE = "@"
  ELSE LET T_CPTCLE = T_PAR_CPTCLE

  IF 0 = SIZE(TRUNC(T_PAR_UNACLE))
  THEN LET T_UNACLE = "@"
  ELSE LET T_UNACLE = T_PAR_UNACLE

  IF 0 = SIZE(TRUNC(T_PAR_CIECLE))
  THEN LET T_CIECLE = "@"
  ELSE LET T_CIECLE = T_PAR_CIECLE

  LET T_REFCLETYP = T_PAR_REFCLETYP
  LET T_REFCLENUM = T_PAR_REFCLENUM

  RETURN
END

BUILD
@IF FORMAT
********************************** Paramètres **********************************
*                                                                              *
* Compagnie.....: xxxx xxxxxxxxxxxxxxxxxxxx Compte: xxxxxx xxxxxxxxxxxxxxxxxxxx*
*                                                                              *
* Unité adm.....: xxxxxxxx xxxxxxxxxxxxxxxxxxxx                                *
*                                                                              *
* Année.........: xx   Période début: xx    Fin: xx                            *
*                                                                              *
* Référence.....: x xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
* Montant >= ...: xxxxxxxxxxxxxxxx  Montant <= ...: xxxxxxxxxxxxxxxx           *
********************************************************************************
@ENDIF
