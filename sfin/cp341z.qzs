;/T/ Liste des fournisseurs / détaillé
;
;/P/ Programmeur...: Chantal Martineau
;    Date Création.: 93-06-11
;
;    Description...: Liste des fournisseurs détaillé
;
;    Paramètres....: Fournisseur (1, plusieurs, tous)
;                    Ordre Alpha-Num
;                    Statut (1, plusieurs, tous)
;                    Catégorie (1,plusieurs,tous)
;
;-------------------------------------------------------------------------------

 USE ussetqzs NOLIST   ; permet de faire les SET REPORT DEV....
 SET REPORT NOBLANK    ; empêche l'impression de ligne blanche

ACCESS VFOC_FOU &
  LINK FOUCIECLE    OF VFOC_FOU, &
       FOUREFTYP    OF VFOC_FOU, &
       FOUCLE       OF VFOC_FOU, &
       FOCADRACH    OF VFOC_FOU &
    TO REFCIECLE               , &
       REFCLETYP               , &
       REFCLENUM               , &
       RADCLE                  OF MCREF_ADRESSE ALIAS MC_REF_ADRACH OPTIONAL &
  LINK FOUCIECLE    OF VFOC_FOU, &
       FOUREFTYP    OF VFOC_FOU, &
       FOUCLE       OF VFOC_FOU, &
       FOCADRPAM    OF VFOC_FOU  &
    TO REFCIECLE               , &
       REFCLETYP               , &
       REFCLENUM               , &
       RADCLE                  OF MCREF_ADRESSE ALIAS MC_REF_ADRPAM OPTIONAL &
  LINK FOUCIECLE    OF VFOC_FOU, &
       FOUCLE       OF VFOC_FOU  &
    TO FOUCIECLE               , &
       FOUCLE                  OF CPFOU_CONTACT OPTIONAL &
  LINK ("FOUSTU"               , &
       FOUSTU OF VFOC_FOU)       &
    TO XELNOM                  , &
       CBICLE                  OF VCBI_DSC ALIAS VCBI_FOUSTU OPTIONAL &
  LINK ("FOUCAT"               , &
       FOUCAT OF VFOC_FOU)       &
    TO XELNOM                  , &
       CBICLE                  OF VCBI_DSC ALIAS VCBI_FOUCAT OPTIONAL &
  LINK FOCCPTCAP OF VFOC_FOU     &
    TO CPTCLE                  OF VCPT_DSC ALIAS  VCPT_CPTCAP OPTIONAL &
  LINK LBQCPTENC OF VFOC_FOU     &
    TO CPTCLE                  OF VCPT_DSC ALIAS  VCPT_CPTENC OPTIONAL &
  LINK FOCCPTESC OF VFOC_FOU     &
    TO CPTCLE                  OF VCPT_DSC ALIAS  VCPT_CPTESC OPTIONAL



CHOOSE FOUCIECLE PARM, &              ; Compagnie fournisseur
       FOCCIECLE PARM, &              ; Compagnie menu
       FOUCLE    PARM, &              ; Fournisseur
       FOUSTU    PARM, &              ; Statut
       FOUCAT    PARM                 ; Catégorie



DEFINE D_TYP_TRI CHAR SIZE 1  = PARM  ; Ordre du tri : 1- Nom fournisseur 2-Numéro Fournisseur

 DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = & ; titre1  du rapport
@IF FRANCAIS
                    "Liste des fournisseurs / détaillé"
@ELSE
                    "%%%Liste des fournisseurs / détaillé"
@ENDIF

 DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = & ; titre2 du rapport selon ordre demandé
@IF FRANCAIS
                    "Par ordre de nom" IF D_TYP_TRI="1" &
                    ELSE "Par ordre de numéro" IF D_TYP_TRI="2"
@ELSE
                    "In Name Order"    IF D_TYP_TRI="1" &
                    ELSE "In Number Order"     IF D_TYP_TRI="2"
@ENDIF
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "CP341"        ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM           ; Nom de la compagnie

;-------------------------------------------------------------------------------
; Permet d'imprimer l'adresse paiement , No téléphone et fax seulement si adresse
; paiement <> adresse achat.
;-------------------------------------------------------------------------------
                                 ;Ligne (1) de l'adesse de paiement si <> adresse achat
DEFINE D_ADR_1 CHAR SIZE 66 =   &
@IF FRANCAIS
        "Adresse paiement : " + &
@ELSE
        "%%%Adresse paiement : " + &
@ENDIF
                                 RADADR1   OF MC_REF_ADRPAM &
            IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Ligne (2) de l'adesse de paiement si <> adresse achat
DEFINE D_ADR_2 CHAR SIZE 40 = RADADR2   OF MC_REF_ADRPAM &
            IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Ligne (3) de l'adesse de paiement si <> adresse achat
DEFINE D_ADR_3 CHAR SIZE 40 = RADADR3   OF MC_REF_ADRPAM &
            IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Ligne (4) de l'adesse de paiement si <> adresse achat
DEFINE D_ADR_4 CHAR SIZE 40 = RADADR4   OF MC_REF_ADRPAM &
            IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Ligne (5) de l'adesse de paiement si <> adresse achat
DEFINE D_CP       CHAR SIZE 7  = RADCP     OF MC_REF_ADRPAM &
            IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Ligne impression no téléphone de l'adesse de paiement si <> adresse achat
DEFINE D_TEL      CHAR SIZE 19 = &
@IF FRANCAIS
                  "Tél.: "   &
@ELSE
                  "Tel.: "   &
@ENDIF
            + RADTEL    OF MC_REF_ADRPAM IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Ligne impression no fax de l'adesse de paiement si <> adresse achat
DEFINE D_FAX      CHAR SIZE 19 = "Fax.: "     + RADFAX    OF  MC_REF_ADRPAM &
            IF FOCADRACH OF VFOC_FOU <> FOCADRPAM OF VFOC_FOU

                                 ;Empêche l'impression de l'entete (1) des contacts si aucun
DEFINE D_ENT_CONTACT1 CHARACTER SIZE 95 = &
@IF FRANCAIS
  "      Contact               Titre           Téléphone   Poste      Fax       Cellulaire  "&
@ELSE
  "%%%   Contact               Titre           Téléphone   Poste      Fax       Cellulaire  "&
@ENDIF
  IF RECORD CPFOU_CONTACT EXIST ELSE " "

                                 ;Empêche l'impression de l'entete (1) des contacts si aucun
DEFINE D_ENT_CONTACT2 CHARACTER SIZE 95 = &
  "-------------------- -------------------- ------------- ----- ------------- -------------" &
  IF RECORD CPFOU_CONTACT EXIST ELSE " "


                                      ;Assigne le numéro ou nom du fournisseur dans variable du tri
DEFINE D_TRI   CHAR SIZE 40 = FOUNOM OF VFOC_FOU IF D_TYP_TRI ="1" ELSE &
                              FOUCLE OF VFOC_FOU IF D_TYP_TRI ="2"



SORT ON D_TRI                            ; Ordre du tri (Nom fournisseur ou Numéro Fournisseur)



USE ustitstd NOLIST  ; Titre standard pour l'application
 SKIP 2

HEADING AT D_TRI &
  TAB 001 "----------------------------------------------------------------------" &
  TAB 071 "--------------------------------------------------------------" &
 SKIP &
@IF FRANCAIS
  TAB 001 "Fournisseur:" &
@ELSE
  TAB 001 "%%%rnisseur:" &
@ENDIF
  TAB 014 FOUCLE OF VFOC_FOU &
  TAB 021 FOUNOM OF VFOC_FOU &
@IF FRANCAIS
  TAB 062 "Enrg. TPS:" &
@ELSE
  TAB 062 "%%%g. TPS:" &
@ENDIF
  TAB 073 FOCENRTPS OF VFOC_FOU &
@IF FRANCAIS
  TAB 089 "Compte CAP.....:" &
@ELSE
  TAB 089 "%%%pte CAP.....:" &
@ENDIF
  TAB 106 FOCCPTCAP OF VFOC_FOU &
  TAB 113 CPTDSCABR OF VCPT_CPTCAP &
 SKIP &
  TAB 021 FOUNOMABR OF VFOC_FOU &
@IF FRANCAIS
  TAB 062 "Enrg. TVQ:" &
@ELSE
  TAB 062 "%%%g. TVQ:" &
@ENDIF
  TAB 073 FOCENRTVQ OF VFOC_FOU &
@IF FRANCAIS
  TAB 089 "Compte encaisse:" &
@ELSE
  TAB 089 "%%%pte encaisse:" &
@ENDIF
  TAB 106 LBQCPTENC OF VFOC_FOU &
  TAB 113 CPTDSCABR OF VCPT_CPTENC &
SKIP &
@IF FRANCAIS
  TAB 021 "Catégorie:" &
@ELSE
  TAB 021 "%%%:" &
@ENDIF
  TAB 032 CBIDSC OF VCBI_FOUCAT &
@IF FRANCAIS
  TAB 062 "Statut...:" &
@ELSE
  TAB 062 "%%%.:" &
@ENDIF
  TAB 073 CBIDSC OF VCBI_FOUSTU PICTURE "^^^^^^^^^^^^^^^" &
@IF FRANCAIS
  TAB 089 "Compte escompte:" &
@ELSE
  TAB 089 "Discount accnt.:" &
@ENDIF
  TAB 106 FOCCPTESC OF VFOC_FOU &
  TAB 113 CPTDSCABR OF VCPT_CPTESC &
 SKIP &
@IF FRANCAIS
  TAB 021 "Terme net:" &
@ELSE
  TAB 021 "%%%me net:" &
@ENDIF
  TAB 032 FOCTERNET OF VFOC_FOU &
@IF FRANCAIS
  TAB 040 "Terme esc(1):" &
@ELSE
  TAB 040 "%%%me esc(1):" &
@ENDIF
  TAB 054 FOCTERESC1 OF VFOC_FOU &
@IF FRANCAIS
  TAB 062 "Terme esc(2):" &
@ELSE
  TAB 062 "%%%me esc(2):" &
@ENDIF
  TAB 076 FOCTERESC2 OF VFOC_FOU &
@IF FRANCAIS
  TAB 089 "Devise.........:" &
@ELSE
  TAB 089 "%%%Devise......:" &
@ENDIF
  TAB 106 DEVCLE OF VFOC_FOU &
SKIP 2 &

@IF FRANCAIS
  TAB 003 "Adresse : " &
@ELSE
  TAB 003 "%%%esse : " &
@ENDIF
  TAB 012 RADADR1    OF MC_REF_ADRACH  &
  TAB 062 D_ADR_1                      &
  SKIP 1 &
  TAB 012 RADADR2    OF MC_REF_ADRACH  &
  TAB 081 D_ADR_2                   &
  SKIP 1 &
  TAB 012 RADADR3    OF MC_REF_ADRACH  &
  TAB 081 D_ADR_3                   &
  SKIP 1 &
  TAB 012 RADADR4    OF MC_REF_ADRACH  &
  TAB 081 D_ADR_4                   &
  SKIP 1 &
  TAB 012 RADCP      OF MC_REF_ADRACH  &
  TAB 081 D_CP                         &
  SKIP 2 &
@IF FRANCAIS
  TAB 003 "Tél.: " &
@ELSE
  TAB 003 "Tel.: " &
@ENDIF
  TAB 009 RADTEL     OF MC_REF_ADRACH  &
  TAB 023 "Fax.: " &
  TAB 029 RADFAX     OF MC_REF_ADRACH  &
  TAB 062 D_TEL                       &
  TAB 082 D_FAX                        &
  SKIP 2


REPORT &
  TAB 003 D_ENT_CONTACT1 PRINT AT D_TRI &
SKIP &
  TAB 003 D_ENT_CONTACT2 PRINT AT D_TRI &
SKIP &
  TAB 003 CTFNOM OF CPFOU_CONTACT &
  TAB 024 CTFTIT OF CPFOU_CONTACT &
  TAB 045 CTFTEL OF CPFOU_CONTACT &
  TAB 059 CTFTELPOS OF CPFOU_CONTACT &
  TAB 065 CTFFAX   OF CPFOU_CONTACT &
  TAB 079 CTFTELCEL OF CPFOU_CONTACT

BUILD cp341z
@IF FORMAT

                                                                                                   1         1         1         1
         1         2         3         4         5         6         7         8         9         0         1         2         3
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
CP341                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                      Liste des fournisseurs / détaillé
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx(1)

------------------------------------------------------------------------------------------------------------------------------------
Fournisseur: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Enrg. TPS: xxxxxxxxxxxxxxx Compte CAP.....: 123456 xxxxxxxxxxxxxxxxxxxx
                    xxxxxxxxxxxxxxxxxxxx                     Enrg. TVQ: xxxxxxxxxxxxxxx Compte encaisse: 123456 xxxxxxxxxxxxxxxxxxxx
                    Catégorie: xxxxxxxxxxxxxxxxxxxxxxxxx     Statut...: xxxxxxxxxxxxxxx Compte escompte: 123456 xxxxxxxxxxxxxxxxxxxx
                    Terme Net: xxxx    Terme esc(1): xxxx    Terme esc(2): xxxx                          Devise.........: xxx

  Adresse : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx         Adresse paiement : xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx (2)
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxx-xxx                                                             xxx-xxx

  Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx                    Tél.: xxxxxxxxxxxxx Fax.: xxxxxxxxxxxxx

        Contact               Titre           Téléphone   Poste      Fax       Cellulaire
  -------------------- -------------------- ------------- ----- ------------- -------------
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxx xxxx  xxxxxxxxxxxxx xxxxxxxxxxxxx
------------------------------------------------------------------------------------------------------------------------------------
 (1) par ordre alphabétique ou par ordre numérique
 (2) Impression de l'adresse d'achat si elle est différente de l'adresse de paiement.
@ENDIF
