;/T/ Consultation ligne de commande     CIECLE, CMDCLE, REDCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 29 mars 1994
;
;    Description..: Permet de visionner et de choisir une ligne de
;                   réception.
;
;    Paramètre....: CIECLE ==> - compagnie de la commande
;                              - segment de la clé de recherche.
;
;                   CMDCLE ==> - numéro de commande
;                              - segment de la clé de recherche.
;
;                   REDCLE ==> - segment de la clé de recherche.
;                              - peut contenir le caractère '@'.
;                              - contient la valeur de retour.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar109 NOMODE NOACTION FIELDMARK &
             FROM 11,18 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_CMDCLE, &
                        T_REDCLE
                     

USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR109"

USE ar109.hlp NOLIST

TEMPORARY T_CIECLE CHARACTER SIZE 4 ; Compagnie de la commande
TEMPORARY T_CMDCLE CHARACTER SIZE 9 ; Numéro de commande
TEMPORARY T_REDCLE CHARACTER SIZE 7 ; Numéro d'item


;-------------------------------------------------------------------------------
;
FILE VCMD_CDE      PRIMARY &
                  OPEN READ &
                  OCCURS 10

 ACCESS   VIA     CIECLE, &
                  CMDCLE  &
          USING   T_CIECLE, &
                  T_CMDCLE  &
          ORDERBY CIECLE , &
                  CMDCLE , &
                  CDECLE

 SELECT IF CDEQTECOM OF VCMD_CDE > CDEQTEREC OF VCMD_CDE

;-------------------------------------------------------------------------------
;

USE ushilite NOLIST     

DRAW 1,1 TO 13,63

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

@IF FRANCAIS
    TITLE "Item    Produit       Qté cmd   Qté rec      Prix" AT 2,7
@ELSE 
   TITLE "%%%Item    Produit       Qté cmd   Qté rec      Prix" AT 2,7
@ENDIF


ALIGN(3,3,6)(,,14) (,,27) (,,37) (,,47)

CLUSTER OCCURS WITH VCMD_CDE

FIELD CDECLE    OF VCMD_CDE HIDDEN LABEL ">>"
FIELD PRDCLE    OF VCMD_CDE
FIELD CDEQTECOM OF VCMD_CDE &
  PICTURE "^^^^^.^^^"
FIELD CDEQTEREC OF VCMD_CDE &
  PICTURE "^^^^^.^^^"
FIELD CDEPRIUNI OF VCMD_CDE
 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_REDCLE, "@")
  THEN LET T_REDCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_REDCLE = ASCII (CDECLE OF VCMD_CDE,6)[1:3] + "." + &
                 ASCII (CDECLE OF VCMD_CDE,6)[4:6]   
  RETURN
END

BUILD
@IF FORMAT
                 ***************************************************************
                 *     Item    Produit       Qté cmd   Qté rec      Prix       *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 * >> xxxxxxx xxxxxxxxxxxx xxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxx *
                 ***************************************************************
@ENDIF
