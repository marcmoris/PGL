;/T/ Autorisation des lots
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 26 mai 1993
;
;    Description..: Cet écran permet d'autoriser les lots pour les
;                   journaliser
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr002 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND, CHANGE &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp  NOLIST
    "CR002"

USE cr002.hlp NOLIST

USE usactecr  NOLIST


;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4 ; Numéro de compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40; Nom de la compagnie du menu.


;-------------------------------------------------------------------------------
;

FILE CRLOT            PRIMARY &
                      OCCURS 16
  ACCESS REQUEST PECCLE OF CRLOT, &
                 LCRCLE OF CRLOT  &
         VIA     CIECLE, &
                 PECCLE, &
                 LCRCLE  &
         USING   T_CIECLEMNU, &
                 PECCLE OF CRLOT, &
                 LCRCLE OF CRLOT  &
         ORDERBY LCRCLE

  ACCESS REQUEST PECCLE OF CRLOT &
         VIA     CIECLE, &
                 PECCLE  &
         USING   T_CIECLEMNU, &
                 PECCLE OF CRLOT &
         ORDERBY LCRCLE

;
; Pour afficher la description du type de lot.
;
DEFINE D_LCRTYPECR CHAR SIZE 16 = "LCRTYPECR"
;
FILE VCBI_DSC         REFERENCE
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_LCRTYPECR, &
               LCRTYPECR OF CRLOT


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER(T_CIENOM)


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Autorisation des lots " &
@ELSE
      " Batch Consultation " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN (,3,19)

FIELD PECCLE OF CRLOT &
        DISPLAY

SKIP

TITLE &
@IF FRANCAIS
 "                                          Vérifié Journalisé       Montant" &
@ELSE
 "                                             Verify    Posting         Total" &
@ENDIF
     AT ,3

SKIP

TITLE &
@IF FRANCAIS
 "Type                    Lot  Usager          le       le             total" &
@ELSE
 "Type                     Batch User            at       at            Amount" &
@ENDIF
     AT ,3

SKIP

;ALIGN(3,2,3) (,,6) (,,29) (,,34) (,,47) (,,58) (,,69)
ALIGN(3,2,3) (,,6) (,,27) (,,32) (,,43) (,,54) (,,65)

CLUSTER OCCURS WITH CRLOT ID BASE 10

FIELD LCRTYPECR  OF CRLOT HIDDEN LABEL " " &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

USE ushilite NOLIST

FIELD CBIDSC OF VCBI_DSC &
        SIZE 20 &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

USE ushilite NOLIST

FIELD LCRCLE     OF CRLOT &
        NUMERIC &
        SIGNIFICANCE 4 &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

USE ushilite NOLIST

FIELD LCRUSR     OF CRLOT &
        DISPLAY size 10 &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

USE ushilite NOLIST

FIELD LCRDATVAL  OF CRLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

USE ushilite NOLIST

FIELD LCRDATJOU  OF CRLOT  FORMAT YYYYMMDD PATTERN "####/##/##"&
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

USE ushilite NOLIST

FIELD LCRCUMMNT  OF CRLOT &
        DISPLAY &
        HILITE DISPLAY INVERSE BLINKING IF LCRNBRTRSERR OF CRLOT <> 0 &
                  ELSE INVERSE          IF LCRATRJOU OF CRLOT = "1" AND &
                                           LCRDATJOU OF CRLOT = 0

CLUSTER


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Si l'usager fait un <return> sur une ligne à l'écran il active la procédure
; suivante.
;
; Si le lot n'est pas autorisé, on met le flag d'autorisation à oui et on
; conserve l'usager de l'autorisation.
;
; Si le lot est déjà autorisé, on enlève le flag d'autorisation et l'usager
; qui l'avait autorisé.
;
;
PROCEDURE DESIGNER 10
BEGIN
  IF LCRDATVAL OF CRLOT <> 0
  THEN BEGIN
    IF LCRNBRTRSERR = 0
    THEN BEGIN
      IF LCRDATJOU OF CRLOT = 0
      THEN BEGIN
        IF LCRATRJOU OF CRLOT = "0"
        THEN BEGIN
          LET LCRATRJOU OF CRLOT = "1"
          LET LCRUSRATR OF CRLOT = LOGONID
        END
        ELSE BEGIN
          LET LCRATRJOU OF CRLOT = "0"
          LET LCRUSRATR OF CRLOT = " "
        END
        DISPLAY LCRTYPECR OF CRLOT
        DISPLAY CBIDSC    OF VCBI_DSC
        DISPLAY LCRCLE    OF CRLOT
        DISPLAY LCRUSR    OF CRLOT
        DISPLAY LCRDATVAL OF CRLOT
        DISPLAY LCRDATJOU OF CRLOT
        DISPLAY LCRCUMMNT OF CRLOT
      END
    END
  END
  ELSE  ERROR 727  ; Le lot doit être validé avant de l'autoriser
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; On ne peut pas détruire dans cette écran.
  ;
  FOR CRLOT
  BEGIN
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF CRLOT &
       AND NOT NEWRECORD     OF CRLOT &
       AND NOT DELETEDRECORD OF CRLOT &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD

;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;**************************** Autorisation des lots *****************************
;* Période ......: xxxxx                                                        *
;*                                             Vérifié Journalisé       Montant *
;* Type                      Lot  Usager          le       le             total *
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;* xx xxxxxxxxxxxxxxxxxxxxxx xxxx xxxxxxxxxxxx xxxxxxxx xxxxxxxx  xxxxxxxxxxxxxx*
;********************************************************************************
