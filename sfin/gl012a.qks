;/T/ Paramètres d'impression
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 31 mars 1992
;
;
;    Description..: Sous-écran pour modifier les paramètres d'impression
;                   servant au programme 'c'.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gl012a SLAVE &
              NOMODE NOACTION FIELDMARK &
              FROM 6,1 TO 18,80 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY, CHANGE &
              NOCOMMIT &
              RECEIVING GLEF_CEDULE

USE gl012a.hlp NOLIST

USE ussectmp NOLIST
    "GL012A"

;-------------------------------------------------------------------------------

FILE GLEF_CEDULE      MASTER


;-------------------------------------------------------------------------------
; Validation des codes bilingue
;

FILE VCBI_DSC         REFERENCE

;------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE D_ECECODARR CHAR SIZE 16 = "ECECODARR"
TEMP   T_ECECODARR CHAR SIZE 4


DEFINE D_DSCLNG CHAR SIZE 15 = &
@IF FRANCAIS
                            "Français" IF ECELNG OF GLEF_CEDULE = "1" &
                       ELSE "Anglais"
@ELSE
                            "French"   IF ECELNG OF GLEF_CEDULE = "1" &
                       ELSE "English"
@ENDIF

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW 1,1 TO 13,80

TITLE &
@IF FRANCAIS
      " Paramètres d'impression " &
@ELSE
      " Print Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(2,3,19)(,,21)

FIELD ECELONPAG OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECELARPAG OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECEESPCOL OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECELARDSC OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECESGNG   OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECESGND   OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECECODARR OF GLEF_CEDULE HIDDEN &
        LOOKUP ON VCBI_DSC &
          VIA XELNOM, CBICLE &
          USING D_ECECODARR, ECECODARR OF GLEF_CEDULE &
          MESSAGE 295 ; Code invalide
FIELD CBIDSC OF VCBI_DSC DISPLAY

SKIP

FIELD ECELNG    OF GLEF_CEDULE HIDDEN
FIELD D_DSCLNG DISPLAY

SKIP
FIELD ECESEPDEC OF GLEF_CEDULE HIDDEN
SKIP
FIELD ECESEPMIL OF GLEF_CEDULE HIDDEN

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Traitement pour les indicateurs de langues de communications.
;
;
PROCEDURE INPUT ECELNG
BEGIN
  USE uslnginp NOLIST
END

PROCEDURE OUTPUT ECELNG
BEGIN
  USE uslngout NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup sur les codes bilingues
;
PROCEDURE INPUT ECECODARR
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ECECODARR = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_ECECODARR, T_ECECODARR
    LET FIELDTEXT = TRUNC(T_ECECODARR)
  END
END

;-------------------------------------------------------------------------------
;
; Force l'éxécution de la procédure PROCESS
;
PROCEDURE INPUT ECESEPMIL
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ECESEPMIL OF GLEF_CEDULE
END

;
; Retourne à l'écran précédant, cette action doit faite que sur le dernier
; champ.
;
PROCEDURE PROCESS ECESEPMIL
BEGIN
  RETURN
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

BUILD
@IF FORMAT

*************************** Paramètres d'impression ****************************
*                                                                              *
* Longueur page.: xxx                                                          *
* Largeur page..: xxx                                                          *
* Espace colonne: x                                                            *
* Larg. desc....: xx                                                           *
* Signe gauche..: x                                                            *
* Signe droite..: xx                                                           *
* Code arrondis.: x xxxxxxxxxxxxxxxxxxxxxxxxx                                  *
* Langue........: x xxxxxxxxxxxxxxx                                            *
* Sép. décimale.: x                                                            *
* Sép. milliers.: x                                                            *
********************************************************************************
@ENDIF
