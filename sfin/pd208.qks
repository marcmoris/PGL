;/T/ 2.3.6 Enregistrer les modifications de diagramme.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-02-16
;
;    Description..: Cette écran permet d'enregistrer les modifications
;                   de diagramme.
;
;/M/ François Déry, 6 mai 1999
;       Modification de la table des diagrammes pour enlever les champs compute
;       qu'elle contenait et création d'une vue pour compenser.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd208 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             ACTIVITIES CHANGE,FIND,ENTRY &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD208"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd208.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Unité de travail                " ACTION DESIGNER D001
  MENUITEM LABEL "Priorité                        " ACTION DESIGNER D002
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D003
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Unité de travail             " ACTION DESIGNER D001
  MENUITEM LABEL "%%%Priorité                     " ACTION DESIGNER D002
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D003
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;

TEMPORARY T_RRMCODREM    CHARACTER SIZE 04
TEMPORARY T_PJTABR       CHARACTER SIZE 02
TEMPORARY T_PJTANN       CHARACTER SIZE 02
TEMPORARY T_COMNUMCOMINT CHARACTER SIZE 03
TEMPORARY T_COMNUMCOM    CHARACTER SIZE 07
TEMPORARY T_QTECMD       INTEGER SIGNED SIZE 08 RESET AT STARTUP ;
TEMPORARY T_NEWREC       CHARACTER SIZE 01 RESET AT STARTUP
TEMPORARY T_QTEANCIENNE  INTEGER SIGNED SIZE 08 RESET AT STARTUP ;


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
use usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Dimension maximum et minimum des pièces de granit
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME PRIMARY &
                 NOITEM &
                 NODELETE &
                 NOAPPEND


  ACCESS VIA     CIECLE,          &
                 DIANUM002        &
         USING   T_CIECLEMNU,                         &
                 DIANUM002        OF PDDIAGRAMME      &
         REQUEST DIANUM002        OF PDDIAGRAMME      &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 DIANUMDIA002


  ACCESS VIA     CIECLE           &
         USING   T_CIECLEMNU      &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 DIANUMDIA002



  ITEM CIECLE           OF PDDIAGRAMME INITIAL T_CIECLEMNU
  ITEM DIADATMAJ        OF PDDIAGRAMME FINAL   SYSDATE &
                                            IF NOT NEWRECORD OF PDDIAGRAMME    &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIAHREMAJ        OF PDDIAGRAMME FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF PDDIAGRAMME    &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIAUSRMAJ        OF PDDIAGRAMME FINAL   LOGONID &
                                            IF NOT NEWRECORD OF PDDIAGRAMME    &
                                               AND &
                                               ALTEREDRECORD OF PDDIAGRAMME

  ITEM DIASTU           OF PDDIAGRAMME FINAL "I" ; INACTIF

;
;
;
FILE VDIAQTE          REFERENCE
  ACCESS VIA   PJTABR, &
               DIANUMDIA001, &
               DIANUMDIA002, &
               CIECLE &
         USING PJTABR OF PDDIAGRAMME, &
               DIANUMDIA001 OF PDDIAGRAMME, &
               DIANUMDIA002 OF PDDIAGRAMME, &
               CIECLE OF PDDIAGRAMME

;-------------------------------------------------------------------------------
;
; Diagramme (nouveau)
;

FILE PDDIAGRAMME     DETAIL &
                     NOITEM &
                     ALIAS A_DIA_NEW OCCURS 8 TIMES

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 DIANUMDIA001     &
         USING   T_CIECLEMNU,                         &
                 PJTABR           OF PDDIAGRAMME,     &
                 DIANUMDIA002     OF PDDIAGRAMME


  ITEM CIECLE           OF A_DIA_NEW   INITIAL T_CIECLEMNU
  ITEM DIADATCRE        OF A_DIA_NEW   INITIAL SYSDATE
  ITEM DIAHRECRE        OF A_DIA_NEW   INITIAL SYSTIME / 10000
  ITEM DIAUSRCRE        OF A_DIA_NEW   INITIAL LOGONID

  ITEM DIADATMAJ        OF A_DIA_NEW   FINAL   SYSDATE &
                                            IF NOT NEWRECORD OF A_DIA_NEW      &
                                               AND &
                                               ALTEREDRECORD OF A_DIA_NEW

  ITEM DIAHREMAJ        OF A_DIA_NEW   FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF A_DIA_NEW      &
                                               AND &
                                               ALTEREDRECORD OF A_DIA_NEW

  ITEM DIAUSRMAJ        OF A_DIA_NEW   FINAL   LOGONID &
                                            IF NOT NEWRECORD OF A_DIA_NEW      &
                                               AND &
                                               ALTEREDRECORD OF A_DIA_NEW

  ITEM PJTABR           OF A_DIA_NEW   INITIAL PJTABR           OF PDDIAGRAMME
  ITEM PJTANN           OF A_DIA_NEW   INITIAL PJTANN           OF PDDIAGRAMME
  ITEM COMNUMCOMINT     OF A_DIA_NEW   INITIAL COMNUMCOMINT     OF PDDIAGRAMME
  ITEM COMNUMCOM        OF A_DIA_NEW   INITIAL COMNUMCOM        OF PDDIAGRAMME
  ITEM PJTNUMSEQ        OF A_DIA_NEW   INITIAL PJTNUMSEQ        OF PDDIAGRAMME
  ITEM PJTNUMPRO        OF A_DIA_NEW   INITIAL PJTNUMPRO        OF PDDIAGRAMME

  ITEM DIANUMDIA001     OF A_DIA_NEW   INITIAL DIANUMDIA002     OF PDDIAGRAMME

  ITEM DIANUM001        OF A_DIA_NEW   INITIAL PJTABR           OF PDDIAGRAMME + &
                                               DIANUMDIA002     OF PDDIAGRAMME

  ITEM DIANUM002        OF A_DIA_NEW   FINAL   PJTABR           OF PDDIAGRAMME + &
                                               DIANUMDIA002     OF A_DIA_NEW

  ITEM DIASTU           OF A_DIA_NEW   INITIAL "A"



;-------------------------------------------------------------------------------
;
; Priorité diagramme
;

FILE PDPRIORITE_DIAG SECONDARY &
                     NOITEM &
                     NEED ALL OCCURS WITH A_DIA_NEW

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 DIANUMDIA002     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF A_DIA_NEW,        &
                 DIANUMDIA002     OF A_DIA_NEW


  ITEM CIECLE           OF PDPRIORITE_DIAG INITIAL T_CIECLEMNU
  ITEM PRICODPRI        OF PDPRIORITE_DIAG INITIAL "99"

  ITEM PJTABR           OF PDPRIORITE_DIAG FINAL   PJTABR           OF A_DIA_NEW

  ITEM PJTANN           OF PDPRIORITE_DIAG FINAL   PJTANN           OF A_DIA_NEW

  ITEM DIANUMDIA002     OF PDPRIORITE_DIAG FINAL   DIANUMDIA002     OF A_DIA_NEW

  ITEM PDGSTU           OF PDPRIORITE_DIAG INITIAL "A"


  ITEM PDGQTEPLACPEGRA  OF PDPRIORITE_DIAG FINAL   DIAQTECMD        OF A_DIA_NEW

  ITEM PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG FINAL   DIAQTECMD        OF A_DIA_NEW

  ITEM DIANUM002        OF PDPRIORITE_DIAG FINAL   PJTABR           OF A_DIA_NEW + &
                                                   DIANUMDIA002     OF A_DIA_NEW

  ITEM COMNUMCOMINT     OF PDPRIORITE_DIAG FINAL   COMNUMCOMINT     OF A_DIA_NEW

  ITEM COMNUMCOM        OF PDPRIORITE_DIAG FINAL   PJTABR           OF A_DIA_NEW + &
                                                   PJTANN           OF A_DIA_NEW + &
                                                   COMNUMCOMINT     OF A_DIA_NEW


  ITEM PJTNUMSEQ        OF PDPRIORITE_DIAG FINAL   PJTNUMSEQ        OF A_DIA_NEW

  ITEM PJTNUMPRO        OF PDPRIORITE_DIAG FINAL   PJTABR           OF A_DIA_NEW + &
                                                   PJTANN           OF A_DIA_NEW + &
                                                   PJTNUMSEQ        OF A_DIA_NEW


;-------------------------------------------------------------------------------
;
; Priorité diagramme (ANCIEN)
;
FILE PDPRIORITE_DIAG  DESIGNER ALIAS A_PRD_OLD


;-------------------------------------------------------------------------------
;
; Produit bon commande (ANCIEN)
;

FILE PDPROD_BON_CMD DESIGNER ALIAS A_PBC_OLD


;------------------------------------------------------------------------------
;
; Diagramme (Vérification)
;

FILE PDDIAGRAMME REFERENCE &
                 ALIAS A_DIA_REF &
                 OCCURS WITH A_DIA_NEW

  ACCESS VIA     CIECLE,                  &
                 PJTABR,                  &
                 DIANUMDIA002             &
         USING   T_CIECLEMNU,                         &
                 PJTABR           OF A_DIA_NEW,       &
                 DIANUMDIA002     OF A_DIA_NEW


;-------------------------------------------------------------------------------
;
; Détail de la commande
;

FILE PDDETAIL_C_I          REFERENCE OCCURS WITH A_DIA_NEW

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 DCINUMLIG        &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF A_DIA_NEW,        &
                 PJTANN           OF A_DIA_NEW,        &
                 COMNUMCOMINT     OF A_DIA_NEW,        &
                 DCINUMLIG        OF A_DIA_NEW


  ITEM TGRCODGRA        OF A_DIA_NEW   FINAL   TGRCODGRA        OF PDDETAIL_C_I

  ITEM TYFCODFIN        OF A_DIA_NEW   FINAL   TYFCODFIN        OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE REFERENCE


  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDDIAGRAMME,      &
                 PJTANN           OF PDDIAGRAMME,      &
                 COMNUMCOMINT     OF PDDIAGRAMME


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

DEFINE D_QTE_DBT INTEGER SIZE 2 = DIAQTEDBTGRA     OF VDIAQTE + &
                                  DIAQTEDBTUSC     OF VDIAQTE + &
                                  DIAQTEDBTSCT     OF VDIAQTE

DEFINE D_QTE_FIN INTEGER SIZE 2 = DIAQTEFINGRA     OF VDIAQTE + &
                                  DIAQTEFINUSC     OF VDIAQTE + &
                                  DIAQTEFINSCT     OF VDIAQTE


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Modification diagramme" &
@ELSE
      " %%%Modification diagramme" &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD DIANUM002        OF PDDIAGRAMME      &
        HIDDEN ID 05                       &
        REQUIRED                           &
        LABEL "Num diagramme.:"            &
        LOOKUP ON PDDIAGRAMME              &
                  VIA     CIECLE,          &
                          DIANUM002        &
         USING   T_CIECLEMNU,                         &
                 DIANUM002        OF PDDIAGRAMME      &
          MESSAGE 2998 ; Ce numéro de diagramme n'existe pas


ALIGN (,3,19)


FIELD DIALON           OF PDDIAGRAMME      &
        DISPLAY


FIELD DIAHAU           OF PDDIAGRAMME      &
        DISPLAY


FIELD DIAEPA           OF PDDIAGRAMME      &
        DISPLAY


FIELD DIAQTECMD        OF PDDIAGRAMME      &
        DISPLAY


SKIP TO LINE 5


ALIGN (,29,45)


FIELD COMNUMCOM        OF PDDIAGRAMME      &
        DISPLAY


FIELD D_QTE_DBT                            &
        DISPLAY                            &
        LABEL "Qte dbt.......:"            &
        PICTURE "^^^^"


FIELD DIAQTEPLADBTGRA  OF VDIAQTE          &
        LABEL "Qte pl. dbt Co:"            &
        DISPLAY                            &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEPLADBTUSC  OF VDIAQTE          &
        DISPLAY                            &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEPLADBTSCT  OF VDIAQTE          &
        DISPLAY                            &
        PICTURE "^^^^^ " TRAILING "-"


SKIP TO LINE 5


ALIGN (,55,71)


FIELD DCINUMLIG        OF PDDIAGRAMME      &
        DISPLAY


FIELD D_QTE_FIN                            &
        DISPLAY                            &
        LABEL "Qte fin.......:"            &
        PICTURE "^^^^"


FIELD DIAQTEPLAFINGRA  OF VDIAQTE          &
        LABEL "Qte pl.fini Co:"            &
        DISPLAY                            &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEPLAFINUSC  OF VDIAQTE          &
        DISPLAY                            &
        PICTURE "^^^^^ " TRAILING "-"


FIELD DIAQTEPLAFINSCT  OF VDIAQTE          &
        DISPLAY                            &
        PICTURE "^^^^^ " TRAILING "-"



DRAW 11,2  TO 11,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      " Diagrammes " &
@ELSE
      " %%%Diagrammes " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 13


TITLE &
@IF FRANCAIS
      "Diagra.  Lig  Gra  D  Fi  D  Long  Haut  Épai  Qte cmd " &
@ELSE
      "%%%Diagra.  Lig  Gra  D  Fi  D  Long  Haut  Épai  Qte cmd " &
@ENDIF
      AT ,14


SKIP TO LINE 14


ALIGN (13,13,14) (16,,16) (23,,23) (,,28) (,,33) (,,36) (,,40) (43,,43) (49,,49) (55,,55) (63,,63)


CLUSTER OCCURS WITH A_DIA_NEW ID BASE 50


FIELD PJTABR           OF A_DIA_NEW        &
        HIDDEN                             &
        LABEL " "                          &
        DEFAULT PJTABR OF PDDIAGRAMME


FIELD DIANUMDIA002     OF A_DIA_NEW        &
        HIDDEN                             &
        REQUIRED                           &
        LOOKUP NOTON A_DIA_REF             &
          VIA     CIECLE,                  &
                  PJTABR,                  &
                  DIANUMDIA002             &
          USING   T_CIECLEMNU,                         &
                  PJTABR           OF A_DIA_NEW,       &
                  DIANUMDIA002     OF A_DIA_NEW        &
          MESSAGE 2997 ; Ce numéro de diagramme existe déjà


FIELD DCINUMLIG        OF A_DIA_NEW        &
        HIDDEN                             &
        LOOKUP ON PDDETAIL_C_I             &
          VIA     CIECLE,                  &
                  PJTABR,                  &
                  PJTANN,                  &
                  COMNUMCOMINT,            &
                  DCINUMLIG                &
          USING   T_CIECLEMNU,                         &
                  PJTABR           OF A_DIA_NEW,       &
                  PJTANN           OF A_DIA_NEW,       &
                  COMNUMCOMINT     OF A_DIA_NEW,       &
                  DCINUMLIG        OF A_DIA_NEW        &
          MESSAGE 3054 ; Ce numéro de ligne n'existe pas


FIELD TGRCODGRA        OF PDDETAIL_C_I     &
        HIDDEN                             &
        DISPLAY


FIELD DCITYPGRA        OF PDDETAIL_C_I     &
        HIDDEN                             &
        DISPLAY


FIELD TYFCODFIN        OF PDDETAIL_C_I     &
        HIDDEN                             &
        DISPLAY


FIELD DCITYPFIN        OF PDDETAIL_C_I     &
        HIDDEN                             &
        DISPLAY


FIELD DIALON           OF A_DIA_NEW        &
        HIDDEN                             &
        REQUIRED


FIELD DIAHAU           OF A_DIA_NEW        &
        HIDDEN                             &
        REQUIRED


FIELD DIAEPA           OF A_DIA_NEW        &
        HIDDEN                             &
        REQUIRED


FIELD DIAQTECMD        OF A_DIA_NEW        &
        HIDDEN                             &
        REQUIRED


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Valide le type de produit pour la ligne de détail
;

PROCEDURE EDIT DCINUMLIG OF A_DIA_NEW
BEGIN
  IF "DI" <> TPDTYPPRD OF PDDETAIL_C_I
  THEN BEGIN
    ERROR 2996 ; Cette ligne de détail ne correspond pas à un détail pour un diagramme
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions minimum et maximum pour un diagramme
;

PROCEDURE EDIT DIALON OF A_DIA_NEW
BEGIN
  IF (FIELDVALUE        > T_DIM_MAX_LAR_DI OR &
      FIELDVALUE        < T_DIM_MIN_LAR_DI)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions minimum et maximum pour un diagramme
;

PROCEDURE EDIT DIAHAU OF A_DIA_NEW
BEGIN
  IF (FIELDVALUE        > T_DIM_MAX_HAU_DI OR &
      FIELDVALUE        < T_DIM_MIN_HAU_DI)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions minimum et maximum pour un diagramme
;

PROCEDURE EDIT DIAEPA OF A_DIA_NEW
BEGIN
  IF (FIELDVALUE        > T_DIM_MAX_EPA_DI OR &
      FIELDVALUE        < T_DIM_MIN_EPA_DI)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
  ;
  ; L'épaisseur de la pièce de granit doit être égale à l épaisseur
  ; de la ligne de commande interne
  ;
  IF FIELDVALUE <> DCIEPA OF PDDETAIL_C_I
  THEN BEGIN
    ERROR 3093 ; Cette dimension est hors limite
  END
END


;-------------------------------------------------------------------------------
;
; Calcul la différence de quantité
;

PROCEDURE EDIT DIAQTECMD OF A_DIA_NEW
BEGIN
  LET T_QTECMD = FIELDVALUE - OLDVALUE(DIAQTECMD OF A_DIA_NEW)
END


;-------------------------------------------------------------------------------
;
; Assigne la quantité planifier Granicor Au total des prièces à fabriquer
;

;PROCEDURE PROCESS DIAQTECMD OF A_DIA_NEW
;BEGIN
;  LET DIAQTEPLADBTGRA  OF PDDIAGRAMME      = DIAQTECMD        OF PDDIAGRAMME
;  LET DIAQTEPLAFINGRA  OF PDDIAGRAMME      = DIAQTECMD        OF PDDIAGRAMME
;END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDIAGRAMME &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDIAGRAMME &
     AND NOT NEWRECORD     OF PDDIAGRAMME &
     AND NOT DELETEDRECORD OF PDDIAGRAMME &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR A_DIA_NEW
  BEGIN
    ;
    ; Calcul du volume et de la surface du diagramme
    ;
    LET TZ_LARGEUR                        = DIALON          OF A_DIA_NEW
    LET TZ_HAUTEUR                        = DIAHAU          OF A_DIA_NEW
    LET TZ_EPAISSEUR                      = DIAEPA          OF A_DIA_NEW
    DO  INTERNAL CALVOL
    LET DIAVOLMC3        OF A_DIA_NEW     = TZ_VOLUME
    LET DIASURMC2        OF A_DIA_NEW     = TZ_MC2

  END

  ;
  ; Mettre inactif les priorités du diagramme modifier
  ;
  WHILE RETRIEVING A_PRD_OLD &
    VIA     CIECLE,          &
            PJTABR,          &
            DIANUMDIA002     &
    USING   T_CIECLEMNU,                      &
            PJTABR           OF PDDIAGRAMME,  &
            DIANUMDIA002     OF PDDIAGRAMME
  BEGIN
    LET PDGSTU           OF A_PRD_OLD     = "I"
    PUT A_PRD_OLD
  END

  ;
  ; Mettre Inactif les prod_semi_fini du diagramme modifier
  ;
  WHILE RETRIEVING A_PBC_OLD &
    VIA     CIECLE,          &
            PJTABR,          &
            DIANUMDIA002     &
    USING   T_CIECLEMNU,                      &
            PJTABR           OF PDDIAGRAMME,  &
            DIANUMDIA002     OF PDDIAGRAMME
  BEGIN
    LET PBCSTU           OF A_PBC_OLD        = "I"
    PUT A_PBC_OLD
  END

END


;-------------------------------------------------------------------------------
;
; Unité de travail
;

PROCEDURE DESIGNER D001
BEGIN
  IF NEWRECORD OF PDDIAGRAMME &
     AND &
     NOT DELETEDRECORD OF PDDIAGRAMME
  THEN BEGIN
    LET T_NEWREC = "1"
  END
  RUN SCREEN  pd211a &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME, &
                      T_NEWREC, &
                      T_QTECMD, &
                      T_QTEANCIENNE &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Priorité
;

PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN  pd211b &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE SAME
END


;-------------------------------------------------------------------------------
;
; Vérification
;

PROCEDURE DESIGNER D003
BEGIN
  RUN SCREEN  pd211c &
              PASSING T_CIECLEMNU, &
                      T_CIENOM, &
                      PDDIAGRAMME &
              MODE SAME
END


BUILD


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
**************************** Modifiaction diagramme*****************************
*                                                                              *
* Num diagramme.: xxxxxxx   Commande int..: xxxxxxxxx Numéro ligne..: xxx      *
* Longueur......: xxxx      Qte dbt.......: xxxx      Qte fin.......: xxxx     *
* Hauteur.......: xxxx      Qte pl. dbt Gr: xxxx      Qte pl. fin Gr: xxxx     *
* Épaisseur.....: xxxx      Qte pl. dbt US: xxxx      Qte pl. fin US: xxxx     *
* Qte commandée.: xxxx      Qte pl. dbt SC: xxxx      Qte pl. fin SC: xxxx     *
*                                                                              *
********************************** Diagrammes **********************************
*                                                                              *
*            Diagra.  Lig  Gra  D  Fi  D  Long  Haut  Épai  Qte cmd            *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*            xxxxxxx  xxx  xxx  x  xx  x  xxxx  xxxx  xxxx    xxxx             *
*                                                                              *
********************************************************************************
@ENDIF
