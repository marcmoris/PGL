;/T/ Raison de rejet (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-13
;
;    Description..: Cette table contient les informations de base sur les
;                   raisons de rejet.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd159 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD159"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd159.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Raison de rejet
;

FILE PDRAISON_REJET PRIMARY NOITEM
  ACCESS VIA     CIECLE,   &
                 RRECODREJ &
         USING   T_CIECLEMNU,                &
                 RRECODREJ OF PDRAISON_REJET &
         REQUEST RRECODREJ OF PDRAISON_REJET &
         ORDERBY CIECLE,   &
                 RRECODREJ

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 RRECODREJ

         ITEM CIECLE OF PDRAISON_REJET INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_RRECODREJ CHARACTER SIZE 16 = "RRECODREJ"
TEMPORARY T_RRECODREJ CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Raison de rejet " &
@ELSE
      " %%%Raison de rejet " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD RRECODREJ OF PDRAISON_REJET &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE                       &
        LOOKUP NOTON PDRAISON_REJET             &
          VIA   CIECLE,                         &
                RRECODREJ                       &
          USING T_CIECLEMNU,                    &
                RRECODREJ OF PDRAISON_REJET     &
          MESSAGE 2909 ; Ce code de scie existe déjà


SKIP 1


FIELD RREDSC001 OF PDRAISON_REJET          &
        HIDDEN ID 10                       &
        REQUIRED


FIELD RREDSC002 OF PDRAISON_REJET          &
        ID SAME                            &
        NOLABEL


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs code unité de travail.
;

PROCEDURE INPUT RRECODREJ
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_RRECODREJ = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd104 MODE F &
                     PASSING T_RRECODREJ, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_RRECODREJ )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRAISON_REJET &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRAISON_REJET &
     AND NOT NEWRECORD     OF PDRAISON_REJET &
     AND NOT DELETEDRECORD OF PDRAISON_REJET &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Raison de rejet ********************************
*                                                                              *
* Code rejet....: xx                                                           *
*                                                                              *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
