;/T/ Cédule de paiement - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 7 septembre 1993
;
;    Description..: Cet écran vérifie si la cédule reçue en paramètre est
;                   référée dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp003bx NOMODE &
               ACTION LABEL " " &
               FROM 23,70 TO 23,80 &
               RECEIVING CPCAP_CEDULE , &
                         T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Cédule de paiement.
;
FILE CPCAP_CEDULE     MASTER

;
; Historique des transactions des comptes à payer.
;
FILE CPCAP_HISTO      REFERENCE



;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ;
  GET CPCAP_HISTO VIA FOUCIECLE , &
                      FOUCLE    , &
                      CAPCLE    , &
                      CPCCLELIG   &
                  USING FOUCIECLE OF CPCAP_CEDULE , &
                        FOUCLE    OF CPCAP_CEDULE , &
                        CAPCLE    OF CPCAP_CEDULE , &
                        CPCCLELIG OF CPCAP_CEDULE   &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 561 ; Destruction annulée, cette information est référée.
  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
