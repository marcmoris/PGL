;/T/ Menu Achats et Réceptions
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 24 février 1994
;
;    Description..: Menu principal du module Achats et Réceptions.
;
;/M/ Pascal Tremblay, 98-01-21
;
;    Ajustement pour le changement de siècle
;
;-------------------------------------------------------------------------------
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN arm01 MENU &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES CHANGE &
             RECEIVING T_FLGHLDOK

USE ussectmp NOLIST
    "ARM01"

USE arm01.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le holding est présent.

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE       DESIGNER &
                      OPEN READ


;
; Pour valider la période comptable.
;
FILE MCPERIODE_CIE     DESIGNER &
                      OPEN READ

;
; Pour afficher les dates de la période courante d'achat.
;
FILE MCPERIODE_CTB     DESIGNER &
                      OPEN READ


;
; Pour la sécurité sur les compagnies.
;
FILE VSEC_USR          DESIGNER &
                      OPEN READ

;
; Pour voir s'il y a une charte de fournisseurs
;
FILE MCHOLDING         DESIGNER &
                      OPEN READ

;-------------------------------------------------------------------------------
;
; Variables saisies ou affichées dans le menu.
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Période du menu
TEMPORARY T_PECDATDEB DATE              RESET AT STARTUP ; Date de début de période
TEMPORARY T_PECDATFIN DATE              RESET AT STARTUP ; Date de fin de période

;
TEMPORARY T_PECCLECOU CHARACTER SIZE 4  RESET AT STARTUP ; Période courante

TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les périodes
TEMPORARY T_MOD       CHARACTER SIZE 2 ; Code de module, pour le Popup sur
                                       ; les périodes

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies

;
; Variables passées en paramètres aux sous-écrans
;
TEMPORARY T_FOUCIECLE    CHARACTER SIZE 4  &
                           RESET AT STARTUP  ; Cie du fournisseur
TEMPORARY T_EMPCIECLE    CHARACTER SIZE 4  &
                           RESET AT STARTUP  ; Cie de l'employé


TEMPORARY T_FOUCLEFND CHARACTER SIZE 6    ; Fournisseur à chercher
TEMPORARY T_FOUCIEFND CHARACTER SIZE 4    ; Compagnie du fournisseur
TEMPORARY T_REQCLEFND CHARACTER SIZE 9    ; Réquisition à chercher
TEMPORARY T_RQDCLEFND INTEGER SIGNED SIZE 4
TEMPORARY T_CMDCLEFND CHARACTER SIZE 9    ; Commande à chercher
TEMPORARY T_CMDAJTFND CHARACTER SIZE 3    ; Séquence d'ajustement
TEMPORARY T_RECCLEFND CHARACTER SIZE 12   ; Réception à chercher
TEMPORARY T_PECCLEFND CHARACTER SIZE 4
TEMPORARY T_LARCLEFND CHARACTER SIZE 4
TEMPORARY T_FACCLEFND CHARACTER SIZE 12   ; FActure de recherche.
TEMPORARY T_ARMTYPFND CHARACTER SIZE 1    ; Type pour la gestion des mémos
TEMPORARY T_NUMREFFND CHARACTER SIZE 12   ; Pièce en référence
TEMPORARY T_MOUCLEFND CHARACTER SIZE 9    ; Mouvement à chercher.

;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "ARL@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "ARP@" ; Code pour les procédures


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST ; Hilite pour tous les écrans

DRAW 1,20 TO 3,61
DRAW 2,1  TO 2,20
DRAW 2,61 TO 2,80

DRAW 2,1  TO 7,1
DRAW 7,1  TO 7,80
DRAW 2,80 TO 7,80

DRAW 5,1  TO 5,80

DRAW 7,40 TO 20,40
DRAW 20,1 TO 20,80

TITLE &
@IF FRANCAIS
      "A C H A T S  &  R É C E P T I O N S" &
@ELSE
      "%%% A C H A T S  &  R E C E P T I O N S " &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

ALIGN(4,4,20) (,,26)
FIELD T_CIECLEMNU HIDDEN ID 05 &
        FIXED            &
        UPSHIFT REQUIRED &
        PATTERN "((^|#)>)|!0" &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Company......:"  &
@ENDIF
        LOOKUP ON MCCOMPAGNIE &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 1305, &  ; Cette compagnie n'existe pas

          ON VSEC_USR &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 1306 ; Vous n'avez pas accès à cette compagnie.

FIELD T_CIENOM &
        FIXED

SKIP 1

ALIGN(4,4,13)(,29,41)(,58,68)
FIELD T_PECCLEMNU ID SAME &
        FIXED            &
@IF FRANCAIS
        LABEL "Période:" &
@ELSE
        LABEL "Period.:" &
@ENDIF
        PICTURE "^^-^^" &
        LOOKUP ON MCPERIODE_CIE &
          VIA CIECLE, &
              PECCLE &
          USING T_CIECLEMNU, &
                T_PECCLEMNU  &
          MESSAGE 1307 ; Cette période n'existe pas


FIELD T_PECDATDEB FIXED  FORMAT YYYYMMDD &
@IF FRANCAIS
        LABEL "Date début:"
@ELSE
        LABEL "Start Date:"
@ENDIF

FIELD T_PECDATFIN FIXED  FORMAT YYYYMMDD &
@IF FRANCAIS
        LABEL "Date fin:"
@ELSE
        LABEL "End Date:"
@ENDIF


SKIP TO 9

ALIGN (2,2)
SUBSCREEN ar002 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM,    &
                        T_PECCLEMNU, &
                        T_FOUCIECLE  &
@IF FRANCAIS
                LABEL "Gestion des produits            "
@ELSE
                LABEL "%%%Gestion des produits          "
@ENDIF

SKIP 1

SUBSCREEN ar003 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_PECCLECOU, &
                        T_PECCLEFND, &
                        T_LARCLEFND  &
@IF FRANCAIS
                LABEL "Gestion des lots                "
@ELSE
                LABEL "Batch management                "
@ENDIF

SKIP 1


SUBSCREEN ar005 HIDDEN &
                PASSING T_CIECLEMNU    , &
                        T_CIENOM       , &
                        T_PECCLEMNU    , &
                        T_FOUCIECLE      &
@IF FRANCAIS
                LABEL "Réservation des # de commande   "
@ELSE
                LABEL "%%%Réservation des # de commande  "
@ENDIF


SKIP 1

SUBSCREEN ar006 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_FOUCIECLE, &
                        T_REQCLEFND, &
                        T_RQDCLEFND  &
@IF FRANCAIS
                LABEL "Réquisition d'achat             "
@ELSE
                LABEL "%%%Réquisition d'achat          "
@ENDIF

SKIP 1

SUBSCREEN ar013 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_FOUCIECLE  &
@IF FRANCAIS
                LABEL "Réquisition d'achat (Sommaire)  "
@ELSE
                LABEL "%%%uisition d'achat (Sommaire)  "
@ENDIF

SKIP 1

SUBSCREEN ar007 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM,    &
                        T_PECCLEMNU, &
                        T_FOUCIECLE, &
                        T_CMDCLEFND, &
                        T_CMDAJTFND  &
@IF FRANCAIS
                LABEL "Commande d'achat                "
@ELSE
                LABEL "%%%Commande d'achat             "
@ENDIF


SKIP TO LINE 9
ALIGN (42,42)


SUBSCREEN ar009 HIDDEN  &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_FOUCIECLE, &
                        T_RECCLEFND  &
@IF FRANCAIS
                LABEL "Réception de commande           "
@ELSE
                LABEL "%%%Réception de commande        "
@ENDIF

SKIP 1

SUBSCREEN ar014 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_FOUCIECLE, &
                        T_MOUCLEFND  &
@IF FRANCAIS
                LABEL "Mouvements de marchandises      "
@ELSE
                LABEL "%%%Mouvements de marchandises   "
@ENDIF

SKIP 1


SUBSCREEN ar015 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM,    &
                        T_PECCLEMNU, &
                        T_FOUCIECLE  &
@IF FRANCAIS
                LABEL "Décomptes physiques             "
@ELSE
                LABEL "%%%Décomptes physiques          "
@ENDIF

SKIP 1

SUBSCREEN ar012 HIDDEN  &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_ARMTYPFND, &
                        T_NUMREFFND, &
                        T_FOUCIECLE, &
                        T_FOUCLEFND  &
@IF FRANCAIS
                LABEL "Gestion des mémos - achat       "
@ELSE
                LABEL "%%%tion des mémos - achat       "
@ENDIF

SKIP 1 


SUBSCREEN ar016 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM,    &
                        T_PECCLEMNU, &
                        T_FOUCIECLE  &
@IF FRANCAIS
                LABEL "Catalogue des produits divers    "
@ELSE
                LABEL "%%%Gestion des produits          "
@ENDIF




SKIP TO 21

ALIGN(31,31)
SUBSCREEN arm05 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L, &
                        T_CIECLEMNU, &
                        T_FOUCIECLE, &
                        T_PECCLEMNU, &
                        T_CIENOM   , &
                        T_PECDATDEB, &
                        T_PECDATFIN  &
@IF FRANCAIS
                LABEL " Listes et rapports "
@ELSE
                LABEL " Lists and Reports  "
@ENDIF

SUBSCREEN arm05 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P, &
                        T_CIECLEMNU, &
                        T_FOUCIECLE, &
                        T_PECCLEMNU, &
                        T_CIENOM   , &
                        T_PECDATDEB, &
                        T_PECDATFIN  &
@IF FRANCAIS
                LABEL "    Procédures      "
@ELSE
                LABEL "    Procedures      "
@ENDIF

SUBSCREEN arm03 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_PECDATDEB, &
                        T_PECDATFIN &
@IF FRANCAIS
                LABEL "Informations de base"
@ELSE
                LABEL " Basic Information  "
@ENDIF

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le holding n'est pas défini on retourne l'usager au menu
  ; principal.
  ;
  IF T_FLGHLDOK = "0"
  THEN RETURN

  USE ussececr NOLIST


  ;
  ; On accède au Holding pour voir s'il y a une charte de fournisseurs
  ; par compagnie et/ou une charte d'employés par compagnie. Si non, on
  ; remplace les clés compagnie du fournisseur et/ou compagnie de l'employé
  ; par les caractères de remplacement contenus dans le Holding. (Voir
  ; la procédure PROCESS du champs T_CIECLEMNU)
  ;
  GET MCHOLDING VIA   CIENMC &
                USING "1"


  IF "sylvboiv" <> LOGONID or "0" = GETSYSTEMVAL("OPTMNU")
  THEN BEGIN
    ACCEPT T_CIECLEMNU
    ACCEPT T_PECCLEMNU
  END
  ELSE BEGIN
    IF "1" = GETSYSTEMVAL("OPTMNU")
    THEN BEGIN
      LET T_CIECLEMNU = "2000"
      GET MCPERIODE_CIE VIA CIECLE, &
                          PCCSTUAR &
                      USING T_CIECLEMNU, &
                            "C" &
                      OPTIONAL
      IF NOT ACCESSOK
      THEN ERROR 1308 ; IL N'Y A PAS DE PÉRIODE COURANTE DÉFINIE.
      ELSE BEGIN
        LET T_PECCLEMNU = PECCLE OF MCPERIODE_CIE
        LET T_PECCLECOU = PECCLE OF MCPERIODE_CIE
      END
 
      IF HLDCHTFOU OF MCHOLDING = "1"
      THEN LET T_FOUCIECLE = T_CIECLEMNU
      ELSE LET T_FOUCIECLE = HLDCHRSUB OF MCHOLDING

      IF HLDCHTEMP OF MCHOLDING = "1"
      THEN LET T_EMPCIECLE = T_CIECLEMNU
      ELSE LET T_EMPCIECLE = HLDCHRSUB OF MCHOLDING

      DISPLAY T_CIECLEMNU
      DISPLAY T_PECCLEMNU

      GET MCCOMPAGNIE VIA CIECLE &
                    USING T_CIECLEMNU OPTIONAL
      IF NOT ACCESSOK
      THEN ERROR 1305 ; Cette compagnie n'existe pas

      LET T_CIENOM = CIENOM OF MCCOMPAGNIE
      DISPLAY T_CIENOM

      GET MCPERIODE_CTB VIA PECCLE    &
                    USING T_PECCLEMNU OPTIONAL
      LET T_PECDATDEB = PECDATDEB OF MCPERIODE_CTB
      LET T_PECDATFIN = PECDATFIN OF MCPERIODE_CTB
      DISPLAY T_PECDATDEB
      DISPLAY T_PECDATFIN

      IF SETSYSTEMVAL("OPTMNU", "0")
      THEN NULL
    END
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
  ;
  ; A cause de l'option requis, si l'usager fait un retour et qu'il y a
  ; une valeur on remet la même.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_CIECLEMNU))
  THEN LET FIELDTEXT = T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;
; On conserve le nom de la compagnie du menu.
;
;
PROCEDURE PROCESS T_CIECLEMNU
BEGIN
  LET T_CIENOM = CIENOM OF MCCOMPAGNIE
  DISPLAY T_CIENOM
  ;
  ; On va chercher la période courante du module ACHAT
  ;
  GET MCPERIODE_CIE VIA CIECLE, &
                        PCCSTUAR &
                    USING T_CIECLEMNU, &
                          "C" &
                    OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 1308 ; Il n'y a pas de période courante définie.
  ELSE LET T_PECCLECOU = PECCLE OF MCPERIODE_CIE

  IF HLDCHTFOU OF MCHOLDING = "1"
  THEN LET T_FOUCIECLE = T_CIECLEMNU
  ELSE LET T_FOUCIECLE = HLDCHRSUB OF MCHOLDING

  IF HLDCHTEMP OF MCHOLDING = "1"
  THEN LET T_EMPCIECLE = T_CIECLEMNU
  ELSE LET T_EMPCIECLE = HLDCHRSUB OF MCHOLDING

END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT T_PECCLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MOD = "AR"
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN  mc109 PASSING T_CIECLEMNU, T_PECCLE, T_MOD  MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
  ;
  ; Si l'usager ne saisit pas de période alors on prend la période courante
  ; par défaut.
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) AND &
     0 = SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLECOU
  ;
  ; On force la validation de la période.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLEMNU
END

;-------------------------------------------------------------------------------
;
;
; Validation du statut de la période.
;
;
PROCEDURE EDIT T_PECCLEMNU
BEGIN
  ;
  IF PCCSTUAR OF MCPERIODE_CIE = "F"
  THEN ERROR 1309 ; Cette période est fermée.
  ;
  IF PCCSTUAR OF MCPERIODE_CIE <> "C"
  THEN WARNING 1310 ; La période saisie n'est pas la courante.
END

;-------------------------------------------------------------------------------
;
;
; Conserve  et affiche les dates de la période saisie.
;
;
PROCEDURE PROCESS T_PECCLEMNU
BEGIN
  GET MCPERIODE_CTB VIA PECCLE &
                    USING T_PECCLEMNU
  LET T_PECDATDEB = PECDATDEB OF MCPERIODE_CTB
  LET T_PECDATFIN = PECDATFIN OF MCPERIODE_CTB
  DISPLAY T_PECDATDEB
  DISPLAY T_PECDATFIN
END

;-------------------------------------------------------------------------------
;
; On demande la compagnie et la période, il faut une procédure designer sur
; ces champs car l'option FIXED empêche par défaut de modifier les champs.
; L'option FIXED est due à un bug de POWERHOUSE sur Unix.
;
;
PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_CIECLEMNU
  ACCEPT T_PECCLEMNU
END


BUILD
@IF FORMAT
                   ******************************************
********************  A C H A T S  &  R E C E P T I O N S   ********************
*                  ******************************************                  *
*  Compagnie.....: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
********************************************************************************
*  Période: xxxxx           Date début: xxxxxxxx         Date fin: xxxxxxxx    *
********************************************************************************
                                       * Réquisition d'achat
                                       *
 Gestion des produits                  * Commande d'achat
                                       * Ajustement de commande
 Gestion des lots                      *
                                       * Réception de commande
 Autorisation des lots                 *
                                       * Préparation de facture
 Réservation des # de commande         *
                                       * Gestion des mémos - achat
                                       *
********************************************************************************
                               Listes et rapports
                                  Procédures
                              Informations de base

@ENDIF
