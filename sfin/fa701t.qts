;/T/ Transfert des factures au niveau des comptes à recevoir.
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur...: Steeve Duguay
;    Date Création.: 6 avril 1995
;
;/M/ Marie-Josée Hamel, 96.02.08, valider le statut des factures
;/M/ Marie-Josée Hamel, 96.02.09, ajouter une ligne d'escompte si mnt d'escompte sur la ligne
;/M/ Marie-Josée Hamel, 96.02.12, ajouter type NC, AJ et CR
;------------------------------------------------------------------------------

USE ussetqts NOLIST   ; permet de faire les SET PROCESS NOLIMIT....

RUN fa701t

GLOBAL TEMPORARY G_ERREUR2   CHARACTER SIZE 5
GLOBAL TEMPORARY G_PECCLE    CHARACTER SIZE 4 PARM
GLOBAL TEMPORARY G_LOT       CHARACTER SIZE 4 PARM
GLOBAL TEMPORARY G_ERREUR    CHARACTER SIZE 1      ; Pour stopper le traitement

;-------------------------------------------
;
; Sélection des factures pour le transafert.
;
REQUEST SELECTION_FACTURE

ACCESS FACPT_A_REC  &

  LINK REFCIECLE OF FACPT_A_REC, &
       REFCLENUM OF FACPT_A_REC, &
       CIECLE    OF FACPT_A_REC  &
    TO CLICIECLE,                &
       CLICLE,                   &
       CIECLE    OF VCLC_CLI     &

   ;
   ; Pour le type de produits
   ;
  LINK  CIECLE    OF FACPT_A_REC,  &
        FARCLE    OF FACPT_A_REC   &
    TO  CIECLE,                    &
        FARCLE    OF FAFAR_DETAIL  &

  LINK  CIECLE    OF FACPT_A_REC,  &
        FARCLEREF OF FACPT_A_REC   &
    TO  CIECLE,                    &
        CARCLE                 OF CRCPT_A_REC OPTIONAL &

   ;
   ; Pour valider la période comptable au achat
   ;
  LINK  CIECLE OF FAFAR_DETAIL,  &
        G_PECCLE                 &
    TO  CIECLE,                  &
        PECCLE  OF MCPERIODE_CIE &

  LINK CIECLE OF FAFAR_DETAIL     , &
       ETACLE OF FAFAR_DETAIL       &
    TO CIECLE                     , &
       ETACLE OF FAELEMENT_TARIF OPTIONAL &

  LINK CIECLE OF FAFAR_DETAIL,   &
       G_PECCLE,                 &
       G_LOT                     &
    TO CIECLE,                   &
       PECCLE,                   &
       LCRCLE OF CRLOT OPTIONAL


CHOOSE  CIECLE    PARM

SELECT FACPT_A_REC IF PECCLE    OF FACPT_A_REC = G_PECCLE AND &
                      LCRCLE    OF FACPT_A_REC = G_LOT    AND &
                      FARDATJOU OF FACPT_A_REC = 0        AND &
                      FARSTU    OF FACPT_A_REC <> "X"

TEMPORARY T_MESERR CHARACTER SIZE 80

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF FACPT_A_REC &
         IF PECCLE OF FACPT_A_REC[1:1] < "8" &
         ELSE "0" + PECCLE OF FACPT_A_REC

SORT ON CIECLE       OF FACPT_A_REC  &
     ON FARCLE       OF FACPT_A_REC  &
     ON D_PECCLE_SIE  &
     ON PRJCLE       OF FAFAR_DETAIL &
     ON PRACLE       OF FAFAR_DETAIL &
     ON CPTCLE       OF FAFAR_DETAIL &
     ON UNACLE       OF FAFAR_DETAIL &
     ON FFDTAXCODTPS OF FAFAR_DETAIL &
     ON FFDTAXCODTVQ OF FAFAR_DETAIL

ITEM G_ERREUR = "1" IF PCCSTUAR OF MCPERIODE_CIE = "F"  ELSE &
                "2" IF NOT RECORD CRLOT EXIST           ELSE &
                "3" IF LCRDATJOU OF CRLOT <> 0          ELSE &
                "5" IF FARSTU OF FACPT_A_REC <> "A"     ELSE &
                "6" IF FARIMP OF FACPT_A_REC <> "1" AND &
                       FARTYPECR OF FACPT_A_REC <> "AJ" AND &
                       0 <> (  FARMNT OF FACPT_A_REC &
                             - FARMNTREC OF FACPT_A_REC) ELSE &
                "8" IF 0 <> SIZE( TRUNCATE( FARCLEREF OF FACPT_A_REC ) ) &
                      AND CARDATJOU OF CRCPT_A_REC = 0  ELSE &
                "0"

ITEM T_MESERR = "La période comptable est fermée..."    IF G_ERREUR = "1" ELSE &
                "Le lot est inexistant.."               IF G_ERREUR = "2" ELSE &
                "Le lot est déjà journalisé..."         IF G_ERREUR = "3" ELSE &
                FARCLE OF FACPT_A_REC +                                        &
                " Cette facture n'est pas approuvée..." IF G_ERREUR = "5" ELSE &
                FARCLE OF FACPT_A_REC +                                        &
                " Cette facture n'est pas imprimée..."  IF G_ERREUR = "6" ELSE &
                FARCLEREF OF FACPT_A_REC +                                     &
                " Cette facture n'est pas journalisée" +                       &
                " dans les CAR..."                      IF G_ERREUR = "8" ELSE &
                " "

; affectation d'erreurs
ITEM G_ERREUR2 = "ARRET" IF T_MESERR <> ""

SUBFILE WFA701X IF G_ERREUR2 = "ARRET" &
INCLUDE T_MESERR, CIECLE OF FACPT_A_REC


SUBFILE WFA701Y AT FARCLE INCLUDE &
        FACPT_A_REC

REQUEST FIN-ERREUR

ACCESS *WFA701X

SORT ON T_MESERR

;
; Pour indiquer le message d'erreur.
;
SUBFILE WFA701ER        KEEP AT T_MESERR &
        INCLUDE CIECLE, &
                G_PECCLE  ALIAS PECCLE, &
                G_LOT     ALIAS LCRCLE, &
                T_MESERR

;-------------------------------------------
;
; Sélection des factures pour le transafert.
;
REQUEST SELECTION_FACTURE TERMINATE IF G_ERREUR2 = "ARRET"

ACCESS *WFA701Y &

  LINK REFCIECLE OF WFA701Y, &
       REFCLENUM OF WFA701Y, &
       CIECLE    OF WFA701Y  &
    TO CLICIECLE,                &
       CLICLE,                   &
       CIECLE    OF VCLC_CLI     &

   ;
   ; Pour le type de produits
   ;
  LINK  CIECLE    OF WFA701Y,  &
        FARCLE    OF WFA701Y   &
    TO  CIECLE,                    &
        FARCLE    OF FAFAR_DETAIL  &

  ; piece référée pour avoir son type
  LINK CIECLE     OF WFA701Y, &
       FARCLEREF  OF WFA701Y  &
    TO CIECLE, &
       FARCLE               OF FACPT_A_REC ALIAS A_FAR_REFEREE OPTIONAL &

  ; piece référée pour avoir son compte de CAR
  LINK CIECLE     OF WFA701Y, &
       FARCLEREF  OF WFA701Y  &
    TO CIECLE, &
       CARCLE               OF CRCPT_A_REC ALIAS A_CAR_REFEREE OPTIONAL &

  LINK CIECLE OF FAFAR_DETAIL     , &
       ETACLE OF FAFAR_DETAIL       &
    TO CIECLE                     , &
       ETACLE OF FAELEMENT_TARIF OPTIONAL &

  LINK FFDTAXTYPTPS OF FAFAR_DETAIL, &
       FFDTAXCODTPS OF FAFAR_DETAIL  &
    TO TAXCLETYP,                    &
       TAXCLECOD OF MCTAXE ALIAS A_TAX_TPS OPTIONAL &

  LINK FFDTAXTYPTVQ OF FAFAR_DETAIL, &
       FFDTAXCODTVQ OF FAFAR_DETAIL  &
    TO TAXCLETYP,                    &
       TAXCLECOD OF MCTAXE ALIAS A_TAX_TVQ OPTIONAL

 ; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF WFA701Y &
         IF PECCLE OF WFA701Y[1:1] < "8" &
         ELSE "0" + PECCLE OF WFA701Y

SORT ON CIECLE       OF WFA701Y  &
     ON FARCLE       OF WFA701Y  &
     ON D_PECCLE_SIE             &
     ON PRJCLE       OF FAFAR_DETAIL &
     ON PRACLE       OF FAFAR_DETAIL &
     ON CPTCLE       OF FAFAR_DETAIL &
     ON UNACLE       OF FAFAR_DETAIL &
     ON FFDTAXCODTPS OF FAFAR_DETAIL &
     ON FFDTAXCODTVQ OF FAFAR_DETAIL


TEMPORARY T_TPS         FLOAT SIZE 8
TEMPORARY T_TVQ         FLOAT SIZE 8
TEMPORARY T_FFDMNTTOT   FLOAT SIZE 8
TEMPORARY T_FFDMNTTOT2  FLOAT SIZE 8
TEMPORARY T_FFDMNTTPS   FLOAT SIZE 8
TEMPORARY T_FFDMNTTVQ   FLOAT SIZE 8

TEMPORARY T_FCT         FLOAT SIZE 8
TEMPORARY T_FFDMNTESC   FLOAT SIZE 8
TEMPORARY T_ESCMNTTPS   FLOAT SIZE 8
TEMPORARY T_ESCMNTTVQ   FLOAT SIZE 8


ITEM T_TPS       = FFDMNTTPS OF FAFAR_DETAIL &
                IF TAXMOD    OF A_TAX_TPS = "S" &
              ELSE 0

ITEM T_TVQ       = FFDMNTTVQ OF FAFAR_DETAIL &
                IF TAXMOD    OF A_TAX_TVQ = "S" &
              ELSE 0

ITEM T_FFDMNTTOT2= (  FFDMNTTOT OF FAFAR_DETAIL   &
                    - T_TPS - T_TVQ) * -1         &
                IF FFDTYP OF FAFAR_DETAIL = "3"   &
              ELSE (  FFDMNTTOT OF FAFAR_DETAIL &
                    - T_TPS - T_TVQ)

ITEM T_FFDMNTTOT = T_FFDMNTTOT  + T_FFDMNTTOT2  + FFDMNTESC OF FAFAR_DETAIL &
                   RESET AT FFDTAXCODTVQ TO 0

ITEM T_FCT =  ( T_FFDMNTTOT2 + FFDMNTESC OF FAFAR_DETAIL ) &
            / T_FFDMNTTOT2 &
          IF FFDMNTESC OF FAFAR_DETAIL <> 0 &
        ELSE 1

ITEM T_FFDMNTTPS = T_FFDMNTTPS  + ROUND( FFDMNTTPS OF FAFAR_DETAIL * T_FCT ) &
                   RESET AT FFDTAXCODTVQ TO 0

ITEM T_FFDMNTTVQ = T_FFDMNTTVQ  + ROUND( FFDMNTTVQ OF FAFAR_DETAIL * T_FCT ) &
                   RESET AT FFDTAXCODTVQ TO 0

ITEM T_FFDMNTESC = FFDMNTESC OF FAFAR_DETAIL * -1

ITEM T_ESCMNTTPS = FFDMNTTPS OF FAFAR_DETAIL &
                 - ROUND( FFDMNTTPS OF FAFAR_DETAIL * T_FCT ) &
                   RESET AT FFDTAXCODTVQ TO 0

ITEM T_ESCMNTTVQ = FFDMNTTVQ OF FAFAR_DETAIL &
                 - ROUND( FFDMNTTVQ OF FAFAR_DETAIL * T_FCT ) &
                   RESET AT FFDTAXCODTVQ TO 0


OUTPUT CRCPT_A_REC ADD AT FARCLE NOITEMS
  ITEM CIECLE      OF CRCPT_A_REC INITIAL CIECLE      OF WFA701Y
  ITEM CARCLE      OF CRCPT_A_REC INITIAL FARCLE      OF WFA701Y
  ITEM REFCIECLE   OF CRCPT_A_REC INITIAL REFCIECLE   OF WFA701Y
  ITEM REFCLETYP   OF CRCPT_A_REC INITIAL REFCLETYP   OF WFA701Y
  ITEM REFCLENUM   OF CRCPT_A_REC INITIAL REFCLENUM   OF WFA701Y
  ITEM RADCLE      OF CRCPT_A_REC INITIAL RADCLE      OF WFA701Y
  ITEM CARDSC1     OF CRCPT_A_REC INITIAL FARDSC1     OF WFA701Y
  ITEM CARDSC2     OF CRCPT_A_REC INITIAL FARDSC2     OF WFA701Y
  ITEM PECCLE      OF CRCPT_A_REC INITIAL PECCLE      OF WFA701Y
  ITEM LCRCLE      OF CRCPT_A_REC INITIAL G_LOT
  ITEM CARTYPECR   OF CRCPT_A_REC INITIAL FARTYPECR   OF WFA701Y
  ITEM CARCMDCLI   OF CRCPT_A_REC INITIAL FARCMDCLI   OF WFA701Y
  ITEM CARDAT      OF CRCPT_A_REC INITIAL FARDAT      OF WFA701Y
  ITEM CARDATJOU   OF CRCPT_A_REC INITIAL 0
  ITEM CARHREJOU   OF CRCPT_A_REC INITIAL 0
  ITEM CARTERNET   OF CRCPT_A_REC INITIAL FARTERNET   OF WFA701Y
  ITEM CARDATNET   OF CRCPT_A_REC INITIAL FARDATNET   OF WFA701Y
  ITEM CARTERESC1  OF CRCPT_A_REC INITIAL FARTERESC1  OF WFA701Y
  ITEM CARDATESC1  OF CRCPT_A_REC INITIAL FARDATESC1  OF WFA701Y
  ITEM CARTERESC2  OF CRCPT_A_REC INITIAL FARTERESC2  OF WFA701Y
  ITEM CARDATESC2  OF CRCPT_A_REC INITIAL FARDATESC2  OF WFA701Y
  ITEM CARTERFRS   OF CRCPT_A_REC INITIAL FARTERFRS   OF WFA701Y
  ITEM CARDATFRS   OF CRCPT_A_REC INITIAL FARDATFRS   OF WFA701Y

  ITEM CARMNT      OF CRCPT_A_REC INITIAL FARMNT      OF WFA701Y - &
                                          FARMNTREC   OF WFA701Y

  ITEM CARMNTTPS   OF CRCPT_A_REC INITIAL FARMNTTPS   OF WFA701Y
  ITEM CARMNTTVP   OF CRCPT_A_REC INITIAL FARMNTTVQ   OF WFA701Y

  ITEM CARMNTNET   OF CRCPT_A_REC INITIAL FARMNTNET   OF WFA701Y - &
                                          FARMNTREC   OF WFA701Y

  ITEM CARCUMMNT   OF CRCPT_A_REC INITIAL FARMNT      OF WFA701Y - &
                                          FARMNTREC   OF WFA701Y

  ITEM CARCPTCAR   OF CRCPT_A_REC INITIAL CLCCPTCAR OF VCLC_CLI &
                                       IF   FARTYPECR OF WFA701Y = "FA" &
                                         OR FARTYPECR OF WFA701Y = "CR" &
                                     ELSE CARCPTCAR OF A_CAR_REFEREE
  ITEM CARDATCRE   OF CRCPT_A_REC INITIAL FARDATCRE OF WFA701Y
  ITEM CARHRECRE   OF CRCPT_A_REC INITIAL FARHRECRE OF WFA701Y
  ITEM CARUSRCRE   OF CRCPT_A_REC INITIAL FARUSRCRE OF WFA701Y
  ITEM CARDATMOD   OF CRCPT_A_REC INITIAL FARDATMOD OF WFA701Y
  ITEM CARUSRMOD   OF CRCPT_A_REC INITIAL FARUSRMOD OF WFA701Y
  ITEM CARHREMOD   OF CRCPT_A_REC INITIAL FARHREMOD OF WFA701Y


;
; Ajout d'un enregistrement dans l'historique du document référé.
; Sert seulement pour les ajustements et les notes de crédit.
;
OUTPUT CRCAR_HISTO ADD AT FARCLE NOITEMS IF FARTYPECR OF WFA701Y = "AJ" &
                                         OR FARTYPECR OF WFA701Y = "NC"

  ITEM CIECLE      OF CRCAR_HISTO = CIECLE    OF WFA701Y
  ITEM CARCLE      OF CRCAR_HISTO = FARCLEREF OF WFA701Y
  ITEM CCDCLELIG   OF CRCAR_HISTO = 1
  ITEM CRHTIMSTP   OF CRCAR_HISTO = SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  ITEM CRHCLE1     OF CRCAR_HISTO = CIECLE    OF CRCPT_A_REC
  ITEM CRHCLE2     OF CRCAR_HISTO = CARCLE    OF CRCPT_A_REC
  ITEM CRHTYPECR   OF CRCAR_HISTO = CARTYPECR OF CRCPT_A_REC
  ITEM CRHTYPTRA   OF CRCAR_HISTO = CARTYPECR OF CRCPT_A_REC
  ITEM CRHMNTCAR   OF CRCAR_HISTO = CARMNT    OF CRCPT_A_REC
  ITEM CRHDAT      OF CRCAR_HISTO = CARDAT    OF CRCPT_A_REC
  ITEM PECCLE      OF CRCAR_HISTO = PECCLE    OF CRCPT_A_REC
  ITEM LCRCLE      OF CRCAR_HISTO = LCRCLE    OF CRCPT_A_REC


;
; Ajout d'un enregistrement dans l'historique de la facture référé par
; la note de crédit qui fait l'objet d'un ajustement.
; Sert seulement pour les ajustements sur notes de crédit.
;
OUTPUT CRCAR_HISTO ALIAS A_CRH_FACTURE ADD AT FARCLE NOITEMS &
                                      IF   FARTYPECR OF WFA701Y = "AJ" &
                                       AND FARTYPECR OF A_FAR_REFEREE = "NC"
  ITEM CIECLE    OF A_CRH_FACTURE = CIECLE    OF CRCAR_HISTO
  ITEM CARCLE    OF A_CRH_FACTURE = FARCLEREF OF A_FAR_REFEREE
  ITEM CRHTIMSTP OF A_CRH_FACTURE = SYSDATE * 1000000 + ROUND(SYSTIME / 100)
  ITEM CRHCLE1   OF A_CRH_FACTURE = CRHCLE1   OF CRCAR_HISTO
  ITEM CRHCLE2   OF A_CRH_FACTURE = CRHCLE2   OF CRCAR_HISTO
  ITEM CRHTYPECR OF A_CRH_FACTURE = CRHTYPECR OF CRCAR_HISTO
  ITEM CRHTYPTRA OF A_CRH_FACTURE = "NC"
  ITEM CCDCLELIG OF A_CRH_FACTURE = CCDCLELIG OF CRCAR_HISTO
  ITEM CRHMNTCAR OF A_CRH_FACTURE = CRHMNTCAR OF CRCAR_HISTO
  ITEM CRHDAT    OF A_CRH_FACTURE = CRHDAT    OF CRCAR_HISTO
  ITEM PECCLE    OF A_CRH_FACTURE = PECCLE    OF CRCAR_HISTO
  ITEM LCRCLE    OF A_CRH_FACTURE = LCRCLE    OF CRCAR_HISTO


OUTPUT CRCAR_IMPUTATION ADD AT FFDTAXCODTVQ NOITEMS
  ITEM CIECLE       OF CRCAR_IMPUTATION INITIAL CIECLE       OF FAFAR_DETAIL
  ITEM CARCLE       OF CRCAR_IMPUTATION INITIAL FARCLE       OF FAFAR_DETAIL
  ITEM CRITIMSTP    OF CRCAR_IMPUTATION INITIAL FFDTIMSTP    OF FAFAR_DETAIL
  ITEM PRJCLE       OF CRCAR_IMPUTATION INITIAL PRJCLE       OF FAFAR_DETAIL
  ITEM PRACLE       OF CRCAR_IMPUTATION INITIAL PRACLE       OF FAFAR_DETAIL
  ITEM IMMCLE       OF CRCAR_IMPUTATION INITIAL IMMCLE       OF FAFAR_DETAIL
  ITEM CPTCLE       OF CRCAR_IMPUTATION INITIAL CPTCLE       OF FAFAR_DETAIL
  ITEM UNACLE       OF CRCAR_IMPUTATION INITIAL UNACLE       OF FAFAR_DETAIL
  ITEM TRPCLE       OF CRCAR_IMPUTATION INITIAL TRPCLE       OF FAFAR_DETAIL
  ITEM ETACLE       OF CRCAR_IMPUTATION INITIAL ETACLE       OF FAFAR_DETAIL
  ITEM CRICODDC     OF CRCAR_IMPUTATION INITIAL "C"
  ITEM CRITAXTYPTPS OF CRCAR_IMPUTATION INITIAL FFDTAXTYPTPS OF FAFAR_DETAIL
  ITEM CRITAXCODTPS OF CRCAR_IMPUTATION INITIAL FFDTAXCODTPS OF FAFAR_DETAIL
  ITEM CRITAXTYPTVP OF CRCAR_IMPUTATION INITIAL FFDTAXTYPTVQ OF FAFAR_DETAIL
  ITEM CRITAXCODTVP OF CRCAR_IMPUTATION INITIAL FFDTAXCODTVQ OF FAFAR_DETAIL
  ITEM CRIMNT       OF CRCAR_IMPUTATION    =    T_FFDMNTTOT
  ITEM CRIMNTTPS    OF CRCAR_IMPUTATION    =    T_FFDMNTTPS
  ITEM CRIMNTTVP    OF CRCAR_IMPUTATION    =    T_FFDMNTTVQ


OUTPUT FACPT_A_REC UPDATE AT FARCLE &
       VIA CIECLE, FARCLE USING CIECLE OF WFA701Y, FARCLE OF WFA701Y
  ITEM FARSTU       OF FACPT_A_REC FINAL "T"
  ITEM FARDATJOU    OF FACPT_A_REC FINAL SYSDATE
  ITEM FARHREJOU    OF FACPT_A_REC FINAL SYSTIME


;
; Pour le rapport de controle
;
SUBFILE WFA701IM KEEP AT FFDTAXCODTVQ &
        INCLUDE &
        CIECLE       OF WFA701Y,            &
        FARCLE       OF WFA701Y,            &
        PRJCLE       OF FAFAR_DETAIL,           &
        PRACLE       OF FAFAR_DETAIL,           &
        IMMCLE       OF FAFAR_DETAIL,           &
        CPTCLE       OF FAFAR_DETAIL,           &
        UNACLE       OF FAFAR_DETAIL,           &
        TRPCLE       OF FAFAR_DETAIL,           &
        ETACLE       OF FAFAR_DETAIL,           &
        FFDTAXCODTPS OF FAFAR_DETAIL,           &
        FFDTAXCODTVQ OF FAFAR_DETAIL,           &
        T_FFDMNTTOT  ALIAS FFDMNTTOT,           &
        T_FFDMNTTPS  ALIAS FFDMNTTPS,           &
        T_FFDMNTTVQ  ALIAS FFDMNTTVQ,           &
        CLICLE       OF VCLC_CLI,               &
        CLINOM       OF VCLC_CLI,               &
        G_PECCLE     ALIAS PECCLE,              &
        G_LOT        ALIAS LCRCLE

;
; Pour créer les imputations d'escompte
;
SUBFILE WFA701A IF T_FFDMNTESC <> 0 INCLUDE &
   CIECLE       OF FAFAR_DETAIL   , &
   FARCLE       OF FAFAR_DETAIL   , &
   FFDTIMSTP    OF FAFAR_DETAIL   , &
   PRJCLE       OF FAFAR_DETAIL   , &
   PRACLE       OF FAFAR_DETAIL   , &
   IMMCLE       OF FAFAR_DETAIL   , &
   ETACPTESC    OF FAELEMENT_TARIF, &
   UNACLE       OF FAFAR_DETAIL   , &
   TRPCLE       OF FAFAR_DETAIL   , &
   ETACLE       OF FAFAR_DETAIL   , &
   FFDTAXTYPTPS OF FAFAR_DETAIL   , &
   FFDTAXCODTPS OF FAFAR_DETAIL   , &
   FFDTAXTYPTVQ OF FAFAR_DETAIL   , &
   FFDTAXCODTVQ OF FAFAR_DETAIL   , &
   T_FFDMNTESC                    , &
   T_ESCMNTTPS                    , &
   T_ESCMNTTVQ                    , &
   CLICLE       OF VCLC_CLI       , &
   CLINOM       OF VCLC_CLI

;
; On retrie pour ne faire qu'une ligne au niveau du compte d'escompte
;
REQUEST ESCOMPTE

ACCESS *WFA701A

SORT ON CIECLE       OF WFA701A &
     ON FARCLE       OF WFA701A &
     ON PRJCLE       OF WFA701A &
     ON PRACLE       OF WFA701A &
     ON ETACPTESC    OF WFA701A &
     ON UNACLE       OF WFA701A &
     ON FFDTAXCODTPS OF WFA701A &
     ON FFDTAXCODTVQ OF WFA701A


OUTPUT CRCAR_IMPUTATION ALIAS A_CRI_ESCOMPTE &
                        ADD AT FFDTAXCODTVQ  NOITEMS
  ITEM CIECLE       OF A_CRI_ESCOMPTE INITIAL CIECLE       OF WFA701A
  ITEM CARCLE       OF A_CRI_ESCOMPTE INITIAL FARCLE       OF WFA701A
  ITEM CRITIMSTP    OF A_CRI_ESCOMPTE INITIAL FFDTIMSTP    OF WFA701A
  ITEM PRJCLE       OF A_CRI_ESCOMPTE INITIAL PRJCLE       OF WFA701A
  ITEM PRACLE       OF A_CRI_ESCOMPTE INITIAL PRACLE       OF WFA701A
  ITEM IMMCLE       OF A_CRI_ESCOMPTE INITIAL IMMCLE       OF WFA701A
  ITEM CPTCLE       OF A_CRI_ESCOMPTE INITIAL ETACPTESC    OF WFA701A
  ITEM UNACLE       OF A_CRI_ESCOMPTE INITIAL UNACLE       OF WFA701A
  ITEM TRPCLE       OF A_CRI_ESCOMPTE INITIAL TRPCLE       OF WFA701A
  ITEM ETACLE       OF A_CRI_ESCOMPTE INITIAL ETACLE       OF WFA701A
  ITEM CRICODDC     OF A_CRI_ESCOMPTE INITIAL "C"
  ITEM CRITAXTYPTPS OF A_CRI_ESCOMPTE INITIAL FFDTAXTYPTPS OF WFA701A
  ITEM CRITAXCODTPS OF A_CRI_ESCOMPTE INITIAL FFDTAXCODTPS OF WFA701A
  ITEM CRITAXTYPTVP OF A_CRI_ESCOMPTE INITIAL FFDTAXTYPTVQ OF WFA701A
  ITEM CRITAXCODTVP OF A_CRI_ESCOMPTE INITIAL FFDTAXCODTVQ OF WFA701A
  ITEM CRIMNT       OF A_CRI_ESCOMPTE SUBTOTAL T_FFDMNTESC OF WFA701A
  ITEM CRIMNTTPS    OF A_CRI_ESCOMPTE SUBTOTAL T_ESCMNTTPS OF WFA701A
  ITEM CRIMNTTVP    OF A_CRI_ESCOMPTE SUBTOTAL T_ESCMNTTVQ OF WFA701A


OUTPUT *WFA701IM ADD ALIAS A_WIM_ESC &
                        AT FFDTAXCODTVQ  NOITEMS
  ITEM CIECLE       OF A_WIM_ESC FINAL CIECLE       OF WFA701A
  ITEM FARCLE       OF A_WIM_ESC FINAL FARCLE       OF WFA701A
  ITEM PRJCLE       OF A_WIM_ESC FINAL PRJCLE       OF WFA701A
  ITEM PRACLE       OF A_WIM_ESC FINAL PRACLE       OF WFA701A
  ITEM IMMCLE       OF A_WIM_ESC FINAL IMMCLE       OF WFA701A
  ITEM CPTCLE       OF A_WIM_ESC FINAL ETACPTESC    OF WFA701A
  ITEM UNACLE       OF A_WIM_ESC FINAL UNACLE       OF WFA701A
  ITEM TRPCLE       OF A_WIM_ESC FINAL TRPCLE       OF WFA701A
  ITEM ETACLE       OF A_WIM_ESC FINAL ETACLE       OF WFA701A
  ITEM FFDTAXCODTPS OF A_WIM_ESC FINAL FFDTAXCODTPS OF WFA701A
  ITEM FFDTAXCODTVQ OF A_WIM_ESC FINAL FFDTAXCODTVQ OF WFA701A
  ITEM FFDMNTTOT    OF A_WIM_ESC FINAL CRIMNT       OF A_CRI_ESCOMPTE
  ITEM FFDMNTTPS    OF A_WIM_ESC FINAL CRIMNTTPS    OF A_CRI_ESCOMPTE
  ITEM FFDMNTTVQ    OF A_WIM_ESC FINAL CRIMNTTVP    OF A_CRI_ESCOMPTE
  ITEM CLICLE       OF A_WIM_ESC FINAL CLICLE       OF WFA701A
  ITEM CLINOM       OF A_WIM_ESC FINAL CLINOM       OF WFA701A
  ITEM PECCLE       OF A_WIM_ESC FINAL G_PECCLE
  ITEM LCRCLE       OF A_WIM_ESC FINAL G_LOT


BUILD
