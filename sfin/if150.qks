;/T/ Catégorie de granit
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 16 septembre 1999
;
;    Description..: Cet \351cran permet de saisir les informations de base 
;                   concernant la catégorie de granit                                 
;
SCREEN if150 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLE, &
                       T_CIENOM

USE ussectmp  NOLIST
    "IF150"

USE if150.hlp NOLIST

USE usactecr  NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLE CHARACTER SIZE 4  ; Numéro de cie
TEMPORARY T_CIENOM CHARACTER SIZE 40 ; Nom de la compagnie

FILE IFCATEGORIE      IN DICT PRIMARY NOITEM &
                      OCCURS 16 TIMES

  ACCESS VIA     CIECLE,   &
                 CATCLE    &
         USING   T_CIECLE, &
                 CATCLE    &  
         REQUEST CATCLE    &
         ORDERBY CATCLE
  
  ACCESS VIA     CIECLE &
         USING   T_CIECLE &
         ORDERBY CATCLE

  ITEM CIECLE OF IFCATEGORIE INITIAL T_CIECLE

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLE
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_FLGDEL CHARACTER SIZE 1

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
      " Categories de granit " &
      CENTERED AT ,1

SKIP 1

USE ustitoff NOLIST


TITLE "Catégorie" AT ,5
TITLE "Desc. francaise" AT ,18
TITLE "Desc. anglaise" AT ,46
SKIP

ALIGN(6,7,8) (,,18) (,,46)
CLUSTER OCCURS WITH IFCATEGORIE
FIELD CATCLE OF IFCATEGORIE &
        HIDDEN &
        LABEL " " &
        NOCHANGE &
        REQUIRED &
        LOOKUP NOTON IFCATEGORIE &
          VIA   CIECLE, &
                CATCLE  &
          USING T_CIECLE, &
                CATCLE OF IFCATEGORIE &
          MESSAGE "*E*Code de categorie deja existant..."

FIELD CATDSCFRA OF IFCATEGORIE &
      REQUIRED

FIELD CATDSCANG OF IFCATEGORIE 

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;------------------------------------------------------------------------------
;
; Valeur par defaut pour la description anglaise
;
PROCEDURE INPUT CATDSCANG
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT))
  THEN LET FIELDTEXT = CATDSCFRA OF IFCATEGORIE
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;------------------------------------------------------------------------------
;
; Appel de l'écran de validation de la destruction d'un code de transaction
;
PROCEDURE DELETE
BEGIN
   ;
   ; Initialisation du flag de retour
   LET T_FLGDEL = "0"

   ;
   ; Appel de l'écran de validation
   RUN SCREEN if150x MODE SAME &
                   PASSING IFCATEGORIE, &
                           T_FLGDEL

   ; Si le code est utilisé on ne peut l'effacer
   IF T_FLGDEL = "1"
   THEN WARNING 3243 ; PD343 Impossible de supprimer cette catégorie,
                     ; elle est déjà utilisée 
   ELSE DELETE IFCATEGORIE
END

BUILD
@IF FORMAT
@ENDIF
