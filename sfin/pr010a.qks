
;/V/ Écran vérifier pour le passage à l'an 2000

SCREEN pr010a      NOMODE NOACTION FIELDMARK STARTUP &
                   FROM 2,1 TO 13,80 &
                   MESSAGE ON 24 &
                   AUTOUPDATE &
                   HELP POPUP FROM 4,9 TO 20,72 &
                   ACTIVITIES FIND, CHANGE &
                   RECEIVING T_CIECLEFND, &
                             T_PROCLEFND, &
                             T_FORCLEFND

TEMPORARY T_CIECLEFND CHARACTER SIZE 04 ; Compagnie de recherche
TEMPORARY T_PROCLEFND CHARACTER SIZE 08 ; Projet de recherche
TEMPORARY T_FORCLEFND CHARACTER SIZE 05 ; Format de présentation

;
; Ouverture d'une transaction QUERY indépendante de l'écran maitre.
; pour ne pas rompre la chaine de lecture.
;
USE ustrasse   NOLIST

FILE GPPROJET   PRIMARY &
                NOITEM  &
                NODELETE

  ACCESS VIA    CIECLE, &
                PROCLE  &
         USING  T_CIECLEFND, &
                PROCLE OF GPPROJET &
        REQUEST PROCLE OF GPPROJET

FILE GPFORMAT   REFERENCE

  ACCESS VIA    FORCLE &
         USING  PROFRM1 OF GPPROJET

TEMPORARY T_PROCLEPOP CHARACTER SIZE 8
TEMPORARY T_PROSTUPAT CHARACTER SIZE 8 ; Popup sur les projets.

;-------------------------------------------------------------------------------

USE ushilite NOLIST     ; Hilite pour tout les écrans

DRAW  1,1 TO 12,80

TITLE &
@IF FRANCAIS
      " Paramètres " &
@ELSE
      " Parameters " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1

ALIGN (3,3,19)

FIELD PROCLE OF GPPROJET &
        HIDDEN ID 10 &
        REQUIRED &
        NOCHANGE

SKIP 1

ALIGN (,,6)

FIELD PRODSC  OF GPPROJET &
        DISPLAY
FIELD PRODSC2 OF GPPROJET &
        DISPLAY
FIELD PRODSC3 OF GPPROJET &
        DISPLAY

SKIP 2

ALIGN (3,3,19) (,,25)

FIELD PROFRM1 OF GPPROJET &
        LOOKUP ON GPFORMAT &
        LABEL "Format........:"
FIELD FORDSC  OF GPFORMAT &
        DISPLAY


;-------------------------------------------------------------------------------
;
; Popup sur les projets.
;
PROCEDURE INPUT PROCLE
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PROSTUPAT = "A"
    LET T_PROCLEPOP = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pr101 MODE F PASSING T_CIECLEFND, &
                                    T_PROCLEPOP, &
                                    T_PROSTUPAT  &
                                    WINDOW WIDTH CONSTANT WHEN CALLING

    LET FIELDTEXT = TRUNCATE(T_PROCLEPOP)
  END
END


PROCEDURE POSTFIND
BEGIN
  DISPLAY PRODSC  OF GPPROJET
  DISPLAY PRODSC2 OF GPPROJET
  DISPLAY PRODSC3 OF GPPROJET
  ACCEPT PROFRM1 OF GPPROJET
  LET T_PROCLEFND = PROCLE  OF GPPROJET
  LET T_FORCLEFND = PROFRM1 OF GPPROJET
  PUSH UPDATE
END

PROCEDURE UPDATE
BEGIN
  PUT GPPROJET
  RETURN
END

BUILD
@IF FORMAT
********************************** Paramètres **********************************
*                                                                              *
* Projet........: xxxxxxxx                                                     *
*                                                                              *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
*                                                                              *
* Format........: xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     *
*                                                                              *
********************************************************************************
@ENDIF

