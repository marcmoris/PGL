;/T/ Consultation des trajets / produits
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 30 mars 1995
;
;    Paramètres...: CIECLE    ==> - Compagnie
;                   TRPCLE    ==> - Trajet / Produit
;                   TRPSEGPAT ==> - Segment
;                                 - critère de sélection sous la forme
;                                   de pattern.
;                   TRPTYPPAT ==> - (T)rajet ou (P)roduit.
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN fa102 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             NOCOMMIT &
             RECEIVING T_CIECLEMNU, &
                       T_TRPCLE   , &
                       T_TRPSEGPAT, &
                       T_TRPTYPPAT

USE ustrasse  NOLIST ; Transaction QUERY pour les sous-écrans

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_TRPCLE    CHARACTER SIZE 3
TEMPORARY T_TRPSEGPAT CHARACTER SIZE 3
TEMPORARY T_TRPTYPPAT CHARACTER SIZE 5

USE fa102.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE FATRAJET_PRODUIT PRIMARY &
                      OCCURS 11
  ACCESS VIA     CIECLE,      &
                 TRPCLE       &
         USING   T_CIECLEMNU, &
                 T_TRPCLE     &
         ORDERBY TRPCLE

  SELECT IF &
    MATCHPATTERN(TRPSEG OF FATRAJET_PRODUIT, TRUNCATE(T_TRPSEGPAT)) AND &
    MATCHPATTERN(TRPTYP OF FATRAJET_PRODUIT, TRUNCATE(T_TRPTYPPAT))


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN (3,3,6)(,,15)
CLUSTER OCCURS WITH FATRAJET_PRODUIT

FIELD TRPCLE    OF FATRAJET_PRODUIT HIDDEN LABEL ">>"
FIELD TRPDSCFRA OF FATRAJET_PRODUIT DISPLAY SIZE 20

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_TRPCLE = TRPCLE OF FATRAJET_PRODUIT
  RETURN
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_TRPCLE, "@")
  THEN LET T_TRPCLE = ""
END

BUILD ;
;
;                                       *****************************************
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       * >> xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
;                                       *****************************************
;
;
;
;
;
;
;
;
;
;
