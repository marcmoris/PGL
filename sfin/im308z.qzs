;/T/ Journal des aliénations
;
;/P/ Programmeur..: Alain Côté
;    Date création: 2 mars 1994
;
;/V3.02.00/ Alain Côté, 2 mars 1994, Immobilisation
;
;    Description..: Impression des écritures de journal avec le détail par
;                   immobilisation. On imprime que les écritures d'aliénation.
;
;    Paramètres...: No. cie    (%CUF_CIECLEMNU)
;                   Période    (%CUPECCLE)
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST


ACCESS IMIMM_SUIVI &

  LINK CIECLE OF IMIMM_SUIVI , &
       IMMCLE OF IMIMM_SUIVI   &
    TO CIECLE , &
       IMMCLE OF IMIMMOBILISATION &

  LINK CIECLE    OF IMIMM_SUIVI , &
       ECJCLENUM OF IMIMM_SUIVI , &
       ECJCLESEQ OF IMIMM_SUIVI   &
    TO ECJCIECLE , &
       ECJCLENUM , &
       ECJCLESEQ OF GLECRITURE_JG

CHOOSE CIECLE PARM, &
       PECCLE PARM

SELECT IMIMM_SUIVI IF IMITYP OF IMIMM_SUIVI = "AL"


;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Journal des aliénations"
@ELSE
       "%%%Journal des aliénations"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 =  " "

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IM308"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE OF IMIMM_SUIVI &
         IF PECCLE OF IMIMM_SUIVI[1:1] < "8" &
         ELSE "0" + PECCLE OF IMIMM_SUIVI

; Ajustement pour le changement de siècle
DEFINE D_ECJCLENUM_TRI CHARACTER SIZE 10 = "1" + ECJCLENUM OF IMIMM_SUIVI &
   IF ECJCLENUM OF IMIMM_SUIVI[1:1] < "8" &
   ELSE "0" + ECJCLENUM OF IMIMM_SUIVI

SORT ON CIECLE    OF IMIMM_SUIVI   &
     ON D_PECCLE_SIE   &
     ON LGLCLE    OF GLECRITURE_JG &
     ON D_ECJCLENUM_TRI      &
     ON ECJCLESEQ OF IMIMM_SUIVI


USE ustitstd NOLIST  ; Titre standard pour l'application
SKIP 2 &
  TAB 001 &
@IF FRANCAIS
"                                Numéro                                                      Montant" &
@ELSE
"%%%                             Numéro                                                      Montant" &
@ENDIF
SKIP &
  TAB 001 &
@IF FRANCAIS
"Période   Lot    Ecriture   immobilisation                 Description                    aliénation  " &
@ELSE
"%%%iode   Lot    Ecriture   immobilisation                 Description                    aliénation  " &
@ENDIF
 SKIP &
  TAB 001 &
"-------   ----   --------   --------------   ----------------------------------------    -------------" &
SKIP 2


REPORT &
  TAB 002 PECCLE    OF IMIMM_SUIVI      &
            PRINT AT LGLCLE             &
  TAB 011 LGLCLE    OF GLECRITURE_JG    &
            PRINT AT LGLCLE             &
  TAB 018 ECJCLENUM OF IMIMM_SUIVI      &
  TAB 030 IMMCLE    OF IMIMM_SUIVI      &
  TAB 046 IMMDSC    OF IMIMMOBILISATION &
  TAB 090 IMMMNTALI OF IMIMMOBILISATION


FOOTING AT LGLCLE &
  SKIP 2


BUILD im308z
@IF FORMAT

IM308                                         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                    Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                           Journal des aliénations
                                    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

                                Numéro                                                      Montant
Période   Lot    Ecriture   immobilisation                 Description                    aliénation
-------   ----   --------   --------------   ----------------------------------------    -------------

 99-99    xxxx   xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99

 99-99    xxxx   xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99

 99-99    xxxx   xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
                 xxxxxxxx    xxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    99,999,999.99
@ENDIF
