;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 16 décembre 1994
;
;    Description..: Détail du format de présentation comptable - Domaine
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gp156b ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPFORMATS  , &
                        GPFOR_DETAILS

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu

USE ustrasse NOLIST ; Transaction QUERY pour les sous-écrans

USE ussectmp NOLIST     ; Variable pour la securite
    "GP156B"            ; Nom de l'écran

USE gp156b.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


FILE GPFORMATS              MASTER

FILE GPFOR_DETAILS          MASTER


;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_DOMAINE          PRIMARY &
                            NOITEMS &
                            OCCURS 22

  ACCESS  VIA     FRMCLE, &
                  FRDCLE, &
                  FDOCLE, &
                  FDOSEQ  &
          USING   FRMCLE OF GPFOR_DETAILS, &
                  FRDCLE OF GPFOR_DETAILS, &
                  FRDCLE OF GPFOR_DETAILS, &
                  FDOSEQ OF GPFRD_DOMAINE  &
          REQUEST FDOSEQ  &
          ORDERBY FRDCLE, &
                  FDOCLE, &
                  FDOSEQ


  ACCESS  VIA     FRMCLE, &
                  FRDCLE  &
          USING   FRMCLE OF GPFOR_DETAILS, &
                  FRDCLE OF GPFOR_DETAILS  &
          ORDERBY FRDCLE, &
                  FDOCLE, &
                  FDOSEQ

  ITEM FRMCLE OF GPFRD_DOMAINE INITIAL FRMCLE OF GPFOR_DETAILS
  ITEM FRDCLE OF GPFRD_DOMAINE INITIAL FRDCLE OF GPFOR_DETAILS
  ITEM FDOREF OF GPFRD_DOMAINE INITIAL 0
  ITEM FDOCLE OF GPFRD_DOMAINE INITIAL FRDCLE OF GPFOR_DETAILS
  ITEM FRDTYP OF GPFRD_DOMAINE INITIAL FRDTYP OF GPFOR_DETAILS
  ITEM FDOTYP OF GPFRD_DOMAINE INITIAL "C"

;----------------------------------------------
; Fichier des détails de format de présentation
;----------------------------------------------
FILE GPFRD_DOMAINE          DESIGNER &
                            ALIAS GPFRD_DOMAINE_3

FILE GPFRD_DOMAINE          DESIGNER &
                            ALIAS GPFRD_DOMAINE_4

FILE GPFOR_DETAILS          DESIGNER &
                            ALIAS GPFOR_DETAILS_2

FILE GPFRD_SOMME            DESIGNER


;--------------------------------------------------------------------------
; Validation du type de format de présentation et lecture de sa description
;--------------------------------------------------------------------------
DEFINE    D_FRDTYP CHARACTER SIZE 16 = "FRDTYP"
TEMPORARY T_FRDTYP CHARACTER SIZE 4

FILE VCBI_DSC               REFERENCE &
                            ALIAS MCCODE_BUIL_FRDTYP

  ACCESS  VIA   XELNOM, &
                CBICLE  &
          USING D_FRDTYP, &
                FRDTYP OF GPFOR_DETAILS


;--------------------------
; Fichier compte debut
;--------------------------
TEMPORARY T_CPTDEB    CHARACTER SIZE 6
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes.
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes.
TEMPORARY T_ERRCPT    CHARACTER SIZE 1


;--------------------------
; Fichier compte fin
;--------------------------
TEMPORARY T_CPTFIN CHARACTER SIZE 6

;------------------------------------------------
; Fichier code de nature comptable pour le compte
;------------------------------------------------
FILE MCNATURE_CTB     DESIGNER &
                      OPEN READ




;-------------------------------------------------------------------------------
;
TEMPORARY T_NATURE     INTEGER SIZE 2
TEMPORARY T_NATURE2    INTEGER SIZE 2
TEMPORARY T_CPTREF     INTEGER SIZE 4
TEMPORARY T_FRMCLE   CHARACTER SIZE 4
TEMPORARY T_FRDCLE     INTEGER UNSIGNED SIZE 4
TEMPORARY T_FDOREF     INTEGER UNSIGNED SIZE 2
TEMPORARY T_FDOCLE     INTEGER UNSIGNED SIZE 2
TEMPORARY T_FDOSEQ     INTEGER UNSIGNED SIZE 2
TEMPORARY T_CPTAJO     INTEGER UNSIGNED SIZE 2

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Détail du format de présentation - Ligne calculée " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1

ALIGN(3,3,19)(,,25)

FIELD FRMCLE OF GPFORMATS                             &
      HIDDEN                                          &
      DISPLAY

FIELD FRMDSC OF GPFORMATS                             &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
"Ligne   Type  Description" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

SKIP

ALIGN (,,3) (,,12) (,,17)

FIELD FRDCLE OF GPFOR_DETAILS                         &
      DISPLAY

FIELD FRDTYP OF GPFOR_DETAILS                         &
      DISPLAY

FIELD FRDDSC OF GPFOR_DETAILS                         &
      DISPLAY

SKIP 1

TITLE &
@IF FRANCAIS
"Compte   Compte" &
@ELSE
      " %%% " &
@ENDIF
      AT ,13

TITLE &
@IF FRANCAIS
"Compte   Compte" &
@ELSE
      " %%% " &
@ENDIF
      AT ,53

SKIP

TITLE &
@IF FRANCAIS
"Séquence  Début    Fin     Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
"Séquence  Début    Fin     Opérateur" &
@ELSE
      " %%% " &
@ENDIF
      AT ,43

SKIP

ALIGN (4,3,4) (,,13) (,,22) (,,34)

CLUSTER OCCUR WITH GPFRD_DOMAINE VERTICAL AT 12,1 FOR 1,40

FIELD FDOSEQ OF GPFRD_DOMAINE                         &
      HIDDEN LABEL " "                                &
      REQUIRED NOCHANGE                               &
      LOOKUP NOTON GPFRD_DOMAINE                      &
             VIA   FRMCLE,                            &
                   FRDCLE,                            &
                   FDOCLE,                            &
                   FDOSEQ                             &
             USING FRMCLE OF GPFOR_DETAILS,           &
                   FRDCLE OF GPFOR_DETAILS,           &
                   FRDCLE OF GPFOR_DETAILS,           &
                   FDOSEQ OF GPFRD_DOMAINE            &
                   MESSAGE 875

FIELD FDOCPTDEB OF GPFRD_DOMAINE

FIELD FDOCPTFIN OF GPFRD_DOMAINE

FIELD FDOOPE    OF GPFRD_DOMAINE                      &
      REQUIRED NOCHANGE

CLUSTER

DRAW 09,01 TO 09,80
DRAW 09,40 TO 23,40
;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les comptes.
;
;
PROCEDURE INPUT FDOCPTDEB
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTDEB = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTDEB   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTDEB)
  END

  IF 0=SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = OLDVALUE(FDOCPTDEB OF GPFRD_DOMAINE)
END


PROCEDURE EDIT FDOCPTDEB
BEGIN
  IF FIELDTEXT = " "
  THEN ERROR 847 ;Compte de début obligatoire

  IF FIELDTEXT <> " "
  THEN BEGIN
    LET T_ERRCPT = "O"
    LET T_NATURE = 0
    WHILE RETRIEVING MCNATURE_CTB SEQUENTIAL
    BEGIN
       IF FIELDTEXT                  >= NACBORINF OF MCNATURE_CTB AND &
          FIELDTEXT                  <= NACBORSUP OF MCNATURE_CTB
       THEN BEGIN
            LET T_NATURE = NACCLE OF MCNATURE_CTB
            IF (NACCLE    OF MCNATURE_CTB   = 4 OR &
                NACCLE    OF MCNATURE_CTB   = 5)
            THEN BEGIN
                 LET T_ERRCPT = "N"
                 BREAK
            END
       END
    END

    IF T_ERRCPT = "O"
    THEN ERROR 830 ;La nature comptable du compte doit être 4 ou 5.

  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour les comptes.
;
;
PROCEDURE INPUT FDOCPTFIN
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTFIN = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_PECCLE    = " "
    LET T_CPTTYPPAT = "@"
    LET T_CPTCATPAT = "@"
    RUN SCREEN mc103 MODE F PASSING T_CPTFIN   , &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTFIN)
  END

  IF 0=SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = OLDVALUE(FDOCPTFIN OF GPFRD_DOMAINE)
END


PROCEDURE EDIT FDOCPTFIN
BEGIN
  IF FIELDTEXT = " "
  THEN ERROR 848 ;Compte de fin obligatoire

  IF FIELDTEXT <> " "
  THEN BEGIN
    LET T_ERRCPT = "O"
    LET T_NATURE2 = 0
    WHILE RETRIEVING MCNATURE_CTB SEQUENTIAL
    BEGIN
       IF FIELDTEXT                  >= NACBORINF OF MCNATURE_CTB AND &
          FIELDTEXT                  <= NACBORSUP OF MCNATURE_CTB
       THEN BEGIN
            LET T_NATURE2 = NACCLE OF MCNATURE_CTB
            IF (NACCLE    OF MCNATURE_CTB   = 4  OR &
                NACCLE    OF MCNATURE_CTB   = 5)
            THEN BEGIN
                 LET T_ERRCPT = "N"
                 BREAK
            END
       END
    END

    IF T_ERRCPT = "O"
    THEN ERROR 830 ;La nature comptable du compte doit être 4 ou 5.


    IF T_NATURE <> T_NATURE2
    THEN ERROR 873

    IF FIELDTEXT < FDOCPTDEB OF GPFRD_DOMAINE
    THEN ERROR 844 ;Compte de fin doit être supérieur ou égal
                   ;au compte de début.
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


PROCEDURE INTERNAL MAJ_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_3 &
         VIA    FRMCLE,           &
                FDOCLE,           &
                FDOSEQ,           &
                FRDTYP            &
         USING  FRMCLE OF GPFRD_DOMAINE, &
                FRDCLE OF GPFRD_DOMAINE, &
                FDOSEQ OF GPFRD_DOMAINE, &
                "F"
   BEGIN
      LET FDOCPTDEB OF GPFRD_DOMAINE_3 = FDOCPTDEB OF GPFRD_DOMAINE
      LET FDOCPTFIN OF GPFRD_DOMAINE_3 = FDOCPTFIN OF GPFRD_DOMAINE
      PUT GPFRD_DOMAINE_3
   END
END


PROCEDURE INTERNAL AJOUT_FORMULE2
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_4 &
         VIA    FRMCLE,             &
                FRDCLE              &
         USING  FRMCLE    OF GPFRD_SOMME, &
                FRSREFLIG OF GPFRD_SOMME
   BEGIN
           LET FRMCLE    OF GPFRD_DOMAINE_3 = FRMCLE      OF GPFRD_DOMAINE_4
           LET FRDCLE    OF GPFRD_DOMAINE_3 = FRDCLE      OF GPFRD_SOMME
           LET FDOREF    OF GPFRD_DOMAINE_3 = FRSREFLIG   OF GPFRD_SOMME
           LET FDOCLE    OF GPFRD_DOMAINE_3 = FDOCLE      OF GPFRD_DOMAINE_4
           LET FDOSEQ    OF GPFRD_DOMAINE_3 = FDOSEQ      OF GPFRD_DOMAINE_4
           LET FRDTYP    OF GPFRD_DOMAINE_3 = "F"
           LET FDOTYP    OF GPFRD_DOMAINE_3 = FDOTYP      OF GPFRD_DOMAINE_4
           LET FDOCPTDEB OF GPFRD_DOMAINE_3 = FDOCPTDEB   OF GPFRD_DOMAINE_4
           LET FDOCPTFIN OF GPFRD_DOMAINE_3 = FDOCPTFIN   OF GPFRD_DOMAINE_4

           IF  FRSOPE OF GPFRD_SOMME     = "+"
           THEN LET FDOOPE    OF GPFRD_DOMAINE_3 = FDOOPE OF GPFRD_DOMAINE_4
           ELSE BEGIN
                IF FDOOPE OF GPFRD_DOMAINE_4 = "+"
                THEN LET FDOOPE OF GPFRD_DOMAINE_3 = "-"
                ELSE LET FDOOPE OF GPFRD_DOMAINE_3 = "+"
           END
           PUT GPFRD_DOMAINE_3 RESET
   END
END


PROCEDURE INTERNAL RECREER_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_4 &
         VIA    FRMCLE,             &
                FRDTYP              &
         USING  FRMCLE OF GPFRD_DOMAINE, &
                "F"
   BEGIN
      DELETE GPFRD_DOMAINE_4
      PUT    GPFRD_DOMAINE_4
   END

   WHILE RETRIEVING GPFRD_SOMME           &
         VIA     FRMCLE                   &
         USING   FRMCLE OF GPFRD_DOMAINE  &
         ORDERBY FRMCLE,                  &
                 FRDCLE,                  &
                 FRSREFLIG
   BEGIN
      DO AJOUT_FORMULE2
   END
END


PROCEDURE INTERNAL DETRUIT_FORMULE_2
BEGIN
   LET T_CPTREF = 0
   WHILE RETRIEVING GPFRD_DOMAINE_4 &
         VIA   FRMCLE,              &
               FRDCLE,              &
               FDOREF               &
         USING FRMCLE OF GPFRD_DOMAINE_3, &
               FRDCLE OF GPFRD_DOMAINE_3, &
               FDOREF OF GPFRD_DOMAINE_3
   BEGIN
      LET T_CPTREF = T_CPTREF + 1
   END

   IF T_CPTREF = 1
   THEN BEGIN
        GET GPFRD_SOMME         &
            VIA    FRMCLE,      &
                   FRDCLE,      &
                   FRSREFLIG    &
            USING  FRMCLE OF GPFRD_DOMAINE_3, &
                   FRDCLE OF GPFRD_DOMAINE_3, &
                   FDOREF OF GPFRD_DOMAINE_3 OPTIONAL
        IF ACCESSOK
        THEN BEGIN
             DELETE GPFRD_SOMME
             PUT GPFRD_SOMME
        END
   END

   LET T_CPTREF = 0
   WHILE RETRIEVING GPFRD_DOMAINE_4 &
         VIA   FRMCLE,              &
               FRDCLE               &
         USING FRMCLE OF GPFRD_DOMAINE_3, &
               FRDCLE OF GPFRD_DOMAINE_3
   BEGIN
      LET T_CPTREF = T_CPTREF + 1
   END

   IF T_CPTREF = 1
   THEN BEGIN
        GET GPFOR_DETAILS_2     &
            VIA    FRMCLE,      &
                   FRDCLE       &
            USING  FRMCLE OF GPFRD_DOMAINE_3, &
                   FRDCLE OF GPFRD_DOMAINE_3 OPTIONAL
        IF ACCESSOK
        THEN BEGIN
             DELETE GPFOR_DETAILS_2
             PUT GPFOR_DETAILS_2
        END
   END
END


PROCEDURE INTERNAL DETRUIT_FORMULE
BEGIN
   WHILE RETRIEVING GPFRD_DOMAINE_3 &
         VIA   FRMCLE,              &
               FDOCLE,              &
               FDOSEQ,              &
               FRDTYP               &
         USING FRMCLE OF GPFRD_DOMAINE, &
               FDOCLE OF GPFRD_DOMAINE, &
               FDOSEQ OF GPFRD_DOMAINE, &
               "F"
   BEGIN
      DO DETRUIT_FORMULE_2
      DELETE GPFRD_DOMAINE_3
      PUT GPFRD_DOMAINE_3
   END
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR GPFRD_DOMAINE
  BEGIN
     ;
     ; Validation de la destruction
     ;
     IF     DELETEDRECORD OF GPFRD_DOMAINE       &
        AND TZ_SECDEL = "0"
     THEN ERROR 1
     ;
     ; Validation de la modification.
     ;
     IF     ALTEREDRECORD     OF GPFRD_DOMAINE       &
        AND NOT NEWRECORD     OF GPFRD_DOMAINE       &
        AND NOT DELETEDRECORD OF GPFRD_DOMAINE       &
        AND TZ_SECMOD = "0"
     THEN ERROR 2

     IF     ALTEREDRECORD OF GPFRD_DOMAINE AND &
        NOT NEWRECORD     OF GPFRD_DOMAINE AND &
        NOT DELETEDRECORD OF GPFRD_DOMAINE
     THEN DO MAJ_FORMULE

     IF DELETEDRECORD     OF GPFRD_DOMAINE
     THEN DO DETRUIT_FORMULE
  END
END

PROCEDURE POSTUPDATE
BEGIN
   DO RECREER_FORMULE
END

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************** Détail du format de présentation - Ligne calculée ***************
*                                                                              *
* Format........: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                         *
*                                                                              *
* Ligne   Type  Description                                                    *
* xxxxx    x    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx             *
********************************************************************************
*           Compte   Compte            *            Compte   Compte            *
* Séquence  Début    Fin     Opérateur *  Séquence  Début    Fin     Opérateur *
*                                      *                                       *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
*  xxxxx    xxxxxx   xxxxxx      x     *   xxxxx    xxxxxx   xxxxxx      x     *
********************************************************************************
@ENDIF

