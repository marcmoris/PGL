;/T/ Maintenance des intervalles
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 18 mai 1993
;
;    Description..: Maintenance des intervalles pour les listes de l'âges des
;                   compte et de la prévision des encaissements.
;
;/M/ Adatation....: Marc Morissette 11-Nov-1993
;
;    Description..: Ajouts de lignes pour commentaires
;
;
;/M/ Adaptation...: Nancy Marceau, 5 avril 1994
;
;    Description..: Permettre la saisie de deux colonnes variables pour les
;                   comptes à recevoir en souffrance. (CR600). Type 'Ages des
;                   comptes' et code '2'.
;
;/M/ Modification : Nancy Marceau, 15 avril 1994
;      - Ajout du sous-écran CR154A pour saisir les messages à produire sur
;        l'état de comptes (CR335). Déplacement des commentaires dans cet écran.
;
;/M/ Modifié par..: Guy Chabot le 11 aout 1994
;    Description..: Ajout du type '4' pour le compte à recevoir en souffrance.
;
;
;;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr154 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp NOLIST
    "CR154"

USE cr154.hlp

USE usactecr NOLIST
@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Messages à produire"    ACTION DESIGNER  D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Messages à produire" ACTION DESIGNER  D001
@ENDIF



;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.

;-------------------------------------------------------------------------------
;
;
FILE CRINTERVALLE     PRIMARY
  ACCESS REQUEST ITVTYP, &
                 ITVCOD  &
         VIA     CIECLE, &
                 ITVTYP, &
                 ITVCOD  &
         USING   T_CIECLEMNU, &
                 ITVTYP OF CRINTERVALLE, &
                 ITVCOD OF CRINTERVALLE  &
         ORDERBY CIECLE, &
                 ITVTYP, &
                 ITVCOD

  ACCESS REQUEST ITVTYP  &
         VIA     CIECLE , &
                 ITVTYP  &
         USING   T_CIECLEMNU, &
                 ITVTYP OF CRINTERVALLE &
         ORDERBY CIECLE, &
                 ITVTYP, &
                 ITVCOD
  ACCESS VIA     CIECLE  &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE, &
                 ITVTYP, &
                 ITVCOD

  ITEM CIECLE OF CRINTERVALLE INITIAL T_CIECLEMNU

;
; Validation du type de liste(bilingue).
;
DEFINE    D_ITVTYP CHARACTER SIZE 16 = "ITVTYP"
TEMPORARY T_ITVTYP CHARACTER SIZE 4

;
; Validation du type de liste.
;
FILE VCBI_DSC         REFERENCE
  ACCESS VIA XELNOM, &
             CBICLE  &
         USING D_ITVTYP, &
               ITVTYP OF CRINTERVALLE


;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST     ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Maintenance des intervalles " &
@ELSE
      " %%%Maintenance des intervalles " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 1
ALIGN (3,3,19) (,,21)

FIELD ITVTYP OF CRINTERVALLE HIDDEN ID 05 &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON VCBI_DSC &
          VIA XELNOM, &
              CBICLE &
          USING D_ITVTYP, &
                ITVTYP OF CRINTERVALLE &
          MESSAGE 682 ; Code invalide

FIELD CBIDSC OF VCBI_DSC DISPLAY

FIELD ITVCOD OF CRINTERVALLE HIDDEN ID 10 &
        REQUIRED &
        LOOKUP NOTON CRINTERVALLE &
          VIA CIECLE, &
              ITVTYP, &
              ITVCOD  &
          USING CIECLE OF CRINTERVALLE, &
                ITVTYP OF CRINTERVALLE, &
                ITVCOD OF CRINTERVALLE  &
          MESSAGE 703 ; Ce code existe déjà

SKIP

ALIGN(3,3,19)

FIELD ITVDSC OF CRINTERVALLE HIDDEN ID 12 &
        REQUIRED
SKIP

DRAW ,1 TO ,80
TITLE &
@IF FRANCAIS
      " Intervalle en jour " &
@ELSE
      "%%%Intervalle en jour " &
@ENDIF
      AT ,1 CENTERED

SKIP 1

TITLE &
@IF FRANCAIS
      "Titre         >=  <=" &
@ENDIF
      AT ,19

SKIP

ALIGN (3,3,19) (,,32) (,,36)
FIELD ITVCOLTIT1 OF CRINTERVALLE HIDDEN ID 15 &
        REQUIRED

FIELD ITVDEB1    OF CRINTERVALLE &
        DISPLAY

FIELD ITVFIN1    OF CRINTERVALLE &
        REQUIRED

SKIP 1

FIELD ITVCOLTIT2 OF CRINTERVALLE ID SAME &
        REQUIRED

FIELD ITVDEB2    OF CRINTERVALLE &
        DISPLAY

FIELD ITVFIN2    OF CRINTERVALLE &
        REQUIRED IF   ITVTYP OF CRINTERVALLE <> "4"


SKIP 1
  ;
  ; Les trois dernières colonnes ne sont pas saisies si l'intervalle est
  ; les comptes à recevoir en souffrance. Type = '4'.
  ;
FIELD ITVCOLTIT3 OF CRINTERVALLE ID SAME &
        REQUIRED IF   ITVTYP OF CRINTERVALLE <> "4"

FIELD ITVDEB3    OF CRINTERVALLE &
        DISPLAY

FIELD ITVFIN3    OF CRINTERVALLE &
        REQUIRED IF   ITVTYP OF CRINTERVALLE <> "4"

SKIP 1

FIELD ITVCOLTIT4 OF CRINTERVALLE ID SAME &
        REQUIRED IF   ITVTYP OF CRINTERVALLE <> "4"


FIELD ITVDEB4    OF CRINTERVALLE &
        DISPLAY

FIELD ITVFIN4    OF CRINTERVALLE &
        REQUIRED IF   ITVTYP OF CRINTERVALLE <> "4"

SKIP 1

FIELD ITVCOLTIT5 OF CRINTERVALLE ID SAME &
        REQUIRED IF   ITVTYP OF CRINTERVALLE <> "4"


FIELD ITVDEB5    OF CRINTERVALLE &
        DISPLAY


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END



;-------------------------------------------------------------------------------
;
; Appel du popup pour les codes bilingues.
;
;
PROCEDURE INPUT ITVTYP
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_ITVTYP = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_ITVTYP, T_ITVTYP
    LET FIELDTEXT = TRUNCATE( T_ITVTYP )
  END
END

PROCEDURE EDIT ITVCOD
BEGIN
  IF ITVTYP = "3"
  THEN BEGIN
    IF  ITVCOD <> "F" AND ITVCOD <> "A"
    THEN ERROR 2404 ; Le type doit être F-francais ou A-Anglais
  END
END

;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT ITVFIN1
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(ITVFIN1 OF CRINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT ITVFIN1
BEGIN
  IF FIELDVALUE <= ITVDEB1 OF CRINTERVALLE
  THEN ERROR 705 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS ITVFIN1
BEGIN
  LET ITVDEB2 OF CRINTERVALLE = ITVFIN1 OF CRINTERVALLE + 1
  DISPLAY ITVDEB2 OF CRINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT ITVFIN2
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(ITVFIN2 OF CRINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT ITVFIN2
BEGIN
  IF FIELDVALUE <= ITVDEB2 OF CRINTERVALLE
  THEN ERROR 705 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS ITVFIN2
BEGIN
  LET ITVDEB3 OF CRINTERVALLE = ITVFIN2 OF CRINTERVALLE + 1
  DISPLAY ITVDEB3 OF CRINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT ITVFIN3
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(ITVFIN3 OF CRINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT ITVFIN3
BEGIN
  IF FIELDVALUE <= ITVDEB3 OF CRINTERVALLE
  THEN ERROR 705 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS ITVFIN3
BEGIN
  LET ITVDEB4 OF CRINTERVALLE = ITVFIN3 OF CRINTERVALLE + 1
  DISPLAY ITVDEB4 OF CRINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Force la validation des intervalles.
;
;
PROCEDURE INPUT ITVFIN4
BEGIN
  IF NOT FINDMODE AND &
     0 = SIZE(FIELDTEXT)
  THEN LET FIELDTEXT = ASCII(ITVFIN4 OF CRINTERVALLE)
END
;
;
; Le nombre de jour de fin doit être plus grand que celui de début et calcul
; le prochain intervalle.
;
;
PROCEDURE EDIT ITVFIN4
BEGIN
  IF FIELDVALUE <= ITVDEB4 OF CRINTERVALLE
  THEN ERROR 705 ;Le nombre de jours de fin doit être supérieur à celui de début
END
;
;
PROCEDURE PROCESS ITVFIN4
BEGIN
  LET ITVDEB5 OF CRINTERVALLE = ITVFIN4 OF CRINTERVALLE + 1
  DISPLAY ITVDEB5 OF CRINTERVALLE
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CRINTERVALLE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CRINTERVALLE &
     AND NOT NEWRECORD     OF CRINTERVALLE &
     AND NOT DELETEDRECORD OF CRINTERVALLE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END


;-------------------------------------------------------------------------------
; Cette procédure est codée afin de ne pas saisir les colonnes 3,4 et 5 lorsque
; nous avons un âge des comptes de type comptes à recevoir en souffrance.
; Type = '1' et code = '2'.
;
PROCEDURE DESIGNER 15
BEGIN
  ACCEPT  ITVCOLTIT1 OF CRINTERVALLE
  DISPLAY ITVDEB1    OF CRINTERVALLE
  ACCEPT  ITVFIN1    OF CRINTERVALLE

  ACCEPT  ITVCOLTIT2 OF CRINTERVALLE
  DISPLAY ITVDEB2    OF CRINTERVALLE
    ;
    ; Ces champs ne sont pas saisis pour le Type = '4'.
    ; Comptes à recevoir en souffrance
    ;
  IF   ITVTYP OF CRINTERVALLE <> "4"
  THEN BEGIN
    ACCEPT  ITVFIN2    OF CRINTERVALLE

    ACCEPT  ITVCOLTIT3 OF CRINTERVALLE
    DISPLAY ITVDEB3    OF CRINTERVALLE
    ACCEPT  ITVFIN3    OF CRINTERVALLE

    ACCEPT  ITVCOLTIT4 OF CRINTERVALLE
    DISPLAY ITVDEB4    OF CRINTERVALLE
    ACCEPT  ITVFIN4    OF CRINTERVALLE

    ACCEPT  ITVCOLTIT5 OF CRINTERVALLE
    DISPLAY ITVDEB5    OF CRINTERVALLE
  END

END

;-------------------------------------------------------------------------------
; Appel du sous-écran des messages à produire
;
PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN cr154a &
       PASSING T_CIECLEMNU, &
               T_CIENOM,    &
               CRINTERVALLE &
       MODE SAME
END



BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************* Maintenance des intervalles **************************
*                                                                              *
* Type..........: x xxxxxxxxxxxxxxxxxxxxxxxxx                                  *
* Code..........: x                                                            *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
****************************** Intervalle en jour ******************************
*                                                                              *
*                 Titre         >=  <=                                         *
* Colonne 1.....: xxxxxxxxxxx  xxx xxx                                         *
*                                                                              *
* Colonne 2.....: xxxxxxxxxxx  xxx xxx                                         *
*                                                                              *
* Colonne 3.....: xxxxxxxxxxx  xxx xxx                                         *
*                                                                              *
* Colonne 4.....: xxxxxxxxxxx  xxx xxx                                         *
*                                                                              *
* Colonne 5.....: xxxxxxxxxxx  xxx                                             *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
