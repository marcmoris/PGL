;/T/ Menu principal gestion des blocs
;
;/P/ Programmeur..: Francois Richard
;    Date Cr\351ation: 16 septembre 1999
;
;    Description..: Menu principal de la gestion des blocs
;
;/M/ Programmeur..: Bernard Samson
;    date.........: 1999-10-04
;    Description..: Ajout du transfert du numero de cet écran dans les para
;                   mettre de l'apelle de l'ecran de commande interne.

SCREEN ifm01 MENU &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             ACTIVITIES CHANGE &
             RECEIVING T_FLGHLDOK

USE ussectmp NOLIST
    "IFM01"

USE ifm01.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le holding est présent.

;-------------------------------------------------------------------------------
;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      DESIGNER &
                      OPEN READ
;
; Pour la securite sur les compagnies.
;
FILE VSEC_USR         DESIGNER &
                      OPEN READ

;
; Période comptable par compagnie
;
FILE MCPERIODE_CIE    DESIGNER

;
; Informations sur le Holding.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"
;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu.
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies
TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; compagnie du client
;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "IFL@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "IFP@" ; Code pour les procédures

TEMPORARY T_PECCLECOUCP CHARACTER SIZE 4  RESET AT STARTUP ; P\351riode courante
TEMPORARY T_PECCLECOUCR CHARACTER SIZE 4  RESET AT STARTUP ; P\351riode courante
TEMPORARY T_PECCLEMNU   CHARACTER SIZE 4  RESET AT STARTUP
TEMPORARY T_ECRAPP      CHARACTER SIZE 5  RESET AT STARTUP

TEMPORARY T_TGRCODGRA_ORI    CHARACTER SIZE 3  RESET AT STARTUP
TEMPORARY T_IFCCLE_ORI       CHARACTER SIZE 2  RESET AT STARTUP
TEMPORARY T_BLCCLE_ORI       CHARACTER SIZE 6  RESET AT STARTUP
TEMPORARY T_BLCRED_ORI       CHARACTER SIZE 1  RESET AT STARTUP

TEMPORARY T_FOUCIECLE    CHARACTER SIZE 4  &
                           RESET AT STARTUP  ; Cie du fournisseur
TEMPORARY T_EMPCIECLE    CHARACTER SIZE 4  &
                           RESET AT STARTUP  ; Cie de l'employ\351


USE ushilite NOLIST

DRAW 1,20 TO 3,61
DRAW 2,1  TO 2,20
DRAW 2,61 TO 2,80

DRAW 2,1  TO 5,1
DRAW 5,1  TO 5,80
DRAW 2,80 TO 5,80

DRAW 5,40 TO 19,40
DRAW 19,1 TO 19,80

SKIP 1
TITLE "M E N U   G E S T I O N   B L O C" CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

ALIGN(4,4,20) (,,26)
FIELD T_CIECLEMNU HIDDEN ID 05 &
        FIXED            &
        UPSHIFT REQUIRED &
        PATTERN "((^|#)>)|!0" &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Company......:"  &
@ENDIF
        LOOKUP ON MCCOMPAGNIE &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 604, & ; Cette compagnie n'existe pas

          ON VSEC_USR &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 655 ; Vous n'avez pas accès à cette compagnie.

FIELD T_CIENOM &
        FIXED


SKIP TO 7

ALIGN (2,2)

SUBSCREEN if001 &
        HIDDEN &
        LABEL "Cartes de blocs                      " &
        PASSING T_CIECLEMNU, &
                T_CIENOM, &
                T_TGRCODGRA_ORI, &
                T_IFCCLE_ORI, &
                T_BLCCLE_ORI, &
                T_BLCRED_ORI

SKIP 1

SUBSCREEN pd175 &
        HIDDEN &
        LABEL "Projets                              " &
        PASSING T_CIECLEMNU, T_CIENOM, T_ECRAPP

SKIP 1

SUBSCREEN if020 &
        HIDDEN &
        LABEL "Lots d'expédition                    " &
        PASSING T_CIECLEMNU, T_CIENOM

SKIP 1

SUBSCREEN cr001 &
        HIDDEN &
        LABEL "Clients                              " &
        PASSING T_CLICIECLE, &
                T_CIECLEMNU, &
                T_CIENOM   , &
                T_PECCLEMNU, &
                T_PECCLECOUCR

SKIP 1

SUBSCREEN cp001 &
        HIDDEN &
        LABEL "Fournisseurs                         " &
        PASSING	T_CIECLEMNU,   &
                T_CIENOM   ,   &
                T_PECCLEMNU,   &
                T_PECCLECOUCP, &
                T_FOUCIECLE,   &
                T_EMPCIECLE

SKIP

SKIP TO 7
ALIGN (42,42)

SUBSCREEN pd210 &
        HIDDEN &
        LABEL "Commandes interne                    " &
        PASSING T_CIECLEMNU, T_CIENOM, T_ECRAPP

SKIP 1

SUBSCREEN if002 &
        HIDDEN &
        LABEL "Expéditions                          " &
        PASSING T_CIECLEMNU, T_CIENOM

SKIP 1
SUBSCREEN pd225 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM,    &
                        T_ECRAPP     &
@IF FRANCAIS
                LABEL "Facturation bloc                      "
@ELSE
                LABEL "%%%Facturation commande interne       "
@ENDIF

skip 1

SUBSCREEN pd214 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM     &
@IF FRANCAIS
                LABEL "Générer les lots de transfert        "
@ELSE
                LABEL "%%%G\351rer les lots de transfert     "
@ENDIF



SKIP TO 20

ALIGN(31,31)
SUBSCREEN ifm90 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L, &
                        T_CIECLEMNU, &
                        T_CIENOM     &
                LABEL " Listes et rapports "

SUBSCREEN ifm90 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P, &
                        T_CIECLEMNU, &
                        T_CIENOM     &
                LABEL "    Procédures      "

SUBSCREEN ifm02 HIDDEN &
                LABEL "Informations de base" &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_CLICIECLE



PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le holding n'est pas défini on retourne l'usager au menu
  ; principal.
  ;
  IF T_FLGHLDOK = "0"
  THEN RETURN

  USE ussececr NOLIST

  ACCEPT T_CIECLEMNU

  ; Écran qui appelle pd175 et pd210
  LET T_ECRAPP = "ifm01"

END

;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
  ;
  ; A cause de l'option requis, si l'usager fait un retour et qu'il y a
  ; une valeur on remet la même.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_CIECLEMNU))
  THEN LET FIELDTEXT = T_CIECLEMNU
END

;-------------------------------------------------------------------------------
;
;
; On conserve le nom de la compagnie du menu.
;
;
PROCEDURE PROCESS T_CIECLEMNU
BEGIN
  IF HLDCHTCLI OF MCHOLDING = "1"
  THEN LET T_CLICIECLE = T_CIECLEMNU
  ELSE LET T_CLICIECLE = HLDCHRSUB OF MCHOLDING

  LET T_CIENOM = CIENOM OF MCCOMPAGNIE
  LET T_FOUCIECLE = T_CLICIECLE
  LET T_EMPCIECLE = T_CLICIECLE
  DISPLAY T_CIENOM

  ;
  ; On va chercher la periode courante du module C/R.
  ;
  GET MCPERIODE_CIE VIA CIECLE, &
                        PCCSTUCR &
                    USING T_CIECLEMNU, &
                        "C" &
                    OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 720 ; Il n'y a pas de p\351riode courante de d\351finie.
  ELSE BEGIN
    LET T_PECCLECOUCR = PECCLE OF MCPERIODE_CIE
    LET T_PECCLEMNU = PECCLE OF MCPERIODE_CIE
  END

  ;
  ; On va chercher la periode courante du module C/P.
  ;
  GET MCPERIODE_CIE VIA CIECLE, &
                        PCCSTUCP &
                    USING T_CIECLEMNU, &
                        "C" &
                    OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 720 ; Il n'y a pas de p\351riode courante de d\351finie.
  ELSE BEGIN
    LET T_PECCLECOUCP = PECCLE OF MCPERIODE_CIE
    LET T_PECCLEMNU = PECCLE OF MCPERIODE_CIE
  END

END
;
;
PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_CIECLEMNU
END


BUILD
