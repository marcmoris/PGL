;/T/ 2.5.7 Facturation commande interne. (Commentaire tranche)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-03-21
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les commentaires pour ventes directes de tranche.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd215e ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDFACTURE,          &
                        PDTRANCHE_FAC


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD215E"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd215e.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_SILENT           CHARACTER SIZE 01


;-------------------------------------------------------------------------------
;
; Facture
;

FILE PDFACTURE MASTER


;-------------------------------------------------------------------------------
;
; Bloc facture
;

FILE PDTRANCHE_FAC MASTER


;-------------------------------------------------------------------------------
;
; Commentaire facture
;

FILE PDCOMMEN_TRANCHE PRIMARY &
                      NOITEM &
                      OCCURS 11 TIMES

  ACCESS VIA     CIECLE,           &
                 FTRNUMFAC,        &
                 TRANUMTRA002      &
         USING   T_CIECLEMNU,                           &
                 FTRNUMFAC         OF PDTRANCHE_FAC,    &
                 TRANUMTRA002      OF PDTRANCHE_FAC     &
         ORDERBY CIECLE,           &
                 FTRNUMFAC,        &
                 TRANUMTRA002,     &
                 COTNUMLIG

  ITEM CIECLE    OF PDCOMMEN_TRANCHE INITIAL T_CIECLEMNU
  ITEM FTRNUMFAC OF PDCOMMEN_TRANCHE INITIAL FTRNUMFAC OF PDFACTURE


;-------------------------------------------------------------------------------
;
; Client
;

FILE VCLC_CLI        REFERENCE

  ACCESS VIA     CLICIECLE,        &
                 CLICLE,           &
                 CIECLE            &
         USING   "----",                                &
                 CLICLE            OF PDFACTURE,        &
                 T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Variable servant au traitement
;

TEMPORARY T_NOLIG INT SIZE 04 RESET AT STARTUP ; Pour no ligne automatique.


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Facturation commande interne " &
@ELSE
      " %%%Facturation commande interne " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD FTRNUMFAC        OF PDTRANCHE_FAC    &
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,28)


FIELD CLICLE           OF PDFACTURE        &
        DISPLAY                            &
        FIXED


FIELD CLINOM           OF VCLC_CLI         &
        DISPLAY                            &
        FIXED


DRAW 8,2 TO 8,79


SKIP TO LINE 8


TITLE &
@IF FRANCAIS
      " Commentaire " &
@ELSE
      " %%%Commentaire " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      "    Lig.   Description                                Imp. " &
@ELSE
      "%%% Lig.   Description                                Imp. " &
@ENDIF
      AT ,2


ALIGN (05,05,06) (,,13) (,,57)


CLUSTER OCCURS WITH PDCOMMEN_TRANCHE ID BASE 90


FIELD COTNUMLIG        OF PDCOMMEN_TRANCHE &
        HIDDEN                             &
        LABEL " "


FIELD COTCOM           OF PDCOMMEN_TRANCHE &
        HIDDEN


FIELD COTFLGIMP        OF PDCOMMEN_TRANCHE &
        HIDDEN


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procedure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT COTNUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT ) AND &
     COTNUMLIG OF PDCOMMEN_TRANCHE = 0
  THEN BEGIN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
  END
END


;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE PROCESS COTNUMLIG
BEGIN
  LET T_NOLIG = COTNUMLIG OF PDCOMMEN_TRANCHE
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;

PROCEDURE INPUT COTFLGIMP
BEGIN
  USE usflginp NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;

PROCEDURE OUTPUT COTFLGIMP
BEGIN
  USE usflgout NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDCOMMEN_TRANCHE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDCOMMEN_TRANCHE &
     AND NOT NEWRECORD     OF PDCOMMEN_TRANCHE &
     AND NOT DELETEDRECORD OF PDCOMMEN_TRANCHE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Ventes directes ********************************
*                                                                              *
* No. facture...: xxxxxxxxx                                                    *
* Client........: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
********************************* Commentaire **********************************
*                                                                              *
*    Lig.   Description                                Imp.                    *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*    xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    x                      *
*                                                                              *
********************************************************************************
@ENDIF
