;/T/ Enregistrer les réservation de bloc.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-15
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   les réservations des blocs de granit de Granicor.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd179 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_RESER_BLOC IN DICT


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD179"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd179.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D001
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variables nécessaire au traitement
;

TEMPORARY T_NOLIG  INTEGER   SIZE 04 RESET AT STARTUP ; Pour no ligne automatique.
TEMPORARY T_SILBLO CHARACTER SIZE 01                  ; Pour valider cle bloc


;-------------------------------------------------------------------------------
;
; Réservation de bloc
;

FILE PDRESERVATION PRIMARY &
                   NOITEM  &
                   NODELETE


  ACCESS VIA     CIECLE,                                     &
                 RESNUMRES                                   &
         USING   T_CIECLEMNU,                                &
                 RESNUMRES       OF PDRESERVATION            &
         REQUEST RESNUMRES       OF PDRESERVATION            &
         ORDERBY CIECLE,                                     &
                 RESNUMRES

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 RESNUMRES

  ITEM CIECLE    OF PDRESERVATION INITIAL T_CIECLEMNU
  ITEM RESDATCRE OF PDRESERVATION INITIAL SYSDATE
  ITEM RESHRECRE OF PDRESERVATION INITIAL SYSTIME / 10000
  ITEM RESUSRCRE OF PDRESERVATION INITIAL LOGONID


;-------------------------------------------------------------------------------
;
; Réservation de bloc
;

FILE PDRESER_BLOCS DETAIL &
                   NOITEM    &
                   OCCURS    9

  ACCESS VIA     CIECLE,                      &
                 RESNUMRES                    &
         USING   T_CIECLEMNU,                 &
                 RESNUMRES OF PDRESERVATION   &
         ORDERBY CIECLE,                      &
                 RESNUMRES,                   &
                 RBLNUMLIG

  ITEM BLONUMBLOAPR OF PDRESER_BLOCS FINAL BLONUMGRA001APR OF PDRESER_BLOCS + &
                                           BLONUMGRA002APR OF PDRESER_BLOCS + &
                                           BLORECAPR       OF PDRESER_BLOCS

  ITEM CIECLE       OF PDRESER_BLOCS INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Bloc de granit
;

FILE PDBLOC REFERENCE OCCURS WITH PDRESER_BLOCS

  ACCESS VIA     CIECLE,                                     &
                 BLONUMGRA001APR,                            &
                 BLONUMGRA002APR,                            &
                 BLORECAPR                                   &
         USING   T_CIECLEMNU,                                &
                 BLONUMGRA001APR OF PDRESER_BLOCS,           &
                 BLONUMGRA002APR OF PDRESER_BLOCS,           &
                 BLORECAPR       OF PDRESER_BLOCS


;-------------------------------------------------------------------------------
;
; Bloc de granit
;

FILE PDBLOC DESIGNER ALIAS BLO_UPDATE &
            NOITEM

  ACCESS VIA     CIECLE,                                     &
                 BLONUMGRA001APR,                            &
                 BLONUMGRA002APR,                            &
                 BLORECAPR                                   &
         USING   T_CIECLEMNU,                                &
                 BLONUMGRA001APR OF PDRESER_BLOCS,           &
                 BLONUMGRA002APR OF PDRESER_BLOCS,           &
                 BLORECAPR       OF PDRESER_BLOCS


;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les bons de commande
;

FILE PDSEQ_RESER_BLOC DESIGNER &
                      TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Réservation " &
@ELSE
      " %%%Réservation " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD RESNUMRES OF PDRESERVATION              &
label "No reservation:"&
        HIDDEN ID 05                          &
        DISPLAY                               &
        REFRESH

FIELD RESNOM    OF PDRESERVATION              &
label "Nom...........:"&
        HIDDEN ID 10                          &
        REQUIRED


FIELD RESDSC001 OF PDRESERVATION              &
label "Description...:"&
        HIDDEN ID 15


FIELD RESDSC002 OF PDRESERVATION              &
        ID SAME                               &
        NOLABEL


DRAW 09,2 TO 09,79


SKIP TO LINE 11


TITLE &
@IF FRANCAIS
      "        Ligne  No bloc          Date réserv   Date fin   Annulé " &
@ELSE
      "%%%     Ligne  No bloc         Date réserv    Date fin   Annulé " &
@ENDIF
      AT ,2

SKIP

ALIGN (10,10,11)(16,16,17)(,21,22)(,27,28)(34,,35)(47,,48)(60,,61)

CLUSTER OCCURS WITH PDRESER_BLOCS ID BASE 20

FIELD RBLNUMLIG       OF PDRESER_BLOCS        &
        HIDDEN                                &
        LABEL " "                             &
        REQUIRED NOCHANGE                     &
        LOOKUP NOTON PDRESER_BLOCS            &
               VIA     CIECLE,                &
                       RESNUMRES,             &
                       RBLNUMLIG              &
               USING   T_CIECLEMNU,           &
                       RESNUMRES OF PDRESER_BLOCS, &
                       RBLNUMLIG OF PDRESER_BLOCS  &
               MESSAGE 2921 ; Ce numéro de ligne existe déjà

FIELD BLONUMGRA001APR OF PDRESER_BLOCS        &
        ID SAME                               &
        LABEL " "                             &
        NUMERIC                               &
        REQUIRED

FIELD BLONUMGRA002APR OF PDRESER_BLOCS        &
        ID SAME                               &
        REQUIRED                              &
        NUMERIC                               &
        LABEL "-"

FIELD BLORECAPR       OF PDRESER_BLOCS        &
        ID SAME                               &
        DEFAULT " "                           &
        LABEL "-"

FIELD T_SILBLO &
        SILENT &
        LOOKUP ON PDBLOC                      &
          MESSAGE 2943 ; Ce numéro de bloc n'existe pas

FIELD RBLDATRES       OF PDRESER_BLOCS         FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        ID SAME                               &
        REQUIRED

FIELD RBLDATFIN       OF PDRESER_BLOCS         FORMAT YYYYMMDD &
PATTERN "####/##/##"&
        ID SAME                               &
        REQUIRED

FIELD RBLFLGCLL       OF PDRESER_BLOCS        &
        ID SAME                               &
        DEFAULT "0"

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Procedure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT RBLNUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT ) AND &
     RBLNUMLIG OF PDRESER_BLOCS = 0
  THEN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
END

;-------------------------------------------------------------------------------
;
; Procedure permettant de générer le numéro de ligne automatique
;

PROCEDURE PROCESS RBLNUMLIG
BEGIN
    LET T_NOLIG = RBLNUMLIG OF PDRESER_BLOCS
END

;-------------------------------------------------------------------------------
;
; Valide que le bloc est en inventaire
;

PROCEDURE EDIT T_SILBLO
BEGIN
  IF BLOSTU OF PDBLOC <> "I" AND BLOSTU OF PDBLOC <> "R"
  THEN BEGIN
    ERROR 2948 ; Le bloc n'a pas le statut inventaire
  END
END

;-------------------------------------------------------------------------------
;
; Procédure pour convertir OUI,NON en 1,0
;
PROCEDURE INPUT RBLFLGCLL
BEGIN
  USE usflginp NOLIST
END

;-------------------------------------------------------------------------------
;
; Procédure pour convertir 1,0 en OUI,NON
;
PROCEDURE OUTPUT RBLFLGCLL
BEGIN
  USE usflgout NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Initialise le numéro de ligne au dernier numéro de ligne trouvé
;

PROCEDURE POSTFIND
BEGIN
  LET T_NOLIG = RBLNUMLIG OF PDRESER_BLOCS
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDRESERVATION &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDRESERVATION &
     AND NOT NEWRECORD     OF PDRESERVATION &
     AND NOT DELETEDRECORD OF PDRESERVATION &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  FOR PDRESER_BLOCS
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDRESER_BLOCS &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDRESER_BLOCS &
       AND NOT NEWRECORD     OF PDRESER_BLOCS &
       AND NOT DELETEDRECORD OF PDRESER_BLOCS &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END


  ;-----------------------------------------------------------------------------
  ;
  ; Évaluation des champs indiquant une mise à jour de la réservation
  ;
  IF (NOT NEWRECORD OF PDRESERVATION AND &
      ALTEREDRECORD OF PDRESERVATION) &
     OR &
     (NOT NEWRECORD OF PDRESER_BLOCS AND &
      ALTEREDRECORD OF PDRESER_BLOCS)
  THEN BEGIN
    LET RESDATMAJ     OF PDRESERVATION = SYSDATE
    LET RESHREMAJ     OF PDRESERVATION = SYSTIME / 10000
    LET RESUSRMAJ     OF PDRESERVATION = LOGONID
  END


  ;-----------------------------------------------------------------------------
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = RESNUMRES OF PDRESERVATION
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_RESER_BLOC OPTIONAL       &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_RESER_BLOC = T_CIECLEMNU
    END
    LET SRBNUMRES OF PDSEQ_RESER_BLOC = SRBNUMRES OF PDSEQ_RESER_BLOC + 1
    LET RESNUMRES OF PDRESERVATION    = SRBNUMRES OF PDSEQ_RESER_BLOC
    LET RESNUMRES OF PDRESER_BLOCS    = SRBNUMRES OF PDSEQ_RESER_BLOC
    PUT PDSEQ_RESER_BLOC
    DISPLAY RESNUMRES OF PDRESERVATION
    INFO = " " NOW RESP
  END

  FOR PDRESER_BLOCS
  BEGIN
    IF NOT DELETEDRECORD OF PDRESER_BLOCS &
       AND &
       ALTEREDRECORD OF PDRESER_BLOCS
    THEN BEGIN
      LET RESNUMRES OF PDRESER_BLOCS = RESNUMRES OF PDRESERVATION
;      GET BLO_UPDATE
;      LET BLOSTU    OF PDBLOC        = "R"
    END
  END

END

;-------------------------------------------------------------------------------
;
; Procedure de destruction
; on ne détruit pas la réservation mais on peut détruire une ligne de détail
;

PROCEDURE DELETE
BEGIN
  FOR PDRESER_BLOCS
  BEGIN
    DELETE PDRESER_BLOCS
    LET BLOSTU    OF PDBLOC        = "I"
    PUT PDBLOC
  END
END

;-------------------------------------------------------------------------------
;
; procedure de destruction pour les lignes de détail
;

PROCEDURE DETAIL DELETE
BEGIN
  LET BLOSTU    OF PDBLOC        = "I"
  PUT PDBLOC
  DELETE PDRESER_BLOCS
END

;-------------------------------------------------------------------------------
;
; Redimension
;

PROCEDURE DESIGNER D001
BEGIN
  RUN SCREEN  pd179a &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDRESERVATION       &
              MODE SAME
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************* Réservation **********************************
*                                                                              *
* No reservation: xxxxxxxxxxx                                                  *
* Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
********************************************************************************
*                                                                              *
*        Ligne  No bloc         Date réserv    Date fin   Annulé               *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*         xxx   xxxxx-xxxxx-x     xxxxxxxx     xxxxxxxx     x                  *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
