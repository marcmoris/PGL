;/P/ Rapport de production en mètre cube par carrière
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFCARRIERE_CUMUL

CHOOSE CIECLE    PARM, &
       CRRCLENUM PARM, &
       CRRCLEANN PARM

DEFINE TMP-MOIS   ZONED SIZE 2 = NCONVERT(PECCLE[3:2])

DEFINE TMP-TOT-01 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 1
DEFINE TMP-TOT-02 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 2
DEFINE TMP-TOT-03 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 3
DEFINE TMP-TOT-04 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 4
DEFINE TMP-TOT-05 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 5
DEFINE TMP-TOT-06 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 6
DEFINE TMP-TOT-07 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 7
DEFINE TMP-TOT-08 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 8
DEFINE TMP-TOT-09 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 9
DEFINE TMP-TOT-10 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 10
DEFINE TMP-TOT-11 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 11
DEFINE TMP-TOT-12 PICTURE "^^^^^^.^"  = CACQTEPROD / 10 IF TMP-MOIS = 12
DEFINE TMP-FAC-13 PICTURE "^^^^^^^.^" = CACQTEPROD / 10

; Ajustement pour le changement de siècle
DEFINE D_PECCLE_SIE CHARACTER SIZE 5 = "1" + PECCLE &
         IF PECCLE[1:1] < "8" &
         ELSE "0" + PECCLE

SORT ON CRRCLENUM &
     ON D_PECCLE_SIE

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "Pour l'année : " + ASCII( CRRCLEANN )
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF313"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
TAB 001 " Carrière    01       02       03       04       05       06       07       08       09       10       11       12      Total" &
SKIP 1 &
TAB 001 " -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- ---------"

FOOTING AT CRRCLENUM &
        TAB 005 CRRCLENUM &
        TAB 011 TMP-TOT-01 SUBTOTAL  &
        TAB 020 TMP-TOT-02 SUBTOTAL  &
        TAB 029 TMP-TOT-03 SUBTOTAL  &
        TAB 038 TMP-TOT-04 SUBTOTAL  &
        TAB 047 TMP-TOT-05 SUBTOTAL  &
        TAB 056 TMP-TOT-06 SUBTOTAL  &
        TAB 065 TMP-TOT-07 SUBTOTAL  &
        TAB 074 TMP-TOT-08 SUBTOTAL  &
        TAB 083 TMP-TOT-09 SUBTOTAL  &
        TAB 092 TMP-TOT-10 SUBTOTAL  &
        TAB 101 TMP-TOT-11 SUBTOTAL  &
        TAB 110 TMP-TOT-12 SUBTOTAL  &
        TAB 119 TMP-FAC-13 SUBTOTAL  &
SKIP 2

FINAL FOOTING &
TAB 001 "          -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- -------- ---------" &
SKIP 1 &
        TAB 011 TMP-TOT-01 SUBTOTAL  &
        TAB 020 TMP-TOT-02 SUBTOTAL  &
        TAB 029 TMP-TOT-03 SUBTOTAL  &
        TAB 038 TMP-TOT-04 SUBTOTAL  &
        TAB 047 TMP-TOT-05 SUBTOTAL  &
        TAB 056 TMP-TOT-06 SUBTOTAL  &
        TAB 065 TMP-TOT-07 SUBTOTAL  &
        TAB 074 TMP-TOT-08 SUBTOTAL  &
        TAB 083 TMP-TOT-09 SUBTOTAL  &
        TAB 092 TMP-TOT-10 SUBTOTAL  &
        TAB 101 TMP-TOT-11 SUBTOTAL  &
        TAB 110 TMP-TOT-12 SUBTOTAL  &
        TAB 119 TMP-FAC-13 SUBTOTAL

BUILD  if313z
