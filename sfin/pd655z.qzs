;/T/ Liste de ce qui reste à emballer (SANS PRIORITÉ)
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-18
;
;    Description...: Liste des diagramme qui reste à emballer
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDPRIORITE_DIAG               &

  LINK CIECLE    OF PDPRIORITE_DIAG, &
       DIANUM002 OF PDPRIORITE_DIAG  &
    TO CIECLE,                       &
       DIANUM002 OF PDDIAGRAMME      &

  LINK CIECLE    OF PDPRIORITE_DIAG, &
       COMNUMCOM OF PDPRIORITE_DIAG  &
    TO CIECLE,                       &
       COMNUMCOM OF PDCOMMAN_INTERNE &

  LINK CIECLE    OF PDPRIORITE_DIAG, &
       COMNUMCOM OF PDPRIORITE_DIAG, &
       PRICODPRI OF PDPRIORITE_DIAG  &
    TO CIECLE,                       &
       COMNUMCOM,                    &
       PRICODPRI OF PDPRIORITE       &

  LINK CIECLE    OF PDPRIORITE_DIAG, &
       COMNUMCOM OF PDPRIORITE_DIAG, &
       DCINUMLIG OF PDPRIORITE_DIAG  &
    TO CIECLE,                       &
       COMNUMCOM,                    &
       DCINUMLIG OF PDDETAIL_C_I


CHOOSE CIECLE PARM , COMNUMCOM PARM


SORT ON COMNUMCOM OF PDPRIORITE_DIAG &
     ON DIANUM002 OF PDPRIORITE_DIAG

;-------------------------------------------------------------------------------
;
; Variable nécessaire pour afficher les quantités à emballer
;

DEFINE D_SUR_MC2 FLOAT SIZE 08 = DIASURMC2 OF PDDIAGRAMME

DEFINE D_QTE_PLA_GRA INTEGER SIZE 04 = PDGQTEPLAFINGRA OF PDPRIORITE_DIAG
DEFINE D_QTE_FIN_GRA INTEGER SIZE 04 = PDGQTEFINPRDGRA OF PDPRIORITE_DIAG

DEFINE D_QTE_PLA_USC INTEGER SIZE 04 = PDGQTEPLAFINUSC OF PDPRIORITE_DIAG
DEFINE D_QTE_FIN_USC INTEGER SIZE 04 = PDGQTEFINRCUUSC OF PDPRIORITE_DIAG

DEFINE D_QTE_PLA_SCT INTEGER SIZE 04 = PDGQTEPLAFINSCT OF PDPRIORITE_DIAG
DEFINE D_QTE_FIN_SCT INTEGER SIZE 04 = PDGQTEFINRCUSCT OF PDPRIORITE_DIAG

DEFINE D_RESTE_GRA INTEGER SIZE 04 = PDGQTEPLAFINGRA OF PDPRIORITE_DIAG - PDGQTEFINPRDGRA OF PDPRIORITE_DIAG &
                                     IF 0 < PDGQTEPLAFINGRA  OF PDPRIORITE_DIAG - PDGQTEFINPRDGRA OF PDPRIORITE_DIAG &
                                     ELSE 0
DEFINE D_RESTE_USC INTEGER SIZE 04 = PDGQTEPLAFINUSC OF PDPRIORITE_DIAG - PDGQTEFINRCUUSC OF PDPRIORITE_DIAG &
                                     IF 0 < PDGQTEPLAFINUSC OF PDPRIORITE_DIAG - PDGQTEFINRCUUSC  OF PDPRIORITE_DIAG &
                                     ELSE 0
DEFINE D_RESTE_SCT INTEGER SIZE 04 = PDGQTEPLAFINSCT OF PDPRIORITE_DIAG - PDGQTEFINRCUSCT OF PDPRIORITE_DIAG &
                                     IF 0 < PDGQTEPLAFINSCT OF PDPRIORITE_DIAG - PDGQTEFINRCUSCT OF PDPRIORITE_DIAG &
                                     ELSE 0

DEFINE D_SUR_PLA_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_GRA
DEFINE D_SUR_FIN_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_GRA
DEFINE D_SUR_RES_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_GRA

DEFINE D_SUR_PLA_USC FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_USC
DEFINE D_SUR_FIN_USC FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_USC
DEFINE D_SUR_RES_USC FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_USC

DEFINE D_SUR_PLA_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_SCT
DEFINE D_SUR_FIN_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_SCT
DEFINE D_SUR_RES_SCT FLOAT SIZE 08 = D_SUR_MC2 * D_RESTE_SCT

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Liste des diagrammes qui reste à emballer sans priorité"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD655"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitstd NOLIST
  SKIP 1

HEADING AT COMNUMCOM &
  SKIP PAGE &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDPRIORITE_DIAG &
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDPRIORITE_DIAG &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP 2 &
  TAB 001 "                *--------- Granicor --------*  *----- Usine secondaire ----*  *---- Sous-contractant -----*" &
  SKIP &
  TAB 001 "Diag.      Cmd  P dbt   Dbt P fin   Fin R emb  P dbt   Dbt P fin   fin R emb  P dbt   Dbt P fin   fin R emb  Gra Fi Épai Long Haut" &
  SKIP

FOOTING AT DIANUM002 &
  TAB 001 DIANUM002 OF PDPRIORITE_DIAG &
  TAB 010 DIAQTECMD OF PDDIAGRAMME &
  TAB 017 PDGQTEPLACPEGRA OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 023 PDGQTECPEPRDGRA OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 029 PDGQTEPLAFINGRA OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 035 PDGQTEFINPRDGRA OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 041 D_RESTE_GRA SUBTOTAL PICTURE "^^^^^" &
  TAB 048 PDGQTEPLACPEUSC OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 055 PDGQTECPERCUUSC OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 060 PDGQTEPLAFINUSC OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 066 PDGQTEFINRCUUSC OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 072 D_RESTE_USC SUBTOTAL PICTURE "^^^^^" &
  TAB 079 PDGQTEPLACPESCT OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 085 PDGQTECPERCUSCT OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 091 PDGQTEPLAFINSCT OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 097 PDGQTEFINRCUSCT OF PDPRIORITE_DIAG SUBTOTAL &
  TAB 103 D_RESTE_SCT SUBTOTAL PICTURE "^^^^^" &
  TAB 110 TGRCODGRA OF PDDIAGRAMME &
  TAB 114 TYFCODFIN OF PDDIAGRAMME &
  TAB 117 DIAEPA OF PDDIAGRAMME &
  TAB 122 DIALON OF PDDIAGRAMME &
  TAB 127 DIAHAU OF PDDIAGRAMME

FOOTING AT COMNUMCOM &
  SKIP 3 &
  TAB 001 "                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*" &
  SKIP &
  TAB 001 "                  Planifier        Fini Reste embal    Planifier        Fini Reste embal    Planifier        Fini Reste embal" &
  SKIP &
  TAB 001 "Total M2:" &
  TAB 017 D_SUR_PLA_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 029 D_SUR_FIN_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 041 D_SUR_RES_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 054 D_SUR_PLA_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 066 D_SUR_FIN_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 078 D_SUR_RES_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 091 D_SUR_PLA_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 103 D_SUR_FIN_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 115 D_SUR_RES_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6


FINAL FOOTING &
  SKIP 3 &
  TAB 001 "                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*" &
  SKIP &
  TAB 001 "                  Planifier        Fini Reste embal    Planifier        Fini Reste embal    Planifier        Fini Reste embal" &
  SKIP &
  TAB 001 "Grand total M2:" &
  TAB 017 D_SUR_PLA_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 029 D_SUR_FIN_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 041 D_SUR_RES_GRA SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 054 D_SUR_PLA_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 066 D_SUR_FIN_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 078 D_SUR_RES_USC SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 091 D_SUR_PLA_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 103 D_SUR_FIN_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 &
  TAB 115 D_SUR_RES_SCT SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6


BUILD pd655z


@IF FORMAT

PD650                                                      GRANICOR INC.                                                  PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Num. commande.: XX-XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX       Tolérence
Priorité......: 99 (AA/MM/JJ)                                         Épaisseur en +: 9999   Épaisseur en -: 9999
                                                                      Dimension en +: 9999   Dimension en -: 9999
                                                                      Critères esthé: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

                *--------- Granicor --------*  *----- Usine secondaire ----*  *---- Sous-contractant -----*
Diag.      Cmd  P dbt   Dbt P fin   Fin R emb  P dbt   Dbt P fin   fin R emb  P dbt   Dbt P fin   fin R emb  Gra Fi Épai Long Haut
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999
XXXXXXX  99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  99999 99999 99999 99999 99999  999 99 9999 9999 9999


                *----------- Granicor ------------*  *------- Usine secondaire --------*  *------- Sous-contractant --------*
                  Planifier        Fini Reste embal    Planifier        Fini Reste embal    Planifier        Fini Reste embal
TOTAL M2 :      999999.9999 999999.9999 999999.9999  999999.9999 999999.9999 999999.9999  999999.9999 999999.9999 999999.9999


@ENDIF
