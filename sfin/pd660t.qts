
;/T/ Liste de débitage par commande interne
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-10
;
;    Description...: Ce rapport permet d'imprimer de débitage des diagrammes
;                    pour une commande interne.
;
;    Paramètres....: ...
;
;    Particularités: ...
;
;
;/M/ Programmeur...:Rémy Demers
;
;    Description...:ajout d'elements dans le subfile pour supprimer des liens
;                   dans le quiz
;-------------------------------------------------------------------------------

USE ussetqts NOLIST

SET LOCK RECORD UPDATE

RUN pd660t

REQUEST DONNE

ACCESS PDCOMMAN_INTERNE &


  LINK CIECLE OF PDCOMMAN_INTERNE,&
       PJTABR OF PDCOMMAN_INTERNE,&
       PJTANN OF PDCOMMAN_INTERNE,&
       COMNUMCOMINT OF PDCOMMAN_INTERNE,&
       "TR" &
    TO CIECLE,        &
       PJTABR,&
       PJTANN,&
       COMNUMCOMINT       ,&
       TPDTYPPRD OF PDDETAIL_C_I &

  LINK CIECLE           OF PDCOMMAN_INTERNE, &
       TGRCODGRA        OF PDDETAIL_C_I      &
    TO CIECLE,                               &
       TGRCODGRA        OF PDTYPE_GRANIT      &

  LINK CIECLE OF PDDETAIL_C_I, &
       COMNUMCOM OF PDDETAIL_C_I, &
       DCINUMLIG OF PDDETAIL_C_I &
   TO  CIECLE                      ,&
       COMNUMCOM                   ,&
       DCINUMLIG OF PDEMBAL_TRANCHE OPT &

  LINK CIECLE           OF PDEMBAL_TRANCHE ,&
       CAINUMCAI        OF PDEMBAL_TRANCHE  &
    TO CIECLE,                               &
       CAINUMCAI        OF PDLIVRE_CAISSON   OPTIONAL &

  LINK CIECLE           OF PDEMBAL_TRANCHE ,&
       TRANUMTRA002     OF PDEMBAL_TRANCHE  &
    TO CIECLE,                              &
       TRANUMTRA002     OF PDTRANCHE OPTIONAL


CHOOSE CIECLE    PARM , &
       COMNUMCOM PARM , &
       COMSTU "C" "P"

;SELECT PDCOMMAN_INTERNE IF COMSTU OF PDCOMMAN_INTERNE <> "T" AND &
;                           COMSTU OF PDCOMMAN_INTERNE <> "X" AND &
;                           COMSTU OF PDCOMMAN_INTERNE <> "A"

;SELECT IF TPDTYPPRD OF PDDETAIL_C_I = "TR"

SORT ON COMNUMCOM OF PDCOMMAN_INTERNE  &
     ON DCINUMLIG OF PDDETAIL_C_I      &
     ON CAINUMCAI OF PDEMBAL_TRANCHE

DEFINE D_CAINUMCAI CHAR  SIZE 9 = CAINUMCAI OF PDEMBAL_TRANCHE IF RECORD PDEMBAL_TRANCHE EXIST ELSE " "
DEFINE D_EXPNUMEXP INTE  SIZE 4 = EXPNUMEXP OF PDLIVRE_CAISSON IF RECORD PDLIVRE_CAISSON EXIST ELSE 0
DEFINE D_BLINUMBLI CHAR  SIZE 2 = BLINUMBLI OF PDLIVRE_CAISSON IF RECORD PDLIVRE_CAISSON EXIST ELSE "0"
DEFINE D_STAT      INTE  SIZE 4 = 1 IF TRASTU OF PDTRANCHE = "E"  AND D_EXPNUMEXP <> 0 ELSE 0
DEFINE D_QTE_EMB   FLOAT SIZE 8 = 0
DEFINE D_QTE_EXP   FLOAT SIZE 8 = 0
DEFINE D_EMB       INTE  SIZE 4 = 1 IF D_CAINUMCAI <> " " ELSE 0
DEFINE D_DIFF      FLOAT SIZE 8 = 0
DEFINE D_SURFACE   FLOAT SIZE 8 = EMTSURMC2VEN OF PDEMBAL_TRANCHE * 10.764262 IF DCIUNMCMD OF PDDETAIL_C_I = "P2" &
                                                                            AND D_CAINUMCAI <> " " ELSE  &
                                  EMTSURMC2VEN OF PDEMBAL_TRANCHE IF DCIUNMCMD OF PDDETAIL_C_I <> "P2" AND D_CAINUMCAI <> " " ELSE 0

DEFINE D_SURFACE_EXP FLOAT SIZE 8 = D_SURFACE IF D_STAT <> 0 ELSE 0 ;IF TRASTU OF PDTRANCHE = "E" ELSE 0
DEFINE D_SURF      FLOAT SIZE 8 = EMTSURMC2VEN OF PDEMBAL_TRANCHE * 10.764262 IF DCIUNMCMD OF PDDETAIL_C_I = "P2" &
                                                                            AND D_CAINUMCAI <> " " ELSE &
                                  EMTSURMC2VEN OF PDEMBAL_TRANCHE IF DCIUNMCMD OF PDDETAIL_C_I <> "P2" AND D_CAINUMCAI <> " " ELSE 0

DEFINE D_SURFSTAT FLOAT SIZE 8 = D_SURFACE_EXP

SUBFILE WPD660-TRANCHE KEEP INCLUDE &
  CIECLE          OF PDCOMMAN_INTERNE, &
  COMNUMCOM       OF PDCOMMAN_INTERNE, &
  DCINUMLIG       OF PDDETAIL_C_I, &
  PJTABR          OF PDCOMMAN_INTERNE ,&
  TRASTU          OF PDTRANCHE ,&
  COMNUMCOMINT    OF PDCOMMAN_INTERNE ,&
  COMNOM          OF PDCOMMAN_INTERNE ,&
  CLICLE          OF PDCOMMAN_INTERNE ,&
  CLICIECLE       OF PDCOMMAN_INTERNE ,&
  TGRDSCFRA001    OF PDTYPE_GRANIT,&
  TYFCODFIN       OF PDDETAIL_C_I,&
  DCIEPA          OF PDDETAIL_C_I,&
  DCIQTECMD       OF PDDETAIL_C_I,&
  DCIUNMCMD       OF PDDETAIL_C_I,&
  DCIQTEUNMPRD    OF PDDETAIL_C_I,&
  DCIQTEEXP       OF PDDETAIL_C_I,&
  D_CAINUMCAI                      ,&
  D_EXPNUMEXP                      ,&
  D_BLINUMBLI ,&
  D_STAT   ,&
  D_QTE_EMB ,&
  D_QTE_EXP ,&
  D_EMB ,&
  D_DIFF,&
  D_SURF,&
  D_SURFACE,&
  D_SURFACE_EXP,&
  D_SURFSTAT
;---------------------------------------------------------------------------
;
REQUEST QUANTITE

ACCESS *WPD660-TRANCHE

SORT ON COMNUMCOM   OF WPD660-TRANCHE &
     ON DCINUMLIG   OF WPD660-TRANCHE &
     ON D_CAINUMCAI OF WPD660-TRANCHE

TEMPORARY T_STAT    INTEGER SIZE 4
TEMPORARY T_SUB_EMB INTEGER SIZE 4
TEMPORARY T_SURFACE FLOAT SIZE 8
TEMPORARY T_SURFACE_EXP FLOAT SIZE 8


ITEM T_SURFACE_EXP SUBTOTAL D_SURFACE_EXP OF WPD660-TRANCHE RESET AT D_CAINUMCAI
ITEM T_SURFACE  SUBTOTAL D_SURFACE OF WPD660-TRANCHE RESET AT D_CAINUMCAI
ITEM T_STAT     SUBTOTAL D_STAT OF WPD660-TRANCHE RESET AT D_CAINUMCAI
ITEM T_SUB_EMB  SUBTOTAL D_EMB OF WPD660-TRANCHE RESET AT D_CAINUMCAI

OUTPUT WPD660-TRANCHE UPDATE
ITEM D_QTE_EMB   OF WPD660-TRANCHE FINAL T_SUB_EMB
ITEM D_QTE_EXP   OF WPD660-TRANCHE FINAL T_STAT
ITEM D_SURFACE   OF WPD660-TRANCHE FINAL T_SURFACE
ITEM D_SURFACE_EXP OF WPD660-TRANCHE FINAL T_SURFACE_EXP
;---------------------------------------------------------------------------
;
REQUEST DIFFERENCE

ACCESS *WPD660-TRANCHE

SORT ON COMNUMCOM   OF WPD660-TRANCHE &
     ON DCINUMLIG   OF WPD660-TRANCHE &
     ON D_CAINUMCAI OF WPD660-TRANCHE &
     ON D_QTE_EMB   OF WPD660-TRANCHE

TEMPORARY T_SURFACE     FLOAT SIZE 8
TEMPORARY T_QTE_EMB     FLOAT SIZE 8
TEMPORARY T_SURFACE_EXP FLOAT SIZE 8
TEMPORARY T_QTE_EXP     FLOAT SIZE 8

ITEM T_SURFACE     SUBTOTAL D_SURFACE     OF WPD660-TRANCHE AT D_CAINUMCAI RESET AT DCINUMLIG
ITEM T_QTE_EMB     SUBTOTAL D_QTE_EMB     OF WPD660-TRANCHE AT D_CAINUMCAI RESET AT DCINUMLIG
ITEM T_SURFACE_EXP SUBTOTAL D_SURFACE_EXP OF WPD660-TRANCHE AT D_CAINUMCAI RESET AT DCINUMLIG
ITEM T_QTE_EXP     SUBTOTAL D_QTE_EXP     OF WPD660-TRANCHE AT D_CAINUMCAI RESET AT DCINUMLIG

OUTPUT WPD660-TRANCHE UPDATE AT DCINUMLIG
ITEM D_SURF         OF WPD660-TRANCHE FINAL T_SURFACE
ITEM D_EMB          OF WPD660-TRANCHE FINAL T_QTE_EMB
ITEM D_SURFSTAT     OF WPD660-TRANCHE FINAL T_SURFACE_EXP
ITEM D_STAT         OF WPD660-TRANCHE FINAL T_QTE_EXP

BUILD pd660t
