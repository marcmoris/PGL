;/T/ Sommaire des transactions à l'équipement

USE ussetqzs NOLIST
SET FORMFEED

ACCESS IMIMM_BDG_CTB  &
  LINK CIECLE, IMMCLE TO &
       CIECLE, IMMCLE OF IMIMMOBILISATION &
  LINK CIECLE, UNACLE OF IMIMM_BDG_CTB TO &
       CIECLE, UNACLE OF MCUNITE_ADM  &
  LINK CPTCLE OF IMIMM_BDG_CTB TO &
       CPTCLE OF VCPT_DSC  &
  LINK NACCLE OF VCPT_DSC TO &
       NACCLE OF MCNATURE_CTB

CHOOSE CIECLE PARM, &
       IMMCLE PARM

DEFINE D_MNTTOT      FLOAT SIZE 8 = IBCMNTENG OF IMIMM_BDG_CTB + &
                                    IBCMNTREE OF IMIMM_BDG_CTB

DEFINE D_MNTENG  FLOAT   SIZE 8 = IBCMNTENG OF IMIMM_BDG_CTB
DEFINE D_MNTREE  FLOAT   SIZE 8 = IBCMNTREE OF IMIMM_BDG_CTB

; Pour faire revenu - dépense
DEFINE D_MNTREE2 FLOAT   SIZE 8 = IBCMNTREE OF IMIMM_BDG_CTB &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - IBCMNTREE OF IMIMM_BDG_CTB

DEFINE D_MNTENG2 FLOAT   SIZE 8 = IBCMNTENG OF IMIMM_BDG_CTB &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - IBCMNTENG OF IMIMM_BDG_CTB

DEFINE D_MNTTOT2 FLOAT   SIZE 8 = D_MNTTOT                   &
                               IF "C" = CPTCODDC             &
                             ELSE 0 - D_MNTTOT


DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &   ; titre1  du rapport
                       "Sommaire des transactions à l'équipement"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "          ; titre2  du rapport
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "GP304"       ; numéro du rapport
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

SORT ON IMMCLE &
     ON NACCLE &
     ON CPTCLE &
     ON UNACLE

USE ustitstd NOLIST
  KEEP

HEADING AT IMMCLE &
  TAB 001 "Équipement :" &
  TAB 013 IMMCLE   OF IMIMM_BDG_CTB &
  TAB 025 IMMDSC   OF IMIMMOBILISATION &
SKIP 3

REPORT &
  TAB 005 CPTCLE      OF IMIMM_BDG_CTB HEAD "Compte"      &
  TAB 012 CPTDSCABR   OF VCPT_DSC      HEAD "Description" &
  TAB 033 UNACLE      OF IMIMM_BDG_CTB HEAD "Unite^Adm."  &
  TAB 042 UNANOMABR   OF MCUNITE_ADM   HEAD "Description" &
  TAB 063 D_MNTENG PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" HEAD "Engagement" &
  TAB 080 D_MNTREE PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" HEAD "Réel" &
  TAB 097 D_MNTTOT PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 HEAD "Total"

FOOT AT NACCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
SKIP &
  TAB 015 "Total" NACDSCFRA OF MCNATURE_CTB &
  TAB 063 D_MNTENG SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D_MNTREE SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D_MNTTOT SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP 2



FOOT AT IMMCLE &
  TAB 063 "---------------" &
  TAB 080 "---------------" &
  TAB 097 "---------------" &
SKIP &
  TAB 015 "Grand total Équipement:" &
  TAB 063 D_MNTENG SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 080 D_MNTREE SUB PICTURE " ^^^,^^^,^^^.^^ " SIGNIF 5 LEAD "<" TRAIL ">" &
  TAB 097 D_MNTTOT SUB PICTURE " ^^^,^^^,^^^.^^ " LEAD "<" TRAIL ">" SIGNIF 5 &
SKIP PAGE

BUILD gp304z
