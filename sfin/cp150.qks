;/T/ Termes de paiement
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 18-Mar-1992
;
;    Description..: Cet écran permet la gestion des termes de paiement.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp150  ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEHLD, &
                        T_CIENOMHLD

USE ussectmp  NOLIST
    "CP150"

USE cp150.hlp NOLIST

USE usactecr  NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-Ecrans"
  MENUITEM LABEL "Test de date" ACTION DESIGNER D001
  MENUITEM LABEL "Vérification" ACTION DESIGNER D002
@ELSE
ACTIONMENU LABEL "Subscreens"
  MENUITEM LABEL "Date test   " ACTION DESIGNER D001
  MENUITEM LABEL "Verification" ACTION DESIGNER D002
@ENDIF

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEHLD CHARACTER SIZE 4  ; Numéro de cie du holding
TEMPORARY T_CIENOMHLD CHARACTER SIZE 40 ; Nom du holding

;-------------------------------------------------------------------------------

FILE CPTERME  PRIMARY &
              NOITEM

  ITEM TERDATCRE OF CPTERME INITIAL SYSDATE
  ITEM TERHRECRE OF CPTERME INITIAL SYSTIME / 10000
  ITEM TERUSRCRE OF CPTERME INITIAL LOGONID
  ITEM TERDATMOD OF CPTERME FINAL   SYSDATE        &
                                            IF NOT NEWRECORD OF CPTERME AND &
                                               ALTEREDRECORD OF CPTERME
  ITEM TERHREMOD OF CPTERME FINAL   SYSTIME / 10000 &
                                            IF NOT NEWRECORD OF CPTERME AND &
                                               ALTEREDRECORD OF CPTERME
  ITEM TERUSRMOD OF CPTERME FINAL   LOGONID         &
                                            IF NOT NEWRECORD OF CPTERME AND &
                                               ALTEREDRECORD OF CPTERME
;-------------------------------------------------------------------------------
;
; Recherche de la description de la catégorie du terme et de son type de
; calcul.
;
FILE VCBI_DSC REFERENCE  ALIAS VCBI_DSC_CAT
FILE VCBI_DSC REFERENCE  ALIAS VCBI_DSC_CAL

;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_TERCAT    CHARACTER SIZE 16 = "TERCAT"
DEFINE    D_TERTYPCAL CHARACTER SIZE 16 = "TERTYPCAL"

TEMPORARY T_TERCAT    CHARACTER SIZE 4
TEMPORARY T_TERTYPCAL CHARACTER SIZE 4

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEHLD
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOMHLD )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Termes de paiement " &
@ELSE
      " Payment Terms " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

;-------------------------------------------------------------------------------

SKIP 1

ALIGN(3,3,19) (30,30,46) (,,51)

FIELD TERCLE OF CPTERME &
      HIDDEN ID 05 &
      REQUIRED NOCHANGE &
      LOOKUP NOTON CPTERME &
        VIA   TERCLE &
        USING TERCLE OF CPTERME &
        MESSAGE 408 ; ce terme existe deja

FIELD TERCAT OF CPTERME &
      HIDDEN ID SAME &
      REQUIRED &
      LOOKUP ON VCBI_DSC_CAT &
        VIA   XELNOM, CBICLE &
        USING D_TERCAT, TERCAT OF CPTERME &
        MESSAGE 409 ; cette categorie n'existe pas

FIELD CBIDSC OF VCBI_DSC_CAT &
      DISPLAY

TITLE &
@IF FRANCAIS
      "Description francaise" &
@ELSE
      "French Description " &
@ENDIF
      AT 7,3

ALIGN(3,3,19)

FIELD TERDSCFRA OF CPTERME &
      HIDDEN ID 15 &
      REQUIRED

FIELD TERDSCABRFRA OF CPTERME &
      HIDDEN ID SAME &
      DEFAULT TERDSCFRA OF CPTERME

TITLE &
@IF FRANCAIS
      "Description anglaise" &
@ELSE
      "English Description " &
@ENDIF
      AT 11,3

FIELD TERDSCANG OF CPTERME &
      HIDDEN ID 20 &
      DEFAULT TERDSCFRA OF CPTERME

FIELD TERDSCABRANG OF CPTERME &
      HIDDEN ID SAME &
      DEFAULT TERDSCANG OF CPTERME

SKIP 1

ALIGN(3,3,19) (,,22)

FIELD TERTYPCAL OF CPTERME &
      HIDDEN ID 25 &
      REQUIRED NOCHANGE &
      IF NOT MATCHPATTERN(TRUNC(TERCAT OF CPTERME), "ES(A|M)") &
      LOOKUP ON VCBI_DSC_CAL &
        VIA   XELNOM, CBICLE &
        USING D_TERTYPCAL, TERTYPCAL OF CPTERME &
        MESSAGE 410 ; ce type de calcul n'existe pas

FIELD CBIDSC OF VCBI_DSC_CAL &
      DISPLAY

SKIP 1

ALIGN(3,3,19) (,30,46) (,56,72)

FIELD TERNBRJRSAJT OF CPTERME &
      IF NOT MATCHPATTERN(TRUNC(TERCAT OF CPTERME), "ES(A|M)") &
      HIDDEN ID 30

FIELD TERNBRMOI OF CPTERME &
      IF NOT MATCHPATTERN(TRUNC(TERCAT OF CPTERME), "ES(A|M)") &
      HIDDEN ID SAME

FIELD TERNBRJRSDEL OF CPTERME &
      IF NOT MATCHPATTERN(TRUNC(TERCAT OF CPTERME), "ES(A|M)") &
      HIDDEN ID SAME

SKIP 1

ALIGN(3,3,19)

FIELD TERPCT OF CPTERME &
      HIDDEN ID 35 &
      REQUIRED &
      IF MATCHPATTERN(TRUNC(TERCAT OF CPTERME), "ES(A|C)")
;      IF TERCAT OF CPTERME = "ESC" OR TERCAT OF CPTERME = "ESA"

SKIP 1

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
; Popup sur les catégories disponibles.
;
PROCEDURE INPUT TERCAT
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TERCAT = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TERCAT, T_TERCAT
    LET FIELDTEXT = TRUNC(T_TERCAT)
  END
END

;-------------------------------------------------------------------------------
; Popup sur les types de calculs disponibles
;
PROCEDURE INPUT TERTYPCAL
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TERTYPCAL = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc102 MODE F PASSING D_TERTYPCAL, T_TERTYPCAL
    LET FIELDTEXT = TRUNC(T_TERTYPCAL)
  END
END

;-------------------------------------------------------------------------------
; On ne peut préciser un pourcentage que pour le terme d'escompte
;
PROCEDURE DESIGNER 35
BEGIN
  IF TERCAT OF CPTERME <> "NET" AND TERCAT OF CPTERME <> "ESM"
  THEN ACCEPT TERPCT OF CPTERME
END

;-------------------------------------------------------------------------------
; Appel de l'écran de test de dates
;
PROCEDURE DESIGNER D001 NODATA
BEGIN
  RUN SCREEN cp150a
END

;-------------------------------------------------------------------------------
; Appel de l'écran de vérification
;
PROCEDURE DESIGNER D002
BEGIN
  RUN SCREEN cp150b MODE F PASSING CPTERME
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF DELETEDRECORD OF CPTERME &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification
  ;
  IF     ALTEREDRECORD     OF CPTERME &
     AND NOT NEWRECORD     OF CPTERME &
     AND NOT DELETEDRECORD OF CPTERME &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction d'un terme de paiement.
  ;
  IF DELETEDRECORD OF CPTERME
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si le terme est référé.
    ;
    RUN SCREEN cp150x MODE SAME &
                      PASSING CPTERME , &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Termes de paiement ******************************
*                                                                              *
* Code Terme....: xxxx       Catégorie.....: xxxx xxxxxxxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
* Description francaise                                                        *
*   Longue......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   Courte......: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Description anglaise                                                         *
*   Longue......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*   Courte......: xxxxxxxxxxxxxxxxxxxx                                         *
*                                                                              *
* Type calcul...: xx xxxxxxxxxxxxxxxxxxxxxxxxx                                 *
*                                                                              *
* Nbr.Jrs Ajust.: xxxx       Nbr. Mois.....: xxxx      Nbr.Jrs. Délai: xxxx    *
*                                                                              *
* Pourcentage...: xxxxxx                                                       *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
