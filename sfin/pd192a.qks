;/P/ Programmeur..: René Bonneau
;    Date Création: 1994-10-20
;
;    Description..: Cette écran permet de faire la gestion des produits qui
;                   sortent de l'usine sans utiliser le processus normal.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd192a ACTIONBAR                    &
              FROM 11,01 TO 23,80          &
              MODE LABEL "" AT 2,80        &
              NOACTION                     &
              FIELDMARK RETAIN STARTUP     &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING PDSORTIE_PRODUIT,  &
                        T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD192A"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;

USE pd192a.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;

USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Clés de recherche pour les popup de consultations et les sous écrans
;

TEMPORARY T_BLONUMGRA001APR  CHARACTER SIZE 04
TEMPORARY T_BLONUMGRA002APR  CHARACTER SIZE 05
TEMPORARY T_BLORECAPR        CHARACTER SIZE 01
TEMPORARY T_CHAMP            INTEGER UNSIGNED SIZE 01
TEMPORARY T_COD              INTEGER UNSIGNED SIZE 02
TEMPORARY T_STU              CHARACTER SIZE 1
TEMPORARY T_CIECLEMNU        CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Sortie de produit
;

FILE PDSORTIE_PRODUIT MASTER


;-------------------------------------------------------------------------------
;
; Sortie de blocs
;

FILE PDSORTIE_BLOCS   PRIMARY OCCURS 6                 &
                      NOITEM

  ACCESS VIA     CIECLE,                               &
                 SPRNUMSOR                             &
         USING   T_CIECLEMNU,                          &
                 SPRNUMSOR        OF PDSORTIE_PRODUIT  &
         ORDERBY CIECLE,                               &
                 BLONUMGRA001APR,                      &
                 BLONUMGRA002APR,                      &
                 BLORECAPR

  ACCESS VIA     CIECLE                                &
         USING   T_CIECLEMNU                           &
         ORDERBY CIECLE,BLONUMGRA001APR,               &
                 BLONUMGRA002APR,                      &
                 BLORECAPR


  ITEM CIECLE    OF PDSORTIE_BLOCS INITIAL T_CIECLEMNU
  ITEM SPRNUMSOR OF PDSORTIE_BLOCS FINAL FIRST(SPRNUMSOR OF PDSORTIE_PRODUIT)


;-------------------------------------------------------------------------------
;
; Blocs
;

FILE PDBLOC           REFERENCE OCCURS WITH PDSORTIE_BLOCS
  ACCESS VIA    CIECLE,                                &
                BLONUMGRA001APR,                       &
                BLONUMGRA002APR,                       &
                BLORECAPR                              &
         USING  T_CIECLEMNU,                           &
                BLONUMGRA001APR  OF PDSORTIE_BLOCS,    &
                BLONUMGRA002APR  OF PDSORTIE_BLOCS,    &
                BLORECAPR        OF PDSORTIE_BLOCS


;-------------------------------------------------------------------------------
;
; Blocs pour mise a jour
;

FILE PDBLOC           DESIGNER ALIAS PDBLOC_MAJ

FILE PDSEQ_SORTIE     DESIGNER

;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT    REFERENCE OCCURS WITH PDSORTIE_BLOCS
  ACCESS VIA    CIECLE,                            &
                TGRCODGRA                          &
         USING  T_CIECLEMNU,                       &
                TGRCODGRA        OF PDBLOC


;-------------------------------------------------------------------------------
;
;
DRAW 3,1 TO 13,80    ; Remplace le Draw pour les écrans pleine page
;USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP 2

TITLE &
@IF FRANCAIS
      "  Blocs   " &
@ELSE
      " %%%Blocs " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP 1


TITLE "No. bloc     Type de granit" at , 08
TITLE "Volume" at , 66

SKIP


ALIGN (05,05,06) (,10,11) (,16,17) (,21,21) (,25,25) (,66,66)


CLUSTER OCCURS WITH PDSORTIE_BLOCS ID BASE 10


  FIELD BLONUMGRA001APR  OF PDSORTIE_BLOCS          &
          HIDDEN                                    &
          NUMERIC                                   &
          NOCHANGE                                  &
          LABEL " "


  FIELD BLONUMGRA002APR  OF PDSORTIE_BLOCS          &
          HIDDEN                                    &
          NUMERIC                                   &
          NOCHANGE                                  &
          LABEL "-"


  FIELD BLORECAPR        OF PDSORTIE_BLOCS          &
          HIDDEN                                    &
          LABEL "-"                                 &
          NOCHANGE                                  &
          LOOKUP ON PDBLOC                          &
            MESSAGE 2943 ; Ce numéro de bloc n'existe pas


  FIELD TGRCODGRA        OF PDBLOC                  &
          HIDDEN                                    &
          NUMERIC                                   &
          DISPLAY                                   &
          REFRESH                                   &
          LABEL " "


  FIELD TGRDSCFRA001     OF PDTYPE_GRANIT           &
          HIDDEN                                    &
          DISPLAY                                   &
          REFRESH                                   &
          LABEL " "


  FIELD BLOVOLMC3BRU     OF PDSORTIE_BLOCS          &
          HIDDEN                                    &
          DISPLAY                                   &
          REFRESH                                   &
          LABEL " "



CLUSTER



;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST

END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN

  USE ussecent NOLIST

  IF SPRCON OF PDSORTIE_PRODUIT = "O"
  THEN ERROR 3012 ; La saisie est impossible car la sortie est confirmée

END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA001APR
BEGIN

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLONUMGRA002APR  = ""
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 1
    LET T_COD              = 1
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F                     &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA001APR )
  END

END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLONUMGRA002APR
BEGIN

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLONUMGRA002APR  = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_BLORECAPR        = ""
    LET T_CHAMP            = 2
    LET T_COD              = 1
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F                     &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDBLOC = TRUNCATE( T_BLONUMGRA001APR )
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLONUMGRA002APR ) ) AND &
     0 =  SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLONUMGRA002APR )
  END

END


;-------------------------------------------------------------------------------
;
; Appel dépisteur No bloc (attemtion cet appel est particulier)
;

PROCEDURE INPUT BLORECAPR
BEGIN

  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_BLONUMGRA001APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLONUMGRA002APR  = BLONUMGRA001APR OF PDBLOC
    LET T_BLORECAPR        = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CHAMP            = 3
    LET T_COD              = 1
    LET T_STU              = ""
    RUN SCREEN pd121 MODE F                     &
                     PASSING T_BLONUMGRA001APR, &
                             T_BLONUMGRA002APR, &
                             T_BLORECAPR,       &
                             T_CHAMP,           &
                             T_COD,             &
                             T_STU,             &
                             T_CIECLEMNU
    LET BLONUMGRA001APR OF PDBLOC = TRUNCATE( T_BLONUMGRA001APR )
    LET BLONUMGRA002APR OF PDBLOC = TRUNCATE( T_BLONUMGRA002APR )
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
  IF 0 <> SIZE( TRUNCATE( T_BLORECAPR ) ) AND &
     0 = SIZE( TRUNCATE( FIELDTEXT) )
  THEN BEGIN
    LET FIELDTEXT                 = TRUNCATE( T_BLORECAPR )
  END
  IF 0 = SIZE( TRUNCATE(FIELDTEXT) )
  THEN LET FIELDTEXT = " "

END


;-------------------------------------------------------------------------------
;
; Vérification du statut du bloc
;

PROCEDURE PROCESS BLORECAPR
BEGIN

  GET PDBLOC_MAJ                                &
      VIA    CIECLE,                            &
             BLONUMGRA001APR,                   &
             BLONUMGRA002APR,                   &
             BLORECAPR                          &
      USING  T_CIECLEMNU,                       &
             BLONUMGRA001APR OF PDSORTIE_BLOCS, &
             BLONUMGRA002APR OF PDSORTIE_BLOCS, &
             BLORECAPR       OF PDSORTIE_BLOCS  OPTIONAL
  IF ACCESSOK AND &
     (BLOSTU OF PDBLOC_MAJ <> "I" AND &
      BLOSTU OF PDBLOC_MAJ <> "R")
  THEN ERROR 2992 ; Ce bloc a un statut différent de (I ou R)

  LET      BLOVOLMC3BRU  OF PDSORTIE_BLOCS = BLOVOLMC3BRU  OF PDBLOC
  LET      BLOVOLMC3NET  OF PDSORTIE_BLOCS = BLOVOLMC3NET  OF PDBLOC
  LET      BLOVOLMC3PYE  OF PDSORTIE_BLOCS = BLOVOLMC3PYE  OF PDBLOC
  DISPLAY  BLOVOLMC3BRU  OF PDSORTIE_BLOCS

END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  ;
  ; Empeche la destruction car la ssortie est confirmée
  ;
  IF SPRCON OF PDSORTIE_PRODUIT = "O" &
     AND  DELETEDRECORD OF PDSORTIE_BLOCS
  THEN ERROR 3013 ; La destruction est impossible, la sortie est confirmée

  IF 0 = SPRNUMSOR        OF PDSORTIE_PRODUIT
  THEN BEGIN
    GET PDSEQ_SORTIE OPTIONAL SEQUENTIAL
    LET CIECLE    OF PDSEQ_SORTIE       = T_CIECLEMNU
    LET SPRNUMSOR OF PDSEQ_SORTIE       = SPRNUMSOR OF PDSEQ_SORTIE + 1
    LET SPRNUMSOR OF PDSORTIE_PRODUIT   = SPRNUMSOR OF PDSEQ_SORTIE
    PUT PDSEQ_SORTIE
  END

  FOR PDSORTIE_BLOCS
  BEGIN

    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF PDSORTIE_BLOCS &
       AND TZ_SECDEL = "0"
    THEN ERROR 1

    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF PDSORTIE_BLOCS &
       AND NOT NEWRECORD     OF PDSORTIE_BLOCS &
       AND NOT DELETEDRECORD OF PDSORTIE_BLOCS &
       AND TZ_SECMOD = "0"
    THEN ERROR 2


    IF NEWRECORD         OF PDSORTIE_BLOCS &
     AND NOT DELETEDRECORD OF PDSORTIE_BLOCS
    THEN BEGIN
      ;
      ; Mise a jour du numéro de bloc et du statut de bloc
      ;
      LET BLONUMBLOAPR   OF PDSORTIE_BLOCS = BLONUMGRA001APR + &
                                             BLONUMGRA002APR + &
                                             BLORECAPR

      GET PDBLOC_MAJ                                &
          VIA    CIECLE,                            &
                 BLONUMGRA001APR,                   &
                 BLONUMGRA002APR,                   &
                 BLORECAPR                          &
          USING  T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDSORTIE_BLOCS, &
                 BLONUMGRA002APR OF PDSORTIE_BLOCS, &
                 BLORECAPR       OF PDSORTIE_BLOCS  OPTIONAL
      IF ACCESSOK ;AND (BLOSTU OF PDBLOC_MAJ = "I" &
                  ;  OR BLOSTU OF PDBLOC_MAJ = "R")
      THEN BEGIN
        LET BLOSTU      OF PDBLOC_MAJ = "T"  ; sortie
        PUT PDBLOC_MAJ
      END
    END

    IF DELETEDRECORD      OF PDSORTIE_BLOCS &
     AND NOT NEWRECORD OF PDSORTIE_BLOCS
    THEN BEGIN
      ;
      ; Remise en inventaire du numéro de bloc lors de la destruction
      ;
      GET PDBLOC_MAJ                                &
          VIA    CIECLE,                            &
                 BLONUMGRA001APR,                   &
                 BLONUMGRA002APR,                   &
                 BLORECAPR                          &
          USING  T_CIECLEMNU,                       &
                 BLONUMGRA001APR OF PDSORTIE_BLOCS, &
                 BLONUMGRA002APR OF PDSORTIE_BLOCS, &
                 BLORECAPR       OF PDSORTIE_BLOCS  OPTIONAL
      IF ACCESSOK ; AND BLOSTU OF PDBLOC_MAJ = "T"
      THEN LET BLOSTU      OF PDBLOC_MAJ = "I"  ; Remis en inventaire
      PUT PDBLOC_MAJ
    END

  END


END



BUILD

@IF FORMAT
***********************************  Blocs   ***********************************
*                                                                              *
*      No. bloc     Type de granit                               Volume        *
*    xxxxx-xxxxx-x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx  *
*    xxxxx-xxxxx-x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx  *
*    xxxxx-xxxxx-x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx  *
*    xxxxx-xxxxx-x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx  *
*    xxxxx-xxxxx-x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx  *
*    xxxxx-xxxxx-x  xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxx  *
*                                                                              *
********************************************************************************
@ENDIF
