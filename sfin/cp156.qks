;/T/ Maintenance des paramètres par compagnie
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 22 Juin 1993
;
;    Description..: La maintenance des paramètres sert lors de l'initialisation
;                   du module. Elle contient des informations d'ordre général
;                   spécifiques à chaque compagnie.
;                   SEUL UN USAGER AVERTI DOIT AVOIR ACCÈS À CET ÉCRAN.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp156 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU, &
                       T_CIENOM

USE ussectmp NOLIST
    "CP156"

USE cp156.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Numéro de cie du holding.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom du holding.

;-------------------------------------------------------------------------------

FILE CPCIE_PARAM      PRIMARY

  ACCESS  VIA   CIECLE &
          USING T_CIECLEMNU

  ITEM CIECLE OF CPCIE_PARAM INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Validation et recherche de la description du compte d'encaisse du fournisseur
;
FILE MCLIVRET_BQ REFERENCE

  ACCESS  VIA   CIECLE, &
                LBQCPTENC &
          USING CIECLE    OF CPCIE_PARAM, &
                LBQCPTENC OF CPCIE_PARAM

FILE VCPT_DSC REFERENCE &
              ALIAS A_VCPT_DSC_LBQCPTENC

  ACCESS  VIA   CPTCLE &
          USING LBQCPTENC OF MCLIVRET_BQ

;-------------------------------------------------------------------------------

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

TEMPORARY T_LBQCPTENC CHARACTER         SIZE 6 ; Compte d'encaisse

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST     ; Draw pour les écrans pleine page
USE ustitstd NOLIST   ; En-tête pour les écrans pleine page
USE ushilite NOLIST     ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Paramètres par compagnie " &
@ELSE
      " %%%Company parameters " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 2

ALIGN (3,3,19) (,,26)

FIELD LBQCPTENC OF CPCIE_PARAM &
      HIDDEN ID 05 &
      REQUIRED &
      LOOKUP &
        ON A_VCPT_DSC_LBQCPTENC &
          VIA   CPTCLE &
          USING LBQCPTENC OF CPCIE_PARAM &
          MESSAGE 411, & ; Ce compte est inexistant
        ON MCLIVRET_BQ &
          VIA   CIECLE, &
                LBQCPTENC &
          USING T_CIECLEMNU, &
                LBQCPTENC OF CPCIE_PARAM  &
          MESSAGE 569 ; Ce compte n'est pas un compte d'encaisse

FIELD CPTDSC OF A_VCPT_DSC_LBQCPTENC DISPLAY

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Popup sur les comptes d'encaisse disponibles
;
PROCEDURE INPUT LBQCPTENC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_LBQCPTENC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc110 PASSING T_CIECLEMNU, T_LBQCPTENC MODE F
    LET FIELDTEXT = TRUNCATE(T_LBQCPTENC)
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF CPCIE_PARAM &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF CPCIE_PARAM &
     AND NOT NEWRECORD     OF CPCIE_PARAM &
     AND NOT DELETEDRECORD OF CPCIE_PARAM &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
;
;xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
;*************************** Paramètres par compagnie ***************************
;*                                                                              *
;*                                                                              *
;* Cpt encaisse..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;*                                                                              *
;********************************************************************************

