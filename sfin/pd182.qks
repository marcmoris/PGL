;/T/ Enregistrer le redimensionnement de tranche.
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-09-27
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le redimensionnement des tranches de granit de Granicor.
;
;/M/ François Déry, 12 mai 1999
;       Ajout du in dict sur la transaction des séquences.
;
;/M/ Programmeur..: Nancy Breton
;    Date modif...: 1998-09-18
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd182 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             ACTIVITIES CHANGE,FIND,ENTRY &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM

TRANSACTION NO_SEQ READ WRITE &
                   RESERVING FOR PROTECTED WRITE PDSEQ_RED_TRAN IN DICT

;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD182"


;-------------------------------------------------------------------------------
;
; Documentation de l'écran.
;
USE pd182.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;@IF FRANCAIS
;ACTIONMENU LABEL "Sous-écrans"
;  MENUITEM LABEL "Redimentionnement               " ACTION DESIGNER D001
;  MENUITEM LABEL "Vérification                    " ACTION DESIGNER D002
;@ELSE
;ACTIONMENU LABEL "Subscreen"
;  MENUITEM LABEL "%%%Redimentionnement            " ACTION DESIGNER D001
;  MENUITEM LABEL "%%%Vérification                 " ACTION DESIGNER D002
;@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

TEMPORARY T_TRALON FLOAT SIZE 8
TEMPORARY T_TRAHAU FLOAT SIZE 8
TEMPORARY T_TRAEPA FLOAT SIZE 8
;-------------------------------------------------------------------------------
;
; Redimension de tranche
;

FILE PDREDIM_TRANCHE PRIMARY &
                     NOITEM
  ACCESS VIA     CIECLE,                      &
                 RDTNUMRED                    &
         USING   T_CIECLEMNU,                 &
                 RDTNUMRED OF PDREDIM_TRANCHE &
         REQUEST RDTNUMRED OF PDREDIM_TRANCHE &
         ORDERBY CIECLE,                      &
                 RDTNUMRED

  ACCESS VIA     CIECLE                  &
         USING   T_CIECLEMNU             &
         ORDERBY CIECLE,                 &
                 RDTNUMRED

     ITEM CIECLE OF PDREDIM_TRANCHE INITIAL T_CIECLEMNU
     ITEM RDTRALON OF PDREDIM_TRANCHE FINAL T_TRALON
     ITEM RDTRAHAU OF PDREDIM_TRANCHE FINAL T_TRAHAU
     ITEM RDTRAEPA OF PDREDIM_TRANCHE FINAL T_TRAEPA
     ITEM TRANUMTRA001 OF PDREDIM_TRANCHE FINAL " "

;-------------------------------------------------------------------------------
;
; Tranche
;
FILE PDTRANCHE SECONDARY &
               NOITEM

  ACCESS VIA     CIECLE,                              &
                 TRANUMTRA002                         &
         USING   T_CIECLEMNU,                         &
                 TRANUMTRA002     OF PDREDIM_TRANCHE  &
         ORDERBY CIECLE,                              &
                 TRANUMTRA001,                        &
                 TRANUMTRA002

         ITEM CIECLE OF PDTRANCHE INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Numéro séquentiel pour les bons de commande
;

FILE PDSEQ_RED_TRAN   DESIGNER &
                      TRANSACTION NO_SEQ


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement

DEFINE D_M2_BRU FLOAT SIZE 8 =  ROUND((TRALON OF PDTRANCHE * TRAHAU OF PDTRANCHE), -2 ) / 100


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;
USE usvarvol NOLIST


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Redimensionnement " &
@ELSE
      " %%%Redimensionnement " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (,3,19)


FIELD RDTNUMRED        OF PDREDIM_TRANCHE  &
label "No redimension:"&
        HIDDEN ID 05                       &
        DISPLAY                            &
        REFRESH


FIELD RDTDATRED        OF PDREDIM_TRANCHE   FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date redimen..:"&
        HIDDEN ID 10                       &
        DEFAULT SYSDATE

FIELD RDTDSC001        OF PDREDIM_TRANCHE  &
label "Description...:"&
        HIDDEN ID 15

FIELD RDTDSC002        OF PDREDIM_TRANCHE  &
        ID SAME                            &
        NOLABEL

FIELD RDTDSC003        OF PDREDIM_TRANCHE  &
        ID SAME                            &
        NOLABEL

SKIP TO LINE 11

TITLE &
@IF FRANCAIS
      " Tranche redimensioné " &
@ELSE
      " %%%Tranche redimesioné " &
@ENDIF
      CENTERED AT ,1

DRAW 11,2 TO 11,79

SKIP TO LINE 13

ALIGN (2,3,19)


FIELD TRANUMTRA002     OF PDREDIM_TRANCHE  &
label "No. tranche...:"&
        HIDDEN ID 20                       &
        NUMERIC                            &
        REQUIRED                           &
        LOOKUP ON PDTRANCHE                &
          VIA     CIECLE,                  &
                  TRANUMTRA002             &
          USING   T_CIECLEMNU,             &
                  TRANUMTRA002     OF PDREDIM_TRANCHE &
          MESSAGE 2940 , & ; Ce numéro de tranche n'existe pas
                NOTON PDTRANCHE            &
          VIA     CIECLE,                  &
                  TRANUMTRA001             &
          USING   T_CIECLEMNU,                        &
                  TRANUMTRA002     OF PDREDIM_TRANCHE &
          MESSAGE 2960 ; Cette tranche est déjà redimensionner


FIELD TRASTU           OF PDTRANCHE        &
label "Statut........:"&
        HIDDEN ID 25                       &
        DISPLAY                            &
        REFRESH


ALIGN (2,3,19)

FIELD TRALON  OF PDTRANCHE &
        LABEL "Longueur......:" &
        HIDDEN ID 30

FIELD TRAHAU OF PDTRANCHE &
        LABEL "Hauteur.......:" &
        HIDDEN ID 35

FIELD TRAEPA OF PDTRANCHE &
        LABEL "Épaisseur.....:" &
        HIDDEN ID 40

FIELD D_M2_BRU                                &
        HIDDEN ID 45                          &
        DISPLAY                               &
        LABEL "Total m2......:"               &
        SIGNIFICANCE 6                        &
        PICTURE "^^.^^^^"

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END





;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDREDIM_TRANCHE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDREDIM_TRANCHE &
     AND NOT NEWRECORD     OF PDREDIM_TRANCHE &
     AND NOT DELETEDRECORD OF PDREDIM_TRANCHE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;-----------------------------------------------------------------------------
  ;
  ; Incrémentation du numéro de séquence
  ;
  IF 0 = RDTNUMRED        OF PDREDIM_TRANCHE
  THEN BEGIN
    START TRANSACTION NO_SEQ
    GET PDSEQ_RED_TRAN OPTIONAL &
      VIA   CIECLE                      &
      USING T_CIECLEMNU
    IF NOT ACCESSOK
    THEN BEGIN
      LET CIECLE OF PDSEQ_RED_TRAN = T_CIECLEMNU
    END
    LET RTQNUM    OF PDSEQ_RED_TRAN  = RTQNUM OF PDSEQ_RED_TRAN + 1
    LET RDTNUMRED OF PDREDIM_TRANCHE = RTQNUM OF PDSEQ_RED_TRAN
    PUT PDSEQ_RED_TRAN
    DISPLAY RDTNUMRED OF PDREDIM_TRANCHE
  END
INFO = ASCII(D_M2_BRU)NOW RESP
LET TRASURMC2 OF PDTRANCHE = D_M2_BRU / 10000
END


;-------------------------------------------------------------------------------
;
; Redimension
;

;PROCEDURE DESIGNER D001
;BEGIN
;  RUN SCREEN  pd182a &
;              PASSING T_CIECLEMNU,        &
;                      T_CIENOM,           &
;                      PDREDIM_TRANCHE,    &
;                      PDTRANCHE           &
;              MODE SAME
;END


;-------------------------------------------------------------------------------
;
; Vérification
;
;PROCEDURE DESIGNER D002
;BEGIN
;  RUN SCREEN  pd182b &
;              PASSING T_CIECLEMNU,       &
;                      T_CIENOM,          &
;                      PDTRANCHE          &
;              MODE F
;END
;-----------------------------------------------------------------------
;
PROCEDURE FIND
BEGIN
  IF PATH = 1
  THEN GET PDREDIM_TRANCHE VIA CIECLE , RDTNUMRED ORDERBY CIECLE &
   , RDTNUMRED USING T_CIECLEMNU , RDTNUMRED OF &
   PDREDIM_TRANCHE
  IF PATH = 2
  THEN GET PDREDIM_TRANCHE VIA CIECLE ORDERBY CIECLE , RDTNUMRED &
  USING T_CIECLEMNU
  GET PDTRANCHE OPTIONAL
END

;-----------------------------------------------------------------------
PROCEDURE ENTRY
BEGIN
  DISPLAY RDTNUMRED OF PDREDIM_TRANCHE
  ACCEPT RDTDATRED OF PDREDIM_TRANCHE
  ACCEPT RDTDSC001 OF PDREDIM_TRANCHE
  ACCEPT RDTDSC002 OF PDREDIM_TRANCHE
  ACCEPT RDTDSC003 OF PDREDIM_TRANCHE
  ACCEPT TRANUMTRA002 OF PDREDIM_TRANCHE
  GET PDTRANCHE   OPT     &
    VIA     CIECLE,       &
            TRANUMTRA002  &
    USING   T_CIECLEMNU,  &
            TRANUMTRA002  OF PDREDIM_TRANCHE
  IF ACCESSOK
  THEN BEGIN
        LET T_TRALON = TRALON OF PDTRANCHE
        LET T_TRAHAU = TRAHAU OF PDTRANCHE
        LET T_TRAEPA = TRAEPA OF PDTRANCHE
        DISPLAY TRALON OF PDTRANCHE
        DISPLAY TRAHAU OF PDTRANCHE
        DISPLAY TRAEPA OF PDTRANCHE
  END
  DISPLAY TRASTU OF PDTRANCHE
  ACCEPT TRALON OF PDTRANCHE
  ACCEPT TRAHAU OF PDTRANCHE
  ACCEPT TRAEPA OF PDTRANCHE
  DISPLAY D_M2_BRU
END

BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Redimesionnement *******************************
*                                                                              *
* No redimension: xxxxxxxxxx                                                   *
* Date redimen..: xxxxxxxx                                                     *
* Description...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
********************************************************************************
*                                                                              *
* No. tranche...: xxxxxxxxx                                                    *
* Statut........: x                                                            *
* Largeur.......: xxxxx                                                        *
* Longueur......: xxxxx                                                        *
* Épaisseur.....: xxxxx                                                        *
* Total m2 brut.: xxxxxxx                                                      *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
