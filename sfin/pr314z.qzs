;/Z/ Cumulatif projet

USE ussetqzs NOLIST
SET FORMFEED NOBLANKS
SET NOHEAD

ACCESS  *WPR314 &
        LINK CIECLE,PROCLE TO CIECLE,PROCLE OF GPPROJET &
        LINK FORCLE TO FORCLE OF GPFORMAT


DEFINE DZ_NOMRAP1 CHAR SIZE 60 = "                      CUMULATIF PROJET                      "
DEFINE DZ_NOMRAP2 CHAR SIZE 60 = " "
DEFINE DZ_NuMRAP CHAR SIZE 12 = "PR314"
DEFINE DZ_CIENOM CHAR SIZE 40 = PARM

;DEFINE D_MNT FLOAT SIZE 8 = (V_MNTCUM1 OF VPRO01 + V_MNTCUM2 OF VPRO01)
DEFINE D_MNT1 CHAR SIZE 15 = "$" + ASCII(ABS(TOTAL_MNT)/100) + "." + &
                             ASCII(MOD(ABS(TOTAL_MNT),100),2)
DEFINE D_MNT2 CHAR SIZE 18 = D_MNT1 IF 100000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:2] + "," + D_MNT1[3:13] &
                                  IF 1000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:3] + "," + D_MNT1[4:12] &
                                  IF 10000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:4] + "," + D_MNT1[5:11] &
                                  IF 100000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:2] + "," + D_MNT1[3:3] + "," + D_MNT1[6:10] &
                                  IF 1000000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:3] + "," + D_MNT1[4:3] + "," + D_MNT1[7:9] &
                                  IF 10000000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:4] + "," + D_MNT1[5:3] + "," + D_MNT1[8:8] &
                                  IF 100000000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:2] + "," + D_MNT1[3:3] + "," + D_MNT1[6:3] + &
                "," + D_MNT1[9:7] IF 1000000000000 > ABS(TOTAL_MNT) &
              ELSE D_MNT1[1:3] + "," + D_MNT1[4:3] + "," + D_MNT1[7:3] + &
                "," + D_MNT1[10:6]

DEFINE D_MNT3 CHAR SIZE 20 = "--------------------" IF FODTYP = "S" &
              ELSE "" IF FODTYP = "E" &
              ELSE RJ(PACK("<" + D_MNT2 + ">")) IF TOTAL_MNT < 0 &
              ELSE RJ(D_MNT2)

DEFINE D_ORDRE INT SIZE 2 = 0 IF TOTAL_MNT < 0 ELSE 1


SORT ON CIECLE,PROCLE,FORCLE,FODLIG,D_ORDRE

USE ustitstd NOLIST
KEEP

HEADING AT PROCLE &
SKIP 3 &
TAB 25 "PROJET:" &
TAB 33 PROCLE &
TAB 53  PRODSC OF GPPROJET &
SKIP &
TAB 53 PRODSC2 OF GPPROJET &
SKIP &
TAB 53 PRODSC3 OF GPPROJET &
SKIP 2 &
TAB 25 "FORMAT:" &
TAB 33 PROFRM1 OF GPPROJET &
TAB 53 FORDSC OF GPFORMAT &
SKIP 3 &
TAB 25 "DESCRIPTION:" &
TAB 87 "MONTANT:"

REPORT &
TAB 25 FODDSC  &
TAB 80 D_MNT3 HEAD " "

BUILD pr314z
