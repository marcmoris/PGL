;/T/ Gestion des projets
;
;/P/ Programmeur..: Steeve Duguay
;    Réalisé le...: 19 février 1995
;
;    Description..: Fiche sous-projet - Information comptable détaillé (engagé)
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gp002i ACTIONBAR &
              MODE LABEL "" AT 2,132 &
              NOACTION FIELDMARK RETAIN STARTUP &
              FROM 1,1 TO 23,132 &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM   , &
                        GPPRA_BDG_CTB, &
                        GPPRO_ACTIVITE, &
                        GPPROJETS

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; Compagnie du menu
TEMPORARY T_PECCLEMNU CHARACTER SIZE 04 ; Période du menu
TEMPORARY T_FOUCIECLE CHARACTER SIZE 04 ; Compagnie fournisseur
TEMPORARY T_CMDCLEFND CHARACTER SIZE  9 ; Appel de la commande.
TEMPORARY T_CMDAJTFND CHARACTER SIZE  3 ; Appel de la commande
TEMPORARY T_RECCLEFND CHARACTER SIZE 12 ; Numéro de réception pour référer pièce

USE ussectmp NOLIST     ; Variable pour la securite
    "GP002I"            ; Nom de l'écran

USE gp002i.hlp NOLIST   ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;----------------------------
; Fichier budget comptable
;----------------------------
FILE GPPRA_BDG_CTB          MASTER


;------------------------------------
; Fichier fiche de sous-projet
;------------------------------------
FILE GPPRO_ACTIVITE         MASTER


;------------------------------------
; Fichier fiche de projet
;------------------------------------
FILE GPPROJETS              MASTER


;--------------------------------
; Fichier budget comptable engagé
;--------------------------------
FILE VHPE_01                PRIMARY &
                            OCCURS 13 &
                            NOITEMS

  ACCESS  VIA     CPTCLE, &
                  CIECLE, &
                  UNACLE, &
                  PRJCLE, &
                  PRACLE  &
          USING   CPTCLE OF GPPRA_BDG_CTB,  &
                  CIECLE OF GPPRA_BDG_CTB,  &
                  UNACLE OF GPPRA_BDG_CTB,  &
                  PRJCLE OF GPPRA_BDG_CTB,  &
                  PRACLE OF GPPRA_BDG_CTB   &
          ORDERBY PECCLE


;-------------------------------
; Fichier compte / client
;-------------------------------
FILE MCCOMPTE               REFERENCE

  ACCESS  VIA   CPTCLE  &
          USING CPTCLE OF GPPRA_BDG_CTB


;----------------------------
; Fichier pour le fournisseur
;----------------------------
FILE VFOC_FOU REFERENCE

  ACCESS VIA   FOUCIECLE, &
               FOUCLE,    &
               FOCCIECLE  &
         USING REFCIECLE OF VHPE_01,     &
               REFCLENUM OF VHPE_01,     &
               HENCIECLE OF VHPE_01

;-------------------------------------------------------------------------------
;

DEFINE D_MNTTOT      FLOAT SIZE 8 = PACMNTENG OF GPPRA_BDG_CTB + &
                                    PACMNTREE OF GPPRA_BDG_CTB

DEFINE D_MNTBDG      FLOAT SIZE 8 = PACMNTBDG    OF GPPRA_BDG_CTB        &
                                 IF PRAPACDATAPP OF GPPRO_ACTIVITE = 0   &
                               ELSE PACMNTBDGREV OF GPPRA_BDG_CTB

DEFINE D_MNTDIS      FLOAT SIZE 8 = D_MNTBDG - D_MNTTOT

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

TEMPORARY T_CIECLE     CHAR*4
TEMPORARY T_PRJCLE     CHAR*8
TEMPORARY T_CPTCLE     CHAR*6
TEMPORARY T_UNACLE     CHAR*8

USE usdrw132 NOLIST
USE ustit132 NOLIST
USE ushilite NOLIST

DRAW 8,1 TO 8,132

SKIP TO 3

TITLE &
@IF FRANCAIS
      " Information comptable détaillée " &
@ELSE
      " %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP

ALIGN (,3,19) (,,28) (,92,103) (,,114)
FIELD PRJCLE    OF GPPROJETS                          &
label "Projet.....:" &
      DISPLAY

FIELD PRJDSC1   OF GPPROJETS                          &
      DISPLAY

FIELD PRJPBCDATAPP OF GPPROJETS                        FORMAT YYYYMMDD &
PATTERN "####/##/##"&
      LABEL "Date app.:"                              &
      DISPLAY

FIELD PRJPBCUSRAPP OF GPPROJETS                       &
      DISPLAY

SKIP 1


TITLE &
@IF FRANCAIS
      "Compte Descrption            Unité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Engagé" &
@ELSE
      " %%% " &
@ENDIF
      AT ,50

TITLE &
@IF FRANCAIS
      "Réel" &
@ELSE
      " %%% " &
@ENDIF
      AT ,70

TITLE &
@IF FRANCAIS
      "Total" &
@ELSE
      " %%% " &
@ENDIF
      AT ,87

TITLE &
@IF FRANCAIS
      "Budget" &
@ELSE
      " %%% " &
@ENDIF
      AT ,104

TITLE &
@IF FRANCAIS
      "Disponibilité" &
@ELSE
      " %%% " &
@ENDIF
      AT ,115


SKIP

ALIGN (3,2,3) (,,10) (,,32) (,,42) (,,60) (,,78) (,,96) (,,114)

FIELD CPTCLE OF GPPRA_BDG_CTB                         &
      LABEL " "                                       &
      HIDDEN                                          &
      DISPLAY

FIELD CPTDSCABRFRA OF MCCOMPTE                        &
      DISPLAY

FIELD UNACLE OF GPPRA_BDG_CTB                         &
      DISPLAY

FIELD PACMNTENG OF GPPRA_BDG_CTB                      &
      DISPLAY

FIELD PACMNTREE OF GPPRA_BDG_CTB                      &
      DISPLAY

FIELD D_MNTTOT                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

FIELD D_MNTBDG                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5

FIELD D_MNTDIS                                        &
      DISPLAY                                         &
      PICTURE "^^^,^^^,^^^.^^ "                       &
      TRAILING SIGN "-" SIGNIFICANCE 5


SKIP TO 9

TITLE &
@IF FRANCAIS
"Période Sous-pr.Immobilisation    Mnt débit        Mnt crédit   Module Réf.   Nom" &
@ELSE
      " %%% " &
@ENDIF
      AT ,3

TITLE &
@IF FRANCAIS
      "Document        Type" &
@ELSE
      " %%% " &
@ENDIF
      AT ,103

SKIP
ALIGN (2,3,4) (,,13) (,,20) (,,34) (,,51) (,,69) (,,74) (,,81) (,,103) (,,120)
CLUSTER OCCURS WITH VHPE_01 ID BASE 10

FIELD PECCLE OF VHPE_01 &
      LABEL " " &
      HIDDEN    &
      DISPLAY

FIELD PRACLE OF VHPE_01 &
      DISPLAY

FIELD IMMCLE OF VHPE_01 &
      DISPLAY

FIELD HPEMNTDBT OF VHPE_01 &
      DISPLAY

FIELD HPEMNTCRT OF VHPE_01 &
      DISPLAY

FIELD HENCODMOD OF VHPE_01 &
      DISPLAY

FIELD REFCLENUM OF VHPE_01 &
      DISPLAY

FIELD FOUNOMABR OF VFOC_FOU &
      DISPLAY

FIELD HENNUMDOC OF VHPE_01

FIELD HENTYPECR OF VHPE_01 &
      DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Appel du programme d'information comptable détaillé. (ENGAGE)
;
;
PROCEDURE INPUT HENNUMDOC
BEGIN
  IF 0 <> INDEX ( FIELDTEXT, "*" )
  THEN BEGIN
   IF HENCODMOD OF VHPE_01 = "AR" AND &
     (HENTYPECR OF VHPE_01 = "CM"  OR &
      HENTYPECR OF VHPE_01 = "AJ")
   THEN BEGIN
        LET T_PECCLEMNU = PECCLE    OF VHPE_01
        LET T_FOUCIECLE = REFCIECLE OF VHPE_01
        LET T_CMDCLEFND = HENCLE2   OF VHPE_01
        LET T_CMDAJTFND = HENCLE3   OF VHPE_01
        RUN SCREEN ar007 MODE F &
                   PASSING T_CIECLEMNU, &
                           T_CIENOM   , &
                           T_PECCLEMNU, &
                           T_FOUCIECLE, &
                           T_CMDCLEFND, &
                           T_CMDAJTFND MODE F
   END

   IF HENCODMOD OF VHPE_01 =  "AR" AND &
      HENTYPECR OF VHPE_01 <> "CM" AND &
      HENTYPECR OF VHPE_01 <> "AJ"
   THEN BEGIN
        LET T_PECCLEMNU = PECCLE    OF VHPE_01
        LET T_FOUCIECLE = REFCIECLE OF VHPE_01
        LET T_RECCLEFND = HENCLE2   OF VHPE_01
        RUN SCREEN ar009 &
             PASSING T_CIECLEMNU, &
                     T_CIENOM,    &
                     T_PECCLEMNU, &
                     T_FOUCIECLE, &
                     T_RECCLEFND  &
             MODE F
   END
  END
END

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

PROCEDURE ENTRY
  DISABLE

PROCEDURE APPEND
  DISABLE

PROCEDURE UPDATE
  DISABLE

PROCEDURE DELETE
  DISABLE

BUILD
@IF FORMAT
xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
@ENDIF

