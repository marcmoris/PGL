;/T/ Sécurité des éléments personnalisés.
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 6 Septembre 1994
;
;    Description..: Permet la gestion de la sécurité sur les éléments
;                   désignés par le client.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;------------------------------------------------------------------------------
SCREEN gs001d ACTIONBAR NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             FROM 8,1 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING GSUSAGER

USE ustrasse NOLIST

USE ussectmp NOLIST     ; Variable pour la sécurité
    "GS001D" ; Nom de l'écran

USE gs001d.hlp NOLIST    ; Use servant à la documentation de l'écran


USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-écrans

TEMPORARY T_XELNOM    CHARACTER SIZE 16 ; Nom de l'élément.
TEMPORARY T_XELNOMFND CHARACTER SIZE 16 ; Pour le popup de recherche.
TEMPORARY T_USRCLEFND CHARACTER SIZE 12 ; Pour le popup de recherche.


TEMPORARY T_FLGMOD    CHARACTER SIZE 1  RESET AT MODE
        ; Indique quand mode recherche par l'usager il n'y a pas
        ; d'insertion.

;
; Fichier des usagers.
;
FILE GSUSAGER           MASTER

;-------------------------------------------------------------------------------
;
; Fichier qui contient les éléments.
;
FILE GSSEC_ELEMENT      PRIMARY &
                        OCCURS 08 &
                        NOITEMS

 ACCESS VIA     USRCLE   , &
                SEEFLGVAL  &
        USING   USRCLE OF GSUSAGER, &
                "0"

 ITEM USRCLE OF GSSEC_ELEMENT INITIAL USRCLE OF GSUSAGER

TEMPORARY T_XELDSC      CHARACTER SIZE 40 ; Description de l'élément

;-------------------------------------------------------------------------------
;
; Fichier qui contient les éléments.
;
FILE GSSEC_ELEMENT      REFERENCE &
                        ALIAS A_SEE_EXIST &
                        OCCURS WITH GSSEC_ELEMENT

 ACCESS VIA     XELNOM   , &
                SEEFLGVAL  &
        USING   XELNOM OF GSSEC_ELEMENT, &
                "1"

 ITEM SEEDSC OF GSSEC_ELEMENT FINAL SEEDSC OF A_SEE_EXIST

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST

DRAW 02,01 TO 16,80
DRAW 06,01 TO 06,80

TITLE &
@IF FRANCAIS
      " Sécurité des éléments personnalisés " &
@ELSE
      " %%%urité des éléments personnalisés " &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP 1

ALIGN(3,3,19) (,,38)

FIELD USRCLE    OF GSUSAGER &
        DISPLAY
FIELD USREMPNOM OF GSUSAGER &
        DISPLAY

SKIP 2
TITLE " Elément          Nom" &
AT ,02
SKIP

ALIGN(2,2,3) (,,20) (,,63)

CLUSTER OCCURS WITH GSSEC_ELEMENT

FIELD XELNOM    OF GSSEC_ELEMENT &
        REQUIRED NOCHANGE &
        HIDDEN &
        LABEL " " &
        LOOKUP ON A_SEE_EXIST &
                VIA     XELNOM   , &
                        SEEFLGVAL  &
                USING   XELNOM OF GSSEC_ELEMENT, &
                        "1"                 &
        MESSAGE 9999 , &

               NOTON GSSEC_ELEMENT &
                VIA     XELNOM, &
                        USRCLE  &
                USING   XELNOM OF GSSEC_ELEMENT, &
                        USRCLE OF GSSEC_ELEMENT  &
        MESSAGE 194
FIELD SEEDSC    OF A_SEE_EXIST &
        DISPLAY

HILITE DATA OFF

FIELD SEEPWD    OF GSSEC_ELEMENT &
        FOR 1,1 &
        POPUP FROM 14,61 TO 16,77 ON INPUT

CLUSTER

;-------------------------------------------------------------------------------
;
; Popup sur les usagers.
;
PROCEDURE INPUT XELNOM
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_XELNOMFND = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN gs114 PASSING T_XELNOMFND MODE F
    LET FIELDTEXT = TRUNC(T_XELNOMFND)
  END
END

PROCEDURE OUTPUT SEEPWD
BEGIN
  LET FIELDTEXT = ""
END

BUILD
@IF FORMAT
********************************************************************************
*                                                                              *
* Usager........: xxxxxxxxxxxx       xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  *
*                                                                              *
********************************************************************************
* Elément          Nom                                                         *
* xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                    *
********************************************************************************
                              ****************
                              * XXXXXXXXXXXX *
                              ****************
@ENDIF
