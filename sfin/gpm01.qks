;/T/ Menu Gestion de projet
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 15 décembre 1994
;
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN gpm01 MENU &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             RECEIVING T_FLGHLDOK

USE ussectmp NOLIST
    "GPM01"

USE gpm01.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le holding est présent.


;
; Validation de la compagnie du menu.
;
FILE MCCOMPAGNIE      DESIGNER &
                      OPEN READ

;
; Pour la securite sur les compagnies.
;
FILE VSEC_USR         DESIGNER &
                      OPEN READ

;
; Informations sur le Holding.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;
;
; Variables saisie ou affichée dans le menu.
;
TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie - client
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_IMMCLEFND CHARACTER SIZE 12 RESET AT STARTUP
TEMPORARY T_PRACLEFND CHARACTER SIZE 6
TEMPORARY T_PRJCLEFND CHARACTER SIZE 8
;
TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies
;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "GPL@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "GPP@" ; Code pour les Procédure

;-------------------------------------------------------------------------------
;
;
USE ushilite NOLIST ; Hilite pour tout les écrans
DRAW 1,20 TO 3,61
DRAW 2,1  TO 2,20
DRAW 2,61 TO 2,80

DRAW 2,1  TO 5,1
DRAW 5,1  TO 5,80
DRAW 2,80 TO 5,80

DRAW 5,40 TO 19,40
DRAW 19,1 TO 19,80

TITLE &
@IF FRANCAIS
      "G E S T I O N   D E   P R O J E T" &
@ELSE
      "P R O J E C T   M A N A G E M E N T" &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

ALIGN(4,4,20) (,,26)
FIELD T_CIECLEMNU HIDDEN ID 05 &
        FIXED            &
        UPSHIFT REQUIRED &
        PATTERN "((^|#)>)|!0" &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Company......:"  &
@ENDIF
        LOOKUP ON MCCOMPAGNIE &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 801 , & ; Cette compagnie n'existe pas

          ON VSEC_USR &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 802 ; Vous n'avez pas accès à cette compagnie.

FIELD T_CIENOM &
        FIXED

SKIP TO 9

ALIGN (2,2)

SUBSCREEN gp001 HIDDEN &
                PASSING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Fiche de Projet     "
@ELSE
                LABEL "%%%Projects "
@ENDIF

SKIP 1
SUBSCREEN gp002 HIDDEN &
                PASSING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Fiche de sous-projet"
@ELSE
                LABEL "%%%Subproject"
@ENDIF

SKIP 1


SKIP TO 9

ALIGN (42,42)

SUBSCREEN gp003 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Consultation par format comptable"
@ELSE
                LABEL "%%%sultation par format comptable"
@ENDIF

SKIP 1

SUBSCREEN gp004 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Consultation par format d'opération"
@ELSE
                LABEL "%%%sultation par format d'opération"
@ENDIF

SKIP 1


SKIP TO 20

ALIGN(31,31)
SUBSCREEN gpm02 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL " Listes et rapports "
@ELSE
                LABEL " Lists and Reports  "
@ENDIF

SUBSCREEN gpm02 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "     Procédures "
@ELSE
                LABEL "     Procedures "
@ENDIF

SUBSCREEN gpm03 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM    &
@IF FRANCAIS
                LABEL "Informations de base"
@ELSE
                LABEL " Basic Information  "
@ENDIF

;-------------------------------------------------------------------------------
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le holding n'est pas défini on retourne l'usager au menu
  ; principal.
  ;
  IF T_FLGHLDOK = "0"
  THEN RETURN
  ;
  ; Valide la sécurité d'accès de l'écran.
  ;
  USE ussececr NOLIST
  ;
  ; Saisie de la compagnie du menu.
  ;
  ACCEPT T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
  ;
  ; A cause de l'option requis, si l'usager fait un retour et qu'il y a
  ; une valeur on remet la même.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_CIECLEMNU))
  THEN LET FIELDTEXT = T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;
; On conserve le nom de la compagnie du menu.
;
;
PROCEDURE PROCESS T_CIECLEMNU
BEGIN
  IF HLDCHTCLI OF MCHOLDING = "1"
  THEN LET T_CLICIECLE = T_CIECLEMNU
  ELSE LET T_CLICIECLE = HLDCHRSUB OF MCHOLDING

  LET T_CIENOM = CIENOM OF MCCOMPAGNIE
  DISPLAY T_CIENOM

  LET T_IMMCLEFND  = ""
  LET T_PRACLEFND  = ""
  LET T_PRJCLEFND  = ""
END

;PROCEDURE DESIGNER 20
;BEGIN
;  LET T_PRJCLEFND  = ""
;  RUN SCREEN pr011 PASSING T_CIECLEMNU, &
;                           T_CIENOM, &
;                           T_PRJCLEFND
;END
;
;PROCEDURE DESIGNER 30
;BEGIN
;  LET T_PRJCLEFND  = ""
;  RUN SCREEN pr014 PASSING T_CIECLEMNU, &
;                           T_CIENOM   , &
;                           T_PRJCLEFND
;END
;PROCEDURE DESIGNER 35
;BEGIN
;  LET T_PRACLEFND  = ""
;  LET T_PRJCLEFND  = ""
;  RUN SCREEN pr015 PASSING T_CIECLEMNU, &
;                           T_CIENOM   , &
;                           T_PRJCLEFND, &
;                           T_PRACLEFND
;END
;PROCEDURE DESIGNER 40
;BEGIN
;  LET T_UNICATFND  = ""
;  LET T_UNICLEFND  = ""
;  RUN SCREEN pr016 PASSING T_CIECLEMNU, &
;                           T_CIENOM   , &
;                           T_UNICATFND, &
;                           T_UNICLEFND
;END

PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_CIECLEMNU
END

BUILD ;
;
;                   ******************************************
;********************  G E S T I O N   D E   P R O J E T S   ********************
;*                  ******************************************                  *
;*  Compagnie.....: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
;********************************************************************************
;                                       *
;                                       *
;                                       *
; Fiche de projet                       *
;                                       *
; Fiche de sous-projet                  *
;                                       *
;                                       *
;                                       *
;                                       *
;                                       *
;                                       *
;                                       *
;********************************************************************************
;                               Listes et rapports
;                                   Procédures
;                              Informations de base
;
