;/P/ Liste des contrats actifs
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Modifié par André Bilodeau 26/04/94
;  - Changement de label (SOS #80)
;

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFCONTRAT &
 LINK CIECLE OF IFCONTRAT, &
      CLTCLE OF IFCONTRAT, &
      CRRCLENUM OF IFCONTRAT, &
      CRRCLEANN OF IFCONTRAT  &
   TO CIECLE, &
      CLTCLE, &
      CRRCLENUM, &
      CRRCLEANN                         OF IFCONTRAT_LIGNE &
 LINK CIECLE OF IFCONTRAT, &
      CLTCLE OF IFCONTRAT, &
      CRRCLENUM OF IFCONTRAT, &
      CRRCLEANN OF IFCONTRAT, &
      CATCLE OF IFCONTRAT_LIGNE, &
      COEDATDEB OF IFCONTRAT_LIGNE &
   TO CIECLE, &
      CLTCLE, &
      BLCCRRCLENUM, &
      BLCCRRCLEANN, &
      CATCLE, &
      COEDATDEB                         OF IFBLOC OPTIONAL

CHOOSE CIECLE PARM, &
       CLTCLE PARM

SELECT IFCONTRAT_LIGNE IF     COESTATUT OF IFCONTRAT_LIGNE = "A"

SORT ON CLTCLE      OF IFCONTRAT_LIGNE &
     ON CRRCLENUM   OF IFCONTRAT_LIGNE &
     ON CRRCLEANN   OF IFCONTRAT_LIGNE &
     ON CATCLE      OF IFCONTRAT_LIGNE &
     ON COEDATDEB   OF IFCONTRAT_LIGNE

DEF D-QTE-FAIRE INT SIZE 4 = COEQTECMD  OF IFCONTRAT_LIGNE - &
                             COEQTEPROD OF IFCONTRAT_LIGNE

DEF D-QTE-TRS INT SIZE 4 = BLCM3TOT OF IFBLOC IF BLCDATREC OF IFBLOC <> 0 &
                           ELSE 0

DEFINE D_CLTCRR INTEGER SIZE 4 = CLTCLE OF IFCONTRAT * 1000 + &
                                 CRRCLENUM OF IFCONTRAT * 10 + &
                                 CRRCLEANN OF IFCONTRAT
;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF317"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
      SKIP 3 &
           TAB 002 &
 "CONTRAT CAT   DEBUT       FIN       QTE-CMD     QTE-ASS     QTE-TRANSP  QTE-FAC     QTE-REC     RESTE    REFERENCE" SKIP 2

FOOTING AT COEDATDEB &
   TAB 003 D_CLTCRR     PIC "^^^^^" PRINT AT CRRCLEANN PICTURE "^^^^^" &
   TAB 011 CATCLE       OF IFCONTRAT_LIGNE &
   TAB 015 COEDATDEB    OF IFCONTRAT_LIGNE &
   TAB 025 COEDATFIN    OF IFCONTRAT_LIGNE &
   TAB 036 COEQTECMD    OF IFCONTRAT_LIGNE PICTURE " ^^^^^.^^" LEAD "-" &
   TAB 048 COEQTEPROD   OF IFCONTRAT_LIGNE PICTURE " ^^^^^.^^" LEAD "-" &
   TAB 060 D-QTE-TRS    SUB              PICTURE " ^^^^^.^^" LEAD "-" &
   TAB 072 COEQTEFAC    OF IFCONTRAT_LIGNE PICTURE " ^^^^^.^^" LEAD "-" &
   TAB 084 COEQTERECL OF IFCONTRAT_LIGNE PICTURE " ^^^^^.^^" LEAD "-" &
   TAB 096 D-QTE-FAIRE                   PICTURE " ^^^^^.^^" LEAD "-" &
   TAB 107 COEREFCLI  OF IFCONTRAT_LIGNE

FOOT AT CRRCLEANN &
   TAB 001 "---------------------------------------------------------------------------------------------------------------------------------"  &
SKIP &
 TAB 003 "Total contrat :" D_CLTCRR    PICTURE "^^^^^" &
 TAB 034 COEQTECMD  OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE " ^^^^^^^.^^" LEAD "-" &
 TAB 046 COEQTEPROD OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE " ^^^^^^^.^^" LEAD "-" &
 TAB 058 D-QTE-TRS                     SUB                PICTURE " ^^^^^^^.^^" LEAD "-" &
 TAB 070 COEQTEFAC  OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE " ^^^^^^^.^^" LEAD "-" &
 TAB 082 COEQTERECL OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE " ^^^^^^^.^^" LEAD "-" &
 TAB 094 D-QTE-FAIRE                   SUB AT COEDATDEB PICTURE " ^^^^^^^.^^" LEAD "-" &
SKIP 2

FINAL FOOT &
        TAB 001 "================================================================================================================================="  &
SKIP    TAB 003 "Grand Total  :" &
        TAB 034 COEQTECMD  OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE "^^^^^^^^.^^" &
        TAB 046 COEQTEPROD OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE "^^^^^^^^.^^" &
        TAB 058 D-QTE-TRS                     SUB                PICTURE " ^^^^^^^.^^" LEAD "-" &
        TAB 070 COEQTEFAC  OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE "^^^^^^^^.^^" &
        TAB 082 COEQTERECL OF IFCONTRAT_LIGNE SUB AT COEDATDEB PICTURE "^^^^^^^^.^^" &
        TAB 094 D-QTE-FAIRE                   SUB AT COEDATDEB PICTURE "^^^^^^^^.^^"


BUILD if317z
