;/T/ Factures / notes de crédit - Adresse client divers
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 4 juin 1993
;
;    Description..: Sous-écran pour saisir l'adresse d'un document pour
;                   les clients divers.
;

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN cr003c SLAVE &
              NOCOMMIT &
              NOMODE NOACTION FIELDMARK &
              FROM 6,10 TO 17,69 &
              MESSAGE ON 24 &
              HELP POPUP FROM 4,9 TO 20,72 &
              ACTIVITIES ENTRY, CHANGE &
              RECEIVING MCREF_ADRESSE

USE cr003c.hlp NOLIST

;-------------------------------------------------------------------------------
;

FILE MCREF_ADRESSE    MASTER


;------------------------------------------------------------------------------

DRAW 1,1 TO 12,60

USE ushilite NOLIST

TITLE &
@IF FRANCAIS
      " Adresse - client divers " &
@ELSE
      " Miscellaneous Client - Address " &
@ENDIF
      CENTERED AT 1,1

USE ustitoff NOLIST

SKIP 1

ALIGN(2,3,19)

FIELD RADNOM    OF MCREF_ADRESSE HIDDEN ID 05 &
        DISPLAY

FIELD RADNOMABR OF MCREF_ADRESSE HIDDEN ID 08 &
        DEFAULT RADNOM OF MCREF_ADRESSE

FIELD RADADR1   OF MCREF_ADRESSE HIDDEN ID 10

FIELD RADADR2   OF MCREF_ADRESSE ID SAME NOLABEL

FIELD RADADR3   OF MCREF_ADRESSE ID SAME NOLABEL

FIELD RADADR4   OF MCREF_ADRESSE ID SAME NOLABEL

FIELD RADCP     OF MCREF_ADRESSE HIDDEN ID 10

FIELD RADTEL    OF MCREF_ADRESSE HIDDEN ID 15


;-------------------------------------------------------------------------------
;
;
; Ajout d'un blanc dans le code postal si l'usager ne la pas saisie.
;
;
PROCEDURE INPUT RADCP
BEGIN
  USE uscpfmt NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT RADTEL
BEGIN
  USE ustelfmt NOLIST
END


;-------------------------------------------------------------------------------
;
; Procédure ENTRY standard plus le verbe RETURN à la fin. L'option AUTORETURN
; ne fonctionne pas avec un écran SLAVE.
;
PROCEDURE ENTRY
BEGIN
  DISPLAY RADNOM    OF MCREF_ADRESSE
  ACCEPT  RADNOMABR OF MCREF_ADRESSE
  ACCEPT  RADADR1   OF MCREF_ADRESSE
  ACCEPT  RADADR2   OF MCREF_ADRESSE
  ACCEPT  RADADR3   OF MCREF_ADRESSE
  ACCEPT  RADADR4   OF MCREF_ADRESSE
  ACCEPT  RADCP     OF MCREF_ADRESSE
  ACCEPT  RADTEL    OF MCREF_ADRESSE
  RETURN
END


;-------------------------------------------------------------------------------
;
;
; La mise à jour va être fait dans l'écran des factures.
;
;
PROCEDURE PREUPDATE
BEGIN
  RETURN
END

BUILD
@IF FORMAT

          ***************** Adresse - client divers ******************
          *                                                          *
          * Nom...........: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
          * Nom abrégé....: xxxxxxxxxxxxxxxxxxxx                     *
          * Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
          *                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
          *                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
          *                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
          * Code Postal...: xxxxxxx                                  *
          * Téléphone.....: xxxxxxxxxxxxx                            *
          *                                                          *
          ************************************************************
@ENDIF
