;/T/ Localisations d'imprimante
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 31 août 1992
;
;    Description..: Définition des codes de localisations pour les imprimantes.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gs150 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72

USE ussectmp NOLIST
    "GS150"

USE gs150.hlp NOLIST

USE usactecr NOLIST


FILE GSLOCAL          PRIMARY OCCURS 18 NOITEMS
  ACCESS VIA XLOCLE &
         USING XLOCLE OF GSLOCAL &
         REQUEST XLOCLE
  ACCESS VIAINDEX XLO_U01 &
         VIA XLOCLE &
         USING "@"

;-------------------------------------------------------------------------------
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = ""
DEFINE DZ_CIENOM CHARACTER SIZE 40 = ""

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
       " Localisations - imprimantes " &
@ELSE
       " %%% " &
@ENDIF
CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

TITLE &
@IF FRANCAIS
       "Code  Description Française   Description anglaise" &
@ELSE
       "Cod   French description      English description" &
@ENDIF
AT 4,8

CLUSTER OCCURS WITH GSLOCAL ID BASE 10
ALIGN(8,6,8) (,,14) (,,38)

FIELD XLOCLE OF GSLOCAL  LABEL " " HIDDEN &
        REQUIRED NOCHANGE &
        LOOKUP NOTON GSLOCAL &
          VIA XLOCLE &
          USING XLOCLE OF GSLOCAL &
          MESSAGE 17 ; Ce code existe déjà.

FIELD XLODSCFRA OF GSLOCAL &
        REQUIRED

FIELD XLODSCANG OF GSLOCAL &
        DEFAULT XLODSCFRA OF GSLOCAL
CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie(ENTRY).
;
;
PROCEDURE INPUT XLOCLE
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  FOR GSLOCAL
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF GSLOCAL &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF GSLOCAL &
       AND NOT NEWRECORD     OF GSLOCAL &
       AND NOT DELETEDRECORD OF GSLOCAL &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD
