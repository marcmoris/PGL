;/T/ Comptes contrôles
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 17 février 1994
;
;/V3.02.00/ Alain Côté, 17 février 1994, Immobilisation
;
;    Description..: Cette écran sert inscrire les comptes comptables qui sont
;                   affectés lors d'une transaction d'aliénation.
;
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN im151 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU , &
                       T_CIENOM


;
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "IM151"

USE im151.hlp NOLIST

USE usactecr NOLIST     ; Actionbar standard dans les écrans pleine
                        ; page et sous-ecran


;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  ; Compagnie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la compagnie du menu.


;-------------------------------------------------------------------------------
;
FILE IMCIE_PARAM      PRIMARY
  ACCESS VIA   CIECLE &
         USING CIECLE OF IMCIE_PARAM

  ITEM CIECLE OF IMCIE_PARAM INITIAL T_CIECLEMNU

;
; Validation du compte de gain.
;
FILE VCPT_DSC         REFERENCE
  ACCESS VIA CPTCLE &
         USING IMPCPTGAIALI OF IMCIE_PARAM

;
; Validation du compte de perte.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_PERTE
  ACCESS VIA CPTCLE &
         USING IMPCPTPERALI OF IMCIE_PARAM

;
; Validation du compte de revenu transitoire.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_REVENU
  ACCESS VIA CPTCLE &
         USING IMPCPTTRAALI OF IMCIE_PARAM

;
; Validation de l'unité adm du compte de gain.
;
FILE MCUNITE_ADM      REFERENCE
  ACCESS VIA CIECLE , &
             UNACLE   &
         USING CIECLE       OF IMCIE_PARAM , &
               IMPUNAGAIALI OF IMCIE_PARAM

;
; Validation de l'unité adm du compte perte.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_PERTE
  ACCESS VIA CIECLE , &
             UNACLE   &
         USING CIECLE       OF IMCIE_PARAM , &
               IMPUNAPERALI OF IMCIE_PARAM

;
; Validation de l'unité adm du compte de revenu.
;
FILE MCUNITE_ADM      REFERENCE &
                      ALIAS A_UNA_REVENU
  ACCESS VIA CIECLE , &
             UNACLE   &
         USING CIECLE       OF IMCIE_PARAM , &
               IMPUNATRAALI OF IMCIE_PARAM

;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING REFERENCE
  ACCESS VIA   CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes et unité adm.
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes
TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur les unités adm.


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Comptes contrôles " &
@ELSE
      " %%%Comptes contrôles " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

SKIP 2

DRAW ,1 TO ,80

TITLE &
@IF FRANCAIS
      " Aliénation " &
@ELSE
      "%%%Aliénation " &
@ENDIF
      CENTERED AT ,1

SKIP

TITLE &
@IF FRANCAIS
      "Compte Description          Unité    Description" &
@ELSE
      "%%%pte Description          Unité    Description" &
@ENDIF
      AT ,19

SKIP 1

ALIGN (19,3,19) (,,26) (,,47) (,,56)

FIELD IMPCPTGAIALI OF IMCIE_PARAM ID 05 HIDDEN &
        REQUIRED &
        LOOKUP &
          ON VCPT_DSC &
            MESSAGE 1615 , & ; Ce compte comptable n'existe pas.
          NOTON IMCIE_PARAM &
            VIA CIECLE &
            USING CIECLE OF IMCIE_PARAM &
            MESSAGE 1616     ; Il existe déjà des comptes controles...

FIELD CPTDSCABR OF VCPT_DSC &
         DISPLAY

FIELD IMPUNAGAIALI   OF IMCIE_PARAM

FIELD UNANOMABR OF MCUNITE_ADM &
        DISPLAY

SKIP 1

FIELD IMPCPTPERALI OF IMCIE_PARAM ID 10 HIDDEN &
        REQUIRED &
        LOOKUP ON A_CPT_PERTE &
          MESSAGE 1615 ; Ce compte comptable n'existe pas

FIELD CPTDSCABR OF A_CPT_PERTE &
        DISPLAY

FIELD IMPUNAPERALI OF IMCIE_PARAM

FIELD UNANOMABR OF A_UNA_PERTE &
        DISPLAY

SKIP 1

FIELD IMPCPTTRAALI OF IMCIE_PARAM ID 15 HIDDEN &
        REQUIRED &
        LOOKUP ON A_CPT_REVENU &
          MESSAGE 1615 ; Ce compte comptable n'existe pas

FIELD CPTDSCABR OF A_CPT_REVENU &
        DISPLAY

FIELD IMPUNATRAALI OF IMCIE_PARAM

FIELD UNANOMABR OF A_UNA_REVENU &
        DISPLAY


;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Compte comptable de gain, popup sur les comptes.
;
;
PROCEDURE INPUT IMPCPTGAIALI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Unité adm. du compte de gain, popup sur les unités administratives.
;
;
PROCEDURE INPUT IMPUNAGAIALI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = IMPCPTGAIALI OF IMCIE_PARAM
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F &
                          PASSING T_CIECLEMNU , &
                                  T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
      ELSE RUN SCREEN mc116 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE( T_UNACLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de l'unité administrative du compte de gain, ce champ est optionel.
;
;
PROCEDURE EDIT IMPUNAGAIALI
BEGIN
  GET MCUNITE_ADM OPTIONAL
  IF     "" <> TRUNCATE( FIELDTEXT ) &
     AND NOT ACCESSOK
  THEN ERROR 1614 ; Cette unité administrative n'existe pas.
END


;-------------------------------------------------------------------------------
;
;
; Compte comptable de perte sur aliénation, popup sur les comptes.
;
;
PROCEDURE INPUT IMPCPTPERALI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Unité adm. du compte de perte, popup sur les unités administratives.
;
;
PROCEDURE INPUT IMPUNAPERALI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = IMPCPTPERALI OF IMCIE_PARAM
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F &
                          PASSING T_CIECLEMNU , &
                                  T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
      ELSE RUN SCREEN mc116 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE( T_UNACLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de l'unité administrative du compte de perte, ce champ est
; optionel.
;
;
PROCEDURE EDIT IMPUNAPERALI
BEGIN
  GET A_UNA_PERTE OPTIONAL
  IF     "" <> TRUNCATE( FIELDTEXT ) &
     AND NOT ACCESSOK
  THEN ERROR 1614 ; Cette unité administrative n'existe pas.
END


;-------------------------------------------------------------------------------
;
;
; Compte comptable de revenu, popup sur les comptes.
;
;
PROCEDURE INPUT IMPCPTTRAALI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "@"
    LET T_PECCLE    = ""
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, &
                                    T_CPTTYPPAT, &
                                    T_CPTCATPAT, &
                                    T_PECCLE
    LET FIELDTEXT = TRUNCATE(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Unité adm. du compte de revenu, popup sur les unités administratives.
;
;
PROCEDURE INPUT IMPUNATRAALI
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE = IMPCPTTRAALI OF IMCIE_PARAM
    LET T_PECCLE = " "
    LET T_UNACLE = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    ;
    ; Il y a trois popup possible:
    ;  1) sur les unités administrative par compagnie(MCUNITE_ADM).
    ;  2) sur les unités administrative d'une charte de compte(MCCHARTE_UNA).
    ;  3) sur les unités administrative qui ne sont pas définis dans la charte
    ;     de compte(MCUNITE_ADM, MCCHARTE_UNA).
    ;
    IF HLDCHTCPT OF MCHOLDING = "0"
    THEN RUN SCREEN mc104 MODE F &
                          PASSING T_CIECLEMNU , &
                                  T_UNACLE
    ELSE BEGIN
      IF HLDCHTCPTRLT OF MCHOLDING = "I"
      THEN RUN SCREEN mc115 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
      ELSE RUN SCREEN mc116 MODE F &
                            PASSING T_CIECLEMNU , &
                                    T_CPTCLE    , &
                                    T_UNACLE    , &
                                    T_PECCLE
    END
    LET FIELDTEXT = TRUNCATE( T_UNACLE )
  END
END


;-------------------------------------------------------------------------------
;
;
; Validation de l'unité administrative du compte de revenu, ce champ est
; optionel.
;
;
PROCEDURE EDIT IMPUNATRAALI
BEGIN
  GET A_UNA_REVENU OPTIONAL
  IF     "" <> TRUNCATE( FIELDTEXT ) &
     AND NOT ACCESSOK
  THEN ERROR 1614 ; Cette unité administrative n'existe pas.
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF IMCIE_PARAM &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF IMCIE_PARAM &
     AND NOT NEWRECORD     OF IMCIE_PARAM &
     AND NOT DELETEDRECORD OF IMCIE_PARAM &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Comptes contrôles *******************************
*                                                                              *
*                                                                              *
********************************** Aliénation **********************************
*                 Compte Description          Unité    Description             *
*                                                                              *
* Gain..........: xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
* Perte.........: xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
* Revenu........: xxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxx xxxxxxxxxxxxxxxxxxxx    *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
