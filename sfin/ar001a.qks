;/T/ Gestion des localisations d'entrepôt
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 28 février 1994
;
;    Description..: Cet écran permet de faire la gestion des localisations de
;                   l'entrepôt. 
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar001a ACTIONBAR & 
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             FOR 10 LINES &
             ON LINE 14  &  
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING ARENTREPOT
                       
USE ustrasse NOLIST      ; Pour les sous-écrans

USE ussectmp NOLIST      ; Variable pour la sécurité
    "AR001A"             ; Nom de l'écran

USE ar001a.hlp NOLIST    ; Use servant à la documentation de l'écran

USE usactecr NOLIST                      

;-------------------------------------------------------------------------------
FILE ARENTREPOT  MASTER


FILE ARENT_LOCAL  PRIMARY NOITEM OCCURS 18
  ACCESS VIA   CIECLE,               &
               ENTCLE,               &   
               ENLCLE                &
         USING CIECLE,               &
               ENTCLE,               &
               ENLCLE  OF ARENT_LOCAL  &
         REQUEST ENLCLE    &  
         ORDERBY CIECLE,   &
                 ENTCLE,   &
                 ENLCLE     

  ACCESS VIA   CIECLE,              &
               ENTCLE               &  
         USING CIECLE,              &
               ENTCLE OF ARENT_LOCAL &
         ORDERBY CIECLE,   &
                 ENTCLE,   &
                 ENLCLE     

  ITEM CIECLE OF ARENT_LOCAL INITIAL CIECLE OF ARENTREPOT
  ITEM ENTCLE OF ARENT_LOCAL INITIAL ENTCLE OF ARENTREPOT

;------------------------------------------------------------------------------
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction


USE ushilite NOLIST     ; Hilite pour tous les écrans

DRAW FROM  2, 1 TO  2,79
DRAW FROM  2, 1 TO  10,1
DRAW FROM  3,80 TO  10,80
DRAW FROM  10,1  TO 10,80


SKIP 1
TITLE &
@IF FRANCAIS
      " Localisations " &
@ELSE
      " %%%Localisations " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

;-------------------------------------------------------------------------------

SKIP
TITLE  &
@IF FRANCAIS
      "Code  Description         Code  Description         Code  Description" &
@ELSE
      "%%%e Description          Code Description          Code Description" &
@ENDIF
      AT ,3

CLUSTER VERTICAL OCCURS WITH ARENT_LOCAL ID BASE 10 FOR 1,26

ALIGN (2,2,3) (,,9) 
FIELD ENLCLE OF ARENT_LOCAL &
        HIDDEN                  &
        REQUIRED NOCHANGE       &
        LABEL " "               &
        LOOKUP NOTON ARENT_LOCAL &
          VIA   CIECLE, &
                ENTCLE, &
                ENLCLE  &
          USING CIECLE, &
                ENTCLE, &
                ENLCLE OF ARENT_LOCAL &
          MESSAGE 1319        ; Ce code de localisation existe déjà

FIELD ENLDSC OF ARENT_LOCAL   &
        FOR , 18 
CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
; Valide si l'usager a le droit d'entrer des données.
;
;
PROCEDURE INPUT ENLCLE 
BEGIN
  USE ussecent NOLIST
END


;
; Procédure servant à modifier la première occurrence d'un cluster
; avec Powerhouse Client.
;
PROCEDURE DESIGNER PH01
BEGIN
  ACCEPT ENLDSC OF ARENT_LOCAL
END


;
; Procédure servant à détruire la première occurrence d'un cluster
; avec Powerhouse Client.
;
PROCEDURE DESIGNER PH02
BEGIN
  DELETE ARENT_LOCAL 
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR ARENT_LOCAL
  BEGIN
     ;
     ; Validation de la destruction
     ;
    IF     DELETEDRECORD OF ARENT_LOCAL &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF ARENT_LOCAL &
       AND NOT NEWRECORD     OF ARENT_LOCAL &
       AND NOT DELETEDRECORD OF ARENT_LOCAL &
       AND TZ_SECMOD = "0"
    THEN ERROR 2

    ;
    ; Destruction d'une localisation d' entrepôt
    ;
    IF DELETEDRECORD OF ARENT_LOCAL
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la localisation de l'entrepôt
      ; est utilisée.
      ;
      RUN SCREEN ar001ax MODE SAME &
                        PASSING ARENT_LOCAL, &
                                T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END 
  END
END

BUILD 


@IF FORMAT
******************************** Localisations ********************************x
* Code Description          Code Description          Code Description         *
* xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx *
* xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx *
* xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx *
* xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx *
* xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx *
* xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx  xxxx xxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF
