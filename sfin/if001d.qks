;/T/ Carte de bloc - Historique de la reservation
;
;/P/ Programmeur..: Francois Richard
;    Date Creation: 3 novembre 1999
;
;    Description..: Permet de consulter l'historique de la reservation de bloc
;
;-------------------------------------------------------------------------------

SCREEN if001d ACTIONBAR & 
              NOMODE &
              NOACTION FIELDMARK STARTUP &
              ACTIVITIES FIND & 
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING T_CIECLEMNU, &
                        T_CIENOM, &
                        IFBLOC

;USE if001d.hlp NOLIST

USE ussectmp  NOLIST
"IF001D"

USE usactecr  NOLIST

USE ustrasse NOLIST

;-------------------------------------------------------------------------------
;
; Variable recu en parametre
;
TEMPORARY T_CIECLEMNU        CHARACTER SIZE 4  ; Num\351ro de cie
TEMPORARY T_CIENOM           CHARACTER SIZE 40 ; Nom de la compagnie

;-------------------------------------------------------------------------------
;
; Carte de bloc
;
FILE IFBLOC            MASTER

;------------------------------------------------------------------------------
;
; Reservation de bloc
;
FILE IFHISTO_RESERV PRIMARY OCCURS 16

 ACCESS VIA     CIECLE,              &
                TGRCODGRA,           &
                IFCCLE,              &
                BLCCLE,              &
                BLCRED               &
         USING  T_CIECLEMNU,         &
                TGRCODGRA OF IFBLOC, &
                IFCCLE OF IFBLOC,    &
                BLCCLE OF IFBLOC,    &
                BLCRED OF IFBLOC     &
        ORDERBY BLRUSRRES,           &
                BLRDATRESDEB

;------------------------------------------------------------------------------
;
; Pour afficher le nom de l'usager qui a reserver
;
FILE GSUSAGER         REFERENCE OCCURS WITH IFHISTO_RESERV
  
   ACCESS VIA   USRCLE &
          USING BLRUSRRES OF IFHISTO_RESERV

;-------------------------------------------------------------------------------
;
;
DEFINE DZ_CIECLE CHARACTER SIZE 4  = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

DRAW THIN 5,1 TO 5,80

SKIP 

TITLE &
      " Historique de la réservation d'un bloc " &
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN (,3,19)(,,22)(,24,25)(,31,32)(,36,42)

FIELD TGRCODGRA OF IFBLOC          &
      LABEL "No bloc.......:"      &
      DISPLAY

FIELD IFCCLE OF IFBLOC &
      DISPLAY

FIELD BLCCLE OF IFBLOC &
      LABEL "-"        &
      DISPLAY

FIELD BLCRED OF IFBLOC &
      LABEL "-"        &
      DISPLAY

FIELD BLCDAT OF IFBLOC  &
      LABEL "Date:"     &
      FORMAT YYYYMMDD   &
      DISPLAY

SKIP 1

TITLE "Usager" AT ,3
TITLE "Début" AT ,57
TITLE "Fin" AT ,69
ALIGN(,,3)(,,15)(,,57)(,,69)

CLUSTER OCCURS WITH IFHISTO_RESERV

FIELD BLRUSRRES OF IFHISTO_RESERV &
      DISPLAY

FIELD USREMPNOM OF GSUSAGER &
      DISPLAY       

FIELD BLRDATRESDEB OF IFHISTO_RESERV &
      DISPLAY                        &     
      FORMAT YYYYMMDD               

FIELD BLRDATRESFIN OF IFHISTO_RESERV &
      DISPLAY                        &
      FORMAT YYYYMMDD               

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la s\351curit\351 d'acc\350s de l'\351cran.
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

BUILD DETAIL LIST
