;/T/ Plan comptable - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 11 août 1993
;
;    Description..: Cet écran vérifie si le compte reçu en paramètre est
;                   référé dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc010x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCCOMPTE , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Compte comptable
;
FILE MCCOMPTE         MASTER

;
; Solde du Grand Livre.
;
FILE MCSOLDE_ANNUEL   REFERENCE

;
; Historique des projets.
;
FILE MCHIS_PROJET     REFERENCE

;
; Compte comptable pour les comptes liés.
;
FILE MCCOMPTE         REFERENCE ALIAS A_CPT_LIE

;
; Compte de banque.
;
FILE MCLIVRET_BQ      REFERENCE

;
;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans les soldes du Grand Livre.
  ;
  GET MCSOLDE_ANNUEL VIA CPTCLE &
                     USING CPTCLE OF MCCOMPTE &
                     OPTIONAL
  IF ACCESSOK
  THEN ERROR 166 ; Destruction annulée, cette information est référée dans
                 ; les soldes au G/L.

  ;
  ; On vérifie dans l'historique des projets.
  ;
  GET MCHIS_PROJET VIA CPTCLE &
                   USING CPTCLE OF MCCOMPTE &
                   OPTIONAL
  IF ACCESSOK
  THEN ERROR 167 ; Destruction annulée, cette information est référée par
                 ; l'historique des projets.

  ;
  ; On vérifie dans le plan comptable par le compte lié.
  ;
  GET A_CPT_LIE VIA CPTCPTLIE &
                USING CPTCLE OF MCCOMPTE &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 158 ; Destruction annulée, cette information est référée par
                 ; une compte ctb.

  ;
  ; On vérifie dans les comptes de banque.
  ;
  GET MCLIVRET_BQ VIA LBQCPTENC &
                  USING CPTCLE OF MCCOMPTE &
                  OPTIONAL
  IF ACCESSOK
  THEN ERROR 165 ; Destruction annulée, cette information est référée par
                 ; une compte de banque.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
