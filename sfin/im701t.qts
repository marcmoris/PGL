;/T/ Programme de transfert des aliénations au Grand Livre
;
;//M/GRA01/Francois Richard/1999-06-17
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Alain Côté
;    Date création: 2 mars 1994
;
;/V3.02.00/ Alain Côté, 2 mars 1994, Immobilisation
;/V3.02.00/ Daniel Labrie, 15 mars 1994, 6
;    Description..: Cette procédure à pour but de créer un écriture de journal
;                   par immobilisation à aliéner.
;
;
;    Exemple d'une écriture d'aliénation:
;
;        Compte                         Unité                 Débit  Crédit
;        -----------------------------  ------------------    -----  ------
;        Compte transitoire             Unité adm.              X
;        ( IMPCPTTRAALI )               ( UNACLE ou
;                                         IMPUNATRAALI )
;        Compte amortissement accumulé  Unité adm.              X
;        (IMMCPTAMTACC)                 ( UNACLE ou
;                                         IMMUNAAMTACC )
;        Compte d'actif                 Unité adm.                     X
;        ( IMMCPTACT )                  ( UNACLE ou
;                                         IMMUNAACT )
;    --->Gain                           Unité adm.                     X
;    |   (IMPCPTGAIALI)                 ( UNACLE ou
;    OU                                   IMPUNAGAIALI )
;    |
;    --->Perte                          Unité adm.              X
;        (IMPCPTPERALI)                 ( UNACLE ou
;                                         IMPUNAPERALI )
;
;-------------------------------------------------------------------------------

USE ussetqts NOLIST

RUN im701t

;
; Pour arrêter la procédure en cas d'erreur.
;
GLOBAL TEMPORARY G_ERREUR        CHARACTER SIZE 5
;
; Période à traiter.
;
GLOBAL TEMPORARY G_PECCLE        CHARACTER SIZE 4
;
; Pour garder la même date du jour partout ou on en a besoin.
;
GLOBAL TEMPORARY G_DATSTP        DATE
;
; Date de fin de la période à traiter.
;
GLOBAL TEMPORARY G_PECDATFIN     DATE
;
; Parametres de holding pour nombre de période ctb.
;
GLOBAL TEMPORARY G_HLDNBRPERCTB INTEGER SIZE 2
;
; Parametre pour le mode de numerotation des écritures
;
GLOBAL TEMPORARY G_HLDECRAUT    CHARACTER SIZE 1
GLOBAL TEMPORARY G_HLDECRCOD    CHARACTER SIZE 1


;-------------------------------------------------------------------------------
;
;
; Valide que la période recu en paramètre soit courante ou subséquente.
; Calcul la derniére période d'amortissement soit celle qui précède celle
; demandé en paramétre.
;
;
REQUEST VALIDE_PERIODE

ACCESS MCPERIODE_CIE &

  LINK PECCLE OF MCPERIODE_CIE &
    TO PECCLE OF MCPERIODE_CTB &

  LINK "1" &
    TO CIENMC OF MCHOLDING

CHOOSE CIECLE PARM, &
       PECCLE PARM


TEMPORARY T_MESERR      CHARACTER SIZE 80 ; Pour les erreurs.


ITEM T_MESERR = &
@IF FRANCAIS
      " La période comptable demandée est une période fermée" &
@ELSE
      " %%%La période comptable demandée est une période fermée" &
@ENDIF
                IF PCCSTUIM OF MCPERIODE_CIE = "F" &
@IF FRANCAIS
 ELSE " On ne peut pas utiliser une période de régularisation" &
@ELSE
 ELSE " %%%On ne peut pas utiliser une période de régularisation" &
@ENDIF
                IF PECRGL OF MCPERIODE_CTB = "1" &
 ELSE ""

ITEM G_DATSTP RESET AT INITIAL TO SYSDATE

ITEM G_PECCLE    = PECCLE    OF MCPERIODE_CIE
ITEM G_PECDATFIN = PECDATFIN OF MCPERIODE_CTB

ITEM G_HLDNBRPERCTB = HLDNBRPERCTB OF MCHOLDING
ITEM G_HLDECRAUT    = HLDECRAUT    OF MCHOLDING
ITEM G_HLDECRCOD    = HLDECRCOD    OF MCHOLDING

;
; Affectation d'erreurs
;
ITEM G_ERREUR = "ARRET" IF T_MESERR <> ""

;
; Sous-fichier pour les messages d'erreurs.
;
SUBFILE WIM701E KEEP &
                AT FINAL &
                IF T_MESERR <> "" &
        INCLUDE T_MESERR


;-------------------------------------------------------------------------------
;
;
; Sélectionne les immobilisations :
;       1) Qui sont actives et transférable (flag <> n).
;       2) Qui ont une aliénation pour la période.
;       3) Dont le montant d'aliénation est supérieur à 0.
;       4) Qui n'ont pas de no. d'écriture (jamais transféré)
;       5) Dont les comptes comptables sont <> ""
;
REQUEST SELECTION_IMMO     TERMINATE IF G_ERREUR = "ARRET" OR G_PECCLE = ""

ACCESS IMIMMOBILISATION &
  ;
  ; Compte comptable pour l'écriture d'aliénation.
  ;
  LINK CIECLE OF IMIMMOBILISATION &
    TO CIECLE OF IMCIE_PARAM      &
  ;
  ; Séquence des écritures de journal.
  ;
  LINK CIECLE    OF IMIMMOBILISATION , &
       G_PECCLE[1:2]                   &
    TO ECSCIECLE , &
       ECSANN OF GLECJ_SEQ OPTIONAL &
  ;
  ; Séquence des lots.
  ;
  LINK CIECLE OF IMIMMOBILISATION , &
       G_PECCLE                     &
    TO LGSCIECLE , &
       PECCLE OF GLLGL_SEQ OPTIONAL

CHOOSE CIECLE PARM, &
       IMMCLE PARM

SELECT IMIMMOBILISATION IF     IMMFLGTRS OF IMIMMOBILISATION = "1" &
                           AND IMMMNTALI OF IMIMMOBILISATION > 0   &
                           AND IMMPECALI OF IMIMMOBILISATION = " " &
                           AND "" <> TRUNCATE( IMMCPTACT OF IMIMMOBILISATION ) &
                           AND "" <> TRUNCATE( IMMCPTAMTACC OF IMIMMOBILISATION)

SELECT IMCIE_PARAM IF     "" <> TRUNCATE( IMPCPTGAIALI OF IMCIE_PARAM ) &
                      AND "" <> TRUNCATE( IMPCPTPERALI OF IMCIE_PARAM ) &
                      AND "" <> TRUNCATE( IMPCPTTRAALI OF IMCIE_PARAM )



TEMPORARY T_NUMSEQ       INTEGER UNSIGNED SIZE 4 ; Dernier numéro d'écriture
TEMPORARY T_ECSNUMSEQ    INTEGER          SIZE 4 ; Incrémentation # écriture
TEMPORARY T_LGLCLE       CHARACTER        SIZE 4 ; Numéro de lot
TEMPORARY T_UNA_ACC      CHARACTER        SIZE 8 ; Unité adm. de amort.acc.
TEMPORARY T_CODDEB       CHARACTER        SIZE 1 ; Code débit/crédit
TEMPORARY T_MNTGAIPER    FLOAT            SIZE 8 ; Gain ou perte

SORT ON CIECLE OF IMIMMOBILISATION &
     ON IMMCLE OF IMIMMOBILISATION

; Génération des numéros d'écriture :
; - Si les numéros d'écritures sont normalement saisie par l'usager
;   ('S' selon le holding, alors le numéro d'écriture généré sera
;   composé de la facon suivante :
;   . 1ere position  : Identifiant d'écriture selon le parametre du holding
;   . 2eme et 3eme   : Deux dernier caractere de l'annee (ex: 1992 -> 92)
;   . Postions 4 a 8 : Séquence de 1 a 9999 generer a partir du fichier
;                      de sequence.
; - Si les numéros d'écritures sont toujours générés par le systeme
;   ('A' selon le holding, alors le numéro d'écriture généré sera
;   composé de la facon suivante :
;   . Position 1 a 2 ; Deux dernier caractere de l'année (ex: 1992 -> 92)
;   . Postions 3 a 8 : Séquence de 1 a 9999 generer a partir du fichier
;                      de sequence.
;
;
;ITEM T_NUMSEQ INITIAL NCONVERT( G_PECCLE[1:2] ) * &
;                      (10 ^ (ECSLONNUM OF GLECJ_SEQ - 2) ) &
;                         IF NOT RECORD GLECJ_SEQ EXIST AND &
;                            G_HLDECRAUT = "A" &
;                 ELSE NCONVERT( G_PECCLE[1:2] ) * &
;                      (10 ^ (ECSLONNUM OF GLECJ_SEQ - 3) ) &
;                         IF NOT RECORD GLECJ_SEQ EXIST &
;                 ELSE ECSNUMSEQ OF GLECJ_SEQ


ITEM T_NUMSEQ INITIAL 0 &
            IF NOT RECORD GLECJ_SEQ EXIST &
            ELSE ECSNUMSEQ OF GLECJ_SEQ

;
; On incrémente le numéro d'écriture de journal par numéro d'immobilistion.
;
ITEM T_ECSNUMSEQ COUNT AT IMMCLE OF IMIMMOBILISATION &
                   RESET AT CIECLE TO T_NUMSEQ + 1

;
; Numéro de lot.
;
ITEM T_LGLCLE = ASCII(LGSSEQ OF GLLGL_SEQ + 1 ,4) RESET AT CIECLE

;
; Ajout du lien entre l'immobilisation et l'écritrue de journal.
;
OUTPUT IMIMM_SUIVI ADD
  ITEM CIECLE       OF IMIMM_SUIVI INITIAL CIECLE OF IMIMMOBILISATION
  ITEM IMMCLE       OF IMIMM_SUIVI INITIAL IMMCLE OF IMIMMOBILISATION
  ITEM PECCLE       OF IMIMM_SUIVI INITIAL G_PECCLE
  ; Ajustement pour le changement de siècle
  ITEM ECJCLENUM    OF IMIMM_SUIVI   =   G_PECCLE[1:2] + &
                                ASCII(T_ECSNUMSEQ ,ECSLONNUM OF GLECJ_SEQ - 2) &
                             IF G_HLDECRAUT = "A" &
                           ELSE G_HLDECRCOD + G_PECCLE[1:2] + &
                                ASCII(T_ECSNUMSEQ ,ECSLONNUM OF GLECJ_SEQ - 3)
  ITEM ECJCLESEQ    OF IMIMM_SUIVI INITIAL ""
  ITEM IMITYP       OF IMIMM_SUIVI INITIAL "AL"
  ;
  ; Montant à amortir.
  ;
  ITEM IMIMNTAMO    OF IMIMM_SUIVI =   IMMMNTINI OF IMIMMOBILISATION &
                                     + IMMMNTAJT OF IMIMMOBILISATION &
                                     - IMMMNTREC OF IMIMMOBILISATION
  ;
  ; Montant non amorti.
  ;
  ITEM IMIMNTNONAMT OF IMIMM_SUIVI =     IMIMNTAMO OF IMIMM_SUIVI &
                                       - IMMAMTACC OF IMIMMOBILISATION &
                                       - IMMMNTAMTANN OF IMIMMOBILISATION
  ITEM IMIMNTAMT    OF IMIMM_SUIVI = 0


;
; Mise à jour de la fiche d'immobilisation.
;
OUTPUT IMIMMOBILISATION UPDATE
  ;
  ; Inscrit la période traitée.
  ;
  ITEM IMMPECALI OF IMIMMOBILISATION = G_PECCLE



;
; Ajout de la ligne d'amortissement accumulé.
;
OUTPUT GLIMPUTATION ADD
  ITEM  ECJCIECLE  OF GLIMPUTATION INITIAL CIECLE    OF IMIMMOBILISATION
  ITEM  ECJCLENUM  OF GLIMPUTATION    =    ECJCLENUM OF IMIMM_SUIVI
  ITEM  ECJCLESEQ  OF GLIMPUTATION INITIAL ""
  ITEM  ECINUMLIG  OF GLIMPUTATION INITIAL 1
  ITEM  PRJCLE     OF GLIMPUTATION INITIAL ""
  ITEM  PRACLE     OF GLIMPUTATION INITIAL ""
  ITEM  CPTCLE     OF GLIMPUTATION INITIAL IMMCPTAMTACC OF IMIMMOBILISATION
  ITEM  CIECLE     OF GLIMPUTATION INITIAL CIECLE       OF IMIMMOBILISATION
  ;
  ; On prend l'unité adm. de l'immobilisation si l'unité adm du compte
  ; d'amortissement accumulé n'est pas spécifié.
  ;
  ITEM  UNACLE     OF GLIMPUTATION INITIAL UNACLE       OF IMIMMOBILISATION &
                          IF "" = TRUNCATE( IMMUNAAMTACC OF IMIMMOBILISATION ) &
                                      ELSE IMMUNAAMTACC OF IMIMMOBILISATION
  ITEM  ECICODDC   OF GLIMPUTATION INITIAL "D"
  ITEM  ECIMNT     OF GLIMPUTATION    =      IMMAMTACC    OF IMIMMOBILISATION &
                                           + IMMMNTAMTANN OF IMIMMOBILISATION


;
; Ajout de la ligne du compte transitoire.
;
OUTPUT GLIMPUTATION ADD &
                    ALIAS A_ECI_TRANSIT
  ITEM  ECJCIECLE  OF A_ECI_TRANSIT INITIAL CIECLE    OF IMIMMOBILISATION
  ITEM  ECJCLENUM  OF A_ECI_TRANSIT    =    ECJCLENUM OF IMIMM_SUIVI
  ITEM  ECJCLESEQ  OF A_ECI_TRANSIT INITIAL ""
  ITEM  ECINUMLIG  OF A_ECI_TRANSIT INITIAL 2
  ITEM  PRJCLE     OF A_ECI_TRANSIT INITIAL ""
  ITEM  PRACLE     OF A_ECI_TRANSIT INITIAL ""
  ITEM  CPTCLE     OF A_ECI_TRANSIT INITIAL IMPCPTTRAALI OF IMCIE_PARAM
  ITEM  CIECLE     OF A_ECI_TRANSIT INITIAL CIECLE       OF IMIMMOBILISATION
  ;
  ; On prend l'unité adm. de l'immobilisation si l'unité adm du compte
  ; transitoire n'est pas spécifiée.
  ;
  ITEM  UNACLE     OF A_ECI_TRANSIT INITIAL UNACLE       OF IMIMMOBILISATION &
                          IF "" = TRUNCATE( IMPUNATRAALI OF IMCIE_PARAM ) &
                                       ELSE IMPUNATRAALI OF IMCIE_PARAM
  ITEM  ECICODDC   OF A_ECI_TRANSIT INITIAL "D"
  ITEM  ECIMNT     OF A_ECI_TRANSIT    =    IMMMNTALI    OF IMIMMOBILISATION



;
; Si le résultat suivant est positif cela indique un gain, si le montant est
; négatif cela indique une perte.
;
ITEM T_MNTGAIPER = IMMMNTALI OF IMIMMOBILISATION &
                   - (   IMMMNTINI    OF IMIMMOBILISATION &
                       + IMMMNTAJT    OF IMIMMOBILISATION &
                       - IMMAMTACC    OF IMIMMOBILISATION &
                       - IMMMNTAMTANN OF IMIMMOBILISATION )


;
; Ajout de la ligne du compte de perte.
;
OUTPUT GLIMPUTATION ADD &
                    ALIAS A_ECI_PERTE &
                    IF T_MNTGAIPER < 0
  ITEM  ECJCIECLE  OF A_ECI_PERTE INITIAL CIECLE    OF IMIMMOBILISATION
  ITEM  ECJCLENUM  OF A_ECI_PERTE    =    ECJCLENUM OF IMIMM_SUIVI
  ITEM  ECJCLESEQ  OF A_ECI_PERTE INITIAL ""
  ITEM  ECINUMLIG  OF A_ECI_PERTE INITIAL 3
  ITEM  PRJCLE     OF A_ECI_PERTE INITIAL ""
  ITEM  PRACLE     OF A_ECI_PERTE INITIAL ""
  ITEM  CPTCLE     OF A_ECI_PERTE INITIAL IMPCPTPERALI OF IMCIE_PARAM
  ITEM  CIECLE     OF A_ECI_PERTE INITIAL CIECLE       OF IMIMMOBILISATION
  ;
  ; On prend l'unité adm. de l'immobilisation si l'unité adm du compte
  ; de perte n'est pas spécifiée.
  ;
  ITEM  UNACLE     OF A_ECI_PERTE INITIAL UNACLE       OF IMIMMOBILISATION &
                          IF "" = TRUNCATE( IMPUNAPERALI OF IMCIE_PARAM ) &
                                       ELSE IMPUNAPERALI OF IMCIE_PARAM
  ITEM  ECICODDC   OF A_ECI_PERTE INITIAL "D"
  ITEM  ECIMNT     OF A_ECI_PERTE    =    ABSOLUTE( T_MNTGAIPER ) &
                                            IF T_MNTGAIPER < 0    &
                                     ELSE 0


;
; Ajout de la ligne du compte de gain.
;
OUTPUT GLIMPUTATION ADD &
                    ALIAS A_ECI_GAIN &
                    IF T_MNTGAIPER >= 0
  ITEM  ECJCIECLE  OF A_ECI_GAIN INITIAL CIECLE    OF IMIMMOBILISATION
  ITEM  ECJCLENUM  OF A_ECI_GAIN    =    ECJCLENUM OF IMIMM_SUIVI
  ITEM  ECJCLESEQ  OF A_ECI_GAIN INITIAL ""
  ITEM  ECINUMLIG  OF A_ECI_GAIN INITIAL 3
  ITEM  PRJCLE     OF A_ECI_GAIN INITIAL ""
  ITEM  PRACLE     OF A_ECI_GAIN INITIAL ""
  ITEM  CPTCLE     OF A_ECI_GAIN INITIAL IMPCPTGAIALI OF IMCIE_PARAM
  ITEM  CIECLE     OF A_ECI_GAIN INITIAL CIECLE       OF IMIMMOBILISATION
  ;
  ; On prend l'unité adm. de l'immobilisation si l'unité adm du compte
  ; de gain n'est pas spécifiée.
  ;
  ITEM  UNACLE     OF A_ECI_GAIN INITIAL UNACLE       OF IMIMMOBILISATION &
                          IF "" = TRUNCATE( IMPUNAGAIALI OF IMCIE_PARAM ) &
                                       ELSE IMPUNAGAIALI OF IMCIE_PARAM
  ITEM  ECICODDC   OF A_ECI_GAIN INITIAL "C"
  ITEM  ECIMNT     OF A_ECI_GAIN    =    T_MNTGAIPER &
                                           IF T_MNTGAIPER >= 0 &
                                    ELSE 0


;
; Ajout de la ligne du compte d'actif(immobilisation).
;
OUTPUT GLIMPUTATION ADD &
                    ALIAS A_ECI_ACTIF
  ITEM  ECJCIECLE  OF A_ECI_ACTIF INITIAL CIECLE    OF IMIMMOBILISATION
  ITEM  ECJCLENUM  OF A_ECI_ACTIF    =    ECJCLENUM OF IMIMM_SUIVI
  ITEM  ECJCLESEQ  OF A_ECI_ACTIF INITIAL ""
  ITEM  ECINUMLIG  OF A_ECI_ACTIF INITIAL 4
  ITEM  PRJCLE     OF A_ECI_ACTIF INITIAL ""
  ITEM  PRACLE     OF A_ECI_ACTIF INITIAL ""
  ITEM  CPTCLE     OF A_ECI_ACTIF INITIAL IMMCPTACT OF IMIMMOBILISATION
  ITEM  CIECLE     OF A_ECI_ACTIF INITIAL CIECLE    OF IMIMMOBILISATION
  ;
  ; On prend l'unité adm. de l'immobilisation si l'unité adm du compte
  ; d'actif accumulé n'est pas spécifiée.
  ;
  ITEM  UNACLE     OF A_ECI_ACTIF INITIAL UNACLE       OF IMIMMOBILISATION &
                          IF "" = TRUNCATE( IMMUNAACT OF IMIMMOBILISATION ) &
                                      ELSE IMMUNAACT OF IMIMMOBILISATION
  ITEM  ECICODDC   OF A_ECI_ACTIF INITIAL "C"
  ITEM  ECIMNT     OF A_ECI_ACTIF    =       IMMMNTINI OF IMIMMOBILISATION &
                                           + IMMMNTAJT OF IMIMMOBILISATION



;
; Ajout des écritures de journal.
;
OUTPUT GLECRITURE_JG ADD
  ITEM ECJCIECLE    OF GLECRITURE_JG INITIAL CIECLE    OF IMIMMOBILISATION
  ITEM ECJCLENUM    OF GLECRITURE_JG    =    ECJCLENUM OF IMIMM_SUIVI
  ITEM ECJCLESEQ    OF GLECRITURE_JG INITIAL ""
  ITEM PECCLE       OF GLECRITURE_JG INITIAL G_PECCLE
  ITEM LGLCLE       OF GLECRITURE_JG    =    T_LGLCLE
  ITEM ECJFLGINTCIE OF GLECRITURE_JG INITIAL "0"
  ITEM ECJTYPECR    OF GLECRITURE_JG INITIAL "RG"
  ITEM ECJFLGANN    OF GLECRITURE_JG INITIAL "0"
  ITEM ECJDAT       OF GLECRITURE_JG INITIAL G_PECDATFIN
  ITEM ECJDATSTP    OF GLECRITURE_JG INITIAL G_DATSTP
  ITEM ECJDATJOU    OF GLECRITURE_JG INITIAL 0
  ITEM ECJHREJOU    OF GLECRITURE_JG INITIAL 0
  ITEM ECJDSC1      OF GLECRITURE_JG INITIAL &
                                       "Transfert d'aliénation - Immobilisation"
  ITEM ECJDSC2      OF GLECRITURE_JG INITIAL "Numéro immo.: " + &
                                             IMMCLE OF IMIMMOBILISATION
  ITEM ECJDSC3      OF GLECRITURE_JG INITIAL ""
  ITEM ECJCIEINT    OF GLECRITURE_JG INITIAL ""
  ITEM ECJTRFUNA    OF GLECRITURE_JG INITIAL "0"
  ITEM ECJNBRPERINI OF GLECRITURE_JG INITIAL 0
  ITEM ECJNBRPERRES OF GLECRITURE_JG INITIAL 0
  ITEM ECJPECDES    OF GLECRITURE_JG INITIAL ""
  ITEM ECJMNTDBT    OF GLECRITURE_JG    =      ECIMNT OF GLIMPUTATION &
                                             + ECIMNT OF A_ECI_TRANSIT &
                                             + ECIMNT OF A_ECI_PERTE
  ITEM ECJMNTCRT    OF GLECRITURE_JG    =      ECIMNT OF A_ECI_ACTIF &
                                             + ECIMNT OF A_ECI_GAIN
  ITEM ECJNUMLIG    OF GLECRITURE_JG INITIAL 4
  ITEM HISCLE       OF GLECRITURE_JG INITIAL 0
  ITEM ECJUSRCRE    OF GLECRITURE_JG INITIAL LOGONID
  ITEM ECJUNAINT1   OF GLECRITURE_JG INITIAL ""
  ITEM ECJUNAINT2   OF GLECRITURE_JG INITIAL ""
  ITEM ECJUNANBR1   OF GLECRITURE_JG INITIAL 0
  ITEM ECJUNANBR2   OF GLECRITURE_JG INITIAL 0



;
; Sous-fichier pour l'impression des écritures généré.
;
SUBFILE WIM701A KEEP &
  INCLUDE CIECLE    OF IMIMMOBILISATION , &
          IMMCLE    OF IMIMMOBILISATION , &
          IMMDSC    OF IMIMMOBILISATION , &
          IMMMNTALI OF IMIMMOBILISATION , &
          ECJCLENUM OF IMIMM_SUIVI      , &
          G_PECCLE                      , &
          T_LGLCLE

;
; NOTE : Nous devons gerer les cumulatifs du lot ici, car le trigger T_ECJ
;        ne fonctionnera pas dans ce cas-ci.
;        Raison : Le lot sera créer une fois que toutes les écritures de
;                 journal se référant a ce lot seront créées.
;                 Donc, dans le 'store' du trigger, on ne trouvera pas le lot.
;
OUTPUT GLLOT ADD &
             AT CIECLE
  ITEM LGLCIECLE    OF GLLOT INITIAL CIECLE   OF IMIMMOBILISATION
  ITEM LGLCLE       OF GLLOT    =    T_LGLCLE
  ITEM PECCLE       OF GLLOT INITIAL G_PECCLE
  ITEM LGLTYPECR    OF GLLOT INITIAL "RG"
  ITEM LGLUSR       OF GLLOT INITIAL LOGONID
  ITEM LGLDSC       OF GLLOT INITIAL "Transfert de l'aliénation"
  ITEM LGLDATVAL    OF GLLOT INITIAL 0
  ITEM LGLDATJOU    OF GLLOT INITIAL 0
  ITEM LGLHREJOU    OF GLLOT INITIAL 0
  ITEM LGLUSRJOU    OF GLLOT INITIAL ""
  ITEM LGLATRJOU    OF GLLOT INITIAL "0"
  ITEM LGLUSRATR    OF GLLOT INITIAL ""
  ITEM LGLNBRTRS    OF GLLOT    =    LGLNBRTRS OF GLLOT + 1 &
                                       RESET AT CIECLE
  ITEM LGLCUMMNT    OF GLLOT SUBTOTAL ECJMNTDBT OF GLECRITURE_JG &
                                      RESET AT CIECLE
  ITEM LGLNBRTRSERR OF GLLOT INITIAL 0
  ITEM LGLCUMMNTVER OF GLLOT    =    LGLCUMMNT OF GLLOT
  ITEM LGLNBRTRSVER OF GLLOT    =    LGLNBRTRS OF GLLOT
  ITEM LGLDATCRE    OF GLLOT INITIAL SYSDATE
  ITEM LGLUSRCRE    OF GLLOT INITIAL LOGONID
  ITEM LGLHRECRE    OF GLLOT INITIAL SYSTIME / 10000
  ITEM LGLDATMOD    OF GLLOT INITIAL 0
  ITEM LGLHREMOD    OF GLLOT INITIAL 0
  ITEM LGLUSRMOD    OF GLLOT INITIAL ""
  ITEM LGLUSRVAL    OF GLLOT INITIAL ""



;
; Mise à jour du fichier de séquence des numéros d'écritures.
;
OUTPUT GLECJ_SEQ ADD UPDATE &
                 AT FINAL   &
                 NOITEM
  ITEM ECSCIECLE OF GLECJ_SEQ INITIAL CIECLE OF IMIMMOBILISATION
  ITEM ECSANN    OF GLECJ_SEQ INITIAL G_PECCLE[1:2]
  ITEM ECSNUMSEQ OF GLECJ_SEQ    =    T_ECSNUMSEQ


;
; Maj du numéro de sequence des lots.
;
OUTPUT GLLGL_SEQ ADD UPDATE &
                 AT CIECLE
  ITEM LGSCIECLE OF GLLGL_SEQ INITIAL CIECLE OF IMIMMOBILISATION
  ITEM PECCLE    OF GLLGL_SEQ INITIAL G_PECCLE
  ITEM LGSSEQ    OF GLLGL_SEQ    =    NCONVERT(T_LGLCLE)


BUILD
