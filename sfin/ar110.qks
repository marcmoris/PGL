;/T/ Consultation des réceptions        CIECLE, FOUCIECLE, FOUCLE, RECCLE
;
;/P/ Programmeur..: Nancy Marceau
;    Date Création: 22 mars 1994
;
;    Description..: Permet de visionner et de choisir une réception
;
;    Paramètre....: CIECLE ==> - compagnie de la commande
;                              - segment de la clé de recherche.
;
;                   FOUCIECLE ==> - compagnie du fournisseur
;                                 - segment de la clé de recherche  
;
;                   FOUCLE ==> - fournisseur
;                              - segment de la clé de recherche  
;
;                   RECCLE ==> - segment de la clé de recherche.
;                              - peut contenir le caractère '@'.
;                              - contient la valeur de retour.
;
;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN ar110 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING  T_CIECLE, &
                        T_FOUCIECLE, &
                        T_FOUCLE, &
                        T_RECCLE 
 
                    

USE ustrasse NOLIST

USE ussectmp NOLIST     
    "AR110"

USE ar110.hlp NOLIST

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Compagnie de la réception
TEMPORARY T_RECCLE    CHARACTER SIZE 12; Code de réception
TEMPORARY T_FOUCIECLE CHARACTER SIZE 4 ; Compagnie du fournisseur
TEMPORARY T_FOUCLE    CHARACTER SIZE 6 ; Numéro de fournisseur


;-------------------------------------------------------------------------------
;
FILE ARRECEPTION       PRIMARY &
                      OCCURS 11

 ACCESS   VIA     CIECLE,    &
                  RECCLE,    &
                  FOUCIECLE, &
                  FOUCLE     & 
          USING   T_CIECLE,    &
                  T_RECCLE,    &
                  T_FOUCIECLE, &
                  T_FOUCLE     &
          ORDERBY CIECLE , &
                  RECCLE   
  
   ;
   ; On ne sélectionne pas les réceptions préliminaires et 
   ; complètes ainsi que les réceptions non journalisées.
   ;
 SELECT IF (   RECSTU OF ARRECEPTION = "A"  &
            OR RECSTU OF ARRECEPTION = "S") &
           AND RECDATJOU OF ARRECEPTION <> 0 


;-------------------------------------------------------------------------------
;

USE ushilite NOLIST 	
USE usdrwfnd NOLIST 	

SKIP 1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN(3,3,6)(,,19)

CLUSTER OCCURS WITH ARRECEPTION

FIELD RECCLE    OF ARRECEPTION HIDDEN LABEL ">>"
FIELD RECCOM    OF ARRECEPTION SIZE 15

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_RECCLE, "@")
  THEN LET T_RECCLE = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisie par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_RECCLE = RECCLE OF ARRECEPTION
  RETURN
END

BUILD
@IF FORMAT
                                       *****************************************
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       * >> xxxxxxxxxxxx xxxxxxxxxxxxxxx       *
                                       *****************************************
@ENDIF
