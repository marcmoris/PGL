;/T/ Liste des blocs achetés non-facturés par fournisseur.
;
;/M/ Réalisé par.......: Marc Poulin     
;    Date..............: 06 avril 2000
;    Description.......: Correction du traitement afin qu'il fonctionne selon les spécifications du client.
;
;    Référence.........: Stabilisation du module Gestion de blocs (point 07).
;
;    ------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin
;    Date..............: 24 mai 2000
;    Description.......: Ajout de la date de production du bloc et du code de client à qui est assigné le bloc et reformattage
;                        partiel du rapport.
;
;    Référence.........: Demande de changement 000062.
;
;    Signet............: MP-00-05-24_D62.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
; Configuration standard
;
USE ussetqzs NOLIST
;
; Déclaration des accès
;
ACCESS IFBLOC LINK ("----"                    , & ; Table des blocs
                    FOUCLE    OF IFBLOC)        &
                 TO FOUCIECLE                 , &
                    FOUCLE    OF CPFOURNISSEUR  & ; Table des fournisseurs

              LINK  CIECLE    OF IFBLOC       , & 
                    TGRCODGRA OF IFBLOC         &
                TO  CIECLE                    , &
                    TGRCODGRA OF PDTYPE_GRANIT    ; Table des types de granit
;
; Sélection selon les paramètres saisis par l'utilisateur
;
CHOOSE CIECLE    PARM, & ; Code de compagnie
       FOUCLE    PARM, & ; Code de fournisseur
       TGRCODGRA PARM, & ; Type de granit
       CAPCLE    " "     ; Numéro de facture (compte à payer) - Sélection des blocs non facturés uniquement
;
; Définition du numéro de commande et du numéro de bloc
;
DEFINE D_NUMCOM  CHARACTER SIZE 09 = PJTABR OF IFBLOC    &
                                   + "-"                 &
                                   + PJTANN OF IFBLOC    &
                                   + "-"                 &
                                   + COMNUMCOMINT     

DEFINE D_NUMBLC  CHARACTER SIZE 14 = TGRCODGRA OF IFBLOC &
                                   + IFCCLE    OF IFBLOC &
                                   + "-"                 &
                                   + BLCCLE    OF IFBLOC &
                                   + "-"                 &
                                   + BLCRED    OF IFBLOC
;
; Variables associées aux entêtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF320"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM
;
; Tri
;
SORT ON FOUCLE    OF IFBLOC & ; Code de fournisseur
     ON TGRCODGRA OF IFBLOC & ; Type de granit
     ON D_NUMBLC              ; Numéro de bloc
;
; Entête du rpport - Impression 60 lignes par 132 colonnes
;
USE ustit132_60 NOLIST 
 SKIP 2                                     &
  TAB 001 "Fournisseur:"                    &
  TAB 014 FOUCLE OF IFBLOC                  & ; Code de fournisseur
  TAB 021 FOUNOM OF CPFOURNISSEUR           & ; Nom du fournisseur
 SKIP 2                                     &
  TAB 001 "  Type de granit                                Numéro de bloc  Date de production  Client  Quantité (m3) " &
 SKIP 1                                     &
  TAB 001 "  --------------------------------------------  --------------  ------------------  ------  --------------" &
 SKIP
;
; Détail du rapport
;
REPORT                                      &
  TAB 003 TGRCODGRA    OF IFBLOC            & ; Type de granit
          PRINT AT TGRCODGRA                &
  TAB 007 TGRDSCFRA001 OF PDTYPE_GRANIT     & ; Description du type de granit
          PRINT AT TGRCODGRA                &
  TAB 049 D_NUMBLC                          & ; Numéro de bloc
  TAB 069 BLCDAT       OF IFBLOC            & ; Date de production du bloc              ; MP-00-05-24_D62
          FORMAT YYYYMMDD                   &                                           ; MP-00-05-24_D62
  TAB 085 CLICLE       OF IFBLOC            & ; Client à qui est assigné le bloc        ; MP-00-05-24_D62
  TAB 093 BLCM3TOT     OF IFBLOC            & ; Quantité totale du bloc en mêtres cube
          PICTURE "^^^,^^^,^^^.^^"          &
          SIGNIFICANCE 4                    &
          NOBWZ                             &
  RESERVE 5 LINES
;
; Sous-total par type de granit
;
FOOTING AT TGRCODGRA                        &
  TAB 093 "--------------"                  &
 SKIP 1                                     &
  TAB 093 BLCM3TOT     OF IFBLOC            & ; Quantité totale du bloc en mêtres cube
          SUBTOTAL                          &
          PICTURE "^^^,^^^,^^^.^^"          &
          SIGNIFICANCE 4                    &
          NOBWZ                             &
 SKIP 2
;
; Total par code de fournisseur
;
FOOTING AT FOUCLE                           &
  TAB 093 "=============="                  &
 SKIP 1                                     &
  TAB 093 BLCM3TOT     OF IFBLOC            & ; Quantité totale du bloc en mêtres cube
          SUBTOTAL                          &
          PICTURE "^^^,^^^,^^^.^^"          &
          SIGNIFICANCE 4                    &
          NOBWZ                             &
 SKIP PAGE


BUILD if320z


@IF FORMAT
IF320                                               GRANICOR INC. (Développement)                                     PAGE:   1
marcpoul                               Liste des blocs achetés non-facturés par fournisseur                        00/04/06  14:16

Fournisseur: XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

  Type de granit                                Numéro de bloc  Date de production  Client  Quantité (m3) 
  --------------------------------------------  --------------  ------------------  ------  --------------
  XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  999XX-999999-X      9999/99/99      XXXXXX  999,999,999.99
                                                999XX-999999-X      9999/99/99      XXXXXX  999,999,999.99
                                                999XX-999999-X      9999/99/99      XXXXXX  999,999,999.99
                                                                                            --------------
                                                                                            999,999,999.99
                                                                                                          
  XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  999XX-999999-X      9999/99/99      XXXXXX  999,999,999.99
                                                999XX-999999-X      9999/99/99      XXXXXX  999,999,999.99
                                                                                            --------------
                                                                                            999,999,999.99
                                                                                            ==============
                                                                                            999,999,999.99
@ENDIF

