;/T/ Reste à emballer par priorité
;
;/P/ Programmeur...: Martin Bruyère
;    Date Création.: 1995-07-10
;    Particularités: IL y a 2 sources.
;    Description...: Ce rapport permet d'imprimer de débitage des diagrammes
;                    pour une commande interne.
;
;/M/ Programmeur...: Rémy Demers
;    Date..........: 97-12-04
;    Description...: Modification mineur apporter sur les liens car le subfile
;                    est maintenant plus complet
;--------------------------------------------------------
;
;/M/ Modifié par...: Marc Poulin.
;    Date..........: 2001-06-13.
;    Description...: Ajout des information relativement aux unités de travail.
;
;    Référence.....: Demande de changement 000138.
;
;    Signet........: MP-01-06-13_D138.
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDTEMPO_EMBAL &

  LINK DIANUM002  OF PDTEMPO_EMBAL , &
       CIECLE     OF PDTEMPO_EMBAL , &
       PRICODPRI  OF PDTEMPO_EMBAL   &
     TO DIANUM002 ,                  &
        CIECLE    ,                  &
        PRICODPRI                    OF PDPRIORITE_DIAG &

  LINK PJTABR       OF PDPRIORITE_DIAG, &
       PJTANN       OF PDPRIORITE_DIAG, &
       COMNUMCOMINT OF PDPRIORITE_DIAG, &
       PRICODPRI    OF PDPRIORITE_DIAG, &
       CIECLE       OF PDPRIORITE_DIAG  &
    TO PJTABR                      , &
       PJTANN                      , &
       COMNUMCOMINT                , &
       PRICODPRI                   , &
       CIECLE                        OF PDPRIORITE &

   LINK DIANUM002 OF PDTEMPO_EMBAL , &
        CIECLE    OF PDTEMPO_EMBAL   &
     TO DIANUM002                  , &
        CIECLE                       OF PDDIAGRAMME &

  LINK PJTABR       OF PDDIAGRAMME, &
       PJTANN       OF PDDIAGRAMME, &
       COMNUMCOMINT OF PDDIAGRAMME, &
       CIECLE       OF PDDIAGRAMME  &
    TO PJTABR      , &
       PJTANN      , &
       COMNUMCOMINT, &
       CIECLE        OF PDCOMMAN_INTERNE

DEFINE D_PRI_LIG CHAR SIZE 6 = TGRCODGRA OF PDDIAGRAMME + &
                               DCINUMLIG OF PDTEMPO_EMBAL

SORT  ON COMNUMCOM OF PDTEMPO_EMBAL &
      ON PRICODPRI OF PDTEMPO_EMBAL &
      ON D_PRI_LIG                  &
      ON DIANUM002 OF PDTEMPO_EMBAL

;-------------------------------------------------------------------------------
;
DEFINE D_DIALON NUMERIC            = DIALON OF PDDIAGRAMME + 4
DEFINE D_DIAHAU NUMERIC            = DIAHAU OF PDDIAGRAMME + 4
DEFINE D_QTE_A_DBT_1 NUMERIC       = DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_EMBAL &       ;DIAQTEEMB OF PDDIAGRAMME &
                                     IF 0 < DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_EMBAL & ;DIAQTEEMB OF PDDIAGRAMME &
                                     ELSE 0

DEFINE D_QTE_A_DBT_2 NUMERIC       = PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_EMBAL &         ;PDGQTECPEPRDGRA  OF PDPRIORITE_DIAG &
                                     IF 0 < PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_EMBAL &         ;PDGQTECPEPRDGRA  OF PDPRIORITE_DIAG &
                                     ELSE 0

DEFINE D_QTE_A_DBT NUMERIC         = D_QTE_A_DBT_1 IF D_QTE_A_DBT_1 < D_QTE_A_DBT_2 ELSE D_QTE_A_DBT_2


  SELECT IF D_QTE_A_DBT > 0


DEFINE D_SUR_MC2 FLOAT SIZE 08 = DIASURMC2 OF PDDIAGRAMME
DEFINE D_QTE_PLA_GRA   INTEGER SIZE 04 = PDGQTEPLAFINGRA OF PDPRIORITE_DIAG
DEFINE D_QTE_FIN_GRA   INTEGER SIZE 04 = PDGQTEFINPRDGRA OF PDPRIORITE_DIAG
DEFINE D_QTE_A_DBT_SUR FLOAT SIZE 4 = D_QTE_A_DBT * D_SUR_MC2
DEFINE D_SUR_PLA_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_PLA_GRA
DEFINE D_SUR_FIN_GRA FLOAT SIZE 08 = D_SUR_MC2 * D_QTE_FIN_GRA
DEFINE D_QTE_EMBAL NUMERIC =  PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - D_QTE_A_DBT
DEFINE D_VOLMC2 FLOAT SIZE 08      = D_QTE_A_DBT * DIASURMC2 OF PDDIAGRAMME
DEFINE D_ETOILE CHARACTER SIZE 1   = "*" IF DIATYPDIA OF PDDIAGRAMME = "T" &
                                ELSE " "

; --------------------------------------------------------------------
; MP-01-06-13_D138

DEFINE D_VIRG_1 CHARACTER SIZE 02 = " ," IF T_DESCR_2 <> " " ELSE " "
DEFINE D_VIRG_2 CHARACTER SIZE 02 = " ," IF T_DESCR_3 <> " " ELSE " "
DEFINE D_VIRG_3 CHARACTER SIZE 02 = " ," IF T_DESCR_4 <> " " ELSE " "
DEFINE D_VIRG_4 CHARACTER SIZE 02 = " ," IF T_DESCR_5 <> " " ELSE " "

DEFINE D_DESCR  CHARACTER SIZE 26 = TRUNCATE(T_DESCR_1) + &
                                    TRUNCATE(D_VIRG_1)  + &
                                    TRUNCATE(T_DESCR_2) + &
                                    TRUNCATE(D_VIRG_2)  + &
                                    TRUNCATE(T_DESCR_3) + &
                                    TRUNCATE(D_VIRG_3)  + &
                                    TRUNCATE(T_DESCR_4) + &
                                    TRUNCATE(D_VIRG_4)  + &
                                    TRUNCATE(T_DESCR_5)   &
                                 IF T_DESCR_1 <> " "      &
                               ELSE "EMBALLAGE"

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Reste à emballer par priorité"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD656ZA"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitgor200_45 NOLIST
  SKIP &

;PAGE HEADING &
  TAB 001 "Num. commande.:" &
  TAB 017 PJTABR OF PDPRIORITE_DIAG &
  TAB 019 "-" &
  TAB 020 COMNUMCOMINT OF PDPRIORITE_DIAG &
  TAB 024 COMNOM OF PDCOMMAN_INTERNE &
  TAB 071 "Tolérence" &
  SKIP &
  TAB 001 "Priorité......:" &
  TAB 017 PRICODPRI OF PDPRIORITE_DIAG &
  TAB 021 PRIDATFIN OF PDPRIORITE &
  TAB 071 "Épaisseur en +:" &
  TAB 087 COMEPAPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Épaisseur en -:" &
  TAB 110 COMEPAMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Dimension en +:" &
  TAB 087 COMDIMPLU OF PDCOMMAN_INTERNE &
  TAB 094 "Dimension en -:" &
  TAB 110 COMDIMMOI OF PDCOMMAN_INTERNE &
  SKIP &
  TAB 071 "Critères esthé:" &
  TAB 087 COMCRIEST001 OF PDCOMMAN_INTERNE PICTURE "^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^" &
  SKIP 2 &
  TAB 001  "                  *------------------- Granicor -----------------------*  "                                             &  ; MP-01-06-13_D138
  SKIP &
   TAB 001 "Diag.        Cmd   Plan.dbt    Dbt   Plan.fin     Fin    Emb  Reste.emb   Gra   Fi    Épai  Long   Haut  Reste à faire" &  ; MP-01-06-13_D138
  SKIP 2

FOOTING AT DIANUM002 &
  TAB 001 DIANUM002 OF PDPRIORITE_DIAG &
  TAB 012 DIAQTECMD OF PDDIAGRAMME &
  TAB 020 PDGQTEPLACPEGRA OF PDPRIORITE_DIAG &
  TAB 030 PDGQTECPEPRDGRA OF PDPRIORITE_DIAG &
  TAB 038 PDGQTEPLAFINGRA OF PDPRIORITE_DIAG &
  TAB 048 PDGQTEFINPRDGRA OF PDPRIORITE_DIAG &
  TAB 054 D_QTE_EMBAL &
  TAB 063 D_QTE_A_DBT PICTURE "^^^^^" &
  TAB 075 TGRCODGRA OF PDDIAGRAMME &
  TAB 081 TYFCODFIN OF PDDIAGRAMME &
  TAB 086 DIAEPA OF PDDIAGRAMME &
  TAB 093 DIALON OF PDDIAGRAMME &
  TAB 100 DIAHAU OF PDDIAGRAMME &
  TAB 106 "("                   & ; MP-01-06-13_D138
  TAB 107 D_DESCR               & ; MP-01-06-13_D138
  TAB 132 ")"                   & ; MP-01-06-13_D138
  SKIP 1

FOOTING AT PRICODPRI &
  SKIP 3 &
  TAB 001  "              *-------------------- Granicor -----------------------*  "   & ; MP-01-06-13_D138
  SKIP &
  TAB 001 "                Planifier          Planifier.Fini         Reste embal "     & ; MP-01-06-13_D138
  SKIP &
  TAB 001 "Total M2:" &
  TAB 015 D_SUR_PLA_GRA   SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 & ; MP-01-06-13_D138
  TAB 035 D_SUR_FIN_GRA   SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 & ; MP-01-06-13_D138
  TAB 055 D_QTE_A_DBT_SUR SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 & ; MP-01-06-13_D138
SKIP PAGE

FOOTING AT COMNUMCOM SKIP PAGE

FINAL FOOTING &
  SKIP 3 &
  TAB 001  "              *-------------------- Granicor -----------------------*  "   & ; MP-01-06-13_D138
  SKIP &
  TAB 001 "                Planifier          Planifier.Fini         Reste embal "     & ; MP-01-06-13_D138
  SKIP &
  TAB 001 "Grand total M2:" &
  TAB 015 D_SUR_PLA_GRA   SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 & ; MP-01-06-13_D138
  TAB 035 D_SUR_FIN_GRA   SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6 & ; MP-01-06-13_D138
  TAB 055 D_QTE_A_DBT_SUR SUBTOTAL PICTURE "^^^^^^.^^^^" OUTPUT SCALE 4 SIGNIFICANCE 6   ; MP-01-06-13_D138


BUILD pd656za

;-------------------------------------------------------------------------------
;/T/ Reste à emballer par priorité et unité de travail.
;
;/M/ Réalisé par...: Marc Poulin.
;    Date..........: 2000-04-03.
;    Description...: Quantité à emballer par priorité et unité de travail.
;
;    Référence.....: Demande de changement 000016.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDTEMPO_EMBAL LINK DIANUM002    OF PDTEMPO_EMBAL            , &
                          CIECLE       OF PDTEMPO_EMBAL            , &
                          PRICODPRI    OF PDTEMPO_EMBAL              &
                       TO DIANUM002                                , &
                          CIECLE                                   , &
                          PRICODPRI    OF PDPRIORITE_DIAG            &

                     LINK PJTABR       OF PDPRIORITE_DIAG          , &
                          PJTANN       OF PDPRIORITE_DIAG          , &
                          COMNUMCOMINT OF PDPRIORITE_DIAG          , &
                          PRICODPRI    OF PDPRIORITE_DIAG          , &
                          CIECLE       OF PDPRIORITE_DIAG            &
                       TO PJTABR                                   , &
                          PJTANN                                   , &
                          COMNUMCOMINT                             , &
                          PRICODPRI                                , &
                          CIECLE       OF PDPRIORITE                 &

                     LINK DIANUM002    OF PDTEMPO_EMBAL            , &
                          CIECLE       OF PDTEMPO_EMBAL              &
                       TO DIANUM002                                , &
                          CIECLE       OF PDDIAGRAMME                &

                     LINK PJTABR       OF PDDIAGRAMME              , &
                          PJTANN       OF PDDIAGRAMME              , &
                          COMNUMCOMINT OF PDDIAGRAMME              , &
                          CIECLE       OF PDDIAGRAMME                &
                       TO PJTABR                                   , &
                          PJTANN                                   , &
                          COMNUMCOMINT                             , &
                          CIECLE       OF PDCOMMAN_INTERNE           &

                     LINK DIANUM002    OF PDDIAGRAMME              , &
                          CIECLE       OF PDDIAGRAMME                &
                       TO DIANUM002                                , &
                          CIECLE       OF PDUNI_TRAV_DIA   OPTIONAL  &

                     LINK CIECLE                                   , &
                          UTRNUMUNITRV OF PDUNI_TRAV_DIA             &
                       TO CIECLE                                   , &
                          UTRNUMUNITRV OF PDUNITE_TRAVAIL  OPTIONAL

;-------------------------------------------------------------------------------

DEFINE D_QTE_A_DBT_1 NUMERIC = DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_EMBAL &      
                        IF 0 < DIAQTECMD OF PDDIAGRAMME - T_SUM OF PDTEMPO_EMBAL 

DEFINE D_QTE_A_DBT_2 NUMERIC = PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_EMBAL & 
                        IF 0 < PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - T_SUM OF PDTEMPO_EMBAL 

DEFINE D_QTE_A_DBT NUMERIC   = D_QTE_A_DBT_1                 &
                            IF D_QTE_A_DBT_1 < D_QTE_A_DBT_2 &
                          ELSE D_QTE_A_DBT_2

SELECT IF D_QTE_A_DBT > 0

;-------------------------------------------------------------------------------

DEFINE D_PRI_LIG      CHARACTER SIZE 06 = TGRCODGRA OF PDDIAGRAMME &
                                        + DCINUMLIG OF PDTEMPO_EMBAL

DEFINE D_UTRNUMUNITRV CHARACTER SIZE 03 = RJ(UTRNUMUNITRV OF PDUNI_TRAV_DIA)

SORT ON COMNUMCOM    OF PDTEMPO_EMBAL  &
     ON PRICODPRI    OF PDTEMPO_EMBAL  &
     ON D_UTRNUMUNITRV                 &
     ON D_PRI_LIG                      &
     ON DIANUM002    OF PDTEMPO_EMBAL

;-------------------------------------------------------------------------------

DEFINE D_QTE_EMBAL NUMERIC       =  PDGQTEPLACPEGRA OF PDPRIORITE_DIAG - D_QTE_A_DBT

DEFINE D_QTE_UNTRA FLOAT SIZE 08 =  ROUND(D_QTE_A_DBT * UTDQTEPRD OF PDUNI_TRAV_DIA,4) * 10000

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Reste à emballer par priorité et unité de travail"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD656ZB"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

;-------------------------------------------------------------------------------

USE ustitstd45 NOLIST
 SKIP 1                                          &
  TAB 001 "Numéro de commande:"                  &
  TAB 021 PJTABR       OF PDPRIORITE_DIAG        &
  TAB 023 "-"                                    &
  TAB 024 COMNUMCOMINT OF PDPRIORITE_DIAG        &
  TAB 028 COMNOM       OF PDCOMMAN_INTERNE       &
 SKIP 1                                          &
  TAB 001 "Priorité..........:"                  &
  TAB 021 PRICODPRI    OF PDPRIORITE_DIAG        &
  TAB 028 PRIDATFIN    OF PDPRIORITE             &
          FORMAT YYYYMMDD                        &
 SKIP 2                                          &
  TAB 001 "                                                                            Reste à     Quantité"                                    &
 SKIP 1                                          &
  TAB 001 "              Unité de travail                Diagramme  Commandé  Emballé  emballer  unité travail  Granit  Fini  Épai  Long  Haut" &
 SKIP 1                                          &
  TAB 001 "--------------------------------------------  ---------  --------  -------  --------  -------------  ------  ----  ----  ----  ----" &
 SKIP 2                                          

FOOTING AT DIANUM002                             &
  TAB 001 D_UTRNUMUNITRV                         &
          PRINT AT D_UTRNUMUNITRV                &
  TAB 005 UTRDSC001       OF PDUNITE_TRAVAIL     &
          PRINT AT D_UTRNUMUNITRV                &
  TAB 048 DIANUM002       OF PDPRIORITE_DIAG     &
  TAB 059 DIAQTECMD       OF PDDIAGRAMME         &
          PICTURE "^^,^^^ "                      &
          TRAILING SIGN "-"                      &
          SIGNIFICANCE 2                         &
          NOBWZ                                  &
  TAB 068 D_QTE_EMBAL                            &
          PICTURE "^^,^^^ "                      &
          TRAILING SIGN "-"                      &
          SIGNIFICANCE 2                         &
          NOBWZ                                  &
  TAB 078 D_QTE_A_DBT                            &
          PICTURE "^^,^^^"                       &
          SIGNIFICANCE 1                         &
  TAB 088 UTDQTEPRD       OF PDUNI_TRAV_DIA      &
          NOBWZ                                  &
  TAB 104 TGRCODGRA       OF PDDIAGRAMME         &
  TAB 111 TYFCODFIN       OF PDDIAGRAMME         &
  TAB 116 DIAEPA          OF PDDIAGRAMME         &
  TAB 122 DIALON          OF PDDIAGRAMME         &
  TAB 128 DIAHAU          OF PDDIAGRAMME         &
 RESERVE 7 LINES

FOOTING AT D_UTRNUMUNITRV                        &
 SKIP 2                                          &
  TAB 020 "Total unité de travail    :                     Morceaux:           Mètres carrés:" &
  TAB 043 D_UTRNUMUNITRV                         &
  TAB 049 D_QTE_UNTRA                            &
          SUBTOTAL  AT DIANUM002                 &
          PICTURE "^^^,^^^,^^^.^^^^"             &
          SIGNIFICANCE 6                         &
          NOBWZ                                  &
  TAB 077 D_QTE_A_DBT                            &
          SUBTOTAL  AT DIANUM002                 &
          PICTURE "^^^,^^^"                      &
          SIGNIFICANCE 1                         &
          NOBWZ                                  &
  TAB 102 DIASURMC2 OF PDDIAGRAMME               &
          SUBTOTAL  AT DIANUM002                 &
          PICTURE "^^,^^^.^^^^"                  &
          SIGNIFICANCE 6                         &
          NOBWZ                                  &
 SKIP 1                                          &
  TAB 001 "___________________________________________________________________________________________________________________________________" &
 SKIP 2                                          

FOOTING AT PRICODPRI                             &
  TAB 020 "Total pour la priorité    : Morceaux:           Mètres carrés:" &
  TAB 044 PRICODPRI    OF PDTEMPO_EMBAL          &
  TAB 058 D_QTE_A_DBT                            &
          SUBTOTAL  AT DIANUM002                 &
          PICTURE "^^^,^^^"                      &
          SIGNIFICANCE 1                         &
          NOBWZ                                  &
  TAB 083 DIASURMC2 OF PDDIAGRAMME               &
          SUBTOTAL  AT DIANUM002                 &
          PICTURE "^^,^^^.^^^^"                  &
          SIGNIFICANCE 6                         &
          NOBWZ                                  &
 SKIP PAGE

BUILD pd656zb


@IF FORMAT
PD656ZB                                              GRANICOR INC. (Développement)                                       PAGE:   9
xxxxxxxx                                  Reste à emballer par priorité et unité de travail                            99/99/ 99:99

Numéro de commande: XX-XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                 
Priorité..........: 99     99/99/99                                      

                                                                            Reste à     Quantité        
              Unité de travail                Diagramme  Commandé  Emballé  emballer  unité travail  Granit  Fini  Épai  Long  Haut
--------------------------------------------  ---------  --------  -------  --------  -------------  ------  ----  ----  ----  ----

XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
                                               XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
                                               XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999

                   Total unité de travail XXX:  999,999,999.9999   Morceaux:999,999    Mètres carrés:99,999.9999 
___________________________________________________________________________________________________________________________________

XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
                                               XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
                                               XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
                                               XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
 
                   Total unité de travail XXX:  999,999,999.9999   Morceaux:999,999    Mètres carrés:99,999.9999 
___________________________________________________________________________________________________________________________________

XXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
                                               XXXXXXX    99,999-  99,999-   99,999    999,999.9999    XXX    XX   9999  9999  9999
___________________________________________________________________________________________________________________________________

                   Total unité de travail XXX:  999,999,999.9999   Morceaux:999,999    Mètres carrés:99,999.9999 
___________________________________________________________________________________________________________________________________

                   Total pour la priorité  99: Morceaux: 999,999   Mètres carrés: 999,999.9999 
@ENDIF
