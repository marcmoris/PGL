;/P/ Liste des blocs non facturés par carrière
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Modifié par André Bilodeau 25/04/94
;  - Ajouter date de transport et le # de facture C/P du bloc
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme, et changement pour que le rapport sorte
;                     par type de granit au lieu de par carriere.
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC &
  LINK CIECLE    OF IFBLOC, &
       FTRNUMFAC OF IFBLOC  &
    TO CIECLE, &
       FTRNUMFAC                    OF PDFACTURE OPTIONAL

CHOOSE CIECLE PARM, &
       TGRCODGRA PARM

DEFINE TMP-DAT-FIN DATE = PARM ;----------PROMPT             &
;------------               "ENTRER LA DATE DE FIN   (AAMMJJ): "

DEFINE D-CPT-BLOC  INTEGER SIZE 1 = 1 IF BLCDATREC <> 0 ELSE 0
DEFINE D-BLCM3TOT  INTEGER SIZE 4 = BLCM3TOT IF BLCDATREC <> 0 ELSE 0
DEFINE D-BLCTFINAL INTEGER SIZE 4 = BLCTFINAL IF BLCDATREC <> 0 ELSE 0


; LE SELECT PREND LES ENREGISTREMENTS REPONDANT AUX CONDITIONS SUIVANTES :

; - LE NUMERO DE FACTURE DANS LA TABLE IFBLOC=0 OU LA DATE DE LA FACTURE
;   EST PLUS GRANDE QUE LA DATE DE FIN ENTRE
; - LA DATE DU IFBLOC EST COMPRISE DANS LES LIMITES ENTREES SI IL Y EN A UNE


SELECT IF     (    FTRNUMFAC OF IFBLOC = 0 &
                OR FTRDATFAC OF PDFACTURE > TMP-DAT-FIN &
              ) &
          AND (    TMP-DAT-FIN >= BLCDAT &
                OR TMP-DAT-FIN = 0 &
              )
;


DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""


SORT ON TGRCODGRA &
     ON ETRCLE &
     ON D_NUMBLC &
     ON D_NUMCOM


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" IF TMP-DAT-FIN = 0 &
        ELSE "En date du : " + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                               ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                               ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF301"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
             TAB 3 "TYPE"                    &
             TAB 8 "NO"                   &
             TAB 25 "DATE"                 &
             TAB 34 "DIMENSIONS "          &
             TAB 48 "TOTAL"                &
             TAB 59 "T/M"                  &
             TAB 68 "NO"                   &
             TAB 75 "NO"                   &
             TAB 83 "CAT"                  &
             TAB 87 "COUL"                 &
             TAB 92 "QUAL"                 &
             TAB 97 "DATE"                 &
             TAB 107 "NO"                   &
             TAB 113 "DATE"                 &
             TAB 121 "NO FAC"             &
             TAB 128 "DATE DE"             &
SKIP 1 &
             TAB 3 "GRAN."                 &
             TAB 8  "BLOC"                 &
             TAB 25 "PROD"                 &
             TAB 32 "LONG HAUT LARG"       &
             TAB 50 "M3"                   &
             TAB 68 "EXP"                  &
             TAB 75 "COM."              &
             TAB 97 "DEBUT"                &
             TAB 105 "FACTURE"              &
             TAB 113 "FACTURE"              &
             TAB 123 "C/P"                 &
             TAB 128 "TRANSPORT"           &
SKIP 1 &
             TAB 003 "____"                    &
             TAB 008 "______________"                   &
             TAB 023 "________"               &
             TAB 032 "______________"         &
             TAB 047 "________"                &
             TAB 057 "________"                &
             TAB 067 "_____"                 &
             TAB 073 "_________"  &
             TAB 083 "___"                &
             TAB 087 "____"               &
             TAB 092 "____"               & 
             TAB 097 "________"                &
             TAB 106 "_____"               &
             TAB 112 "________"             &
             TAB 121 "______"             &
             TAB 128 "________"             &
SKIP 2

REPORT  TAB 003 TGRCODGRA OF IFBLOC PRINT AT TGRCODGRA OF IFBLOC  &
        TAB 008 D_NUMBLC                           &
        TAB 023 BLCDAT OF IFBLOC                   &
        TAB 032 BLCVENDULONG OF IFBLOC             &
        TAB 037 BLCVENDUHAU OF IFBLOC              &
        TAB 042 BLCVENDULAR OF IFBLOC              &
        TAB 047 BLCM3TOT OF IFBLOC   PIC "^^^^^.^^"&
        TAB 057 BLCTFINAL OF IFBLOC  PIC "^^^^^.^^"&
        TAB 067 EXNCLE OF IFBLOC        PIC "^^^^^"&
        TAB 073 D_NUMCOM                           &
        TAB 083 CATCLE OF IFBLOC                   &
        TAB 087 CLRCLE OF IFBLOC                   &
        TAB 092 QLTCLE OF IFBLOC		   &
        TAB 097 COEDATDEB OF IFBLOC                &
        TAB 106 FTRNUMFAC OF IFBLOC PIC "^^^^^"    &
        TAB 112 FTRDATFAC OF PDFACTURE             &
        TAB 121 CAPCLE OF IFBLOC PIC "^^^^^^"      &
        TAB 128 BLCDATREC OF IFBLOC

FOOTING AT TGRCODGRA OF IFBLOC      &
        TAB 9 "______________"            &
        TAB 47 "________" &
        TAB 57 "________"         &

SKIP 1  &
        TAB 4 COUNT "Blocs"         &
        TAB 30 "Sous-total:"        &
        TAB 47 BLCM3TOT SUBTOTAL PICTURE "^^^^^.^^" &
        TAB 57 BLCTFINAL SUBTOTAL  PICTURE "^^^^^.^^" &
SKIP 1  &
        TAB 4 D-CPT-BLOC SUBTOTAL RESET AT TGRCODGRA PICTURE "^^^^^" "Transp." &
        TAB 30 "Sous-total:"                &
        TAB 47 D-BLCM3TOT SUBTOTAL RESET AT TGRCODGRA PICTURE "^^^^^.^^" &
        TAB 57 D-BLCTFINAL SUBTOTAL RESET AT TGRCODGRA PICTURE "^^^^^.^^" &
SKIP 2
FINAL FOOTING  &
        TAB 9 "________"            &
        TAB 47 "________"         &
        TAB 57 "________"         &
SKIP 1  &
        TAB 4 COUNT "Blocs"         &
        TAB 30 "Total:"             &
        TAB 46 BLCM3TOT SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 56 BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 1  &
        TAB 4 D-CPT-BLOC SUBTOTAL PICTURE "^^^^^" "Transp." &
        TAB 30 "Total:"                &
        TAB 46 D-BLCM3TOT SUBTOTAL PICTURE "^^^^^^.^^" &
        TAB 56 D-BLCTFINAL SUBTOTAL PICTURE "^^^^^^.^^" &
SKIP 2
BUILD  if301z
