;/P/ Rapport inventaire des blocs non factures
;
;/M/ Programmeur...: Bernard Samson
;    Date..........: 1999-11-22
;    Description...: Changement pour avoir les blocs par type de granit a la
;                     place de par carriere.
;
;********************************************
;  pour une periode donnee par carriere.
;
;  PAR : Patrick Bourbeau
;  DATE: Avril 1991
;********************************************
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
USE ussetqzs NOLIST

ACCESS IFBLOC

;Numero de bloc
DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC
;Montant total expedition
DEFINE D_COUEXPTOT FLOAT SIZE 8 = (BLCMNTEXPM3 OF IFBLOC / 100 ) &
                              * (BLCM3TOT OF IFBLOC  )

;cout bloc
DEFINE D_COUBLC FLOAT SIZE 8 = (BLCM3TOT OF IFBLOC ) &
                               * (BLCMNTM3 OF IFBLOC / 100   )




SORT ON TGRCODGRA OF IFBLOC &
     ON CLICLE    OF IFBLOC &
     ON D_NUMBLC


CHOOSE CIECLE PARM

DEF TMP-DAT-DEB         DATE = PARM ;PROMPT  "DATE DEBUT (AAMMJJ):"
DEF TMP-DAT-FIN         DATE = PARM ;PROMPT  "DATE FIN   (AAMMJJ):"



SELECT IF  (FTRNUMFAC OF IFBLOC = 0 ) &
       AND (((TMP-DAT-DEB <= BLCDAT) AND (TMP-DAT-FIN >= BLCDAT)) &
       OR (TMP-DAT-DEB = 0 AND TMP-DAT-FIN = 0))

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" &
                        IF     TMP-DAT-DEB = 0 &
                           AND TMP-DAT-FIN = 0 &
        ELSE "Du "         + ASCII( TMP-DAT-DEB , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-DEB , 8 )[7:2] + &
             " Au "        + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                             ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF331"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
        TAB 39 "VALEUR DES BLOCS NON FACTURES DU          AU         " &
        TAB 72 TMP-DAT-DEB PIC "^^/^^/^^" BWZ &
        TAB 84 TMP-DAT-FIN PIC "^^/^^/^^" BWZ &
SKIP 2 &
        TAB 001 "________________________________________________________________________________________________________________________________" &
SKIP &
        TAB 006 "# BLOC"     &
        TAB 021 "VOLUME"     &
        TAB 035 "VALEUR"     &
        TAB 049 "VALEUR"     &
        TAB 062 "NO"         &
        TAB 069 "NO"         &
        TAB 076 "VALEUR/M3"  &
        TAB 090 "VALEUR/M3"  &
SKIP 1 &
        TAB 023 "M3" 	     &
        TAB 035 "ACHAT"      &
        TAB 048 "TRANSPORT"  &
        TAB 060 "CLIENT"     &
        TAB 068 "EXPED"      &
        TAB 078 "BLOC"       &
        TAB 090 "TRANSPORT"  &
SKIP &
        TAB 1 "________________________________________________________________________________________________________________________________" &
SKIP 1

REPORT  TAB 002 D_NUMBLC                      &
        TAB 021 BLCM3TOT OF IFBLOC            &
        TAB 033 D_COUBLC PICTURE " ^^^,^^^.^^"  FLOAT "$"   &
        TAB 046 D_COUEXPTOT PICTURE " ^^^,^^^.^^"  FLOAT "$" &
        TAB 060 CLICLE OF IFBLOC                                                      &
        TAB 068 EXNCLE     OF IFBLOC                &
        TAB 076 BLCMNTM3   OF IFBLOC PICTURE " ^^,^^^.^^"  FLOAT "$"     &
        TAB 090 BLCMNTEXPM3 OF IFBLOC PICTURE " ^^,^^^.^^"  FLOAT "$" &
SKIP 1
FOOTING AT CLICLE OF IFBLOC            &
        TAB 002   "_______"            &
        TAB 017  "__________"          &
        TAB 032  "____________"        &
        TAB 075  "___________"           &
        TAB 090  "___________"         &

SKIP &
        TAB 002   COUNT PICTURE "^^^^"                    &
        TAB 017  BLCM3TOT SUBTOTAL PICTURE "^^^,^^^.^^"  &
        TAB 032  D_COUBLC SUBTOTAL PICTURE "^,^^^,^^^.^^"  FLOAT "$"   &
        TAB 076  BLCMNTM3 SUBTOTAL PICTURE "^^^,^^^.^^"  FLOAT "$"  &
        TAB 090  BLCMNTEXPM3 SUBTOTAL PICTURE "^^^,^^^.^^"  FLOAT "$" &
SKIP 2

FINAL FOOTING                           &
        TAB 001 "================================================================================================================================" &
SKIP 1 &
        TAB 001   COUNT PICTURE " ^^^^"                   &
        TAB 016  BLCM3TOT SUBTOTAL PICTURE " ^^^,^^^.^^"  &
SKIP &
        TAB 001 "================================================================================================================================"

BUILD if331z
