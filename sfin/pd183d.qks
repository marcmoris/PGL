;/T/ 2.4.1 Enregistrer le montage. (Rapport journalier)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-10-11
;
;    Description..: Cette unité de traitement permet de consulter les
;                   informations de controle sur le le montage des blocs
;                   de granit.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd183d ACTIONBAR                    &
             MODE LABEL "" AT 2,80         &
             NOACTION                      &
             ACTIVITIES FIND,ENTRY,CHANGE  &
             FIELDMARK RETAIN STARTUP      &
             HELP POPUP FROM 4,9 TO 20,72  &
             RECEIVING T_CIECLEMNU,        &
                       T_CIENOM,           &
                       PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Débute une transaction pour le sous-écran
;
USE ustrasse  NOLIST


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD183D"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd183d.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_RMOSTU    CHARACTER SIZE 16 = "RMOSTU"
TEMPORARY T_RMOSTU    CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Montage bloc
;

FILE PDMONTAGE_BLOC    MASTER


;-------------------------------------------------------------------------------
;
; Montage bloc journalier
;

FILE PDMONT_BLOC_JOU PRIMARY &
                     NOITEM

  ACCESS VIA     CIECLE,                              &
                 RMSNUMSEQ,                           &
                 BLONUMGRA001APR,                     &
                 BLONUMGRA002APR,                     &
                 BLORECAPR                            &
         USING   T_CIECLEMNU,                         &
                 RMSNUMSEQ         OF PDMONTAGE_BLOC, &
                 BLONUMGRA001APR   OF PDMONTAGE_BLOC, &
                 BLONUMGRA002APR   OF PDMONTAGE_BLOC, &
                 BLORECAPR         OF PDMONTAGE_BLOC  &
         ORDERBY CIECLE,                              &
                 RMSNUMSEQ

  ACCESS VIA     CIECLE           &
         USING   T_CIECLEMNU      &
         ORDERBY CIECLE,          &
                 RMSNUMSEQ


  ITEM CIECLE            OF PDMONT_BLOC_JOU INITIAL T_CIECLEMNU
  ITEM RMSNUMSEQ         OF PDMONT_BLOC_JOU FINAL RMSNUMSEQ         OF PDMONTAGE_BLOC
  ITEM BLONUMGRA001APR   OF PDMONT_BLOC_JOU FINAL BLONUMGRA001APR   OF PDMONTAGE_BLOC
  ITEM BLONUMGRA002APR   OF PDMONT_BLOC_JOU FINAL BLONUMGRA002APR   OF PDMONTAGE_BLOC
  ITEM BLORECAPR         OF PDMONT_BLOC_JOU FINAL BLORECAPR         OF PDMONTAGE_BLOC
  ITEM BLONUMBLOAPR      OF PDMONT_BLOC_JOU FINAL BLONUMBLOAPR      OF PDMONTAGE_BLOC


;-------------------------------------------------------------------------------
;
; Rapport de montage
;

FILE PDRAP_MONTAGE REFERENCE

  ACCESS VIA     CIECLE,           &
                 RMSNUMSEQ         &
         USING   T_CIECLEMNU,                        &
                 RMSNUMSEQ         OF PDMONTAGE_BLOC &
         ORDERBY CIECLE,           &
                 RMSNUMSEQ


;-------------------------------------------------------------------------------
;
; Scies
;

FILE PDSCIES REFERENCE

  ACCESS VIA     CIECLE,                              &
                 SCINUMSCI                            &
         USING   T_CIECLEMNU,                         &
                 SCINUMSCI        OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
; Code bilingue (Statut)
;

FILE VCBI_DSC  REFERENCE &
               ALIAS A_CBI_RMOSTU

  ACCESS VIA   XELNOM,                               &
               CBICLE                                &
         USING D_RMOSTU,                             &
               RMOSTU           OF PDRAP_MONTAGE


;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Rapport de montage " &
@ELSE
      " %%%Rapport de montage " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5


ALIGN (,3,19)


FIELD RMSNUMSEQ        OF PDRAP_MONTAGE    &
label "Num. montage..:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,25)


FIELD RMOSTU           OF PDRAP_MONTAGE    &
label "Statut........:"&
        DISPLAY                            &
        FIXED


FIELD CBIDSC           OF A_CBI_RMOSTU     &
        DISPLAY                            &
        FIXED


ALIGN (,3,19)


FIELD RMOPRI           OF PDRAP_MONTAGE    &
label "Priorité......:"&
        DISPLAY                            &
        FIXED


ALIGN (,3,19) (,,25)


FIELD SCINUMSCI       OF PDRAP_MONTAGE     &
label "Numéro scie...:"&
        DISPLAY                            &
        FIXED


FIELD SCIDSC001        OF PDSCIES          &
        DISPLAY                            &
        FIXED


DRAW 10,2 TO 10,79


SKIP TO LINE 10


TITLE &
@IF FRANCAIS
      " Rapport journalier " &
@ELSE
      " %%%Rapport journalier " &
@ENDIF
      CENTERED AT ,1


SKIP TO LINE 12


ALIGN (2,3,19) (,23,24) (,29,30)

FIELD BLONUMGRA001APR  OF PDMONTAGE_BLOC   &
        HIDDEN ID 26                       &
        LABEL "No bloc.......:"            &
        FIXED                              &
        NUMERIC                            &
        BWZ


FIELD BLONUMGRA002APR  OF PDMONTAGE_BLOC   &
        HIDDEN ID 27                       &
        LABEL "-"                          &
        FIXED                              &
        NUMERIC                            &
        BWZ


FIELD BLORECAPR        OF PDMONTAGE_BLOC   &
        HIDDEN ID 28                       &
        LABEL "-"                          &
        FIXED


ALIGN (2,3,19)



FIELD MBJDATRAPMON     OF PDMONT_BLOC_JOU   FORMAT YYYYMMDD &
PATTERN "####/##/##"&
label "Date rapp jour:"&
        HIDDEN ID 30                       &
        REQUIRED


FIELD MBJPOSDEB        OF PDMONT_BLOC_JOU  &
label "Position déb..:"&
        HIDDEN ID 35                       &
        REQUIRED


FIELD MBJPOSFIN        OF PDMONT_BLOC_JOU  &
label "Position fin..:"&
        HIDDEN ID 40                       &
        REQUIRED


FIELD MBJVOLNETMC3     OF PDMONT_BLOC_JOU  &
label "Volume net....:"&
        HIDDEN ID 55                       &
        REQUIRED


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide que la position du jour soit plus petite que la position d'hier
;

PROCEDURE EDIT MBJPOSFIN
BEGIN
  IF MBJPOSFIN OF PDMONT_BLOC_JOU  > MBJPOSDEB OF PDMONT_BLOC_JOU
  THEN BEGIN
    ERROR 2979 ; Cette position est invalide
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDMONT_BLOC_JOU &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDMONT_BLOC_JOU &
     AND NOT NEWRECORD     OF PDMONT_BLOC_JOU &
     AND NOT DELETEDRECORD OF PDMONT_BLOC_JOU &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

  ;
  ; Mise à jour des informations sur la mise à jour
  ;
  IF (NOT NEWRECORD OF PDRAP_MONTAGE  AND &
      ALTEREDRECORD OF PDRAP_MONTAGE)     &
     OR                                   &
      (NEWRECORD OF PDMONT_BLOC_JOU OR    &
      ALTEREDRECORD OF PDMONT_BLOC_JOU)
  THEN BEGIN
    LET RMODATDERMAJ     OF PDRAP_MONTAGE = SYSDATE
    LET RMOHREDERMAJ     OF PDRAP_MONTAGE = SYSTIME / 10000
    LET RMOUSRDERMAJ     OF PDRAP_MONTAGE = LOGONID
  END
END


BUILD

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Rapport de montage ******************************
*                                                                              *
* Num. montage..: xxxxxxxxxxx                                                  *
* Statut........: x     xxxxxxxxxxxxxxxxxxxxxxxxx                              *
* Priorité......: xx                                                           *
* Numéro scie...: xx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx               *
*                                                                              *
****************************** Rapport journalier ******************************
*                                                                              *
* No bloc.......: xxxx-xxxxx-x                                                 *
* Date rapp jour: xxxxxxxx                                                     *
* Position déb..: xxxx                                                         *
* Position fin..: xxxx                                                         *
* Volume net....: xxxxxxx                                                      *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
