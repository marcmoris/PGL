;/T/ Liste des ventes par type de granit
;
;/P/ Programmeur.......: Patrick Langlois
;    Date Création.....: 28 Mai 1999
;
;    Description.......: Liste des ventes par type de granit.
;
;    Paramètres........: Compagnie,type de granit, type de fini, date début, date fin
;
;***********************************************************************************************************************************
;
;/M/ Modifié par.......: Marc Poulin     
;    Date modification.: 29 février 2000
;    Description.......: Ajustements du rapport au niveau de la sélection et de la conversion du prix de vente de USD vers CAN.
;
;    Référence.........: Demande de changement 999999.
;    
;    Signet............: MP-00-02-29_D999999.
;
;-----------------------------------------------------------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST
SET FORMFEED NOBLANKS


;
; Plus rapide avec une vue
;
ACCESS V_VNTGRA LINK CIECLE        OF V_VNTGRA            , &
                     TYFCODFIN     OF V_VNTGRA              &
                  TO CIECLE                               , &
                     TYFCODFIN     OF PDTYPE_FINI      OPT  &

                 LINK CIECLE       OF V_VNTGRA            , &
                      PJTABR       OF V_VNTGRA            , &
                      PJTANN       OF V_VNTGRA            , &
                      COMNUMCOMINT OF V_VNTGRA              &
                   TO CIECLE                              , &
                      PJTABR                              , &
                      PJTANN                              , &
                      COMNUMCOMINT OF PDCOMMAN_INTERNE OPT

CHOOSE CIECLE    PARM, &
       TGRCODGRA PARM, &
       TYFCODFIN PARM, &
       EXPDATEXP PARM RANGE

SORT ON CIECLE   , &
        TGRCODGRA, &
        TYFCODFIN, &
        TRAEPA

DEFINE D_DATDEB DATE = PARM
DEFINE D_DATFIN DATE = PARM
DEFINE D_DATPRD DATE = PARM

SELECT IF TRADATCRE OF V_VNTGRA < D_DATPRD ; MP-00-02-29_D999999

DEFINE D_DATDEBCHR     CHARACTER SIZE 10 = ASCII(D_DATDEB)[1:4] + "/" + &
                                           ASCII(D_DATDEB)[5:2] + "/" + &
                                           ASCII(D_DATDEB)[7:2]


DEFINE D_DATFINCHR     CHARACTER SIZE 10 = ASCII(D_DATFIN)[1:4] + "/" + &
                                           ASCII(D_DATFIN)[5:2] + "/" + &
                                           ASCII(D_DATFIN)[7:2]

DEFINE DZ_NOMRAP1     CHARACTER SIZE 60 = "Ventes par type de granit"
DEFINE DZ_NOMRAP2     CHARACTER SIZE 60 = "Date de début: " + D_DATDEBCHR + "   Date de fin: " + D_DATFINCHR
DEFINE D_FOT2         CHARACTER SIZE 50 = "(" + TRUNC(TGRDSCFRA001) + ")"

; MP-00-02-29_D999999

DEFINE D_TAUX_CNV     FLOAT     SIZE 08 = PARM ; Taux de conversion pour les devises étrangères

DEFINE D_VNT          INTEGER   SIZE 08 = ((DCIPRIUNMPRD OF V_VNTGRA) * EMTSURMC2VEN OF V_VNTGRA)            &
                                       IF  COMDEV OF PDCOMMAN_INTERNE = "CAN"                                &
                                     ELSE ((DCIPRIUNMPRD OF V_VNTGRA) * EMTSURMC2VEN OF V_VNTGRA * D_TAUX_CNV)

; -------------------

DEFINE DZ_NUMRAP      CHARACTER SIZE 12 = "pd663z"      ; numéro du rapport
DEFINE DZ_CIENOM      CHARACTER SIZE 40 = PARM          ; Nom de la compagnie

USE ustitstd45 NOLIST
  SKIP 3 &
 TAB 002 "Nom granit                 Fini                      Ep.      M2 / Fini       Ventes $$" &
  SKIP 1 &
 TAB 002 "-------------------------  ------------------------  ----  --------------  --------------"

FOOTING AT TRAEPA     &
SKIP 1                &
 TAB 002 TGRCODGRA    PRINT AT TGRCODGRA &
 tab 006 TGRDSCFRA001 PRINT AT TGRCODGRA &
 TAB 029 TYFCODFIN    PRINT AT TYFCODFIN &
 TAB 032 TYFDSCFRA001 PRINT AT TYFCODFIN &
 TAB 055 TRAEPA                          &
 TAB 061 EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^^^" &
 TAB 077 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" 

FOOTING AT TYFCODFIN  &
SKIP 1                &
 TAB 061 "--------------  --------------" &
 SKIP 1 &
 TAB 061 EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^^^" &
 TAB 077 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" & 
SKIP 3


FOOTING AT TGRCODGRA  &
SKIP 2                &
 TAB 061 "==============  ==============" &
 SKIP 1 &
 TAB 006 "TOTAL "     &
 TAB 012 D_FOT2       &
 TAB 061 EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^^^" &
 TAB 077 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" & 
SKIP 1 &
 TAB 061 "==============  ==============" &
SKIP 2 &
TAB 002 "_________________________________________________________________________________________" &
SKIP 3


FOOTING AT CIECLE     &
SKIP 2                &
 TAB 061 "**************  **************" &
 SKIP 1 &
 TAB 006 "GRAND TOTAL" &
 TAB 061 EMTSURMC2VEN SUBTOTAL PICTURE "^,^^^,^^^.^^^^" &
 TAB 077 D_VNT        SUBTOTAL PICTURE "^^^,^^^,^^^.^^" & 
 SKIP 1                                                 &
 TAB 061 "**************  **************"


@IF FORMAT
@ENDIF


BUILD pd663z
