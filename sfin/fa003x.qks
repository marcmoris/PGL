;/T/ Gestion des trajets / produits - Validation de la destruction
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 29 mars 1995
;
;    Description..: Cet écran vérifie si le trajet / produit reçue en
;                   paramètre est référée dans une autre table, pour le
;                   traitement de la destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN fa003x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING FATRAJET_PRODUIT, &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Définitions de projets
;
FILE FATRAJET_PRODUIT  MASTER

FILE FAGRILLE_TARIF    REFERENCE
FILE FASEGMENT         REFERENCE


;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référée dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN

  GET FAGRILLE_TARIF   VIA   CIECLE,    &
                             TRPCLE     &
                       USING CIECLE OF FATRAJET_PRODUIT, &
                             TRPCLE OF FATRAJET_PRODUIT  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 2717 ; Destruction annulée, le trajet / produit est reféré
                  ; dans la grille de tarification.

  GET FASEGMENT        VIA   CIECLE,    &
                             TRPCLE     &
                       USING CIECLE OF FATRAJET_PRODUIT, &
                             TRPCLE OF FATRAJET_PRODUIT  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 2718 ; Destruction annulée, le trajet / produit est reféré
                  ; dans un segment.

  GET FASEGMENT        VIA   CIECLE,    &
                             SEGCLE     &
                       USING CIECLE OF FATRAJET_PRODUIT, &
                             TRPCLE OF FATRAJET_PRODUIT  &
                       OPTIONAL
  IF ACCESSOK
  THEN ERROR 2718 ; Destruction annulée, le trajet / produit est reféré
                  ; dans un segment.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
