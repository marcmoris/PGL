;/T/ Passerelle d'entré, Facture Compte à recevoir
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 23-Nov-1993
;
;    Description..: Calcul les date des terme de la facture
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cr702 AUTOUPDATE

FILE CRCPT_A_REC DESIGNER

FILE CRTERME     DESIGNER OPEN READ SHARE

;
;Sert a validé de lot
;
; Si il n'y a pas eux d'erreur dans le qtp le lot est rendu de type PF
; je le remet de type FA après le calcul

FILE CRLOT       DESIGNER

TEMPORARY T_CIECLE CHARACTER SIZE 4
TEMPORARY T_PECCLE CHARACTER SIZE 4
TEMPORARY T_LCRCLE CHARACTER SIZE 4

;-------------------------------------------------------------------------------
;
; Variables temporaire pour le calcul des dates due.
;
USE ustertmp NOLIST

FIELD T_CIECLE REQUIRED
FIELD T_PECCLE REQUIRED
FIELD T_LCRCLE REQUIRED

;-------------------------------------------------------------------------------
;
;
; Procédure pour le calcul des dates due.
;
;
USE ustmecal NOLIST

PROCEDURE INTERNAL CALCUL_DAT_TERME
BEGIN
    LET TZ_DATCAL = CARDAT OF CRCPT_A_REC

    GET CRTERME VIA TMECLE &
      USING CARTERNET OF CRCPT_A_REC  OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET CARDATNET OF CRCPT_A_REC = TZ_DATRES
    END

    GET CRTERME VIA TMECLE &
      USING CARTERESC1 OF CRCPT_A_REC OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET CARDATESC1 OF CRCPT_A_REC = TZ_DATRES
    END

    GET CRTERME VIA TMECLE &
      USING CARTERESC2 OF CRCPT_A_REC  OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET CARDATESC2 OF CRCPT_A_REC = TZ_DATRES
    END

    GET CRTERME VIA TMECLE &
                USING CARTERFRS OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET CARDATFRS OF CRCPT_A_REC = TZ_DATRES
    END
END

PROCEDURE UPDATE
BEGIN
  GET CRLOT &
  VIA CIECLE , &
      PECCLE , &
      LCRCLE , &
      LCRTYPECR &
  USING T_CIECLE, &
        T_PECCLE, &
        T_LCRCLE, &
        "PF" OPTIONAL
  IF ACCESSOK
  THEN BEGIN

    WHILE RETRIEVING CRCPT_A_REC &
      VIA CIECLE, &
          PECCLE, &
          LCRCLE &
      USING T_CIECLE, &
            T_PECCLE, &
            T_LCRCLE
    BEGIN
      IF ACCESSOK
      THEN BEGIN
        DO INTERNAL CALCUL_DAT_TERME
        PUT CRCPT_A_REC RESET
      END
    END
    LET LCRTYPECR OF CRLOT = "FA"
    PUT CRLOT RESET
  END
END

PROCEDURE POSTUPDATE
BEGIN
  INFO = "Terminer avec succès " NOW
  RETURN
END
BUILD
