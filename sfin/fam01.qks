;/T/ Menu facturation
;
;/P/ Programmeur..: Steeve Duguay
;    Date création: 29 mars 1995
;
;
;/M/ Marie-Josée Hamel, 96.02.06, ajouter gestion des lots dans le menu
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000

;
SCREEN fam01 MENU &
             HELP POPUP FROM 4,9 TO 20,72 &
             NOMODE NOACTION FIELDMARK RETAIN STARTUP &
             RECEIVING T_FLGHLDOK

USE ussectmp NOLIST
    "FAM01"

USE fam01.hlp NOLIST

TEMPORARY T_FLGHLDOK CHARACTER SIZE 1 ; Si le holding est présent.


;
; Validation de la compagnie du menu.
;
FILE MCCOMPAGNIE      DESIGNER &
                      OPEN READ

;
; Pour la securite sur les compagnies.
;
FILE VSEC_USR         DESIGNER &
                      OPEN READ

;
; Pour afficher les dates de la période courante au grand livre.
;
FILE MCPERIODE_CTB    DESIGNER &
                      OPEN READ

;
; Pour valider la periode comptable.
;
FILE MCPERIODE_CIE    DESIGNER &
                      OPEN READ

;
; Informations sur le Holding.
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"

;-------------------------------------------------------------------------------
;
;
; Variables saisie ou affichée dans le menu.
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CLICIECLE CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie
TEMPORARY T_PECCLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Période du menu
TEMPORARY T_FARCLEFND CHARACTER SIZE 9  RESET AT STARTUP
TEMPORARY T_PECDATDEB DATE              RESET AT STARTUP ; Date de début de période
TEMPORARY T_PECDATFIN DATE              RESET AT STARTUP ; Date de fin de période
;
TEMPORARY T_PECCLECOU CHARACTER SIZE 4  RESET AT STARTUP ; Période courante

TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les périodes
TEMPORARY T_MOD       CHARACTER SIZE 2 ; Code de module, pour le Popup sur
                                       ; les périodes

;
; Variables passées en paramètres aux sous-écrans
;
TEMPORARY T_FOUCIECLE    CHARACTER SIZE 4  &
                           RESET AT STARTUP  ; Cie du fournisseur
TEMPORARY T_EMPCIECLE    CHARACTER SIZE 4  &
                           RESET AT STARTUP  ; Cie de l'employé
TEMPORARY T_FOUCLEFND    CHARACTER SIZE 6    ; F.sseur à chercher
TEMPORARY T_CAPCLEFND    CHARACTER SIZE 15   ; C.A.P à chercher
TEMPORARY T_FACCLEFND    CHARACTER SIZE 12   ; Facture d'achat
TEMPORARY T_PECCLEFND    CHARACTER SIZE 4    ; Période a rechercher
TEMPORARY T_LCRCLEFND    CHARACTER SIZE 4
TEMPORARY T_LBQCPTENCFND CHARACTER SIZE 6    ; Cpt encaisse à chercher
TEMPORARY T_PAMCLEFND    CHARACTER SIZE 7    ; no chèque à chercher

;
TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les compagnies
;
; Variables pour l'écran des menus Rapports et Procédures
;
DEFINE D_XMOCOD_L CHARACTER SIZE 4 = "FAL@" ; Code pour les listes et rapports
DEFINE D_XMOCOD_P CHARACTER SIZE 4 = "FAP@" ; Code pour les Procédure

;-------------------------------------------------------------------------------
;
;
USE ushilite NOLIST ; Hilite pour tout les écrans

DRAW 1,20 TO 3,61
DRAW 2,1  TO 2,20
DRAW 2,61 TO 2,80

DRAW 2,1  TO 7,1
DRAW 7,1  TO 7,80
DRAW 2,80 TO 7,80

DRAW 5,1  TO 5,80

DRAW 7,40 TO 19,40
DRAW 19,1 TO 19,80

TITLE &
@IF FRANCAIS
      "F A C T U R A T I O N" &
@ELSE
      "F A C T U R A T I O N" &
@ENDIF
      CENTERED AT 2,1

USE ustitoff NOLIST

SKIP TO 4

ALIGN(4,4,20) (,,26)
FIELD T_CIECLEMNU HIDDEN ID 05 &
        FIXED &
        UPSHIFT REQUIRED &
        PATTERN "((^|#)>)|!0" &
@IF FRANCAIS
        LABEL "Compagnie.....:" &
@ELSE
        LABEL "Company......:"  &
@ENDIF
        LOOKUP ON MCCOMPAGNIE &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 801 , & ; Cette compagnie n'existe pas

          ON VSEC_USR &
          VIA CIECLE &
          USING T_CIECLEMNU &
          MESSAGE 802 ; Vous n'avez pas accès à cette compagnie.

FIELD T_CIENOM &
        FIXED

SKIP 1

ALIGN(4,4,13)(,30,42)(,58,68)
FIELD T_PECCLEMNU ID SAME &
        FIXED             &
@IF FRANCAIS
        LABEL "Période:" &
@ELSE
        LABEL "Period.:" &
@ENDIF
        PICTURE "^^-^^" &
        LOOKUP ON MCPERIODE_CIE &
          VIA CIECLE, &
              PECCLE &
          USING T_CIECLEMNU, &
                T_PECCLEMNU  &
          MESSAGE 405 ; Cette période est inexistante


FIELD T_PECDATDEB FIXED FORMAT YYYYMMDD PATTERN "####/##/##" &
@IF FRANCAIS
        LABEL "Date début:"
@ELSE
        LABEL "Start Date:"
@ENDIF

FIELD T_PECDATFIN FIXED FORMAT YYYYMMDD PATTERN "####/##/##" &
@IF FRANCAIS
        LABEL "Date fin:"
@ELSE
        LABEL "End Date:"
@ENDIF


SKIP TO 9

ALIGN (2,2)

SUBSCREEN cr020 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM   , &
                        T_PECCLEMNU, &
                        T_PECCLECOU, &
                        T_PECCLEFND, &
                        T_LCRCLEFND  &
@IF FRANCAIS
                LABEL "Gestion des lots                      "
@ELSE
                LABEL "Batch management                      "
@ENDIF

SKIP 1

SUBSCREEN fa004 HIDDEN &
               PASSING T_CLICIECLE, &
                       T_CIECLEMNU, &
                       T_CIENOM   , &
                       T_PECCLEMNU, &
                       T_PECCLECOU, &
                       T_FARCLEFND  &
@IF FRANCAIS
                LABEL "Facturation"
@ELSE
                LABEL "%%%Facturation"
@ENDIF

SKIP TO 9

ALIGN (42,42)

SUBSCREEN fa001 HIDDEN &
                PASSING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Période de roulement"
@ELSE
                LABEL "%%%Période de roulement"
@ENDIF

SKIP 1
SUBSCREEN fa002 HIDDEN &
                PASSING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Éléments de tarifications"
@ELSE
                LABEL "%%%Éléments de tarifications"
@ENDIF

SKIP 1


SUBSCREEN fa003 HIDDEN &
                PASSING T_CLICIECLE, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "Trajet / Produit "
@ELSE
                LABEL "%%%Trajet / Produit "
@ENDIF


;SKIP 1


SKIP TO 20

ALIGN(31,31)
SUBSCREEN fam02 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_L, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL " Listes et rapports "
@ELSE
                LABEL " Lists and Reports  "
@ENDIF

SUBSCREEN fam02 HIDDEN &
                MODE F &
                PASSING D_XMOCOD_P, &
                        T_CIECLEMNU, &
                        T_CIENOM &
@IF FRANCAIS
                LABEL "     Procédures "
@ELSE
                LABEL "     Procedures "
@ENDIF

SUBSCREEN fam03 HIDDEN &
                PASSING T_CIECLEMNU, &
                        T_CIENOM    &
@IF FRANCAIS
                LABEL "Informations de base"
@ELSE
                LABEL " Basic Information  "
@ENDIF


;-------------------------------------------------------------------------------
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; Si le holding n'est pas défini on retourne l'usager au menu
  ; principal.
  ;
  IF T_FLGHLDOK = "0"
  THEN RETURN
  ;
  ; Valide la sécurité d'accès de l'écran.
  ;
  USE ussececr NOLIST
  ;
  ; Saisie de la compagnie du menu.
  ;
  ACCEPT T_CIECLEMNU
  ACCEPT T_PECCLEMNU
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les compagnies.
;
;
PROCEDURE INPUT T_CIECLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 PASSING T_CIECLE MODE F
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
  ;
  ; A cause de l'option requis, si l'usager fait un retour et qu'il y a
  ; une valeur on remet la même.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_CIECLEMNU))
  THEN LET FIELDTEXT = T_CIECLEMNU
END


;-------------------------------------------------------------------------------
;
;
; On conserve le nom de la compagnie du menu.
;
;
PROCEDURE PROCESS T_CIECLEMNU
BEGIN
  IF HLDCHTCLI OF MCHOLDING = "1"
  THEN LET T_CLICIECLE = T_CIECLEMNU
  ELSE LET T_CLICIECLE = HLDCHRSUB OF MCHOLDING

  LET T_CIENOM = CIENOM OF MCCOMPAGNIE
  DISPLAY T_CIENOM

  ;
  ; On va chercher la periode courante du module G/L.
  ;
  GET MCPERIODE_CIE VIA CIECLE, &
                        PCCSTUCR &
                    USING T_CIECLEMNU, &
                          "C" &
                    OPTIONAL
  IF NOT ACCESSOK
  THEN ERROR 720 ; Il n'y a pas de période courante de définie.
  ELSE LET T_PECCLECOU = PECCLE OF MCPERIODE_CIE
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT T_PECCLEMNU
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MOD = "CP"
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN  mc109 PASSING T_CIECLEMNU, T_PECCLE, T_MOD  MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
  ;
  ; Si l'usager ne saisie pas de periode alors on prend la période courante
  ; par défaut.
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) AND &
     0 = SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLECOU
  ;
  ; On forme la validation de la période.
  ;
  IF 0 = SIZE(FIELDTEXT) AND &
     0 <> SIZE(TRUNCATE(T_PECCLEMNU))
  THEN LET FIELDTEXT = T_PECCLEMNU
END

;-------------------------------------------------------------------------------
;
;
; Validation du statut de la période.
;
;
PROCEDURE EDIT T_PECCLEMNU
BEGIN
  ;
  IF PCCSTUCP OF MCPERIODE_CIE = "F"
  THEN ERROR 406 ; Cette période est fermée.
  ;
  IF PCCSTUCP OF MCPERIODE_CIE <> "C"
  THEN WARNING 407 ; Cette période diffère de la période courante
END

;-------------------------------------------------------------------------------
;
;
; Conserve les dates de la période et les affiches.
;
;
PROCEDURE PROCESS T_PECCLEMNU
BEGIN
  GET MCPERIODE_CTB VIA PECCLE &
                    USING T_PECCLEMNU
  LET T_PECDATDEB = PECDATDEB OF MCPERIODE_CTB
  LET T_PECDATFIN = PECDATFIN OF MCPERIODE_CTB
  DISPLAY T_PECDATDEB
  DISPLAY T_PECDATFIN
END

;-------------------------------------------------------------------------------
;
; On demande la compagnie et la période, il faut une procédure designer sur
; ces champs car l'option FIXED nous empèche par defaut de modifier les champs.
; L'option FIXED est due à un bug de POWERHOUSE sur Unix.
;
;
PROCEDURE DESIGNER 5
BEGIN
  ACCEPT T_CIECLEMNU
  ACCEPT T_PECCLEMNU
END

BUILD
;                   ******************************************
;********************         F A C T U R A T I O N          ********************
;*                  ******************************************                  *
;*  Compagnie.....: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx              *
;********************************************************************************
;*  Période: xxxxx            Date début: xxxxxxxx          Date fin: xxxxxxxx  *
;********************************************************************************
;                                       *
; Gestion des lots                      * Période de roulement
;                                       *
; Facturation                           * Éléments de tarifications
;                                       *
;                                       * Trajet / Produit
;                                       *
;                                       *
;                                       *
;                                       *
;                                       *
;********************************************************************************
;                               Listes et rapports
;                                   Procédures
;                              Informations de base
