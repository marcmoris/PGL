;/T/ Liste des réceptions / Détaillée
;
;/P/ Programmeur...: Nancy Marceau
;    Date Création.: 3 mai 1994
;
;    Description...: Cette liste permet l'impression des réceptions des
;                    commandes d'achat selon les paramètres demandés.
;
;    Paramètres....: Compagnie
;                    Entrepôt           (Un, plusieurs ou tous)
;                    Devise             (Une ou toutes)
;                    Numéro commande    (Borne de début, <retour>=tous)
;                    Numéro commande    (Borne de fin)
;                    Statut             (P=préliminaire, A=approuvée,
;                                        S=sélectionnée,C=complète,<retour>=tous)
;
;/M/672/Marc Trudel/1999-10-22
;  Etat..........: Terminé
;  Description...: GRA01
;                  La ligne est trop longue.  Reajustement de la liste sur
;                  une largeur de 180 car. (caracteres recuperes largeur de 
;                  largeur de 180 car. (caract. recuperes et desc. raccourciee). raccourcie)
;                  La quantite a recevoir doit tenir compte des receptions
;                  anterieures.
;
;/M/ Programmeur...: Remi Demers
;    Date modif....: 20 oct 1999 
;    Statut........: encour 
;    Description...: enlever les parametre de lancement et laisser 
;                    seulement le numero de reception et imprimer oui/non
;
;
;/M/672/Marc Trudel/1999-10-13
;  Etat..........: Terminé
;  Description...: GRA01
;                  Ajouter la colonne "Quantite a recevoir".  Le format passe
;                  de 132 a 220 car manque d'espace pour ajouter la colonne.
;
;/M/ Francois Richard, 99-07-09
;
;    Ajustement pour le changement de siècle
;
;/M/ Pascal Tremblay, 98-01-20
;
;    Ajustement pour le changement de siècle
;
;/M/ Programmeur...: Remi Demers
;    Date modif....: 13 Novembre 1997
;    Statut........: Termine
;    Description...: Ajoute les parametres de selection plage de periode
;
;/M/ Caroline Kaine, 12 nov 96, Format passe de 180 a 175
;
;
;***********************************************************************************************************************************
;
;/M/ Modifié par.......: Marc Poulin     
;    Date..............: 03 mars 2000
;    Description.......: Ajout des notes descriptives associées aux items commandés et réceptionnés.
;
;    Référence.........: Stabilisation du module Achat/Réception Inventaire (point 15).
;
;    Signet............: MP-00-03-10_S15.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 10 mars 2000.
;    Description.......: La mention "Signature autorisée" ne doit apparaître que sur la dernière page du bon de commande.
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 18.
;    
;    Signet............: MP-00-03-10_S18.
;
;    ---------------------------------------------------------------------------------------------
;
;/M/ Modifié par.......: Marc Poulin.
;    Date..............: 14 mars 2000.
;    Description.......: Ajuster l'ordre de tri du rapport afin que l'unité administrative et le compte soit trié dans le même 
;                        ordre que la liste CP302 (Vérification des factures / Notes de crédit / Ajustement).
;
;    Référence.........: Stabilisation du module Achats/Réceptions Inventaire (Magasin) - point 21.
;    
;    Signet............: MP-00-03-14_S21.
;
;-----------------------------------------------------------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS ; MP-00-03-10_S15.

ACCESS *WAR324   &
  ;
  ; Pour afficher la date de réquisition si la réception de la commande
  ; en référait une.
  ;
  LINK (CIECLE OF WAR324, &
        CMDCLE OF WAR324, &
        "000") &
     TO CIECLE, &
        CMDCLE, &
        CMDAJT OF ARCOMMANDE   &

  ;
  ; Pour afficher le nom de l'entrepôt
  ;
  LINK CIECLE OF WAR324, &
       ENTCLE OF WAR324  &
    TO CIECLE, &
       ENTCLE OF ARENTREPOT    &

  ;
  ; Pour sélectionner la devise du fournisseur.
  ;
  LINK FOUCIECLE OF WAR324, &
       FOUCLE    OF WAR324, &
       CIECLE    OF WAR324  &
    TO FOUCIECLE, &
       FOUCLE,    &
       FOCCIECLE OF VFOC_FOU     &

  ;
  ; Pour afficher le nom du fournisseur divers
  ;
  LINK FOUCIECLE OF WAR324, &
       REFCLETYP OF WAR324, &
       FOUCLE    OF WAR324, &
       RADCLE    OF WAR324  &
    TO REFCIECLE, &
       REFCLETYP, &
       REFCLENUM, &
       RADCLE OF MCREF_ADRESSE    &

  ;
  ; Statut de la réception
  ;
  LINK ("RECSTU",                 &
         RECSTU OF WAR324)       &
    TO XELNOM,                    &
       CBICLE OF VCBI_DSC   ALIAS A_CBI_RECSTU OPTIONAL &

  ;
  ; Devise du consolidé
  ;
  LINK ("1") &
    TO CIENMC OF MCHOLDING    &

  ;
  ; Détail de la réception de commande d'achat
  ;
  LINK CIECLE OF WAR324, &
       RECCLE OF WAR324  &
    TO CIECLE, &
       RECCLE OF ARREC_DETAIL &

  ;
  ;
  ;
  LINK CIECLE OF WAR324, &
       CMDCLE OF WAR324, &
       CMDAJT OF ARREC_DETAIL, &
       REDCLE OF ARREC_DETAIL &
    TO CIECLE, &
       CMDCLE, &
       CMDAJT, &
       CDECLE OF ARCMD_DETAIL   &

  ; MP-00-03-10_S15.
  ;
  ; Récupération des notes descriptives associées aux items commandés et réceptionnés.
  ;
  LINK (CIECLE OF ARCMD_DETAIL , &
        CMDCLE OF ARCMD_DETAIL , &
        CMDAJT OF ARCMD_DETAIL , &
        CDECLE OF ARCMD_DETAIL , &
        "1")                     &
    TO  CIECLE                 , &
        CMDCLE                 , &
        CMDAJT                 , &
        CDECLE                 , &
        CDNIMP                   OF ARCDE_NOTE     OPTIONAL &

  ;
  ; Statut de la ligne de réception
  ;
  LINK ("REDSTU",                 &
         REDSTU OF ARREC_DETAIL)  &
    TO XELNOM,                    &
       CBICLE OF VCBI_DSC   ALIAS A_CBI_REDSTU OPTIONAL &


  ;
  ; Pour aller chercher la quantité reçue à date pour chacune des
  ; réceptions afin de pouvoir calculer la quantité à recevoir pour
  ; la commande.
  ;
  LINK CIECLE OF WAR324      , &
       CMDCLE OF WAR324      , &
       RECCLE OF WAR324      , &
       REDCLE OF ARREC_DETAIL  &
    TO CIECLE                , &
       CMDCLE                , &
       RECCLE                , &
       REDCLE                 OF *WAR324C

DEFINE D_COP CHARACTER SIZE 1 = PARM
DEFINE D_RADNOM CHARACTER SIZE 40 = RADNOM OF MCREF_ADRESSE[1:40]

; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des réceptions / Détaillée"
@ELSE
       "%%%Liste des réceptions / Détaillée"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       ""
@ELSE
       "%%%"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR324Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM


USE usflgdet NOLIST ;Flag Bilingue (O/N)

;
;Indique si la réception est journalisée.
;
DEFINE D_RECDATJOU CHARACTER SIZE 3 &
        = DZ_FLGAFFO IF RECDATJOU OF WAR324 <> 0 ELSE &
          DZ_FLGAFFN

 ; Statuts de réception demandés
 ;
DEFINE D_CBIDSC1 CHARACTER SIZE 15 = "Approuvée " &
  IF RECSTU = "A"&
    ELSE ""

DEFINE D_CBIDSC2 CHARACTER SIZE 15 = "Complète "&
  IF RECSTU = "C" &
    ELSE ""

DEFINE D_CBIDSC3 CHARACTER SIZE 15 = "Préliminaire " &
  IF RECSTU = "P" &
    ELSE ""

DEFINE D_CBIDSC4 CHARACTER SIZE 15 = "Sélectionnée " &
  IF RECSTU = "S" &
    ELSE ""

DEFINE D_CBIDSCDEM CHARACTER SIZE 70 = &
  PACK (TRUNCATE(D_CBIDSC1) + " " + &
        TRUNCATE(D_CBIDSC2) + " " + &
        TRUNCATE(D_CBIDSC3) + " " + &
        TRUNCATE(D_CBIDSC4))


DEFINE D_CBIDSC_RECSTU CHARACTER SIZE 15 = CBIDSC OF A_CBI_RECSTU
DEFINE D_CBIDSC_REDSTU CHARACTER SIZE 15 = CBIDSC OF A_CBI_REDSTU

DEFINE D_RECCLE CHARACTER SIZE 3 = RECCLE OF WAR324[10:12]

DEFINE    D_DEVCLEIMP   CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING OR &
             0 = SIZE(TRUNCATE(FOUCLE OF WAR324)) &
          ELSE "/" + DEVCLE OF VFOC_FOU

; Ajustement pour le changement de siècle
DEFINE D_CMDCLE_TRI CHARACTER SIZE 10 = CMDCLE OF WAR324 &
  IF CMDCLE OF WAR324[1:1] < "8" &
  ELSE "0" + CMDCLE OF WAR324 


DEFINE D_CPTUNA CHAR SIZE 15 = TRUNC(UNACLE OF ARREC_DETAIL) + "-" + &
                               TRUNC(CPTCLE OF ARREC_DETAIL)

DEFINE D_COPIE CHAR SIZE 20 = "Copie magasin"  if d_cop = "1" else &
                              "Copie comptabilité"

SORT ON CIECLE OF WAR324       & ; Compagnie
     ON ENTCLE OF WAR324       & ; Entrepôt
     ON DEVCLE OF VFOC_FOU     & ; Devise
     ON RECSTU OF WAR324       & ; Statut de la commande
     ON D_CMDCLE_TRI           & ; Numéro de commande
     ON RECCLE OF WAR324       & ; Numéro de réception
     ON D_CPTUNA               & ; Unité administrative et compte ; MP-00-03-14_S21.
     ON REDCLE OF ARREC_DETAIL & ; Numéro d'item de la réception
     ON CDNSEQ OF ARCDE_NOTE     ; Numéro de séquence de la note descriptive. ; MP-00-03-10_S15.


USE ustit220 NOLIST
 SKIP 2                         &
  TAB 001 "Devise............:" &
  TAB 021 DEVCLE OF VFOC_FOU    &
 SKIP                           &
  TAB 001 "Entrepôt..........:" &
  TAB 021 ENTCLE OF WAR324      &
  TAB 027 ENTNOM OF ARENTREPOT  &
 SKIP                           &
  TAB 001 "Statuts demandés..:" &
  TAB 021 D_CBIDSCDEM           &
 SKIP 2                         &
  TAB 001 "Commande  No.                                                                       ------- Dates ------              Nombre   Période            Montant "    &
 SKIP                           &
  TAB 001 "d'achat   réception Fournisseur                                     Statut          Commande   Réception  Journalisée d'items comptable  Lot        brut  "    &
 SKIP                           &
  TAB 001 "                                                                                    aa/mm/jj    aa/mm/jj" &
 SKIP                           &
  TAB 001 "--------- --------- ----------------------------------------------- --------------- --------   ---------  ----------- ------- --------- ----  ---------------" &
 SKIP 1



HEADING AT RECCLE &
  TAB 001 CMDCLE OF WAR324 &
  TAB 014 D_RECCLE  &
  TAB 021 FOUCLE OF WAR324   &
  TAB 028 D_RADNOM    &
  TAB 069 D_CBIDSC_RECSTU  &
  TAB 086 CMDDAT    OF ARCOMMANDE    &
  TAB 097 RECDAT    OF WAR324   &
  TAB 111 D_RECDATJOU &
  TAB 121 RECNBRITE OF WAR324 &
  TAB 129 PECCLE    OF WAR324 &
          PICTURE "^^-^^"            &
  TAB 137 LARCLE    OF WAR324 &
  TAB 143 RECMNTTOT OF WAR324 &
  TAB 158 D_DEVCLEIMP &
 SKIP &
  TAB 010 "No Emballages:"             &
  TAB 025 RECNUMLISEMB  OF WAR324 &
  TAB 036 RECNUMLISEMB2 OF WAR324 &
  TAB 047 RECNUMLISEMB3 OF WAR324 &
  TAB 058 RECNUMLISEMB4 OF WAR324 &
  TAB 069 "Transporteur:"              &
  TAB 083  RECTRP OF WAR324       &
  TAB 124 "Connaissement:"             &
  TAB 139  RECNUMCON OF WAR324    &
 SKIP &
  TAB 010 "Commentaire..:"             &
  TAB 025 RECCOM OF WAR324        &
 SKIP 2 &
  TAB 001 "Item    Produit                                                Qté cmd.   Qté recue   Qté à rec.  Prix unitaire     Montant net         TPS         TVQ  Escompte         Total" &
 SKIP 1

;
; Calculer la quantité à recevoir en tenant comptes des réceptions
; antérieures.
;
DEFINE D_QTE_A_REC  FLOAT SIZE 8 = CDEQTECOM    OF ARCMD_DETAIL - &
                                   T_REC_A_DATE OF WAR324C &
                                   IF REDCLE OF WAR324C = REDCLE OF ARREC_DETAIL


; --------------------------------------------------------------------------------
; MP-00-03-10_S15.

FOOTING AT CDNSEQ                   &
  TAB 001 REDCLE    OF ARREC_DETAIL &
          PRINT AT REDCLE           &
  TAB 009 PRDCLE    OF ARREC_DETAIL &
          PRINT AT REDCLE           &
  TAB 022 REDDSC    OF ARREC_DETAIL &
          PRINT AT REDCLE           &
  TAB 062 CDEQTECOM OF ARCMD_DETAIL &
          PRINT AT REDCLE           &
          PICTURE "^^,^^^.^^^ "     &
          TRAILING SIGN "-"         &
          SIGNIFICANCE 6            &
          BWZ                       &
  TAB 074 REDQTEREC OF ARREC_DETAIL &
          PRINT AT REDCLE           &
          PICTURE "^^,^^^.^^^ "     &
          TRAILING SIGN "-"         &
          SIGNIFICANCE 6            &
          BWZ                       &
  TAB 086 D_QTE_A_REC               &
          PRINT AT REDCLE           &
          PICTURE "^^,^^^.^^^ "     &
          TRAILING SIGN "-"         &
          SIGNIFICANCE 6            &
  TAB 099 REDPRIUNI OF ARREC_DETAIL &
          PRINT AT REDCLE           &
  TAB 114 REDMNTNET OF ARREC_DETAIL &
          PRINT AT REDCLE           &
  TAB 130 REDMNTTPS OF ARREC_DETAIL &
          PRINT AT REDCLE           &
          PICTURE "^^^,^^^.^^ "     &
          TRAILING SIGN "-"         &
          SIGNIFICANCE 5            &
          BWZ                       &
  TAB 142 REDMNTTVQ OF ARREC_DETAIL &
          PRINT AT REDCLE           &
          PICTURE "^^^,^^^.^^ "     &
          TRAILING SIGN "-"         &
          SIGNIFICANCE 5            &
          BWZ                       &
  TAB 154 REDMNTESC OF ARREC_DETAIL &
          PRINT AT REDCLE           &
          PICTURE "^,^^^.^^ "       &
          TRAILING SIGN "-"         &
          SIGNIFICANCE 5            &
          BWZ                       &
  TAB 164 REDMNTTOT OF ARREC_DETAIL &
          PRINT AT REDCLE           &
 SKIP                               &
  TAB 022 CDNDSC OF ARCDE_NOTE      

; --------------------------------------------------------------------------------


FOOTING AT D_CPTUNA                                                              & ; MP-00-03-14_S21.
 SKIP                                                                            &
  TAB 114 "--------------- ----------- ----------- --------- ---------------"    &
 SKIP                                                                            &
  TAB 009 "Compte:"                                                              &
  TAB 017 D_CPTUNA                                                               &
  TAB 114 REDMNTNET OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          BWZ                                                                    &
  TAB 130 REDMNTTPS OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          PICTURE "^^^,^^^.^^ "                                                  &
          TRAILING SIGN "-"                                                      &
          SIGNIFICANCE 5                                                         &
          BWZ                                                                    &
  TAB 142 REDMNTTVQ OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          PICTURE "^^^,^^^.^^ "                                                  &
          TRAILING SIGN "-"                                                      &
          SIGNIFICANCE 5                                                         &
          BWZ                                                                    &
  TAB 154 REDMNTESC OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          PICTURE "^,^^^.^^ "                                                    &
          TRAILING SIGN "-"                                                      &
          SIGNIFICANCE 5                                                         &
          BWZ                                                                    &
  TAB 164 REDMNTTOT OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
 SKIP 2                                                                          


FOOTING AT RECCLE                                                                &
 SKIP                                                                            &
  TAB 114 "--------------- ----------- ----------- --------- ---------------"    &
 SKIP                                                                            &
  TAB 011 "Total réception:"                                                     &
  TAB 114 REDMNTNET OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          BWZ                                                                    &
  TAB 130 REDMNTTPS OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          PICTURE "^^^,^^^.^^ "                                                  &
          TRAILING SIGN "-"                                                      &
          SIGNIFICANCE 5                                                         &
          BWZ                                                                    &
  TAB 142 REDMNTTVQ OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          PICTURE "^^^,^^^.^^ "                                                  &
          TRAILING SIGN "-"                                                      &
          SIGNIFICANCE 5                                                         &
          BWZ                                                                    &
  TAB 154 REDMNTESC OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
          PICTURE "^,^^^.^^ "                                                    &
          TRAILING SIGN "-"                                                      &
          SIGNIFICANCE 5                                                         &
          BWZ                                                                    &
  TAB 164 REDMNTTOT OF ARREC_DETAIL                                              &
          SUBTOTAL AT REDCLE                                                     & ; MP-00-03-10_S15.
  TAB 181 D_DEVCLEIMP                                                            &
 SKIP PAGE

PAGE FOOTING                             &
 SKIP 2                                  &
  TAB 145 "____________________________" &
          PRINT AT RECCLE                & ; MP-00-03-10_S18.
 SKIP                                    &
  TAB 002 D_COPIE                        &
  TAB 145 "    Signature autorisée"      &
          PRINT AT RECCLE                  ; MP-00-03-10_S18.


BUILD ar324z

@IF FORMAT

R324                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                  Page: 9999
99/99/99 HH:MM xxxxxxxxxxxx                       Liste des réceptions / Détaillée
                                    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Devise............: xxx
Entrepôt..........: xxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Statuts demandés..: xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxx

 Commande  No.                                                                       ------------ Dates -----------   Nombre   Période                 Montant
  d'achat  réception Fournisseur                                     Statut         Commande   Réception  Journalisée d'items comptable  Lot              brut
 --------- --------- ----------------------------------------------- --------------- --------  ---------  ----------- ------- ---------  ----   ---------------
 xxxxxxxxx    xxx    xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xx/xx/xx   xx/xx/xx      xxx       999     xx-xx    xxxx   999,999,999.99-/xxx
          No Emballages: xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx xxxxxxxxxx Transporteur: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Connaissement: xxxxxxxxxxxxxxxxxxxx

  Item    Produit                                          Qté cmd.  Qté recue   Qté \340 rec.  Prix unitaire     Montant net         TPS     TVQ    Escompte      Total
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
                                                                                                             --------------- ----------- ----------- --------- ---------------
          Compte: 12345678-123456                                                                            999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-


  Item    Produit                                          Qté cmd.   Qté recue   Qté à rec.  Prix unitaire     Montant net         TPS         TVQ  Escompte      Total
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
  xxxxxxx xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999- 99,999.999- 99,999.999- 999,999,999.99- 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
                                                                                                             --------------- ----------- ----------- --------- ---------------
          Compte: 12345678-123456                                                                            999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-
                                                                                                             --------------- ----------- ----------- --------- ---------------
            Total réception:                                                                                 999,999,999.99- 999,999.99- 999,999.99- 9,999.99- 999,999,999.99-/xxx
                                                                                                                                                               ---------------
              Total pour l'entrepôt: xxxx en devise xxx                                                                                                        999,999,999.99-
                                                                                                                                                               ---------------

@ENDIF
