;/Z/ Statistique de vente par date.

USE ussetqzs NOLIST
SET FORMFEED NOBLANKS
SET NOHEAD

ACCESS  *WFA302A &

        LINK CIECLE    OF WFA302A, &
             FHITRPCLE OF WFA302A  &
          TO CIECLE,               &
             TRPCLE OF FATRAJET_PRODUIT OPTIONAL &

        LINK CIECLE    OF WFA302A, &
             FHIETACLE OF WFA302A  &
          TO CIECLE,               &
             ETACLE OF FAELEMENT_TARIF OPTIONAL


DEFINE D_TRI    CHARACTER SIZE 1 = "1"                             &
                                IF (T_ETATYP OF WFA302A = "1"   OR &
                                    T_ETATYP OF WFA302A = "2"   OR &
                                    T_ETATYP OF WFA302A = "3") AND &
                                    T_NUMTRX OF WFA302A = "1"      &
                              ELSE "2"

DEFINE D_TRI2   CHARACTER SIZE 1 = "1"                             &
                                IF FHIDAT OF WFA302A = 0           &
                              ELSE "2"

DEFINE D_SIZETA   INTEGER SIZE 4 = SIZE(TRUNCATE(FHIETACLE OF WFA302A))

SORT ON T_TYPTRX,   &
        FHITRPCLE,  &
        D_TRI2,     &
        FHIDAT,     &
        T_ETATYP,   &
        D_TRI,      &
        T_NUMTRX,   &
        D_SIZETA,   &
        FHIETACLE


DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = " Statistiques de ventes par date "
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "  "
DEFINE DZ_NUMRAP  CHARACTER SIZE 12 = "FA302Z"
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM

DEFINE D_ENTETE   CHARACTER SIZE 60 = "Produit : "                 + &
                                      FHITRPCLE OF WFA302A         + &
                                      "  "                         + &
                                      TRPDSCFRA OF FATRAJET_PRODUIT  &
                                   IF FHITRPCLE <> " "               &
                                 ELSE "SOMMAIRE Toutes origines -" + &
                                      " destinations"


DEFINE D_LABEL1   CHARACTER SIZE 30 = "PASSAGERS"               &
                                   IF T_ETATYP OF WFA302A = "1" &
                                 ELSE "VÉHICULES"               &
                                   IF T_ETATYP OF WFA302A = "2" &
                                 ELSE "AUTRES"                  &
                                   IF T_ETATYP OF WFA302A = "3" &
                                 ELSE " "

DEFINE D_LABEL2   CHARACTER SIZE 40 = "Quantité     Montant      Rendement" &
                                   IF T_ETATYP OF WFA302A = "1"          OR &
                                      T_ETATYP OF WFA302A = "2"          OR &
                                      T_ETATYP OF WFA302A = "3"             &
                                 ELSE " "

DEFINE D_LABEL3   CHARACTER SIZE 40 = "--------     -------     -----------" &
                                   IF T_ETATYP OF WFA302A = "1"           OR &
                                      T_ETATYP OF WFA302A = "2"           OR &
                                      T_ETATYP OF WFA302A = "3"              &
                                 ELSE " "

DEFINE D_LABEL4   CHARACTER SIZE 30 = "---------"               &
                                   IF T_ETATYP OF WFA302A = "1" &
                                 ELSE "---------"               &
                                   IF T_ETATYP OF WFA302A = "2" &
                                 ELSE "------"                  &
                                   IF T_ETATYP OF WFA302A = "3" &
                                 ELSE " "

DEFINE D_DATTOT   CHARACTER SIZE 10 = ASCII(FHIDAT OF WFA302A,8)[3:2] + "/" + &
                                      ASCII(FHIDAT OF WFA302A,8)[5:2] + "/" + &
                                      ASCII(FHIDAT OF WFA302A,8)[7:2]         &
                                   IF FHIDAT OF WFA302A <> 0                  &
                                 ELSE "T O T A L"

DEFINE D_ETADSC   CHARACTER SIZE 30 = FHIDSC OF WFA302A                 &
                                   IF FHIETACLE = "9999"                &
                                 ELSE ETADSCFRA OF FAELEMENT_TARIF      &
                                   IF FHIETACLE <> " "                  &
                                 ELSE "Total passagers"                 &
                                   IF T_ETATYP OF WFA302A = "1"     AND &
                                      T_NUMTRX OF WFA302A = "2"         &
                                 ELSE "Total passagers (segment)"       &
                                   IF T_ETATYP OF WFA302A = "1"     AND &
                                      T_NUMTRX OF WFA302A = "3"         &
                                 ELSE "Total véhicules"                 &
                                   IF T_ETATYP OF WFA302A = "2"     AND &
                                      T_NUMTRX OF WFA302A = "2"         &
                                 ELSE "Total véhicules (segment)"       &
                                   IF T_ETATYP OF WFA302A = "2"     AND &
                                      T_NUMTRX OF WFA302A = "3"         &
                                 ELSE "Total autres"                    &
                                   IF T_ETATYP OF WFA302A = "3"     AND &
                                      T_NUMTRX OF WFA302A = "2"         &
                                 ELSE "Total autres (segment)"          &
                                   IF T_ETATYP OF WFA302A = "3"     AND &
                                      T_NUMTRX OF WFA302A = "3"         &
                                 ELSE "Grand total (pass.+véhicules)"   &
                                   IF T_ETATYP OF WFA302A = "4"     AND &
                                      T_NUMTRX OF WFA302A = "1"         &
                                 ELSE "Grand total (segment)"           &
                                   IF T_ETATYP OF WFA302A = "4"     AND &
                                      T_NUMTRX OF WFA302A = "2"         &
                                 ELSE " "

DEFINE D_RENFAC    FLOAT SIZE 08 = ROUND((T_MNTFAC1 OF WFA302A / &
                                          T_QTEFAC1 OF WFA302A) * 100)


DEFINE D_SOULIG   CHARACTER SIZE 7 = "_______"   &
                                  IF D_TRI = "1" &
                                ELSE " "

USE ustit66l NOLIST
       SKIP 2 &
   TAB 001 D_ENTETE &
       SKIP 2

REPORT &
   TAB 001 D_LABEL1 PRINT AT T_ETATYP &
   TAB 060 D_LABEL2 PRINT AT T_ETATYP &
       SKIP &
   TAB 001 D_LABEL4 PRINT AT T_ETATYP &
   TAB 060 D_LABEL3 PRINT AT T_ETATYP &
       SKIP &
   TAB 012 D_DATTOT PRINT AT FHIDAT   &
   TAB 025 D_ETADSC &
   TAB 061 T_QTEFAC1 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 073 T_MNTFAC1 PICTURE "^^^,^^^ "    &
                     TRAILING SIGN "-" BWZ &
   TAB 083 D_RENFAC  PICTURE "^^^,^^^.^^ " &
                     TRAILING SIGN "-" BWZ

FOOTING AT T_ETATYP  SKIP 2

FOOTING AT FHIDAT &
TAB 025 "______________________________________________________________________" &
    SKIP 2

FOOTING AT D_TRI &
        SKIP &
        TAB 061 D_SOULIG &
        TAB 073 D_SOULIG &
        TAB 086 D_SOULIG &
        SKIP

FOOTING AT D_TRI2    SKIP PAGE
FOOTING AT FHITRPCLE SKIP PAGE

BUILD fa302a
