;/T/ 2.2.4 Liste des sorties/réceptions de produit
;
;/P/ Programmeur...: Alain Simard
;    Date Création.: 30 Mai 1995
;
;    Description...: Liste des sorties et des réceptions des produits
;
;    Paramètres....: aucun
;
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS *WPD310SR &

        LINK    G_CIECLE    OF WPD310SR,   &
                G_TGRCODGRA OF WPD310SR    &
        TO      CIECLE,                    &
                TGRCODGRA OF PDTYPE_GRANIT &

        LINK    G_CIECLE    OF WPD310SR,   &
                G_RSOCODSOR OF WPD310SR    &
        TO      CIECLE,                    &
                RSOCODSOR OF PDRAISON_SORTIE OPT &

        LINK    G_FOUCIECLE OF WPD310SR , &
                G_FOUCLE OF WPD310SR &
        TO      FOUCIECLE, &
                FOUCLE OF VFOC_FOU OPT


SORT    ON G_DAT_MOU OF WPD310SR &
        ON G_NUM_MOU OF WPD310SR &
        ON G_CAINUMCAI           &
        ON G_CODE_PROD OF WPD310SR


SELECT  VFOC_FOU &
        IF FOCCIECLE OF VFOC_FOU = G_CIECLE OF WPD310SR

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 &
        = "Liste des mouvements (réception/sortie) de produit"

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 &
        = "Pour la période du " &
        + ASCII(G_DATE_DEB,8)[1:4] &
        + "/" &
        + ASCII(G_DATE_DEB,8)[5:2] &
        + "/" &
        + ASCII(G_DATE_DEB,8)[7:2] &
        + " au " &
        + ASCII(G_DATE_FIN,8)[1:4] &
        + "/" &
        + ASCII(G_DATE_FIN,8)[5:2] &
        + "/" &
        + ASCII(G_DATE_FIN,8)[7:2]

;-------------------------------------------------------------------------------
;
; Numéro du rapport.
;

DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD310"


;-------------------------------------------------------------------------------
;
; Nom de la compagnie.
;

DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


;-------------------------------------------------------------------------------
;
; Définition des éléments variables
;

DEFINE D_TYPE_MOU CHAR SIZE 9 &
        = CASE OF G_TYP_MOU OF WPD310SR &
        WHEN "S"        THEN "Sortie" &
        WHEN "R"        THEN "Réception" &
        DEFAULT " "

DEFINE D_CONFIRME CHAR SIZE 3 &
        = CASE OF G_SPRCON OF WPD310SR &
        WHEN "1"        THEN "Oui" &
        WHEN "0"        THEN "Non" &
        DEFAULT " "

DEFINE D_TYPE_PROD CHAR SIZE 5 &
        = CASE OF G_TYPE_PROD OF WPD310SR &
        WHEN "B"        THEN "Bloc" &
        WHEN "T"        THEN "Tran." &
        WHEN "D"        THEN "Diag." &
        WHEN "P"        THEN "Prod." &
        DEFAULT " "


DEFINE D_TGRDSCFRA001 CHAR SIZE 25 = TGRDSCFRA001 OF PDTYPE_GRANIT


DEFINE D_NUMERO CHAR SIZE 12 &
        =       G_CODE_PROD OF WPD310SR[1:4] &
                + "-" &
                + G_CODE_PROD OF WPD310SR[5:5] &
                + "-" &
                + G_CODE_PROD OF WPD310SR[10:1] &
                IF G_TYPE_PROD = "B" &

        ELSE    G_CODE_PROD OF WPD310SR[1:2] &
                + "-" &
                + G_CODE_PROD OF WPD310SR[3:5] &
                IF G_TYPE_PROD OF WPD310SR = "D" &

        ELSE    G_CODE_PROD OF WPD310SR[1:7] &
                IF G_TYPE_PROD OF WPD310SR = "T" &

        ELSE    " "

USE ustitstd_prod NOLIST
                TAB 017 "Num. mouvement.:" &
                TAB 034 G_NUM_MOU OF WPD310SR &
                TAB 100 "Type mouvement:" &
                TAB 116 D_TYPE_MOU &
        SKIP 1 &
                TAB 017 "Date mouvement:" &
                TAB 034 G_DAT_MOU OF WPD310SR PIC "^^^^/^^/^^" &
        SKIP 1 &
                TAB 017 "Raison........:" &
                TAB 034 G_RSOCODSOR OF WPD310SR &
                TAB 041 RSODSC001 OF PDRAISON_SORTIE &
        SKIP 1 &
                TAB 017 "Fournisseur...:" &
                TAB 034 G_FOUCLE OF WPD310SR &
                TAB 041 FOUNOM OF VFOC_FOU &
        SKIP 1 &
                TAB 017 "No de P/O.....:" &
                TAB 034 G_BCMNUMBCM OF WPD310SR BWZ &
        SKIP 1 &
                TAB 017 "Demandeur.....:" &
                TAB 034 G_DMR_CNS OF WPD310SR &
        SKIP 1 &
                TAB 017 "Confirmé (O/N):" &
                TAB 034 D_CONFIRME &
        SKIP 2 &
                TAB 092 "--Bloc--" &
                TAB 101 "-Tranche-" &
                TAB 113 "-Dia/prod-" &
                TAB 127 "-Diag-" &
        SKIP 1 &
                TAB 001 "Caisson" &
                TAB 011 "Numéro" &
                TAB 023 "Genre" &
                TAB 030 "Type de granit" &
                TAB 066 "Longueur" &
                TAB 075 "Hauteur" &
                TAB 083 "Epaisseur" &
                TAB 092 "  M3 net" &
                TAB 101 "    M2" &
                TAB 113 "Fini  Qte" &
                TAB 127 "Prio." &
        SKIP 2

REPORT &
                TAB 001 G_CAINUMCAI OF WPD310SR &
                TAB 011 D_NUMERO &
                TAB 023 D_TYPE_PROD &
                TAB 030 G_TGRCODGRA OF WPD310SR &
                TAB 034 D_TGRDSCFRA001 &
                TAB 066 G_LONGUEUR OF WPD310SR PICTURE "^^^^" &
                TAB 075 G_LARGEUR OF WPD310SR PICTURE "^^^^" &
                TAB 083 G_EPAISSEUR OF WPD310SR PICTURE "^^^^" &
                TAB 092 G_VOLMC3 OF WPD310SR PICTURE "^^^^.^^^^" BWZ &
                        OUTPUT SCALE 4 &
                TAB 101 G_SURMC2 OF WPD310SR PICTURE "^^^^.^^^^" BWZ &
                TAB 114 G_CDDFIN OF WPD310SR &
                TAB 118 G_QTE OF WPD310SR PICTURE "^^^^" BWZ &
                TAB 128 G_CODPRI OF WPD310SR &
        SKIP

FOOTING AT G_NUM_MOU &
        SKIP PAGE

BUILD pd310z
@IF FORMAT
                                                                                                   1
         1         2         3         4         5         6         7         8         9         0         1         2         3
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
             |xx999                           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                 PAGE:9999
             |xxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    AA/MM/JJ 99:99
             |                  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
             |
             |
             |
             |
             |
-------------+

                Num. mouvement.: 9999999                                                            Type mouvement: (entrée/sortie)
                Date mouvement.: AA/MM/JJ
                Raison.........: XXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
                Destination....: 123456 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                No de P/O......: 123456
                Demandeur......: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
                Confirmé(O/N)..: X

                                                                                           --bloc-- -tranche-   -Dia/Prod-   -Diag-
 Numéro      GENRE  Type de granit                               Longueur Largeur Épaisseur  M3 net      M2     Fini  Qte     Prio.
9999-99999-A bloc   999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999   999.9999
9999-99999-A bloc   999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999   999.9999
9999-99999-A bloc   999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999   999.9999

XX-XXXXX     diag.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                                      12
XX-XXXXX     diag.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                                      12
XX-XXXXX     diag.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                                      12

             prod.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                        12  1234
             prod.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                        12  1234
             prod.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999                        12  1234

9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
9999999      tran.  999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   9999    9999      9999            999.999
@ENDIF
