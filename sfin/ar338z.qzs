;/T/ Liste des soumissions par réquisitions d'achat 
;
;/P/ Programmeur...: Steeve Duguay
;    Date Création.: 2 février 1995
;
;    Paramètres....: Compagnie 
;                    Entrepôt           (Un, plusieurs ou tous)
;                    Numéro réquisition (Borne de début, <retour>=tous)
;                    Numéro réquisition (Borne de fin)
;                     
;
;/M/                 Caroline Kaine, 12 nov 96, Psse du format 180 a 175
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANK

ACCESS ARREQUISITION   &
  LINK CIECLE OF ARREQUISITION, &
       ENTCLE OF ARREQUISITION &
    TO CIECLE, &
       ENTCLE OF ARENTREPOT   &
  
  LINK CIECLE OF ARREQUISITION, &
       REQCLE OF ARREQUISITION, &
       REQCLENUM OF ARREQUISITION  &
    TO CIECLE,                 &
       REQCLE,                 &
       REQCLENUM OF ARREQ_SOUMISSION   &

  LINK FOUCIECLE OF ARREQ_SOUMISSION, &
       FOUCLE    OF ARREQ_SOUMISSION, &
       CIECLE    OF ARREQ_SOUMISSION  &
    TO FOUCIECLE, &
       FOUCLE,    &
       FOCCIECLE OF VFOC_FOU   &

   ;
   ; Devise du consolidé
   ;
  LINK ("1") &
    TO CIENMC OF MCHOLDING    


CHOOSE CIECLE    PARM,       &  ; Compagnie du menu
       ENTCLE    PARM,       &  ; Entrepôt
       REQCLE    PARM RANGE, &  ; Numéro de réquisition
       REQFLGSOU "1"

; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = &
@IF FRANCAIS
       "Liste des soumissions par réquisition d'achat"
@ELSE
       "%%%Liste des soumissions par réquisition d'achat"
@ENDIF

DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = &
@IF FRANCAIS
       ""
@ELSE
       "%%%"
@ENDIF

;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "AR338Z"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM  CHARACTER SIZE 40 = PARM 

DEFINE D_DEVCLE    CHARACTER SIZE 04 = " " &
          IF DEVCLE OF VFOC_FOU = DEVCLE OF MCHOLDING &
          ELSE "/" + DEVCLE OF VFOC_FOU

DEFINE D_FOUNOM    CHARACTER SIZE 30 = FOUNOM OF VFOC_FOU[1:30]

USE usflgdet NOLIST ;Flag Bilingue (O/N)

SORT ON CIECLE    OF ARREQUISITION &  ; Compagnie
     ON ENTCLE    OF ARREQUISITION &  ; Entrepôt
     ON REQCLE    OF ARREQUISITION &  ; Numéro réquisition
     ON REQCLENUM OF ARREQUISITION &  ; Numéro d'item du produit
     ON FOUCLE    OF ARREQ_SOUMISSION


USE ustit175 NOLIST
  SKIP 2 &
@IF FRANCAIS
TAB 001 "Entrepôt..........:" &
@ELSE 
TAB 001 "%%%Entrepôt..........:" &
@ENDIF
TAB 021 ENTCLE OF ARREQUISITION    &
TAB 027 ENTNOM OF ARENTREPOT &
  SKIP 2 &

@IF FRANCAIS
         TAB 001 "No                     Date" &
  SKIP   TAB 001 "réquisition  Item    Requise                    Produit                            Qté commandée         Fournisseur             Prix unitaire  " &" &
@ELSE
         TAB 001 "%%%                    Date" &
  SKIP   TAB 001 "%%%" &
@ENDIF
  SKIP   TAB 001 "                    aa/mm/jj" &" &
  SKIP   TAB 001 "----------- ------- -------- ----------------------------------------------------- ------------- ------------------------------ --------------- " &" &
  SKIP 2

REPORT &
  TAB 002 REQCLE    OF ARREQUISITION PRINT AT REQCLE &
  TAB 013 REQCLENUM OF ARREQUISITION PRINT AT REQCLENUM &
  TAB 021 REQDATREQ OF ARREQUISITION PRINT AT REQCLENUM &
  TAB 030 PRDCLE    OF ARREQUISITION PRINT AT REQCLENUM &
  TAB 043 REQDSC    OF ARREQUISITION PRINT AT REQCLENUM &
  TAB 085 REQQTEREQ OF ARREQUISITION PRINT AT REQCLENUM &
    PICTURE "^^,^^^.^^^ "      &
    TRAILING SIGN "-"          &
    SIGNIFICANCE 6             &
    BWZ  &
  TAB 098 D_FOUNOM &                      
  TAB 129 RSOPRIUNI OF ARREQ_SOUMISSION &
  TAB 144 D_DEVCLE

FOOTING AT REQCLENUM SKIP 2 
 
BUILD ar338z 
@IF FORMAT

Entrepôt..........: xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

No                     Date                                                                                                                     
réquisition  Item    Requise                    Produit                            Qté commandée         Fournisseur             Prix unitaire  " &
----------- ------- -------- ----------------------------------------------------- ------------- ------------------------------ --------------- " &
 xxxxxxxxx  xxx.xxx 99/99/99 xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999-  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx ^,^^^,^^^.^^^^- 
 xxxxxxxxx  xxx.xxx 99/99/99 xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999-  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx ^,^^^,^^^.^^^^- 
 xxxxxxxxx  xxx.xxx 99/99/99 xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999-  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx ^,^^^,^^^.^^^^- 
 xxxxxxxxx  xxx.xxx 99/99/99 xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999-  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx ^,^^^,^^^.^^^^- 
 xxxxxxxxx  xxx.xxx 99/99/99 xxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99,999.999-  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx ^,^^^,^^^.^^^^- 
@ENDIF
