;/P/ Liste des blocs non attache a un fournisseur, par type de granit
;
;/M/ François Déry, 18 mai 1999
;       Conversion vers unix/interbase.
;
;/M/ Programmeur....: Bernard Samson
;    Date...........: 21-10-1999
;    Description....: Ajustement pour que le numero de bloc s'incrive sous sa
;                     nouvelle forme et pour que le rapport sorte par type de
;		      granit au lieu de par carriere.
;
;
;/M/ Programmeur..: Remy Demers
;    Date.........: 02-11-1999
;    Description..: Alignement du rapport
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST
SET NOBLANKS

ACCESS IFBLOC
CHOOSE CIECLE PARM, &
       TGRCODGRA PARM

DEFINE TMP-DAT-FIN    DATE = PARM


; LE SELECT PREND LES ENREGISTREMENTS REPONDANT AUX CRITERES SUIVANT :
; - LA DATE DU BLOC <= LA DATE DEMANDE EN PARAMETRE
; - LE BLOC EST EXPEDIE

SELECT IF     0 = SIZE(TRUNC(FOUCLE OF IFBLOC)) &
          AND (   BLCDAT OF IFBLOC <= TMP-DAT-FIN &
               OR                                 &
                  TMP-DAT-FIN       = 0           &
              )


DEFINE D_NUMBLC CHARACTER SIZE 14 =   TGRCODGRA OF IFBLOC &
                                    + IFCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCCLE OF IFBLOC    &
                                    + "-"                 &
                                    + BLCRED OF IFBLOC

DEFINE D_NUMCOM CHARACTER SIZE 9 =   PJTABR OF IFBLOC &
                                    + "-"              &
                                    + PJTANN OF IFBLOC &
                                    + "-"              &
                                    + COMNUMCOMINT     &
                                    IF PJTABR OF IFBLOC <> "" AND &
                                       PJTANN OF IFBLOC <> "" AND &
                                       COMNUMCOMINT OF IFBLOC <> "" &
                                    ELSE ""
SORT ON CIECLE OF IFBLOC &
     ON TGRCODGRA OF IFBLOC &
     ON D_NUMBLC         &
     ON D_NUMCOM        &
     ON CATCLE OF IFBLOC &
     ON COEDATDEB OF IFBLOC


;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = "" IF TMP-DAT-FIN = 0 &
        ELSE "En date du : " + ASCII( TMP-DAT-FIN , 8 )[3:2] + "/" + &
                               ASCII( TMP-DAT-FIN , 8 )[5:2] + "/" + &
                               ASCII( TMP-DAT-FIN , 8 )[7:2]
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IF312"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


USE ustitprd47 NOLIST
  SKIP 2 &
TAB 003 "TYPE    NO               DATE     DIMENSIONS      TOTAL      T/M     NO      NO       CAT COUL QUAL   DATE"        &
SKIP 1 &
TAB 003 "GRAN. BLOC              PROD    LONG HAUT LARG     M3               FACT   COMANDE                     DEBUT"  &
SKIP 1 &
TAB 003 "____ ______________  ________  ________________ _______    _______ ______  _________  ___ ____ ____  ________"    &
SKIP 2

REPORT  TGRCODGRA OF IFBLOC PRINT AT TGRCODGRA OF IFBLOC &
        D_NUMBLC                           &
        BLCDAT       OF IFBLOC             &
        BLCVENDULONG OF IFBLOC             &
        BLCVENDUHAU  OF IFBLOC             &
        BLCVENDULAR  OF IFBLOC             &
        BLCM3TOT     OF IFBLOC             &
        BLCTFINAL    OF IFBLOC             &
        FTRNUMFAC         OF IFBLOC  &
        D_NUMCOM                             &
        CATCLE         OF IFBLOC             &
        CLRCLE       OF IFBLOC             &
        QLTCLE       OF IFBLOC             &
        COEDATDEB    OF IFBLOC

FOOTING AT TGRCODGRA OF IFBLOC &
        TAB 8 "______________"            &
        TAB 48 "__________"         &
        TAB 59 "__________"         &
SKIP 1  &
        TAB 8 COUNT "Blocs"         &
        TAB 32 "Sous-total:"        &
        TAB 49 BLCM3TOT  SUBTOTAL PICTURE "^^,^^^.^^" &
        TAB 60 BLCTFINAL SUBTOTAL PICTURE "^^,^^^.^^" &
SKIP 2

FOOTING AT CIECLE &
SKIP 3 &
        TAB 8 "______________"            &
        TAB 48 "__________"         &
        TAB 59 "__________"         &
SKIP 1  &
        TAB 8 COUNT "Blocs"         &
        TAB 32 "Total:"             &
        TAB 48 BLCM3TOT  SUBTOTAL PICTURE "^^^,^^^.^^" &
        TAB 59 BLCTFINAL SUBTOTAL PICTURE "^^^,^^^.^^" &
SKIP 2

BUILD  if312z
