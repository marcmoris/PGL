;/T/ Cédule de paiement
;
;/P/ Programmeur..: Thomas BRENNEUR
;    Date Création: 14 Juin 1993
;
;    Description..: Maintenance des montants prévus par date due, une facture
;                   a toujours au minimum une ligne de cédule. La ligne 01 est
;                   créée par la journalisation, l'usager ne peut pas
;                   détruire cette ligne.
;
;/M/ Adatation....: Marc Morissette 10 Novembre
;
;    Description..: Label :  Reste a vent. -> Solde à vent.
;
;-------------------------------------------------------------------------------
;/V/ 3.00.02    Guy Chabot 12-mai-1994 (197).
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN cp003b ACTIONBAR &
              MODE LABEL "" AT 2,80 &
              NOACTION FIELDMARK RETAIN STARTUP &
              HELP POPUP FROM 4,9 TO 20,72 &
              RECEIVING   T_CIECLEMNU  , &
                          T_CIENOM     , &
                          D_SLD_A_PYE  , &
                          CPCPT_A_PAYER, &
                          D_DEVCLE

USE ustrasse    NOLIST


TRANSACTION MAJ_TBL_CAP INHERITED


USE cp003b.hlp  NOLIST

USE ussectmp    NOLIST
    "CP003B"

USE usactecr    NOLIST

;-------------------------------------------------------------------------------

TEMPORARY T_CIECLEMNU CHARACTER SIZE  4 ; Cie du menu.
TEMPORARY T_CIENOM    CHARACTER SIZE 40 ; Nom de la Cie du menu.
DEFINE    D_DEVCLE    CHARACTER SIZE  4 ; Devise du Compte à payer

;-------------------------------------------------------------------------------
;
; Compte à payer
;
FILE CPCPT_A_PAYER    MASTER

;-------------------------------------------------------------------------------
;
; Informations sue le solde du compte à payer
;
FILE VCAP_SLD   REFERENCE

  ACCESS  VIA   FOUCIECLE, &
                FOUCLE   , &
                CAPCLE     &
          USING FOUCIECLE OF CPCPT_A_PAYER, &
                FOUCLE    OF CPCPT_A_PAYER, &
                CAPCLE    OF CPCPT_A_PAYER

;-------------------------------------------------------------------------------
;
; Cédules de paiement du compte à payer
;
FILE CPCAP_CEDULE     PRIMARY &
                      OCCURS 14 &
                      NOITEMS

  ACCESS  VIA     FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE     &
          USING   FOUCIECLE OF CPCPT_A_PAYER, &
                  FOUCLE    OF CPCPT_A_PAYER, &
                  CAPCLE    OF CPCPT_A_PAYER  &
          ORDERBY FOUCIECLE, &
                  FOUCLE   , &
                  CAPCLE   , &
                  CPCCLELIG

  ITEM FOUCIECLE OF CPCAP_CEDULE INITIAL FOUCIECLE OF CPCPT_A_PAYER
  ITEM FOUCLE    OF CPCAP_CEDULE INITIAL FOUCLE    OF CPCPT_A_PAYER
  ITEM CAPCLE    OF CPCAP_CEDULE INITIAL CAPCLE    OF CPCPT_A_PAYER

;-------------------------------------------------------------------------------
;
; Vue sur le calcul du solde de la cédule de paiement
;
FILE VCPC_SLD REFERENCE &
              OCCURS WITH CPCAP_CEDULE &
              NOITEM

  ACCESS  VIA   FOUCIECLE, &
                FOUCLE   , &
                CAPCLE   , &
                CPCCLELIG  &
          USING FOUCIECLE OF CPCAP_CEDULE, &
                FOUCLE    OF CPCAP_CEDULE, &
                CAPCLE    OF CPCAP_CEDULE, &
                CPCCLELIG OF CPCAP_CEDULE

;-------------------------------------------------------------------------------
;

DEFINE D_SLD_A_PYE FLOAT SIZE 8 &
        = CAPMNT      OF CPCPT_A_PAYER &
        + V_CAPMNTAJT OF VCAP_SLD      &
        + V_CAPMNTCRT OF VCAP_SLD      &
        - V_CAPMNTPYE OF VCAP_SLD      &
        - V_CAPMNTESC OF VCAP_SLD

DEFINE D_BALCED FLOAT SIZE 8 &
        = (  CAPMNT      OF CPCPT_A_PAYER &
           + V_CAPMNTAJT OF VCAP_SLD      &
           + V_CAPMNTCRT OF VCAP_SLD )    &
        - CAPCUMCED OF CPCPT_A_PAYER

;-------------------------------------------------------------------------------

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE  4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------

USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Cédule de paiement " &
@ELSE
      " Payment Schedule " &
@ENDIF
      CENTERED AT ,1

SKIP

USE ustitoff NOLIST           ; Pour placer le TITLE OFF

ALIGN (,3,19) (,46,62) (,,76)

FIELD CAPCLE OF CPCPT_A_PAYER &
      FIXED &
      DISPLAY PREDISPLAY &
@IF FRANCAIS
        LABEL "Facture.......:"
@ELSE
        LABEL "Invoice.......:"
@ENDIF

FIELD CAPMNT OF CPCPT_A_PAYER &
      DISPLAY PREDISPLAY &
      FIXED

FIELD D_DEVCLE &
      DISPLAY PREDISPLAY &
      FIXED

ALIGN (,3,19) (,46,62)

FIELD D_SLD_A_PYE &
      DISPLAY &
@IF FRANCAIS
        LABEL "Solde à payer.:" &
@ELSE
        LABEL "Bal. to Pay...:" &
@ENDIF
        PICTURE "^^,^^^,^^^.^^ " TRAILING SIGN "-" SIGNIF 5

FIELD D_BALCED &
      DISPLAY &
@IF FRANCAIS
        LABEL "Solde à vent..:" &
@ELSE
        LABEL "Left to Alloc.:" &
@ENDIF
        PICTURE "^^,^^^,^^^.^^ " TRAILING SIGN "-" SIGNIF 5

SKIP

DRAW FROM ,1 TO ,80

SKIP 1

TITLE &
@IF FRANCAIS
      " No                  Paiement         Montant         Montant      Montant    " &
@ELSE
      " %%                  %%%%%%%%         %%%%%%%         %%%%%%%%     %%%%%%%    " &
@ENDIF
      AT ,2

SKIP

TITLE &
@IF FRANCAIS
      "Céd.  Date due          prévu    d'ajustement            Payé   d'escompte   " &
@ELSE
      "%%%%  %%%%%%%%          %%%%%    %%%%%%%%%%%%            %%%%   %%%%%%%%%%   " &
@ENDIF
      AT ,2
SKIP

ALIGN(2,2,3) (,,8) (,,20) (,,35)(,,51)(,,67)

CLUSTER OCCURS WITH CPCAP_CEDULE ID BASE 01

FIELD CPCCLELIG OF CPCAP_CEDULE &
      HIDDEN &
      LABEL " " &
      DISPLAY

FIELD CPCDATDUE OF CPCAP_CEDULE  FORMAT YYYYMMDD PATTERN "####/##/##"&
      REQUIRED

FIELD CPCMNT    OF CPCAP_CEDULE &
      REQUIRED

FIELD V_CPCMNTAJT OF VCPC_SLD &
      DISPLAY &
      PICTURE "^^,^^^,^^^.^^ " TRAIL "-" SIGNIFICANCE 5 BWZ

FIELD V_CPCMNTPYE OF VCPC_SLD &
      DISPLAY &
      PICTURE "^^,^^^,^^^.^^ " TRAIL "-" SIGNIFICANCE 5 BWZ

FIELD V_CPCMNTESC OF VCPC_SLD &
      DISPLAY &
      PICTURE "^^^,^^^.^^ " TRAIL "-" SIGNIFICANCE 5 BWZ

CLUSTER

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès pour l'écran par son code.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END

;-------------------------------------------------------------------------------
;
;
; Montant prévu de paiement.
;
;
PROCEDURE EDIT CPCMNT
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec NOLIST
  ;
  ; Mise à jour du total de la cédule.
  ;
  LET CAPCUMCED OF CPCPT_A_PAYER = CAPCUMCED OF CPCPT_A_PAYER - &
                                   OLDVALUE(CPCMNT OF CPCAP_CEDULE) + &
                                   FIELDVALUE
END

;-------------------------------------------------------------------------------
;
; - Numérotation des lignes.
; - Affiche le total de la cédule et le reste à ventiler.
;
;
PROCEDURE PROCESS CPCMNT
BEGIN
  IF CPCCLELIG OF CPCAP_CEDULE = 0 AND &
     NEWRECORD OF CPCAP_CEDULE
  THEN BEGIN
    LET CAPNUMLIGCED OF CPCPT_A_PAYER = CAPNUMLIGCED OF CPCPT_A_PAYER + 1
    LET CPCCLELIG    OF CPCAP_CEDULE  = CAPNUMLIGCED OF CPCPT_A_PAYER
    DISPLAY CPCCLELIG OF CPCAP_CEDULE
  END
  DISPLAY D_BALCED
END

;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE INPUT CPCDATDUE
BEGIN
  USE ussecent NOLIST
END

;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR CPCAP_CEDULE
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF CPCAP_CEDULE &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la mise à jour(Entrée, Modification)
    ;
    IF     ALTEREDRECORD     OF CPCAP_CEDULE &
       AND NOT NEWRECORD     OF CPCAP_CEDULE &
       AND NOT DELETEDRECORD OF CPCAP_CEDULE &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
    ;
    ; Si l'usager modifie la cédule, il faut mettre la flag à Oui dans le
    ; fichier maitre.
    ;
    IF ALTEREDRECORD OF CPCAP_CEDULE
    THEN LET CAPFLGCED OF CPCPT_A_PAYER = "1"
    ;
    ; Mise à jour du timbre de modification si l'usager change la cédule
    ;
    IF    NOT NEWRECORD OF CPCPT_A_PAYER &
      AND ALTEREDRECORD OF CPCAP_CEDULE
    THEN BEGIN
      LET CAPDATMOD OF CPCPT_A_PAYER = SYSDATE
      LET CAPHREMOD OF CPCPT_A_PAYER = SYSTIME / 10000
      LET CAPUSRMOD OF CPCPT_A_PAYER = LOGONID
    END
    ;
    ; Destruction d'une ligne de cédule.
    ;
    IF DELETEDRECORD OF CPCAP_CEDULE
    THEN BEGIN
      ;
      ; On initialise le flag de retour pour savoir si le sous-écran a
      ; bien fonctionné.
      ;
      LET T_FLGDEL = ""
      ;
      ; Appel du sous-écran qui valide si la cédule est référé.
      ;
      RUN SCREEN cp003bx MODE SAME &
                         PASSING CPCAP_CEDULE , &
                                 T_FLGDEL
      ;
      ; Si la valeur de retour égale 1, cela indique que le sous-écran
      ; n'a trouvé aucune référence pour l'identifiant demandé.
      ;
      IF T_FLGDEL = ""
      THEN ERROR " "
    END
  END
  ;
  ; La cédule doit balancer avec le montant brut de la facture, car l'usager
  ; peut modifier la cédule après journalisation, c'est pour cette raison
  ; qu'il faut s'assurer que la cédule balance.
  ;
  IF D_BALCED <> 0
  THEN ERROR 485 ; La cédule ne balance pas.
END

;-------------------------------------------------------------------------------
;
; - Validation pour la ligne 01.
; - Mise à jour du total de la cédule.
;
;
PROCEDURE DELETE
BEGIN
  IF CPCCLELIG OF CPCAP_CEDULE = 1
  THEN ERROR 484 ; On ne peut détruire la ligne 01.

  IF CPCFLGSEL OF CPCAP_CEDULE = "1"
  THEN ERROR 561 ; On ne peut  modifier cette ligne, elle est sélectionnée...

  IF NOT DELETEDRECORD OF CPCAP_CEDULE
  THEN BEGIN
    LET CAPCUMCED OF CPCPT_A_PAYER = CAPCUMCED OF CPCPT_A_PAYER - &
                                     CPCMNT OF CPCAP_CEDULE
    DISPLAY D_BALCED
  END
  DELETE CPCAP_CEDULE
END

;-------------------------------------------------------------------------------
;
; VALIDATION DE LA MODIFICATION D'UNE LIGNE
;
;

PROCEDURE DESIGNER 01
BEGIN
  IF CPCFLGSEL OF CPCAP_CEDULE = "1"
  THEN ERROR 561 ; On ne peut  modifier cette ligne, elle est sélectionnée...
  ACCEPT CPCDATDUE OF CPCAP_CEDULE
  ACCEPT CPCMNT    OF CPCAP_CEDULE
END

PROCEDURE POSTUPDATE
BEGIN
  COMMIT TRANSACTION MAJ_TBL_CAP
END

BUILD list detail

@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
****************************** Cédule de paiement ******************************
* Facture.......: xxxxxxxxxxxx               Montant brut..: xxxxxxxxxxxxxxxxxx*
* Solde à payer.: xxxxxxxxxxxxxx             Reste à vent..: xxxxxxxxxxxxxx    *
********************************************************************************
* No                  Paiement         Montant         Montant      Montant    *
*Céd.  Date due          prévu    d'ajustement            Payé   d'escompte    *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
* xx   xxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxxxxx  xxxxxxxxxxx   *
********************************************************************************
@ENDIF
