;/T/ Passerelle d'entré, Facture
;
;/P/ Programmeur..: Steeve Duguay
;    Date Création: 12 mai 1995
;
;    Description..: Calcul les date des terme de la facture
;

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN fa702 AUTOUPDATE

FILE FACPT_A_REC DESIGNER

FILE CRTERME     DESIGNER OPEN READ SHARE

;
;Sert a validé de lot
;
; Si il n'y a pas eux d'erreur dans le qtp le lot est rendu de type PF
; je le remet de type FA après le calcul

FILE CRLOT       DESIGNER

TEMPORARY T_CIECLE CHARACTER SIZE 4
TEMPORARY T_PECCLE CHARACTER SIZE 4
TEMPORARY T_LCRCLE CHARACTER SIZE 4

;-------------------------------------------------------------------------------
;
; Variables temporaire pour le calcul des dates due.
;
USE ustertmp NOLIST

FIELD T_CIECLE REQUIRED
FIELD T_PECCLE REQUIRED
FIELD T_LCRCLE REQUIRED

;-------------------------------------------------------------------------------
;
;
; Procédure pour le calcul des dates due.
;
;
USE ustmecal NOLIST

PROCEDURE INTERNAL CALCUL_DAT_TERME
BEGIN
    LET TZ_DATCAL = FARDAT OF FACPT_A_REC

    GET CRTERME VIA TMECLE &
      USING FARTERNET OF FACPT_A_REC  OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET FARDATNET OF FACPT_A_REC = TZ_DATRES
    END

    GET CRTERME VIA TMECLE &
      USING FARTERESC1 OF FACPT_A_REC OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET FARDATESC1 OF FACPT_A_REC = TZ_DATRES
    END

    GET CRTERME VIA TMECLE &
      USING FARTERESC2 OF FACPT_A_REC  OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET FARDATESC2 OF FACPT_A_REC = TZ_DATRES
    END

    GET CRTERME VIA TMECLE &
                USING FARTERFRS OPTIONAL
    IF ACCESSOK
    THEN BEGIN
      DO INTERNAL TER_DATE_DUE
      LET FARDATFRS OF FACPT_A_REC = TZ_DATRES
    END
END

PROCEDURE UPDATE
BEGIN
  GET CRLOT &
  VIA CIECLE , &
      PECCLE , &
      LCRCLE , &
      LCRTYPECR &
  USING T_CIECLE, &
        T_PECCLE, &
        T_LCRCLE, &
        "PF" OPTIONAL
  IF ACCESSOK
  THEN BEGIN

    WHILE RETRIEVING FACPT_A_REC &
      VIA CIECLE, &
          PECCLE, &
          LCRCLE &
      USING T_CIECLE, &
            T_PECCLE, &
            T_LCRCLE
    BEGIN
      IF ACCESSOK
      THEN BEGIN
        DO INTERNAL CALCUL_DAT_TERME
        PUT FACPT_A_REC RESET
      END
    END
    LET LCRTYPECR OF CRLOT = "FA"
    PUT CRLOT RESET
  END
END

PROCEDURE POSTUPDATE
BEGIN
  INFO = "Terminer avec succès " NOW
  RETURN
END
BUILD
