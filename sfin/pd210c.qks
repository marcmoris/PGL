;/T/ 2.3.1 Enregistrer la commande interne. - Détail commande
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1995-01-30
;
;    Description..: Sous-écran pour saisir le détail de la commande.
;
; *-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
;           ***** ATTENTION la procédure APPEND à été codé. *****
;                                        ------
; *-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
;***********************************************************************************************************************************
;
;/M/ Modifié par.......: Marc Poulin     
;    Date modification.: 06 mars 2000
;    Description.......: Afficher la description associée au type de granit. Tenir compte que la description 3 et 4 sont possibles.
;
;    Référence.........: Demande de changement 000020.
;
;    Signet............: MP-00-03-06_D20.
;
;-----------------------------------------------------------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd210c ACTIONBAR                     &
              MODE LABEL "" AT 2,80         &
              NOACTION                      &
              AUTOUPDATE                    &
              FIELDMARK RETAIN STARTUP      &
              HELP POPUP FROM 4,9 TO 20,72  &
              RECEIVING T_CIECLEMNU,        &
                        T_CIENOM,           &
                        PDCOMMAN_INTERNE


;-------------------------------------------------------------------------------
;
; Variables pour la sécurité.
;

USE ussectmp  NOLIST
    "PD210C"


;-------------------------------------------------------------------------------
;
; Panorama d'aide
;

USE pd210c.hlp NOLIST


;-------------------------------------------------------------------------------
;
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Début d'une transaction pour le sous-écran
;

USE ustrasse NOLIST


@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Ventilation comptable           " ACTION DESIGNER D001 MARK
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%Ventilation comptable        " ACTION DESIGNER D001 MARK
@ENDIF


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;

DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable servant à l'appel du/des dépisteur(s) (pop-up)
;
TEMPORARY T_INFO         CHARACTER SIZE 20 ; Description 1, 2, 3, ou 4 du type de granit ; MP-00-03-06_D20.
TEMPORARY T_TPDTYPPRD    CHARACTER SIZE 04 ; Type de produit
TEMPORARY T_TGRCODGRA    CHARACTER SIZE 04 ; Type de granit
TEMPORARY T_TYFCODFIN    CHARACTER SIZE 04 ; Type de fini
TEMPORARY T_UNMCODUNM    CHARACTER SIZE 04 ; Unité de mesure
TEMPORARY T_CPTCLE       CHARACTER SIZE 06 ; Compte
TEMPORARY T_UNACLE       CHARACTER SIZE 08 ; Unité administrative
TEMPORARY T_CPTTYPPAT    CHARACTER SIZE 01 ; Pattern type de compte
TEMPORARY T_CPTCATPAT    CHARACTER SIZE 08 ; Pattern catégorie du compte
TEMPORARY T_PECCLE       CHARACTER SIZE 04 ; Période


;-------------------------------------------------------------------------------
;
; Variable servant au traitement
;

TEMPORARY T_NOLIG            INTEGER        SIZE 04 RESET AT STARTUP ; Pour numéro LIGNE AUTOMATIQUE.
TEMPORARY T_SILENT           CHARACTER      SIZE 01
TEMPORARY T_SILENT_2         CHARACTER      SIZE 01


;-------------------------------------------------------------------------------
;
; Commande interne
;

FILE PDCOMMAN_INTERNE MASTER NODELETE


;-------------------------------------------------------------------------------
;
; Détail de la commande
;

FILE PDDETAIL_C_I          PRIMARY &
                           NOITEM  &
                           OCCURS 14

  ACCESS VIA     CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT     &
         USING   T_CIECLEMNU,                          &
                 PJTABR           OF PDCOMMAN_INTERNE, &
                 PJTANN           OF PDCOMMAN_INTERNE, &
                 COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
         ORDERBY CIECLE,          &
                 PJTABR,          &
                 PJTANN,          &
                 COMNUMCOMINT,    &
                 TPDTYPPRD,       &
                 TGRCODGRA,       &
                 TYFCODFIN,       &
                 DCIEPA,          &
                 DCINUMLIG


  ITEM CIECLE       OF PDDETAIL_C_I INITIAL T_CIECLEMNU
  ITEM PJTABR       OF PDDETAIL_C_I INITIAL PJTABR OF PDCOMMAN_INTERNE FIXED
  ITEM PJTANN       OF PDDETAIL_C_I INITIAL PJTANN OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOMINT OF PDDETAIL_C_I INITIAL COMNUMCOMINT OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMSEQ    OF PDDETAIL_C_I INITIAL PJTNUMSEQ OF PDCOMMAN_INTERNE FIXED
  ITEM PJTNUMPRO    OF PDDETAIL_C_I INITIAL PJTNUMPRO OF PDCOMMAN_INTERNE FIXED
  ITEM COMNUMCOM    OF PDDETAIL_C_I INITIAL COMNUMCOM OF PDCOMMAN_INTERNE FIXED
  ITEM DCILIADIA    OF PDDETAIL_C_I INITIAL "0"
  ITEM DCISTU       OF PDDETAIL_C_I INITIAL "0"


;-------------------------------------------------------------------------------
;
; Type de produit
;

FILE PDTYPE_PRODUIT REFERENCE OCCURS WITH PDDETAIL_C_I

  ACCESS VIA     CIECLE,          &
                 TPDTYPPRD        &
         USING   T_CIECLEMNU,     &
                 TPDTYPPRD        OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Détail de la commande interne
;

FILE PDDETAIL_C_I          REFERENCE ALIAS A_DCI_DETAIL


;-------------------------------------------------------------------------------
;
; Diagramme
;

FILE PDDIAGRAMME           DESIGNER


;-------------------------------------------------------------------------------
;
; Ventilation de la ligne de commande
;

FILE PDVENTILATION         DESIGNER


;-------------------------------------------------------------------------------
;
; Unité de mesure (production)
;

FILE PDUNITE_MESURE REFERENCE &
                    ALIAS UNM_COMMANDE &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA     CIECLE,          &
                 UNMCODUNM        &
         USING   T_CIECLEMNU,     &
                 DCIUNMCMD        OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Unité de mesure (Vente)
;

FILE PDUNITE_MESURE REFERENCE &
                    ALIAS UNM_VENTE &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA     CIECLE,          &
                 UNMCODUNM        &
         USING   T_CIECLEMNU,     &
                 DCIUNMVEN        OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE &
                   OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,          &
                TGRCODGRA        &
         USING  T_CIECLEMNU,     &
                TGRCODGRA        OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI   REFERENCE &
                   OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,          &
                TYFCODFIN        &
         USING  T_CIECLEMNU,     &
                TYFCODFIN        OF PDDETAIL_C_I


;-------------------------------------------------------------------------------
;
; Conversion unité mesure de commande à unité mesure production
;

FILE PDCONVERT_UN_M REFERENCE &
                    ALIAS A_CUM_UNM_CMD &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,          &
                CUMCODUNMINT,    &
                CUMCODUNMEXT     &
         USING  T_CIECLEMNU,                      &
                DCIUNMCMD        OF PDDETAIL_C_I, &
                UNMCODUNM        OF PDTYPE_PRODUIT


;-------------------------------------------------------------------------------
;
; Conversion unité mesure vente à unité mesure production
;

FILE PDCONVERT_UN_M REFERENCE &
                    ALIAS A_CUM_PRX_VEN &
                    OCCURS WITH PDDETAIL_C_I

  ACCESS VIA    CIECLE,          &
                CUMCODUNMINT,    &
                CUMCODUNMEXT     &
         USING  T_CIECLEMNU,                        &
                UNMCODUNM        OF PDTYPE_PRODUIT, &
                DCIUNMVEN        OF PDDETAIL_C_I



;-------------------------------------------------------------------------------
;
;

USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans


SKIP


TITLE &
@IF FRANCAIS
      " Détail de la commande interne " &
@ELSE
      " %%%Détail de la commande interne " &
@ENDIF
      CENTERED AT ,1


USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 4


ALIGN (,3,19) (,21,22) (,24,25) (,,32)


FIELD PJTABR           OF PDCOMMAN_INTERNE &
        LABEL "Num. commande.:"            &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD PJTANN           OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD COMNUMCOMINT     OF PDCOMMAN_INTERNE &
        LABEL "-"                          &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


FIELD COMNOM           OF PDCOMMAN_INTERNE &
        DISPLAY                            &
        PREDISPLAY                         &
        FIXED


DRAW 5,2 TO 5,79


SKIP TO LINE 6


TITLE &
@IF FRANCAIS
      " No.  Type  " &
@ELSE
      " %%%No.  Type  " &
@ENDIF
      AT ,2

SKIP

TITLE &
@IF FRANCAIS
     " lig  prod Quantité  Un Gra D Fi D Ép.  Prx ven  Un   Quantité Commen." &
@ELSE
     "%%%g  prod Quantité  Un Gra D Fi D Ép.  Prx ven  Un   Quantité Commen." &
@ENDIF
      AT ,2


SKIP TO LINE 8

ALIGN (02,02,03) (04,,04) (,,08) (09,,09) (12,,12) (23,,23) (26,,26) (30,,30) &
        (32,,32) (35,,35) (37,,37) (42,,42) (51,,51) (54,,54) (65,,65)


CLUSTER OCCURS WITH PDDETAIL_C_I ID BASE 40


FIELD DCISTU           OF PDDETAIL_C_I     &
        HIDDEN                             &
        LABEL " "                          &
        REFRESH                            &
        DISPLAY


FIELD DCINUMLIG        OF PDDETAIL_C_I     &
        ID SAME                            &
        NUMERIC                            &
        LOOKUP NOTON PDDETAIL_C_I          &
          VIA     CIECLE,                  &
                  PJTABR,                  &
                  PJTANN,                  &
                  COMNUMCOMINT,            &
                  DCINUMLIG                &
          USING   T_CIECLEMNU,                          &
                  PJTABR           OF PDCOMMAN_INTERNE, &
                  PJTANN           OF PDCOMMAN_INTERNE, &
                  COMNUMCOMINT     OF PDCOMMAN_INTERNE, &
                  DCINUMLIG        OF PDDETAIL_C_I      &
          MESSAGE 3055 ; Ce numéro de ligne existe déjà


FIELD DCILIADIA        OF PDDETAIL_C_I     &
        NOID                               &
        DISPLAY                            &
        REFRESH


FIELD TPDTYPPRD        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE                          &
        LOOKUP ON PDTYPE_PRODUIT           &
          MESSAGE 2918 ; Ce produit n'existe pas


FIELD DCIQTECMD        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        output scale 2                     &
        significance 4                     &
        PICTURE "^^^^^^^.^^"


FIELD DCIUNMCMD        OF PDDETAIL_C_I     &
        ID SAME                            &
        DEFAULT UNMCODUNM OF PDTYPE_PRODUIT &
        LOOKUP ON UNM_COMMANDE             &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas


FIELD TGRCODGRA        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE                          &
        NUMERIC                            &
        LOOKUP ON PDTYPE_GRANIT            &
          MESSAGE 2910 ; Ce type de granit n'existe pas


FIELD DCITYPGRA        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE


FIELD TYFCODFIN        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE                          &
        NUMERIC                            &
        LOOKUP ON PDTYPE_FINI              &
          MESSAGE 2911 ; Ce type de fini n'exsite pas


FIELD DCITYPFIN        OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE


FIELD DCIEPA           OF PDDETAIL_C_I     &
        ID SAME                            &
        REQUIRED                           &
        DUPLICATE

;
; Il ne peut y avoir 2 lignes identique pour une même commande
; Particularité pour les tranches
; --------------------------------
; Granit, Fini, Épaisseur doit être unique par commande
;
;

;FIELD T_SILENT                             &
;        SILENT                             &
;        LOOKUP NOTON PDDETAIL_C_I          &
;          VIA   CIECLE,                    &
;                PJTABR,                    &
;                PJTANN,                    &
;                COMNUMCOMINT,              &
;                TPDTYPPRD,                 &
;                TGRCODGRA,                 &
 ;               TYFCODFIN,                 &
;                DCIEPA                     &
;          USING T_CIECLEMNU,                       &
;                PJTABR        OF PDCOMMAN_INTERNE, &
 ;               PJTANN        OF PDCOMMAN_INTERNE, &
 ;               COMNUMCOMINT  OF PDCOMMAN_INTERNE, &
 ;               "TR",                              &
 ;               TGRCODGRA     OF PDDETAIL_C_I,     &
 ;               TYFCODFIN     OF PDDETAIL_C_I,     &
  ;              DCIEPA        OF PDDETAIL_C_I      &
  ;        MESSAGE 3187 ; Cette ligne de détail correspond à une ligne déjà saisie


FIELD DCIPRIVEN        OF PDDETAIL_C_I     &
        ID SAME                            &
        DUPLICATE                          &
        PICTURE "^,^^^.^^"                 &
        SIGNIFICANCE 4


FIELD DCIUNMVEN        OF PDDETAIL_C_I     &
        ID SAME                            &
        DEFAULT DCIUNMCMD OF PDDETAIL_C_I  &
        DUPLICATE                          &
        LOOKUP ON UNM_VENTE                &
          MESSAGE 3028 ; Cette unité de mesure n'existe pas


FIELD T_SILENT_2                           &
        SILENT                             &
        LOOKUP NOTON PDDETAIL_C_I          &
          VIA   CIECLE,                    &
                PJTABR,                    &
                PJTANN,                    &
                COMNUMCOMINT,              &
                TPDTYPPRD,                 &
                TGRCODGRA,                 &
                TYFCODFIN,                 &
                DCIEPA,                    &
                DCIPRIVEN,                 &
                DCIUNMVEN                  &
          USING T_CIECLEMNU,                       &
                PJTABR        OF PDCOMMAN_INTERNE, &
                PJTANN        OF PDCOMMAN_INTERNE, &
                COMNUMCOMINT  OF PDCOMMAN_INTERNE, &
                TPDTYPPRD     OF PDDETAIL_C_I,     &
                TGRCODGRA     OF PDDETAIL_C_I,     &
                TYFCODFIN     OF PDDETAIL_C_I,     &
                DCIEPA        OF PDDETAIL_C_I,     &
                DCIPRIVEN     OF PDDETAIL_C_I,     &
                DCIUNMVEN     OF PDDETAIL_C_I      &
          MESSAGE 3187 ; Cette ligne de détail correspond à une ligne déjà saisie


FIELD DCIQTEUNMPRD     OF PDDETAIL_C_I     &
        ID SAME                            &
        DISPLAY                            &
        output scale 2                     &
        significance 4                     &
        PICTURE "^^^^^^^.^^"


FIELD DCICOM           OF PDDETAIL_C_I     &
        ID SAME                            &
        FOR , 14


SUBSCREEN pd210f AUTO         &
   HIDDEN                     &
   NOID                       &
   NOLABEL                    &
   MODE SAME                  &
   PASSING T_CIECLEMNU,       &
           T_CIENOM,          &
           PDCOMMAN_INTERNE,  &
           PDDETAIL_C_I


CLUSTER


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN

  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Assigne la valeur à afficher pour le champ
;

PROCEDURE OUTPUT DCISTU
BEGIN
  IF FIELDTEXT = "0"
  THEN BEGIN
    LET FIELDTEXT = "*"
  END
  ELSE BEGIN
    LET FIELDTEXT = " "
  END
END


;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE INPUT DCINUMLIG
BEGIN
  IF 0 = SIZE(FIELDTEXT ) &
     AND &
     0 = SIZE(TRUNCATE(DCINUMLIG OF PDDETAIL_C_I))
  THEN BEGIN
    LET FIELDTEXT = ASCII( T_NOLIG + 1 )
  END
END

;------------------------------------------------------------------------
;
;
PROCEDURE INPUT DCITYPFIN OF PDDETAIL_C_I
BEGIN
IF FIELDTEXT ="1" OR FIELDTEXT="2"
THEN LET DCITYPFIN OF PDDETAIL_C_I = FIELDTEXT
 IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"
 THEN BEGIN
  IF DCITYPFIN OF PDDETAIL_C_I = "1"
  THEN  LET T_INFO = TYFDSCFRA001 OF PDTYPE_FINI
  ELSE  LET T_INFO = TYFDSCFRA002 OF PDTYPE_FINI
 END
 ELSE BEGIN
  IF DCITYPFIN OF PDDETAIL_C_I = "1"
  THEN  LET T_INFO = TYFDSCANG001 OF PDTYPE_FINI
  ELSE  LET T_INFO = TYFDSCANG002 OF PDTYPE_FINI
 END

WARNING = T_INFO
END

;-----------------------------------------------------------------------
; MP-00-03-06_D20.
;
; Afficher la bonne description de type de granit.
;
PROCEDURE INPUT DCITYPGRA OF PDDETAIL_C_I
BEGIN
  IF 0=SIZE(TRUNC(FIELDTEXT))
  THEN LET FIELDTEXT = DCITYPGRA OF PDDETAIL_C_I ; Forcer la procédure EDIT.
END

PROCEDURE EDIT DCITYPGRA OF PDDETAIL_C_I
BEGIN
  IF COMCODLNGCOR OF PDCOMMAN_INTERNE = "F"
  THEN BEGIN
    IF FIELDTEXT = "1"
    THEN LET T_INFO = TGRDSCFRA001 OF PDTYPE_GRANIT
    ELSE IF FIELDTEXT = "2" 
         THEN LET T_INFO = TGRDSCFRA002 OF PDTYPE_GRANIT
         ELSE IF FIELDTEXT = "3" 
              THEN LET T_INFO = TGRDSCFRA003 OF PDTYPE_GRANIT
              ELSE IF FIELDTEXT = "4" 
                   THEN LET T_INFO = TGRDSCFRA004 OF PDTYPE_GRANIT
  END
  ELSE BEGIN
    IF FIELDTEXT = "1"
    THEN LET T_INFO = TGRDSCANG001 OF PDTYPE_GRANIT
    ELSE IF FIELDTEXT = "2"
         THEN LET T_INFO = TGRDSCANG002 OF PDTYPE_GRANIT
         ELSE IF FIELDTEXT = "3"
              THEN LET T_INFO = TGRDSCANG003 OF PDTYPE_GRANIT
              ELSE IF FIELDTEXT = "4" 
                   THEN LET T_INFO = TGRDSCANG004 OF PDTYPE_GRANIT
  END

  WARNING = T_INFO
END
;-------------------------------------------------------------------------------
;
; Procédure permettant de générer le numéro de ligne automatique
;

PROCEDURE PROCESS DCINUMLIG
BEGIN
  LET T_NOLIG = NCONVERT(DCINUMLIG OF PDDETAIL_C_I)
END


;-------------------------------------------------------------------------------
;
; Assigne la valeur à afficher pour le champ
;

PROCEDURE OUTPUT DCILIADIA
BEGIN
  IF FIELDTEXT = "1"
  THEN BEGIN
    LET FIELDTEXT = "*"
  END
  ELSE BEGIN
    LET FIELDTEXT = " "
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs.
;

PROCEDURE INPUT TPDTYPPRD OF PDDETAIL_C_I
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TPDTYPPRD = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd118 MODE F &
                      PASSING T_TPDTYPPRD, &
                              T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TPDTYPPRD )
  END
END


;-------------------------------------------------------------------------------
;
; Validation pour le champ
;

PROCEDURE EDIT TPDTYPPRD OF PDDETAIL_C_I
BEGIN
  IF DCILIADIA    OF PDDETAIL_C_I = "1" &
     AND &
     OLDVALUE(TPDTYPPRD OF PDDETAIL_C_I) <> FIELDTEXT
  THEN BEGIN
    ERROR 3171 ; La production a déjà débuté pour cette ligne de détail
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;

PROCEDURE INPUT DCIUNMCMD
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Validation de l'unité de vente
;

PROCEDURE EDIT DCIUNMCMD
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = SIZE(TRUNCATE(DCIUNMCMD OF PDDETAIL_C_I))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Unité de mesure permise pour vendre un diagramme
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "DI"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un produit de dimension
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "PD"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2" &
       AND &
       FIELDTEXT <> "UN"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre une tranche
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "TR"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2" &
       AND &
       FIELDTEXT <> "TR"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un bloc
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "BL"
  THEN BEGIN
    IF FIELDTEXT <> "M3" &
       AND &
       FIELDTEXT <> "P3"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
END


;-------------------------------------------------------------------------------
;
; Appel du dépisteur pour le champ.
;

PROCEDURE INPUT TGRCODGRA
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TGRCODGRA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd100 MODE F &
                     PASSING T_TGRCODGRA, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TGRCODGRA )
  END
END


;-------------------------------------------------------------------------------
;
; Validation pour le champ
;

PROCEDURE EDIT TGRCODGRA OF PDDETAIL_C_I
BEGIN
  ;
  ; On le peut modifier une ligne de détail si cette ligne est lié à un diagramme
  ;
  IF DCILIADIA OF PDDETAIL_C_I = "1"
  THEN BEGIN
    ERROR 3161 ; On ne peut modifier une ligne de détail liée à un diagramme
  END
END
;------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;

PROCEDURE INPUT TYFCODFIN
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd101 MODE F &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
END


;-------------------------------------------------------------------------------
;
; Validation pour le champ
;

PROCEDURE EDIT TYFCODFIN OF PDDETAIL_C_I
BEGIN
  ;
  ; On le peut modifier une ligne de détail si cette ligne est lié à un diagramme
  ;
  IF DCILIADIA OF PDDETAIL_C_I = "1"
  THEN BEGIN
    ERROR 3161 ; On ne peut modifier une ligne de détail liée à un diagramme
  END
END


;-------------------------------------------------------------------------------
;
; Validation pour le champ
;

PROCEDURE EDIT DCIEPA OF PDDETAIL_C_I
BEGIN
  ;
  ; On le peut modifier une ligne de détail si cette ligne est lié à un diagramme
  ;
  IF DCILIADIA OF PDDETAIL_C_I = "1"
  THEN BEGIN
    ERROR 3161 ; On ne peut modifier une ligne de détail liée à un diagramme
  END
END


;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;

PROCEDURE EDIT DCIPRIVEN
BEGIN
  ;
  ; Valide que le champ a été saisie
  ;
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = DCIPRIVEN OF PDDETAIL_C_I
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END

  IF FIELDVALUE <> OLDVALUE(DCIPRIVEN OF PDDETAIL_C_I) &
     AND &
     0 <> OLDVALUE(DCIPRIVEN OF PDDETAIL_C_I)
  THEN BEGIN
    LET DCISTU OF PDDETAIL_C_I = "0"
    DISPLAY DCISTU OF PDDETAIL_C_I
    WARNING 3128 ; Vérifier l'imputation
  END
END


;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;

PROCEDURE PROCESS DCIPRIVEN
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET     DCIUNMPRD    OF PDDETAIL_C_I  = UNMCODUNM    OF PDTYPE_PRODUIT
  LET     DCIQTEUNMPRD OF PDDETAIL_C_I  = ROUND((DCIQTECMD    OF PDDETAIL_C_I * CUMFACCON OF A_CUM_UNM_CMD),4)
  LET     DCIPRIUNMPRD OF PDDETAIL_C_I  = ROUND((DCIPRIVEN OF PDDETAIL_C_I * CUMFACCON OF A_CUM_PRX_VEN),2)
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  LET     DCIMNT       OF PDDETAIL_C_I  = ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD OF PDDETAIL_C_I),2)
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champ.
;

PROCEDURE INPUT DCIUNMVEN
BEGIN
  ;
  ; Appel du dépisteur
  ;
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNMCODUNM = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd113 MODE F &
                     PASSING T_UNMCODUNM, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_UNMCODUNM )
  END
END


;-------------------------------------------------------------------------------
;
; Valide que le champ a été saisie
;

PROCEDURE EDIT DCIUNMVEN
BEGIN
  IF 0 = SIZE(TRUNCATE(FIELDTEXT)) &
     AND &
     0 = SIZE(TRUNCATE(DCIUNMVEN OF PDDETAIL_C_I))
  THEN BEGIN
    ERROR 3044 ; Ce champ est requis
  END
  ;
  ; Unité de mesure permise pour vendre un diagramme
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "DI"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un produit de dimension
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "PD"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre une tranche
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "TR"
  THEN BEGIN
    IF FIELDTEXT <> "M2" &
       AND &
       FIELDTEXT <> "P2"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
  ;
  ; Unité de mesure permise pour vendre un bloc
  ;
  IF TPDTYPPRD OF PDDETAIL_C_I = "BL"
  THEN BEGIN
    IF FIELDTEXT <> "M3" &
       AND &
       FIELDTEXT <> "P3"
    THEN BEGIN
      ERROR 3184 ; Cette unité de mesure ne peut être utilisé pour ce type de produit
    END
  END
END


;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;

PROCEDURE PROCESS DCIUNMVEN
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET     DCIUNMPRD    OF PDDETAIL_C_I  = UNMCODUNM    OF PDTYPE_PRODUIT
  LET     DCIQTEUNMPRD OF PDDETAIL_C_I  = ROUND((DCIQTECMD    OF PDDETAIL_C_I * CUMFACCON OF A_CUM_UNM_CMD),4)
  LET     DCIPRIUNMPRD OF PDDETAIL_C_I  = ROUND((DCIPRIVEN    OF PDDETAIL_C_I * CUMFACCON OF A_CUM_PRX_VEN),2)
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  LET     DCIMNT       OF PDDETAIL_C_I  = ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD OF PDDETAIL_C_I),2)

  IF FIELDTEXT <> OLDVALUE(DCIUNMVEN OF PDDETAIL_C_I) &
     AND &
     0 <> OLDVALUE(DCIPRIVEN OF PDDETAIL_C_I)
  THEN BEGIN
    WARNING 3128 ; Vérifier l'imputation
  END
END


;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;

PROCEDURE PROCESS DCIQTECMD
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET     DCIUNMPRD    OF PDDETAIL_C_I  = UNMCODUNM    OF PDTYPE_PRODUIT
  LET     DCIQTEUNMPRD OF PDDETAIL_C_I  = ROUND((DCIQTECMD    OF PDDETAIL_C_I * CUMFACCON OF A_CUM_UNM_CMD),4)
  LET     DCIPRIUNMPRD OF PDDETAIL_C_I  = ROUND((DCIPRIVEN    OF PDDETAIL_C_I * CUMFACCON OF A_CUM_PRX_VEN),2)
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  LET     DCIMNT       OF PDDETAIL_C_I  = ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD     OF PDDETAIL_C_I),2)
END


;-------------------------------------------------------------------------------
;
; Conversion pour les unités de production
;

PROCEDURE PROCESS DCIUNMCMD
BEGIN
  ;
  ; Conversion pour les unités de production
  ;
  LET     DCIUNMPRD    OF PDDETAIL_C_I = UNMCODUNM    OF PDTYPE_PRODUIT
  LET     DCIQTEUNMPRD OF PDDETAIL_C_I = ROUND((DCIQTECMD    OF PDDETAIL_C_I * CUMFACCON        OF A_CUM_UNM_CMD),4)
  LET     DCIPRIUNMPRD OF PDDETAIL_C_I = ROUND((DCIPRIVEN    OF PDDETAIL_C_I * CUMFACCON        OF A_CUM_PRX_VEN),2)
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  LET     DCIMNT       OF PDDETAIL_C_I = ROUND((DCIQTEUNMPRD OF PDDETAIL_C_I * DCIPRIUNMPRD     OF PDDETAIL_C_I),2)
END


;-------------------------------------------------------------------------------
;
; Procédure APPEND
;

PROCEDURE APPEND
BEGIN
  LET DCISTU OF PDDETAIL_C_I = "0"
  DISPLAY DCISTU OF PDDETAIL_C_I
  ACCEPT DCINUMLIG OF PDDETAIL_C_I
  DISPLAY DCILIADIA OF PDDETAIL_C_I
  ACCEPT TPDTYPPRD OF PDDETAIL_C_I
  ACCEPT DCIQTECMD OF PDDETAIL_C_I
  ACCEPT DCIUNMCMD OF PDDETAIL_C_I
  ACCEPT TGRCODGRA OF PDDETAIL_C_I
  ACCEPT DCITYPGRA OF PDDETAIL_C_I
  ACCEPT TYFCODFIN OF PDDETAIL_C_I
  ACCEPT DCITYPFIN OF PDDETAIL_C_I
  ACCEPT DCIEPA OF PDDETAIL_C_I
  ;
  ; Validation spéciale si TRANCHE
  ;
  ;IF TPDTYPPRD OF PDDETAIL_C_I = "TR"
  ;THEN BEGIN
  ;  EDIT T_SILENT
  ;iEND
  ACCEPT DCIPRIVEN OF PDDETAIL_C_I
  ACCEPT DCIUNMVEN OF PDDETAIL_C_I
  EDIT T_SILENT_2
  DISPLAY DCIQTEUNMPRD OF PDDETAIL_C_I
  ACCEPT DCICOM OF PDDETAIL_C_I
  RUN SCREEN pd210f &
    PASSING T_CIECLEMNU, &
            T_CIENOM, &
            PDCOMMAN_INTERNE, &
            PDDETAIL_C_I &
    MODE SAME
END



;-------------------------------------------------------------------------------
;
; Ventilation comptable
;

PROCEDURE DESIGNER D001
BEGIN

  RUN SCREEN  pd210f &
              PASSING T_CIECLEMNU,        &
                      T_CIENOM,           &
                      PDCOMMAN_INTERNE,   &
                      PDDETAIL_C_I        &
              MODE F
END


;-------------------------------------------------------------------------------
;
; Recherche le dernier numéro de ligne utilisé
;

PROCEDURE POSTFIND
BEGIN
  GET A_DCI_DETAIL OPTIONAL  &
    VIA     CIECLE,          &
            PJTABR,          &
            PJTANN,          &
            COMNUMCOMINT     &
    USING   T_CIECLEMNU,                          &
            PJTABR           OF PDCOMMAN_INTERNE, &
            PJTANN           OF PDCOMMAN_INTERNE, &
            COMNUMCOMINT     OF PDCOMMAN_INTERNE  &
    ORDERBY CIECLE,          &
            PJTABR,          &
            PJTANN,          &
            COMNUMCOMINT,    &
            DCINUMLIG        DESCENDING
  IF ACCESSOK
  THEN BEGIN
    LET T_NOLIG = NCONVERT(DCINUMLIG OF A_DCI_DETAIL)
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE ENTRY
BEGIN
  FOR PDDETAIL_C_I
  BEGIN
    PERFORM APPEND
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN

  IF (COMSTU OF PDCOMMAN_INTERNE = "T") OR &
     (COMSTU OF PDCOMMAN_INTERNE = "A") OR &
     (COMSTU OF PDCOMMAN_INTERNE = "X")
  THEN ERROR = "Le status de la commande interne ne permet pas de modifier"

  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDETAIL_C_I &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDETAIL_C_I &
     AND NOT NEWRECORD     OF PDDETAIL_C_I &
     AND NOT DELETEDRECORD OF PDDETAIL_C_I &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

;-------------------------------------------------------------------------------
;
; Destruction des lignes de ventilation associer à une ligne de commande interne
;

PROCEDURE INTERNAL DESTR_LIGNE_VENTILATION
BEGIN
  WHILE RETRIEVING PDVENTILATION &
    VIA     PJTABR,           &
            PJTANN,           &
            COMNUMCOMINT,     &
            DCINUMLIG,        &
            CIECLE            &
    USING   PJTABR            OF PDDETAIL_C_I, &
            PJTANN            OF PDDETAIL_C_I, &
            COMNUMCOMINT      OF PDDETAIL_C_I, &
            DCINUMLIG         OF PDDETAIL_C_I, &
            CIECLE            OF PDDETAIL_C_I
  BEGIN
    DELETE PDVENTILATION
    PUT PDVENTILATION
  END
END


;-------------------------------------------------------------------------------
;
; Procédure DELETE
;

PROCEDURE DELETE
BEGIN
  ;
  ; Valide que l'on peut détruire les lignes de detail de la commande
  ;
  IF NOT DELETEDRECORD OF PDDETAIL_C_I
  THEN BEGIN
    IF DCILIADIA    OF PDDETAIL_C_I = "1"
    THEN BEGIN
      ERROR 3171 ; La production a déjà débuté pour cette ligne de détail
    END
    ;
    ; Destruction des lignes de ventilation de la commande
    ;
    ELSE BEGIN
      WHILE RETRIEVING PDVENTILATION &
        VIA     PJTABR,           &
                PJTANN,           &
                COMNUMCOMINT,     &
                DCINUMLIG,        &
                CIECLE            &
        USING   PJTABR            OF PDDETAIL_C_I, &
                PJTANN            OF PDDETAIL_C_I, &
                COMNUMCOMINT      OF PDDETAIL_C_I, &
                DCINUMLIG         OF PDDETAIL_C_I, &
                CIECLE            OF PDDETAIL_C_I
      BEGIN
        DELETE PDVENTILATION
        PUT PDVENTILATION
      END
      DELETE PDDETAIL_C_I
    END
  END
END

BUILD DETAIL LIST


@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
************************ Détail de la commande interne *************************
* Num. commande.: xx-xx-xxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx        *
********************************************************************************
* No.  Type                                                                    *
* lig  prod Quantité  Un Gra D Fi D Ép.  Prx ven  Un   Quantité Commen.        *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
* xxxx xxx xxxxxxxxxx xx xxx x xx x xxxx xxxxxxxx xx xxxxxxxxxx xxxxxxxxxxxxxx *
*                                                                              *
********************************************************************************
@ENDIF
