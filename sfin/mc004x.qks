;/T/ Taxes - Validation de la destruction
;
;/P/ Programmeur..: Alain Côté
;    Date création: 10 août 1993
;
;    Description..: Cet écran vérifie si le code de taxe reçu en paramètre est
;                   référé dans une autre table, pour le traitement de la
;                   destruction dans l'écran maitre.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc004x NOMODE &
              ACTION LABEL " " &
              FROM 23,70 TO 23,80 &
              RECEIVING MCTAXE   , &
                        T_FLGDEL

;-------------------------------------------------------------------------------
;
TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag de retour.


;-------------------------------------------------------------------------------
;
; Taxes.
;
FILE MCTAXE           MASTER

;
; Fournisseur par compagnie.
;
FILE CPFOU_CIE        DESIGNER &
                      OPEN READ

;
; Imputation des comptes à payer.
;
FILE CPCAP_IMPUTATION DESIGNER &
                      OPEN READ

;
; Imputation des paiements.
;
FILE CPPAM_IMPUTATION DESIGNER &
                      OPEN READ

;
; Clients par compagnie.
;
FILE CRCLI_CIE        DESIGNER &
                      OPEN READ

;
; Imputation des comptes à recevoir.
;
FILE CRCAR_IMPUTATION DESIGNER &
                      OPEN READ



;-------------------------------------------------------------------------------
;
;
; La procédure suivante valide si la clé est référée, si oui alors
; on indique une erreur à l'usager. Si la clé n'est pas référé dans aucune
; table alors la valeur de retour sera 1.
;
; Note: si le verbe ERROR est activé dans la procédure INITIALIZE, le
;       reste de la procédure ne sera pas effectué.
;
;
PROCEDURE INITIALIZE
BEGIN
  ;
  ; On vérifie dans le fournisseur - compagnie.
  ;
  GET CPFOU_CIE VIA FOCTAXCODTPS , &
                    FOCTAXTYPTPS   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 159 ; Destruction annulée, cette information est référée par un
                 ; fournisseur-compagnie.
  ;
  ;
  GET CPFOU_CIE VIA FOCTAXCODTVQ , &
                    FOCTAXTYPTVQ   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 159 ; Destruction annulée, cette information est référée par un
                 ; fournisseur-compagnie.

  ;
  ; On vérifie dans les imputations des comptes à payer.
  ;
  GET CPCAP_IMPUTATION VIA CCITAXCODTPS , &
                           CCITAXTYPTPS   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 160 ; Destruction annulée, cette information est référée par un
                 ; compte à payer.
  ;
  ;
  GET CPCAP_IMPUTATION VIA CCITAXCODTVQ , &
                           CCITAXTYPTVQ   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 160 ; Destruction annulée, cette information est référée par un
                 ; compte à payer.
  ;
  ;
  GET CPPAM_IMPUTATION VIA CPITAXCODTPS , &
                           CPITAXTYPTPS   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 161 ; Destruction annulée, cette information est référée par un
                 ; paiement.
  ;
  ;
  GET CPPAM_IMPUTATION VIA CPITAXCODTVQ , &
                           CPITAXTYPTVQ   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 161 ; Destruction annulée, cette information est référée par un
                 ; paiement.

  ;
  ; On vérifie dans le client - compagnie.
  ;
  GET CRCLI_CIE VIA CLCTAXCODTPS , &
                    CLCTAXTYPTPS   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 162 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.
  ;
  ;
  GET CRCLI_CIE VIA CLCTAXCODTVP , &
                    CLCTAXTYPTVP   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 162 ; Destruction annulée, cette information est référée par un
                 ; client-compagnie.

  ;
  ; On vérifie dans les imputations des comptes à recevoir.
  ;
  GET CRCAR_IMPUTATION VIA CRITAXCODTPS , &
                           CRITAXTYPTPS   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 163 ; Destruction annulée, cette information est référée par un
                 ; compte à recevoir.
  ;
  ;
  GET CRCAR_IMPUTATION VIA CRITAXCODTVP , &
                           CRITAXTYPTVP   &
                USING TAXCLECOD OF MCTAXE , &
                      TAXCLETYP OF MCTAXE   &
                OPTIONAL
  IF ACCESSOK
  THEN ERROR 163 ; Destruction annulée, cette information est référée par un
                 ; compte à recevoir.

  ;
  ; On indique par la valeur 1, que la clé n'est pas référée.
  ;
  LET T_FLGDEL = "1"
  RETURN
END

BUILD
