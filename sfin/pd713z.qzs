;/T/ 2.4.4. Enregistrer le débitage - PROCÉDURE DE TRANSFERT
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-03-31
;
;    Description..: Le but de cette unité de traitement est de gérer
;                   le débitage des pièce de granit.
;
;-------------------------------------------------------------------------------

USE ussetqzs NOLIST

ACCESS PDFICMES &
  LINK CIECLE    OF PDFICMES, &
       FMGNUMRAP OF PDFICMES  &
    TO CIECLE,                &
       DEBNUMRAP OF PDDEBITAGE

;-------------------------------------------------------------------------------
;
; Variables obligatoires pour les en-têtes standards.
;

DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = "Transfert - débitage"
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""
;
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "PD713"
;
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM


;
; Différence entre les deux dates
;
DEFINE DZ_DIFSECTOT_DEB   FLOAT SIZE 8 = DEBDURSEC    OF PDDEBITAGE
DEFINE DZ_DIFSECTOT_ARRET FLOAT SIZE 8 = DEBDURSECARR OF PDDEBITAGE
DEFINE DZ_DIFSECTOT_OPER  FLOAT SIZE 8 = DEBDURSEC    OF PDDEBITAGE - &
                                         DEBDURSECARR OF PDDEBITAGE
;
; Différence de jour
;
DEFINE DZ_DIFJOU_DEB   FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT_DEB / (60 * 60 * 24))
DEFINE DZ_DIFJOU_ARRET FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT_ARRET / (60 * 60 * 24))
DEFINE DZ_DIFJOU_OPER  FLOAT SIZE 8 = FLOOR (DZ_DIFSECTOT_OPER / (60 * 60 * 24))
;
;
;
DEFINE DZ_DIFHRE_DEB   FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_DEB   - (DZ_DIFJOU_DEB   * 60 * 60 * 24)) / (60 * 60 ))
DEFINE DZ_DIFHRE_ARRET FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_ARRET - (DZ_DIFJOU_ARRET * 60 * 60 * 24)) / (60 * 60 ))
DEFINE DZ_DIFHRE_OPER  FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_OPER  - (DZ_DIFJOU_OPER  * 60 * 60 * 24)) / (60 * 60 ))
;
;
;
DEFINE DZ_DIFMIN_DEB   FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_DEB   - ( (DZ_DIFJOU_DEB   * 60 * 60 * 24) + (DZ_DIFHRE_DEB   * 60 * 60) ) ) / 60)
DEFINE DZ_DIFMIN_ARRET FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_ARRET - ( (DZ_DIFJOU_ARRET * 60 * 60 * 24) + (DZ_DIFHRE_ARRET * 60 * 60) ) ) / 60)
DEFINE DZ_DIFMIN_OPER  FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_OPER  - ( (DZ_DIFJOU_OPER  * 60 * 60 * 24) + (DZ_DIFHRE_OPER  * 60 * 60) ) ) / 60)
;
;
;
DEFINE DZ_DIFSEC_DEB   FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_DEB   - ((DZ_DIFJOU_DEB   * 60 * 60 * 24) + (DZ_DIFHRE_DEB   * 60 * 60) + (DZ_DIFMIN_DEB   * 60))))
DEFINE DZ_DIFSEC_ARRET FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_ARRET - ((DZ_DIFJOU_ARRET * 60 * 60 * 24) + (DZ_DIFHRE_ARRET * 60 * 60) + (DZ_DIFMIN_ARRET * 60))))
DEFINE DZ_DIFSEC_OPER  FLOAT   SIZE 8 = FLOOR ((DZ_DIFSECTOT_OPER  - ((DZ_DIFJOU_OPER  * 60 * 60 * 24) + (DZ_DIFHRE_OPER  * 60 * 60) + (DZ_DIFMIN_OPER  * 60))))
;
;
;
DEFINE DZ_DIFTIM_DEB      CHARACTER SIZE 6 = ASCII(DZ_DIFHRE_DEB,2)   + ":" + ASCII(DZ_DIFMIN_DEB,2)
DEFINE DZ_DIFTIM_ARRET    CHARACTER SIZE 6 = ASCII(DZ_DIFHRE_ARRET,2) + ":" + ASCII(DZ_DIFMIN_ARRET,2)
DEFINE DZ_DIFTIM_OPER     CHARACTER SIZE 6 = ASCII(DZ_DIFHRE_OPER,2)  + ":" + ASCII(DZ_DIFMIN_OPER,2)
;


;
; Message sur la liste
;
DEFINE D_MESSAGE CHARACTER SIZE 80 = &
         "Rapport transféré " IF DEBSTU OF PDDEBITAGE = "T" &
    ELSE "Erreur vérifier le rapport"


SORT ON FMGNUMRAP OF PDFICMES


USE ustitstd NOLIST
SKIP


FOOTING AT FMGNUMRAP &
  SKIP 1 &
  TAB 001 "Num. rapport..:" &
  TAB 017 FMGNUMRAP OF PDFICMES &
  TAB 028  "Code machine..:" &
  TAB 044 MDECODMAC OF PDDEBITAGE &
  TAB 055 "Date product..:" &
  TAB 071 DEBDATPRD OF PDDEBITAGE &
  TAB 082 "Quart travail.:" &
  TAB 098 QRTCODQRT OF PDDEBITAGE &
  SKIP &
  TAB 001 "Hre opération.:" &
  TAB 017 DZ_DIFTIM_DEB     &
  TAB 028 "Hre arrêt.....:" &
  TAB 044 DZ_DIFTIM_ARRET   &
  TAB 055 "Hre total.....:" &
  TAB 071 DZ_DIFTIM_OPER    &
  SKIP 2 &
  TAB 049 D_MESSAGE



BUILD pd713z
@IF FORMAT

PD713                                         GRANICOR INC.                                                               PAGE:    1
AA/MM/JJ HH:MM XXXXXXXXXXXX                  Transfert - débitage

Num. rapport..: xxxxxxxxx  Code machine..: xxxx Erreur vérifier le rapport
@ENDIF
