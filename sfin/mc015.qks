;/T/ Charte comptable par compte
;
;//M/GRA01/Francois Richard/1999-06-18
;  Description.: PROSIG
;                Modification des traitements dans le quel
;                on retrouve des années, périodes
;                tel que select, sort, choose, define, if.
;                Correction des formats sur les dates dans les écrans.
;
;/P/ Programmeur..: Marc Morissette
;    Date Création: 21 Oct 1993
;
;    Description..: Cet écran permet la gestion des associations de
;                   unité compte soit Incluse ou Excluse selon le consolidé
;                   Inclue ou Exclus ne change en rien le fonctionement de
;                   l'écran mais les imputations sont grandement touchés.
;
;
;/M/ Marie-Josée Hamel, 96.04.01, établir un lien entre le plan comptable et
;                                 la charte pour les périodes d'activité

;/V/ Écran vérifier pour le passage à l'an 2000

;-------------------------------------------------------------------------------
SCREEN mc015 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIENOM

TEMPORARY T_CCUPECDEBA_SIE1 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

TEMPORARY T_CCUPECDEBI_SIE1 CHARACTER SIZE 5 ; Temporaire servant pour le changement de siècle

USE ussectmp NOLIST
    "MC015"

USE mc015.hlp NOLIST

USE usactecr NOLIST

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding

;-------------------------------------------------------------------------------
;
;
FILE MCCHARTE_UNA PRIMARY NOITEM OCCURS 15
  ACCESS REQUEST CIECLE OF MCCHARTE_UNA, &
                 CPTCLE OF MCCHARTE_UNA, &
                 UNACLE OF MCCHARTE_UNA &
         VIA     CIECLE, &
                 CPTCLE, &
                 UNACLE &
         USING   CIECLE OF MCCHARTE_UNA, &
                 CPTCLE OF MCCHARTE_UNA, &
                 UNACLE OF MCCHARTE_UNA &
         ORDERBY CPTCLE,CIECLE, UNACLE

  ACCESS REQUEST CIECLE OF MCCHARTE_UNA, &
                 CPTCLE OF MCCHARTE_UNA  &
         VIA     CIECLE, &
                 CPTCLE  &
         USING   CIECLE OF MCCHARTE_UNA, &
                 CPTCLE OF MCCHARTE_UNA  &
         ORDERBY CPTCLE,CIECLE,  UNACLE

 ITEM CIECLE OF MCCHARTE_UNA INITIAL FIRST(CIECLE OF MCCHARTE_UNA)
 ITEM CPTCLE OF MCCHARTE_UNA INITIAL FIRST(CPTCLE OF MCCHARTE_UNA)


;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC USING "1"

;
; Validation de la compagnie.
;
FILE MCCOMPAGNIE      REFERENCE
 ACCESS VIA   CIECLE &
        USING CIECLE OF MCCHARTE_UNA
;
; Validation de l'unité adminitrative.
;
FILE MCUNITE_ADM      REFERENCE OCCURS WITH MCCHARTE_UNA
 ACCESS VIA CIECLE, UNACLE &
            USING CIECLE OF MCCHARTE_UNA, &
                  UNACLE OF MCCHARTE_UNA

;
; Validation des comptes
;
FILE VCPT_DSC         REFERENCE
 ACCESS VIA CPTCLE USING CPTCLE OF MCCHARTE_UNA


;
; pour valider les periodes de debut et de fin d'activité de la charte
;
FILE MCPERIODE_CTB DESIGNER OPEN READ


;-------------------------------------------------------------------------------
;
;
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes

TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les unités adm. et cie
TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur les unités adm.

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Charte comptable par compte " &
@ELSE
      " Chart of Accounts by %%% " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

SKIP

ALIGN(3,3,19)(,,30)
FIELD CIECLE    OF MCCHARTE_UNA HIDDEN ID 05 &
LABEL "Compagnie.....:"&
        REQUIRED &
        NOCHANGE &
        LOOKUP ON MCCOMPAGNIE &
          MESSAGE 120 ; Cette compagnie n'existe pas.
FIELD CIENOMABR OF MCCOMPAGNIE  &
        DISPLAY

FIELD CPTCLE    OF MCCHARTE_UNA HIDDEN ID SAME  &
LABEL "Compte........:"&
        REQUIRED &
        NOCHANGE &
        LOOKUP ON VCPT_DSC &
          MESSAGE 111 , & ; Ce compte n'existe pas.
        NOTON MCCHARTE_UNA &
          VIA CPTCLE, &
              CIECLE, &
              UNACLE &
          USING CPTCLE OF MCCHARTE_UNA, &
                CIECLE OF MCCHARTE_UNA, &
                UNACLE OF MCCHARTE_UNA &
          MESSAGE 130 ; la charte existe deja
FIELD CPTDSC    OF VCPT_DSC     DISPLAY

SKIP
@IF FRANCAIS
 TITLE "Période"      AT ,60
SKIP
 TITLE "Unité adm."   AT ,6
 TITLE "Description"  AT ,17
 TITLE "Active"       AT ,56
 TITLE "Inactive"     AT ,65
@ELSE
 TITLE "Period"       AT ,60
SKIP
 TITLE "%%%%   "     AT ,6
 TITLE "Description" AT ,17
 TITLE "Active"      AT ,56
 TITLE "Inactive"    AT ,65
@ENDIF
SKIP

ALIGN(6,6,7) (,,17) (,,56) (,,66)

CLUSTER OCCURS WITH MCCHARTE_UNA ID BASE 15

FIELD UNACLE    OF MCCHARTE_UNA  LABEL " " HIDDEN &
        REQUIRED &
        NOCHANGE &
        LOOKUP ON MCUNITE_ADM &
          MESSAGE 116 ; Cette unité adm. n'existe pas.
FIELD UNANOMABR OF MCUNITE_ADM  DISPLAY

FIELD CCUPECDEBA OF MCCHARTE_UNA DUPLICATE
FIELD CCUPECDEBI OF MCCHARTE_UNA DUPLICATE

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE INITIALIZE
BEGIN
  IF HLDCHTCPT OF MCHOLDING = "0"
  THEN ERROR 131 ; La notion de charte de compte n'est pas la
  ;
  ; Valide la sécurité d'accès pour l'écran par son code.
  ;
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE INPUT CIECLE
BEGIN
  ;
  ; Popup sur les compagnies.
  ;
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc106 MODE F PASSING T_CIECLE
    LET FIELDTEXT = TRUNC(T_CIECLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Popup sur les unités administratives.
;
;
PROCEDURE INPUT UNACLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_CIECLE = CIECLE OF MCCHARTE_UNA
    RUN SCREEN mc104 PASSING T_CIECLE, T_UNACLE MODE F
    LET FIELDTEXT = TRUNC(T_UNACLE)
  END
  ;
  ; Force le LOOKUP.
  ;
  IF     NOT FINDMODE &
     AND 0 =  SIZE(FIELDTEXT) &
     AND 0 <> SIZE(TRUNCATE(UNACLE OF MCCHARTE_UNA))
  THEN LET FIELDTEXT = UNACLE OF MCCHARTE_UNA
END


;-------------------------------------------------------------------------------
;
;
; Aide au choix du compte ou pour la consultaion
;
;
PROCEDURE INPUT CPTCLE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R" ; Régulier
    LET T_CPTCATPAT = "@" ; Catégorie
    LET T_PECCLE    = " "
    RUN SCREEN mc103 PASSING T_CPTCLE, T_CPTTYPPAT, T_CPTCATPAT, T_PECCLE MODE F
    LET FIELDTEXT = TRUNC(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
;
; Aide au choix de la période comptable.
;
;
PROCEDURE INPUT CCUPECDEBA
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc105 PASSING T_PECCLE MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
  IF 0 = SIZE( TRUNCATE( FIELDTEXT ) )
  THEN LET FIELDTEXT = CPTPECDEBA OF VCPT_DSC
END

;-------------------------------------------------------------------------------
;
;
; Validation de la période de début
;
;
PROCEDURE EDIT CCUPECDEBA
BEGIN
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    GET MCPERIODE_CTB VIA PECCLE &
                      USING FIELDTEXT &
                      OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 134 ; La periode n'existe pas
  END
END


;-------------------------------------------------------------------------------
;
;
; Aide au choix de la période comptable.
;
;
PROCEDURE INPUT CCUPECDEBI
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_PECCLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc105 PASSING T_PECCLE MODE F
    LET FIELDTEXT = TRUNC(T_PECCLE)
  END
  IF 0 = SIZE( TRUNCATE( FIELDTEXT ) )
  THEN LET FIELDTEXT = CPTPECDEBI OF VCPT_DSC
END

;-------------------------------------------------------------------------------
;
;
; Validation de la période de fin
;
;
PROCEDURE EDIT CCUPECDEBI
BEGIN
  IF 0 <> SIZE(TRUNCATE(FIELDTEXT))
  THEN BEGIN
    GET MCPERIODE_CTB VIA PECCLE &
                      USING FIELDTEXT &
                      OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 134 ; La periode n'existe pas

    ; Ajustement pour le changement de siècle
    IF CCUPECDEBI OF MCCHARTE_UNA[1:1] < "8"
    THEN LET T_CCUPECDEBI_SIE1 = "1" + CCUPECDEBI OF MCCHARTE_UNA
    ELSE LET T_CCUPECDEBI_SIE1 = "0" + CCUPECDEBI OF MCCHARTE_UNA

    ; Ajustement pour le changement de siècle
    IF CCUPECDEBA OF MCCHARTE_UNA[1:1] < "8"
    THEN LET T_CCUPECDEBA_SIE1 = "1" + CCUPECDEBA OF MCCHARTE_UNA
    ELSE LET T_CCUPECDEBA_SIE1 = "0" + CCUPECDEBA OF MCCHARTE_UNA

    IF T_CCUPECDEBI_SIE1 < T_CCUPECDEBA_SIE1
    THEN ERROR 135 ; la periode de fin doit etre > que le debut
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Copie d'une charte de compte d'une unité adm. à une autre.
;
;
PROCEDURE DESIGNER COPI NODATA
BEGIN
  FOR MCCHARTE_UNA
  BEGIN
    IF ALTEREDRECORD OF MCCHARTE_UNA
    THEN ERROR 132 ; Faire une mise à jour.
  END

  RUN SCREEN mc015a MODE E
  INFO 133 ; Faire une recherche.
END

;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  FOR MCCHARTE_UNA
  BEGIN
    ;
    ; Validation de la destruction
    ;
    IF     DELETEDRECORD OF MCCHARTE_UNA &
       AND TZ_SECDEL = "0"
    THEN ERROR 1
    ;
    ; Validation de la modification.
    ;
    IF     ALTEREDRECORD     OF MCCHARTE_UNA &
       AND NOT NEWRECORD     OF MCCHARTE_UNA &
       AND NOT DELETEDRECORD OF MCCHARTE_UNA &
       AND TZ_SECMOD = "0"
    THEN ERROR 2
  END
END

BUILD
@IF FORMAT


xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************* Charte comptable *******************************
* Compagnie.....: xxxx       xxxxxxxxxxxxxxxxxxxx                              *
* Compte........: xxxxxxxx   xxxxxxxxxxxxxxxxxxxx                              *
*                                                          Période             *
*    Unité adm. Description                            Active   Inactive       *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
*     xxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxx     xxxxx         *
********************************************************************************
@ENDIF
