;/T/ Impression de la liste des erreurs de l'inventaire physique des blocs .
;
;/P/ Programmeur..: René Bonneau
;    Date Création: 1996-04-02
;
;    Description..: Le but de cette unité de traitement est d'imprimer
;                   la liste d'erreur des blocs de l'inventaire physique
;                   de bloc de granit de Granicor.
;
;    Paramètres....: Compagnie
;
;/M/ François Déry, 4 mai 1999
;       Ajout d'un & dans le page heading.
;-------------------------------------------------------------------------------
;
; initialisation des paramètres globales de QUIZ.
;
CANCEL CLEAR

USE ussetqzs NOLIST

ACCESS INBLOC                                           &
  LINK TGRCODGRA,                                       &
       CIECLE                                           &
    TO TGRCODGRA,                                       &
       CIECLE        OF PDTYPE_GRANIT OPTIONAL          &
  LINK BLONUMGRA001APR  OF INBLOC,                      &
       BLONUMGRA002APR  OF INBLOC,                      &
       BLORECAPR        OF INBLOC,                      &
       CIECLE           OF INBLOC                       &
    TO BLONUMGRA001APR,                                 &
       BLONUMGRA002APR,                                 &
       BLORECAPR,                                       &
       CIECLE           OF PDBLOC OPTIONAL

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG306Z"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

DEFINE D-ERREUR CHARACTER SIZE 26 = &
                        "NUMERO DE BLOC INVALIDE" &
                IF NOT RECORD PDBLOC EXIST &
                ELSE &
                        "STATUT DU BLOC INVALIDE" &
                IF BLOSTU OF PDBLOC <> "I" AND &
                   BLOSTU OF PDBLOC <> "S" AND &
                   BLOSTU OF PDBLOC <> "R" AND &
                   BLOSTU OF PDBLOC <> "A" &
                ELSE ""

DEFINE D-BLOC-TRI CHARACTER SIZE 10 =           &
        BLONUMGRA001APR OF INBLOC +             &
        BLONUMGRA002APR OF INBLOC +             &
        BLORECAPR       OF INBLOC

DEFINE D-DESC CHAR SIZE 40 = IBLDESC OF INBLOC[1:40]

SORT ON IBLNUMPGE &
     ON IBLHREINV &
     ON D-BLOC-TRI

SELECT IF D-ERREUR <> ""

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES ERREURS DE  L'INVENTAIRE PHYSIQUE  DE BLOCS                           ")

USE ustitgor200_45 NOLIST
SKIP 1 &
  TAB 025 D-TITRE-CENTRE &
SKIP 2 &
   TAB 003 "PAGE LOCA   NUM. BLOC               COMMENTAIRE                          STATUT GRANIT              ERREUR" &
SKIP &
  TAB 003 "COMMENTAIRE" &
SKIP &
  TAB 003 "----- ----   ---- ----               -----------                         ------  ------             ------" &
SKIP

REPORT  &
  TAB 001 IBLNUMPGE       OF INBLOC             &
  TAB 009 IBLNUMLOC       OF INBLOC             &
  TAB 014 BLONUMGRA001APR OF INBLOC             &
  TAB 018 "-"                                   &
  TAB 019 BLONUMGRA002APR OF INBLOC             &
  TAB 024 "-"                                   &
  TAB 027 D-DESC                                &
  TAB 069 BLORECAPR       OF INBLOC             &
  TAB 079 BLOSTU          OF PDBLOC             &
  TAB 083 TGRCODGRA       OF PDBLOC             &
;  TAB 092 TGRCODGRA       OF INBLOC             &
;  TAB 080 IBLNUMPGE       OF INBLOC             &
;  TAB 087 IBLNUMLOC       OF INBLOC             &
  TAB 095 D-ERREUR                              &
;SKIP                                            &
;  TAB 003 IBLDESC         OF INBLOC             &
SKIP

FINAL FOOT SKIP 3  &
        "NOMBRE DE BLOCS EN ERREUR..........:" COUNT

BUILD ig306z_1

;==============================================================================

ACCESS PDBLOC                           &
  LINK TGRCODGRA       OF PDBLOC,       &
       CIECLE          OF PDBLOC        &
    TO TGRCODGRA,                       &
       CIECLE OF PDTYPE_GRANIT OPTIONAL &
  LINK BLONUMGRA001APR OF PDBLOC,       &
       BLONUMGRA002APR OF PDBLOC,       &
       BLORECAPR       OF PDBLOC,       &
       CIECLE          OF PDBLOC        &
    TO BLONUMGRA001APR,                 &
       BLONUMGRA002APR,                 &
       BLORECAPR,                       &
       CIECLE OF INBLOC OPTIONAL

CHOOSE CIECLE PARM

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG306Z"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

SELECT IF (BLOSTU OF PDBLOC = "I" OR &
                   BLOSTU OF PDBLOC = "R" OR &
                   BLOSTU OF PDBLOC = "A" OR &
              BLOSTU OF PDBLOC = "S") AND &
           NOT RECORD INBLOC EXIST

DEFINE D_BLOC_TRI CHARACTER SIZE 10 = &
       BLONUMGRA001APR + BLONUMGRA002APR + BLORECAPR

SORT ON D_BLOC_TRI

DEFINE D-ERREUR CHARACTER SIZE 40 = &
  "NUMERO DE BLOC EXISTE AVANT ET NON APRES"

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES NUMEROS DE BLOCS EXISTANT AVANT L'INVENTAIRE ET NON APRES             ")

USE ustitgor200_45 NOLIST

SKIP 1 &
  TAB 025 D-TITRE-CENTRE &
SKIP 2 &
  TAB 003 "NUM. BLOC     STATUT  GRANIT  DESCRIPTION                               ERREUR" &
SKIP &
  TAB 003 "---------     ------  ------  -----------                               ------" &
SKIP

REPORT  &
  TAB 003 BLONUMGRA001APR OF PDBLOC             &
  TAB 007 "-"                                   &
  TAB 008 BLONUMGRA002APR OF PDBLOC             &
  TAB 013 "-"                                   &
  TAB 014 BLORECAPR       OF PDBLOC             &
  TAB 019 BLOSTU          OF PDBLOC             &
  TAB 027 TGRCODGRA       OF PDBLOC             &
  TAB 033 TGRDSCFRA001    OF PDTYPE_GRANIT      &
  TAB 075 D-ERREUR                              &
SKIP 1

FINAL FOOT SKIP 3  &
        "NOMBRE DE BLOCS EN ERREUR..........:" COUNT

BUILD ig306z_2

;==============================================================================

ACCESS INBLOC

CHOOSE CIECLE PARM

DEFINE D-NBRE-BLOC INTEGER SIZE 2 = 1

DEFINE D_BLOC_TRI CHARACTER SIZE 10 = &
       BLONUMGRA001APR + BLONUMGRA002APR + BLORECAPR

SORT ON D_BLOC_TRI

SET SUBFILE AT D_BLOC_TRI NAME WIG306Z2

REPORT SUMMARY                          &
        CIECLE          OF INBLOC   &
        BLONUMGRA001APR OF INBLOC   &
        BLONUMGRA002APR OF INBLOC   &
        BLORECAPR       OF INBLOC   &
        D-NBRE-BLOC SUBTOTAL

BUILD ig306z_3

;==============================================================================

ACCESS *WIG306Z2      &
  LINK BLONUMGRA001APR, &
       BLONUMGRA002APR, &
       BLORECAPR,       &
       CIECLE           &
    TO BLONUMGRA001APR, &
       BLONUMGRA002APR, &
       BLORECAPR,       &
       CIECLE OF INBLOC

;-------------------------------------------------------------------------------
; Variables obligatoires pour les en-têtes standards.
;
DEFINE DZ_NOMRAP1 CHARACTER SIZE 60 = PARM
DEFINE DZ_NOMRAP2 CHARACTER SIZE 60 = ""

;-------------------------------------------------------------------------------
; Numéro du rapport.
;
DEFINE DZ_NUMRAP  CHARACTER SIZE  7 = "IG306Z"

;-------------------------------------------------------------------------------
; Nom de la compagnie.
;
DEFINE DZ_CIENOM CHARACTER SIZE 40 = PARM

SELECT IF D-NBRE-BLOC > 1

DEFINE D-ERREUR CHARACTER SIZE 40 = &
  "NUMERO DE BLOC EN DOUBLE"

DEFINE D-TITRE-CENTRE CHARACTER SIZE 80 = &
        CENTRE ("LISTE DES NUMEROS DE BLOCS EN DOUBLE                                            ")

USE ustitgor200_45 NOLIST

SKIP 1 &
  TAB 025 D-TITRE-CENTRE &
SKIP 2 &
TAB 003 "NUM. BLOC      PAGE   LOCALISATION  ERREUR                                    COMMENTAIRE" &
SKIP &
TAB 003 "---------      ----   ------------  ------                                    -----------" &
SKIP

REPORT &
  TAB 003 BLONUMGRA001APR OF INBLOC             &
  TAB 007 "-"                                   &
  TAB 008 BLONUMGRA002APR OF INBLOC             &
  TAB 013 "-"                                   &
  TAB 014 BLORECAPR       OF INBLOC             &
  TAB 017 IBLNUMPGE       OF INBLOC             &
  TAB 028 IBLNUMLOC       OF INBLOC             &
  TAB 039 D-ERREUR                              &
  TAB 081 IBLDESC         OF INBLOC             &
SKIP 1

FINAL FOOT SKIP 3  &
        "NOMBRE DE BLOCS EN ERREUR..........:" COUNT

BUILD ig306z_4

