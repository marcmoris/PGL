;/T/ Impression de facture
;
;/P/ Programmeur.....: René Bonneau
;    Date création...: 16 mai 1996
;    Description.....: Écran fantôme pour la lecture de fichier utile dans
;                      l'ecran fac001 (impression de la facture)
;                      Nécessaire à cause d'un dépassement de fichier lus
;
;-------------------------------------------------------------------------------
SET NESTING 100

;/V/ Écran vérifier pour le passage à l'an 2000


    SCREEN fac001a NOMODE &
               ACTION LABEL " "                         &
               FROM 2,70 TO 2,80                        &
               RECEIVING                                &
                                PDFACTURE,              &
                                PDCOMMAN_INTERNE,       &
                                TZ_T_TMEDSC_RETOUR,&
                                TZ_T_CBIDSC_RETOUR,     &
                                TZ_ADRFAC001_RETOUR,    &
                                TZ_ADRFAC002_RETOUR,    &
                                TZ_ADRFAC003_RETOUR,    &
                                TZ_ADRFAC004_RETOUR,    &
                                TZ_ADRFAC005_RETOUR,    &
                                TZ_ADRFAC006_RETOUR,    &
                                TZ_ADRLIV001_RETOUR,    &
                                TZ_ADRLIV002_RETOUR,    &
                                TZ_ADRLIV003_RETOUR,    &
                                TZ_ADRLIV004_RETOUR,    &
                                TZ_ADRLIV005_RETOUR,    &
                                TZ_ADRLIV006_RETOUR,    &
                                TZ_COMFRSCOUINC_RETOUR, &
                                TZ_CODLIVDSC_RETOUR,    &
                                TZ_PJTNUMPRO_RETOUR,    &
                                TZ_PJTNOM_RETOUR,       &
                                TZ_NUMBONLIV_RETOUR,    &
                                TZ_VIA_RETOUR,       &
                                TZ_CLILNG_RETOUR,       &
                                TZ_TAXTPS_RETOUR,       &
                                TZ_TAXTVQ_RETOUR,       &
                                TZ_TAXPCTTPS_RETOUR,    &
                                TZ_TAXPCTTVQ_RETOUR,    &
                                TZ_DEVTAU_RETOUR,       &
                                TZ_DEVDESC_RETOUR,      &
                                TZ_BLINUMBLI_RETOUR,    &
                                TZ_EXPNUMEXP_RETOUR


TEMPORARY TZ_T_TMEDSC_RETOUR          CHAR SIZE 40
TEMPORARY TZ_T_CBIDSC_RETOUR            CHARACTER SIZE 40
TEMPORARY TZ_ADRFAC001_RETOUR           CHARACTER SIZE 40 ; Adresse
TEMPORARY TZ_ADRFAC002_RETOUR           CHARACTER SIZE 40 ;
TEMPORARY TZ_ADRFAC003_RETOUR           CHARACTER SIZE 40 ; de
TEMPORARY TZ_ADRFAC004_RETOUR           CHARACTER SIZE 40 ;
TEMPORARY TZ_ADRFAC005_RETOUR           CHARACTER SIZE 40 ; facturation
TEMPORARY TZ_ADRFAC006_RETOUR           CHARACTER SIZE 40 ;

TEMPORARY TZ_ADRLIV001_RETOUR           CHARACTER SIZE 40 ; Adresse
TEMPORARY TZ_ADRLIV002_RETOUR           CHARACTER SIZE 40 ;
TEMPORARY TZ_ADRLIV003_RETOUR           CHARACTER SIZE 40 ; de
TEMPORARY TZ_ADRLIV004_RETOUR           CHARACTER SIZE 40 ;
TEMPORARY TZ_ADRLIV005_RETOUR           CHARACTER SIZE 40 ; livraison
TEMPORARY TZ_ADRLIV006_RETOUR           CHARACTER SIZE 40 ;

TEMPORARY TZ_PJTNUMPRO_RETOUR           CHARACTER SIZE 09 ; Num. projet
TEMPORARY TZ_COMFRSCOUINC_RETOUR        CHARACTER SIZE 40 ; Frais de courtage
TEMPORARY TZ_CODLIVDSC_RETOUR           CHARACTER SIZE 25 ; Desc. code liv.
TEMPORARY TZ_PJTNOM_RETOUR              CHARACTER SIZE 40 ; Nom projet
TEMPORARY TZ_NUMBONLIV_RETOUR           CHARACTER SIZE 12 ; Num. bon livraison
TEMPORARY TZ_VIA_RETOUR              CHARACTER SIZE 40 ; Nom du transporteur
TEMPORARY TZ_CLILNG_RETOUR              CHARACTER SIZE 1  ; Code de langue
TEMPORARY TZ_TAXTPS_RETOUR              CHARACTER SIZE 25 ; taxe fédérale
TEMPORARY TZ_TAXTVQ_RETOUR              CHARACTER SIZE 25 ; taxe provinciale
TEMPORARY TZ_TAXPCTTPS_RETOUR           FLOAT     SIZE 4  ; % taxe fédérale
TEMPORARY TZ_TAXPCTTVQ_RETOUR           FLOAT     SIZE 4  ; % taxe provinciale
TEMPORARY TZ_DEVTAU_RETOUR              FLOAT     SIZE 8  ; % Taux de la devise
TEMPORARY TZ_DEVDESC_RETOUR             CHARACTER SIZE 15 ; % Desc de la devise
TEMPORARY TZ_BLINUMBLI_RETOUR           CHARACTER SIZE 2  ; Num bon livraison
TEMPORARY TZ_EXPNUMEXP_RETOUR           INTEGER   SIZE 4  ; Num expédition

TEMPORARY T_ADRFAC001           CHARACTER SIZE 40 ; Adresse
TEMPORARY T_ADRFAC002           CHARACTER SIZE 40 ;
TEMPORARY T_ADRFAC003           CHARACTER SIZE 40 ; de
TEMPORARY T_ADRFAC004           CHARACTER SIZE 40 ;
TEMPORARY T_ADRFAC005           CHARACTER SIZE 40 ; facturation
TEMPORARY T_ADRFAC006           CHARACTER SIZE 40 ;

TEMPORARY T_ADRLIV001           CHARACTER SIZE 40 ; Adresse
TEMPORARY T_ADRLIV002           CHARACTER SIZE 40 ;
TEMPORARY T_ADRLIV003           CHARACTER SIZE 40 ; de
TEMPORARY T_ADRLIV004           CHARACTER SIZE 40 ;
TEMPORARY T_ADRLIV005           CHARACTER SIZE 40 ; livraison
TEMPORARY T_ADRLIV006           CHARACTER SIZE 40 ;

TEMPORARY T_PJTNUMPRO           CHARACTER SIZE 09 ; Num. projet
TEMPORARY T_COMFRSCOUINC        CHARACTER SIZE 40 ; Frais de courtage
TEMPORARY T_CODLIVDSC           CHARACTER SIZE 25 ; Desc. code liv.
TEMPORARY T_CBIDSC              CHARACTER SIZE 40
TEMPORARY T_TMEDSC              CHAR SIZE 40
TEMPORARY T_PJTNOM              CHARACTER SIZE 40 ; Nom projet
TEMPORARY T_NUMBONLIV           CHARACTER SIZE 12 ; Num. bon livraison
TEMPORARY T_VIA                 CHARACTER SIZE 40 ; Nom du transporteur
TEMPORARY T_CLILNG              CHARACTER SIZE 01 ; Code de langue
TEMPORARY T_TAXTPS              CHARACTER SIZE 25 ; Taxe fédérale
TEMPORARY T_TAXTVQ              CHARACTER SIZE 25 ; taxe provinciale
TEMPORARY T_TAXPCTTPS           FLOAT     SIZE 4  ; % Taxe fédérale
TEMPORARY T_TAXPCTTVQ           FLOAT     SIZE 4  ; % taxe provinciale
TEMPORARY T_DEVTAU              FLOAT     SIZE 8  ; % Taux de la devise
TEMPORARY T_DEVDESC             CHARACTER SIZE 15 ; % Desc de la devise
TEMPORARY T_BLINUMBLI           CHARACTER SIZE 2  ; Num bon livraison
TEMPORARY T_EXPNUMEXP           INTEGER   SIZE 4  ; Num expédition
TEMPORARY T_DEVCLE              CHARACTER SIZE 4

; Fichier lus dans cet écran
;---------------------------

FILE PDFACTURE                     MASTER

FILE PDCOMMAN_INTERNE              MASTER

FILE MCREF_ADRESSE                 DESIGNER

FILE CRTERME                        DESIGNER

FILE VCBI_DSC                      DESIGNER

FILE PDPROJET                      DESIGNER

FILE MCCODE_BILINGUE               DESIGNER

FILE PDFACT_BON_LIVR               DESIGNER

FILE PDEXPEDITION                  DESIGNER

FILE CPFOURNISSEUR                 DESIGNER

FILE CRCLIENT                      DESIGNER

FILE MCTAXE                        DESIGNER

FILE MCDEVISE                      DESIGNER

PROCEDURE FIND
BEGIN
  ;
  ; Lire client
  ;
  GET CRCLIENT OPTIONAL                       &
    VIA   CLICIECLE,                          &
          CLICLE                              &
    USING CLICIECLE OF PDFACTURE,             &
          CLICLE    OF PDFACTURE
  IF NOT ACCESSOK
  THEN ERROR 9999
  ELSE LET T_CLILNG = CLILNG OF CRCLIENT

  IF FTRTYPFAC OF PDFACTURE = "C"
  THEN BEGIN
    LET T_DEVCLE = COMDEV OF PDCOMMAN_INTERNE
  END
  ELSE BEGIN
    LET T_DEVCLE = DEVCLE OF CRCLIENT
  END
;
  GET CRTERME                                   &
    VIA     TMECLE                              &
    USING   FTRTERNET OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    IF T_CLILNG = "F"
    THEN BEGIN
      LET T_TMEDSC = TMEDSCFRA OF CRTERME
    END
    ELSE BEGIN
      LET T_TMEDSC = TMEDSCANG OF CRTERME
    END
  END

  ;
  ; Lire la devise du client
  ;
  GET MCDEVISE                                  &
    VIA    DEVCLE                               &
    USING  T_DEVCLE OPTIONAL
  IF ACCESSOK
  THEN BEGIN
    IF CLILNG OF CRCLIENT = "F"
    THEN BEGIN
      LET T_DEVDESC = DEVDSCFRA OF MCDEVISE
    END
    ELSE BEGIN
      LET T_DEVDESC = DEVDSCANG OF MCDEVISE
    END
    LET T_DEVTAU = DEVTAU OF MCDEVISE
  END
  ;
  ; lire taxe TPS
  ;
  GET MCTAXE OPTIONAL                         &
    VIA    TAXCLETYP,                         &
           TAXCLECOD                          &
    USING "F",                                &
           FTRTAXCODTPS OF PDFACTURE

  IF ACCESSOK
  THEN BEGIN
    IF CLILNG OF CRCLIENT = "F"
    THEN BEGIN
      LET T_TAXTPS = TAXDSCFRA OF MCTAXE
;      LET T_TAXTPS = "TPS: " + TAXDSCFRA OF MCTAXE
    END
    ELSE BEGIN
      LET T_TAXTPS = TAXDSCANG OF MCTAXE
;      LET T_TAXTPS = "GTS: " + TAXDSCANG OF MCTAXE
    END
    LET T_TAXPCTTPS = TAXPCT OF MCTAXE
  END
  ;
  ; lire taxe TVQ
  ;
  GET MCTAXE OPTIONAL                         &
    VIA    TAXCLETYP,                         &
           TAXCLECOD                          &
    USING "P",                                &
           FTRTAXCODTVQ OF PDFACTURE

  IF ACCESSOK
  THEN BEGIN
    IF CLILNG OF CRCLIENT = "F"
    THEN BEGIN
      LET T_TAXTVQ = TAXDSCFRA OF MCTAXE
;      LET T_TAXTVQ = "TVQ: " + TAXDSCFRA OF MCTAXE
    END
    ELSE BEGIN
      ; Verifier le libeller en anglais
      LET T_TAXTVQ = TAXDSCANG OF MCTAXE
;      LET T_TAXTVQ = "TVQ: " + TAXDSCANG OF MCTAXE
    END
    LET T_TAXPCTTVQ = TAXPCT OF MCTAXE
  END
  IF FTRTYPFAC OF PDFACTURE = "C"
  THEN BEGIN
    ;
    ; Frais de courtage
    ;
    GET MCCODE_BILINGUE OPTIONAL                &
      VIA   XELNOM,                             &
            CBICLE                              &
      USING "COMFRSCOUINC",                     &
            COMFRSCOUINC OF PDCOMMAN_INTERNE
    IF ACCESSOK
    THEN BEGIN
      IF CLILNG OF CRCLIENT = "F"
      THEN BEGIN
        LET T_COMFRSCOUINC = CBIDSCFRA        OF MCCODE_BILINGUE
      END
      ELSE BEGIN
        LET T_COMFRSCOUINC = CBIDSCANG        OF MCCODE_BILINGUE
      END
    END
    ;
    ; Code de livraison
    ;
    GET MCCODE_BILINGUE OPTIONAL                &
      VIA   XELNOM,                             &
            CBICLE                              &
      USING "PLVCODLIV",                        &
            PLVCODLIV OF PDCOMMAN_INTERNE
    IF ACCESSOK
    THEN BEGIN
      IF CLILNG OF CRCLIENT = "F"
      THEN BEGIN
        LET T_CODLIVDSC = CBIDSCFRA        OF MCCODE_BILINGUE
      END
      ELSE BEGIN
        LET T_CODLIVDSC = CBIDSCANG        OF MCCODE_BILINGUE
      END
    END
;------------------
   GET VCBI_DSC OPTIONAL                &
      VIA   XELNOM,                             &
            CBICLE                              &
      USING "PLVCODLIV",                        &
            PLVCODLIV OF PDCOMMAN_INTERNE
    IF ACCESSOK
    THEN BEGIN
     LET T_CBIDSC = CBIDSC OF VCBI_DSC
    END
;-----------------
   ;
    ; Lecture projet
    ;
    GET PDPROJET OPTIONAL                       &
      VIA   PJTNUMPRO                           &
      USING PJTNUMPRO OF PDCOMMAN_INTERNE
    IF ACCESSOK
    THEN BEGIN
      LET T_PJTNUMPRO = PJTNUMPRO OF PDPROJET[1:2] &
                        + "-" &
                        + PJTNUMPRO OF PDPROJET[3:2] &
                        + "-" &
                        + PJTNUMPRO OF PDPROJET[5:3]
      LET T_PJTNOM    = PJTNOM    OF PDPROJET
    END
  END
  ;
  ; Adresse de facturation
  ;
  GET MCREF_ADRESSE OPTIONAL                  &
    VIA   REFCIECLE,                          &
          REFCLETYP,                          &
          REFCLENUM,                          &
          RADCLE                              &
    USING "----",                             &
          "C",                                &
          CLICLE        OF PDFACTURE,         &
          FTRNUMADRFAC  OF PDFACTURE
  IF ACCESSOK
  THEN BEGIN
    LET T_ADRFAC001 = RADNOM  OF MCREF_ADRESSE
    LET T_ADRFAC002 = RADADR1 OF MCREF_ADRESSE
    LET T_ADRFAC003 = RADADR2 OF MCREF_ADRESSE
    LET T_ADRFAC004 = RADADR3 OF MCREF_ADRESSE
    LET T_ADRFAC005 = RADADR4 OF MCREF_ADRESSE[1:29] + "  " + &
                      RADCP   OF MCREF_ADRESSE
    LET T_ADRFAC006 = "Tel.:"  + RADTEL OF MCREF_ADRESSE + &
                      "  Fax.:" + RADFAX OF MCREF_ADRESSE
  END
  ;
  ; Adresse de livraison
  ;
  GET MCREF_ADRESSE OPTIONAL                  &
    VIA   REFCIECLE,                          &
          REFCLETYP,                          &
          REFCLENUM,                          &
          RADCLE                              &
    USING "----",                             &
          "C",                                &
          CLICLE        OF PDFACTURE,         &
          FTRNUMADRLIV  OF PDFACTURE

  IF ACCESSOK
  THEN BEGIN
    LET T_ADRLIV001 = RADNOM  OF MCREF_ADRESSE
    LET T_ADRLIV002 = RADADR1 OF MCREF_ADRESSE
    LET T_ADRLIV003 = RADADR2 OF MCREF_ADRESSE
    LET T_ADRLIV004 = RADADR3 OF MCREF_ADRESSE
    LET T_ADRLIV005 = RADADR4 OF MCREF_ADRESSE[1:18] + "  " + &
                      RADCP   OF MCREF_ADRESSE

    LET T_ADRLIV006 = "Tel.:" +  RADTEL OF MCREF_ADRESSE  +       &
                      "  Fax.:" + RADFAX OF MCREF_ADRESSE
  END


  GET PDFACT_BON_LIVR OPTIONAL                &
    VIA   CIECLE,                             &
          FTRNUMFAC                           &
    USING CIECLE OF PDFACTURE,                &
          FTRNUMFAC OF PDFACTURE
  IF ACCESSOK
  THEN BEGIN
    LET T_NUMBONLIV = RJ(TRUNCATE(ASCII(EXPNUMEXP OF PDFACT_BON_LIVR)) &
                      + "-" &
                      + TRUNCATE(BLINUMBLI OF PDFACT_BON_LIVR))
    LET T_BLINUMBLI = BLINUMBLI OF PDFACT_BON_LIVR
    LET T_EXPNUMEXP = EXPNUMEXP OF PDFACT_BON_LIVR
    ;
    ; Expédition
    ;
    GET PDEXPEDITION OPTIONAL                   &
      VIA   CIECLE,                             &
            EXPNUMEXP                           &
      USING CIECLE    OF PDFACTURE,             &
            EXPNUMEXP OF PDFACT_BON_LIVR
    IF ACCESSOK
    THEN BEGIN
      ;
      ; Fousisseur de transpour (VIA:)
      ;
      GET CPFOURNISSEUR OPTIONAL          &
        VIA   FOUCLE                      &
        USING FOUCLE OF PDEXPEDITION
      IF ACCESSOK
      THEN BEGIN
         IF EXPPREPAI OF PDEXPEDITION = "O"
          THEN LET T_VIA = FOUNOMABR OF CPFOURNISSEUR
         ELSE  LET T_VIA = "Collect"
      END
    END
  END
  RETURN
END

PROCEDURE EXIT
BEGIN
  LET TZ_ADRFAC001_RETOUR      = T_ADRFAC001
  LET TZ_ADRFAC002_RETOUR      = T_ADRFAC002
  LET TZ_ADRFAC003_RETOUR      = T_ADRFAC003
  LET TZ_ADRFAC004_RETOUR      = T_ADRFAC004
  LET TZ_ADRFAC005_RETOUR      = T_ADRFAC005
  LET TZ_ADRFAC006_RETOUR      = T_ADRFAC006

  LET TZ_ADRLIV001_RETOUR      = T_ADRLIV001
  LET TZ_ADRLIV002_RETOUR      = T_ADRLIV002
  LET TZ_ADRLIV003_RETOUR      = T_ADRLIV003
  LET TZ_ADRLIV004_RETOUR      = T_ADRLIV004
  LET TZ_ADRLIV005_RETOUR      = T_ADRLIV005
  LET TZ_ADRLIV006_RETOUR      = T_ADRLIV006

  LET TZ_COMFRSCOUINC_RETOUR   = T_COMFRSCOUINC
  LET TZ_CODLIVDSC_RETOUR      = T_CODLIVDSC
  LET TZ_T_CBIDSC_RETOUR         = T_CBIDSC
  LET TZ_T_TMEDSC_RETOUR        = T_TMEDSC
  LET TZ_PJTNUMPRO_RETOUR      = T_PJTNUMPRO
  LET TZ_PJTNOM_RETOUR         = T_PJTNOM
  LET TZ_NUMBONLIV_RETOUR      = T_NUMBONLIV
  LET TZ_VIA_RETOUR            = T_VIA
  LET TZ_CLILNG_RETOUR         = T_CLILNG
  LET TZ_TAXTPS_RETOUR         = T_TAXTPS
  LET TZ_TAXTVQ_RETOUR         = T_TAXTVQ
  LET TZ_TAXPCTTPS_RETOUR      = T_TAXPCTTPS
  LET TZ_TAXPCTTVQ_RETOUR      = T_TAXPCTTVQ
  LET TZ_DEVTAU_RETOUR         = T_DEVTAU
  LET TZ_DEVDESC_RETOUR        = T_DEVDESC
  LET TZ_BLINUMBLI_RETOUR      = T_BLINUMBLI
  LET TZ_EXPNUMEXP_RETOUR      = T_EXPNUMEXP

END

BUILD

