;/T/ Machine opération de surface finis possible (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-18
;
;    Description..: Cette table contient les informations de base des
;                   machines servant à éffectuer les opérations de surface et
;                   les finis qu'elles peuvent exécuter.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd168 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD168"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd168.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à l'appel des sous-écrans de consultation
;
TEMPORARY T_MOPCODMAC CHARACTER SIZE 04
TEMPORARY T_TYFCODFIN CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Variable nécessaire au traitement
;

TEMPORARY T_SILENT       CHARACTER SIZE 1


;-------------------------------------------------------------------------------
;
; Machine opération de surface fini possible
;

FILE PDMACH_OP_S_F_P PRIMARY NOITEM

  ACCESS VIA     CIECLE,   &
                 MOPCODMAC &
         USING   T_CIECLEMNU,                  &
                 MOPCODMAC OF PDMACH_OP_S_F_P  &
         REQUEST MOPCODMAC OF PDMACH_OP_S_F_P  &
         ORDERBY MOPCODMAC

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 MOPCODMAC

         ITEM CIECLE OF PDMACH_OP_S_F_P INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
; Machine opération de surface
;

FILE PDMACH_OP_SURF REFERENCE
  ACCESS VIA     CIECLE ,                         &
                 MOPCODMAC                        &
         USING   T_CIECLEMNU,                     &
                 MOPCODMAC OF PDMACH_OP_S_F_P


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE ALIAS TYF_AVANT

  ACCESS VIA     CIECLE,                         &
                 TYFCODFIN                       &
         USING   T_CIECLEMNU,                    &
                 MFPCODFINAVT OF PDMACH_OP_S_F_P


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI REFERENCE ALIAS TYF_APRES

  ACCESS VIA     CIECLE,                         &
                 TYFCODFIN                       &
         USING   T_CIECLEMNU,                    &
                 MFPCODFINAPR OF PDMACH_OP_S_F_P


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Machine opération surf. fini possible " &
@ELSE
      " %%%Machine opération surf. fini possible " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19) (22,23,23)


FIELD MOPCODMAC OF PDMACH_OP_S_F_P              &
        HIDDEN ID 05                            &
        REQUIRED                                &
        LOOKUP ON PDMACH_OP_SURF                &
          VIA   CIECLE,                         &
                MOPCODMAC                       &
          USING T_CIECLEMNU,                    &
                MOPCODMAC OF PDMACH_OP_S_F_P    &
          MESSAGE 2915 ; Ce code de machine n'existe pas.


FIELD MOPDSC001 OF PDMACH_OP_SURF               &
        HIDDEN ID 10                            &
        DISPLAY                                 &
        NOLABEL


FIELD MFPCODFINAVT     OF PDMACH_OP_S_F_P       &
        HIDDEN ID 15                            &
        REQUIRED                                &
        NUMERIC                                 &
        LOOKUP ON TYF_AVANT                     &
          VIA   CIECLE,                         &
                TYFCODFIN                       &
          USING T_CIECLEMNU,                    &
                MFPCODFINAVT     OF PDMACH_OP_S_F_P &
          MESSAGE 2911 ; Ce code de fini n'existe pas.


FIELD TYFDSCFRA001 OF TYF_AVANT                 &
       HIDDEN ID 20                             &
       DISPLAY                                  &
       NOLABEL


FIELD MFPCODFINAPR     OF PDMACH_OP_S_F_P       &
        HIDDEN ID 25                            &
        REQUIRED                                &
        NUMERIC                                 &
        LOOKUP ON TYF_APRES                     &
          VIA   CIECLE,                         &
                TYFCODFIN                       &
          USING T_CIECLEMNU,                    &
                MFPCODFINAPR     OF PDMACH_OP_S_F_P &
          MESSAGE 2911 ; Ce code de fini n'existe pas.


FIELD TYFDSCFRA001 OF TYF_APRES                 &
       HIDDEN ID 30                             &
       DISPLAY                                  &
       NOLABEL


FIELD T_SILENT                                &
        SILENT                                &
        LOOKUP NOTON PDMACH_OP_S_F_P          &
          VIA     CIECLE,                     &
                  MOPCODMAC,                  &
                  MFPCODFINAVT,               &
                  MFPCODFINAPR                &
          USING   T_CIECLEMNU ,               &
                  MOPCODMAC        OF PDMACH_OP_S_F_P , &
                  MFPCODFINAVT     OF PDMACH_OP_S_F_P , &
                  MFPCODFINAPR     OF PDMACH_OP_S_F_P   &
          MESSAGE 2972 ; Cette combinaison existe déjà

;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs code de machine.
;

PROCEDURE INPUT MOPCODMAC
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_MOPCODMAC = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd112 MODE F &
                     PASSING T_MOPCODMAC, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_MOPCODMAC )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs type de fini.
;

PROCEDURE INPUT MFPCODFINAVT
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd101 MODE F &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs type de fini.
;

PROCEDURE INPUT MFPCODFINAPR
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd101 MODE F &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDMACH_OP_S_F_P  &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDMACH_OP_S_F_P  &
     AND NOT NEWRECORD     OF PDMACH_OP_S_F_P  &
     AND NOT DELETEDRECORD OF PDMACH_OP_S_F_P  &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT


@ENDIF
