;/T/ Consultation des codes de taxe    TAXCLETYP, TAXCLECOD, TAXMODPAT
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 16 avril 1992
;
;    Description..: Permet de visionner et de choisir un code de taxe.
;
;
;    Paramètre....: TAXCLETYP ==> - Type de taxe. (Fédéral,Provincial)
;                                 - segment de la clé de recherche.
;
;                   TAXCLECOD ==> - code de taxe.
;                                 - segment de la clé de recherche.
;                                 - peut contenir le caractére '@'.
;                                 - contien la valeur de retour.
;
;                   TAXMODPAT ==> - mode de taxation.
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc111 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_TAXCLETYP, &
                       T_TAXCLECOD, &
                       T_TAXMODPAT

USE mc111.hlp NOLIST

TEMPORARY T_TAXCLETYP CHARACTER SIZE 1
TEMPORARY T_TAXCLECOD CHARACTER SIZE 2
TEMPORARY T_TAXMODPAT CHARACTER SIZE 5

;-------------------------------------------------------------------------------
FILE VTAX_DSC         PRIMARY &
                      OCCURS 11
  ACCESS VIA TAXCLETYP, &
             TAXCLECOD &
         USING T_TAXCLETYP, &
               T_TAXCLECOD  &
         ORDERBY TAXCLECOD

  SELECT IF ( MATCHPATTERN(TAXMOD OF VTAX_DSC, TRUNCATE(T_TAXMODPAT) ) )

;-------------------------------------------------------------------------------
;
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN(3,3,6) (,,9)

CLUSTER OCCURS WITH VTAX_DSC

FIELD TAXCLECOD OF VTAX_DSC HIDDEN LABEL ">>" DISPLAY
FIELD TAXDSC    OF VTAX_DSC DISPLAY

CLUSTER


;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_TAXCLECOD, "@")
  THEN LET T_TAXCLECOD = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_TAXCLECOD = TAXCLECOD OF VTAX_DSC
  RETURN
END

BUILD ;
;                                       *****************************************
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       * >> xx xxxxxxxxxxxxxxxxxxxx            *
;                                       *****************************************
