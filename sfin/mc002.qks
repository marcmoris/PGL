;/T/ Gestion des compagnies
;
;/P/ Programmeur..: Guy Chabot
;    Date Création: 24-Feb-1992
;
;    Description..: Cet écran permet la gestion des compagnies du système
;                   c'est ici que l'on défini également le Consolidé (Holding)
;                   à même titre qu'une autre compagnies mais avec le niveau
;                   de nomenclature (1).
;
;
;/M/ Marie-Josée Hamel, 96.02.15, enlever le bas de facture
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN mc002 ACTIONBAR &
             MODE LABEL "" AT 2,80 &
             NOACTION FIELDMARK RETAIN STARTUP &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIENOM

USE ussectmp NOLIST
    "MC002"

USE mc002.hlp NOLIST

USE usactecr NOLIST

@IF FRANCAIS
ACTIONMENU LABEL "Sous-écrans"
  MENUITEM LABEL "Sécurité des éléments       " ACTION DESIGNER D001
@ELSE
ACTIONMENU LABEL "Subscreen"
  MENUITEM LABEL "%%%urité des éléments       " ACTION DESIGNER D001
@ENDIF

;-------------------------------------------------------------------------------
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 04 ; Numéro de la compagnie
TEMPORARY T_CIENOMMNU CHARACTER SIZE 40 ; Nom de la compagnie
TEMPORARY T_CIENOM   CHARACTER SIZE 40; Nom de la compagnie-holding

;-------------------------------------------------------------------------------
;
FILE MCCOMPAGNIE      PRIMARY
 ACCESS REQUEST CIECLE OF MCCOMPAGNIE &
        VIA     CIECLE &
        USING   CIECLE OF MCCOMPAGNIE &
        ORDERED
 ACCESS SEQUENTIAL &
        ORDERBY CIECLE

;
; Destruction globale, on détruit la sécurité par compagnie et les
; périodes par compagnie.
;
FILE GSSEC_USR        DELETE &
                      NOITEM
  ACCESS VIA CIECLE &
         USING CIECLE OF MCCOMPAGNIE
;
FILE MCPERIODE_CIE    DELETE &
                      NOITEM
  ACCESS VIA CIECLE &
         USING CIECLE OF MCPERIODE_CIE

;
; Table qui verifie si le niveau de consolidation respecte les normes
;
FILE MCCOMPAGNIE      DESIGNER &
                      OPEN 1   &
                      ALIAS A_CIE_EXISTE

;
; Paramètres pour la compagnie consolidé(holding)
;
FILE MCHOLDING        REFERENCE
  ACCESS VIA CIENMC &
         USING "1"

;
; Pour valider le avance a.
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_AVANCE
 ACCESS VIA CPTCLE &
        USING CIECPTAVA OF MCCOMPAGNIE

;
; Pour valider le avance due
;
FILE VCPT_DSC         REFERENCE &
                      ALIAS A_CPT_DUE
 ACCESS VIA CPTCLE &
        USING CIECPTDUE OF MCCOMPAGNIE

;
; Pour valider l'unité de bilan.
;
FILE MCUNITE_ADM      REFERENCE
 ACCESS VIA CIECLE, &
            UNACLE &
        USING CIECLE    OF MCCOMPAGNIE, &
              CIEUNABIL OF MCCOMPAGNIE

;-------------------------------------------------------------------------------
;
TEMPORARY T_CPTCLE    CHARACTER SIZE 6 ; Popup sur les comptes
TEMPORARY T_CPTTYPPAT CHARACTER SIZE 1 ; Popup sur les comptes
TEMPORARY T_CPTCATPAT CHARACTER SIZE 8 ; Popup sur les comptes
TEMPORARY T_PECCLE    CHARACTER SIZE 4 ; Popup sur les comptes
TEMPORARY T_CIECLE    CHARACTER SIZE 4 ; Popup sur les unités adm.
TEMPORARY T_UNACLE    CHARACTER SIZE 8 ; Popup sur les unités adm.

TEMPORARY T_FLGDEL CHARACTER SIZE 1 ; Flag pour le sous-écran de destruction

DEFINE DZ_CIECLE CHARACTER SIZE 4 = " "
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )

;-------------------------------------------------------------------------------
;
USE usdrwecr NOLIST
USE ustitstd NOLIST
USE ushilite NOLIST

SKIP

TITLE &
@IF FRANCAIS
      " Compagnies " &
@ELSE
      " Companies " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST

ALIGN (3,3,19) (,43,59)
FIELD CIECLE    OF MCCOMPAGNIE HIDDEN ID 05 &
        REQUIRED NOCHANGE &
        LOOKUP NOTON MCCOMPAGNIE &
          VIA CIECLE &
          USING CIECLE OF MCCOMPAGNIE &
          MESSAGE 104

FIELD CIENMC    OF MCCOMPAGNIE &
        REQUIRED NOCHANGE &
        PATTERN "1|1.##|1.##.##|1.##.##.##|1.##.##.##.##|1.##.##.##.##.##" &
        LOOKUP NOTON MCCOMPAGNIE &
          VIA CIENMC &
          USING CIENMC OF MCCOMPAGNIE &
          MESSAGE 105

ALIGN(3,3,19)
FIELD CIENOM    OF MCCOMPAGNIE HIDDEN ID 10 REQUIRED
FIELD CIENOMABR OF MCCOMPAGNIE HIDDEN ID 12 DEFAULT CIENOM OF MCCOMPAGNIE

FIELD CIEADR1   OF MCCOMPAGNIE HIDDEN ID 15 REQUIRED
FIELD CIEADR2   OF MCCOMPAGNIE HIDDEN NOLABEL ID SAME
FIELD CIEADR3   OF MCCOMPAGNIE HIDDEN NOLABEL ID SAME
FIELD CIEADR4   OF MCCOMPAGNIE HIDDEN NOLABEL ID SAME

SKIP 1
ALIGN (3,3,19) (43,43,59)
FIELD CIECP     OF MCCOMPAGNIE HIDDEN ID 20
FIELD CIETEL    OF MCCOMPAGNIE HIDDEN ID SAME
FIELD CIEFAX    OF MCCOMPAGNIE HIDDEN ID 30
FIELD CIETLX    OF MCCOMPAGNIE HIDDEN ID SAME

SKIP 1
ALIGN(3,3,19)
FIELD CIEENRTPS OF MCCOMPAGNIE HIDDEN ID 35

FIELD CIEENRTVQ OF MCCOMPAGNIE HIDDEN ID 40

ALIGN(3,3,19)(,,28)
FIELD CIECPTAVA OF MCCOMPAGNIE HIDDEN ID 45
FIELD CPTDSC    OF A_CPT_AVANCE &
        DISPLAY

FIELD CIECPTDUE OF MCCOMPAGNIE HIDDEN ID 50
FIELD CPTDSC    OF A_CPT_DUE &
        DISPLAY

FIELD CIEUNABIL OF MCCOMPAGNIE HIDDEN ID 55
FIELD UNANOM    OF MCUNITE_ADM &
        DISPLAY

;-------------------------------------------------------------------------------
;
;
; Valide la sécurité d'accès de l'écran.
;
;
PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Validation du numéro de nomenclature selon la table suivante
;
; 1. pour saisir 1.01          il faut que 1          existe
; 2. pour saisir 1.09          il faut que 1          existe
; 3. pour saisir 1.09.01       il faut que 1.09       existe
; 4. pour saisir 1.09.07       il faut que 1.09       existe
; 5. pour saisir 1.02.03.04.06 il faut que 1.02.03.04 existe
;
; Composition du numéro: 1.99.99.99.99.99
;                        1234567890123456
;                                      ||-> Niveau 5 position 15 et 16
;                                   ||----> Niveau 4 position 12 et 13
;                                ||-------> Niveau 3 position  9 et 10
;                             ||----------> Niveau 2 position  6 et  7
;                          ||-------------> Niveau 1 position  3 et  4
;
;
PROCEDURE EDIT CIENMC
BEGIN
  ;
  ; niveau 5 de consolidation
  ;
  IF "" <> FIELDTEXT[15:1]
  THEN BEGIN
    GET A_CIE_EXISTE VIA CIENMC USING FIELDTEXT[1:13] OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 106 ; La cie de niveau supérieure n'a pas été définie.
  END
  ELSE BEGIN
    ;
    ; niveau 4 de consolidation
    ;
    IF "" <> FIELDTEXT[12:1]
    THEN BEGIN
      GET A_CIE_EXISTE VIA CIENMC USING FIELDTEXT[1:10] OPTIONAL
      IF NOT ACCESSOK
      THEN ERROR 106 ; La cie de niveau supérieure n'a pas été définie.
    END
    ELSE BEGIN
      ;
      ; niveau 3 de consolidation
      ;
      IF "" <> FIELDTEXT[9:1]
      THEN BEGIN
        GET A_CIE_EXISTE VIA CIENMC USING FIELDTEXT[1:7] OPTIONAL
        IF NOT ACCESSOK
        THEN ERROR 106 ; La cie de niveau supérieure n'a pas été définie.
      END
      ELSE BEGIN
        ;
        ; niveau 2 de consolidation
        ;
        IF "" <> FIELDTEXT[6:1]
        THEN BEGIN
          GET A_CIE_EXISTE VIA CIENMC USING FIELDTEXT[1:4] OPTIONAL
          IF NOT ACCESSOK
          THEN ERROR 106 ; La cie de niveau supérieure n'a pas été définie.
        END
        ;
        ; niveau 1 de consolidation
        ;
        ELSE BEGIN
          IF "" <> FIELDTEXT[3:1]
          THEN BEGIN
            GET A_CIE_EXISTE VIA CIENMC USING FIELDTEXT[1:1] OPTIONAL
            IF NOT ACCESSOK
            THEN ERROR 106 ; La cie de niveau supérieure n'a pas été définie.
          END
        END
      END
    END
  END
END

;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le code postal.
;
;
PROCEDURE INPUT CIECP
BEGIN
  USE uscpfmt NOLIST  ;Fomate le code postal
END

;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CIETEL
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END

;-------------------------------------------------------------------------------
;
;
; Cette procedure formate le numero de telephone nord americain.
;
;
PROCEDURE INPUT CIEFAX
BEGIN
  USE ustelfmt NOLIST  ;Fomate le numéro de téléphone nord américain
END


;-------------------------------------------------------------------------------
;
;
; Accés au sous-écran de consultation des comptes.
;
;
PROCEDURE INPUT CIECPTAVA
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "AC"
    LET T_PECCLE    = " "
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, T_CPTTYPPAT, T_CPTCATPAT, T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTCLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Validation si le compte est actif.
;
;
PROCEDURE EDIT CIECPTAVA
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT))
  THEN BEGIN
    GET A_CPT_AVANCE OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 111

    IF CPTTYP OF A_CPT_AVANCE <> "R"
    THEN ERROR 117; On ne peut pas utliser ce compte dans une imputation.

    IF CPTCAT OF A_CPT_AVANCE <> "AC"
    THEN ERROR 118; Ce compte n'est pas de la bonne catégorie.

  END
END

;-------------------------------------------------------------------------------
;
; Accés au sous-écran de consultation des comptes.
;
PROCEDURE INPUT CIECPTDUE
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CPTCLE    = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    LET T_CPTTYPPAT = "R"
    LET T_CPTCATPAT = "AC"
    LET T_PECCLE    = " "
    RUN SCREEN mc103 MODE F PASSING T_CPTCLE, T_CPTTYPPAT, T_CPTCATPAT, T_PECCLE
    LET FIELDTEXT = TRUNC(T_CPTCLE)
  END
END


;-------------------------------------------------------------------------------
;
; Validation si le compte est actif.
;
PROCEDURE EDIT CIECPTDUE
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT))
  THEN BEGIN
    GET A_CPT_DUE OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 111

    IF CPTTYP OF A_CPT_DUE <> "R"
    THEN ERROR 117; On ne peut pas utliser ce compte dans une imputation.

    IF CPTCAT OF A_CPT_DUE <> "AC"
    THEN ERROR 118; Ce compte n'est pas de la bonne catégorie.

  END
END

;-------------------------------------------------------------------------------
;
;
; Accés au sous-écran de consultation des unités administrative.
;
;
PROCEDURE INPUT CIEUNABIL
BEGIN
  IF 0 <> IND(FIELDTEXT,"*")
  THEN BEGIN
    LET T_CIECLE = CIECLE OF MCCOMPAGNIE
    LET T_UNACLE = FIELDTEXT[1:IND(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN mc104 MODE F PASSING T_CIECLE,T_UNACLE
    LET FIELDTEXT = TRUNC(T_UNACLE)
  END
END

;-------------------------------------------------------------------------------
;
;
; Valide l'unité de bilan.
;
;
PROCEDURE EDIT CIEUNABIL
BEGIN
  IF 0 <> SIZE(TRUNC(FIELDTEXT))
  THEN BEGIN
    GET MCUNITE_ADM OPTIONAL
    IF NOT ACCESSOK
    THEN ERROR 116
  END
END

;-------------------------------------------------------------------------------
;
; Appel du sous-écran de sécurité. MC002A
;
PROCEDURE DESIGNER D001
BEGIN
  IF    ALTEREDRECORD OF MCCOMPAGNIE
  THEN ERROR 1390 ; Vous devez faire une mise à jour avant d'accéder le sous-écran

  LET T_CIECLEMNU = CIECLE OF MCCOMPAGNIE
  LET T_CIENOMMNU = CIENOM OF MCCOMPAGNIE
  RUN SCREEN mc002a PASSING T_CIECLEMNU  , &
                            T_CIENOMMNU    &
                            MODE F
END
;-------------------------------------------------------------------------------
;
;
; Valide si l'usager peut faire de la saisie.
;
;
PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
;
; Valide les traitements permis par écran et par usager.
;
;
PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF MCCOMPAGNIE &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF MCCOMPAGNIE &
     AND NOT NEWRECORD     OF MCCOMPAGNIE &
     AND NOT DELETEDRECORD OF MCCOMPAGNIE &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
  ;
  ; Destruction de la compagnie.
  ;
  IF DELETEDRECORD OF MCCOMPAGNIE
  THEN BEGIN
    ;
    ; On initialise le flag de retour pour savoir si le sous-écran a
    ; bien fonctionné.
    ;
    LET T_FLGDEL = ""
    ;
    ; Appel du sous-écran qui valide si la compagnie est référée.
    ;
    RUN SCREEN mc002x MODE SAME &
                      PASSING MCCOMPAGNIE , &
                              T_FLGDEL
    ;
    ; Si la valeur de retour égale 1, cela indique que le sous-écran
    ; n'a trouvé aucune référence pour l'identifiant demandé.
    ;
    IF T_FLGDEL = ""
    THEN ERROR " "
  END
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
********************************** Compagnies **********************************
* Compagnie.....: xxxx                    Nomenclature..: xxxxxxxxxxxxxxxx     *
* Nom compagnie.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Nom abrégé cie: xxxxxxxxxxxxxxxxxxxx                                         *
* Adresse.......: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
* Code postal...: xxxxxxx                 Téléphone.....: xxxxxxxxxxxxx        *
* Numéro du FAX.: xxxxxxxxxxxxx           Télex.........: xxxxxxxx             *
*                                                                              *
* Enreg. TPS....: xxxxxxxxxxxxxxx                                                   *
* Enreg. TVP....: xxxxxxxxxx                                                   *
* Cpt. avance à.: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                 *
* Cpt. avance de: xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                 *
* Unité de bilan: xxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx            *
*                                                                              *
* Bas de facture: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
*                 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx *
********************************************************************************
@ENDIF
