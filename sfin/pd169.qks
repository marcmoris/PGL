;/T/ Défaut de bloc (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-18
;
;    Description..: Cette table contient les informations de base des
;                   défaut de bloc.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd169 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD169"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd169.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie


;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Variable nécessaire pour appel de dépisteur (pop-up)
;
TEMPORARY T_DBLCODDEF CHAR SIZE 04


;-------------------------------------------------------------------------------
;
; Défauts de bloc
;

FILE PDDEFAUT_DE_BLOC PRIMARY NOITEM

  ACCESS VIA     CIECLE,    &
                 DBLCODDEF  &
         USING   T_CIECLEMNU,                  &
                 DBLCODDEF OF PDDEFAUT_DE_BLOC &
         REQUEST DBLCODDEF OF PDDEFAUT_DE_BLOC &
         ORDERBY CIECLE,    &
                 DBLCODDEF

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY CIECLE,     &
                 DBLCODDEF

         ITEM CIECLE OF PDDEFAUT_DE_BLOC INITIAL T_CIECLEMNU

;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Défaut de bloc " &
@ELSE
      " %%%Défaut de bloc " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD DBLCODDEF OF PDDEFAUT_DE_BLOC           &
        HIDDEN ID 05                          &
        REQUIRED NOCHANGE                     &
        LOOKUP NOTON PDDEFAUT_DE_BLOC         &
          VIA   CIECLE,                       &
                DBLCODDEF                     &
          USING T_CIECLEMNU,                  &
                DBLCODDEF OF PDDEFAUT_DE_BLOC &
          MESSAGE 2916 ; Ce défaut existe déjà.


FIELD DBLDSCFRA OF PDDEFAUT_DE_BLOC           &
        HIDDEN ID 10                          &
        REQUIRED


FIELD DBLDSCANG OF PDDEFAUT_DE_BLOC           &
        HIDDEN ID 15                          &
        REQUIRED


;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs unité de mesure
;

PROCEDURE INPUT DBLCODDEF
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_DBLCODDEF = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd115 MODE F &
                     PASSING T_DBLCODDEF, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_DBLCODDEF )
  END
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDDEFAUT_DE_BLOC &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDDEFAUT_DE_BLOC &
     AND NOT NEWRECORD     OF PDDEFAUT_DE_BLOC &
     AND NOT DELETEDRECORD OF PDDEFAUT_DE_BLOC &
     AND TZ_SECMOD = "0"
  THEN ERROR 2
END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
******************************** Défaut de bloc ********************************
*                                                                              *
* Code défaut...: xx                                                           *
* Descr français: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
* Descr anglais.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
