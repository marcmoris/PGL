;/T/ Produits de dimension (Pilotage)
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-13
;
;    Description..: Cette table contient les informations de base des
;                   produits de dimension.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd162 ACTIONBAR                    &
             MODE LABEL "" AT 2,80        &
             NOACTION                     &
             FIELDMARK RETAIN STARTUP     &
             HELP POPUP FROM 4,9 TO 20,72 &
             RECEIVING T_CIECLEMNU ,      &
                       T_CIENOM


;-------------------------------------------------------------------------------
; Variables pour la sécurité.
;
USE ussectmp  NOLIST
    "PD162"


;-------------------------------------------------------------------------------
; Documentation de l'écran.
;
USE pd162.hlp NOLIST


;-------------------------------------------------------------------------------
; Actionbar standard
;
USE usactecr  NOLIST


;-------------------------------------------------------------------------------
;
; Variables saisie ou affichée dans le menu. (et utilisés dans le passage de
; paramètres).
;
TEMPORARY T_CIECLEMNU CHARACTER SIZE 4  RESET AT STARTUP ; Compagnie menu
TEMPORARY T_CIENOM    CHARACTER SIZE 40 RESET AT STARTUP ; nom compagnie

;-------------------------------------------------------------------------------
;
; Numéro et nom de la compagnie pour l'en-tête de l'écran.
;
DEFINE DZ_CIECLE CHARACTER SIZE 4 = T_CIECLEMNU
DEFINE DZ_CIENOM CHARACTER SIZE 40 = CENTER( T_CIENOM )


;-------------------------------------------------------------------------------
;
; Sert à valider les codes bilingue et pour les sous-écrans de consultation
;
DEFINE    D_TGRCODGRA CHARACTER SIZE 16 = "TGRCODGRA"
TEMPORARY T_TGRCODGRA CHARACTER SIZE 04

DEFINE    D_TYFCODFIN CHARACTER SIZE 16 = "TYFCODFIN"
TEMPORARY T_TYFCODFIN CHARACTER SIZE 04


;-------------------------------------------------------------------------------
;
; Dimension maximun et minimun
;

USE usdimprd NOLIST


;-------------------------------------------------------------------------------
;
; Variable pour le calcul du volume d'une pièce de granit
;

USE usvarvol NOLIST


;-------------------------------------------------------------------------------
;
; Produit de dimension
;

FILE PDPROD_DIMENSION PRIMARY NOITEM
  ACCESS VIA     CIECLE,     &
                 PDILON ,    &
                 PDIHAU,     &
                 PDIEPA,     &
                 TGRCODGRA,  &
                 TYFCODFIN   &
         USING   T_CIECLEMNU,                   &
                 PDILON    OF PDPROD_DIMENSION, &
                 PDIHAU    OF PDPROD_DIMENSION, &
                 PDIEPA    OF PDPROD_DIMENSION, &
                 TGRCODGRA OF PDPROD_DIMENSION, &
                 TYFCODFIN OF PDPROD_DIMENSION  &
         REQUEST PDILON    OF PDPROD_DIMENSION, &
                 PDIHAU    OF PDPROD_DIMENSION, &
                 PDIEPA    OF PDPROD_DIMENSION, &
                 TGRCODGRA OF PDPROD_DIMENSION, &
                 TYFCODFIN OF PDPROD_DIMENSION  &
         ORDERBY CIECLE, &
                 PDILON, &
                 PDIHAU, &
                 PDIEPA, &
                 TGRCODGRA, &
                 TYFCODFIN

  ACCESS VIA     CIECLE      &
         USING   T_CIECLEMNU &
         ORDERBY PDILON, &
                 PDIHAU, &
                 PDIEPA, &
                 TGRCODGRA, &
                 TYFCODFIN


  ITEM PDITYPPRD OF PDPROD_DIMENSION  INITIAL "PD"
  ITEM CIECLE OF PDPROD_DIMENSION INITIAL T_CIECLEMNU


;-------------------------------------------------------------------------------
;
; Type de granit
;

FILE PDTYPE_GRANIT REFERENCE


;-------------------------------------------------------------------------------
;
; Type de fini
;

FILE PDTYPE_FINI   REFERENCE


;-------------------------------------------------------------------------------
;
;
USE usdrwecr NOLIST ; Draw pour les écrans pleine page
USE ustitstd NOLIST ; En-tête pour les écrans pleine page
USE ushilite NOLIST ; Hilite pour tout les écrans

SKIP

TITLE &
@IF FRANCAIS
      " Produit de dimension " &
@ELSE
      " %%%Produit de dimension " &
@ENDIF
      CENTERED AT ,1

USE ustitoff NOLIST           ; Pour placer le TITLE OFF


SKIP TO LINE 5

ALIGN (2,3,19)


FIELD PDILON OF PDPROD_DIMENSION                &
        HIDDEN ID 05                            &
        REQUIRED NOCHANGE


FIELD PDIHAU OF PDPROD_DIMENSION                &
        HIDDEN ID 10                            &
        REQUIRED NOCHANGE


FIELD PDIEPA OF PDPROD_DIMENSION                &
        HIDDEN ID 15                            &
        REQUIRED NOCHANGE


FIELD TGRCODGRA OF PDPROD_DIMENSION             &
        HIDDEN ID 20                            &
        REQUIRED NOCHANGE                       &
        NUMERIC                                 &
        LOOKUP ON PDTYPE_GRANIT                 &
          VIA   CIECLE,                         &
                TGRCODGRA                       &
          USING T_CIECLEMNU,                    &
                TGRCODGRA OF PDPROD_DIMENSION   &
          MESSAGE 2910 ; Ce type de granit n'existe pas.


FIELD TYFCODFIN OF PDPROD_DIMENSION             &
        HIDDEN ID 25                            &
        REQUIRED NOCHANGE                       &
        NUMERIC                                 &
        LOOKUP ON PDTYPE_FINI                   &
          VIA   CIECLE,                         &
                TYFCODFIN                       &
          USING T_CIECLEMNU,                    &
                TYFCODFIN OF PDPROD_DIMENSION   &
          MESSAGE 2911 ; Ce type de fini n'existe pas.


FIELD PDIQTE OF PDPROD_DIMENSION                &
        HIDDEN ID 30                            &
        REQUIRED


ALIGN (,3,19)


FIELD PDISURMC2        OF PDPROD_DIMENSION &
        DISPLAY


FIELD PDIVOLMC3        OF PDPROD_DIMENSION &
        DISPLAY




;-------------------------------------------------------------------------------
;
; Valide la sécurité d'accès de l'écran.
;

PROCEDURE INITIALIZE
BEGIN
  USE ussececr NOLIST
END


;-------------------------------------------------------------------------------
;
; Procedure pour le calcul du volume d'une pièce de granit
;

USE uscalvol NOLIST


;-------------------------------------------------------------------------------
;
; Valide les dimensions minimal et maximal
;

PROCEDURE EDIT PDILON
BEGIN
  IF (FIELDVALUE        > T_DIM_MAX_LAR_PD OR &
      FIELDVALUE        < T_DIM_MIN_LAR_PD)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul de la surface et du volume de la pièce
;

PROCEDURE PROCESS PDILON
BEGIN
  LET     TZ_LARGEUR                           = PDILON OF PDPROD_DIMENSION
  LET     TZ_HAUTEUR                           = PDIHAU OF PDPROD_DIMENSION
  LET     TZ_EPAISSEUR                         = PDIEPA OF PDPROD_DIMENSION
  DO INTERNAL CALVOL
  LET     PDIVOLMC3        OF PDPROD_DIMENSION = TZ_VOLUME
  LET     PDISURMC2        OF PDPROD_DIMENSION = TZ_MC2
  DISPLAY PDIVOLMC3        OF PDPROD_DIMENSION
  DISPLAY PDISURMC2        OF PDPROD_DIMENSION
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions minimal et maximal
;

PROCEDURE EDIT PDIHAU
BEGIN
  IF (FIELDVALUE        > T_DIM_MAX_HAU_PD OR &
      FIELDVALUE        < T_DIM_MIN_HAU_PD)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul de la surface et du volume de la pièce
;

PROCEDURE PROCESS PDIHAU
BEGIN
  LET     TZ_LARGEUR                           = PDILON OF PDPROD_DIMENSION
  LET     TZ_HAUTEUR                           = PDIHAU OF PDPROD_DIMENSION
  LET     TZ_EPAISSEUR                         = PDIEPA OF PDPROD_DIMENSION
  DO INTERNAL CALVOL
  LET     PDIVOLMC3        OF PDPROD_DIMENSION = TZ_VOLUME
  LET     PDISURMC2        OF PDPROD_DIMENSION = TZ_MC2
  DISPLAY PDIVOLMC3        OF PDPROD_DIMENSION
  DISPLAY PDISURMC2        OF PDPROD_DIMENSION
END


;-------------------------------------------------------------------------------
;
; Valide les dimensions minimal et maximal
;

PROCEDURE EDIT PDIEPA
BEGIN
  IF (FIELDVALUE        > T_DIM_MAX_EPA_PD OR &
      FIELDVALUE        < T_DIM_MIN_EPA_PD)
  THEN BEGIN
    ERROR 2962 ; Cette dimension est hors des limites
  END
END


;-------------------------------------------------------------------------------
;
; Calcul de la surface et du volume de la pièce
;

PROCEDURE PROCESS PDIEPA
BEGIN
  LET     TZ_LARGEUR                           = PDILON OF PDPROD_DIMENSION
  LET     TZ_HAUTEUR                           = PDIHAU OF PDPROD_DIMENSION
  LET     TZ_EPAISSEUR                         = PDIEPA OF PDPROD_DIMENSION
  DO INTERNAL CALVOL
  LET     PDIVOLMC3        OF PDPROD_DIMENSION = TZ_VOLUME
  LET     PDISURMC2        OF PDPROD_DIMENSION = TZ_MC2
  DISPLAY PDIVOLMC3        OF PDPROD_DIMENSION
  DISPLAY PDISURMC2        OF PDPROD_DIMENSION
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs type de granit.
;

PROCEDURE INPUT TGRCODGRA
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TGRCODGRA = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd100 MODE F &
                     PASSING T_TGRCODGRA, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TGRCODGRA )
  END
END


;-------------------------------------------------------------------------------
;
; Appel du popup pour le champs type de fini.
;

PROCEDURE INPUT TYFCODFIN
BEGIN
  IF 0 <> INDEX(FIELDTEXT,"*")
  THEN BEGIN
    LET T_TYFCODFIN = FIELDTEXT[1:INDEX(FIELDTEXT,"*") - 1] + "@"
    RUN SCREEN pd101 MODE F &
                     PASSING T_TYFCODFIN, &
                             T_CIECLEMNU
    LET FIELDTEXT = TRUNCATE( T_TYFCODFIN )
  END
END


;-------------------------------------------------------------------------------
;
;
;

PROCEDURE EDIT PDIQTE
BEGIN
  ;
  ; Valide la partie décimale.
  ;
  USE usvaldec0 NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide si l'usager peut faire de la saisie.
;

PROCEDURE PREENTRY
BEGIN
  USE ussecent NOLIST
END


;-------------------------------------------------------------------------------
;
; Valide les traitements permis par écran et par usager.
;

PROCEDURE PREUPDATE
BEGIN
  ;
  ; Validation de la destruction
  ;
  IF     DELETEDRECORD OF PDPROD_DIMENSION &
     AND TZ_SECDEL = "0"
  THEN ERROR 1
  ;
  ; Validation de la modification.
  ;
  IF     ALTEREDRECORD     OF PDPROD_DIMENSION &
     AND NOT NEWRECORD     OF PDPROD_DIMENSION &
     AND NOT DELETEDRECORD OF PDPROD_DIMENSION &
     AND TZ_SECMOD = "0"
  THEN ERROR 2

END

BUILD
@IF FORMAT

xxxx                xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                   x
***************************** Produit de dimension *****************************
*                                                                              *
* Longueur......: xxxxx                                                        *
* Largeur.......: xxxxx                                                        *
* Épaisseur.....: xxxxx                                                        *
* Code granit...: xxx                                                          *
* Code fini.....: xx                                                           *
* Quantité......: xxxxxxxx                                                     *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
*                                                                              *
********************************************************************************
@ENDIF
