;/T/ 5.3 Opérateur PAYE
;
;/P/ Programmeur..: Martin Bruyère
;    Date Création: 1994-07-13
;
;    Description..: Permet de visionner et de choisir un opérateur.
;
;    Paramètres...: T_EMPLOYE_NUM   ==> - code
;                                       - segment de la clé de recherche
;                                       - peut contenir la valeur @
;                                       - contient la valeur de retour.
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pd110 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             HELP POPUP FROM 4,9 TO 20,72 &
             ACTIVITIES FIND &
             RECEIVING T_EMPLOYE_NUM, &
                       T_CIECLEMNU


TEMPORARY T_EMPLOYE_NUM   CHARACTER SIZE 06
TEMPORARY T_CIECLEMNU     CHARACTER SIZE 04


USE pd110.hlp NOLIST


USE ustrasse NOLIST


;-------------------------------------------------------------------------------
;
; Opérateur
;

FILE PDOPERATEUR PRIMARY &
                   OCCURS 11

  ACCESS VIA     CIECLE,      &
                 OPENUMEMP    &
         USING   T_CIECLEMNU, &
                 T_EMPLOYE_NUM &
         ORDERBY CIECLE,      &
                 OPENUMEMP


;-------------------------------------------------------------------------------
;
; Concaténation du prénom et du nom de l'employé
;

DEFINE D_NOM CHARACTER SIZE 28 = TRUNCATE(OPEPNM OF PDOPERATEUR) + " " + &
                                 TRUNCATE(OPENOM OF PDOPERATEUR)


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST     ; Hilite pour tout les écrans
USE usdrwfnd NOLIST     ; Draw pour les écrans de choix et consultations

USE ustitoff NOLIST     ; Pour placer le TITLE OFF

SKIP TO 2

ALIGN(3,3,6)(,,13)

CLUSTER OCCURS WITH PDOPERATEUR

FIELD OPENUMEMP OF PDOPERATEUR HIDDEN NUMERIC LABEL ">>" DISPLAY
FIELD D_NOM                    DISPLAY SIZE 28

CLUSTER

;-------------------------------------------------------------------------------
;
; Enleve le @ si l'usager fait aucun choix à l'écran.
;

PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_EMPLOYE_NUM , "@")
  THEN LET T_EMPLOYE_NUM = ""
END

;-------------------------------------------------------------------------------
;
; Retourne la valeur choisi par l'usager.
;

PROCEDURE DESIGNER 1
BEGIN
  LET T_EMPLOYE_NUM = OPENUMEMP OF PDOPERATEUR
  RETURN
END

BUILD

@IF FORMAT

                                       *****************************************
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       * >> xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
                                       *****************************************
@ENDIF
