;/T/ Consultation des lots             LGLCIECLE, PECCLE, LGLCLE
;
;/P/ Programmeur..: Alain Côté
;    Date Création: 5-Mar-1992
;
;    Description..: Permet de visionner et de choisir un lot.
;                   Les lots sont sélectionnés par usager(LOGONID), qu'il
;                   ne soit pas journalisé et selon la période comptable.
;
;
;-------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN gl101 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_LGLCIECLE, T_PECCLE, T_LGLCLE

USE gl101.hlp NOLIST

TEMPORARY T_LGLCIECLE CHARACTER SIZE 4
TEMPORARY T_PECCLE    CHARACTER SIZE 4
TEMPORARY T_LGLCLE    CHARACTER SIZE 4

;-------------------------------------------------------------------------------
;
FILE GLLOT            PRIMARY &
                      OCCURS 10
 ACCESS VIA LGLCIECLE, &
            PECCLE   , &
            LGLCLE     &
        USING T_LGLCIECLE, &
              T_PECCLE   , &
              T_LGLCLE     &
        ORDERBY LGLCIECLE, &
                PECCLE   , &
                LGLCLE

 SELECT IF LGLUSR    OF GLLOT = LOGONID AND &
           LGLDATJOU OF GLLOT = 0       AND &
           LGLATRJOU OF GLLOT = "0"

;
; Pour afficher la description du type d'écriture.
;
DEFINE D_LGLTYPECR CHAR SIZE 16 = "LGLTYPECR"
;
FILE VCBI_DSC         REFERENCE &
                      OCCURS WITH GLLOT
  ACCESS VIA XELNOM, &
             CBICLE &
         USING D_LGLTYPECR, &
               LGLTYPECR OF GLLOT


;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

@IF FRANCAIS
 TITLE "Lot     Type            Date val.  " AT 2,6
@ELSE
 TITLE "Batch   Type            Date val. " AT 2,5
@ENDIF

ALIGN(3,3,6) (,,11) (,,14) (,,30)

CLUSTER OCCURS WITH GLLOT

FIELD LGLCLE    OF GLLOT HIDDEN LABEL ">>"
FIELD LGLTYPECR OF GLLOT
FIELD CBIDSC    OF VCBI_DSC SIZE 15
FIELD LGLDATVAL OF GLLOT FORMAT YYYYMMDD PATTERN "####/##/##"

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> IND(T_LGLCLE, "@")
  THEN LET T_LGLCLE = ""
END


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_LGLCLE = LGLCLE OF GLLOT
  RETURN
END

BUILD ;
;
;                                       *****************************************
;                                       *    Lot     Type            Date val.  *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       * >> xxxx xx xxxxxxxxxxxxxxx xxxxxxxx   *
;                                       *****************************************
;
;
;
;
;
;
;
;
;
;
