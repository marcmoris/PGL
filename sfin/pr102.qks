;/T/ Consultation des activités        CIECLE, PROCLE, ACTCLE, ACTSTUPAT
;
;/P/ Programmeur..: Alain Côté
;    Date création: 13 avril 1993
;
;    Description..: Affiche les codes d'activités par projet.
;
;    Paramètres...: CIECLE    ==> - numéro de compagnie
;                                 - segment de la clé de recherche
;
;                   PROCLE    ==> - numéro de projet
;                                 - segment de la clé de recherche
;
;                   ACTCLE    ==> - numéro d'activités
;                                 - segment de la clé de recherche
;                                 - peut contenir la valeur @
;                                 - contient la valeur de retour
;
;                   ACTSTUPAT ==> - Statut de l'activités
;                                 - critère de sélection sous la forme
;                                   de pattern.
;
;------------------------------------------------------------------------------

;/V/ Écran vérifier pour le passage à l'an 2000


SCREEN pr102 NOMODE NOACTION FIELDMARK &
             FROM 11,40 TO 23,80 &
             ACTIVITIES FIND &
             RECEIVING T_CIECLEMNU, &
                       T_PROCLE, &
                       T_ACTCLE, &
                       T_ACTSTUPAT

TEMPORARY T_CIECLEMNU CHARACTER SIZE 4
TEMPORARY T_PROCLE    CHARACTER SIZE 8
TEMPORARY T_ACTCLE    CHARACTER SIZE 3
TEMPORARY T_ACTSTUPAT CHARACTER SIZE 5

USE pr102.hlp NOLIST

;-------------------------------------------------------------------------------
;
FILE GPACTIVITE       PRIMARY &
                      OCCURS 11
  ACCESS VIA CIECLE, &
             PROCLE, &
             ACTCLE  &
         USING T_CIECLEMNU, &
               T_PROCLE   , &
               T_ACTCLE     &
         ORDERBY ACTCLE

  SELECT IF &
    MATCHPATTERN(ACTSTU OF GPACTIVITE, TRUNCATE(T_ACTSTUPAT))

;-------------------------------------------------------------------------------
;
USE ushilite NOLIST
USE usdrwfnd NOLIST

USE ustitoff NOLIST

SKIP TO 2

ALIGN (3,3,6)(,,10)
CLUSTER OCCURS WITH GPACTIVITE

FIELD ACTCLE    OF GPACTIVITE HIDDEN LABEL ">>"
FIELD ACTDSC1   OF GPACTIVITE DISPLAY SIZE 30

CLUSTER


;-------------------------------------------------------------------------------
;
;
PROCEDURE DESIGNER 1
BEGIN
  LET T_ACTCLE = ACTCLE OF GPACTIVITE
  RETURN
END

;-------------------------------------------------------------------------------
;
;
PROCEDURE EXIT
BEGIN
  IF 0 <> INDEX(T_ACTCLE, "@")
  THEN LET T_ACTCLE = ""
END

BUILD ;
;
;                                       *****************************************
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       * >> xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx*
;                                       *****************************************
;
;
;
;
;
;
;
;
;
;
